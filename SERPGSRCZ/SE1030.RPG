     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Update next coupon date on securities')       *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE1030 - Midas SE Update Next Coupon Date on Securities      *
      *                                                               *
      *  Program Function: This Program Will Update the Next Coupon   *
      *                    Date (NCD) on File SECTYD.                 *
      *                                                               *
      *  Phase(s): COB - Automatic.                                   *
      *                                                               *
      *  Called By: SEC1030 - Midas SE Update Next Coupon Date on     *
      *                       Securities.                             *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CRE073             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      *                 CSE075             Date 17Nov05               *
      *                 CSE071             Date 19Jul05               *
      *                 CAS006             Date 21Jan03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CSE031  *CREATE    Date 09Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *             (Recompile)                                       *
      *  MD046248 - Finastra Rebranding                               *
      *  CRE073 - Interest Rate Rounding (Recompile)                  *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE075 - US Enhanced Treasury Upgrade to MidasPlus           *
      *           (Recompile)                                         *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  CAS006 - Hedge Accounting Phase B (Recompile)                *
      *  CSE031 - Coupon Fixing for Floating Rate Notes               *
      *                                                               *
      *****************************************************************
      *
     FSECTY   UF  E           K        DISK         KINFSR *PSSR
      ** Midas SE Securities
      *
      *
      /SPACE 3
      *
      ** Array containing Copyright statement
     E                    CPY@    1   1 80
      /COPY ZSRSRC,ZDATE2Z1
      /COPY ZSRSRC,ZNCDZ1
      *
      *
      /SPACE 3
      *
      ** Local data area for database error details
      *
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      *                                     134 141 DBFILE
      *                                     142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      **  Short data structure for access programs
      *
     IDSFDY     E DSDSFDY
      ** First DS for access programs, short data structure
      *
     IDSSDY     E DSDSSDY
      ** Second DS for access programs, long data structure
      *
     ISDBANK    E DSSDBANKPD
      ** Data Structure to receive Bank Details
      *
     IINVTP     E DSINVTPD
     I              RECI                            INRECI
     I              LCD                             INLCD
     I              CHTP                            INCHTP
     I              TNLU                            INTNLU
      ** Data Structure to receive Investment Type Details
      *
      /COPY ZSRSRC,ZNCDZ2
      *
      /EJECT
      *================================================================
      *  M A I N   P R O C E S S I N G                                *
      *================================================================
      *
      **  Perform Initialisation.
      *
     C                     EXSR SRINIT
      *
      **  Perform Detail Processing.
      *
     C                     EXSR SRPROC
      *
      **  Perform Termination Processing.
      *
     C                     EXSR SRTERM
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
      *
      *****************************************************************
      *  SRINIT   - Subroutine to do Program Initialisation.          *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:     AOBANKR0                                          *
      *****************************************************************
      *
     C           SRINIT    BEGSR
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Define LDA
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** Initialise LDA field.
      *
     C                     MOVEL'SE1030'  DBPGM
      *
      ** Access Bank details via access program
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '       ' PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           PRTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE            *************
     C                     MOVEL'SDBANKPD'DBFILE           *DB ERROR 01*
     C                     MOVELPOPTN     DBKEY            *************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Check System Date Format, DDMMYY (*IN98 off)
      *                            MMDDYY (*IN98 on).
      *
     C           BJDFIN    IFEQ 'M'
     C                     SETON                     98
     C                     ENDIF
      *
     C                     ENDSR
      *
      /TITLE SR/SRPROC
      *****************************************************************
      *  SRPROC   - Subroutine to do Detail Processing.               *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *****************************************************************
      *
     C           SRPROC    BEGSR
      *
      ** Read Securities file
      *
     C           *LOVAL    SETLLSECTY
     C                     READ SECTY                    80
      *
     C           *IN80     DOWEQ*OFF
      *
      ** Access AOINVTR0 to get the processing type
     C                     CALL 'AOINVTR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM SITP      PINVT   3
     C                     PARM NMCY      PCCYI   3
     C           INVTP     PARM INVTP     DSFDY
      *
     C           PRTCD     IFNE *BLANKS
     C                     MOVELNMCY      WKEY1   7
     C                     MOVE SITP      WKEY1
     C                     Z-ADD2         DBASE            *************
     C                     MOVE 'INVTPD  'DBFILE           *DB ERROR 02*
     C                     MOVELWKEY1     DBKEY            *************
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Only process FRN/Mortgage Securites. (i.e. Securities where
      ** the Investment type has a processing type of 3 or 8.)
     C           PROT      IFEQ '3'
     C           PROT      OREQ '8'
      *
      ** Only process if Business Day convention is not blanks
     C           BCNV      IFNE *BLANKS
      *
      ** Calculate Next Coupon Date
     C                     Z-ADDBJRDNB    ECD     50
     C                     EXSR ZNCD
      *
      ** Update security with the next coupon date
     C                     UPDATSECTYDF
     C                     ENDIF
     C                     ENDIF
      *
      ** Read next record
     C                     READ SECTY                    80
     C                     ENDDO
      *
     C                     ENDSR
      *
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C           ULRUN     IFEQ *BLANK
     C                     MOVE 'Y'       ULRUN   1
     C                     SETON                     U7U8LR
     C                     DUMP
      *
     C                     RETRN
     C                     ENDIF
      *
      ** If *PSSR already run, then just end the program here.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      /TITLE SR/#LTERM
      *****************************************************************
      *  SRTERM   - Subroutine to do Terminate program                *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *****************************************************************
      *
     C           SRTERM    BEGSR
     C                     SETON                     LR
     C                     RETRN
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      /COPY ZSRSRC,ZDATE1Z2
      /EJECT
      /COPY ZSRSRC,ZDATE2Z2
      /EJECT
     C/COPY ZSRSRC,ZNCDZ3
      /EJECT
      *
**  CPY@
(c) Finastra International Limited 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
