     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Cost of Funds Report')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module
     F*                                                               *
     F*  SE1086 - COST OF FUNDS REPORT                                *
     F*                                                               *
     F*  Function: This report enables an evaluation of the Cost of   *
     F*   (COB)    Funding the security trading business. The User's  *
     F*            Value Date Positions are reported from the Start   *
     F*            of the year to today together with the applicable  *
     F*            Rates. The Cost or Earnings that can be attributed *
     F*            to these Positions is shown.                       *
     F*            Monthly Totals and a final Total are shown for     *
     F*            each Currency within Branch and Book.              *
     F*                                                               *
     F*  Called by: SEC0805 - Cost of Funds Report                    *
     F*                                                               *
     F*            Frequency:                                         *
     F*            - on request during COB                            *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. 227234             Date 17Jul04               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAC005             Date 21Dec98               *
      *                 102506             Date 18Oct98               *
      *                 100017             Date 18Apr96               *
      *                 070070             Date 26Feb96               *
     F*                 092961             DATE 04OCT95               *
     F*                 086491             DATE 15JUN95               *
     F*                 S01455             DATE 05NOV93               *
     F*                 054913             DATE 07MAY93               *
     F*                 S11029             DATE 19APR93               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  227234 - SECURITIES TRADING COST OF FUNDS REPORT (SE1086P1)  *
      *           doesn't show any costs/earning rate, even if there  *
      *           are positions, trades and prices in the system.     *
      *           Adjust WRATE field by 7 decimal places.             *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAC005 - PCA-SE Enhancement.                                 *
     F*  102506 - Interest not correct                                *
     F*  100017 - Position settlement unable to pick up day adj. on   *
     F*           SECTYD. Change subroutine to use DADJN to stop      *
     F*           interest day adjustment being used for amortisation.*
     F*           Recompile over amended subroutine ZDAYS.            *
     F*  070070 - SPURIOUS DATES(IE. 1JAN72) APPEARING ON SE1086P1.   *
     F*           WADD ARRAY WHICH SETS UP END OF MONTH DAY NUMBER    *
     F*           IS NOT USED AT ALL AND SO EOM 'DD' PART OF DDMMYY   *
     F*           IS ALWAYS BLANK. USE THE CORRECT EOM 'DD' VALUE     *
     F*           FOR SETTING UP THE DDMMYY VALUES.                   *
     F*  092961 - Recompile over ZDAYSZ2 which was amended by 086491  *
     F*           (Wrong number of days calculated for day basis 1    *
     F*           when start or end day is last day of February)      *
     F*  086491 - Wrong nbr of days calculated for day basis 1        *
     F*           when start or end day is last day of February       *
     F*           Recompiled for ZSRSRC/ZDAYSZ2 changes.              *
     F*  S01455 - 'FF' ACCOUNT KEY ENHANCEMENTS.                      *
     F*           Recompiled due to changes in PF/TABTB80.            *
     F*  054913 - Only Write End of Report if Print File opened.      *
     F*         - PSEQ needs to be passed to SEQ for RCFAU.           *
     F*  S11029 - Program rewritten to extract Year-to-date Funding   *
     F*           information and to do so correctly and efficiently. *
     F*           (Includes fixes for 051052, etc.)                   *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
      *
     FCOFEXD  IF  E           K        DISK
     FSE1086AUO   E                    PRINTER
     F                                              KINFDS SPOOLU
     FSE1086P1O   E                    PRINTER                        UC
     F                                              KINFDS SPOOL1
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
      *   30  - CAC005 ON                                             *   CAC005
     F*   37  - Multibranching ON                                     *
     F*                                                               *
     F*   60  - Work Indicator                                        *
     F*                                                               *
     F*   89  - End of File on PF/COFEXD                              *
     F*                                                               *
     F*   98  - Date Format: DDMMYY (off); MMDDYY (on)                *
     F* 90-99 - Used by Standard Subroutines                          *
     F*                                                               *
     F* U7+U8 - Database Error                                        *
     F*                                                               *
     F*****************************************************************
      /SPACE 3
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E   I N D E X                              *
     F*                                                               *
     F*  INIT   - Program Initialisation                              *
     F*  PROCES - Detail Processing                                   *
     F*  REPORT - Process Report Lines                                *
     F*  CCOST  - Calculate the Cost/Earnings for a Position          *
     F*                                                               *
     F*  CHGKEY - Call Access Programs, etc. for Changed Details      *
     F*  WP1REC - Format and Write a Detail Record to the Report      *
     F*  WP1MTH - Format and Write a Monthly Total to the Report      *
     F*  WP1CCY - Format and Write a Currency Total to the Report     *
     F*  WP1END - Write End of Report                                 *
     F*                                                               *
     F*  AUDIT  - Produce Audit Report and End Program                *
     F*  *PSSR  - Program Error Processing Subroutine                 *
     F*  RCFP1  - RCF Processing for P1 Report                        *
     F*  RCFAU  - RCF Processing for Audit Report                     *
     F*                                                               *
     F*  ZDATE1 - Validate Date and Convert into Midas Day Number     *
     F*  ZDATE2 - Convert Midas Day Number into Date (DDMMYY/MMDDYY)  *
     F*  ZDAYS  - Calculation Number of Days between Two Dates        *
     F*  ZSEDIT - Edit an Amount                                      *
     F*                                                               *
     F*****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
     E********************WADD       12  2 0                              070070
     E                    WADD   12  12  2 0                              070070
     E                    WAMD       12  5 0
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZDAYSZ1
     E/COPY ZSRSRC,ZSEDITZ1
     I/EJECT
      *
     IRUNDAT    E DS
      ***  Data Structure to obtain Multibranching Indicator.
      *
     ISPOOL1      DS
      ***  File Information Data Structure for SE1086P1.
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      ***  File Information Data Structure for SE1086AU.
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
      ***  Dummy Data Structures used by Access Programs.
      *
     ISDBANK    E DSSDBANKPD
      ***  External data structures for Bank Details
      *
     ISDBOOK    E DSSDBOOKPD
      ***  External data structures for Book Details
      *
     ISDBRCH    E DSSDBRCHPD
      ***  External data structures for Branch Details
      *
     ISDCURR    E DSSDCURRPD
      ***  External data structures for Currency Details
      *
     ISDPRFC    E DSSDPRFCPD                                              CAC005
      ** Profit Centre Accounting                                         CAC005
      *                                                                   CAC005
     IDSFDY     E DSDSFDY
      ***  Short data structure for access objects
      *
     IDSSDY     E DSDSSDY
      *** Long date structure for access objects
      *
     I/COPY ZSRSRC,ZSEDITZ2
      *
     IDBERR       DS                            256
      ***  Local Data Area DS for Database Error Information.
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *
     INEWKEY      DS
      ***  Data Structure to hold Key of present record.
     I                                        1   3 BHBA
     I                                        4   5 BHBK
     I                                        6   8 CCY
      *
     IOLDKEY      DS
      ***  Data Structure to hold Key of last record.
     I                                        1   3 WOBHBA
     I                                        4   5 WOBHBK
     I                                        6   8 WOCCY
      *
     I            DS
      ***  Data Structure to hold Date, Used in SR/INIT.
     I                                        1   60WDATE
     I                                        1   20WDD
     I                                        3   40WMM
     I                                        5   60WYY
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***  Perform Initialisation.
      *
     C                     EXSR INIT
      *
      ***  Perform Detail Processing.
      *
     C                     EXSR PROCES
      *
      ***  Output Audit Report and End Program.
      *
     C                     EXSR AUDIT
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
      /TITLE SR/INIT
      *****************************************************************
      *                                                               *
      *  INIT   - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *  ZDAT   -
      *  ZDAT   -
      *  AOBANKR0 Access Program                                      *
      *                                                               *
      *****************************************************************
      *
     C           INIT      BEGSR                           ***  INIT  ***
      *
      ***  Copyright Statement.
     C                     MOVEACPY@      BIS@   80
      *
      ***  Receive Parameter List.
      *
     C           *ENTRY    PLIST
     C                     PARM           PSEQ    5
      *
      ***  Initialise LDA field.
      *
     C                     MOVEL'SE1086'  DBPGM
      *
      ***  Call Access Program for Bank Details - Title, Run Date and
      ***  Run Day Number.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ***  Handle Database Error.
      *
     C           WRTCD     IFNE *BLANKS
     C                     MOVE 'SDBANKPD'DBFILE           ***************
     C                     Z-ADD001       DBASE            * DB ERROR 01 *
     C                     EXSR *PSSR                      ***************
     C                     ENDIF
      *
      ***  Check System Date Format, DDMMYY (*IN98 off)
      ***                            MMDDYY (*IN98 on).
      *
     C           BJDFIN    IFEQ 'M'
     C                     SETON                     98
     C                     ENDIF
      *
      ***  Access RUNDAT Data Area for Multibranching Indicator.
      *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
     C           AGMBIN    IFEQ 'Y'
     C                     SETON                     37
     C                     ENDIF
      *
      ***  RCF Processing for Audit File.
      *
     C                     EXSR RCFAU
      *
      ***  Define fields.
      *
      ***  Date fields.
     C           *LIKE     DEFN BPPD      WOBPPD
     C           *LIKE     DEFN BPPD      WFRDAT
     C           *LIKE     DEFN BPPD      WTODAT
     C           *LIKE     DEFN BPPD      WENDAT
      *
      ***  Month fields.
     C           *LIKE     DEFN ZMTH      WRUNMM
     C           *LIKE     DEFN ZMTH      WBPPMM
     C           *LIKE     DEFN ZMTH      WFRMM
     C           *LIKE     DEFN ZMTH      WENDMM
      *
      ***  COFEXD Work fields.
     C           *LIKE     DEFN CECP      WPOSN
     C           *LIKE     DEFN ERNR      WERNR
     C           *LIKE     DEFN COFF      WCOFF
     C           *LIKE     DEFN FUDY      WFUDY
     C           *LIKE     DEFN FUDV      WFUDV
      *
      ***  Report Work and Total fields.
     C           *LIKE     DEFN CECP      WCOST
     C           *LIKE     DEFN CECP      WMTOT
     C           *LIKE     DEFN CECP      WCTOT
     C                     Z-ADD0         WRATE  117
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
      *
      ***  Security fields used in SR/ZDAYS.
     C                     MOVE *BLANK    DYBS    1
     C                     Z-ADD0         LCPN    50
     C                     Z-ADD0         ITLD    50
     C                     Z-ADD0         IADJ    30
     C                     Z-ADD0         NCD     50                      102506
     C                     Z-ADD0         CPNR   117                      102506
     C                     Z-ADD0         MATY    50                      102506
     C                     Z-ADD0         FCPN    50                      102506
      *
      ***  Initialise Program fields.
      *
      ***  Obtain Month of Run Date.
      *
     C                     Z-ADDBJRDNB    ZDAYNO
     C                     EXSR ZDATE2
     C                     Z-ADDZMTH      WRUNMM
     C                     Z-ADDZMTH      WMM     20
     C                     Z-ADDZYEAR     WYY     20
      *
      ***  Obtain End of Month Midas Dates for the 12 Months prior to
      ***  the Run Date.
      *
     C                     DO   12
      *
      ***  Calculate Prior Month.
      *
     C           WMM       IFEQ 1
     C                     Z-ADD12        WMM
      *
     C           WYY       IFEQ 00
     C                     Z-ADD99        WYY
     C                     ELSE
     C                     SUB  1         WYY
     C                     ENDIF
      *
     C                     ELSE
     C                     SUB  1         WMM
     C                     ENDIF
      *
      ***  Look Up End Day for Month, (ignoring FEB29 for the moment).
      *
     C                     Z-ADDWMM       M       20
     C                     Z-ADDWADD,M    WDD
      *
      ***  Calculate Midas Day Number for End of Month Date.
      *
     C                     Z-ADDWDATE     ZDATE
      *
      ***  For MMDDYY Format Date, need to swap round Month and Year.
      *
     C           *IN98     IFEQ *ON
     C                     MOVELWMM       WRK4    4
     C                     MOVE WDD       WRK4
     C                     MOVELWRK4      ZDATE
     C                     ENDIF
      *
     C                     EXSR ZDATE1
      *
      ***  If Month is February, check if it is a Leap Year.
      *
     C           M         IFEQ 2
     C           ZDAYNO    ADD  1         WDAY29  50
     C           WDAY29    LOKUPZF29                     60
     C           *IN60     IFEQ *ON
     C                     Z-ADDWDAY29    ZDAYNO
     C                     ENDIF
     C                     ENDIF
      *
      ***  Save End of Month Day Number in Array.
      *
     C                     Z-ADDZDAYNO    WAMD,M
      *
      ***  End of End of Month Dates Loop.
     C                     ENDDO
      *
      ** Check if CAC005 - PCA-SE Enhancement is on.                      CAC005
      *                                                                   CAC005
     C                     MOVE 'N'       CAC005  1                       CAC005
     C                     MOVE *OFF      *IN30                           CAC005
     C                     CALL 'AOSARDR0'                                CAC005
     C                     PARM *BLANKS   @RTCD   7                       CAC005
     C                     PARM '*VERIFY' @OPTN   7                       CAC005
     C                     PARM 'CAC005'  @SARD   6                       CAC005
      *                                                                   CAC005
     C           @RTCD     IFEQ *BLANK                                    CAC005
     C                     MOVE 'Y'       CAC005                          CAC005
     C                     MOVE *ON       *IN30                           CAC005
     C                     ENDIF                                          CAC005
      *                                                                   CAC005
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/PROCES
      *****************************************************************
      *                                                               *
      *  PROCES - Subroutine to do the Detail Processing.             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  AUDIT  - Produce Audit Report and End Program                *
      *  CHGKEY - Call Access Programs, etc. for Changed Details      *
      *  REPORT - Process Report Lines                                *
      *  WP1MTH - Format and Write a Monthly Total to the Report      *
      *  WP1CCY - Format and Write a Currency Total to the Report     *
      *  WP1END - Write End of Report                                 *
      *                                                               *
      *****************************************************************
      *
     C           PROCES    BEGSR                           *** PROCES ***
      *
      ***  Read first record from Extract File.
      *
     C                     READ COFEXD                   89
      *
      ***  If End of Records, (*IN89), Perform: Audit Reporting (No
      ***  Records).
      *
     C           *IN89     IFEQ *ON
     C                     EXSR AUDIT
     C                     ENDIF
      *
      ***  Obtain Branch, Book and Currency information.
      *
     C                     EXSR CHGKEY
      *
      ***  Do Until End of File.
      *
     C           *IN89     DOUEQ*ON
      *
      ***  Count records read.
      *
     C                     ADD  1         RCOUNT
      *
      ***  Process change of Key or Date.
      *
     C           NEWKEY    IFNE OLDKEY
     C           BPPD      ORNE WOBPPD
      *
      ***  Process Report Lines.
      *
     C                     EXSR REPORT
      *
      ***  Call Access Programs for changed Details.
      *
     C           NEWKEY    IFNE OLDKEY
     C                     EXSR CHGKEY
     C                     ENDIF
      *
     C                     ENDIF
      *
      ***  Process record just read.
      *
     C           CEAR      IFEQ 'A'
      ***  Amount
     C                     ADD  CECP      WPOSN
     C                     ELSE
      ***  Rate
     C                     Z-ADDERNR      WERNR
     C                     Z-ADDCOFF      WCOFF
     C                     Z-ADDFUDY      WFUDY
     C                     Z-ADDFUDV      WFUDV
     C                     ENDIF
      *
      ***  Read next record.
      *
     C                     READ COFEXD                   89
      *
      ***  End of Do Until End of Valid Records.
     C                     ENDDO
      *
      ***  Write final records and Totals to Report.
      ***  (Clear NEWKEY to force Write of records up to Run Date.)
      *
     C                     CLEARNEWKEY
     C                     EXSR REPORT
     C                     EXSR WP1MTH
     C                     EXSR WP1CCY
     C                     EXSR WP1END
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/REPORT
      *****************************************************************
      *                                                               *
      *  REPORT - Process Report Lines.                               *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls:                                                       *
      *  CCOST  - Calculate the Cost/Earnings for a Position          *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *  WP1MTH - Format and Write a Monthly Total to the Report      *
      *                                                               *
      *****************************************************************
      *
     C           REPORT    BEGSR                           *** REPORT ***
      *
      ***  If record is a different Branch/Book/Currency then End Date
      ***  should be Run Date. Otherwise, the End Date is the Position
      ***  Date of the record just read.
      *
     C           NEWKEY    IFNE OLDKEY
     C                     Z-ADDBJRDNB    WENDAT
     C                     MOVE WRUNMM    WENDMM
     C                     ELSE
     C                     Z-ADDBPPD      WENDAT
     C                     Z-ADDWBPPMM    WENDMM
     C                     ENDIF
      *
      ***  Calculate and Write records to Print File up to the End Date
      *
     C           WTODAT    DOUEQWENDAT
      *
      ***  Work out the intermediate To Date: If From Date Month is not
      ***  the same as the Final End Date Month, then must calculate to
      ***  the From Date Month End, Else calculate to End Date.
      *
     C           WFRMM     IFNE WENDMM
     C                     Z-ADDWFRMM     M
     C                     Z-ADDWAMD,M    WTODAT
     C                     ELSE
     C                     Z-ADDWENDAT    WTODAT
     C                     ENDIF
      *
      ***  Calculate the Cost/Earnings to the To Date.
      *
     C                     EXSR CCOST
      *
      ***  Write out the record.
      *
     C                     EXSR WP1REC
      *
      ***  If going across a Month End, need to Output Monthly Total.
      *
     C           WFRMM     IFNE WENDMM
     C                     EXSR WP1MTH
      *
      ***  Calculate the Start of next Month Date and Month number.
      *
     C           WTODAT    ADD  1         WFRDAT
      *
     C           WFRMM     IFEQ 12
     C                     Z-ADD1         WFRMM
     C                     ELSE
     C                     ADD  1         WFRMM
     C                     ENDIF
      *
     C                     ENDIF
      *
      ***  End of Write records to End Date.
     C                     ENDDO
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/CCOST
      *****************************************************************
      *                                                               *
      *  CCOST  - Subroutine to Calculate the Cost/Earnings Amount    *
      *           for the current Currency Position.                  *
      *                                                               *
      *  Called By: REPORT                                            *
      *  Calls    :                                                   *
      *                                                               *
      *****************************************************************
      *
     C           CCOST     BEGSR                           *** CCOST  ***
      *
      ***  Initialise the Rate. If Position is negative, use Earnings
      ***  Rate, else use Funding Rate.
      ***  (Note: Rates are held on File as 11,0 instead of 11,7 )
      *
     C           WPOSN     IFLT 0
     C                     MOVE WERNR     WRATE
     C                     ELSE
     C                     MOVE WCOFF     WRATE
     C                     ENDIF
      *
      ***  If the Position is zero then the Cost is also zero.
      *
     C           WPOSN     IFEQ 0
     C                     CLEARWCOST
     C                     ELSE
      *
      ***  Calculate Number of Days in Period.
      ***  (Note: Currently, only Day Bases 1, 2 and 3 are permitted.)
      *
     C                     Z-ADDWFRDAT    ZSDATE
     C                     Z-ADDWTODAT    ZEDATE
     C                     MOVE WFUDY     DYBS
      *
     C                     EXSR ZDAYS
      *
      ***  Calculate Number of Days in the Interest Year.
      ***  (Note: Currently, only Divisor Bases 1 and 2 are permitted.)
      *
     C           WFUDV     IFEQ 1
     C                     Z-ADD36000     WDIV    50
     C                     ELSE
     C                     Z-ADD36500     WDIV
     C                     ENDIF
      *
      ***  Calculate Cost/Earnings Amount:
      ***  (Note Position value has already been multiplied by Price).
      ***
      ***   Position * Days in Period * Cost/Earnings Rate
      ***  ------------------------------------------------
      ***                 Days in Year * 100
      *
     C           WPOSN     MULT ZDDAYS    WCALC  307
     C                     MULT WRATE     WCALC     H
     C           WCALC     DIV  WDIV      WCOST     H
      *
      ***  End of If/Else Position zero.
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/CHGKEY
      *****************************************************************
      *                                                               *
      *  CHGKEY - Call Access Programs for any changed details.       *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls:                                                       *
      *  WP1MTH - Format and Write a Monthly Total to the Report      *
      *  WP1CCY - Format and Write a Currency Total to the Report     *
      *  WP1END - Write End of Report                                 *
      *  RCFP1  - RCF Processing for P1 Report                        *
      *  AOBRCHR0 Access Program                                      *
      *  AOBOOKR0 Access Program                                      *
      *  AOCURRR0 Access Program                                      *
      *                                                               *
      *****************************************************************
      *
     C           CHGKEY    BEGSR                           *** CHGKEY ***
      *
      ***  Unless first time through, must write Last Monthly Total and
      ***  Currency Total for previous Branch/Book/Currency.
      *
     C           RCOUNT    IFNE 0
     C                     EXSR WP1MTH
     C                     EXSR WP1CCY
     C                     ENDIF
      *
      ***  Check for change of Branch.
      *
     C           BHBA      IFNE WOBHBA
      *
      ***  Access Branch file for Branch Name.
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   WRTCD
     C                     PARM '*KEY   ' WOPTN
     C                     PARM BHBA      WBRCA   3
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
      *
      ***  Database Error if not found.
      *
     C           WRTCD     IFNE *BLANKS
     C                     MOVEL'SDBRCHPD'DBFILE           ***************
     C                     MOVELWBRCA     DBKEY            * DB ERROR 02 *
     C                     Z-ADD002       DBASE            ***************
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ***  Write End of Report, Close Old Print File and Open New one.
      ***  (Only Write End of Report if not first time through.)          054913
      *
     C           RCOUNT    IFNE 0                                         054913
     C                     EXSR WP1END
     C                     ENDIF                                          054913
     C                     CLOSESE1086P1
     C                     OPEN SE1086P1
     C                     EXSR RCFP1
      *
     C                     ENDIF
      *
      ***  Check for change of Book.
      *
     C           BHBK      IFNE WOBHBK
      *                                                                   CAC005
      ** Move book to bookcode-workfield                                  CAC005
      *                                                                   CAC005
     C                     MOVE BHBK      WBOOK   2                       CAC005
      *                                                                   CAC005
      ** If CAC005 is on, retrieve user book from AOBKPCR0.               CAC005
      *                                                                   CAC005
     C           CAC005    IFEQ 'Y'                                       CAC005
     C                     CALL 'AOBKPCR0'                                CAC005
     C                     PARM           PRTCD   7                       CAC005
     C                     PARM '*USER  ' POPTN   7                       CAC005
     C           PMBKCD    PARM *BLANKS   PMBKCD  2                       CAC005
     C           PMPRFC    PARM *BLANKS   PMPRFC  4                       CAC005
     C                     PARM BHBK      PMPSBK  2                       CAC005
      *                                                                   CAC005
     C           PRTCD     IFEQ *BLANKS                                   CAC005
     C                     MOVELPMBKCD    WBOOK                           CAC005
     C                     MOVE PMPRFC    PPRFC                           CAC005
      *                                                                   CAC005
     C                     CALL 'AOPRFCR0'                                CAC005
     C                     PARM           @RTCD                           CAC005
     C                     PARM '*KEY   ' @OPTN                           CAC005
     C                     PARM PPRFC     @PRFC   4                       CAC005
     C           SDPRFC    PARM SDPRFC    DSFDY                           CAC005
      *                                                                   CAC005
     C           @RTCD     IFEQ *BLANKS                                   CAC005
     C                     MOVE DSPCDS    PPCDS                           CAC005
     C                     ELSE                                           CAC005
     C                     MOVE *BLANKS   PPCDS                           CAC005
     C                     ENDIF                                          CAC005
      *                                                                   CAC005
     C                     ELSE                                           CAC005
     C                     MOVEL*BLANKS   PPRFC                           CAC005
     C                     MOVE *BLANKS   PPCDS                           CAC005
     C                     ENDIF                                          CAC005
     C                     ENDIF                                          CAC005
      *
      ***  Access Book file for Book Name.
      *
     C                     CALL 'AOBOOKR0'
     C                     PARM *BLANKS   WRTCD
     C                     PARM '*KEY   ' WOPTN
     C***********          PARM BHBK      WBKCD   2                       CAC005
     C                     PARM WBOOK     WBKCD   2                       CAC005
     C           SDBOOK    PARM SDBOOK    DSFDY
      *
      ***  Database Error if not found.
      *
     C           WRTCD     IFNE *BLANKS
     C                     MOVEL'SDBOOKPD'DBFILE           ***************
     C                     MOVELWBKCD     DBKEY            * DB ERROR 03 *
     C                     Z-ADD003       DBASE            ***************
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDIF
      *
      ***  Check for change of Currency
      *
     C           CCY       IFNE WOCCY
      *
      ***  Access Currency file for Currency Name and Decimal Places.
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   WRTCD
     C                     PARM '*KEY   ' WOPTN
     C                     PARM CCY       WCYCD   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
      ***  Database Error if not found.
      *
     C           WRTCD     IFNE *BLANKS
     C                     MOVEL'SDCURRPD'DBFILE           ***************
     C                     MOVELWCYCD     DBKEY            * DB ERROR 04 *
     C                     Z-ADD004       DBASE            ***************
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDIF
      *
      ***  If any of the Key fields changed, must write out new Header.
      *
     C                     WRITEHEADP1
      *
      ***  Process the New Position Date.
      *
     C                     Z-ADDBPPD      WOBPPD
     C                     Z-ADDBPPD      WFRDAT
     C                     Z-ADDBPPD      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZMTH      WBPPMM
     C                     MOVE ZMTH      WFRMM
      *
      ***  Save the New Key.
      *
     C                     MOVE NEWKEY    OLDKEY
      *
      ***  Clear Work fields.
      *
     C                     CLEARWPOSN
     C                     CLEARWERNR
     C                     CLEARWCOFF
     C                     CLEARWFUDY
     C                     CLEARWFUDV
     C                     CLEARWCOST
     C                     CLEARWMTOT
     C                     CLEARWCTOT
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/WP1REC
      *****************************************************************
      *                                                               *
      *  WP1REC - Subroutine to Write a Detail record to the Report.  *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls:                                                       *
      *  ZSFILE   Access Program                                      *
      *                                                               *
      *****************************************************************
      *
     C           WP1REC    BEGSR                           *** WP1REC ***
      *
      ***  Add Cost to Totals fields.
      *
     C                     ADD  WCOST     WMTOT
     C                     ADD  WCOST     WCTOT
      *
      ***  Format fields to output to the Report.
      *
      ***  Position Date.
      *
     C                     Z-ADDWFRDAT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RDATE
      *
      ***  Position (unsigned).
      *
     C                     Z-ADDWPOSN     ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVELZOUT21    WK20   20
     C                     MOVE WK20      RPOSN
      *
      ***  Narrative.
      *
     C           WPOSN     IFGT 0
     C                     MOVE 'LONG '   RNARR
     C                     ELSE
     C           WPOSN     IFEQ 0
     C                     MOVE 'FLAT '   RNARR
     C                     ELSE
     C                     MOVE 'SHORT'   RNARR
     C                     ENDIF
     C                     ENDIF
      *
      ***  Cost/Earnings Rate.
      *
     C**********           Z-ADDWRATE     ZFLD15                                              227234
     C           WRATE     MULT 10000000  ZFLD15                                              227234
     C                     Z-ADD7         ZDECS
     C                     MOVE 'L'       ZECODE
     C                     EXSR ZSEDIT
     C**********           MOVELZOUT21    RRATE                                               227234
     C                     MOVE ZOUT21    RRATE                                               227234
      *
      ***  Cost/Earnings Amount.
      *
     C                     Z-ADDWCOST     ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVELZOUT21    RCOST
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     Z-ADD1         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ***  Write out Detail Record.
      *
     C                     WRITEDETAIL
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/WP1MTH
      *****************************************************************
      *                                                               *
      *  WP1MTH - Subroutine to Write a Monthly Total to the Report.  *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           WP1MTH    BEGSR                           *** WP1MTH ***
      *
      ***  Find the three character narrative for the Month.
      *
     C                     Z-ADDWFRMM     M
     C                     MOVE ZMNM,M    RMONTH
      *
      ***  Format the Total Amount.
      *
     C                     Z-ADDWMTOT     ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVELZOUT21    RMTOT
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     Z-ADD3         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ***  Write out Monthly Total Format.
      *
     C                     WRITEMTHTOT
      *
      ***  Clear the Monthly Total.
      *
     C                     CLEARWMTOT
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/WP1CCY
      *****************************************************************
      *                                                               *
      *  WP1CCY - Subroutine to Write a Currency Total to the Report. *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           WP1CCY    BEGSR                           *** WP1CCY ***
      *
      ***  Format the Total Amount.
      *
     C                     Z-ADDWCTOT     ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVELZOUT21    RCTOT
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     Z-ADD2         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ***  Write out Currency Total Format.
      *
     C                     WRITECCYTOT
      *
      ***  Clear the Currency Total.
      *
     C                     CLEARWCTOT
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/WP1END
      *****************************************************************
      *                                                               *
      *  WP1MTH - Subroutine to Write End of Report.                  *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           WP1END    BEGSR                           *** WP1END ***
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     Z-ADD4         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ***  Write out Monthly Total Format.
      *
     C                     WRITETRAILP1
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/AUDIT
      *****************************************************************
      *                                                               *
      *  AUDIT  - Subroutine to Output Audit report and End Program.  *
      *                                                               *
      *  Called By: Main Processing, PROCES, *PSSR                    *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           AUDIT     BEGSR                           *** AUDIT  ***
      *
      ***  Output Report Header and File Controls.
      *
     C                     WRITEHEADAU
     C                     WRITECONTROL
      *
      ***  If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
     C                     WRITEDBERROR
     C                     ELSE
      *
      ***  Or, if no records read, Output "No Details" message.
      *
     C           RCOUNT    IFEQ 0
     C                     WRITENODTLS
     C                     ENDIF
     C                     ENDIF
      *
      ***  End Program and Return.
      *
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
     C           *NAMVAR   DEFN LDA       WLDA  256
     C           *LOCK     IN   WLDA
     C                     MOVELDBERR     WLDA
     C                     OUT  WLDA
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     EXSR AUDIT
     C                     ENDIF
      *
      ***  If *PSSR already run, then just end the program here.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFP1
      *****************************************************************
      *                                                               *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ     5
     C                     PARM BHBA      SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFAU
      *****************************************************************
      *                                                               *
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFAU     BEGSR                            *** RCFAU  ***
      *
      ***  Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C***********          PARM           SEQ                             054913
     C                     PARM PSEQ      SEQ                             054913
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/ZDATE1
     C/COPY ZSRSRC,ZDATE1Z2
      /TITLE SR/ZDATE2
     C/COPY ZSRSRC,ZDATE2Z2
      /TITLE SR/ZDAYS
     C/COPY ZSRSRC,ZDAYSZ2
      /TITLE SR/ZSEDIT
     C/COPY ZSRSRC,ZSEDITZ3
      /EJECT
      ***
**  CPY@
(c) Finastra International Limited 2001
**  WADD - END OF MONTH DAY NUMBERS (EXCLUDING 29TH FEB)                  070070
312831303130313130313031                                                  070070
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**   ZF29 DAY NOS OF FEB 29 TO 2096 (INCLUDING 2000)
0006001521029820444305904073650882610287117481320914670161311759219053
2051421975234362489726358278192928030741322023366335124365853804639507
40968424294389045351
