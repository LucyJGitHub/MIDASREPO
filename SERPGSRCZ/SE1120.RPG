     H        1                                                           SE1120
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Depot and Client Holdings Extract')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module
     F*                                                               *
     F*  SE1120 - DEPOT AND CLIENT HOLDINGS PER SECURITY EXTRACT      *
     F*                                                               *
     F*  Function: To extract security positions, obtaining for each  *
     F*   (COB)    security:                                          *
     F*            - depot positions                                  *
     F*            - portfolio client positions                       *
     F*            - user (bank) book positions                       *
     F*            for the Depot and Client Holdings per Security     *
     F*            Report                                             *
     F*                                                               *
     F*  Called By: SEC0809 - Depot and Client Holdings by Security   *
     F*                                                               *
     F*            Frequency:                                         *
     F*            - On request during COB                            *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 BUG7985            Date 18Aug05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*                 095547             Date 06Jan97               *
     F*                 052254             Date 10Jan94               *
     F*                 S01117             DATE 08NOV90               *
     F*                 E14825             DATE 22AUG88               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  BUG7985 - Do not access TABLETR instead access AOBRCHR1.     *
     F*  095547 - DON'T EXTRACT IF ALL POSITIONS ARE 0                *
     F*  052254 - RECOMPILED as CURRENT FACTOR amended from 9,8       *
     F*           to 10,9                                             *
     F*  S01117 - Multi-Branching                                     *
     F*  E14825 - Report was showing Customer Holdings as negative    *
     F*           when the Client is Long in a Security.              *
     F*                                                               *
     F*****************************************************************
      *
     FDPOSN   IF  E           K        DISK
     FCPOSN   IF  E           K        DISK
     FUDEPP   IF  E           K        DISK
     F*TABSE***IF  E           K        DISK                                                 BUG7985
     F**********  TABLETAF                          KIGNORE                                  BUG7985
     F**********  TABTB10F                          KIGNORE                           S01117 BUG7985
     F**********  TABTB11F                          KIGNORE                                  BUG7985
     F**********  TABTB36F                          KIGNORE                                  BUG7985
     F**********  TABTB40F                          KIGNORE                                  BUG7985
     F**********  TABTE10F                          KIGNORE                                  BUG7985
     F**********  TABTE20F                          KIGNORE                                  BUG7985
     F**********  TABTG10F                          KIGNORE                                  BUG7985
     F**********  TABTG20F                          KIGNORE                                  BUG7985
     F**********  TABLETHF                          KIGNORE                                  BUG7985
     F*********** TABLETJF                          KIGNORE               S01117
     F**********  TABLETKF                          KIGNORE                                  BUG7985
     F**********  TABLETLF                          KIGNORE                                  BUG7985
     F**********  TABLETNF                          KIGNORE                                  BUG7985
     F**********  TABLETPF                          KIGNORE                                  BUG7985
     F**********  TABLETXF                          KIGNORE                                  BUG7985
     F**********  TABLET2F                          KIGNORE                                  BUG7985
     F**********  TABLET3F                          KIGNORE                                  BUG7985
     F**********  TABLET5F                          KIGNORE                                  BUG7985
     FDCSEXD  O   E                    DISK
     FDCSEXA  O   E                    DISK
     FSE1120AUO   E                    PRINTER
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   10  - First cycle indicator                                 *
     F*   50  - File chain fail indicator                             *
     F*   90  - End file processing indicator                         *
     F*                                                               *
     F*   U7  - DATABASE ERROR                                        *
     F*   U8  - DATABASE ERROR                                        *
     F*                                                               *
     F*****************************************************************
      /SPACE 3
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E   I N D E X                              *
     F*                                                               *
     F*  BAINIT - Initialisation routine                              *
     F*  BBMAIN - Main-line processing                                *
     F*  BCCLOS - Closing routine                                     *
     F*  FDPOSN - Process Depot Positions file                        *
     F*  FCPOSN - Process Customer Positions file                     *
     F*  FUDEPP - Process User Depot Positions file                   *
     F*  PAUDIT - Process audit details                               *
     F*  WDCSEX - Write Depot & Client Holdings Extract detail        *
     F*  *PSSR  - Error handling                                      *   S01117
     F*                                                               *
     F***ZSYSTM*-*Chain*to*the*installation*control*data*record       *   S01117
     F*                                                               *
     F*****************************************************************
      /EJECT
     E                    CPY@    1   1 80
      /SPACE 3
     I*
     I*  Local data area
     I*LDA*******UDS                            256                       S01117
     I***********                           134 177 DBLOT                 S01117
     I***********                           134 138 DBFILE                S01117
     I***********                           139 167 DBKEY                 S01117
     I***********                           168 175 DBPGM                 S01117
     I***********                           176 177 DBASE                 S01117
     ILDA         DS                            256                       S01117
     I                                      134 183 DBLOT                 S01117
     I                                      134 141 DBFILE                S01117
     I                                      142 170 DBKEY                 S01117
     I                                      171 180 DBPGM                 S01117
     I                                      181 1830DBASE                 S01117
     I*
     IRUNDT       DS                                                      S01117
      ** DATA AREA RUNDAT                                                 S01117
     I                                        1   7 RUNA                  S01117
     I                                    P   8  100RUND                  S01117
     I                                       11  11 SSF                   S01117
     I                                       12  12 DATF                  S01117
     I                                       13  13 MBIN                  S01117
     I**  EXTERNAL DS FOR BANK DETAILS                                    S01117
     ISDBANK    E DSSDBANKPD                                              S01117
     I              BJURPT                          TITL                  S01117
     I** DATA STRUCTURE FOR ACCESS OBJECTS, SHORT DATA STRUCTURE          S01117
     IDSFDY     E DSDSFDY                                                 S01117
     IDSSDY     E DSDSSDY                                                                    BUG7985
      * second DS for access programs, long data structure                                   BUG7985
      *                                                                                      BUG7985
     ISDBRCH    E DSSDBRCHPD                                                                 BUG7985
     I              QQDFAC                          WWDFAC                                   BUG7985
      *                                                                                      BUG7985
     I*
     I*  Program status data structure
     I*
     IPSDS       SDS
     I                                      244 246 WSID
     I                                      254 263 USRID
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     C/EJECT
     C*================================================================
     C*  P R O G R A M     S T A R T
     C*================================================================
     C*
     C**  Initialisation routine
     C*
     C                     EXSR BAINIT
     C*
     C**  Mainline processing
     C*
     C                     EXSR BBMAIN
     C*
     C**  Closing routine
     C*
     C*================================================================
     C*  P R O G R A M     E N D
     C*================================================================
     C*
     C                     EXSR BCCLOS
      /EJECT
     C*****************************************************************
     C*                                                               *
     C*  BAINIT  - INITIALISATION ROUTINE                             *
     C*                                                               *
     C*****************************************************************
     C*
     C           BAINIT    BEGSR
     C*
     C**  Prepare LDA
     C*
     C           *NAMVAR   DEFN           LDA                             S01117
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVE *BLANKS   DBLOT
     C                     MOVEL'SE1120'  DBPGM
     C                     OUT  LDA                                       S01117
     C*
     C** IN THE DATA AREA RUNDAT                                          S01117
     C*
     C           *NAMVAR   DEFN RUNDAT    RUNDT                           S01117
     C                     IN   RUNDT                                     S01117
     C*
     C           DATF      IFEQ 'M'                                       S01117
     C                     SETON                     98                   S01117
     C                     END                                            S01117
     C*
     C**   ACCESS BANK DETAILS, FOR TITLE                                 S01117
     C*
     C                     CALL 'AOBANKR0'                                S01117
     C                     PARM '*MSG   ' @RTCD   7                       S01117
     C                     PARM '*FIRST ' @OPTN   7                       S01117
     C           SDBANK    PARM SDBANK    DSFDY                           S01117
     C*                                                                   S01117
     C           @RTCD     IFNE *BLANK                                    S01117
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVEL'SDBANKPD'DBFILE                          S01117
     C                     MOVEL@OPTN     DBKEY            ***************S01117
     C                     MOVEL@OPTN     DBOPTN  7        * DBERROR 11  *S01117
     C                     Z-ADD11        DBASE            ***************S01117
     C                     OUT  LDA                                       S01117
     C                     SETON                     U7U8                 S01117
     C                     WRITEHEADAU                                    S01117
     C                     WRITEERROR                                     S01117
     C                     EXSR *PSSR                                     S01117
     C                     END                                            S01117
     C*
     C**  Define fields
     C*
     C************LIKE     DEFN UDBR      SAVBR                           S01117
     C           *LIKE     DEFN UDBA      SAVBR                           S01117
     C           *LIKE     DEFN UDSN      SAVSN
     C           *LIKE     DEFN UDBK      SAVBK
     C           *LIKE     DEFN DCNT      TDCNT
     C           *LIKE     DEFN DCNV      TDCNV
     C           *LIKE     DEFN DCNS      TDCNS
     C           *LIKE     DEFN DCTR      TDCTR
     C           *LIKE     DEFN DCVR      TDCVR
     C********** *LIKE     DEFN BICN      SAVCN
     C           *LIKE     DEFN A8BICN    SAVCN
     C*
     C**  Initialise fields
     C*
     C                     Z-ADD*ZEROS    CRDEP   50
     C                     Z-ADD*ZEROS    CRCUS   50
     C                     Z-ADD*ZEROS    CRUSE   50
     C                     Z-ADD*ZEROS    CXDEP   50
     C                     Z-ADD*ZEROS    CXCUS   50
     C                     Z-ADD*ZEROS    CXUSE   50
     C                     Z-ADD*ZEROS    CTOTL   50
     C                     Z-ADD*ZEROS    PAGE
     C                     SETOF                     10
     C*
     C****Get*Installation*Control*Data*record*1***                       S01117
     C***********                                                         S01117
     C***********          EXSR ZSYSTM                                    S01117
     C***********                                                         S01117
     C****If*error*in*ZSYSTM,*abort*program********                       S01117
     C***********                                                         S01117
     C************IN99     IFEQ '1'                                       S01117
     C***********          MOVE '01'      DBASE             ************  S01117
     C***********          MOVEL'TABLE'   DBFILE            * DATABASE *  S01117
     C***********          MOVELZTABKY    DBKEY             * ERROR 01 *  S01117
     C***********          SETON                     U7U8   ************  S01117
     C***********          WRITEHEADAU                                    S01117
     C***********          WRITEERROR                                     S01117
     C***********          WRITETRAILAU                                   S01117
     C***********                                                         S01117
     C***********          END                                            S01117
     C*
     C                     ENDSR
     C*
      /EJECT
     C*****************************************************************
     C*                                                               *
     C*  BBMAIN  - MAINLINE PROCESSING                                *
     C*                                                               *
     C*****************************************************************
     C*
     C           BBMAIN    BEGSR
     C*
     C           *INU7     IFEQ '1'
     C           *INU8     ANDEQ'1'
     C                     GOTO BBEND
     C                     END
     C*
     C**  Process Depot Positions file & write extract records
     C*
     C                     EXSR FDPOSN
     C*
     C**  Process Customer Positions file & write extract records
     C*
     C                     EXSR FCPOSN
     C*
     C**  Process User Depot Positions file & write extract records
     C*
     C                     EXSR FUDEPP
     C*
     C           *INU7     IFNE '1'
     C           *INU8     ANDNE'1'
     C*
     C**  Process Depot & Client Holdings Extract audit details
     C*
     C                     EXSR PAUDIT
     C*
     C                     END
     C*
     C           BBEND     ENDSR
     C*
      /SPACE 3
     C********************************************************************
     C*                                                                  *
     C*  BCCLOS  - CLOSING ROUTINE                                       *
     C*                                                                  *
     C********************************************************************
     C**
     C           BCCLOS    BEGSR
     C*
     C                     SETON                     LR
     C*
     C                     RETRN
     C*
     C                     ENDSR
     C*                                                                =====
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*  FCPOSN  - Sub routine to read Customer Positions file and       *
     C*            write an Extract detail record                        *
     C*                                                                  *
     C********************************************************************
     C**
     C           FCPOSN    BEGSR
     C*
     C           *LOVAL    SETLLCPOSNDF
     C*
     C**  Read file & count records
     C*
     C                     READ CPOSNDF                  90
     C*
     C           *IN90     DOWEQ'0'
     C                     ADD  1         CRCUS
     C*
     C**  Load output fields
     C*
     C                     MOVE 'D'       DCID
     C***********          Z-ADDCSBR      DCBR                            S01117
     C                     MOVE CSBA      DCBA                            S01117
     C                     MOVE CSSC      DCSS
     C                     MOVE 'P'       DCDP
     C**********           Z-ADDCSCN      DCCN                                                CSD027
     C                     MOVE CSCN      DCCN                                                CSD027
     C                     MOVE *BLANKS   DCBC
     C*********************Z-SUBCSNT      DCNT                            E14825
     C*********************Z-SUBCSNV      DCNV                            E14825
     C*********************Z-SUBCSNS      DCNS                            E14825
     C                     Z-ADDCSNT      DCNT                            E14825
     C                     Z-ADDCSNV      DCNV                            E14825
     C                     Z-ADDCSNS      DCNS                            E14825
     C                     Z-ADD*ZEROS    DCTR
     C                     Z-ADD*ZEROS    DCVR
     C*
     C**  Count records extracted
     C*
     C           DCNT      IFNE 0                                         095547
     C           DCNV      ORNE 0                                         095547
     C           DCNS      ORNE 0                                         095547
     C                     ADD  1         CXCUS
     C*
     C**  Write Extract record
     C*
     C                     WRITEDCSEXDF
     C                     END                                            095547
     C*
     C                     READ CPOSNDF                  90
     C                     END
     C*
     C                     ENDSR                                       SR**
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*  FDPOSN  - Sub routine to read Depot Positions file and          *
     C*            write an Extract detail record                        *
     C*                                                                  *
     C********************************************************************
     C**
     C           FDPOSN    BEGSR
     C*
     C           *LOVAL    SETLLDPOSNDF
     C*
     C**  Read file & count records
     C*
     C                     READ DPOSNDF                  90
     C*
     C           *IN90     DOWEQ'0'
     C                     ADD  1         CRDEP
     C*
     C**  Load output fields
     C*
     C                     MOVE 'D'       DCID
     C***********          Z-ADDDSBR      DCBR                            S01117
     C                     MOVE DSBA      DCBA                            S01117
     C                     MOVE DSSC      DCSS
     C                     MOVE 'D'       DCDP
     C**********           Z-ADDDDPT      DCCN                                                CSD027
     C                     MOVE DDPT      DCCN                                                CSD027
     C                     MOVE *BLANKS   DCBC
     C                     Z-ADDDSNT      DCNT
     C                     Z-ADDDSNV      DCNV
     C                     Z-ADDDSNS      DCNS
     C                     Z-ADDDSRT      DCTR
     C                     Z-ADDDSRV      DCVR
     C*
     C**  Count records extracted
     C*
     C           DCNT      IFNE 0                                         095547
     C           DCNV      ORNE 0                                         095547
     C           DCNS      ORNE 0                                         095547
     C                     ADD  1         CXDEP
     C*
     C**  Write Extract record
     C*
     C                     WRITEDCSEXDF
     C                     END                                            095547
     C*
     C                     READ DPOSNDF                  90
     C                     END
     C*
     C                     ENDSR                                       SR**
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*  FUDEPP  - Sub routine to read User Depot Positions file and     *
     C*            write an Extract detail record                        *
     C*                                                                  *
     C********************************************************************
     C**
     C           FUDEPP    BEGSR
     C*
     C           *LOVAL    SETLLUDEPPDF
     C*
     C**  Read file & count records
     C*
     C                     READ UDEPPDF                  90
     C***********          Z-ADDUDBR      SAVBR                           S01117
     C                     MOVE UDBA      SAVBR                           S01117
     C                     MOVE UDSN      SAVSN
     C                     MOVE UDBK      SAVBK
     C*
     C           *IN90     DOWEQ'0'
     C                     ADD  1         CRUSE
     C*
     C**  If change of Branch access Customer No.
     C*
     C***********UDBR      IFNE SAVBR                                     S01117
     C           UDBA      IFNE SAVBR                                     S01117
     C           *IN10     OREQ '0'
     C**********           MOVE *BLANKS   KEY12  12                                          BUG7985
     C***********          MOVE UDBR      KEY12                           S01117
     C**********           MOVE UDBA      KEY12                           S01117             BUG7985
     C**********           MOVEL'10'      KEY12                                              BUG7985
     C********** KEY12     CHAINTABLETRF             50                                      BUG7985
     C*
     C**   .. data base error if not found
     C*
     C********** *IN50     IFEQ '1'
     C********** *IN50     OREQ '0'
     C********** RECI      ANDEQ'*'
     C                     CALL 'AOBRCHR1'                                                   BUG7985
     C                     PARM *BLANKS   PRTCD   7                                          BUG7985
     C                     PARM '*KEY   ' POPTN   7                                          BUG7985
     C                     PARM UDBA      PBRCH   3                                          BUG7985
     C           SDBRCH    PARM SDBRCH    DSSDY                                              BUG7985
      *                                                                                      BUG7985
     C           PRTCD     IFNE *BLANKS                                                      BUG7985
     C           *LOCK     IN   LDA                                       S01117
     C***********          MOVE '02'      DBASE             ************  S01117
     C                     Z-ADD2         DBASE             ************  S01117
     C                     MOVE 'SDBRCHPD'DBFILE                                             BUG7985
     C                     MOVELUDBA      DBKEY                                              BUG7985
     C**********           MOVEL'TABLE'   DBFILE            * DATABASE *                     BUG7985
     C**********           MOVELKEY12     DBKEY             * ERROR 02 *                     BUG7985
     C                     OUT  LDA                         ************  S01117
     C                     SETON                     U7U8
     C                     WRITEHEADAU
     C                     WRITEERROR
     C***********          WRITETRAILAU                                   S01117
     C                     EXSR *PSSR                                     S01117
     C                     GOTO FUEND
     C                     END
     C           *IN10     IFEQ '0'
     C**********           Z-ADDBICN      SAVCN                                              BUG7985
     C                     MOVE A8BICN    SAVCN                                              BUG7985
     C                     END
     C                     END
     C*
     C**  If change of Branch/Security/Book ...
     C*
     C***********UDBR      IFNE SAVBR                                     S01117
     C           UDBA      IFNE SAVBR                                     S01117
     C           UDSN      ORNE SAVSN
     C           UDBK      ORNE SAVBK
     C*
     C**    .. load output fields
     C*
     C                     MOVE 'D'       DCID
     C***********          Z-ADDSAVBR     DCBR                            S01117
     C                     MOVE SAVBR     DCBA                            S01117
     C                     MOVE SAVSN     DCSS
     C                     MOVE 'U'       DCDP
     C**********           Z-ADDSAVCN     DCCN                                               BUG7985
     C                     MOVE SAVCN     DCCN                                               BUG7985
     C                     MOVE SAVBK     DCBC
     C                     Z-ADDTDCNT     DCNT
     C                     Z-ADDTDCNV     DCNV
     C                     Z-ADDTDCNS     DCNS
     C                     Z-ADDTDCTR     DCTR
     C                     Z-ADDTDCVR     DCVR
     C*
     C**    .. count records extracted
     C*
     C           DCNT      IFNE 0                                         095547
     C           DCNV      ORNE 0                                         095547
     C           DCNS      ORNE 0                                         095547
     C                     ADD  1         CXUSE
     C*
     C**    .. write Extract record
     C*
     C                     WRITEDCSEXDF
     C                     END                                            095547
     C*
     C**    .. initialise fields
     C*
     C                     Z-ADD*ZEROS    TDCNT
     C                     Z-ADD*ZEROS    TDCNV
     C                     Z-ADD*ZEROS    TDCNS
     C                     Z-ADD*ZEROS    TDCTR
     C                     Z-ADD*ZEROS    TDCVR
     C*
     C**    .. save fields
     C*
     C***********          Z-ADDUDBR      SAVBR                           S01117
     C                     MOVE UDBA      SAVBR                           S01117
     C                     MOVE UDSN      SAVSN
     C                     MOVE UDBK      SAVBK
     C**********           Z-ADDBICN      SAVCN                                              BUG7985
     C                     MOVE A8BICN    SAVCN                                              BUG7985
     C                     END
     C*
     C**  Accumulate totals
     C*
     C                     ADD  UDNT      TDCNT
     C                     ADD  UDNV      TDCNV
     C                     ADD  UDNS      TDCNS
     C                     ADD  UNRT      TDCTR
     C                     ADD  USNR      TDCTR
     C                     ADD  UNRV      TDCVR
     C                     ADD  USRV      TDCVR
     C*
     C                     READ UDEPPDF                  90
     C                     SETON                     10
     C                     END
     C*
     C**  Write record for last read or 1 record only
     C*
     C           *IN90     IFEQ '1'
     C           *IN10     ANDEQ'1'
     C                     MOVE 'D'       DCID
     C***********          Z-ADDUDBR      DCBR                            S01117
     C                     MOVE UDBA      DCBA                            S01117
     C                     MOVE UDSN      DCSS
     C                     MOVE 'U'       DCDP
     C**********           Z-ADDBICN      DCCN                                               BUG7985
     C                     MOVE A8BICN    DCCN                                               BUG7985
     C                     MOVE UDBK      DCBC
     C                     Z-ADDTDCNT     DCNT
     C                     Z-ADDTDCNV     DCNV
     C                     Z-ADDTDCNS     DCNS
     C                     Z-ADDTDCTR     DCTR
     C                     Z-ADDTDCVR     DCVR
     C*
     C           DCNT      IFNE 0                                         095547
     C           DCNV      ORNE 0                                         095547
     C           DCNS      ORNE 0                                         095547
     C                     ADD  1         CXUSE
     C*
     C                     WRITEDCSEXDF
     C                     END                                            095547
     C                     END
     C*
     C           FUEND     ENDSR                                       SR**
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*  PAUDIT  - Sub routine to process audit details and print        *
     C*                                                                  *
     C********************************************************************
     C**
     C           PAUDIT    BEGSR
     C*
     C**  Get total records extracted
     C*
     C           CXCUS     ADD  CXDEP     CTOTL
     C                     ADD  CXUSE     CTOTL
     C*
     C**  Write audit details
     C*
     C                     WRITEHEADAU
     C                     WRITEDETLAU
     C***********          WRITETRAILAU                                   S01117
     C*
     C**  Write Depot & Client holdings extract audit record
     C*
     C                     Z-ADDCTOTL     DCNR
     C*
     C                     WRITEDCSEXAF
     C*
     C                     ENDSR
     C*
      *****************************************************************   S01117
      /EJECT                                                              S01117
      *****************************************************************   S01117
      *                                                               *   S01117
      *  *PSSR  - Error control subroutine                            *   S01117
      *                                                               *   S01117
      *****************************************************************   S01117
      *                                                                   S01117
     C           *PSSR     BEGSR                           ** *PSSR **    S01117
     C**                                                                  S01117
     C           @RUN      IFEQ *BLANKS                                   S01117
     C                     MOVE 'Y'       @RUN    1                       S01117
     C                     DUMP                                           S01117
     C                     END                                            S01117
     C**                                                                  S01117
     C                     ENDSR                                          S01117
      *                                                                   S01117
     C*****************************************************************
      /EJECT
     C*COPY*ZSRSRC*ZSYSTMZ1****                                           S01117
      /EJECT
      ***
**  CPY@
(c) Finastra International Limited 2001
