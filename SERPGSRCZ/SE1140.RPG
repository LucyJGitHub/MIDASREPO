     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Amortisation by security report')             *
/*OVRF*: OVRDBF FILE(BKAMTX) TOFILE(BKAMT) SHARE(*NO)               : *   CSE006
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module
     F*                                                               *
     F*  SE1140 - AMORTISATION BY SECURITY REPORT                     *
     F*                                                               *
     F*  Function: To print Amortised and Unamortised Discount/       *
     F*   (COB)    Premium Amounts for each Security. The sequence of *
     F*            the report is Branch, Currency, Investment Type,   *
     F*            Book and Security.                                 *
     F*                                                               *
     F*  Called by: SEC0616 - Amortisation by Security Report         *
     F*                                                               *
     F*            Frequency:                                         *
     F*            - On request during COB                            *
     F*                                                               *
      *  NOTE: Any amendments done on this program must also be made  *
      *        to SE9006.                                             *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. 248698             Date 01Jun20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 226449             Date 24Jun15               *
      *                 AR745122           Date 13May11               *
      *                 CSE105             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      *                 245495             Date 13Feb07               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 BUG12676           Date 30Nov06               *
      *                 CSD031             Date 10Apr06               *
      *                 BUG7816            Date 26Jul05               *
      *                 CSD027             Date 09Dec05               *
      *                 CSE071             Date 19Jul05               *
      *                 BUG8819            Date 08Nov05               *
      *                 CGL029             Date 01Sep03               *
      *                 194370             Date 12Jul01               *
      *                 211407             Date 07Jan03               *
      *                 180056             Date 18Oct00               *
      *                 CSE035             Date 22Apr02               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 206536             Date 24Jul02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 192955             Date 20Sep01               *
      *                 192084             Date 20Sep01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
     F*                 140272             Date 28Apr00               *
     F*                 078835             Date 20Apr00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CSE006             Date 24Jun99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAC005             Date 21Dec98               *
      *                 102506             Date 18Oct98               *
      *                 133695             Date 06Oct98               *
     F*                 091183             Date 29Jul96               *
     F*                 100017             Date 18Apr96               *
     F*                 092961             DATE 04OCT95               *
     F*                 086491             DATE 15JUN95               *
     F*                 052254             DATE 10JAN94               *
     F*                 S01401             DATE 16JUN93               *
     F*                 048918             DATE 10JAN93               *
     F*                 048414             DATE 10DEC92               *
     F*                 048310             DATE 04DEC92               *
     F*                 S01397             DATE 10SEP92               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  248698 - Rounding discrepancies on some SE reports. Applied  *
      *           fix 228682.                                         *
      *  228682 - Recompiled due to ZCNSDZ1 changes. Applied 176594.  *
      *  MD046248 - Finastra Rebranding                               *
      *  226449 - Recompiled over changes made in /COPY ZDYYRZ3.      *
      *         - Applied for MD034387.                               *
      *  AR745122 - SUBHEAD printed without detailsiand totals        *
      *             (Child: AR745123)                                 *
      *  CSE105 - Portuguese Upgrade - Discount/Premium Amortisation  *
      *           on Trade Date (Recompile)                           *
     F*  245495 - Applied 042704, MASOUD and 229448.                  *
      *  042704 - Do not display MBS with factor of 0                 *
      *  MASOUD - Disc/Prem was not updated by current factor         *
      *  229448 - Face Value was not updated by current factor        *
      *  BUG12676 - Set Amortization Rate to 'N/A' for non-linear     *
      *             Yield basis.                                      *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  BUG7816- Error encounter when writing SUBHEAD (line 473)     *
      *           in the main processing loop.  This is due to        *
      *           file SE1140P1 is not open.                          *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  BUG8819- SEC0616  MSGW, I/O operation was applied to closed  *
      *           file SE1140P1. Fix based on 235415.                 *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  194370 - Recompile after change on copy sources ZYLDZ2,      *
      *           For securities with divisor basis = '5' and first   *
      *           coupon date > valuation date (rundate + 1), set     *
      *           event control date to first coupon date before      *
      *           processing SR/ZDYYR. This is to ensure that the     *
      *           correct hypothetical last and next coupon dates are *
      *           used in deriving the price/yield rate.              *
      *  211407 - Recompile after change on copy sources ZYLDZ2,      *
      *           ZACCRZ1 and ZYCEZ1 for NX/NNX processing.           *
      *           The change was to call subroutine ZLCD1 instead     *
      *           of ZLCD in retrieving the PERIOD for NNX processing *
      *           and ZNCD1 is called instead of ZNCD in retrieving   *
      *           the WBENCD for NNX processing. Also rewrite tests   *
      *           for identifying if position is NX,NNX or CUM.       *
      *  180056 - Recompiled over changed in ZDYYRZ3.                 *
      *  CSE035 - NX/NNX Coupon Payment Processing.                   *
      *           Recompile over changed ZACCRZ1/ZYLDZ2
      *  206536 - other branches reports do not start at page 1       *
      *  192955 - I/O error in stmt 40100. Check overflow.            *
      *  192084 - Divide by zero error.                               *
     F*  140272 - Recompile over changed ZYLDZ2                       *
     F*  078835 - Amortization by Security report not produced for    *
     F*           other branches even though there are records.       *
     F*  CSE006 - Repurchase Agreements (REPOs) enhancement           *
     F*  CAC005 - PCA-SE Enhancement.                                 *
     F*  102506 - Interest not correct                                *
     F*  133695 - For change in investment type, both the currency    *
     F*           and the investment type should be checked.          *
     F*  091183 - Amortisation to date field should show disc/prem    *
     F*           amortised against latest nominal position (DPAP)    *
     F*           and NOT the sum of all disc/prem amortised (DPFP).  *
     F*  100017 - Position settlement unable to pick up day adjustment*
     F*           on SECTYD. Therefore subroutine ZDAYSZ2 changed to  *
     F*           use indicator DADJN to not apply interest rate      *
     F*           adjustment for amortisation if set to 'N'.          *
     F*  092961 - Recompile over ZDAYSZ2 which was amended by 086491  *
     F*           (Wrong number of days calculated for day basis 1    *
     F*           when start or end day is last day of February)      *
     F*  086491 - Wrong nbr of days calculated for day basis 1        *
     F*           when start or end day is last day of February       *
     F*           Recompiled for ZSRSRC/ZDAYSZ2 changes.              *
     F*  052254 - RECOMPILED as CURRENT FACTOR amended from 9,8       *
     F*           to 10,9                                             *
     F*  S01401 - MT5xx SWIFT Messages Feature:                       *
     F*          - Recompiled due to changes to files.                *
     F*  048918 - RECOMPILE FOR CHANGES MADE TO PRINTER FILE SE1140P1 *
     F*  048414 - AMORTIZATION BY SECURITY REPORT ERRORS              *
     F*  048310 - THIS PROGRAM COULD HAVE NEVER BEEN TESTED IN A REAL *
     F*           COB. SE1140AU IS UNDER UC AND SHOULD BE OPENED BEFORE
     F*           WE ATTEMPT TO CALL ZSFILE.                          *
     F*           ALSO AOBANKR0 AND AOCURRR0 HAVE WRONG PARAMETERS.   *
     F*  S01397 - Rel 10 Incorporation of BNZ SARs A46008 and A46013. *
     F*         - New report.                                         *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
      *
     FSE1140AUO   E                    PRINTER                        UC
     F                                              KINFDS SPOOLU
     FSE1140P1O   E                    PRINTER                        UC
     F                                              KINFDS SPOOL
     FINVTP   IF  E           K        DISK
     FSECTY   IF  E           K        DISK
     FBKPOS   IF  E           K        DISK
     FBKAMT   IF  E           K        DISK
     F/COPY WNCPYSRC,SE1140F001
     FBKAMTX  IF  E           K        DISK                               CSE006
     F            BKAMTF                            KRENAMEBKAMTFX        CSE006
     FSEBKAMJ1IF  E           K        DISK                               CAC005
     FBSPOSDL1IF  E           K        DISK                               CSE006
     FTRADS   IF  E           K        DISK                               CSE006
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   01  - Error on CHAIN INVTP and CLINT                        *
      *   30  - CAC005 ON                                             *   CAC005
     F*   35  - Multibranching                                        *
     F*   80  - End of file for BKAMT                                 *
     F*   81  - Record not found in INVTP                             *
     F*   82  - Record not found in BKPOS                             *
     F*   83  - Record not found in SECTY                             *
     F/COPY WNCPYSRC,SE1140F002
     F*                                                               *
     F*   98  - Date Format Flag is 'M' for MMDDYY                    *
     F* 90-99 - Used by Standard Subroutines                          *
     F*                                                               *
     F*   U8  - File Controls Out of Balance                          *
     F* U7+U8 - Database Error                                        *
     F*                                                               *
     F*****************************************************************
      /SPACE 3
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E   I N D E X                              *
     F*                                                               *
     F*  INIT   - Initial Processing.                                 *
     F*  PRINT  - Format and Print Detail Lines                       *
     F*  PRINT2 - Format and Print Detail Lines for Buy/Sell Info     *   CSE006
     F*  BSINFO - Format and Print Amortisation from Buy/Sell Trades  *   CSE006
     F*  TOTALS - Format and Print Totals Line                        *
     F*  TOTL2  - Format and Print Totals Line for accumlated P/L     *   CSE006
     F*  RCFAU  - RCF Processing for Audit Report                     *
     F*  RCFP1  - RCF Processing for P1 Report                        *
     F*  ERROR  - Output DB Error information to Audit Report         *
     F*  *PSSR  - Error handling                                      *
     F*                                                               *
     F*  ZCNSD  - Calculates Consideration from Nominal and Price     *
     F*  ZDATE2 - Convert a Day Number to Date Formats                *
     F*  ZDAYS  - Calculates Number of days between two Dates         *
     F*  ZSEDIT - Convert numeric field into alphanumeric with commas,*
     F*           sign and decimal point                              *
     F*                                                               *
     F*****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
     E/COPY ZSRSRC,ZDAYSZ1
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZSEDITZ1
     E/COPY ZSRSRC,ZPOWER8Z1
     E/COPY WNCPYSRC,SE1140E001
     I/EJECT
      *
     I            DS
      ***  Definition of alphanumeric and signed branch fields.
     I                                        1   3 BHBA
     I                                        1   3 BRANCH
      *
     ILDA         DS                            256
      *** Local Data Area for database errors.
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *
     IRUNDT       DS
      ***  RUNDAT Data Area.
     I                                        1   7 RUNA
     I                                    P   8  100RUND
     I                                       11  11 SSF
     I                                       12  12 DATF
     I                                       13  13 MBIN
      *
     ISPOOL       DS
      ***  DS for Line Number to handle overflow.
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
      *
     ISPOOLU      DS
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     ISDBANK    E DSSDBANKPD
      ** External data structures for Bank Details
      *
     ISDBRCH    E DSSDBRCHPD
      ** External data structures for Branch Details
      *
     ISDCURR    E DSSDCURRPD
      ** External data structures for Currency Details
     I/COPY WNCPYSRC,SE1140I001
      *                                                                   CSE006
     ISCSARD    E DSSCSARDPD                                              CSE006
      ** External data structures for Switchable Features File            CSE006
      *
     IDSFDY     E DSDSFDY
      ** Short data structure for access objects
      *
     IDSSDY     E DSDSSDY
      ** Long date structure for access objects
      *
     I/COPY ZSRSRC,ZSEDITZ2
     C/EJECT
      *================================================================
      *  P R O G R A M     S T A R T                                  *
      *================================================================
      *
      ***  Initial Processing.
      *
     C                     EXSR INIT
      *
      ***  Main Processing.
      *
      ***  Read first record from Book Position and Book file.
      *
     C           CAC005    IFEQ 'Y'                                       CAC005
     C                     READ SEBKAMJ1                 80               CAC005
     C                     ELSE                                           CAC005
     C                     READ BKAMT                    80
     C                     ENDIF                                          CAC005
      *
      ***  Check for requested branch for Multibranching.
      *
     C***********MBIN      IFEQ 'Y'                                       078835
     C***********ENT       IFNE 'ALL'                                     078835
     C***********ENT       ANDNEBHBA                                      078835
     C***********          SETON                     80                   078835
     C***********          GOTO ENDSEL                                    078835
     C***********          END                                            078835
     C***********          END                                            078835
      *
      ***  Whilst End of File has not been reached.
      *
     C           *IN80     DOWNE'1'
     C                     ADD  1         COUNT                           CSE006
      *                                                                   CSE006
      ***  If Repos enhancement is present then EXSR BSINFO               CSE006
      *                                                                   CSE006
     C           CSE006    IFEQ 'Y'                                       CSE006
     C*******    COUNT     IFEQ 1                                         CSE006             BUG8819
     C*******    BYSL      ANDEQ'Y'                                       CSE006             BUG8819
     C*******              MOVELBHBA      SVBHBR                          CSE006             BUG8819
     C*******              ENDIF                                          CSE006             BUG8819
     C           BHBA      IFNE SVBHBR                                    CSE006
     C                     EXSR BSINFO                                    CSE006
     C                     ENDIF                                          CSE006
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
     C           BYSL      IFNE 'Y'                                       CSE006
      *
      ***  Change in Keys.
      *
     C           ENT       IFEQ BHBA                                      078835
     C           ENT       OREQ 'ALL'                                     078835
     C*                                                                   078835
      ** Control SUBHEAD printing                                                           AR745122
      *                                                                                     AR745122
     C                     EXSR SRSUBC                                                      AR745122
      *                                                                                     AR745122
     C           PSUBH     IFEQ 'Y'                                                         AR745122
      *                                                                                     AR745122
     C           BHBA      IFNE SVBHBR
     C           NCRY      ORNE SVNCRY
     C           INVT      ORNE SVINVT
      *
      ***  Produce Investment Type Totals.
      *
     C           RCDDET    IFEQ 'Y'
     C                     EXSR TOTALS
     C                     MOVE 'N'       RCDDET
     C                     END
      *
      ***  Initialise Totals fields.
      *
     C                     CLEARTTNOM
     C                     CLEARTTDSC
     C                     CLEARTTDPFP
     C                     CLEARTTBUDU
     C/COPY WNCPYSRC,SE1140C011
      *
      ***  Change of Branch.
      *
     C           BHBA      IFNE SVBHBR
      *
      ***  Access Branch file for Branch Name.
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   RTCD
     C                     PARM '*KEY   ' OPTN
     C                     PARM BRANCH    BRCD    3
     C***********          PARM           SDBRCH                          048414
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
      *
      ***  Database Error if not found.
      *
     C           RTCD      IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'BRCH'    DBFILE           ***************
     C                     MOVELBRANCH    DBKEY            * DB ERROR 02 *
     C                     Z-ADD002       DBASE            ***************
     C                     OUT  LDA
     C                     SETON                     U7U8LR
     C                     EXSR ERROR
     C                     END
      *
      ***  Save Branch field.
      *
     C                     MOVELBHBA      SVBHBR
     C                     MOVELA8BRNM    RBRNM
      *
      ***  Headings are required.
      *
     C                     MOVE 'Y'       HEAD
     C                     END
      *                                                                   133695
      ***  Change of Investment Type.                                     133695
      ***  (Change of Investment Type or Currency)                        133695
      *                                                                   133695
     C           INVT      IFNE SVINVT                                    133695
     C           NCRY      ORNE SVNCRY                                    133695
      *                                                                   133695
      ***  Access Investment Types file for Investment Name.              133695
      *                                                                   133695
     C           KLINV     CHAININVTP                81                   133695
      *                                                                   133695
      ***  Database Error if not found.                                   133695
      *                                                                   133695
     C           *IN81     IFNE '0'                                       133695
     C           *LOCK     IN   LDA                                       133695
     C                     MOVEL'INVTP'   DBFILE           ***************133695
     C                     MOVELNCRY      DBKEY            * DB ERROR 04 *133695
     C                     MOVE INVT      DBKEY            ***************133695
     C                     Z-ADD004       DBASE                           133695
     C                     OUT  LDA                                       133695
     C                     SETON                     U7U8LR               133695
     C                     EXSR ERROR                                     133695
     C                     END                                            133695
      *                                                                   133695
      ***  Save Investment Type field.                                    133695
      *                                                                   133695
     C                     MOVE INVT      SVINVT                          133695
     C                     END                                            133695
      *
      ***  Change of Currency.
      *
     C           NCRY      IFNE SVNCRY
      *
      ***  Access Currency file for Currency Name.
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   RTCD
     C                     PARM '*KEY   ' OPTN
     C                     PARM NCRY      CCY     3
     C***********          PARM           SDCURR                          048310
     C           SDCURR    PARM SDCURR    DSSDY
      *
      ***  Database Error if not found.
      *
     C           RTCD      IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'CURR'    DBFILE           ***************
     C                     MOVELNCRY      DBKEY            * DB ERROR 03 *
     C                     Z-ADD003       DBASE            ***************
     C                     OUT  LDA
     C                     SETON                     U7U8LR
     C                     EXSR ERROR
     C                     END
      *
      ***  Save Currency field.
      *
     C                     MOVE NCRY      SVNCRY
     C                     MOVELA6CYNM    RCYNM
     C                     END
      *
      *****Change*of*Investment*Type.                                     133695
      ***********                                                         133695
     C***********INVT      IFNE SVINVT                                    133695
      ***********                                                         133695
      *****Access*Investment*Types*file*for*Investment*Name.              133695
      ***********                                                         133695
     C***********KLINV     CHAININVTP                81                   133695
      ***********                                                         133695
      *****Database*Error*if*not*found.                                   133695
      ***********                                                         133695
     C************IN81     IFNE '0'                                       133695
     C************LOCK     IN   LDA                                       133695
     C***********          MOVEL'INVTP'   DBFILE           ************** 133695
     C***********          MOVELNCRY      DBKEY            * DB ERROR 04  133695
     C***********          MOVE INVT      DBKEY            ************** 133695
     C***********          Z-ADD004       DBASE                           133695
     C***********          OUT  LDA                                       133695
     C***********          SETON                     U7U8LR               133695
     C***********          EXSR ERROR                                     133695
     C***********          END                                            133695
      ***********                                                         133695
      *****Save*Investment*Type*field.                                    133695
      ***********                                                         133695
     C***********          MOVE INVT      SVINVT                          133695
     C***********          END                                            133695
      *
      ***  Only process Interest Bearing Securities.
      *
     C           PROT      IFEQ '1'
     C           PROT      OREQ '3'
     C           PROT      OREQ '5'
     C           PROT      OREQ '6'
     C           PROT      OREQ '8'
     C           PROT      OREQ '9'
      *
      ***  Print Headings if required.
      *
     C           HEAD      IFEQ 'Y'
      *
      ***  If not first time through print trailer and close print file
      *
     C           RPTDET    IFEQ 'Y'
     C           @P1SW     ANDEQ'Y'                                                          BUG7816
      *                                                                                       192955
     C           LINCNT    IFGE 52                                                            192955
     C                     WRITEHEADP1                                                        192955
     C                     WRITESUBHEAD                                                       192955
     C                     Z-ADD13        LINCNT                                              192955
     C                     END                                                                192955
      *                                                                                       192955
     C                     WRITETRAILP1
     C                     CLOSESE1140P1
     C                     MOVE 'N'       @P1SW                                              BUG7816
     C/COPY WNCPYSRC,SE1140C012
     C                     ELSE
     C                     MOVE 'Y'       RPTDET
     C                     END
      *
     C                     Z-ADD0         PAGE                                                206536
     C                     OPEN SE1140P1
     C                     MOVE 'Y'       @P1SW                                              BUG7816
     C                     EXSR RCFP1
     C                     WRITEHEADP1
     C                     Z-ADD7         LINCNT  20
     C                     MOVE 'N'       HEAD
     C                     END
      *
      ***  Check for overflow.
      *
     C           LINCNT    IFGE 52
     C                     WRITEHEADP1
     C                     Z-ADD7         LINCNT
     C                     END
      *
      ***  Print Subheadings.
     C/COPY WNCPYSRC,SE1140C013
      *
     C           @P1SW     IFEQ 'Y'                                                          BUG7816
     C                     WRITESUBHEAD
     C/COPY WNCPYSRC,SE1140C014
     C                     ADD  6         LINCNT
     C                     ENDIF                                                             BUG7816
      *
      ***  END of Interest Bearing Securities.
     C                     END
      *
      ***  END of Change of Keys.
     C                     END
      *---------------------------------------------------------------*
      *                                                                   CSE006
      ***  Only process Interest Bearing Securities.
      *
     C           PROT      IFEQ '1'
     C           PROT      OREQ '3'
     C           PROT      OREQ '5'
     C           PROT      OREQ '6'
     C           PROT      OREQ '8'
     C           PROT      OREQ '9'
      *
      ***  Change of Security.
      *
     C           BHSC      IFNE SVBHSC
      *
      ***  Access Security file for various fields needed by Standard
      ***  Subroutines.
      *
     C           BHSC      CHAINSECTY                83
      *
      ***  Database Error if not found.
      *
     C           *IN83     IFNE '0'
     C           *LOCK     IN   LDA
     C                     MOVEL'SECTY'   DBFILE           ***************
     C                     MOVELBHSC      DBKEY            * DB ERROR 05 *
     C                     Z-ADD005       DBASE            ***************
     C                     OUT  LDA
     C                     SETON                     U7U8LR
     C                     EXSR ERROR
     C                     END
      *
      ** Save Security field.
      *
     C                     MOVE BHSC      SVBHSC
     C                     END
     C/COPY WNCPYSRC,SE1140C038
      *
      ***  Access Book Position file.
      *
     C           KLBKP     CHAINBKPOS                82
     C*                                                                                       042704
     C* SET ON *IN84 IF MORTGAGED BACKED AND CURRENT FACTOR = 0                               042704
     C* OR IF SECURITY HAS MATURED                                                            042704
     C*                                                                                       042704
     C                     SETOF                     84                                       042704
     C*                                                                                       042704
     C           PROT      IFEQ '8'                                                           042704
     C           CFCT      ANDEQ0                                                             042704
     C           RECI      ORNE 'D'                                                           042704
     C                     SETON                     84                                       042704
     C                     END                                                                042704
      *
      ***  Note if a record has been found.
      *
     C           *IN82     IFEQ '0'
     C           *IN84     ANDNE'1'                                                           042704
     C           NPSN      ANDNE0                                                             042704
     C           BUDU      ANDNE0                                                             042704
     C/COPY WNCPYSRC,SE1140C015
     C                     MOVE 'Y'       RCDDET
     C                     MOVE 'Y'       RPTDET
     C                     EXSR PRINT
      *
      ***  Accumulate Investment Type Totals.
      *
      ***  Face Value.
     C/COPY WNCPYSRC,SE1140C016
     C                     ADD  NOMAMT    TTNOM
     C/COPY WNCPYSRC,SE1140C017
      ***  Discount/Premium Amount.
     C                     ADD  DSCAMT    TTDSC
      ***  Amount Amortised to Date.
     C***********          ADD  DPFP      TTDPFP                          091183
     C                     ADD  DPAP      TTDPFP                          091183
      ***  Amount to Amortise.
     C                     ADD  BUDU      TTBUDU
     C/COPY WNCPYSRC,SE1140C018
     C                     END
      *
      ***  END of Interest Bearing Securities.
     C                     END
      *                                                                                     AR745122
     C                     ENDIF                                                            AR745122
      *
     C*                                                                   078835
     C                     END                                            078835
      *** End of BYSL condition                                           CSE006
      *                                                                   CSE006
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
      ***  Read next record.
      *
     C           CAC005    IFEQ 'Y'                                       CAC005
     C                     READ SEBKAMJ1                 80               CAC005
     C                     ELSE                                           CAC005
     C                     READ BKAMT                    80
     C                     ENDIF                                          CAC005
      *
      ***  Check for requested branch for Multibranching.
      *
     C***********MBIN      IFEQ 'Y'                                       078835
     C***********ENT       IFNE 'ALL'                                     078835
     C***********ENT       ANDNEBHBA                                      078835
     C***********          SETON                     80                   078835
     C***********          GOTO ENDSEL                                    078835
     C***********          END                                            078835
     C***********          END                                            078835
      *                                                                   CSE006
      ***  END of Do While NOT End of File.
     C                     END
      *
     C           ENDSEL    TAG
      *                                                                   CSE006
      *** Only process totals if not Buy-Sell Book                        CSE006
      *                                                                   CSE006
     C           BYSL      IFNE 'Y'                                       CSE006
      *                                                                   CSE006
      *
      ***  If no Investment Type totals produced for last Investment
      ***  Type and there are no Database Errors then produce totals.
      *
     C           *INU7     IFNE '1'
     C           *INU8     ANDNE'1'
     C           RCDDET    ANDEQ'Y'
     C                     EXSR TOTALS
     C                     END
      *                                                                   CSE006
     C                     ENDIF                                          CSE006
      *
      ***  If details have been written to detail printer file and
      ***  there are no Database Errors then print trailer.
      *
     C           *INU7     IFNE '1'
     C           *INU8     ANDNE'1'
     C           RPTDET    ANDEQ'Y'
      *
      ***  Check for overflow.
      *
     C           LINCNT    IFGE 55
     C                     WRITEHEADP1
     C/COPY WNCPYSRC,SE1140C019
     C                     WRITESUBHEAD
     C/COPY WNCPYSRC,SE1140C020
     C                     Z-ADD13        LINCNT
     C                     END
      *
     C                     WRITETRAILP1
     C                     ADD  3         LINCNT
     C                     END
      *
      ***  End of Program.
      *
     C           ENDPGM    TAG
     C/COPY WNCPYSRC,SE1140C021
     C                     SETON                     LR
      *
      ***  If no details have been written to detail printer file then
      ***  open audit printer file and say so there.
      *
     C           RPTDET    IFNE 'Y'
     C                     OPEN SE1140AU
     C                     EXSR RCFAU                                     048310
     C                     Z-ADD0         PAGE
     C                     WRITEHEADAU
     C                     WRITENONE
     C                     CLOSESE1140AU                                  048310
     C                     END
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  INIT   - Subroutine to do Program Setup.                     *
      *                                                               *
      *  Called By: Main Processing.                                  *
      ***Calls****:*RCFAU                                             *   048310
      *                                                               *
      *****************************************************************
      *
     C           INIT      BEGSR                           ***  INIT  ***
      *
      *** Switch on initialisation repos switch                           CSE006
      *                                                                   CSE006
     C                     MOVE *ON       *IN40                           CSE006
     C                     Z-ADD0         COUNT   30                      CSE006
      *                                                                                      BUG7816
      *** SE1140P1 open/close status switch (N as CLOSED; Y as OPEN)                         BUG7816
      *                                                                                      BUG7816
     C                     MOVE 'N'       @P1SW   1                                          BUG7816
      *                                                                   CSE006
      ***  Read in RUNDAT Data Area.
      *
     C           *NAMVAR   DEFN RUNDAT    RUNDT
     C                     IN   RUNDT
      *
      ***  Check system date format DDMMMYY or MMDDYY.
      *
     C           DATF      IFEQ 'M'
     C                     SETON                     98
     C                     END
      *
      ***  Read and set up Local Data Area.
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL'SE1140'  DBPGM
     C                     OUT  LDA
      *
      ***  Set Indicator for Multibranching.
      *
     C           MBIN      IFEQ 'Y'
     C                     SETON                     35
     C                     END
      *
      ***  Register Spool Entry for AU.
      *
     C**********           EXSR RCFAU                                     048310
      *
      ***  Set up Copyright.
      *
     C                     MOVEACPY@      BIS@   80
      *
      ***  Input Parameter:
      *
     C           *ENTRY    PLIST
     C                     PARM           SEQ     5
     C                     PARM           ENT     3
      *
      ***  Access Bank details for Report Title.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   RTCD    7
     C                     PARM '*FIRST ' OPTN    7
     C**********           PARM *BLANKS   KEY    10                       048310
     C**********           PARM           SDBANK                          048310
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ***  Database Error if not found.
      *
     C           RTCD      IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'BANK'    DBFILE           ***************
     C                     MOVEL'*FIRST'  DBKEY            * DB ERROR 01 *
     C                     Z-ADD001       DBASE            ***************
     C                     OUT  LDA
     C                     SETON                     U7U8LR
     C                     EXSR ERROR
     C                     END
      *                                                                                       CSE035
      ** Access SAR details file to see if CSE035 is installed                                CSE035
      *                                                                                       CSE035
     C                     CALL 'AOSARDR0'                                                    CSE035
     C                     PARM *BLANKS   @RTCD   7                                           CSE035
     C                     PARM '*VERIFY' @OPTN   7                                           CSE035
     C                     PARM 'CSE035'  @SARD   6                                           CSE035
      *                                                                                       CSE035
     C           @RTCD     IFEQ *BLANK                                                        CSE035
     C                     MOVE 'Y'       CSE035  1                                           CSE035
     C                     ELSE                                                               CSE035
     C                     MOVE 'N'       CSE035                                              CSE035
     C                     ENDIF                                                              CSE035
     C/COPY WNCPYSRC,SE1140C001
      *
      ***  Key list for INVTP.
      *
     C           KLINV     KLIST
     C                     KFLD           NCRY
     C                     KFLD           INVT
      *
      ***  Key list for BKPOS.
      *
     C           KLBKP     KLIST
     C                     KFLD           BHSC
     C                     KFLD           BHBA
     C                     KFLD           BHBK
     C                     KFLD           BHMK
     C                     KFLD           BHTV
     C                     KFLD           LPSD
     C/COPY WNCPYSRC,SE1140C022
      *
      ***  Define program variables.
      *
     C           *LIKE     DEFN NCRY      SVNCRY
     C           *LIKE     DEFN INVT      SVINVT
     C           *LIKE     DEFN BHBA      SVBHBR
     C           *LIKE     DEFN NPSN      TTNOM
     C           *LIKE     DEFN APNC      TTDSC
     C************LIKE     DEFN DPFP      TTDPFP                          091183
     C           *LIKE     DEFN DPAP      TTDPFP                          091183
     C           *LIKE     DEFN BUDU      TTBUDU
     C/COPY WNCPYSRC,SE1140C023
     C           *LIKE     DEFN BHSC      SVBHSC
     C           *LIKE     DEFN APNC      NOMAMT
     C           *LIKE     DEFN APNC      NOMAM1                                              229448
     C           *LIKE     DEFN APNC      DSCAMT
     C           *LIKE     DEFN LPSD      DAYS
     C           *LIKE     DEFN APNC      RATE
     C           *LIKE     DEFN A6NBDP    CDP
     C           *LIKE     DEFN BSPL      TTBSPL                          CSE006
     C           *LIKE     DEFN BSAPL     TTBSAP                          CSE006
     C           *LIKE     DEFN BSOPL     TTBSOP                          CSE006
      *
      ***  Initialise Saved fields.
      *
     C                     CLEARSVBHBR
     C                     CLEARSVNCRY
     C                     CLEARSVINVT
     C                     CLEARSVBHSC
      *
      ***  Set up Parameters that condition the First Time processing.
      *
     C                     MOVE 'N'       RCDDET  1
     C                     MOVE 'N'       RPTDET  1
     C                     MOVE 'N'       HEAD    1
      *                                                                   CAC005
      ** Check if CAC005 - PCA-SE Enhancement is on.                      CAC005
      *                                                                   CAC005
     C                     MOVE 'N'       CAC005  1                       CAC005
     C                     MOVE *OFF      *IN30                           CAC005
     C                     CALL 'AOSARDR0'                                CAC005
     C                     PARM *BLANKS   @RTCD   7                       CAC005
     C                     PARM '*VERIFY' @OPTN   7                       CAC005
     C                     PARM 'CAC005'  @SARD   6                       CAC005
      *                                                                   CAC005
     C           @RTCD     IFEQ *BLANK                                    CAC005
     C                     MOVE 'Y'       CAC005                          CAC005
     C                     MOVE *ON       *IN30                           CAC005
     C                     ENDIF                                          CAC005
      *                                                                   CSE006
      ** Access SAR details file to determine if CSE006                   CSE006
      ** (REPOS enhancement switchable feature) is on.                    CSE006
      *                                                                   CSE006
     C                     CALL 'AOSARDR0'                                CSE006
     C                     PARM *BLANKS   @RTCD                           CSE006
     C                     PARM '*VERIFY' @OPTN                           CSE006
     C                     PARM 'CSE006'  @SARD   6                       CSE006
     C           SCSARD    PARM SCSARD    DSFDY                           CSE006
      *                                                                   CSE006
      * DATABASE ERROR                                                    CSE006
      *                                                                   CSE006
     C           @RTCD     IFNE *BLANKS                                   CSE006
     C           @RTCD     ANDNE'*NRF'                                    CSE006
     C           *LOCK     IN   LDA                                       CSE006
     C                     MOVEL'SCSARDPD'DBFILE           ***************CSE006
     C                     MOVEL'*FIRST'  DBKEY            * DB ERROR 05 *CSE006
     C                     Z-ADD005       DBASE            ***************CSE006
     C                     OUT  LDA                                       CSE006
     C                     SETON                     U7U8LR               CSE006
     C                     EXSR ERROR                                     CSE006
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
     C           @RTCD     IFEQ *BLANK                                    CSE006
     C                     MOVEL'Y'       CSE006  1                       CSE006
     C                     ELSE                                           CSE006
     C                     MOVEL'N'       CSE006                          CSE006
     C                     ENDIF                                          CSE006
     C/COPY WNCPYSRC,SE1140C039
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  PRINT  - Subroutine to Format/Print detail records.          *
      *                                                               *
      *  Called By: Main Processing.                                  *
      *  Calls    : ZCNSD                                             *
      *             ZSEDIT                                            *
      *             ZDAYS                                             *
      *                                                               *
      *****************************************************************
      *
     C           PRINT     BEGSR                           *** PRINT ***
      *
      ***  Book.
     C                     MOVE BHBK      RBHBK
      *                                                                   CAC005
      ** CAC005 Installed.                                                CAC005
      *                                                                   CAC005
     C           CAC005    IFEQ 'Y'                                       CAC005
     C                     MOVE PSBKCD    RBHBK                           CAC005
     C                     MOVE PSPRFC    PPRFC                           CAC005
     C                     ENDIF                                          CAC005
      *                                                                   CAC005
      ***  Security.
     C                     MOVE BHSC      RBHSC
      *
      ***  Market.
     C           BHMK      IFEQ 'S'
     C                     MOVEL'SEC '    RBHMK
     C                     ELSE
     C           BHMK      IFEQ 'P'
     C                     MOVEL'PRIM'    RBHMK
     C                     ELSE
     C                     MOVEL'GREY'    RBHMK
     C                     END
     C                     END
      *
      ***  Face Value.
      *
      ***  Convert Nominal Position to an Amount.
      *
     C/COPY WNCPYSRC,SE1140C002
     C                     Z-ADDNPSN      ZNOML
     C                     Z-ADD100       ZPRC
     C/COPY WNCPYSRC,SE1140C037
     C                     Z-ADDA6NBDP    CDP
     C                     Z-ADDA6NBDP    ZCDP                            048414
     C                     EXSR ZCNSD
     C                     Z-ADDZCONS     NOMAMT
      *                                                                                       229448
      *  APPLY THE FACTOR TO THE NOMINAL AMOUNT                                               229448
      *                                                                                       229448
     C                     Z-ADDNOMAMT    NOMAM1                                              229448
     C           PROT      IFEQ '8'                                                           229448
     C           CFCT      ANDNE0                                                             229448
     C                     MULT CFCT      NOMAMT                                              229448
     C                     ENDIF                                                              229448
      *
      ***  Format Amount field.
      *
     C                     Z-ADDNOMAMT    ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C/COPY WNCPYSRC,SE1140C003
     C                     MOVE ZOUT21    RNOM
      *
      ***  Discount/Premium Amount.
      *
      ***  First calculate Amount.
      *
     C/COPY WNCPYSRC,SE1140C004
     C***********NOMAMT    SUB  APNC      DSCAMT                                              229448
     C           NOMAM1    SUB  APNC      DSCAMT                                              229448
     C/COPY WNCPYSRC,SE1140C005
      *                                                                                       MASOUD
      *  APPLY THE FACTOR TO THE DISC/PREM AMOUNT                                             MASOUD
      *                                                                                       MASOUD
     C           PROT      IFEQ '8'                                                           MASOUD
     C           CFCT      ANDNE0                                                             MASOUD
     C                     MULT CFCT      DSCAMT                                              MASOUD
     C                     ENDIF                                                              MASOUD
      *
      ***  Format Amount field.
      *
     C                     Z-ADDDSCAMT    ZFLD15
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RDSC
      *
      ***  Amount Amortised to Date.
      *
     C***********          Z-ADDDPFP      ZFLD15                          091183
     C/COPY WNCPYSRC,SE1140C035
      *                                                                                       MASOUD
      *  APPLY THE FACTOR TO THE AMOUNT AMORTIZED TO DATE                                     MASOUD
      *                                                                                       MASOUD
     C           PROT      IFEQ '8'                                                           MASOUD
     C           CFCT      ANDNE0                                                             MASOUD
     C                     MULT CFCT      DPAP                                                MASOUD
     C                     ENDIF                                                              MASOUD
     C                     Z-ADDDPAP      ZFLD15                          091183
     C/COPY WNCPYSRC,SE1140C036
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RDPFP
      *
      ***  Amount to Amortise.
      *
     C/COPY WNCPYSRC,SE1140C006
      *                                                                                       MASOUD
      *  APPLY THE FACTOR TO THE AMOUNT TO AOMORTIZE                                          MASOUD
      *                                                                                       MASOUD
     C           PROT      IFEQ '8'                                                           MASOUD
     C           CFCT      ANDNE0                                                             MASOUD
     C                     MULT CFCT      BUDU                                                MASOUD
     C                     ENDIF                                                              MASOUD
     C                     Z-ADDBUDU      ZFLD15
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RBUDU
     C/COPY WNCPYSRC,SE1140C007
      *
      ***  Amortisation Rate.
      *
     C           STAD      IFEQ 'Y'                                                         BUG12676
     C           STAD      OREQ 'N'                                                         BUG12676
     C           STAD      OREQ 'C'                                                         BUG12676
     C           SYBS      ANDEQ*BLANKS                                                     BUG12676
      ***  Calculate number of days between last position date and
      ***  Maturity Date.
      *
     C                     Z-ADDLPSD      ZSDATE
     C                     Z-ADDMATY      ZEDATE
     C                     MOVE 'N'       DADJN                           100017
     C                     Z-ADD0         NCD     50                      102506
     C                     EXSR ZDAYS
     C                     MOVE ' '       DADJN                           100017
     C                     Z-ADDZDDAYS    DAYS
      *
      ***  Calculate Rate.
      *
     C/COPY WNCPYSRC,SE1140C008
     C           DAYS      IFEQ 0                                                             192084
     C                     Z-ADDDPAP      RATE      H                                         192084
     C                     ELSE                                                               192084
     C           DSCAMT    DIV  DAYS      RATE      H
     C                     END                                                                192084
     C/COPY WNCPYSRC,SE1140C009
      *
      ***  Format Rate.
      *
     C                     MOVE RATE      ZFLD15
     C                     MOVE 'L'       ZECODE
     C                     EXSR ZSEDIT
     C/COPY WNCPYSRC,SE1140C024
     C                     MOVE ZOUT21    RRATE
     C                     ELSE                                                             BUG12676
     C                     MOVEL*BLANKS   RRATE                                             BUG12676
     C                     MOVE 'N/A'     RRATE                                             BUG12676
     C                     ENDIF                                                            BUG12676
     C/COPY WNCPYSRC,SE1140C025
      *
      ***  Check for overflow.
      *
     C           LINCNT    IFGE 57
     C                     WRITEHEADP1
     C/COPY WNCPYSRC,SE1140C026
     C                     WRITESUBHEAD
     C/COPY WNCPYSRC,SE1140C027
     C                     Z-ADD13        LINCNT
     C                     END
      *
      ***  Print Detail.
     C/COPY WNCPYSRC,SE1140C028
      *
     C           @P1SW     IFEQ 'Y'                                                          BUG7816
     C                     WRITEDETAIL
     C/COPY WNCPYSRC,SE1140C029
     C                     ADD  1         LINCNT
      *                                                                   CAC005
     C           CAC005    IFEQ 'Y'                                       CAC005
     C                     ADD  1         LINCNT                          CAC005
     C                     ENDIF                                          CAC005
     C                     ENDIF                                                             BUG7816
      *
     C                     ENDSR
      *
     C/COPY WNCPYSRC,SE1140C010
      *****************************************************************   CSE006
      /EJECT                                                              CSE006
      *****************************************************************   CSE006
      *                                                               *   CSE006
      *  PRINT2 - Subroutine to Format/Print detail records for Buy/  *   CSE006
      *           Sell Information.                                   *   CSE006
      *                                                               *   CSE006
      *  Called By: Main Processing.                                  *   CSE006
      *  Calls    : ZCNSD                                             *   CSE006
      *             ZSEDIT                                            *   CSE006
      *             ZDAYS                                             *   CSE006
      *                                                               *   CSE006
      *****************************************************************   CSE006
      *                                                                   CSE006
     C           PRINT2    BEGSR                           *** PRINT2 *** CSE006
      *                                                                   CSE006
      ***  Book.                                                          CSE006
      *                                                                   CSE006
     C                     MOVE BHBK      RBSBK                           CSE006
      *                                                                   CSE006
      ** CAC005 Installed.                                                CSE006
      *                                                                   CSE006
     C           CAC005    IFEQ 'Y'                                       CSE006
     C                     MOVE PSBKCD    RBSBK                           CSE006
     C                     MOVE PSPRFC    PPRFC                           CSE006
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
      ***  Security.                                                      CSE006
      *                                                                   CSE006
     C                     MOVE BHSC      RBHSC                           CSE006
      *                                                                   CSE006
      ***  Trade Ref.                                                     CSE006
     C                     MOVE TDRF      RBSPRF                          CSE006
      *                                                                   CSE006
      ***  Trade Nominal                                                  CSE006
      *                                                                   CSE006
      ***  Convert Nominal Position to an Amount.                         CSE006
      *                                                                   CSE006
     C/COPY WNCPYSRC,SE1140C002                                           CSE006
     C                     Z-ADDNOML      ZNOML                           CSE006
     C                     Z-ADD100       ZPRC                            CSE006
     C                     Z-ADDA6NBDP    CDP                             CSE006
     C                     Z-ADDA6NBDP    ZCDP                            CSE006
     C                     EXSR ZCNSD                                     CSE006
     C                     Z-ADDZCONS     NOMAMT                          CSE006
      *                                                                   CSE006
      ***  Format Amount field.                                           CSE006
      *                                                                   CSE006
     C                     Z-ADDNOMAMT    ZFLD15                          CSE006
     C                     Z-ADDA6NBDP    ZDECS                           CSE006
     C                     MOVE 'J'       ZECODE                          CSE006
     C                     EXSR ZSEDIT                                    CSE006
      *                                                                   CSE006
     C/COPY WNCPYSRC,SE1140C003                                           CSE006
      *                                                                   CSE006
     C                     MOVE ZOUT21    RNOM                            CSE006
      *                                                                   CSE006
      ***  Profit/Loss                                                    CSE006
      *                                                                   CSE006
      ***  Format Amount field.                                           CSE006
      *                                                                   CSE006
     C                     Z-ADDBSPL      ZFLD15                          CSE006
     C                     EXSR ZSEDIT                                    CSE006
     C                     MOVE ZOUT21    RBSPL                           CSE006
      *                                                                   CSE006
      ***  Amortised Profit/Loss                                          CSE006
      *                                                                   CSE006
     C/COPY WNCPYSRC,SE1140C005                                           CSE006
      *                                                                   CSE006
      ***  Format Amount field.                                           CSE006
      *                                                                   CSE006
     C                     Z-ADDBSAPL     ZFLD15                          CSE006
     C                     EXSR ZSEDIT                                    CSE006
     C                     MOVE ZOUT21    RBSAPL                          CSE006
      *                                                                   CSE006
      ***  Unamortised Profit/Loss                                        CSE006
      *                                                                   CSE006
     C/COPY WNCPYSRC,SE1140C005                                           CSE006
      *                                                                   CSE006
      ***  Format field.                                                  CSE006
      *                                                                   CSE006
     C                     Z-ADDBSOPL     ZFLD15                          CSE006
     C                     EXSR ZSEDIT                                    CSE006
     C                     MOVE ZOUT21    RBSOPL                          CSE006
      *                                                                   CSE006
      ***  Amortisation Rate.                                             CSE006
      *                                                                   CSE006
      ***  Calculate number of days between Value Date on the first       CSE006
      ***  and second trade.                                              CSE006
      *                                                                   CSE006
     C           DYBS      IFNE *BLANK                                    CSE006
     C                     Z-ADDBSVD1     ZSDATE                          CSE006
     C                     Z-ADDBSVD2     ZEDATE                          CSE006
     C                     MOVE 'N'       DADJN                           CSE006
     C                     Z-ADD0         NCD     50                      CSE006
     C                     EXSR ZDAYS                                     CSE006
     C                     MOVE ' '       DADJN                           CSE006
     C                     Z-ADDZDDAYS    DAYS                            CSE006
     C                     ELSE                                           CSE006
     C                     Z-ADD0         TDAYS   50                      CSE006
     C           BSVD2     SUB  BSVD1     TDAYS   50                      CSE006
     C                     Z-ADDTDAYS     DAYS                            CSE006
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
      ***  Calculate Amortisation Rate.                                   CSE006
      *                                                                   CSE006
     C/COPY WNCPYSRC,SE1140C008                                           CSE006
     C           DAYS      IFNE 0                                         CSE006
     C           BSPL      DIV  DAYS      RATE      H                     CSE006
     C                     ELSE                                           CSE006
     C                     Z-ADD0         RATE                            CSE006
     C                     ENDIF                                          CSE006
     C/COPY WNCPYSRC,SE1140C009                                           CSE006
      *                                                                   CSE006
      ***  Format Rate.                                                   CSE006
      *                                                                   CSE006
     C                     MOVE RATE      ZFLD15                          CSE006
     C                     MOVE 'L'       ZECODE                          CSE006
     C                     EXSR ZSEDIT                                    CSE006
     C                     MOVE ZOUT21    RBSRAT                          CSE006
      *                                                                   CSE006
      ***  Check for overflow.                                            CSE006
      *                                                                   CSE006
     C           LINCNT    IFGE 57                                        CSE006
     C                     WRITEHEADP1                                    CSE006
     C                     WRITESUBH2                                     CSE006
     C                     Z-ADD13        LINCNT                          CSE006
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
      ***  Print Detail.                                                  CSE006
      *                                                                   CSE006
     C                     WRITEDETAIL2                                   CSE006
     C                     ADD  1         LINCNT                          CSE006
      *                                                                   CSE006
     C           CAC005    IFEQ 'Y'                                       CSE006
     C                     ADD  1         LINCNT                          CSE006
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
     C                     ADD  BSPL      TTBSPL                          CSE006
     C                     ADD  BSAPL     TTBSAP                          CSE006
     C                     ADD  BSOPL     TTBSOP                          CSE006
      *                                                                   CSE006
     C                     ENDSR                                          CSE006
      *                                                                   CSE006
     C/COPY WNCPYSRC,SE1140C010                                           CSE006
      ****************************************************************    CSE006
      /EJECT                                                              CSE006
      *****************************************************************   CSE006
      *                                                               *   CSE006
      *  BSINFO - Subroutine to Format/Print amortisation from Buy/Sell   CSE006
      *         - Trades                                              *   CSE006
      *                                                               *   CSE006
      *  Called By: Main Processing.                                  *   CSE006
      *  Calls    : ZCNSD                                             *   CSE006
      *             ZSEDIT                                            *   CSE006
      *             ZDAYS                                             *   CSE006
      *             TOTL2                                             *   CSE006
      *                                                               *   CSE006
      *****************************************************************   CSE006
      *                                                                   CSE006
     C           BSINFO    BEGSR                           *** BSINFO **  CSE006
      *                                                                   CSE006
      ***  Initialise REPOS Indicator                                     CSE006
      *                                                                   CSE006
     C                     MOVEL'0'       *IN40                           CSE006
      *                                                                   CSE006
     C           *LOVAL    SETLLBKAMTX                                    CSE006
     C           SVBHBR    SETLLBKAMTX                                    CSE006
      *                                                                   CSE006
     C                     MOVEL'0'       *IN48                           CSE006
      *                                                                   CSE006
     C           SVBHBR    CHAINBKAMTX               48                   CSE006
      *                                                                   CSE006
      *** Headings are required                                           CSE006
      *                                                                   CSE006
     C                     MOVE 'Y'       HEAD                            CSE006
      *                                                                   CSE006
     C           *IN48     DOWEQ'0'                                       CSE006
      *                                                                   CSE006
      ***  Change of Branch.                                              CSE006
      *                                                                   CSE006
     C           BHBA      IFNE SVBHBR                                    CSE006
      *                                                                   CSE006
      ***  Access Branch file for Branch Name.                            CSE006
      *                                                                   CSE006
     C**********           CALL 'AOBRCHR0'                                CSE006              CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   RTCD                            CSE006
     C                     PARM '*KEY   ' OPTN                            CSE006
     C                     PARM BRANCH    BRCD    3                       CSE006
     C********** SDBRCH    PARM SDBRCH    DSFDY                           CSE006              CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
      *                                                                   CSE006
      ***  Database Error if not found.                                   CSE006
      *                                                                   CSE006
     C           RTCD      IFNE *BLANKS                                   CSE006
     C           *LOCK     IN   LDA                                       CSE006
     C                     MOVEL'BRCH'    DBFILE           ***************CSE006
     C                     MOVELBRANCH    DBKEY            * DB ERROR 10 *CSE006
     C                     Z-ADD010       DBASE            ***************CSE006
     C                     OUT  LDA                                       CSE006
     C                     SETON                     U7U8LR               CSE006
     C                     EXSR ERROR                                     CSE006
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
      ***  Save Branch field.                                             CSE006
      *                                                                   CSE006
     C                     MOVELBHBA      SVBHBR                          CSE006
     C                     MOVELA8BRNM    RBRNM                           CSE006
      *                                                                   CSE006
      ***  Headings are required.                                         CSE006
      *                                                                   CSE006
     C                     MOVE 'Y'       HEAD                            CSE006
     C                     GOTO ENDBS                                     CSE006
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
     C           BYSL      IFEQ 'Y'                                       CSE006
      *                                                                   CSE006
      ***  Change in Keys for headings and subheadings                    CSE006
      *                                                                   CSE006
     C           NCRY      IFNE SVNCRY                                    CSE006
     C           INVT      ORNE SVINVT                                    CSE006
      *                                                                   CSE006
      ***  Produce Total lines                                            CSE006
     C           RCDDET    IFEQ 'Y'                                       CSE006
     C                     EXSR TOTL2                                     CSE006
     C                     MOVE 'N'       RCDDET                          CSE006
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
      ***  Initialise Totals fields.                                      CSE006
      *                                                                   CSE006
     C                     CLEARTTBSPL                                    CSE006
     C                     CLEARTTBSAP                                    CSE006
     C                     CLEARTTBSOP                                    CSE006
      *                                                                   CSE006
      ***  Change of Investment Type.                                     CSE006
      ***  (Change of Investment Type or Currency)                        CSE006
      *                                                                   CSE006
     C           INVT      IFNE SVINVT                                    CSE006
     C           NCRY      ORNE SVNCRY                                    CSE006
      *                                                                   CSE006
      ***  Access Investment Types file for Investment Name.              CSE006
      *                                                                   CSE006
     C           KLINV     CHAININVTP                81                   CSE006
      *                                                                   CSE006
      ***  Database Error if not found.                                   CSE006
      *                                                                   CSE006
     C           *IN81     IFNE '0'                                       CSE006
     C           *LOCK     IN   LDA                                       CSE006
     C                     MOVEL'INVTP'   DBFILE           ***************CSE006
     C                     MOVELNCRY      DBKEY            * DB ERROR 06 *CSE006
     C                     MOVE INVT      DBKEY            ***************CSE006
     C                     Z-ADD006       DBASE                           CSE006
     C                     OUT  LDA                                       CSE006
     C                     SETON                     U7U8LR               CSE006
     C                     EXSR ERROR                                     CSE006
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
      ***  Save Investment Type field.                                    CSE006
      *                                                                   CSE006
     C                     MOVE INVT      SVINVT                          CSE006
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
      ***  Change of Currency.                                            CSE006
      *                                                                   CSE006
     C           NCRY      IFNE SVNCRY                                    CSE006
      *                                                                   CSE006
      ***  Access Currency file for Currency Name.                        CSE006
      *                                                                   CSE006
     C                     CALL 'AOCURRR0'                                CSE006
     C                     PARM *BLANKS   RTCD                            CSE006
     C                     PARM '*KEY   ' OPTN                            CSE006
     C                     PARM NCRY      CCY     3                       CSE006
     C           SDCURR    PARM SDCURR    DSSDY                           CSE006
      *                                                                   CSE006
      ***  Database Error if not found.                                   CSE006
      *                                                                   CSE006
     C           RTCD      IFNE *BLANKS                                   CSE006
     C           *LOCK     IN   LDA                                       CSE006
     C                     MOVEL'CURR'    DBFILE           ***************CSE006
     C                     MOVELNCRY      DBKEY            * DB ERROR 07 *CSE006
     C                     Z-ADD007       DBASE            ***************CSE006
     C                     OUT  LDA                                       CSE006
     C                     SETON                     U7U8LR               CSE006
     C                     EXSR ERROR                                     CSE006
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
      ***  Save Currency field.                                           CSE006
      *                                                                   CSE006
     C                     MOVE NCRY      SVNCRY                          CSE006
     C                     MOVELA6CYNM    RCYNM                           CSE006
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
      ***  Print Headings if required.                                    CSE006
      *                                                                   CSE006
     C           HEAD      IFEQ 'Y'                                       CSE006
      *                                                                   CSE006
      ***  If not first time through print trailer and close print file   CSE006
      *                                                                   CSE006
     C           RPTDET    IFEQ 'Y'                                       CSE006
     C           @P1SW     ANDEQ'Y'                                                          BUG7816
     C                     WRITETRAILP1                                   CSE006
     C                     CLOSESE1140P1                                  CSE006
     C                     MOVE 'N'       @P1SW                                              BUG7816
     C                     ELSE                                           CSE006
     C                     MOVE 'Y'       RPTDET                          CSE006
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
     C                     Z-ADD0         PAGE                                                206536
     C                     OPEN SE1140P1                                  CSE006
     C                     MOVE 'Y'       @P1SW                                              BUG7816
     C                     EXSR RCFP1                                     CSE006
     C                     WRITEHEADP1                                    CSE006
     C                     Z-ADD7         LINCNT  20                      CSE006
     C                     MOVE 'N'       HEAD                            CSE006
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
      ***  Check for overflow.                                            CSE006
      *                                                                   CSE006
     C           LINCNT    IFGE 52                                        CSE006
     C                     WRITEHEADP1                                    CSE006
     C                     Z-ADD7         LINCNT                          CSE006
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
      *** Initialise *IN49                                                CSE006
      *                                                                   CSE006
     C                     MOVEL'0'       *IN49                           CSE006
      *                                                                   CSE006
      ***  Key List for BSPOSDL1                                          CSE006
     C           KLBSP     KLIST                                          CSE006
     C                     KFLD           BHBA                            CSE006
     C                     KFLD           BHBK                            CSE006
     C                     KFLD           BHSC                            CSE006
      *                                                                   CSE006
     C           *LOVAL    SETLLBSPOSDL1                                  CSE006
     C           KLBSP     SETLLBSPOSDL1                                  CSE006
      *                                                                   CSE006
      ***  Get Buy-Sell back position by Security                         CSE006
      *                                                                   CSE006
     C           KLBSP     READEBSPOSDL1                 49               CSE006
     C           *IN49     DOWEQ'0'                                       CSE006
      *                                                                   CSE006
     C           *IN49     IFEQ '0'                                       CSE006
     C           RECI      ANDEQ'D'                                       CSE006
      *                                                                   CSE006
      ***  Chain to trading file to get trade details                     CSE006
      *                                                                   CSE006
     C           BSPRF     CHAINTRADS                50                   CSE006
      *                                                                   CSE006
      ***  If no record found then issue database error.                  CSE006
     C           *IN50     IFEQ '1'                                       CSE006
     C           *LOCK     IN   LDA                                       CSE006
     C                     MOVEL'BRCH'    DBFILE           ***************CSE006
     C                     MOVELBRANCH    DBKEY            * DB ERROR 08 *CSE006
     C                     Z-ADD008       DBASE            ***************CSE006
     C                     OUT  LDA                                       CSE006
     C                     SETON                     U7U8LR               CSE006
     C                     EXSR ERROR                                     CSE006
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
      ***  Chain to Securities file to get to Day Basis field.            CSE006
      *                                                                   CSE006
     C           BHSC      CHAINSECTY                83                   CSE006
      *                                                                   CSE006
      ***  If no record found then issue database error.                  CSE006
     C           *IN83     IFEQ '1'                                       CSE006
     C           *LOCK     IN   LDA                                       CSE006
     C                     MOVEL'BRCH'    DBFILE           ***************CSE006
     C                     MOVELBRANCH    DBKEY            * DB ERROR 08 *CSE006
     C                     Z-ADD009       DBASE            ***************CSE006
     C                     OUT  LDA                                       CSE006
     C                     SETON                     U7U8LR               CSE006
     C                     EXSR ERROR                                     CSE006
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
      ** Save Security field.                                             CSE006
      *                                                                   CSE006
     C                     MOVE BHSC      SVBHSC                          CSE006
      *                                                                   CSE006
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
      ***  Print Subheadings.                                             CSE006
      *                                                                   CSE006
     C                     WRITESUBH2                                     CSE006
     C                     ADD  6         LINCNT                          CSE006
      *                                                                   CSE006
      ***  Note if a record has been found.                               CSE006
      *                                                                   CSE006
     C                     MOVE 'Y'       RCDDET                          CSE006
     C                     MOVE 'Y'       RPTDET                          CSE006
     C                     EXSR PRINT2                                    CSE006
      *                                                                   CSE006
     C           KLBSP     READEBSPOSDL1                 49               CSE006
     C                     ENDDO                                          CSE006
      *                                                                   CSE006
      ***  END of Change of Keys.                                         CSE006
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
      *                                                                   CSE006
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
      *** Read next record                                                CSE006
      *                                                                   CSE006
     C                     READ BKAMTX                   48               CSE006
      *                                                                   CSE006
     C                     ENDDO                                          CSE006
      *                                                                   CSE006
      ***  If no Investment Type totals produced for last Investment      CSE006
      ***  Type and there are no Database Errors then produce totals.     CSE006
      *                                                                   CSE006
     C           *INU7     IFNE '1'                                       CSE006
     C           *INU8     ANDNE'1'                                       CSE006
     C           RCDDET    ANDEQ'Y'                                       CSE006
     C                     EXSR TOTL2                                     CSE006
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
      *** End SR for BYSL book                                            CSE006
      *                                                                   CSE006
     C           ENDBS     TAG                                            CSE006
      *                                                                   CSE006
      ***  Set on  REPOS Indicator                                        CSE006
      *                                                                   CSE006
     C                     MOVEL'1'       *IN40                           CSE006
      *                                                                   CSE006
     C                     ENDSR                                          CSE006
      *                                                                   CSE006
     C/COPY WNCPYSRC,SE1140C010                                           CSE006
      ****************************************************************    CSE006
      /EJECT                                                                                AR745122
      ****************************************************************                      AR745122
      *                                                              *                      AR745122
      *  SRSUBC - Control SUBHEAD printing                           *                      AR745122
      *                                                              *                      AR745122
      *  Called By: Main Processing                                  *                      AR745122
      *  Calls    :                                                  *                      AR745122
      *                                                              *                      AR745122
      ****************************************************************                      AR745122
      *                                                                                     AR745122
     C           SRSUBC    BEGSR                                                            AR745122
      *                                                                                     AR745122
     C                     MOVE 'N'       PSUBH   1                                         AR745122
      *                                                                                     AR745122
      ** Access Investment Types file                                                       AR745122
      *                                                                                     AR745122
     C           KLINV     CHAININVTP                81                                     AR745122
      *                                                                                     AR745122
      ** Database Error if not found                                                        AR745122
      *                                                                                     AR745122
     C           *IN81     IFNE '0'                                                         AR745122
     C           *LOCK     IN   LDA                                                         AR745122
     C                     MOVEL'INVTP'   DBFILE                                            AR745122
     C                     MOVELNCRY      DBKEY                                             AR745122
     C                     MOVE INVT      DBKEY                                             AR745122
     C                     Z-ADD011       DBASE                                             AR745122
     C                     OUT  LDA                                                         AR745122
     C                     SETON                     U7U8LR                                 AR745122
     C                     EXSR ERROR                                                       AR745122
     C                     END                                                              AR745122
      *                                                                                     AR745122
      **  Access Security file                                                              AR745122
      *                                                                                     AR745122
     C           BHSC      CHAINSECTY                83                                     AR745122
      *                                                                                     AR745122
      ** Database Error if not found.                                                       AR745122
      *                                                                                     AR745122
     C           *IN83     IFNE '0'                                                         AR745122
     C           *LOCK     IN   LDA                                                         AR745122
     C                     MOVEL'SECTY'   DBFILE                                            AR745122
     C                     MOVELBHSC      DBKEY                                             AR745122
     C                     Z-ADD012       DBASE                                             AR745122
     C                     OUT  LDA                                                         AR745122
     C                     SETON                     U7U8LR                                 AR745122
     C                     EXSR ERROR                                                       AR745122
     C                     END                                                              AR745122
      *                                                                                     AR745122
      ***  Access Book Position file                                                        AR745122
      *                                                                                     AR745122
     C           KLBKP     CHAINBKPOS                82                                     AR745122
      *                                                                                     AR745122
      * Set ON *IN84 if Mortgaged Backed and Current Factor = 0                             AR745122
      * or if Security has matured                                                          AR745122
      *                                                                                     AR745122
     C                     SETOF                     84                                     AR745122
      *                                                                                     AR745122
     C           PROT      IFEQ '8'                                                         AR745122
     C           CFCT      ANDEQ0                                                           AR745122
     C           RECI      ORNE 'D'                                                         AR745122
     C                     SETON                     84                                     AR745122
     C                     END                                                              AR745122
      *                                                                                     AR745122
      ** Note if a record has been found                                                    AR745122
      *                                                                                     AR745122
     C           *IN82     IFEQ '0'                                                         AR745122
     C           *IN84     ANDNE'1'                                                         AR745122
     C           NPSN      ANDNE0                                                           AR745122
     C           BUDU      ANDNE0                                                           AR745122
     C                     MOVE 'Y'       PSUBH                                             AR745122
     C                     ENDIF                                                            AR745122
      *                                                                                     AR745122
     C                     ENDSR                                                            AR745122
      *                                                                                     AR745122
      /EJECT
      *****************************************************************
      *                                                               *
      *  TOTALS - Subroutine to Format/Print Investment Type Totals   *
      *                                                               *
      *  Called By: Main Processing.                                  *
      *  Calls    : ZSEDIT                                            *
      *                                                               *
      *****************************************************************
      *
     C           TOTALS    BEGSR                           *** TOTALS ***
      *
      ***  Face Value.
      *
     C                     Z-ADDTTNOM     ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    TNOM
      *
      ***  Discount/Premium Amount.
      *
     C                     Z-ADDTTDSC     ZFLD15
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    TDSC
      *
      ***  Amount Amortised to Date.
      *
     C                     Z-ADDTTDPFP    ZFLD15
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    TDPFP
      *
      ***  Amount to Amortise.
      *
     C                     Z-ADDTTBUDU    ZFLD15
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    TBUDU
     C/COPY WNCPYSRC,SE1140C030
      *
      ***  Check for overflow.
      *
     C           LINCNT    IFGE 54
     C                     WRITEHEADP1
     C/COPY WNCPYSRC,SE1140C031
     C                     WRITESUBHEAD
     C/COPY WNCPYSRC,SE1140C032
     C                     Z-ADD13        LINCNT
     C                     END
      *
      ***  Print Totals.
     C/COPY WNCPYSRC,SE1140C033
      *
     C           @P1SW     IFEQ 'Y'                                                          BUG7816
     C                     WRITETOTAL
     C/COPY WNCPYSRC,SE1140C034
     C                     ADD  4         LINCNT
     C                     ENDIF                                                             BUG7816
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT                                                              CSE006
      *****************************************************************   CSE006
      *                                                               *   CSE006
      *  TOTL2 - Subroutine to Format/Print Accumulated Profit/Loss   *   CSE006
      *                                                               *   CSE006
      *  Called By: Main Processing.                                  *   CSE006
      *  Calls    : ZSEDIT                                            *   CSE006
      *                                                               *   CSE006
      *****************************************************************   CSE006
      *                                                                   CSE006
     C           TOTL2     BEGSR                           *** TOTL2 **** CSE006
      *                                                                   CSE006
      ***  Profit/Loss                                                    CSE006
      *                                                                   CSE006
     C                     Z-ADDTTBSPL    ZFLD15                          CSE006
     C                     Z-ADDA6NBDP    ZDECS                           CSE006
     C                     MOVE 'J'       ZECODE                          CSE006
     C                     EXSR ZSEDIT                                    CSE006
     C                     MOVE ZOUT21    TBSPL                           CSE006
      *                                                                   CSE006
      ***  Amount Amortised to Date.                                      CSE006
      *                                                                   CSE006
     C                     Z-ADDTTBSAP    ZFLD15                          CSE006
     C                     EXSR ZSEDIT                                    CSE006
     C                     MOVE ZOUT21    TBSAPL                          CSE006
      *                                                                   CSE006
      ***  Amount to Amortise.                                            CSE006
      *                                                                   CSE006
     C                     Z-ADDTTBSOP    ZFLD15                          CSE006
     C                     EXSR ZSEDIT                                    CSE006
     C                     MOVE ZOUT21    TBSOPL                          CSE006
      *                                                                   CSE006
      ***  Check for overflow.                                            CSE006
      *                                                                   CSE006
     C           LINCNT    IFGE 54                                        CSE006
     C                     WRITEHEADP1                                    CSE006
     C                     WRITESUBH2                                     CSE006
     C                     Z-ADD13        LINCNT                          CSE006
     C                     END                                            CSE006
      *                                                                   CSE006
      ***  Print Totals.                                                  CSE006
      *                                                                   CSE006
     C                     WRITETOTAL2                                    CSE006
     C                     ADD  4         LINCNT                          CSE006
      *                                                                   CSE006
     C                     ENDSR                                          CSE006
      *                                                                   CSE006
      *****************************************************************   CSE006
      /EJECT                                                              CSE006
      *****************************************************************
      *                                                               *
      *  ERROR  - This subroutine prints database error information on*
      *           the audit printer file and dumps the program.       *
      *                                                               *
      *  Called By: Main Processing.                                  *
      *  Calls    : None.                                             *
      *                                                               *
      *****************************************************************
      *
     C           ERROR     BEGSR                           *** ERROR ***
      *
      ***  If details have been printed to the detail printer file.
      *
     C           RPTDET    IFEQ 'Y'
     C                     WRITEERRORP1
     C                     END
      *
      ***  Open audit printer file.
      *
     C                     OPEN SE1140AU
     C                     EXSR RCFAU                                     048310
     C                     Z-ADD0         PAGE
     C                     WRITEHEADAU
     C                     WRITEDBERR
     C                     CLOSESE1140AU                                  048310
      *
     C                     DUMP
     C                     GOTO ENDPGM
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  RCFP1  - Subroutine to register P1 printer file via RCF      *
      *                                                               *
      *  Called By: Main Processing.                                  *
      *  Calls    : None.                                             *
      *                                                               *
      *****************************************************************
      *
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM1  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           SEQ     5
     C                     PARM BHBA      SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM1
     C                     PARM *BLANKS   ZSERR   1
      *
     C           ZSERR     IFEQ 'Y'
      *
      ***  Error during ZSFILE Processing, Return to Calling Program.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     END
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  RCFAU  - Subroutine to register AU printer file via RCF.     *
      *                                                               *
      *  Called By: INIT                                              *
      *  Calls    : None.                                             *
      *                                                               *
      *****************************************************************
      *
     C           RCFAU     BEGSR                            *** RCFAU  ***
      *
      ***  Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
     C           ZSERR     IFEQ 'Y'
      *
      ***  Error during ZSFILE Processing, Return to Calling Program.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     END
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
     C/COPY ZSRSRC,ZCNSDZ1
      /EJECT
     C/COPY ZSRSRC,ZDATE2Z2
      /EJECT
     C/COPY ZSRSRC,ZDAYSZ2
      /EJECT
     C/COPY ZSRSRC,ZSEDITZ3
      /EJECT
      ***
**  CPY@
(c) Finastra International Limited 2001
**   ZF29 DAY NOS OF FEB 29 TO 2096 (INCLUDING 2000)
0006001521029820444305904073650882610287117481320914670161311759219053
2051421975234362489726358278192928030741322023366335124365853804639507
40968424294389045351
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  POWER8 - ARRAY OF POWERS FOR CURRENCY CONVERSION
00000001
00000010
00000100
00001000
00010000
00100000
01000000
10000000
     X/COPY WNCPYSRC,SE1140X001
