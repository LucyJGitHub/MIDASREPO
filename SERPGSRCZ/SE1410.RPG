     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Create Depot Movement Events')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module
     F*                                                               *
     F*  SE1410 - CREATE DEPOT MOVEMENT EVENTS AND AUDIT REPORT       *
     F*                                                               *
     F*  Function: The Depot Movements file is read to project        *
     F*   (COB)    Forward-dated Depot Movement Events which will     *
     F*            affect Positions or require reporting as Projected *
     F*            Events.                                            *
     F*            Event record shows Movement Type, Depot,           *
     F*            Movement Date and whether movement is In or Out.   *
     F*                                                               *
     F*  Called by: SEC1102 - Create Depot Movement Events for Forward*
     F*                       Dated Depot Movements.                  *
     F*                                                               *
     F*  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. 220795             Date 02Mar20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027A            Date 17May06               *
      *                 CSE071             Date 19Jul05               *
      *                 CGL029             Date 01Sep03               *
      *                 222727             Date 05Nov03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CSE015             Date 06Dec99               *
      *                 CSE017             Date 20Oct99               *
      *                 CSE012             Date 16Aug99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01445             Date 04Nov93               *
      *                 052254             Date 10Jan94               *
     F*                 S01401             DATE 16JUN93               *
     F*                 S01397             DATE 10SEP92               *
     F*                 E34034             DATE 07JAN92               *
     F*                 E32674             DATE 03DEC91               *
     F*                 S01220             DATE 12NOV90               *
     F*                 S01117             DATE 02NOV90               *
     F*                 S01271             DATE 02NOV90               *
     F*                 E20110             DATE 07FEB90               *
     F*                 S01176             DATE 15AUG88               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  220795 - REPO processing cause FOOB errors. Records tagged   *
      *           as Reversed or RECI = 'R' should not be included in *
      *           the records count of PF/DPTMVD.                     *
      *           Applied for MD-55196.                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027A- Conversion of cust. no. to alpha (post 103 build)   *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  222727 - Release 5.0 errors  (Recompile)                     *
      *  CSE015 - Forward Valued Depot Movements (Recompile)          *
      *  CSE017 - Cum/Ex Indicator on Depot Movements (Recompile)     *
      *  CSE012 - SE Depot Movement Narratives (Recompiled).          *
     F*  S01445 - DEPOT MOVEMENT CHARGES ENHANCEMENT.                 *
     F*           Recompiled due to changes in PF/DPTMVD.             *
     F*  052254 - RECOMPILED as CURRENT FACTOR amended from 9,8       *
     F*           to 10,9                                             *
     F*  S01401 - MT5xx SWIFT Messages Feature:                       *
     F*          - Recompiled due to changes to files.                *
     F*  S01397 - Rel 10 Incorporation of BNZ SARs A46008 and A46013. *
     F*          - Recompiled due to changes to files.                *
     F*  E34034 - Recompiled over changed printer file                *
     F*  E32674 - Recompiled due to error in DPTMVD                   *
     F*  S01220 - STRATEGIC RISK MANAGEMENT                           *
     F*           Recompiled over changed PF/SECTYD                   *
     F*  S01117 - Multibranching.                                     *
     F*  S01271 - Recompiled for Pay/Receive in Input Cycle changes   *
     F*           to TRADSD and/or DPTMVD                             *
     F*  E20110 - Past Events should not appear in Future Projections.*
     F*  S01176 - Securities Trading 3.1 (Australia)                  *
     F*                                                               *
     F*****************************************************************
      *
     FDPTMV   IPE E           K        DISK
     FDPTMVA  IF  E                    DISK
     FSECTY   IF  E           K        DISK
     F*TABSE***IF**E********** K        DISK                              S01117
     F************TABLETAF                          KIGNORE               S01117
     F************TABLETHF                          KIGNORE               S01117
     F*********** TABLETJF                          KIGNORE               S01117
     F************TABLETLF                          KIGNORE               S01117
     F************TABLETKF                          KIGNORE               S01117
     F************TABLETNF                          KIGNORE               S01117
     F************TABLETPF                          KIGNORE               S01117
     F************TABLETRF                          KIGNORE               S01117
     F************TABLETXF                          KIGNORE               S01117
     F**ICD*1*****TABTB10F                          KIGNORE               S01117
     F************TABTB10F                          KIGNORE               S01117
     F**ICD*1*****TABTB11F                          KIGNORE               S01117
     F************TABTB36F                          KIGNORE               S01117
     F************TABTB40F                          KIGNORE               S01117
     F************TABTE10F                          KIGNORE               S01117
     F************TABTE20F                          KIGNORE               S01117
     F**CCY*******TABTG10F                          KIGNORE
     F************TABTG20F                          KIGNORE               S01117
     F************TABLET2F                          KIGNORE               S01117
     F************TABLET3F                          KIGNORE               S01117
     F************TABLET5F                          KIGNORE               S01117
     FDMEVED  O   E                    DISK
     FDMEVEA  O   E                    DISK
     FSE1410AUO   E                    PRINTER
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   40  - Detail records read                                   *
     F*   50  - Work indicator                                        *
     F****51**-*ICD*record*2*not*found*                               *   S01117
     F*   52  - Chain failed on SECTY                                 *
     F*                                                               *
     F* 90-99 - Used in Standard Subroutines                          *
     F*                                                               *
     F*   U7  - Database Error                                        *
     F*   U8  - Database / Control Error                              *
     F*                                                               *
     F*****************************************************************
      /SPACE 3
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E   I N D E X                              *
     F*                                                               *
     F*  SRLR   - Total Time Calculations                             *
     F*  *PSSR  - Error handling subroutine                           *   S01117
     F*                                                               *
     F*  ZEVCD  - Calculate Event Control Date                        *
     F***ZICD2**-*Access*ICD*record*2**(TABTB11)                      *
     F***ZSYSTM*-*Access*ICD*record*1**(TABTB10)*                     *   S01117
     F*                                                               *
     F*****************************************************************
      /EJECT
     E                    CPY@    1   1 80
      /SPACE 3
     IDPTMVDF
     I              RECI                            DRECI
     I              DPSS                            DMSS  L1
     I              DPRN                            DMRN
     I              DPMV                            DMMT
     I              DPBN                            DMBC
     I***********   DPBC                            DMBR                  S01117
     I              DPBA                            DMBA                  S01117
     I              DPNA                            DMAN
     I              DPBK                            DMBO
     ISECTYDF
     I              NMCY                            DMCN
      *
      ** Local Data Area for DB Error Information
      *
     I***LDA*****   UDS                            256                    S01117
     ILDA         DS                            256                       S01117
     I***********                           134 177 #DBLOT                S01117
     I                                      134 183 #DBLOT                S01117
     I            DS
     I***********                           134 177 DBLOT                 S01117
     I***********                           134 138 DBFILE                S01117
     I***********                           139 167 DBKEY                 S01117
     I***********                           168 175 DBPGM                 S01117
     I***********                           176 177 DBASE                 S01117
     I                                      134 183 DBLOT                 S01117
     I                                      134 141 DBFILE                S01117
     I                                      142 170 DBKEY                 S01117
     I                                      171 180 DBPGM                 S01117
     I                                      181 1830DBASE                 S01117
      *                                                                   S01117
      ** Data area RUNDAT                                                 S01117
      *                                                                   S01117
     IRUNDT       DS                                                      S01117
     I                                        1   7 RUNA                  S01117
     I                                    P   8  100RUND                  S01117
     I                                       11  11 SSF                   S01117
     I                                       12  12 DATF                  S01117
     I                                       13  13 MBIN                  S01117
      *                                                                   S01117
      ** External DS for bank details                                     S01117
      *                                                                   S01117
     ISDBANK    E DSSDBANKPD                                              S01117
     I*                                                                   S01117
     ISDGELR    E DSSDGELRPD                                              S01117
     I** General ledger details                                           S01117
     I*                                                                   S01117
      *                                                                   S01117
      ** First DS for access programs, short data structure               S01117
      *                                                                   S01117
     IDSFDY     E DSDSFDY                                                 S01117
     I*
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I*
     I           SDS
     I                                      244 246 WSID
     I                                      254 263 USID
      *                                                                                       CGL029
     IDSSDY     E DSDSSDY                                                                     CGL029
      *                                                                                       CGL029
     C/EJECT
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***  Initial Processing
      *
     C           *IN40     IFEQ '0'
      *
      * Prepare LDA for the database error output
      *
     C                     MOVEL*BLANK    DBLOT
     C                     MOVEL'SE1410'  DBPGM
      *
     C**Access*the*I.C.D.*record*1**(PF/TABTB10)*                         S01117
      ***********                                                         S01117
     C***********          EXSR ZSYSTM                                    S01117
      *                                                                   S01117
      ** Read in RUNDAT data area                                         S01117
      *                                                                   S01117
     C           *NAMVAR   DEFN RUNDAT    RUNDT                           S01117
     C                     IN   RUNDT                                     S01117
      *                                                                   S01117
      ** Check system date format, DDMMYY or MMDDYY.                      S01117
      *                                                                   S01117
     C           DATF      COMP 'M'                      98               S01117
      *                                                                   S01117
      ** Get Bank Title by access object                                  S01117
      *                                                                   S01117
     C                     CALL 'AOBANKR0'                                S01117
     C                     PARM '*MSG   ' @RTCD   7                       S01117
     C                     PARM '*FIRST ' @OPTN   7                       S01117
     C           SDBANK    PARM SDBANK    DSFDY                           S01117
     C*                                                                   S01117
     C**Error*in*ZSYSTM*                                                  S01117
     C************IN99     IFEQ '1'                                       S01117
     C           @RTCD     IFNE *BLANKS                                   S01117
      *
     C                     MOVE '1'       *INLR
     C                     MOVEL'SDBANKPD'DBFILE           ***************S01117
     C                     MOVEL@OPTN     DBKEY            * DB ERROR 01 *S01117
     C                     Z-ADD1         DBASE            ***************S01117
     C                     MOVEL@OPTN     DBOPTN  7                       S01117
     C                     SETON                       U7U8               S01117
     C                     EXSR *PSSR                                     S01117
     C                     ELSE
      *
      **Access*the*I.C.D.*record*2**(PF/TABTB11)                          S01117
     C* Access general ledger details                                     S01117
      *
     C******************** EXSR ZICD2                                     S01117
     C**********           CALL 'AOGELRR0'                                             S01117 CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM '*MSG   ' @RTCD   7                       S01117
     C                     PARM '*FIRST ' @OPTN   7                       S01117
     C********** SDGELR    PARM SDGELR    DSFDY                                        S01117 CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
      **Error*in*ZICD2****                                                S01117
     C********** *IN99*****IFEQ '1'                                       S01117
     C***********          MOVE '1'       *IN51                           S01117
     C           @RTCD     IFNE *BLANKS                                   S01117
     C                     MOVE '1'       *INLR
     C*********************MOVEL'TABLE   'DBFILE           ***************S01117
     C                     MOVEL'SDGELRPD'DBFILE           ***************S01117
     C                     MOVEL@OPTN     DBKEY            * DB ERROR 02 *S01117
     C                     Z-ADD2         DBASE            ***************S01117
     C                     MOVEL@OPTN     DBOPTN  7                       S01117
     C                     SETON                       U7U8               S01117
     C                     EXSR *PSSR                                     S01117
     C                     ELSE
      **ICD*records*1*&*2*found************                               S01117
      * Calculate the Event Control Date
      *                                                                   S01117
      ** Setup required input parameters for SR.ZEVCD                     S01117
      *                                                                   S01117
     C                     Z-ADDBJDNWD    ZZDNWD                          S01117
     C                     Z-ADDBKAPDT    ZZAPDT                          S01117
      *                                                                   S01117
     C                     EXSR ZEVCD
     C                     MOVE '1'       *IN40
     C                     MOVE 'D'       DMRI
     C                     MOVE 'S'       DMMK
      *
     C                     END
     C                     END
     C                     END
      *
      *================================================================
      /EJECT
      *================================================================
      *
      ***  Main Processing
      *
      * Records read
     C           DRECI     IFNE '*'
     C           DRECI     ANDNE'R'                                                           220795
     C                     ADD  1         TRCNT   50
      *
      * Access securities file LF/SECTY
      *  if new Security.
      *
     C           *INL1     IFEQ '1'
     C           *IN40     ANDEQ'1'
     C           DMSS      CHAINSECTY                52
     C           *IN52     IFEQ '1'
     C                     MOVE '1'       *INLR
     C***********          MOVEL'03'      DBASE            ***************S01117
     C                     Z-ADD3         DBASE            * DB ERROR 03 *S01117
     C                     MOVEL'SECTY  ' DBFILE           ***************
     C                     MOVELDMSS      DBKEY
     C                     SETON                       U7U8               S01117
     C                     EXSR *PSSR                                     S01117
     C                     END
     C                     END
      *
     C           *IN40     IFEQ '1'
     C           *IN52     ANDEQ'0'
     C           RECI      ANDEQ'D'
      *
      * Set up output values
      * ~~~~~~~~~~~~~~~~~~~~~
      * ========
      *  NOTE :         _________________________________________________
      * ========        ¦ Movement| Mvt In/Out | In/Out | Depot |  Mvt   ¦
      * Set up DEPOT    ¦ Type    | required   |        |       |  Date  ¦
      * and MVT. DATE   ¦----------------------|-------------------------¦
      * according to    ¦ DT      | In         |  I     |  In   |   In   ¦
      * the table --->  ¦         | Out        |  O     |  Out  |   In   ¦
      *                 ¦         |            |        |       |        ¦
      *                 ¦ RP,RR)  | In         |  I     |  In   |   In   ¦
      *                 ¦ BL,BB)  | Out        |  O     |  In   |   Out  ¦
      *                 ¦ PD,PL)  |            |        |       |        ¦S01176
      *                 ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
     C**********           Z-ADD0         DMDP                                               CSD027A
     C                     MOVE *BLANKS   DMDP                                               CSD027A
     C                     MOVE ' '       DMIO
     C                     Z-ADD0         DMMD
      *
     C                     Z-ADD0         EVIN    10
     C                     Z-ADD0         EVOUT   10
      *
      *         If Movement Type (DPTMVD) = DT
      *         -    write event record for Mvt IN
      *         -    write event record for Mvt OUT
      *
     C           DMMT      IFEQ 'DT'
     C           DPMD      ANDGTRUND                                      E20110
     C                     ADD  1         EVIN
     C                     ADD  1         EVOUT
     C                     END
      *
      **********If*Movement*Type*=*RP,*RR,*BL*or*BB********************   S01176
      *         If Movement Type = RP, RR, BL, PD, PL or BB               S01176
      *         -    If In Date NE 99999 and GT
      *              Event Control Date (P/ZEVCD)
      *              -    write event record for Mvt IN,
      *         -    If Out Date NE 99999 and GT
      *              Event Control Date
      *              -    write event record for Mvt OUT.
      *
     C           DMMT      IFEQ 'RP'
     C           DMMT      OREQ 'RR'
     C           DMMT      OREQ 'BL'
     C           DMMT      OREQ 'BB'
     C           DMMT      OREQ 'PD'                                      S01176
     C           DMMT      OREQ 'PL'                                      S01176
      *
     C           DPMD      IFNE 99999
     C           DPMD      ANDGTECD
     C           DPMD      ANDGTRUND                                      E20110
     C                     ADD  1         EVIN
     C                     END
      *
     C           DPDO      IFNE 99999
     C           DPDO      ANDGTECD
     C           DPDO      ANDGTRUND                                      E20110
     C                     ADD  1         EVOUT
     C                     END
     C                     END
      *
      ***  Movement IN Event
      *
     C           EVIN      IFNE 0
     C**********           Z-ADDDPID      DMDP                                               CSD027A
     C                     MOVE DPID      DMDP                                               CSD027A
     C                     MOVE 'I'       DMIO
     C                     Z-ADDDPMD      DMMD
     C                     ADD  1         OUTREC  50
     C                     WRITEDMEVEDF
     C                     END
      *
      ***  Movement OUT Event
      *
     C           EVOUT     IFNE 0
     C**********           Z-ADDDPOD      DMDP                                               CSD027A
     C                     MOVE DPOD      DMDP                                               CSD027A
     C                     MOVE 'O'       DMIO
     C                     Z-ADDDPDO      DMMD
      *
     C           DMMT      IFEQ 'DT'
     C                     Z-ADDDPMD      DMMD
     C                     ELSE
     C**********           Z-ADDDPID      DMDP                                               CSD027A
     C                     MOVE DPID      DMDP                                               CSD027A
     C                     END
      *
      * Write Event Record
      *
     C                     ADD  1         OUTREC  50
     C                     WRITEDMEVEDF
     C                     END
     C                     END
     C                     END
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *                                                               *
     CLR                   EXSR SRLR
      *                                                               *
      *================================================================
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRLR   - Subroutine for Control Processing                   *
      *                                                               *
      *****************************************************************
      *
     C           SRLR      BEGSR                           ***  SRLR  ***
      *
      ** If no records read - Empty Primary File
      ***access*the*I.C.D.*records*1*(PF/TABTB10)*                        S01117
      ** Read in RUNDAT data area and get title by access object          S01117
      *
     C           *IN40     IFEQ '0'
     C************IN99     ANDEQ'0'                         file.         S01117
     C           @RTCD     ANDEQ*BLANKS                     file.         S01117
     C***********          EXSR ZSYSTM                                    S01117
      *                                                                   S01117
      ** Read in RUNDAT data area                                         S01117
      *                                                                   S01117
     C                     IN   RUNDT                                     S01117
      *                                                                   S01117
      ** Check system date format, DDMMYY or MMDDYY.                      S01117
      *                                                                   S01117
     C           DATF      COMP 'M'                      98               S01117
      *                                                                   S01117
      ** Get Bank Title by access object                                  S01117
      *                                                                   S01117
     C                     CALL 'AOBANKR0'                                S01117
     C                     PARM '*MSG   ' @RTCD   7                       S01117
     C                     PARM '*FIRST ' @OPTN   7                       S01117
     C           SDBANK    PARM SDBANK    DSFDY                           S01117
      *                                                                   S01117
     C           @RTCD     IFNE *BLANKS                                   S01117
     C                     MOVEL'SDBANKPD'DBFILE           ***************S01117
     C                     MOVEL@OPTN     DBKEY            * DB ERROR 01 *S01117
     C                     Z-ADD1         DBASE            ***************S01117
     C                     MOVEL@OPTN     DBOPTN  7                       S01117
     C                     SETON                       U7U8               S01117
     C                     EXSR *PSSR                                     S01117
     C                     END                                            S01117
     C                     END
      *
      * Write control report headings
      *
     C                     WRITEHEADAU
      *
      * Error
      *
     C************IN99     IFEQ '1'                                       S01117
     C************IN52     OREQ '1'                                       S01117
     C***********          MOVE '1'       *INU8                           S01117
     C***********          MOVE '1'       *INU7                           S01117
      ***********                                                         S01117
     C************IN52     IFEQ '0'                        ***************S01117
     C***********          MOVEL'01'      DBASE            **DB ERROR 01**S01117
     C***********          MOVEL'TABLE'   DBFILE           ***************S01117
     C***********          MOVELZTABKY    DBKEY                           S01117
     C***********          END                                            S01117
      ***********                                                         S01117
     C************IN51     IFEQ '1'                        ***************S01117
     C***********          MOVEL'02'      DBASE            **DB ERROR 02**S01117
     C***********          END                             ***************S01117
     C*                                                                   S01117
     C           *INU7     IFEQ '1'                                       S01117
     C           *INU8     ANDEQ'1'                                       S01117
     C                     WRITEERROR
      *
     C                     END
      *
      *  Access Depot Movements' AUDIT record.
      *
     C                     Z-ADD0         NORE       50
     C                     READ DPTMVA                   50
     C                     Z-ADDNORE      TRIN    50
      *
      *  Compare Control Totals
     C                     WRITETRTOT
      *
      ***  Depot Mvt. Event Totals
      *
     C           TRIN      IFNE TRCNT
     C           *INU7     IFEQ '0'                                       S01117
     C                     WRITEDIFF
     C                     END                                            S01117
     C           *INU8     IFEQ '0'
     C                     MOVE '1'       *INU8            ***************
     C***********          MOVEL'04'      DBASE            **DB ERROR 04**S01117
     C***********          MOVEL'DPTMVA  'DBFILE           ***************S01117
     C***********          MOVEL'*AUDIT**'DBKEY                           S01117
     C***********          WRITEERROR                                     S01117
     C                     END
     C                     END
      *
      *          - Write Depot Mvt. Events Audit record
      *
     C                     Z-ADDOUTREC    NORE
     C                     WRITEDMEVEAF
      *
     C***IF*NO*OUTPUT*-*Print*a*message*to*this*effect*                   S01117
     C************IN40     IFEQ '0'                                       S01117
     C***********          WRITENONE                                      S01117
      ***********                                                         S01117
      *********ELSE***-*Print*Records*Written*Total*                      S01117
     C***********          ELSE                                           S01117
     C                     WRITECONTROL
     C***********          END                                            S01117
      *
      *  END OF PROGRAM
     C***********          WRITETRAILAU                                   S01117
      *
      *  If Database Erorrs found, update LDA
      *
     C           *NAMVAR   DEFN           LDA
      *
     C           *INU8     IFEQ '1'
     C           *INU7     ANDEQ'1'                                       S01117
     C           *LOCK     IN   LDA
     C                     MOVELDBLOT     #DBLOT
     C                     OUT  LDA
     C                     END
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************   S01117
      *                                                               *   S01117
      * *PSSR - Subroutine to handle abnormal error conditions        *   S01117
      *                                                               *   S01117
      * Called from: after abnormal operation occurs                  *   S01117
      *                                                               *   S01117
      * Calls: None                                                   *   S01117
      *                                                               *   S01117
      *****************************************************************   S01117
     C           *PSSR     BEGSR                           ** *PSSR **    S01117
      *                                                                   S01117
     C           @RUN      IFEQ *BLANKS                                   S01117
     C                     MOVE 'Y'       @RUN    1                       S01117
     C                     DUMP                                           S01117
     C                     END                                            S01117
      *                                                                   S01117
     C                     ENDSR                                          S01117
     C********************************************************************S01117
      /EJECT
     C/COPY ZSRSRC,ZEVCDZ1
      /EJECT
     C*/COPY*ZSRSRC,ZICD2Z1**
      */EJECT
     C***/COPY*ZSRSRC*ZSYSTMZ1                                            S01117
      *EJECT
      ***
**  CPY@
(c) Finastra International Limited 2001
