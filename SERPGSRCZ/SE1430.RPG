     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Project Forward Book Positions')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module
     F*                                                               *
     F*  SE1430 - PROJECT FORWARD BOOK POSITIONS                      *
     F*                                                               *
     F*  Function: This program projects Forward Portfolio Book       *
     F*   (COB)    Positions by Branch/Security/Book/Market/Date      *
     F*            using Forward-valued Trades (TREVED). Data is held *
     F*            as for Current Positions, with only two fields     *
     F*            projected: Nominal-Value Date Basis and Ex-Coupon  *
     F*            Nominal. The Current Position, if it exists, is    *
     F*            used as the starting point for these two balances. *
     F*                                                               *
     F*  Called by: SEC1104 - Project Forward Book Positions          *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      *  Prev Amend No. CAS006             Date 21Jan03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 052254             Date 10Jan94               *
      *                 S01397             Date 10Sep92               *
     F*                 E34034             DATE 07JAN92               *
     F*                 S01117             DATE 02NOV90               *
     F*                 E19101             DATE 29JAN90               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CAS006 - Hedge Accounting Phase B (Recompile)                *
     F*  052254 - RECOMPILED AS CURRENT FACTOR AMENDED FROM 9,8       *
     F*           TO 10,9                                             *
     F*  S01397 - Rel 10 Incorporation of BNZ SARs A46008 and A46013. *
     F*          - Recompiled due to changes to files.                *
     F*  E34034 - Recompiled over changed printer file                *
     F*  S01117 - Multibranching                                      *
     F*  E19101 - Ex-Coupon Nominal should be added for a Trade       *   E19101
     F*           Purchase and subtracted for a Trade Sale.           *   E19101
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
      *
     FBPSEV   IPE E           K        DISK
     FBKPHD   IF  E           K        DISK
     FBKPOS   IF  E           K        DISK
     F*TABSE***IF* E          K        DISK                               S01117
     F*********** TABLETAF                          KIGNORE               S01117
     F*********** TABLETHF                          KIGNORE               S01117
     F*********** TABLETJF                          KIGNORE               S01117
     F*********** TABLETLF                          KIGNORE               S01117
     F*********** TABLETKF                          KIGNORE               S01117
     F*********** TABLETNF                          KIGNORE               S01117
     F*********** TABLETPF                          KIGNORE               S01117
     F*********** TABLETRF                          KIGNORE               S01117
     F*********** TABLETXF                          KIGNORE               S01117
     F**ICD*1*****TABTB10F                          KIGNORE
     F*********** TABTB11F                          KIGNORE               S01117
     F*********** TABTB36F                          KIGNORE               S01117
     F*********** TABTB40F                          KIGNORE               S01117
     F*********** TABTE10F                          KIGNORE               S01117
     F*********** TABTE20F                          KIGNORE               S01117
     F*********** TABTG10F                          KIGNORE               S01117
     F*********** TABTG20F                          KIGNORE               S01117
     F*********** TABLET2F                          KIGNORE               S01117
     F*********** TABLET3F                          KIGNORE               S01117
     F*********** TABLET5F                          KIGNORE               S01117
     FTREVEA  IF  E                    DISK
     FBKPOFD  O   E                    DISK
     F            BKPOSDF                           KRENAMEBKPOFDF
     FBKPOFA  O   E                    DISK
     FSE1430AUO   E                    PRINTER
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   L1  - Branch/Security/Book/Market/Date combination change   *
     F*   L2  - Branch/Security/Book/Market combination change        *
     F*                                                               *
     F*   40  - Detail records read                                   *
     F*   50  - Work indicator                                        *
     F*                                                               *
     F* 90-99 - Used in Standard Subroutines                          *
     F*                                                               *
     F*   U7  - Database Error                                        *
     F*   U8  - Database / Control Error                              *
     F*                                                               *
     F*****************************************************************
      /SPACE 3
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E   I N D E X                              *
     F*                                                               *
     F*  L2DET  - Access Book Positions (Current)                     *
     F*  SRLR   - Total Calcs Routine                                 *
     F*                                                               *
     F*  *PSSR  - Error Handling Subroutine                           *   S01117
     F*                                                               *
     F*  ZSYSTM - Access ICD record                                   *
     F***ZSYSTM*-*Access*ICD*record*1**(TABTB10)**********            *   S01117
     F*                                                               *
     F*****************************************************************
      /EJECT
     E                    CPY@    1   1 80
      /EJECT
     ITREVEDF
     I***********   TRBN                            BPBR  L2              S01117
     I              TRBA                            BPBA  L2              S01117
     I              TRSS                            BPSC  L2
     I              TRBK                            BPBK  L2
     I              TRMK                            BPMK  L2
     I              TRVD                            BPPD  L1
     IBKPOSDF
     I***********   BPBR                            XBPBR                 S01117
     I              BPBA                            XBPBA                 S01117
     I              BPSC                            XBPSC
     I              BPBK                            XBPBK
     I              BPMK                            XBPMK
     I              BPPD                            XBPPD
     I              BPTV                            XBPTV
     I              NPSN                            XNPSN
     I              ECNP                            XECNP
     I              APPB                            XAPPB
     I              RPTP                            XRPTP
     I              PILP                            XPILP
     I              APNC                            XAPNC
     I              SNPI                            XSNPI
     I              SNSI                            XSNSI
     I              SANP                            XSANP
     I              SANS                            XSANS
     I              ANMP                            XANMP
     I              AAPR                            XAAPR
     I              ADJI                            XADJI
     I              TNLU                            XTNLU
     ITREVEAF
     I              NORE                            AUDIT1
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *
      ***  Local Data Area for DB ERROR Information
     I*LDA*******UDS                            256                       S01117
     ILDA         DS                            256                       S01117
     I***********                           134 177 #DBLOT                S01117
     I            DS
     I***********                           134 177 DBLOT                 S01117
     I***********                           134 138 DBFILE                S01117
     I***********                           139 167 DBKEY                 S01117
     I***********                           168 175 DBPGM                 S01117
     I***********                           176 177 DBASE                 S01117
     I                                      134 183 DBLOT                 S01117
     I                                      134 141 DBFILE                S01117
     I                                      142 170 DBKEY                 S01117
     I                                      171 180 DBPGM                 S01117
     I                                      181 1830DBASE                 S01117
     I*                                                                   S01117
     IRUNDT       DS                                                      S01117
     I*                                                                   S01117
     I** DATA AREA RUNDAT                                                 S01117
     I*                                                                   S01117
     I                                        1   7 RUNA                  S01117
     I                                    P   8  100RUND                  S01117
     I                                       11  11 SSF                   S01117
     I                                       12  12 DATF                  S01117
     I                                       13  13 MBIN                  S01117
     I*                                                                   S01117
     ISDBANK    E DSSDBANKPD                                              S01117
     I*                                                                   S01117
     I**  EXTERNAL DS FOR BANK DETAILS                                    S01117
     I*                                                                   S01117
     I*                                                                   S01117
     I**  FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE              S01117
     I*                                                                   S01117
     IDSFDY     E DSDSFDY                                                 S01117
     I*
     I           SDS
     I                                      244 246 WSID
     I                                      254 263 USID
     I            DS
     I                                        1  17 #BKP
     I                                        1  10 BPSC
     I***********                            11  130BPBR                  S01117
     I                                       11  13 BPBA                  S01117
     I                                       14  15 BPBK
     I                                       16  16 BPMK
     I                                       17  17 BPTV
     I            DS
     I                                        1  17 XBKP
     I                                        1  10 XBPSC
     I***********                            11  130XBPBR                 S01117
     I                                       11  13 XBPBA                 S01117
     I                                       14  15 XBPBK
     I                                       16  16 XBPMK
     I                                       17  17 XBPTV
     C/EJECT
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***  Initial Processing
      *
      * Access the Book Positions Header file  - LF/BKPHD
      *
     C           KBKPHD    KLIST
     C                     KFLD           BPSC
     C***********          KFLD           BPBR                            S01117
     C                     KFLD           BPBA                            S01117
     C                     KFLD           BPBK
     C                     KFLD           BPMK
     C                     KFLD           BPTV
      *
      * Access the Book Positions (Current) file  - LF/BKPOS
      *
     C           KBKPOS    KLIST
     C                     KFLD           BPSC
     C***********          KFLD           BPBR                            S01117
     C                     KFLD           BPBA                            S01117
     C                     KFLD           BPBK
     C                     KFLD           BPMK
     C                     KFLD           BPTV
     C                     KFLD           LPSD
      *
     C           *IN40     IFEQ '0'
      *
      ***  Trade/Value Date Basis
     C                     MOVE 'V'       BPTV
      *
      * Prepare LDA for the database error output
      *
     C           *NAMVAR   DEFN           LDA                             S01117
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVEL*BLANK    DBLOT
     C                     MOVEL'SE1430'  DBPGM
     C                     OUT  LDA                                       S01117
      *
      **Access*the*I.C.D.*record*(PF/TABTB10)*****************************S01117
      ***********                                                         S01117
     C***********          EXSR ZSYSTM                                    S01117
     C*                                                                   S01117
     C**  In the dataarea rundat                                          S01117
     C*                                                                   S01117
     C           *NAMVAR   DEFN RUNDAT    RUNDT                           S01117
     C                     IN   RUNDT                                     S01117
     C*                                                                   S01117
     C**  Check system date format, DDMMYY or MMDDYY.                     S01117
     C*                                                                   S01117
     C           DATF      COMP 'M'                      98               S01117
     C*                                                                   S01117
     C**  Get Bank Title by access object                                 S01117
     C*                                                                   S01117
     C                     CALL 'AOBANKR0'                                S01117
     C                     PARM '*MSG   ' @RTCD   7                       S01117
     C                     PARM '*FIRST ' @OPTN   7                       S01117
     C           SDBANK    PARM SDBANK    DSFDY                           S01117
      *
      **Error*in*ZSYSTM**********************                             S01117
      ***********                                                         S01117
     C************IN99     IFEQ '1'                                       S01117
     C           @RTCD     IFNE *BLANKS                                   S01117
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVE '1'       *INLR
     C                     MOVEL'SDBANKPD'DBFILE           ***************S01117
     C                     MOVEL@OPTN     DBKEY            ** DB ERROR 1 *S01117
     C                     Z-ADD1         DBASE            ***************S01117
     C                     MOVEL@OPTN     DBOPTN  7                       S01117
     C                     SETON                       U7U8               S01117
     C                     OUT  LDA                                       S01117
     C                     EXSR *PSSR                                     S01117
     C                     MOVE '1'       *INLR
     C                     ELSE
      *
      * ICD record found.
     C                     MOVE '1'       *IN40
     C                     END
     C                     END
      /EJECT
      *================================================================
      *
      ***  Main Processing
      *
     C           *IN40     IFEQ '1'
      *
      * At change of Br/Sec/Book/Mkt set up initial values of the
      *   accumulator fields -    1. Nominal - Value Date Basis (NPSN)
      *                           2. Ex-Coupon Niminal.         (ECNP)
      *
     C           *INL2     CASEQ'1'       L2DET
     C                     END
      *
      * For each event record add/sub event nominal to Nominal-Vdat basis.
      *  (NOTE : Trade type  EQ  TP : ADD,  TS : SUB.)
      *
      *   Set up output fields.
      *
      ***  Number of Events
     C                     ADD  1         IN01    50
      *
      *   Update : Nominal - Value Date Basis
      *
     C           TRTT      IFEQ 'TS'
     C                     SUB  TRON      NPSN
     C                     ELSE
     C           TRTT      IFEQ 'TP'
     C                     ADD  TRON      NPSN
     C                     END
     C                     END
      *
      *   Update : Ex-Coupon Nominal
      *
     C           TRED      IFEQ 'X'
     C           TRTT      IFEQ 'TS'
     C**********           ADD  TRON      ECNP                            E19101
     C                     SUB  TRON      ECNP                            E19101
     C                     END
      *
     C           TRTT      IFEQ 'TP'
     C**********           SUB  TRON      ECNP                            E19101
     C                     ADD  TRON      ECNP                            E19101
     C                     END
     C                     END
      *
     C                     END
      *
      *================================================================
      /EJECT
      *================================================================
      *
      ***  Total Time Processing
      *
      * Output Projected Positions record at value date change.
      *
     CL1         *INL1     IFEQ '1'
     CL1         *IN40     ANDEQ'1'
      *
     CL1                   ADD  1         OUTREC
     CL1                   MOVE 'D'       RECI
     CL1                   WRITEBKPOFDF
      *
     CL1                   END
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *                                                               *
     CLR                   EXSR SRLR
      *                                                               *
      *================================================================
      /EJECT
      *****************************************************************
      *                                                               *
      *  L2DET  - Subroutine to access Book Positions (Current) file  *
      *           and set up the initial values for the accumulator   *
      *           fields :  Nominal - Value Date Basis  (NPSN)        *
      *                     Ex-Coupon Nominal           (ECNP)        *
      *                                                               *
      *****************************************************************
      *
     C           L2DET     BEGSR                           *** L2DET  ***
      *
      * Zero the initial accumulator values.
     C                     Z-ADD0         NPSN
     C                     Z-ADD0         ECNP
      *
      * Access the Book Positions Header file  - LF/BKPHD
     C           KBKPHD    CHAINBKPHDDF              50
      *
      * Access the Book Positions (Current) file  - LF/BKPOS
     C           *IN50     IFEQ '0'
     C           KBKPOS    SETGTBKPOS
     C                     READPBKPOS                    50
      *
     C           #BKP      IFEQ XBKP
     C           *IN50     ANDEQ'0'
     C                     Z-ADDXNPSN     NPSN
     C                     Z-ADDXECNP     ECNP
     C                     END
      *
     C                     END
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRLR   - Subroutine for Control Processing                   *
      *                                                               *
      *****************************************************************
      *
     C           SRLR      BEGSR                           ***  SRLR  ***
      *
      * If no records read - Empty Primary File,
      ***access*the*I.C.D.*record*(PF/TABTB10)****************************S01117
      *  In the dtaara/rundat and get title by access object              S01117
      *
     C           *IN40     IFEQ '0'
     C************IN99     ANDEQ'0'                                       S01117
     C           @RTCD     ANDEQ*BLANKS                                   S01117
     C***********          EXSR ZSYSTM                                    S01117
     C*                                                                   S01117
     C**  In the dataarea rundat                                          S01117
     C*                                                                   S01117
     C                     IN   RUNDT                                     S01117
     C*                                                                   S01117
     C**  Check system date format, DDMMYY or MMDDYY.                     S01117
     C*                                                                   S01117
     C           DATF      COMP 'M'                      98               S01117
     C*                                                                   S01117
     C*                                                                   S01117
     C* Get Bank Title by access object                                   S01117
     C*                                                                   S01117
     C                     CALL 'AOBANKR0'                                S01117
     C                     PARM '*MSG   ' @RTCD   7                       S01117
     C                     PARM '*FIRST ' @OPTN   7                       S01117
     C           SDBANK    PARM SDBANK    DSFDY                           S01117
     C*                                                                   S01117
     C           @RTCD     IFNE *BLANKS                                   S01117
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVEL*BLANK    DBLOT                           S01117
     C                     MOVEL'SE1430'  DBPGM                           S01117
     C                     MOVEL'SDBANKPD'DBFILE           ***************S01117
     C                     MOVEL@OPTN     DBKEY            * DB ERROR 01 *S01117
     C                     Z-ADD2         DBASE            ***************S01117
     C                     OUT  LDA                                       S01117
     C                     MOVEL@OPTN     DBOPTN  7                       S01117
     C                     MOVEL'SE1430'  DBPGM                           S01117
     C                     SETON                       U7U8               S01117
     C                     EXSR *PSSR                                     S01117
     C                     END                                            S01117
     C                     END
      *
      * Write control report headings
      *
     C                     WRITEHEADAU
      *
      **Error*in*ZSYSTM***************************************************S01117
     C************IN99     IFEQ '1'                                       S01117
     C***********          MOVE '1'       *INU8                           S01117
     C***********          MOVE '1'       *INU7            ***************S01117
     C***********          MOVEL'01'      DBASE            **DB ERROR 01**S01117
     C***********          MOVEL'TABLE'   DBFILE           ***************S01117
     C***********          MOVELZTABKY    DBKEY                           S01117
     C*                                                                   S01117
     C           *INU7     IFEQ '1'                                       S01117
     C           *INU8     ANDEQ'1'                                       S01117
     C*                                                                   S01117
     C                     WRITEERROR
     C                     END
      *
      *  Access AUDIT record for Book Trade Events
      *
     C                     Z-ADD0         AUDIT1
     C                     READ TREVEAF                  50
      *
      *  Control Totals
     C                     WRITETOT01
      *
      *  WRITE Audit record
      *
     C                     Z-ADDOUTREC    NORE
     C                     WRITEBKPOFAF
      *
      ***IF*NO*OUTPUT/EMPTY*FILES*-*Print*a*message*to*this*effect********S01117
      ***********                                                         S01117
     C************IN40     IFEQ '0'                                       S01117
     C***********          WRITENONE                                      S01117
      ***********                                                         S01117
      *********ELSE***-*Print*Records*Written*Total***********************S01117
      ***********                                                         S01117
     C***********          ELSE                                           S01117
     C                     WRITECONTROL
     C***********          END                                            S01117
      *
      *  END OF PROGRAM
     C***********          WRITETRAILAU                                   S01117
      *
      ***If*Database*Errors*found,*update*LDA*****************************S01117
      ***********                                                         S01117
     C************NAMVAR   DEFN           LDA                             S01117
      ***********                                                         S01117
     C************INU8     IFEQ '1'                                       S01117
     C************LOCK     IN   LDA                                       S01117
     C***********          MOVELDBLOT     #DBLOT                          S01117
     C***********          OUT  LDA                                       S01117
     C***********          END                                            S01117
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT                                                              S01117
     C*****************************************************************   S01117
     C*                                                               *   S01117
     C* *PSSR - SUBROUTINE TO HANDLE ABNORMAL ERROR CONDITIONS        *   S01117
     C*                                                               *   S01117
     C* CALLED FROM : AFTER ABNORMAL OPERATION OCCURS                 *   S01117
     C*                                                               *   S01117
     C* CALLS : NOTHING                                               *   S01117
     C*                                                               *   S01117
     C*****************************************************************   S01117
     C*                                                                   S01117
     C           *PSSR     BEGSR                           ** *PSSR **    S01117
     C*                                                                   S01117
     C           @RUN      IFEQ *BLANKS                                   S01117
     C                     MOVE 'Y'       @RUN    1                       S01117
     C                     DUMP                                           S01117
     C                     END                                            S01117
     C*                                                                   S01117
     C                     ENDSR                                          S01117
     C********************************************************************S01117
      /EJECT
     C*COPY*ZSRSRC,ZSYSTMZ1                                               S01117
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
