     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Project Forward Depot Positions')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module
     F*                                                               *
     F*  SE1450 - PROJECT FORWARD DEPOT POSITIONS                     *
     F*                                                               *
     F*  Function: This program projects Forward Portfolio Depot      *
     F*   (COB)    Positions by Branch/Security/Book/Market/Date      *
     F*            using Forward-valued Trades (TREVED & PCTEVD) and  *
     F*            Forward-dated Depot Movements (DMEVED). Data is    *
     F*            held as for Current Positions, with only two fields*
     F*            projected: Nominal-Value Date Basis and Ex-Coupon  *
     F*            Nominal. The Current Position, if it exists, is    *
     F*            used as the starting point for these two balances. *
     F*                                                               *
     F*  Called by: SEC1106 - Project Forward Depot Positions.        *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*  LAST AMEND NO  063085             DATE 07MAY96               *
     F*  PREV AMEND NO. 052254             DATE 10JAN94               *
     F*                 E34034             DATE  7JAN92               *
     F*                 E29170             DATE 29OCT91               *
     F*                 S01117             DATE 04JUN91               *
     F*                 E19101             DATE 29JAN90               *
     F*                 E16323             DATE 09OCT89               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
     F*  063085 - POSITION NOT TO BE UPDATED FOR A 'RP', 'RR,         *
     F*           'PD', 'PL', 'BB', AND 'BL' EVENTS                   *
     F*  052254 - RECOMPILED as CURRENT FACTOR amended from 9,8       *
     F*           to 10,9                                             *
     F*  E34034 - Recompiled over changed printer file                *
     F*  E29170 - Report Control Facility changes                     *
     F*  S01117 - Multibranching                                      *
     F*  E19101 - Portfolio Customer Trade Events must be included in *   E19101
     F*           Forward Depot Positions.                            *   E19101
     F*         - Ex-Coupon Nominal should be added for a Trade       *   E19101
     F*           Purchase and subtracted for a Trade Sale.           *   E19101
     F*  E16323 - Incorrect calculation of Depot Forward Position.    *   E16323
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
      *
     FDPSEV   IPE E           K        DISK
     FDPOSN   IF  E           K        DISK
     FTABSE   IF  E           K        DISK
     F            TABLETAF                          KIGNORE
     F            TABLETHF                          KIGNORE
     F*********** TABLETJF                          KIGNORE               S01117
     F            TABLETLF                          KIGNORE
     F            TABLETKF                          KIGNORE
     F            TABLETNF                          KIGNORE
     F            TABLETPF                          KIGNORE
     F            TABLETRF                          KIGNORE
     F            TABLETXF                          KIGNORE
     F**ICD*1*****TABTB10F                          KIGNORE
     F            TABTB11F                          KIGNORE
     F            TABTB36F                          KIGNORE
     F            TABTB40F                          KIGNORE
     F            TABTE10F                          KIGNORE
     F            TABTE20F                          KIGNORE
     F            TABTG10F                          KIGNORE
     F            TABTG20F                          KIGNORE
     F            TABLET2F                          KIGNORE
     F            TABLET3F                          KIGNORE
     F            TABLET5F                          KIGNORE
     FTREVEA  IF  E                    DISK
     FDMEVEA  IF  E                    DISK
     FPCTEVA  IF  E                    DISK                               E19101
     FDPOSFD  O   E                    DISK
     F            DPOSNDF                           KRENAMEDPOSFDF
     FDPOSFA  O   E                    DISK
     FSE1450AUO   E                    PRINTER
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   L1  - Branch/Security/Depot/Date combination change         *
     F*   L2  - Branch/Security/Depot combination change              *
     F*                                                               *
     F*   11  - Trade Events File read                                *
     F*   12  - Depot Movements File read                             *
     F*   13  - Portfolio Customer Trade Events File read             *   E19101
     F*                                                               *
     F*   40  - Detail records read                                   *
     F*   50  - Work indicator                                        *
     F*                                                               *
     F* 90-99 - Used in Standard Subroutines                          *
     F*                                                               *
     F*   U7  - Database Error                                        *
     F*   U8  - Database / Control Error                              *
     F*                                                               *
     F*****************************************************************
      /SPACE 3
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E   I N D E X                              *
     F*                                                               *
     F*  L2DET  - Access Depot Positions (Current)                    *
     F*  SRLR   - Total Calcs Routine                                 *
     F*  *PSSR  - Error Handling Subroutine                           *   S01117
     F*                                                               *
     F*  ZSYSTM - Access ICD record                                   *
     F*                                                               *
     F*****************************************************************
      /EJECT
     E                    CPY@    1   1 80
      /EJECT
     ITREVEDF     11
     I***********   TRBN                            DSBR  L2              S01117
     I              TRBA                            DSBA  L2              S01117
     I              TRSS                            DSSC  L2
     I              TROD                            DDPT  L2
     I              TRVD                            DDTE  L1
     I              TRON                            EAMT
      *
     IDMEVEDF     12
     I***********   DMBR                            DSBR  L2              S01117
     I              DMBA                            DSBA  L2              S01117
     I              DMSS                            DSSC  L2
     I              DMDP                            DDPT  L2
     I              DMMD                            DDTE  L1
     I              DMAN                            EAMT
      *
     IPCTEVDF     13                                                      E19101
     I***********   TRBN                            DSBR  L2       E19101 S01117
     I              TRBA                            DSBA  L2              S01117
     I              TRSS                            DSSC  L2              E19101
     I              TROD                            DDPT  L2              E19101
     I              TRVD                            DDTE  L1              E19101
     I              TRON                            EAMT                  E19101
      *
     IDPOSNDF
     I***********   DSBR                            XDSBR                 S01117
     I              DSBA                            XDSBA                 S01117
     I              DSSC                            XDSSC
     I              DDPT                            XDDPT
     I              DDTE                            XDDTE
     I              DSNT                            XDSNT
     I**************DSNV                            XDSNV
     I              DSRT                            XDSRT
     I              DSRV                            XDSRV
     I              DSNS                            XDSNS
     I**************DEXN                            XDEXN
     I              DSCT                            XDSCT
     I              DSCV                            XDSCV
      *
     ITREVEAF
     I              NORE                            AUDIT1
     IDMEVEAF
     I              NORE                            AUDIT2
     IPCTEVAF                                                             E19101
     I              NORE                            AUDIT3                E19101
      *
      ***  Local Data Area for DB Error Information
     I*LDA********UDS                            256                      S01117
     I***********                           134 177 #DBLOT                S01117
     ILDA         DS                            256                       S01117
     I                                      134 183 DBLOT                 S01117
     I                                      134 141 DBFILE                S01117
     I                                      142 170 DBKEY                 S01117
     I                                      171 180 DBPGM                 S01117
     I                                      181 1830DBASE                 S01117
     I***********                           134 177 DBLOT                 S01117
     I***********                           134 138 DBFILE                S01117
     I***********                           139 167 DBKEY                 S01117
     I***********                           168 175 DBPGM                 S01117
     I***********                           176 177 DBASE                 S01117
     I*
     I           SDS
     I                                      244 246 WSID
     I                                      254 263 USID
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     C/EJECT
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***  Initial Processing
      *
      * Access the Depot Positions (Current) file  - LF/DPOSN
      *
     C           KDPOSN    KLIST
     C***********          KFLD           DSBR                            S01117
     C                     KFLD           DSBA                            S01117
     C                     KFLD           DSSC
     C                     KFLD           DDPT
      *
     C           *IN40     IFEQ '0'
      *
      * Prepare LDA for the database error output
      *
     C           *NAMVAR   DEFN           LDA                             S01117
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVEL*BLANK    DBLOT
     C                     MOVEL'SE1450'  DBPGM
     C                     OUT  LDA                                       S01117
      *
      * Access the I.C.D. record (PF/TABTB10)
      *
     C                     EXSR ZSYSTM
      *
      * Error in ZSYSTM
     C           *IN99     IFEQ '1'
     C                     MOVE '1'       *INLR
     C                     ELSE
      *
      * ICD record found.
     C                     MOVE '1'       *IN40
     C                     END
     C                     END
      *
      *================================================================
      /EJECT
      *================================================================
      *
      ***  Main Processing
      *
     C           *IN40     IFEQ '1'
      *
      * At change of Br/Sec/Depot set up initial values of the
      *   accumulator fields :
      *    1. Nominal - Value Date Basis (DSNV)
      *      2. Ex-Coupon Nominal.         (DEXN)
      *
     C           *INL2     CASEQ'1'       L2DET
     C                     END
      *
      *
      * For each event record add/sub event nominal to Nominal-Vdat basis.
      *
      ***(NOTE*:*Trade*Event****-*Trade*Type**EQ**TP*:*sub,**TS*:*add.)   E19101
      *  (NOTE : Trade Event    - Trade Type  EQ  TS : sub,  TP : add.)   E19101
      *  (       P.Cust Event   - Trade Type  EQ  TP : sub,  TS : add.)   E19101
      *  (       Depot Movement - I/O Ind.    EQ  O  : sub,  I  : add.)
      *
      ***  Add Event Amount
     C                     MOVE '+'       #ADD    1
      *
     C           *IN11     IFEQ '1'
     C           TRTT      ANDEQ'TS'                                      E16323
     C***********TRTT      ANDEQ'TP'                                      E16323
      *
     C           *IN12     OREQ '1'
     C           DMIO      ANDEQ'O'
      *
     C           *IN13     OREQ '1'                                       E19101
     C           TRTT      ANDEQ'TP'                                      E19101
      *
      ***  Subtract Event Amount
     C                     MOVE ' '       #ADD
     C                     END
      *
      *   Set up output fields.
      *
      *   Keep 'Records read' count.
      *
      ***  Trade Events
     C           *IN11     IFEQ '1'
     C                     ADD  1         IN01    50
     C                     ELSE
      *
      ***  Depot Movement Events
     C           *IN12     IFEQ '1'                                         9101
     C                     ADD  1         IN02    50
     C                     ELSE                                           E19101
      *
      ***  Portfolio Customer Events
     C                     ADD  1         IN03    50                      E19101
     C                     END                                            E19101
     C                     END
      *
      ***  Event amounts to be zero for events RP, RR, PD, PL, BB, and BL.063085
      *                                                                   063085
     C           *IN12     IFEQ '1'                                       063085
     C           DMMT      IFEQ 'RP'                                      063085
     C           DMMT      OREQ 'RR'                                      063085
     C           DMMT      OREQ 'PD'                                      063085
     C           DMMT      OREQ 'PL'                                      063085
     C           DMMT      OREQ 'BB'                                      063085
     C           DMMT      OREQ 'BL'                                      063085
     C                     Z-ADD*ZERO     EAMT                            063085
     C                     END                                            063085
     C                     END                                            063085
      *
      *   Update : Nominal - Value Date Basis
      *
     C           #ADD      IFEQ '+'
     C                     ADD  EAMT      DSNV
     C                     ELSE
     C                     SUB  EAMT      DSNV
     C                     END
      *
      *   Update : Ex-Coupon Nominal  (Trade Events only)
      *
     C           *IN11     IFEQ '1'
     C           TRED      ANDEQ'X'
     C           *IN13     OREQ '1'                                       E19101
     C           TRED      ANDEQ'X'                                       E19101
      *
     C           #ADD      IFEQ '+'
     C                     ADD  EAMT      DEXN
     C                     ELSE
     C                     SUB  EAMT      DEXN
     C                     END
     C                     END
      *
     C                     END
      *
      *================================================================
      /EJECT
      *================================================================
      *
      ***  Total Time Processing
      *
      * Output Projected Positions record at value date change.
      *
     CL1         *INL1     IFEQ '1'
     CL1         *IN40     ANDEQ'1'
      *
     CL1                   ADD  1         OUTREC
     CL1                   MOVE 'D'       RECI
     CL1                   WRITEDPOSFDF
     CL1                   END
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *                                                               *
     CLR                   EXSR SRLR
      *                                                               *
      *================================================================
      /EJECT
      *****************************************************************
      *                                                               *
      *  L2DET  - Subroutine to access Depot Positions (Current) file *
      *           and set up the initial values for the accumulator   *
      *           fields :  Nominal - Value Date Basis  (DSNV)        *
      *                     Ex-Coupon Nominal           (DEXN)        *
      *                                                               *
      *****************************************************************
      *
     C           L2DET     BEGSR                           *** L2DET  ***
      *
      * Zero the initial accumulator values.
      *
     C                     Z-ADD0         DSNV
     C                     Z-ADD0         DEXN
      *
      * Access the Depot Positions (Current) file  - LF/DPOSN
      *
     C           KDPOSN    CHAINDPOSN                50
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRLR   - Subroutine for Control Processing                   *
      *                                                               *
      *****************************************************************
      *
     C           SRLR      BEGSR                           ***  SRLR  ***
      *
      * If no records read - Empty Primary File,
      *  access the I.C.D. record (PF/TABTB10)
      *
     C           *IN40     IFEQ '0'
     C           *IN99     ANDEQ'0'
     C                     MOVEL*BLANK    DBLOT                           S01117
     C                     MOVEL'SE1450'  DBPGM                           S01117
     C                     EXSR ZSYSTM
     C                     END
      *
      * Write control report headings
      *
     C                     WRITEHEADAU
      *
      * Error in ZSYSTM
     C           *IN99     IFEQ '1'
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INU7            ***************
     C***********          MOVEL'01'      DBASE            * DB ERROR 01 *S01117
     C                     Z-ADD1         DBASE            * DB ERROR 01 *S01117
     C                     MOVEL'TABLE'   DBFILE           ***************
     C                     MOVELZTABKY    DBKEY
     C                     OUT  LDA                                       S01117
     C                     EXSR *PSSR                                     S01117
     C                     WRITEERROR
      *
     C                     END
      *
     C                     Z-ADD0         AUDIT1
     C                     Z-ADD0         AUDIT2
     C                     Z-ADD0         AUDIT3                          E19101
      *
      *  Access AUDIT records
      *
     C                     READ TREVEAF                  50
     C                     READ DMEVEAF                  50
     C                     READ PCTEVAF                  50               E19101
      *
      *  Compare Control Totals
      *
     C                     WRITETOT01
     C                     WRITETOT02
      *
      *  WRITE Audit record
      *
     C                     Z-ADDOUTREC    NORE
     C                     WRITEDPOSFAF
      *
      ***IF*NO*OUTPUT/EMPTY*FILES*-*Print*a*message*to*this*effect*****   E29170
      ***********                                                         E29170
     C************IN40     IFEQ '0'                                       E29170
     C***********          WRITENONE                                      E29170
      ***********                                                         E29170
      **************ELSE*-*Print*Records*Written*Total*****************   E29170
      ***********                                                         E29170
     C***********          ELSE                                           E29170
     C                     WRITECONTROL
     C***********          END                                            E29170
      *
      *  END OF PROGRAM
     C***********          WRITETRAILAU                                   E29170
      ***********                                                         E29170
      ***If*Database*Erorrs*found,*update*LDA*****************************S01117
      ***********                                                         S01117
     C************NAMVAR   DEFN           LDA                             S01117
      ***********                                                         S01117
     C************INU8     IFEQ '1'                                       S01117
      ***********                                                         S01117
     C************LOCK     IN   LDA                                       S01117
     C***********          MOVELDBLOT     #DBLOT                          S01117
     C***********          OUT  LDA                                       S01117
     C***********          END                                            S01117
      *
     C                     ENDSR
      *
      *****************************************************************   S01117
      /EJECT                                                              S01117
      *****************************************************************   S01117
      *                                                               *   S01117
      *  *PSSR  - Error control subroutine                            *   S01117
      *                                                               *   S01117
      *****************************************************************   S01117
      *                                                                   S01117
     C           *PSSR     BEGSR                           ** *PSSR **    S01117
     C**                                                                  S01117
     C           @RUN      IFEQ *BLANKS                                   S01117
     C                     MOVE 'Y'       @RUN    1                       S01117
     C                     DUMP                                           S01117
     C                     END                                            S01117
     C**                                                                  S01117
     C                     ENDSR                                          S01117
      *                                                                   S01117
      *****************************************************************
      /EJECT
     C/COPY ZSRSRC,ZSYSTMZ1
      /EJECT
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
