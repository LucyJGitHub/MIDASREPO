     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Gen past due depot position events')          *
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module                            *
     F*                                                               *
     F*  SE1465 - Generate Past Due Depot Position Events             *
     F*                                                               *
     F*  Function:  This program will read through the position       *
     F*    (COB)    settlements file, and then generate depot position*
     F*             events for live and unsettled records with value  *
     F*             date on or before the event control date          *
     F*             control date.                                     *
     F*                                                               *
     F*  Called By: SEC1117 - SE Project Depot Position Events        *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      *                 241795             Date 04Sep06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 187848             Date 04Jan01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU011             Date 11Mar98               *
      *                 088939 *Create     Date 19Aug96               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *           (Recompile)                                         *
      *  241795 - Populate ECSS whether CEU011 is switched on or off. *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  187848 - Duplicate records for Treaty tax security.          *
     F*  CEU011 - Position/Risk Reporting                             *
     F*  088939 - Generate past due events                            *
     F*                                                               *
     F*****************************************************************
     F*
     FPOSETDL1IF  E           K        DISK
     FSDCURRL1IF  E           K        DISK                               CEU011
      **  SD Currency Codes Update                                        CEU011
      *                                                                   CEU011
     FDPEVED  O   E                    DISK                      A
     FDPEVEA  UF  E                    DISK
     FSE1465AUO   E                    PRINTER
     F*
     F*****************************************************************
     F*                                                               *
     F*     F U N C T I O N   O F   I N D I C A T O R S               *
     F*                                                               *
     F*    80            End of file on POSETDL1                      *
     F*    81            End of file on DPEVEA                        *
     F*    82            Audit header already written                 *
     F*    U7-U8         Databaser error                              *
     F*    LR            End of program                               *
     F*                                                               *
     F*****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
     E** Array containing Copyright statement
     E                    CUR       200  3                                CEU011
     E                    SRT       200  2 0                              CEU011
     E/COPY ZSRSRC,ZHOLE
     I/EJECT
     I*
     ILDA         DS                            256
     I** Local data area data structure
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     IRUNDT       DS                             13
     I** Data area RUNDAT
     I                                       13  13 MBIN
     I*
     ISDBANK    E DSSDBANKPD
     I** Bank details
     I*
     ISDBRCH    E DSSDBRCHPD
     I** Branch details
     I*
     ISDCUST    E DSSDCUSTPD
     I              QQDFAC                          #DFAC1                                    CGL029
     I** Customer details
     I*
     ISDGELR    E DSSDGELRPD
     I** General ledger details
     I*
     ISCSARD    E DSSCSARDPD                                              CEU011
      **  Switchable Features File                                        CEU011
      *                                                                   CEU011
     IDSFDY     E DSDSFDY
     I** Short data structure
     I*
     IDSSDY     E DSDSSDY
     I** Long data structure
     I/COPY ZSRSRC,ZHOLI
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  S U B R O U T I N E    I N D E X                             *
     C*                                                               *
     C*  Main routine                                                 *
     C*  #LINIT -  Initial processing                                 *
     C*  #LOUTR -  Write record to depot position events              *
     C*  #LDEPP -  Setup record to add to depot position events       *
     C*  #LGBDT -  Get branch details from SDBRCHPD                   *
     C*  #LGCDT -  Get customer details from SDCUSTPD                 *
     C*  #LHASH -  Add events amounts to hash totals                  *
     C*  #LAUDP -  Audit processing                                   *
     C*  *PSSR  -  Error handling subroutine                          *
     C*  ZEVCD  -  Calculate Event Control Date                       *
     C*                                                               *
     C*****************************************************************
     C/SPACE 3
     C*****************************************************************
     C*             S T A R T   O F   P R O G R A M                   *
     C*****************************************************************
     C*
     C** Perform initial processing
     C*
     C                     EXSR #LINIT
     C*
     C** Write record to the appropriate file
     C*
     C                     EXSR #LOUTR
     C*
     C** Perform audit processing
     C*
     C                     EXSR #LAUDP
     C*
     C** End the program and return to calling program
     C*
     C                     SETON                     LR
     C                     RETRN
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C* #LINIT - Initial Processing                                   *
     C* Called from : Main process                                    *
     C* Calls : None                                                  *
     C*                                                               *
     C*****************************************************************
     C*
     C           #LINIT    BEGSR
     C*
     C** Set up copyright parameter
     C*
     C                     MOVEACPY@      ACT@   80
     C*
     C** Setup LDA for possible DB error processing later
     C*
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     Z-ADD0         DBASE
     C                     MOVEL'SE1465'  DBPGM
     C                     OUT  LDA
     C*
     C** In RUNDAT
     C*
     C           *NAMVAR   DEFN RUNDAT    RUNDT
     C                     IN   RUNDT
     C*
     C** Get bank details
     C*
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C*
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE            ***************
     C                     MOVEL'SDBANKPD'DBFILE           * DB ERROR 01 *
     C                     MOVEL'*FIRST'  DBKEY            ***************
     C                     SETON                     U7U8LR
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C*
     C** Get general ledger details
     C*
     C**********           CALL 'AOGELRR0'                                                    CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C********** SDGELR    PARM SDGELR    DSFDY                                               CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
     C*
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE            ***************
     C                     MOVEL'SDGELRPD'DBFILE           * DB ERROR 02 *
     C                     MOVEL'*FIRST'  DBKEY            ***************
     C                     SETON                     U7U8LR
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *                                                                   CEU011
     C                     CALL 'AOSARDR0'                                CEU011
     C                     PARM *BLANKS   WRTCD   7                       CEU011
     C                     PARM '*VERIFY' WOPTN   7                       CEU011
     C                     PARM 'CEU011'  WSARD   6                       CEU011
     C           SCSARD    PARM SCSARD    DSFDY                           CEU011
      *                                                                   CEU011
     C           WRTCD     IFEQ *BLANKS                                   CEU011
     C                     MOVE 'Y'       CEU011  1                       CEU011
     C                     ELSE                                           CEU011
     C                     MOVE 'N'       CEU011  1                       CEU011
     C                     ENDIF                                          CEU011
      *                                                                   CEU011
     C           WRTCD     IFNE *BLANKS                                   CEU011
     C           WRTCD     ANDNE'*NRF   '                                 CEU011
     C                     MOVEL'CEU011'  DBKEY            ************** CEU011
     C                     MOVE 'SCSARDPD'DBFILE           * DB ERROR 07* CEU011
     C                     MOVEL'007'     DBASE            ************** CEU011
     C                     SETON                     U7U8LR               CEU011
     C                     EXSR *PSSR                                     CEU011
     C                     ENDIF                                          CEU011
      *                                                                   CEU011
     C********** CEU011    IFEQ 'Y'                                                    CEU011 241795
     C           *LOVAL    SETLLSDCURRL1                                  CEU011
     C                     READ SDCURRL1                 83               CEU011
     C                     MOVE '0'       *IN83                           CEU011
     C                     Z-ADD1         X                               CEU011
     C           *IN83     DOWEQ'0'                                       CEU011
     C                     MOVE A6CYCD    CUR,X                           CEU011
     C                     MOVE A6SSNB    SRT,X                           CEU011
     C                     ADD  1         X       30                      CEU011
     C                     READ SDCURRL1                 83               CEU011
     C                     ENDDO                                          CEU011
     C**********           ENDIF                                                       CEU011 241795
     C*
     C** Calculate the event control date
     C*
     C                     Z-ADDBJDNWD    ZZDNWD
     C                     Z-ADDBKAPDT    ZZAPDT
     C                     EXSR ZEVCD
     C           ECD       SETLLPOSETDL1
     C*
     C** Initialise field counters
     C*
     C                     Z-ADD0         WDNORE  50
     C                     Z-ADD0         RPOSR
     C                     Z-ADD0         RDEPP
     C*
     C** Write audit header
     C*
     C                     MOVE BJURPT    RTITL
     C                     MOVE BJMRDT    RDATE
     C                     Z-ADD1         RPAGE
     C                     WRITEHEADAU
     C                     MOVE '1'       *IN82
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C* #LOUTR - Write record to depot position events file           *
     C* Called from : Main process                                    *
     C* Calls : #LDEPP - Setup record to add to depot posn events     *
     C*                                                               *
     C*****************************************************************
     C*
     C           #LOUTR    BEGSR
     C*
     C** Process every record with due date on or before event control  date
     C*
     C                     READ POSETDL1                 80
     C           *IN80     DOWEQ'0'
     C           PDUD      ANDLEECD
      *                                                                   187848
      ** Only Process Non-Treaty tax position settlement                  187848
      *                                                                   187848
     C           CRTX      IFEQ *BLANK                                    187848
     C*
     C** Increment records read counter
     C*
     C                     ADD  1         RPOSR
     C*
     C** Process only those settled through nostro and with date
     C** generated not equal to run date
     C*
     C           PSMT      IFEQ 01
     C           PSMT      OREQ 02
     C           PSMT      OREQ 07
     C           PSMT      OREQ 08
     C           PSMT      OREQ 12
     C*
     C           PDTG      IFNE BJRDNB
     C*
     C                     MOVE 'D'       RECI
     C                     MOVE PBCA      CCBA
     C                     MOVE PSSH      CCSS
     C                     Z-ADDPDUD      CCDT
     C                     MOVE PSCU      CCEC
     C                     Z-ADDPSAT      CCEA
     C                     Z-ADDPAMD      CCNA
     C                     Z-ADDPNMP      CCNP
     C                     MOVE PNCY      CCNC
     C                     Z-ADD0         CCEP
     C                     Z-ADDPSMT      CCSM
     C                     MOVE PSEA      CCSA
     C                     MOVE PSEB      CCTA
     C                     MOVE *BLANKS   ORBR
     C                     MOVE *BLANKS   OSBR
     C                     MOVE *BLANKS   COOB
     C                     Z-ADD0         CCAS
     C                     Z-ADD0         CCAN
     C                     MOVE '5'       CCTC
      *                                                                   CEU011
     C********** CEU011    IFEQ 'Y'                                                    CEU011 241795
     C                     Z-ADD1         X                               CEU011
     C           CCEC      LOKUPCUR,X                    84               CEU011
     C           *IN84     IFEQ '0'                                       CEU011
     C           *LOCK     IN   LDA                                       CEU011
     C                     Z-ADD6         DBASE            ***************CEU011
     C                     MOVEL'SDCURRL1'DBFILE           * DB ERROR 06 *CEU011
     C                     MOVELCCEC      DBKEY            ***************CEU011
     C                     SETON                     U7U8LR               CEU011
     C                     OUT  LDA                                       CEU011
     C                     EXSR *PSSR                                     CEU011
     C                     ELSE                                           CEU011
     C                     MOVE SRT,X     ECSS    20                      CEU011
     C                     ENDIF                                          CEU011
     C**********           ENDIF                                                       CEU011 241795
     C*
     C           PEVT      IFEQ 'MT'
     C                     MOVE 'MA'      CCET
     C                     MOVE 'F'       MDTI
     C                     ELSE
     C                     MOVE PEVT      CCET
     C                     MOVE *BLANKS   MDTI
     C                     ENDIF
     C*
     C** Setup interbranch fields
     C*
     C                     MOVE *BLANKS   EXEI
     C                     MOVE 'B'       ELVB
     C                     MOVE 'C'       ELVC
     C                     MOVE 'S'       ELVS
     C                     MOVE 'N'       IBRE
     C                     MOVE 'Y'       ICYE
     C*
     C** Set up company of booking branch
     C*
     C                     MOVELCCBA      WBRCH
     C                     EXSR #LGBDT
     C                     MOVE A8CMCD    COBB
     C*
     C** For the first event, booking and event branch are the same.  Likewise,
     C** company of booking branch is the same as the event company
     C*
     C                     MOVE CCBA      EBRC
     C                     MOVE COBB      ECPY
     C*
     C** Set up depot, report name and town
     C*
     C**********           Z-ADDPCPY      CCDP                                                CSD027
     C                     MOVE PCPY      CCDP                                                CSD027
     C                     EXSR #LGCDT
     C                     MOVELBBCRNM    CCDN
     C                     MOVELBBCRTN    CCTW
     C*
     C** Set up event code
     C*
     C                     SELEC
     C           CCET      WHEQ 'CP'
     C                     MOVE '31D2'    CCCE
     C           CCET      WHEQ 'DV'
     C                     MOVE '31D3'    CCCE
     C           CCET      WHEQ 'MA'
     C                     MOVE '31D5'    CCCE
     C                     ENDSL
     C*
     C** Set up due to or from depot field
     C*
     C           PPRE      IFEQ 'P'
     C                     MOVE 'T'       CCFD
     C                     MOVE 'O'       CCIO
     C                     Z-SUBCCNA      CCNA
     C                     Z-SUBCCEA      CCEA
     C                     ELSE
     C                     MOVE 'F'       CCFD
     C                     MOVE 'I'       CCIO
     C                     ENDIF
     C*
     C** Setup payment date
     C*
     C           CCET      IFEQ 'CP'
     C           CCET      OREQ 'MA'
     C                     Z-ADDCCDT      ZDAYNO                          059752
     C                     Z-ADD0         ZNRDYS                          S01512
     C                     MOVE CCEC      ZCCY                            059752
     C                     EXSR ZFWDT                                     S01512
     C                     Z-ADDZDYNBR    CCPD                            S01512
     C                     ELSE
     C                     Z-ADDPDUD      CCPD
     C                     ENDIF
     C*
     C** In a multibranching system, if booking and settle branches are
     C** different, get and save settlement company
     C*
     C           MBIN      IFEQ 'Y'
     C           CCBA      ANDNECCTA
     C                     MOVELCCTA      WBRCH
     C                     EXSR #LGBDT
     C                     MOVE A8CMCD    WSECY   3
     C                     MOVE A8BICN    WBICN   6
     C*
     C** For the 1st interbranch event, clear settlement account and branch
     C*
     C                     MOVE *BLANKS   CCSA
     C                     Z-ADD0         CCSM
     C                     ENDIF
     C*
     C** Add the record to the depot position events file
     C*
     C                     Z-ADDCCEA      #HAMT
     C                     EXSR #LHASH
     C                     WRITEDPEVEDF
     C                     ADD  1         RDEPP
     C*
     C** Generate second and third event records if interbranch event
     C** These are the two events for the settlement branch
     C*
     C           CCBA      IFNE CCTA
     C           CCTA      ANDNE*BLANKS
     C*
     C** Second record, between settlement branch and depot
     C*
     C                     MOVE CCTA      EBRC
     C                     MOVE WSECY     ECPY
     C                     MOVE 'Y'       IBRE
     C*
     C           COBB      IFNE WSECY
     C                     MOVE 'Y'       ICYE
     C                     ELSE
     C                     MOVE 'N'       ICYE
     C                     ENDIF
     C*
     C                     Z-ADDPSMT      CCSM
     C                     MOVE PSEA      CCSA
     C*
     C** Write second record to the depot position events file
     C*
     C                     Z-ADDCCEA      #HAMT
     C                     EXSR #LHASH
     C                     WRITEDPEVEDF
     C                     ADD  1         RDEPP
     C*
     C** Third record, between settlement branch and booking branch
     C** Switch the incoming/outgoing indicator
     C*
     C           CCIO      IFEQ 'I'
     C                     MOVE 'O'       CCIO
     C                     ELSE
     C                     MOVE 'I'       CCIO
     C                     ENDIF
     C*
     C** Depot is now the settlement branch internal customer number
     C*
     C                     MOVE WBICN     CCDP
     C*
     C** Clear the settlement account and method
     C*
     C                     Z-ADD0         CCSM
     C                     MOVE *BLANKS   CCSA
     C*
     C** Write third record to the depot position events file
     C*
     C                     Z-ADDCCEA      #HAMT
     C                     EXSR #LHASH
     C                     WRITEDPEVEDF
     C                     ADD  1         RDEPP
     C*
     C                     ENDIF
     C*
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF                                          187848
     C*
     C** Read next available record
     C*
     C                     READ POSETDL1                 80
     C                     ENDDO
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C* #LGBDT - Get branch details from SDBRCHPD                     *
     C* Called from : #LOUTR - Write record to output file            *
     C*               #LDEPP - Setup record to add to depot positions *
     C* Calls : None                                                  *
     C*                                                               *
     C*****************************************************************
     C*
     C           #LGBDT    BEGSR
     C*
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*KEY   ' WOPTN   7
     C                     PARM           WBRCH   3
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
     C*
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD3         DBASE            ***************
     C                     MOVEL'SDBRCHPD'DBFILE           * DB ERROR 03 *
     C                     MOVELWBRCH     DBKEY            ***************
     C                     SETON                     U7U8LR
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C* #LGCDT - Get customer details from SDCUSTPD                   *
     C* Called from : #LOUTR - Write record to output file            *
     C* Calls : None                                                  *
     C*                                                               *
     C*****************************************************************
     C*
     C           #LGCDT    BEGSR
     C*
     C                     MOVELPCPY      @KEY1
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM           @KEY1  10
     C                     PARM *BLANKS   @KYST   7
     C           SDCUST    PARM SDCUST    DSSDY
     C*
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD4         DBASE            ***************
     C                     MOVEL'SDCUSTPD'DBFILE           * DB ERROR 04 *
     C                     MOVELPCPY      DBKEY            ***************
     C                     SETON                     U7U8LR
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C* #LHASH - Subroutine to add Event Amounts To Hash Totals       *
     C*          (Determine Dec. Posn. and ADD as Positive Values)    *
     C* Called from : #LDEPP - Setup record to add to depot positions *
     C* Calls : None                                                  *
     C*                                                               *
     C*****************************************************************
     C*
     C           #LHASH    BEGSR
     C*
     C** Set up fields to be hashed as positive
     C*
     C           #HAMT     IFGE 0
     C                     Z-ADD#HAMT     #HAMT  150
     C                     ELSE
     C                     Z-SUB#HAMT     #HAMT
     C                     ENDIF
     C*
     C** Split up the hash amount as  Integer & Decimal parts
     C*
     C                     MOVEL#HAMT     #HI    120
     C                     MOVE #HAMT     #HD     30
     C*
     C** Update the depot position trailer file
     C*
     C                     ADD  1         WDNORE
     C                     ADD  #HD       HRDC
     C           HRDC      IFLT #HD
     C                     ADD  1         #HI
     C                     ENDIF
     C                     ADD  #HI       HRWN
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C* #LAUDP - Audit Processing                                     *
     C* Called from : Main processing                                 *
     C* Calls : None                                                  *
     C*                                                               *
     C*****************************************************************
     C*
     C           #LAUDP    BEGSR
     C*
     C** If records are added to depot events file, update trailer file
     C*
     C           WDNORE    IFNE 0
     C                     READ DPEVEA                   81
     C*
     C** Database error if no record in audit file
     C*
     C           *IN81     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     Z-ADD5         DBASE            ***************
     C                     MOVEL'DPEVEA'  DBFILE           * DB ERROR 05 *
     C                     MOVEL'*FIRST'  DBKEY            ***************
     C                     SETON                     U7U8LR
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ELSE
     C*
     C                     ADD  WDNORE    NORE
     C                     UPDATDPEVEAF
     C                     ENDIF
     C                     ENDIF
     C*
     C** Write audit controls
     C*
     C                     WRITECNTRLAU
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C* *PSSR - Subroutine to handle abnormal error conditions        *
     C* Called from : After abnormal operation occurs                 *
     C* Calls : None                                                  *
     C*                                                               *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR
     C*
     C** If header not written yet, write first before the db error msg
     C*
     C           *IN82     IFEQ '0'
     C                     MOVE BJURPT    RTITL
     C                     MOVE BJMRDT    RDATE
     C                     Z-ADD1         RPAGE
     C                     WRITEHEADAU
     C                     ENDIF
     C                     WRITEERRORAU
     C*
     C           @RUN      IFEQ *BLANKS
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     ENDIF
     C*
     C                     SETON                     U7U8LR
     C                     RETRN                                          CEU011
     C*
     C                     ENDSR
     C/EJECT
     C/COPY ZSRSRC,ZACCH
     C/EJECT
     C/COPY ZSRSRC,ZEVCDZ1
     C/EJECT
     C/COPY ZSRSRC,ZFWDT
**  CPY@
(c) Finastra International Limited 2001
