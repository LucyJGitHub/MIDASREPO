     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Depot movements extract for confirmations')   *
      *****************************************************************
      *                                                               *
      *  Midas - Securities and Trading Module                        *
      *                                                               *
      *  SE1876 - SE Depot Movements Extract for Confirmations        *
      *                                                               *
      *  Function:  This program will extract information from depo   *
      *  (IC)       movements and output the data to PF/MGF51NPD.     *
      *                                                               *
      *                                                               *
      *  Called By: SEC0304 - Input Cycle Confirmation Print          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD054797           Date 11Feb20               *
      *  Prev Amend No. MD046248           Date 05Feb18               *
      *                 CDL099             Date 06Oct17               *
      *                 MD034387           Date 25Jan16               *
      *                 MD034776           Date 09Jul15               *
      *                 CGL165             Date 15Feb17               *
      *                 CDL096             Date 22Sep14               *
      *                 CDL094             Date 11Jun14               *
      *                 AR1067108          Date 11Dec12               *
      *                 AR1001183          Date 20Nov12               *
      *                 AR589197           Date 11Aug10               *
      *                 AR580937           Date 26Jul10               *
      *                 AR581532           Date 21Jul10               *
      *                 AR549800           Date 11Jun10               *
      *                 CSW210             Date 04May10               *
      *                 CER059             Date 19Jul10               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      *                 243124A            Date 28Feb07               *
      *                 234140             Date 26Oct06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CDL049             Date 11May06               *
      *                 CSD027             Date 09Dec05               *
      *                 CDL038             Date 10May05               *
      *                 237063             Date 20Nov05               *
      *                 BUG7411            Date 01Jun05               *
      *                 CSE071             Date 19Jul05               *
      *                 CDL028             Date 07Feb05               *
      *                 229857             Date 07Oct04               *
      *                 229544             Date 15Sep04               *
      *                 226570             Date 27Jun04               *
      *                 BUG3122            Date 17Jul04               *
      *                 CDL020             Date 03Feb04               *
      *                 CDL019             Date 03Feb04               *
      *                 CDL018             Date 03Feb04               *
      *                 CDL017             Date 03Feb04               *
      *                 CDL016             Date 03Feb04               *
      *                 CGL029             Date 01Sep03               *
      *                 222727             Date 05Nov03               *
      *                 194370             Date 12Jul01               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 196380             Date 15May02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CPK015             Date 17Jun01               *
      *                 CSW025             Date 26Mar02               *
      *                 175115             Date 01Oct01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CSE023             Date 12Jul00               *
      *                 CDL008             Date 02May00               *
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CSE015             Date 06Dec99               *
      *                 CSE017             Date 20Oct99               *
      *                 CSE012             Date 16Aug99               *
      *                 CSE006             DATE 06Sep99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 151945             DATE 06Jan99               *
      *                 140428             Date 06Aug98               *
      *                 CSW014             Date 22Jun98               *
      *                 CSW098  *CREATE    Date 01MaY98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD054797 - SEC7560 failed - DATABASE ERROR in file DPTMVDJ2, *
      *             in SE1876, at 006. Fix is to only process records *
      *             from DPTMVDJ2/J3 if DPTMVD.DCFR='Y'.              *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL099 - Split Value Date (Recompile)                        *
      *  MD034387 - Recompiled over changes made in /COPY ZDYYRZ3     *
      *  MD034776 - Reverse AR1001183A to allow negative interest     *
      *             rate. (Recompile)                                 *
      *  CGL165 - Dual Withholding Tax (Recompile)                    *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *           (Recompile)                                         *
      *  CDL094 - Enhance  Receive Settlement Instructions            *
      *           (Recompiled)                                        *
      *  AR1067108 - PMC1140 Component failed during Clean COB        *
      *              Applied fix AR1001183A                           *
      *  AR1001183A - Coupon rate was still used in computation of    *
      *              interest instead of using the details in FRN tab *
      *              (Reopen) (Recompile)                             *
      *  AR589197 - The input validation of //xx has been removed if  *
      *             ISO15022 is switched on, it should be reapplied   *
      *             as the code is still used                         *
      *  AR580937 - Correct formatting of Field 97A tag               *
      *  AR581532 - Field tag 97A CASH included the slash after       *
      *             invalid account as output in MT515 and MT518      *
      *  AR549800 - Introduce IBAN flag to PF/MGF51NPD                *
      *  CSW210 - SWIFT 2010 Changes                                  *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *           (Recompile)                                         *
      *  243124A - DBase error caused by REPO with deal not yet       *
      *            authorised.                                        *
      *  234140 - EU witholding tax                                   *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  237063 - EU Tax fixes upgrade to MP build 103 (Recompile).   *
      *  BUG7411- CDL028 fields missing from DLGHISPD (Recompile)     *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  CDL028 - Automatic Rate Interpolations (Recompile)           *
      *  229857 - Recompiled due to change in ZYLDZ2.                 *
      *  229544 - Recompiled due to inserted fields in DLCHISPD.      *
      *  226570 - Recompiled due to change in ZYLDZ2.                 *
      *  BUG3122 - Recompiled due to changes in SDSECSPD.             *
      *  CDL020 - Apply Base Rate at Value Date (Recompile)           *
      *  CDL019 - Allow Base Rate Changes on Fixed Deposits/Loans     *
      *           (Recompile)                                         *
      *  CDL018 - Multiple Settlement Accounts on Loans/Deposits      *
      *           (Recompile)                                         *
      *  CDL017 - Penalty Fee & Early Maturity of Fixed Term Loan/Dep *
      *           (Recompile)                                         *
      *  CDL016 - Automatic rollover of Fixed Term Loans/Deposits     *
      *           (Recompile)                                         *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  222727 - Release 5.0 errors  (Recompile)                     *
      *  194370 - Recompile after change on copy sources ZYLDZ2,      *
      *           For securities with divisor basis = '5' and first   *
      *           coupon date > valuation date (rundate + 1), set     *
      *           event control date to first coupon date before      *
      *           processing SR/ZDYYR. This is to ensure that the     *
      *           correct hypothetical last and next coupon dates are *
      *           used in deriving the price/yield rate.              *
      *  196380 - COB Error in SEC05.  Deal associated to a Depot     *
      *           Movement is not in DEALSDC, but in DLCHISPD. Fix is *
      *           to search DLCHISPD if deal is not found in DEALSDC. *
      *  CPK015 - Conversion of ZYIELD from PLI to RPG.               *
      *           Recompile over changed ZYLDZ2.                      *
      *  CSW025 - Midas Message Manager                               *
      *           - Pass original change type                         *
      *  175115 - Do not check Securities Customer file if Depot      *
      *           Movement is a Repo or Reverse Repo.                 *
      *  CSE023 - Treaty withholding Tax (Recompile)                  *
      *   CDL008 - Continuous Linked Settlement (Recompiled MGOREFPD) *
      *  CSE015 - Forward Valued Depot Movements (Recompile)          *
      *  CSE017 - Cum/Ex Indicator on Depot Movements (Recompile)     *
      *  CSE012 - SE Depot Movement Narratives (Recompiled).          *
      *  CSE006 - Repurchase Agreements (REPOs) enhancement           *
     F*  151945 - Recompiled Over changed ZDYYRZ3 .                   *
      *  140428 - SWIFT98 fix                                         *
      *           Record locking error in MGF51NPD when updated       *
      *           or written records in this program are accessed     *
      *           by M051N or MG5900. Fix is to issue a COMIT         *
      *           after successful update/write.                      *
      *  CSW014 - ERI Information for non-EMU users.                  *
      *  CSW098 - SWIFT 1998 changes.                                 *
      *                                                               *
      *****************************************************************
     FDPTMVDJ1IF  E           K        DISK         KINFSR *PSSR
     FDPTMVDJ2IF  E           K        DISK         KINFSR *PSSR
     F            DPTMVDD1                          KRENAMEDPTMVDD2
     FDPTMVDJ3IF  E           K        DISK         KINFSR *PSSR
     F            DPTMVDD1                          KRENAMEDPTMVDD3
     FSECTY   IF  E           K        DISK         KINFSR *PSSR
     FDEALS   IF  E           K        DISK         KINFSR *PSSR
     F            DEALSDAF                          KIGNORE
     F            DEALSDBF                          KIGNORE
     F            DEALSDDF                          KIGNORE
     F            DEALSDEF                          KIGNORE
     F            DEALSDGF                          KIGNORE
     F            DEALSDFF                          KIGNORE
     FDEALSALLIF  E           K        DISK                                                   196380
     F            DEALSDBF                          KIGNORE                                   196380
     F            DEALSDCF                          KIGNORE                                   196380
     F            DEALSDDF                          KIGNORE                                   196380
     F            DEALSDEF                          KIGNORE                                   196380
     F            DEALSDGF                          KIGNORE                                   196380
     F            MMDELDP0                          KIGNORE                                   196380
     F            MMDENBP0                          KIGNORE                                   196380
     F            MMDENSP0                          KIGNORE                                   196380
     F            FXDEALP0                          KIGNORE                                   196380
     F            DLBHISD0                          KIGNORE                                   196380
     F            DLDHISD0                          KIGNORE                                   196380
     F            DLEHISD0                          KIGNORE                                   196380
     F            DLGHISD0                          KIGNORE                                   196380
     FDPTMVDY2IF  E           K        DISK         KINFSR *PSSR
     F            DPTMVDD2                          KRENAMEDEPOTY2
     FINVTP   IF  E           K        DISK         KINFSR *PSSR
     FSEDEV   IF  E           K        DISK         KINFSR *PSSR
     FSECEO   IF  E           K        DISK         KINFSR *PSSR          E20085
     F            SEDEVDF                           KRENAMESEDEVDO        E20085
     F            SNDEVDF                           KRENAMESNDEVDO        E20568
     FMGOMSGLAIF  E           K        DISK         KINFSR *PSSR
     F            MGOMSGD0                          KRENAMEOMSGLA
     FSECRTNL1IF  E           K        DISK         KINFSR *PSSR          CSE006
     FMGOREFL0UF  E           K        DISK         KINFSR *PSSR
     F            MGOREFD0                          KRENAMEOREFL0
     FMGF51NPDO   E                    DISK         KINFSR *PSSR
     F                                              KCOMIT
     FMGOREFPDO   E           K        DISK         KINFSR *PSSR
     F                                              KCOMIT
     FMGOMSGPDO   E           K        DISK         KINFSR *PSSR
     F                                              KCOMIT
     FSE1876AUO   E                    PRINTER      KINFDS SPOOLU
     F                                              KINFSR *PSSR
     F/COPY WNCPYSRC,SE1876F001
      /TITLE FUNCTION OF INCICATORS
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *    21  - End of LF/DPTMVDJ1                                   *
      *    22  - End of LF/DPTMVDJ2                                   *
      *    23  - End of LF/DPTMVDJ3                                   *
      *    24  - End of LF/DPTMVDY2                                   *
      *    25  - End of LF/MGOREFL0                                   *
      *    26  - End of LF/MGOMSGLA                                   *
      *    27  - End of LF/MGOREFL0                                   *
      *    28  - End of LF/SECTY                                      *
      *    30  - End of LF/SECTRNL1                                   *   CSE006
      *                                                               *
      *    40  - PGM/MG9505 not found                                 *
      *    41  - PGM/MG5910 not found                                 *
      *    42  - PGM/MG5900 not found                                 *
      *    43  - PGM/ZM0060 not found                                 *
      *                                                               *
      *    50  - Unable to write PF/MGOMSGPD                          *
      *    51  - Unable to write PF/MGOREFPD                          *
      *    52  - Unable to write PF/MGF51NPD                          *
      *                                                               *
      *    98  - Date Format: DDMMYY (off); MMDDYY (on)               *
      *  90-99 - Used by Standard Subroutines                         *
      *                                                               *
      *  U7+U8 - Database Error                                       *
      *                                                               *
      *****************************************************************
      /TITLE SUBROUTINE INDEX
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  SRINIT - Initial Processing                                  *
      *  SRINCY - Process Depot Movement Input Cycle Messages         *
      *  SRCOB  - Process Depot Movement Maturity Messages            *
      *  SRCMSG - Delete or Cancel Previous Messages                  *
      *  SRTRAN - Rerieve and update last SWIFT sequence              *
      *  SROMSG - Output cancellation record to MGOMSGPD              *
      *  SROREF - Output cancellation record to MGOREFPD              *
      *  SRNRDT - Find the next rate change date                      *
      *  SRPIF  - Set up Repurchase Information                       *   CSE006
      *                                                               *
      *  ZNCD   - Determines Next Coupon Date                         *
      *  SRAUDT - Run Control Process                                 *
      *                                                               *
      *  *PSSR  - Program error handling routine                      *
      *                                                               *
      *****************************************************************
     E/TITLE E-SPECIFICATIONS
      *
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZDAYSZ1
     E/COPY ZSRSRC,ZPOWER8Z1
     E/COPY ZSRSRC,ZACCRZ0
     E/COPY ZSRSRC,ZNCDZ1
      *                                                                   CSW014
     E                    OA         13  1                                CSW014
     E                    @OA        13  1                                CSW014
      *                                                                   CSW014
      ** Array for Original Amount                                        CSW014
      /SPACE 3
      *
      ** Rename duplicate fields
     ISECTYDF
     I              CHTP                            SYCHTP
     IDEALSDCF
     I              CHTP                            DSCHTP
     IOREFL0
     I              TRNO                            ITRNO
     I              LSCC                            ILSCC
     IOMSGLA
     I              MTAG                            AMTAG
     I              MFLD                            AMFLD
     I              TRNO                            ATRNO
     I              MSEQ                            AMSEQ
      *
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
     ISPOOLU      DS
      *
      **  File Information Data Structure for SE1876AU.
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     IMULT        DS                        500
      *
      ** Multiple occurrence data structure for message
     I                                        1   5 MTAG
     I                                        6  55 MFLD
     I                                       56  57 CTRC
     I                                       58  73 TRNO
     I                                       74  74 PTDL
     I                                       75  77 MSEQ
      *
     I            DS
      *
      ** DS used to set pointer to LF/MGOREFL0
     I                                        1  16 WMGKEY
     I                                        1   3 WM0103
     I                                        4   9 WM0409
     I                                       10  16 WM1016
     I            DS
      *
      ** DS to set up PTRAN (parm passed to MG5900/10)
     I                                        1  15 PTRAN
     I                                        1   6 DPRN
     I I            '       '                 7  13 P713
     I                                       14  15 WTNTP
     I            DS
      *
      ** DS to set up TRNO field
     I                                        1  16 WTRNO
     I                                        1   3 WTR13
     I                                        4   9 WTR49
     I                                       10  16 WTR16
     I            DS
      *
      ** DS used to extract MGDSS1, MGRSS1, MGAWIL, & MGBOF1
     I                                        1  35 WDRAB
     I                                        1   1 WP0101
     I                                        1   2 WP0102
     I                                        1   3 WP0103                                  AR589197
     I                                        3   4 WP0304
     I                                        2  35 WP0235
     I                                        3  35 WP0335
     I                                        4  35 WP0435                                  AR580937
     I                                        5  35 WP0535
     I            DS
     I                                        1   2 CRLF
     I                                        1   1 WWCR
     I                                        2   2 WWLF
     I            DS
      *** DS used to extract DPEUCL field
     I                                        1  10 DPEUCL
     I                                        1   4 EU0104
     I                                        2  10 EU0210
     I                                        5  10 EU0510
     I            DS
      *** DS used to extract DPSAFA field
     I                                        1   6 DPSAFA
     I                                        2   6 SA0206
      ** DS for CRLF control character
      *
     I/COPY ZSRSRC,ZNCDZ2
     I/COPY ZSRSRC,ZYLDZ1
      *
     IDSFDY     E DSDSFDY
      ** First Data Structure for Access Objects
      *
     IDSSDY     E DSDSSDY
      ** Second Data Structure for Access Objects
      *
     ISDBANK    E DSSDBANKPD
      ** Bank Details
      *
     ISDBRCH    E DSSDBRCHPD
      ** Branch Details
      *
     ISDCURR    E DSSDCURRPD
      ** Currency Details
      *
     ISDSTRD    E DSSDSTRDPD
      ** Securities Trading ICD Details
      *
     ISDGELR    E DSSDGELRPD
      ** General Ledger Details
      *
     ISDSECS    E DSSDSECSPD
     I              QQACCD                          QQACD1                                    CGL029
      ** Securities Trading Clients
      *
     ISDNOST    E DSSDNOSTPD
     I              QQACCD                          QQACD2                                    CGL029
      ** Nostro Details
      *
     ISDCCSY    E DSSDCCSYPD
      ** Currency Clearing Details
      *
     ISDSARD    E DSSCSARDPD
      ** Switchable Features Details
     I/COPY WNCPYSRC,SE1876I001
      *
      *****************************************************************
     C/TITLE Main Processing
      *
      *****************************************************************
      *  P R O G R A M     S T A R T                                  *
      *****************************************************************
      *
      ** Perform Initialisation.
     C                     EXSR SRINIT
      *
      ** If PARM from SEC0304 (PICFG) is 'N' then
      **    Process Depo Movement "Input Cycle Messages"
     C           PICFG     IFEQ 'N'
      *
      ** Set On Ind to identify 'I/C PROCESSING' to Audit Report
     C                     MOVE '1'       *IN75
      *
      ** Read the Depot Movements/Extension Join File. Note that the
      ** Logical is choosing the Depot Movements where the Depo
      ** Movement Confirmation is required (DCFR='Y') and S.W.I.F.T.
      ** Depo Movement Conf Message has been requested (DPGMEC='Y').
      *
     C                     READ DPTMVDJ1                 21
     C                     MOVE CHTP      @CHG    1                                           CSW025
     C           *IN21     DOWEQ*OFF
      *
      ** Count the record read.
     C                     ADD  1         RRLIV1
     C                     MOVEL'A'       MGCHG                                               CSW025
      *                                                                                      243124A
      ** Check if REPO is Authorised                                                         243124A
      *                                                                                      243124A
     C                     EXSR SRAUTH                                                       243124A
     C           PRCREC    IFEQ 'Y'                                                          243124A
      *
      ** if CSW003 = Off OR
      **    CSW003 = On AND DPWHEN = 'S' then
      **      Process At Start Date Record
     C           CSW003    IFNE 'Y'
     C           CSW003    OREQ 'Y'
     C           DPWHEN    ANDEQ'S'
     C                     MOVE 'S'       SMFLAG
     C                     MOVE 'DM'      WTNTP   2
     C                     EXSR SRINCY
     C                     ENDIF
      *
      ** if CSW003 = Off OR
      ** if CSW003 = On AND DPWHEN = 'M' THEN
      **    Process At Maturity Date Record
     C           CSW003    IFNE 'Y'
     C           CSW003    OREQ 'Y'
     C           DPWHEN    ANDEQ'M'
      *
     C                     MOVE 'M'       SMFLAG
      *
     C           CSW003    IFNE 'Y'
     C                     MOVE 'DM'      WTNTP
     C                     ELSE
     C                     MOVE 'DN'      WTNTP
     C                     ENDIF
      *
     C                     EXSR SRINCY
      *
     C                     ENDIF
      *
     C                     ENDIF                                                             243124A
      *                                                                                      243124A
     C                     READ DPTMVDJ1                 21
     C                     MOVE CHTP      @CHG    1                                           CSW025
     C                     ENDDO
      *
     C                     ENDIF
      *
      ** If PARM from SEC0304 (PICFG) is 'Y' then
      **    Process Depo Movement "Input Cycle Termination Messages"
     C           PICFG     IFEQ 'Y'
      *
      ** Set Off Ind to identify 'COB PROCESSING' to Audit Report
     C                     MOVE '0'       *IN75
      *
      ** Set Date-Processing-Flag to 'IN '
     C                     MOVE 'IN '     WPROC   3
      *
      ** Set Pointer to read Depo Movements with Date In (DPMD)
      ** Equal To Tomorrow (up to Date of Next Working Day).
      *
     C                     MOVE *LOVAL    WBDPSS
     C                     MOVE *LOVAL    WBDPRN
     C           BJRDNB    ADD  1         WBRDNB
     C           KLJ2      SETLLDPTMVDJ2
     C                     READ DPTMVDJ2                 22
      *
      ** Do while there's a Depo Movement with Date In (DPMD)
      ** > OR = Tomorrow (Run Date + 1)  AND
      ** < OR = Next Working Day (BJDNWD)
      *
     C           *IN22     DOWEQ'0'
     C           DPMD      ANDLEBJDNWD
      *
      ** Count the record read.
     C                     ADD  1         RRLIV2
      *
     C                     EXSR SRCOB
      *
     C                     READ DPTMVDJ2                 22
     C                     ENDDO
      *
      ** Process Depo Movement 'Date Out' Records
      **
      ** Set Date-Processing-Flag to 'OUT'
     C                     MOVE 'OUT'     WPROC   3
      *
      ** Set Pointer to read Depot Movements with Date Out (DPDO)
      ** Equal To Tomorrow (up to Date of Next Working Day).
      *
     C                     MOVE *LOVAL    WCDPSS
     C                     MOVE *LOVAL    WCDPRN
     C           BJRDNB    ADD  1         WCRDNB
     C           KLJ3      SETLLDPTMVDJ3
     C                     READ DPTMVDJ3                 23
      *
      ** Do while there's a Depo Movement with Date Out(DPDO)
      ** > OR = Tomorrow (Run Date + 1)  AND
      ** < OR = Next Working Day (BJDNWD)
      *
     C           *IN23     DOWEQ'0'
     C           DPDO      ANDLEBJDNWD
      *
      ***  Count the record read.
     C                     ADD  1         RRLIV3
      *
     C                     EXSR SRCOB
      *
     C                     READ DPTMVDJ3                 23
     C                     ENDDO
      *
     C                     ENDIF
      *
      ** Produce Audit Report
     C                     EXSR SRAUDT
      *
      *****************************************************************
      *  P R O G R A M     E N D                                      *
      *****************************************************************
      *                                                               *
     C/COPY WNCPYSRC,SE1876C001
     C                     MOVE '1'       *INLR
     C                     RETRN
      *
      *****************************************************************
      /TITLE SR/SRINIT
      *****************************************************************
      *                                                               *
      *  SRINIT - Subroutine to do program initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls    :                                                   *
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR
      *
      **  Copyright Statement.
     C                     MOVEACPY@      MKI@   80
      *
      *** Define Local Data Area
     C           *NAMVAR   DEFN           LDA
      *
      ** KLIST for DPTMVDY2
     C           KLY2      KLIST
     C                     KFLD           WYDPSS
     C                     KFLD           WYDPRN
     C                     KFLD           DPWHEN
      *
      ** KLIST for DPTMVDJ2
     C           KLJ2      KLIST
     C                     KFLD           WBRDNB
     C                     KFLD           WBDPSS
     C                     KFLD           WBDPRN
      *
      ** KLIST for DPTMVDJ3
     C           KLJ3      KLIST
     C                     KFLD           WCRDNB
     C                     KFLD           WCDPSS
     C                     KFLD           WCDPRN
      *
      ** KLIST for INVTP (Nominal Ccy and Security Investment Type)
     C           KINVTP    KLIST
     C                     KFLD           NMCY
     C                     KFLD           SITP
      *
      ** Get parameters from calling program (SEC0304)
      ** PICFG - 'N' = I/C;  'Y' = COB
      *
     C           *ENTRY    PLIST
     C                     PARM           PICFG   1
      *
      ** Access Bank Details
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD001       DBASE            ***************
     C                     MOVEL'SDBANKPD'DBFILE           * DB ERROR 01 *
     C                     MOVEL'*FIRST  'DBKEY            ***************
     C                     MOVEL'SE1876  'DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      **  Check System Date Format, DDMMYY (*IN98 off)
      **                            MMDDYY (*IN98 on).
      *
     C           BJDFIN    IFEQ 'M'
     C                     SETON                     98
     C                     ENDIF
      *
      ** Access PF/SDGELRPD
     C**********           CALL 'AOGELRR0'                                                    CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C********** SDGELR    PARM SDGELR    DSFDY                                               CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD002       DBASE            **************
     C                     MOVEL'SDGELRPD'DBFILE           * DB ERROR 2 *
     C                     MOVE *BLANKS   DBKEY            **************
     C                     MOVEL'SE1876  'DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Check if SAR CSW003 is present
     C                     CALL 'AOSARDR0'
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*VERIFY' WOPTN   7
     C                     PARM 'CSW003'  WSARD   6
     C           SDSARD    PARM SDSARD    DSFDY
      *
     C           WRTCD     IFNE *BLANKS
     C           WRTCD     ANDNE'*NRF   '
     C           *LOCK     IN   LDA
     C                     Z-ADD003       DBASE            **************
     C                     MOVEL'SDSARDPD'DBFILE           * DB ERROR 3 *
     C                     MOVEL'CSW003  'DBKEY            **************
     C                     MOVEL'SE1876  'DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           WRTCD     IFEQ *BLANK
     C                     MOVE 'Y'       CSW003  1
     C                     ELSE
     C                     MOVE 'N'       CSW003
     C                     ENDIF
      *
      ** Check if SAR CEU003 is present
     C                     CALL 'AOSARDR0'
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*VERIFY' WOPTN   7
     C                     PARM 'CEU003'  WSARD   6
     C           SDSARD    PARM SDSARD    DSFDY
      *
     C           WRTCD     IFNE *BLANKS
     C           WRTCD     ANDNE'*NRF   '
     C           *LOCK     IN   LDA
     C                     Z-ADD004       DBASE            **************
     C                     MOVEL'SDSARDPD'DBFILE           * DB ERROR 4 *
     C                     MOVEL'CEU003  'DBKEY            **************
     C                     MOVEL'SE1876  'DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           WRTCD     IFEQ *BLANK
     C                     MOVE 'Y'       CEU003  1
     C                     ELSE
     C                     MOVE 'N'       CEU003
     C                     ENDIF
      *                                                                   CSW014
      *** Check if SAR CSW014 is present                                  CSW014
      *                                                                   CSW014
     C                     CALL 'AOSARDR0'                                CSW014
     C                     PARM '*BLANKS' WRTCD                           CSW014
     C                     PARM '*VERIFY' WOPTN                           CSW014
     C                     PARM 'CSW014'  WSARD                           CSW014
      *                                                                   CSW014
     C           WRTCD     IFNE *BLANKS                                   CSW014
     C           WRTCD     ANDNE'*NRF   '                                 CSW014
     C           *LOCK     IN   LDA                                       CSW014
     C                     Z-ADD027       DBASE            ***************CSW014
     C                     MOVEL'SDSARDPD'DBFILE           * DB ERROR 30 *CSW014
     C                     MOVELWSARD     DBKEY            ***************CSW014
     C                     OUT  LDA                                       CSW014
     C                     EXSR *PSSR                                     CSW014
     C                     ENDIF                                          CSW014
      *                                                                   CSW014
     C           WRTCD     IFEQ *BLANKS                                   CSW014
     C                     MOVE 'Y'       CSW014  1                       CSW014
     C                     ELSE                                           CSW014
     C                     MOVE 'N'       CSW014                          CSW014
     C                     ENDIF                                          CSW014
      *                                                                   CSE006
      *** Check if SAR CSE006 is present                                  CSE006
      *                                                                   CSE006
     C                     CALL 'AOSARDR0'                                CSE006
     C                     PARM '*BLANKS' WRTCD                           CSE006
     C                     PARM '*VERIFY' WOPTN                           CSE006
     C                     PARM 'CSE006'  WSARD                           CSE006
      *                                                                   CSE006
     C           WRTCD     IFNE *BLANKS                                   CSE006
     C           WRTCD     ANDNE'*NRF   '                                 CSE006
     C           *LOCK     IN   LDA                                       CSE006
     C                     Z-ADD028       DBASE            ***************CSE006
     C                     MOVEL'SDSARDPD'DBFILE           * DB ERROR 28 *CSE006
     C                     MOVELWSARD     DBKEY            ***************CSE006
     C                     OUT  LDA                                       CSE006
     C                     EXSR *PSSR                                     CSE006
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
     C           WRTCD     IFEQ *BLANKS                                   CSE006
     C                     MOVE 'Y'       CSE006  1                       CSE006
     C                     ELSE                                           CSE006
     C                     MOVE 'N'       CSE006                          CSE006
     C                     ENDIF                                          CSE006
      *                                                                                       CSW210
      ** Check if SWIFT Changes 2010 (CSW210) is installed.                                   CSW210
      *                                                                                       CSW210
     C                     CALL 'SWIF2010'                                                    CSW210
     C                     PARM *BLANKS   WRTCD                                               CSW210
      *                                                                                       CSW210
     C           WRTCD     IFEQ 'CSW2010'                                                     CSW210
     C                     MOVE 'Y'       CSW210  1                                           CSW210
     C                     ELSE                                                               CSW210
     C                     MOVE 'N'       CSW210                                              CSW210
     C                     ENDIF                                                              CSW210
     C/COPY WNCPYSRC,SE1876C002
      *
      ** Define Workfields
     C           *LIKE     DEFN DPRN      WBDPRN
     C           *LIKE     DEFN BJRDNB    WBRDNB
     C           *LIKE     DEFN DPRN      WCDPRN
     C           *LIKE     DEFN BJRDNB    WCRDNB
     C           *LIKE     DEFN DPSS      WBDPSS
     C           *LIKE     DEFN DPSS      WCDPSS
     C           *LIKE     DEFN DPSS      WYDPSS
     C           *LIKE     DEFN DPRN      WYDPRN
      *
      ** Initialise Record Count workfields to Zero
     C                     Z-ADD*ZERO     RRLIV1
     C                     Z-ADD*ZERO     RRLIV2
     C                     Z-ADD*ZERO     RRLIV3
      *
     C                     MOVE *BLANK    SMFLAG  1
      *
      ** Set CRLF control character
     C                     MOVE *LOVAL    CRLF
     C                     BITON'457'     WWCR
     C                     BITON'257'     WWLF
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/SRAUTH                                                                       243124A
      *****************************************************************                      243124A
      *                                                               *                      243124A
      *  SRAUTH - Subroutine to Check if the deal is being            *                      243124A
      *             authorised.                                       *                      243124A
      *                                                               *                      243124A
      *  Called By: Main Processing                                   *                      243124A
      *  Calls:                                                       *                      243124A
      *                                                               *                      243124A
      *****************************************************************                      243124A
      *                                                                                      243124A
     C           SRAUTH    BEGSR                                                             243124A
      *                                                                                      243124A
     C                     MOVE 'Y'       PRCREC  1                                          243124A
      *                                                                                      243124A
      ** Access LF/DEALS using key of Assoc. Deal No. if not Blank                           243124A
      *                                                                                      243124A
     C           DPAD      IFNE *ZERO                                                        243124A
     C           DPAD      CHAINDEALS                29                                      243124A
     C           *IN29     IFEQ '1'                                                          243124A
      *                                                                                      243124A
     C           DPAD      CHAINDLCHISD0             29                                      243124A
     C           *IN29     IFEQ '1'                                                          243124A
     C                     MOVE 'N'       PRCREC                                             243124A
     C                     ENDIF                                                             243124A
      *                                                                                      243124A
     C                     ENDIF                                                             243124A
     C                     ENDIF                                                             243124A
      *                                                                                      243124A
     C                     ENDSR                                                             243124A
      *****************************************************************                      243124A
      /TITLE SR/SRINCY
      *****************************************************************
      *                                                               *
      *  SRINCY - Subroutine to process depo movement input cycle     *
      *           messages.                                           *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls: SRAMSF, SRCMSG, SRF5IN                                *
      *                                                               *
      *****************************************************************
      *
     C           SRINCY    BEGSR
      *
      ** Only movement with type Repo-Purchase (RP),
      ** Repo-Reverse (RR), Pledge-Deposit (PD),
      ** and Pledge-Loan (PL) will be processed.
     C           DPMV      IFEQ 'RP'
     C           DPMV      OREQ 'RR'
     C           DPMV      OREQ 'PD'
     C           DPMV      OREQ 'PL'
      *
      ** Set Record-Extracted-Flag to 'N'
     C                     MOVE 'N'       WEXTCT  1
      *
      ** Clear/Initialise PF/MGF51NPD record
     C                     CLEARMGF51ND0
      *
      ** Access Master Files
     C                     EXSR SRAMSF
      *
      ** If Confirmation Generated Ind (DPCONG) is not 'N',
      ** meaning, this is not the 1st msg for this movement,
      **    THEN Delete or Cancel all previous confirmation msgs
     C           DPCONG    IFNE 'N'
     C                     EXSR SRCMSG
     C                     ENDIF
      *
      ** If DPTMVDJ1/Last Change Type is not 'D', then,
      **    Output record to new extract file, PF/MGF51NPD
     C           CHTP      IFNE 'D'
     C                     EXSR SRF5IN
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRCOB
      *****************************************************************
      *                                                               *
      *  SRCOB  - Subroutine to process depo movement input cycle     *
      *           termination messages.                               *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls: SRAMSF, SRF5IN                                        *
      *                                                               *
      *****************************************************************
      *
     C           SRCOB     BEGSR
      *
      ** Set Record-Extracted-Flag to 'N'
     C                     MOVE 'N'       WEXTCT  1
      *
      ** Clear PF/MGF51NPD record
     C                     CLEARMGF51ND0
      *
      ** If Depo Movement Conf Msg has been requested (DPGMEC='Y').
     C           DPGMEC    IFEQ 'Y'
     C           DCFR      ANDEQ'Y'                                                         MD054797
      *
     C           WPROC     IFEQ 'IN '
     C           DPMV      ANDEQ'RP'
      *
     C           WPROC     OREQ 'IN '
     C           DPMV      ANDEQ'PD'
      *
     C           WPROC     OREQ 'OUT'
     C           DPMV      ANDEQ'RR'
      *
     C           WPROC     OREQ 'OUT'
     C           DPMV      ANDEQ'PL'
      *
     C           CSW003    IFNE 'Y'
     C           CSW003    OREQ 'Y'
     C           DPWHEN    ANDEQ'M'
      *
     C                     MOVE 'M'       SMFLAG
     C           CSW003    IFEQ 'Y'
     C                     MOVEL'DN'      WTNTP
     C                     ELSE
     C                     MOVEL'DM'      WTNTP
     C                     ENDIF
      *
      ** If Confirmation Message for this movement has
      ** already been generated (DPCONG='Y'), Perform
      ** DB Error processing.
     C           DPCONG    IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     Z-ADD006       DBASE            **************
     C                     MOVEL'DPTMVDJ2'DBFILE           * DB ERROR 6 *
     C                     MOVELDPSS      DBKEY            **************
     C                     MOVE DPRN      DBKEY
     C                     MOVEL'SE1876  'DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Access Master Files
     C                     EXSR SRAMSF
      *
      ** Output record to PF/MGF51NPD
     C                     EXSR SRF5IN
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/SRAMSF
      *****************************************************************
      *                                                               *
      *  SRAMSF - Access Master Files                                 *
      *                                                               *
      *  Called By: SRINCY, SRCOB                                     *
      *                                                               *
      *  Calls    :                                                   *
      *                                                               *
      *****************************************************************
      *
     C           SRAMSF    BEGSR
      *
      ** Save Branch Code as a character field
     C                     MOVE DPBA      WWDPBA  3
      *
      ** Access LF/SDBRCHL1 for Branch Code to get
      ** Branch Internal Customer Number (A8BICN)
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM '*MSG   ' WRTCD
     C                     PARM '*KEY   ' WOPTN
     C                     PARM WWDPBA    WBRCH   3
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD005       DBASE            **************
     C                     MOVEL'SDBRCHPD'DBFILE           * DB ERROR 5 *
     C                     MOVELWWDPBA    DBKEY            **************
     C                     MOVEL'SE1876  'DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Save Branch Internal Customer Number
     C                     MOVE A8BICN    WWBICN  6
      *
      ** Access SDSECSPD to check if Out Depot is Euroclear/Cedel Cust
     C           DPMV      IFNE 'RP'                                      175115
     C           DPMV      ANDNE'RR'                                      175115
      *                                                                   175115
     C                     MOVE DPOD      WWCUST  6
     C                     CALL 'AOSECSR0'
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*KEY   ' WOPTN   7
     C                     PARM           WWCUST
     C           SDSECS    PARM SDSECS    DSSDY
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD024       DBASE            **************
     C                     MOVEL'SDSECSPD'DBFILE           * DB ERROR 24*
     C                     MOVELDPOD      DBKEY            **************
     C                     MOVEL'SE1876  'DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           BFCLAS    IFEQ 'E'
     C           BFCLAS    OREQ 'C'
     C                     MOVE 'Y'       W@ODEC  1
     C                     ELSE
     C                     MOVE *BLANK    W@ODEC
     C                     ENDIF
      *                                                                   175115
     C                     ENDIF                                          175115
      *
      ** Access SDSECSPD to check if In Depot is Euroclear/Cedel Cust
     C                     MOVE DPID      WWCUST
     C                     CALL 'AOSECSR0'
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*KEY   ' WOPTN   7
     C                     PARM           WWCUST
     C           SDSECS    PARM SDSECS    DSSDY
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD025       DBASE            **************
     C                     MOVEL'SDSECSPD'DBFILE           * DB ERROR 25*
     C                     MOVELDPID      DBKEY            **************
     C                     MOVEL'SE1876  'DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           BFCLAS    IFEQ 'E'
     C           BFCLAS    OREQ 'C'
     C                     MOVE 'Y'       W@IDEC  1
     C                     ELSE
     C                     MOVE *BLANK    W@IDEC
     C                     ENDIF
      *
      ** Save Beneficiary Customer Number as a CHAR field
     C                     MOVE DPBN      WWDPBN  6
      *
      ** Access PF/SDSECSPD using key Counterparty Customer Number
     C                     CALL 'AOSECSR0'
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*KEY   ' WOPTN   7
     C                     PARM WWDPBN    WWCUST  6
     C           SDSECS    PARM SDSECS    DSSDY
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD006       DBASE            **************
     C                     MOVEL'SDSECSPD'DBFILE           * DB ERROR 6 *
     C                     MOVELWWDPBN    DBKEY            **************
     C                     MOVEL'SE1876  'DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Access PF/DPTMVDY2 using key of Security Short Name,
      ** Depot Reference, and When Produced
     C                     MOVELDPSS      WYDPSS
     C                     MOVELDPRN      WYDPRN
     C           KLY2      CHAINDPTMVDY2             24
     C           *IN24     IFEQ *OFF
     C                     MOVE 'Y'       WWEXSS  1
     C                     ELSE
     C                     CLEARDEPOTY2
     C                     MOVE 'N'       WWEXSS
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/SRCMSG
      *****************************************************************
      *                                                               *
      *  SRCMSG - Delete or Cancel previous messages.                 *
      *                                                               *
      *  Called By: SRINCY                                            *
      *  Calls: SRTRAN, SROMSG, SROREF, MG5910                        *
      *                                                               *
      *****************************************************************
      *
     C           SRCMSG    BEGSR
      *
      ** Set Record-Found-Flag to 'N'
     C                     MOVE 'N'       WRFND   1
      *
      ** Set up key field (WMGKEY);
      **   Start Date    - WMGKEY = 'SED' + DPRN + *HIVAL
      **   Maturity Date - WMGKEY - 'SEE' + DPRN + *HIVAL
     C           WTNTP     IFEQ 'DM'
     C                     MOVE 'SED'     WM0103
     C                     ELSE
     C           WTNTP     IFEQ 'DN'
     C                     MOVE 'SEE'     WM0103
     C                     ENDIF
     C                     ENDIF
     C                     MOVE DPRN      WM0409
     C                     MOVE *HIVAL    WM1016
      *
      ** Set file pointer to the last generated msg for the trans
     C           WMGKEY    SETGTMGOREFL0
     C                     READPMGOREFL0            N    25
      *
     C           *IN25     DOWEQ*OFF
     C           MTRN      ANDEQDPRN
     C           TNTP      ANDEQWTNTP
      *
      ** Save the Trans Reference Number for later CHAIN and UPDATE
     C                     MOVELITRNO     W@TRNO 16
      *
     C           MTPY      IFEQ '518'
      *
      ** If Msg Status (MGST) is 'CRTD' OR
      **    Msg Status (MGST) is 'AMND' AND
      **    Last Conf Codeword (LSCC) is not 'CANCEL', then
      **    Call MG9505 to update MGOREFPD record with
      **    Msg Status set to 'DLTD' and Msg Group set to '4'
      *
     C           MGST      IFEQ 'CRTD'
     C           ILSCC     ANDNE'CANCEL  '
     C           MGST      OREQ 'AMND'
     C           ILSCC     ANDNE'CANCEL  '
      *
     C                     CALL 'MG9505'               40
     C                     PARM ITRNO     WTNUM  16
     C                     PARM *BLANKS   WRTNC   1
      *
      ** If MG9505 is not found, perform DB Error processing
     C           *IN40     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD007       DBASE            ***************
     C                     MOVE *BLANKS   DBKEY            * DB ERROR 7  *
     C                     MOVEL'MG9505'  DBPGM            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** If MG9505 ended abnormally, perform DB Error processing
     C           WRTNC     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     Z-ADD008       DBASE            ***************
     C                     MOVE *BLANKS   DBKEY            * DB ERROR 8  *
     C                     MOVELWTNUM     DBKEY            ***************
     C                     MOVEL'MG9505'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Set Record-Found-Flag to 'Y'
     C                     MOVE 'Y'       WRFND
      *
     C                     ENDIF
      *
      ** If message is already sent or logically acknowledge
     C           MGSG      IFEQ '2'
     C           MGSG      OREQ '3'
      *
      ** If Cancellation is not requested (CARQ <> 'Y') and
      ** Last Conf Codeword is not 'CANCEL' (LSCC <> 'CANCEL')
     C           CARQ      IFNE 'Y'
     C           ILSCC     ANDNE'CANCEL  '
      *
      ** Retrieve and update last SWIFT Sequence for transaction
     C                     EXSR SRTRAN
      *
      ** Output cancellation record to PF/MGOMSGPD
     C                     EXSR SROMSG
      *
      ** Output cancellation record to PF/MGOREFPD
     C                     EXSR SROREF
      *
      ** Set record-found-workfield to 'Y'
     C                     MOVE 'Y'       WRFND
      *
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
      *** If previous message is the old counterpart (MT512),
      *** set record-found-workfield to 'Y' too so that the
      *** Confirmation Generated indictor is reset properly.
      *
     C           MTPY      IFEQ '512'
      *
     C           MGST      IFEQ 'CRTD'
     C           MGST      OREQ 'AMND'
     C           ILSCC     ANDNE'CANCEL  '
     C                     MOVE 'Y'       WRFND
     C                     ENDIF
      *
      *** If message is already sent or logically acknowledge
     C           MGSG      IFEQ '2'
     C           MGSG      OREQ '3'
     C           CARQ      IFNE 'Y'
     C           ILSCC     ANDNE'CANCEL  '
     C                     MOVE 'Y'       WRFND
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** If record was successfully deleted/cancelled,
      ** then call MG5910 to update Confirmation Generated
      ** Indicator (DPTMVDX1/DPCONG) to 'N'.
     C           WRFND     IFEQ 'Y'
     C           ILSCC     ANDNE'CANCEL  '
      *
     C                     CALL 'MG5910'               41
     C                     PARM 'SE'      PMODI   2
     C                     PARM           PTRAN  15
     C                     PARM 'CE'      PPROG   2
     C                     PARM 'C'       PMODE   1
     C                     PARM *BLANKS   PCONG   1
     C                     PARM *BLANKS   PRCDG   1
     C                     PARM *BLANKS   PERCD   1
     C                     PARM           CSW003  1
      *
      ** If MG5910 is not found, perform DB Error processing
     C           *IN41     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD009       DBASE            ***************
     C                     MOVE *BLANKS   DBKEY            * DB ERROR 9  *
     C                     MOVEL'MG5910'  DBPGM            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** If MG5910 ended abnormally, perform DB Error processing
     C           PERCD     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     Z-ADD010       DBASE            ***************
     C                     MOVE *BLANKS   DBKEY            * DB ERROR 10 *
     C                     MOVELPTRAN     DBKEY            ***************
     C                     MOVEL'MG5910'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           CSW003    IFEQ 'Y'
     C           WRFND     ANDEQ'Y'
     C                     LEAVE
     C                     ENDIF
      *
      ** Read previous record
     C                     READPMGOREFL0            N    25
     C                     ENDDO
      *
     C                     MOVEL'D'       MGCHG                                               CSW025
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRTRAN
      *****************************************************************
      *                                                               *
      *  SRTRAN - Retrieve and Update Last SWIFT Sequence Number      *
      *                                                               *
      *  Called By: SRCMSG                                            *
      *  Calls: MG5900                                                *
      *                                                               *
      *****************************************************************
      *
     C           SRTRAN    BEGSR
      *
     C                     CALL 'MG5900'               42
     C                     PARM 'SE'      PMODI   2
     C                     PARM           PTRAN  15
     C                     PARM           PPROG   2
     C                     PARM 'U'       PMODE   1
     C                     PARM *ZEROS    PLSWS   70
     C                     PARM *BLANKS   PERCD   1
     C                     PARM           CSW003  1
      *
      ** If MG5900 is not found, perform DB Error processing
     C           *IN42     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD011       DBASE            ***************
     C                     MOVE *BLANKS   DBKEY            * DB ERROR 11 *
     C                     MOVEL'MG5900'  DBPGM            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** If MG5900 ended abnormally, perform DB Error processing
     C           PERCD     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     Z-ADD012       DBASE            ***************
     C                     MOVE *BLANKS   DBKEY            * DB ERROR 12 *
     C                     MOVELPTRAN     DBKEY            ***************
     C                     MOVEL'MG5900'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** If MG5900 returns successfully
     C           PLSWS     IFEQ 9999999
     C                     Z-ADD1         WLSWS   70
     C                     ELSE
     C           PLSWS     ADD  1         WLSWS
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/SROMSG
      *****************************************************************
      *                                                               *
      *  SROMSG - Output cancellation message to PF/MGOMSGPD          *
      *                                                               *
      *  Called By: SRCMSG                                            *
      *  Calls:                                                       *
      *                                                               *
      *****************************************************************
      *
     C           SROMSG    BEGSR
      *
      ** Clear multiple occurrence data structure
     C                     Z-ADD1         X1      30
     C           X1        OCUR MULT
      *
     C           X1        DOWLE500
     C           MULT      ANDNE*BLANKS
     C                     CLEARMULT
     C                     ADD  1         X1
     C           X1        IFLE 500
     C           X1        OCUR MULT
     C                     ENDIF
     C                     ENDDO
      *
      ** Reset MULT Index (X) to Zero
     C                     Z-ADD*ZERO     X1
      *
      ** Check if this transaction number is in MGOMSGLA
     C           ITRNO     CHAINMGOMSGLA             26
      *
      ** Process all records in MGOMSGLA with the same
      ** Transaction Reference Number
      ** (Rewrite the whole message but change the content
      **  of tags :20C: and :23G: and output Subsequence A1)
     C           *IN26     DOWEQ*OFF
     C           ITRNO     ANDEQATRNO
      *
      ** Setup WBLOCK workfield
     C           AMTAG     IFEQ ':16R:'
     C           AMFLD     IFEQ 'GENL'
     C                     MOVE 'GENL'    WBLOCK  4
     C                     ELSE
     C                     MOVE *BLANKS   WBLOCK
     C                     ENDIF
     C                     ENDIF
      *
      ** Set up WTRNO workfield
      **    Start Date    - WTRNO = 'SED' + Depo Movmt Ref + New Seq No
      **    Maturity Date - WTRNO = 'SEE' + Depo Movmt Ref + New Seq No
      *
     C                     MOVE *BLANK    WTRNO
      *
     C           WTNTP     IFEQ 'DM'
      *
     C                     MOVEL'SED'     WTR13
     C                     ELSE
      *
     C           WTNTP     IFEQ 'DN'
     C                     MOVEL'SEE'     WTR13
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     MOVELDPRN      WTR49
     C                     MOVELWLSWS     WTR16
      *
      ** Access the xth element of MULT
     C                     ADD  1         X1
     C           X1        OCUR MULT
      *
     C                     MOVELAMTAG     MTAG
     C                     MOVELAMFLD     MFLD
     C                     MOVELWTRNO     TRNO
     C                     MOVELCRLF      CTRC
     C                     MOVELAMSEQ     MSEQ
      *
      ** Change the data of tag :20C:
     C           AMTAG     IFEQ ':20C:'
     C           WBLOCK    ANDEQ'GENL'
     C                     MOVE *BLANKS   MFLD
     C           ':SEME//' CAT  WTRNO:0   MFLD
     C                     ENDIF
      *
      ** Change the data of tag :23G:
     C           AMTAG     IFEQ ':23G:'
     C                     MOVE *BLANKS   MFLD
     C                     MOVEL'CANC'    MFLD
     C                     ENDIF
      *
      ** Output Subsequence A1
     C           AMTAG     IFEQ ':22F:'
     C           WBLOCK    ANDEQ'GENL'
      *
      ** Access the xth element of MULT
     C                     ADD  1         X1
     C           X1        OCUR MULT
      *
     C                     MOVEL':16R:'   MTAG
     C                     MOVEL'LINK'    MFLD
     C                     MOVELWTRNO     TRNO
     C                     MOVELCRLF      CTRC
     C                     MOVEL'A1'      MSEQ
      *
      ** Access the xth element of MULT
     C                     ADD  1         X1
     C           X1        OCUR MULT
      *
     C                     MOVEL':20C:'   MTAG
     C           ':PREV//' CAT  ITRNO:0   MFLD
     C                     MOVELWTRNO     TRNO
     C                     MOVELCRLF      CTRC
     C                     MOVEL'A1'      MSEQ
      *
      ** Access the xth element of MULT
     C                     ADD  1         X1
     C           X1        OCUR MULT
      *
     C                     MOVEL':16S:'   MTAG
     C                     MOVEL'LINK'    MFLD
     C                     MOVELWTRNO     TRNO
     C                     MOVELCRLF      CTRC
     C                     MOVEL'A1'      MSEQ
      *
     C                     ENDIF
      *
      ** Read the next record on LF/MGOMSGLA
     C                     READ MGOMSGLA                 26
      *
     C                     ENDDO
      *
      ** Write the content MULT to PF/MGOMSGPD
     C                     Z-ADD1         Y       30
     C           Y         OCUR MULT
      *
     C           Y         DOWLEX1
     C           MFLD      ANDNE*BLANKS
      *
     C                     WRITEMGOMSGD0               50
      *
      ** Unable to write to PF/MGOMSGPD, perform DB Error processing
     C           *IN50     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD013       DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 13   *
     C                     MOVEL'MGOMSGPD'DBFILE           * * * * * * * *
     C                     MOVEL'SE1876  'DBPGM                          *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ADD  1         Y
      *
      ** Condition access to MULT to prevent OCUR error
      ** if X1 = 500 and Y = 501
     C           Y         IFLE X1
     C           Y         OCUR MULT
     C                     ENDIF
      *
     C                     ENDDO
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/SROREF
      *****************************************************************
      *                                                               *
      *  SROREF - Output cancellation record to PF/MGOREFPD           *
      *                                                               *
      *  Called By: SRCMSG                                            *
      *  Calls:                                                       *
      *                                                               *
      *****************************************************************
      *
     C           SROREF    BEGSR
      *
      ** Update LF/MGOREFL0
     C           W@TRNO    CHAINOREFL0               27
     C           *IN27     IFEQ *OFF
     C                     MOVEL'Y'       CARQ
     C                     MOVELBJMRDT    LADT
     C                     TIME           LATM
     C                     UPDATOREFL0
     C                     ENDIF
      *
      ** New Transaction Reference Number
      ** (Being setup in SR/SROMSG - content of tag :20C:)
     C                     MOVELWTRNO     TRNO
      *
      ** Convert run date to YYMMDD, for Message Generation Date
     C                     CALL 'ZM0060'               43
     C                     PARM BJRDNB    ZMDAY   50
     C                     PARM           ZSDAT   6
      *
      ** Program ended in error, perform DB Error processing
     C           *IN43     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD014       DBASE              * * * * * * *
     C                     MOVEL'       ' DBKEY              * DBERR 14  *
     C                     MOVEL'ZM0060'  DBFILE             * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVE ZSDAT     MGDE
     C                     MOVEL'1'       MGSG
     C                     MOVEL'CRTD'    MGST
     C                     MOVE *BLANK    CARQ
     C                     MOVEL'CANCEL  'LSCC
     C/COPY WNCPYSRC,SE1876C003
      *
      ** Write cancellation record to PF/MGOREFPD
     C                     WRITEMGOREFD0               51
      *
      ** Unable to write to PF/MGOREFPD, perform DB Error processing
     C           *IN51     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD015       DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 15   *
     C                     MOVEL'MGOREFPD'DBFILE           * * * * * * * *
     C                     MOVEL'SE1876  'DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/SRF5IN
      *****************************************************************
      *                                                               *
      *  SRF5IN - Output PF/MGF51NPD record                           *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *                                                               *
      *****************************************************************
      *
     C           SRF5IN    BEGSR
      *
      ** Access LF/SECTY details using Security Shortname
      ** (DPSS) as key
     C           DPSS      CHAINSECTY                28
     C           *IN28     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD016       DBASE            ***************
     C                     MOVEL'SECTY   'DBFILE           * DB ERROR 16 *
     C                     MOVELDPSS      DBKEY            ***************
     C                     MOVEL'SE1876'  DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Message Type
     C                     MOVEL'518'     MGMTYP
      *
      ** Destination Customer
      ** (Set to Beneficial Customer Number)
     C                     MOVELDPBN      MGDEST
      *
      ** Transaction Reference Number
      ** (Set to Depo Movement Reference Number)
     C                     MOVELDPRN      MGTNUM
      *
      ** Module Id
     C                     MOVEL'SE'      MGMODI
      *
      ** Transaction Type
      ** (Start Date - Set to 'DM'; Maturity Date - Set to 'DN'
     C                     MOVELWTNTP     MGTTYP
      *
      ** Transaction Sub-Type
      ** (Set to Depot Movement Type)
     C                     MOVELDPMV      MGTSTP
      *
      ** Branch Code of Sender
     C                     MOVELDPBA      MGSNBR
      *
      ** Settlement Type
     C                     MOVE *BLANK    MGSETP
      *
      ** Value Date
     C           SMFLAG    IFEQ 'S'
     C           DPMV      ANDEQ'RP'
      *
     C           SMFLAG    OREQ 'S'
     C           DPMV      ANDEQ'PD'
      *
     C           SMFLAG    OREQ 'M'
     C           DPMV      ANDEQ'RR'
      *
     C           SMFLAG    OREQ 'M'
     C           DPMV      ANDEQ'PL'
      *
     C                     Z-ADDDPDO      MGVDAT
     C                     ELSE
     C                     Z-ADDDPMD      MGVDAT
     C                     ENDIF
      *
      ***  Next Coupon Date. If Security is not Interest-Bearing,
      ***  (implied by Initial Date = 0 or Coupon Rate = 0), then set
      ***  the Next Coupon Date to zero.
      *
     C           ITLD      IFEQ 0
     C           CPNR      OREQ 0
     C                     Z-ADD0         MGNCDT
     C                     ELSE
      *
      ***  Want Next Coupon AFTER Value Date, so add 1.
     C                     Z-ADD*ZERO     ECD
     C           MGVDAT    ADD  1         ECD
     C                     EXSR ZNCD
     C                     Z-ADDNCD       MGNCDT
     C                     ENDIF
      *
      ** Trade Date
     C/COPY WNCPYSRC,SE1876C005
      **   Access LF/DEALS using key of Assoc. Deal No.
     C           DPAD      CHAINDEALS                29
     C           *IN29     IFEQ *ON
      *                                                                                       196380
     C           DPAD      CHAINDLCHISD0             29                                       196380
      *                                                                                       196380
     C           *IN29     IFEQ '1'                                                           196380
      *                                                                                       196380
     C           *LOCK     IN   LDA
     C                     Z-ADD020       DBASE            **************
     C**********           MOVEL'DEALS'   DBFILE           * DB ERROR 20*                     196380
     C                     MOVEL'DEALSALL'DBFILE           * DB ERROR 20*                     196380
     C                     MOVELDPAD      DBKEY            **************
     C                     MOVEL'SE1876'  DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF                                                              196380
     C/COPY WNCPYSRC,SE1876C006
      *                                                                                       196380
      *
      ** Set Trade Date to Deal Date (DDAT)
     C                     Z-ADDDDAT      MGTDAT
      *
      ** Trade Time (HH:MM:SS) where SS is always '00'
     C                     MOVELDPTRTT    MGTRTT
     C                     MOVE '00'      MGTRTT
      *
      ** Market Price/Yield/Discount
     C           CSW003    IFEQ 'Y'
     C           DPPRIC    ANDNE*ZERO
     C                     Z-ADDDPPRIC    ZPRICE
     C                     ELSE
     C           MKTP      IFNE *ZERO
     C                     Z-ADDMKTP      ZPRICE
     C                     ELSE
     C                     Z-ADDMKPR      ZPRICE
     C                     ENDIF
     C                     ENDIF
      *
     C                     Z-ADDMGVDAT    ZFDATE
      *
      ** Output Price as Price/Discount/Yield
     C                     EXSR ZPRCO
     C                     Z-ADDZPRCOT    MGPRIC
      *
      ** Deal Amount
     C           DPMV      IFEQ 'RR'
     C           DPMV      OREQ 'RP'
     C           DPMV      OREQ 'PL'
     C           DPMV      OREQ 'PD'
      *
      *** Access LF/SDCURRPD using key nominal currency
     C                     CALL 'AOCURRR0'
     C                     PARM '*MSG   ' WRTCD
     C                     PARM '*KEY   ' WOPTN
     C                     PARM NMCY      WCUCY   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
      *** Handle Database error
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD026       DBASE            ***************
     C                     MOVEL'SDCURRPD'DBFILE           * DB ERROR 26 *
     C                     MOVELNMCY      DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      *** Save number of decimal places
     C                     MOVE A6NBDP    WWDPI   10
      *
     C                     Z-ADDZPRICE    ZPRC   169
     C                     Z-ADDDPNA      ZNOML
     C                     Z-ADDNMDP      NMDP    10
     C                     Z-ADDWWDPI     ZCDP    10
     C                     EXSR ZCNSD
     C                     Z-ADDZCONS     MGDLAM
      *
     C                     ENDIF
      *
      ** Settlement Amount
      **  =(Nominal Amt * Price + Accrued Intr) * Exchange Rate
     C           MGDLAM    ADD  DPITRA    WSTAM  150
     C           DPTDER    IFNE 0
     C           WSTAM     MULT DPTDER    WSTAM
     C                     ENDIF
      *
     C                     Z-ADDWSTAM     MGSTAM
      *
      ** Trade Basis
     C                     MOVELSTBS      MGSTBS
      *
      ** Price Basis
     C                     MOVELSPBS      MGSPBS
      *
      ** Cum/Ex Indicator, 'X' if Accrued Intr > 0;
      **                   'C' if Accrued Intr < 0;
      **                   ' ' if Accrued Intr = 0
     C           DPITRA    IFGT *ZERO
     C                     MOVEL'C'       MGEXIN
     C                     ELSE
     C           DPITRA    IFLT *ZERO
     C                     MOVEL'X'       MGEXIN
     C                     ELSE
     C                     MOVE *BLANK    MGEXIN
     C                     ENDIF
     C                     ENDIF
      *
      ***  Number of Days of Accrued Interest. If SECTYD/CPNR is not
      ***  *Zero AND SAR CSW003 is present, this is the number of days
      ***  between Last Coupon Date before (or on) the value date of the
      ***  movement and Value Date OR Value Date and Next Coupon Date,
      ***  Otherwise, Zero
      *
     C           CPNR      IFNE *ZERO
     C           CSW003    ANDEQ'Y'
     C                     EXSR DAYSAC
     C                     Z-ADDZDDAYS    MGACND
     C                     ELSE
     C                     Z-ADD0         MGACND
     C                     ENDIF
      *
      ** Interest Amount
     C           CSW003    IFEQ 'Y'
     C                     Z-ADDDPITRA    MGITRA
     C                     ENDIF
      *
      ** Settlement Currency
     C           DPMV      IFEQ 'RR'
     C           DPMV      OREQ 'RP'
     C           DPMV      OREQ 'PL'
     C           DPMV      OREQ 'PD'
      *
     C           CEU003    IFEQ 'Y'
     C           STCY      ANDNE*BLANKS
     C                     MOVELSTCY      MGSTCY
     C                     ELSE
     C                     MOVELCCY       MGSTCY
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Pay Code
      **  = '1' if Free/Against Pymnt is 'A' and Guaranteed Del is ' '
      **  = '5' if Free/Against Pymnt is 'A' and Guaranteed Del is 'G'
      **  = '3' if condition 1 or 2 is not satisfied
     C           DPFIND    IFEQ 'A'
     C           DPGDEL    ANDEQ*BLANK
     C                     MOVEL'1'       MGPCOD
     C                     ELSE
     C           DPFIND    IFEQ 'A'
     C           DPGDEL    ANDEQ'G'
     C                     MOVEL'5'       MGPCOD
     C                     ELSE
     C                     MOVEL'3'       MGPCOD
     C                     ENDIF
     C                     ENDIF
      *
      ** Investor (Investor from LF/DPTMVDY2)
     C                     MOVELD2INVE    MGINVE
      *
      ** Euroclear/Cedel Code
     C           DPEUCL    IFNE *BLANKS
     C           EU0104    IFEQ '//XC'
     C           EU0104    OREQ '//XE'
     C                     MOVELEU0510    MGEUCL
     C                     ELSE
     C                     MOVELEU0210    MGEUCL
     C                     ENDIF
     C                     ENDIF
      *
      ** Safekeeping Account Line
     C                     MOVE *BLANKS   MGSA1L
      *
      ** Account for Payment
     C                     MOVE *BLANKS   MGAP1L
     C                     MOVE 'N'       MGFAP1                                            AR549800
      *
      ** Processing Type
      *
      ** Access LF/INVTP using SECTYD/NMCY and SECTYD/SITP
     C           KINVTP    CHAININVTP                29
     C           *IN29     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD022       DBASE            ***************
     C                     MOVEL'INVTP   'DBFILE           * DB ERROR 22 *
     C                     MOVELCCY       DBKEY5  5        ***************
     C                     MOVE SITP      DBKEY5
     C                     MOVELDBKEY5    DBKEY
     C                     MOVEL'SE1876  'DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELPROT      MGPROT
      *
      ** Quantity of Securities
     C                     Z-ADDDPNA      MGQANT
      *
      ** ISIN Code Of Security
     C                     MOVELISIN      MGISIN
      *
      ** Format Security Full Name 1 and 2
      **  Check if Security Full Name-1 is not blank
     C           SFN1      IFNE *BLANKS
     C                     MOVELSFN1      MGSFN1
     C                     MOVELSFN2      MGSFN2
     C                     ELSE
     C                     MOVELSESN      MGSFN1
     C                     MOVELSRPN      MGSFN2
     C                     ENDIF
      *
      ** Day Basis
     C                     MOVELDYBS      MGDYBS
      *
      ** Divisor Basis
     C                     MOVELDVBS      MGDVBS
      *
      ** Payment Frequency
     C           CPNR      IFGT *ZERO
      *
     C                     Z-ADD1         XX      20
     C                     Z-ADD0         YY      20
     C           XX        DOWLE12
      *
     C           CD,XX     IFNE *ZERO
     C                     ADD  1         YY
     C                     ENDIF
      *
     C                     ADD  1         XX
     C                     ENDDO
      *
     C                     SELEC
     C           YY        WHEQ 12
     C                     MOVEL'MNTH'    MGPFRE
     C           YY        WHEQ 4
     C                     MOVEL'QUTR'    MGPFRE
     C           YY        WHEQ 2
     C                     MOVEL'SEMI'    MGPFRE
     C           YY        WHEQ 1
     C                     MOVEL'ANNU'    MGPFRE
     C                     OTHER
     C                     MOVEL*BLANK    MGPFRE
     C                     ENDSL
      *
     C                     ENDIF
      *
      ** Partly Paid
     C                     MOVELPPDI      MGPRTP
      *
      ** Nominal Currency
     C                     MOVELNMCY      MGNMCY
      *
      ** Next Rate Change Date
     C                     EXSR SRNRDT
      *
      ** Maturity Date
     C                     Z-ADDMATY      MGMDAT
      *
      ** Currency Factor
     C                     Z-ADDCFCT      MGCUFC
      *
      ** Coupon Rate
     C                     Z-ADDCPNR      MGCPNR
      *
      ** Certificate Nos. Required
     C                     MOVE *BLANK    MGCRTN
      *
      ** Instruction Sub-type
     C                     MOVELDPINSS    MGINSS
      *
      ** Standing Instr Override
     C                     MOVELDPSIOR    MGSTAN
      *
      ** Deliverer of Securities & Deliverer of Securities-1
     C           WWEXSS    IFEQ 'Y'
     C           D2DSSN    ANDNE*BLANKS
     C           WWEXSS    OREQ 'Y'
     C           D2DSS1    ANDNE*BLANKS
      *
      ** If Extended Settlement is defined
     C                     MOVELD2DSSN    MGDSSN
     C                     MOVELD2DSS1    MGDSS1
      *
     C                     MOVE *BLANKS   WDRAB
     C                     MOVELMGDSS1    WDRAB
     C********** WP0102    IFEQ '/C'                                                        AR580937
     C********** WP0102    OREQ '/D'                                                        AR580937
     C********** WP0102    IFEQ '/C/'                                              AR580937 AR589197
     C********** WP0102    OREQ '/D/'                                              AR580937 AR589197
     C           WP0103    IFEQ '/C/'                                                       AR589197
     C           WP0103    OREQ '/D/'                                                       AR589197
     C                     MOVE *BLANKS   MGDSS1
     C**********           MOVELWP0335    MGDSS1                                            AR580937
     C                     MOVELWP0435    MGDSS1                                            AR580937
     C                     ELSE
     C           WP0101    IFEQ '/'
     C                     MOVE *BLANKS   MGDSS1
     C                     MOVELWP0235    MGDSS1
     C                     ENDIF
     C                     ENDIF
      *
     C                     ELSE
      *
      ** Otherwise, (Extended Settlement is not defined)
      *
     C           SMFLAG    IFEQ 'S'
     C           DPMV      ANDEQ'RP'
      *
     C           SMFLAG    OREQ 'S'
     C           DPMV      ANDEQ'PD'
      *
     C           SMFLAG    OREQ 'M'
     C           DPMV      ANDEQ'RR'
      *
     C           SMFLAG    OREQ 'M'
     C           DPMV      ANDEQ'PL'
      *
     C                     MOVELDPID      MGDSSN
      *
     C           W@IDEC    IFEQ 'Y'
     C                     MOVELSA0206    MGDSS1
     C                     ENDIF
      *
     C                     ELSE
      *
     C                     MOVELDPOD      MGDSSN
      *
     C           W@ODEC    IFEQ 'Y'
     C           EU0104    IFEQ '//XC'
     C           EU0104    OREQ '//XE'
     C                     MOVELEU0510    MGDSS1
     C                     ELSE
     C                     MOVELEU0210    MGDSS1
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
      *** Receiver of Securities and Receiver of Secuties-1
     C           WWEXSS    IFEQ 'Y'
     C           D2RSSN    ANDNE*BLANKS
     C           WWEXSS    OREQ 'Y'
     C           D2RSS1    ANDNE*BLANKS
      *
     C                     MOVELD2RSSN    MGRSSN
     C                     MOVELD2RSS1    MGRSS1
      *
     C                     MOVE *BLANKS   WDRAB
     C                     MOVELMGRSS1    WDRAB
     C********** WP0102    IFEQ '/C'                                                        AR580937
     C********** WP0102    OREQ '/D'                                                        AR580937
     C********** WP0102    IFEQ '/C/'                                              AR580937 AR589197
     C********** WP0102    OREQ '/D/'                                              AR580937 AR589197
     C           WP0103    IFEQ '/C/'                                                       AR589197
     C           WP0103    OREQ '/D/'                                                       AR589197
     C                     MOVE *BLANKS   MGRSS1
     C**********           MOVELWP0335    MGRSS1                                            AR580937
     C                     MOVELWP0435    MGRSS1                                            AR580937
     C                     ELSE
     C           WP0101    IFEQ '/'
     C                     MOVE *BLANKS   MGRSS1
     C                     MOVELWP0235    MGRSS1
     C                     ENDIF
     C                     ENDIF
      *
     C                     ELSE
      *
     C           SMFLAG    IFEQ 'S'
     C           DPMV      ANDEQ'RP'
      *
     C           SMFLAG    OREQ 'S'
     C           DPMV      ANDEQ'PD'
      *
     C           SMFLAG    OREQ 'M'
     C           DPMV      ANDEQ'RR'
      *
     C           SMFLAG    OREQ 'M'
     C           DPMV      ANDEQ'PL'
      *
     C                     MOVELDPOD      MGRSSN
      *
     C           W@ODEC    IFEQ 'Y'
     C           EU0104    IFEQ '//XC'
     C           EU0104    OREQ '//XE'
     C                     MOVELEU0510    MGRSS1
     C                     ELSE
     C                     MOVELEU0210    MGRSS1
     C                     ENDIF
     C                     ENDIF
      *
     C                     ELSE
      *
     C                     MOVELDPID      MGRSSN
      *
     C           W@IDEC    IFEQ 'Y'
     C                     MOVELSA0206    MGRSS1
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Account with Institution & A/C Line
     C           WWEXSS    IFEQ 'Y'
     C           D2AWIN    ANDNE*BLANKS
     C           WWEXSS    OREQ 'Y'
     C           D2AWIL    ANDNE*BLANKS
      *
      **  If D2AWIN and/or D2AWIL is defined
     C                     MOVELD2AWIN    MGAWIN
     C                     MOVELD2AWIL    MGAWIL
      *
     C                     MOVE *BLANKS   WDRAB
     C                     MOVELMGAWIL    WDRAB
      *                                                                                     AR549800
     C                     MOVELD2FAI1    MGFAI1                                            AR549800
      *
     C********** WP0102    IFEQ '/C'                                                        AR580937
     C********** WP0102    OREQ '/D'                                                        AR580937
      ***Extract*Position*3*-*35***************************************                     AR580937
     C********** WP0102    IFEQ '/C/'                                              AR580937 AR589197
     C********** WP0102    OREQ '/D/'                                              AR580937 AR589197
     C           WP0103    IFEQ '/C/'                                                       AR589197
     C           WP0103    OREQ '/D/'                                                       AR589197
      ** Extract Position 4 - 35                                                            AR580937
     C                     MOVE *BLANKS   MGAWIL
     C**********           MOVELWP0335    MGAWIL                                            AR580937
     C                     MOVELWP0435    MGAWIL                                            AR580937
      *
     C                     ELSE
      *
     C           WP0102    IFEQ '//'
      ** Extract Position 5 - 35
     C                     MOVE *BLANKS   MGAWIL
     C                     MOVELWP0535    MGAWIL
      *
      ** A/c with Inst Issuer Code
     C                     CALL 'AOCCSYR0'
     C                     PARM *BLANKS   WRTCD
     C                     PARM '*KEY   ' WOPTN
     C                     PARM MGNMCY    CYCD    3
     C                     PARM WP0304    CLSY    2
     C           SDCCSY    PARM SDCCSY    DSFDY
      *
      ** Handle Database Error
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD017       DBASE            ***************
     C                     MOVEL'SDCCSYPD'DBFILE           * DB ERROR 17 *
     C                     MOVELCYCD      DBKEY5           ***************
     C                     MOVE CLSY      DBKEY5
     C                     MOVELDBKEY5    DBKEY
     C                     MOVEL'SE1876  'DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Move Issuer Code (DPISCD) to MGAWIC
     C                     MOVELDPISCD    MGAWIC
      *
     C                     ELSE
      *
     C           WP0101    IFEQ '/'
      ** Extract Position 2 - 35
     C                     MOVE *BLANKS   MGAWIL
     C                     MOVELWP0235    MGAWIL
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
      **  Otherwise, (D2AWIN and/or D2AWIL is not defined)
     C                     ELSE
      *
      **  If At Start Date Processing
     C           SMFLAG    IFEQ 'S'
      *
     C           SDST      IFEQ 01
     C           SDST      OREQ 07
     C           SDST      OREQ 08
     C                     MOVELTSTN      MGAWIN
     C                     ENDIF
      *
     C                     ELSE
      **  Otherwise, (At Maturity Date Processing)
      *
     C           MDST      IFEQ 01
     C           MDST      OREQ 07
     C           MDST      OREQ 08
     C                     MOVELTMAN      MGAWIN
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
      ** Beneficiary of Money & A/C Line
     C           MGAWIN    IFNE *BLANKS
     C           MGAWIL    ORNE *BLANKS
      *
     C           WWEXSS    IFEQ 'Y'
     C           D2BOFN    ANDNE*BLANKS
     C           WWEXSS    OREQ 'Y'
     C           D2BOF1    ANDNE*BLANKS
      *
     C                     MOVELD2BOFN    MGBOFN
     C                     MOVELD2BOF1    MGBOF1
      *
     C                     MOVE *BLANKS   WDRAB
     C                     MOVELMGBOF1    WDRAB
      *                                                                                     AR549800
     C                     MOVELD2FBF1    MGFBF1                                            AR549800
      *
     C                     SELEC
     C********** WP0102    WHEQ '/C'                                                        AR580937
     C********** WP0102    OREQ '/D'                                                        AR580937
      ***Extract*position*3*-*35***************************************                     AR580937
     C********** WP0102    WHEQ '/C/'                                              AR580937 AR589197
     C********** WP0102    OREQ '/D/'                                              AR580937 AR589197
     C           WP0103    WHEQ '/C/'                                                       AR589197
     C           WP0103    OREQ '/D/'                                                       AR589197
      ** Extract position 4 - 35                                                            AR580937
     C                     MOVE *BLANKS   MGBOF1
     C**********           MOVELWP0335    MGBOF1                                            AR580937
     C                     MOVELWP0435    MGBOF1                                            AR580937
      *
     C           WP0102    WHEQ '//'
      ** Extract position 5 - 35
     C                     MOVE *BLANKS   MGBOF1
     C                     MOVELWP0535    MGBOF1
      *
      ** Ben of Money Issuer Code
     C                     CALL 'AOCCSYR0'
     C                     PARM *BLANKS   WRTCD
     C                     PARM '*KEY   ' WOPTN
     C                     PARM MGNMCY    CYCD    3
     C                     PARM WP0304    CLSY    2
     C           SDCCSY    PARM SDCCSY    DSFDY
      *
      ** Handle Database Error
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD023       DBASE            ***************
     C                     MOVEL'SDCCSYPD'DBFILE           * DB ERROR 23 *
     C                     MOVELCYCD      DBKEY5           ***************
     C                     MOVE CLSY      DBKEY5
     C                     MOVELDBKEY5    DBKEY
     C                     MOVEL'SE1876  'DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELDPISCD    MGBOFC
      *
     C           WP0101    WHEQ '/'
      ** Extract Position 2 - 35
     C                     MOVE *BLANKS   MGBOF1
     C                     MOVELWP0235    MGBOF1
      *
     C                     ENDSL
      *
     C                     ENDIF
     C                     ENDIF
      *
      ** Reallowance
     C                     Z-ADD*ZERO     MGISDI
      *
      ** Broker Commission ID
     C                     MOVE *BLANKS   MGTBCI
      *
      ** Broker Commission Amount
     C                     Z-ADD*ZERO     MGTBCA
      *
      ** Customer Com ID
     C                     MOVE *BLANKS   MGTCCI
      *
      ** Customer Com Amount
     C                     Z-ADD*ZERO     MGTCCA
      *
      ** Charge 1 ID
     C                     MOVE *BLANKS   MGTCI1
      *
      ** Charge Amount 1
     C                     Z-ADD*ZERO     MGTCA1
      *
      ** Charge 2 ID
     C                     MOVE *BLANKS   MGTCI2
      *
      ** Charge Amount 2
     C                     Z-ADD*ZERO     MGTCA2
      *
      ** Charge 3 ID
     C                     MOVE *BLANKS   MGTCI3
      *
      ** Charge Amount 3
     C                     Z-ADD*ZERO     MGTCA3
      *
      ** Charge 4 ID
     C                     MOVE *BLANKS   MGTCI4
      *
      ** Charge Amount 4
     C                     Z-ADD*ZERO     MGTCA4
      *
      ** Charge 5 ID
     C                     MOVE *BLANKS   MGTCI5
      *
      ** Charge Amount 5
     C                     Z-ADD*ZERO     MGTCA5
      *
      ** Charge 6 ID
     C                     MOVE *BLANKS   MGTCI6
      *
      ** Charge Amount 6
     C                     Z-ADD*ZERO     MGTCA6
      *
      ** Charge 7 ID
     C                     MOVE *BLANKS   MGTCI7
      *
      ** Charge Amount 7
     C                     Z-ADD*ZERO     MGTCA7
      *
      ** Tax ID
      *
     C                     MOVE *BLANKS   MGTAX1
      *
      ** Tax Amount
     C                     Z-ADD*ZERO     MGTAXA
      *
      ** Settlement Amount in Alternate Currency
     C           MGSTCY    IFNE MGNMCY
     C           MGDLAM    ADD  DPITRA    MGRESU
     C                     ENDIF
      *
      ** Original Currency
     C                     MOVE *BLANKS   MGOTRC
      *
      ** Original Amounts
     C                     Z-ADD*ZERO     MGOTRA
      *                                                                   CSW014
      *** If CSW014 is on AND Sender to Receiver (D2SRL1)                 CSW014
      *** field was entered, then, extract Original Ccy and Amount        CSW014
     C           CSW014    IFEQ 'Y'                                       CSW014
     C           D2SRL1    ANDNE*BLANKS                                   CSW014
     C                     EXSR SROCMT                                    CSW014
     C                     ENDIF                                          CSW014
      *
     C           CSW003    IFEQ 'Y'
     C                     Z-ADDDPTDER    MGDAXR
     C                     ENDIF
      *
      ** Nominal Decimal Place
     C                     Z-ADDNMDP      MGNMDP
      *                                                                   CSE006
      *** Check if Repurchase Agreements switchable  feature is on        CSE006
      *** If so, then set up repurchase information                       CSE006
      *                                                                   CSE006
     C           CSE006    IFEQ 'Y'                                       CSE006
     C           DPMV      IFEQ 'RP'                                      CSE006
     C           DPMV      OREQ 'PD'                                      CSE006
     C           DPMV      OREQ 'RR'                                      CSE006
     C           DPMV      OREQ 'PL'                                      CSE006
     C           DPMV      OREQ 'BB'                                      CSE006
     C           DPMV      OREQ 'BL'                                      CSE006
     C                     EXSR SRPIF                                     CSE006
     C                     ENDIF                                          CSE006
     C                     ENDIF                                          CSE006
      *                                                                                       CSW210
     C           CSW210    IFEQ 'Y'                                                           CSW210
     C                     MOVELD2DIDN    MGDIDN                                              CSW210
     C                     MOVELD2DAD1    MGDAD1                                              CSW210
     C                     MOVELD2DAD2    MGDAD2                                              CSW210
     C                     MOVELD2DAD3    MGDAD3                                              CSW210
     C                     MOVELD2DAD4    MGDAD4                                              CSW210
     C                     MOVELD2DLIN    MGDLIN                                              CSW210
      *                                                                                     AR581532
     C                     MOVE *BLANKS   WDRAB                                             AR581532
     C                     MOVELMGDLIN    WDRAB                                             AR581532
      *                                                                                     AR581532
     C                     SELEC                                                            AR581532
      *                                                                                     AR581532
     C********** WP0102    WHEQ '/C'                                               AR581532 AR580937
     C********** WP0102    OREQ '/D'                                               AR581532 AR580937
     C********** WP0102    WHEQ '/C/'                                              AR580937 AR589197
     C********** WP0102    OREQ '/D/'                                              AR580937 AR589197
     C           WP0103    WHEQ '/C/'                                                       AR589197
     C           WP0103    OREQ '/D/'                                                       AR589197
      *                                                                                     AR581532
      ***Extract*position*3*-*35***************************************            AR581532 AR580937
      ** Extract position 4 - 35                                                            AR580937
      *                                                                                     AR581532
     C                     MOVE *BLANKS   MGDLIN                                            AR581532
     C**********           MOVELWP0335    MGDLIN                                   AR581532 AR580937
     C                     MOVELWP0435    MGDLIN                                            AR580937
      *                                                                                     AR581532
     C           WP0102    WHEQ '//'                                                        AR581532
      *                                                                                     AR581532
      ** Extract position 5 - 35                                                            AR581532
      *                                                                                     AR581532
     C                     MOVE *BLANKS   MGDLIN                                            AR581532
     C                     MOVELWP0535    MGDLIN                                            AR581532
      *                                                                                     AR581532
     C           WP0101    WHEQ '/'                                                         AR581532
      *                                                                                     AR581532
      ** Extract Position 2 - 35                                                            AR581532
      *                                                                                     AR581532
     C                     MOVE *BLANKS   MGDLIN                                            AR581532
     C                     MOVELWP0235    MGDLIN                                            AR581532
      *                                                                                     AR581532
     C                     ENDSL                                                            AR581532
      *                                                                                     AR581532
     C                     MOVELD2FDBT    MGFDBT                                            AR549800
     C                     ENDIF                                                              CSW210
      *
      ** Write record to PF/MGF51NPD
     C                     MOVE @CHG      MGCHG                                               CSW025
     C                     WRITEMGF51ND0               52
      *
      ** Unable to write to PF/MGF51NPD
     C           *IN52     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD019       DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 019  *
     C                     MOVEL'MGF51NPD'DBFILE           * * * * * * * *
     C                     MOVEL'SE1876'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *                                                                   140428
     C                     COMIT                                          140428
      *
      ** Set Record-Extract-Flag (WEXTCT) to 'Y'
     C                     MOVEL'Y'       WEXTCT
      *
     C                     ENDSR
      *****************************************************************   CSW014
      /TITLE SR/SROCMT                                                    CSW014
      *****************************************************************   CSW014
      *                                                               *   CSW014
      *  SROCMT - Subroutine to extract Original Currency and Amount  *   CSW014
      *           from Sender to Receiver field                       *   CSW014
      *                                                               *   CSW014
      *  Called By: SRF5IN                                            *   CSW014
      *  Calls:                                                       *   CSW014
      *                                                               *   CSW014
      *****************************************************************   CSW014
      *                                                                   CSW014
     C           SROCMT    BEGSR                           *** SROCMT *** CSW014
      *                                                                   CSW014
     C                     MOVE *BLANKS   WWSR06  6                       CSW014
     C                     MOVELD2SRL1    WWSR06                          CSW014
     C           WWSR06    IFEQ '/OCMT/'                                  CSW014
      *                                                                   CSW014
      ** Original Currency (posn. 7 - 9)                                  CSW014
     C           3         SUBSTD2SRL1:7  MGOTRC                          CSW014
      *                                                                   CSW014
      ** Original Amount (posn. 4 - 16)                                   CSW014
     C           13        SUBSTD2SRL1:10 WWOTRA 13                       CSW014
     C           WWOTRA    IFNE *BLANKS                                   CSW014
      *                                                                   CSW014
     C                     MOVE *BLANKS   OA                              CSW014
     C                     Z-ADD1         X       20                      CSW014
     C                     Z-ADD1         I       20                      CSW014
     C           X         DOWLE13                                        CSW014
      *                                                                   CSW014
     C           1         SUBSTWWOTRA:X  WCHAR1  1                       CSW014
     C                     MOVE WCHAR1    WNUMB1  10                      CSW014
     C                     MOVE WNUMB1    WALPA1  1                       CSW014
     C           WCHAR1    IFEQ WALPA1                                    CSW014
     C                     MOVE WCHAR1    OA,I                            CSW014
     C                     ADD  1         I                               CSW014
     C                     ENDIF                                          CSW014
      *                                                                   CSW014
     C                     ADD  1         X                               CSW014
     C                     ENDDO                                          CSW014
      *                                                                   CSW014
     C                     MOVE *BLANKS   @OA                             CSW014
     C                     Z-ADD13        X                               CSW014
     C                     Z-ADD13        I                               CSW014
     C           X         DOWGE1                                         CSW014
     C           OA,X      IFNE *BLANK                                    CSW014
     C                     MOVE OA,X      @OA,I                           CSW014
     C                     SUB  1         I                               CSW014
     C                     ENDIF                                          CSW014
     C                     SUB  1         X                               CSW014
     C                     ENDDO                                          CSW014
     C                     MOVEA@OA       W@OTRA 13                       CSW014
     C                     MOVE W@OTRA    MGOTRA                          CSW014
      *                                                                   CSW014
     C                     ENDIF                                          CSW014
     C                     ENDIF                                          CSW014
      *                                                                   CSW014
     C                     ENDSR                           *** SROCMT *** CSW014
      *****************************************************************
      /TITLE SR/SRNRDT
      *****************************************************************
      *                                                               *
      *  SRNRDT - Find the next rate change date                      *
      *                                                               *
      *  Called By: SRF5IN                                            *
      *  Calls:                                                       *
      *                                                               *
      *****************************************************************
      *
     C           SRNRDT    BEGSR
      *
     C           RCFQ      IFNE *BLANKS
      *
      ** If SECTY/Rate Change Frequency (RCFQ) is not blank and
      **    SECTY/Next Change Date (NCHD) is greater than today's date,
      **    then, Move SECTY/Next Change Date to Next Rate Change Date
     C           NCHD      IFGT BJRDNB
     C                     Z-ADDNCHD      MGNRDT
     C                     ENDIF
      *
     C                     ELSE
      *
      ** Otherwise, (SECTY/Rate Change Frequency is blank)
      **    If SECTY/Coupon Rate Day 1 is not *Zero and
      **       SECTY/Coupon Rate is not *Zero, then, determine
      **       the Next Rate Change Date via SR/ZNCR using
      **       Value Date + 1 as the Event Control Date
      *
     C           CD01      IFNE *ZERO
     C           CPNR      ANDNE*ZERO
     C                     Z-ADD*ZERO     ECD     50
     C           MGVDAT    ADD  1         ECD
     C                     EXSR ZNCR
     C                     Z-ADDNCR       MGNRDT
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/DAYSAC
      *****************************************************************
      *                                                               *
      *  DAYSAC - Subroutine to calculate the Number of Days of       *
      *           Accrued Interest.                                   *
      *                                                               *
      *  Called By: SRF5IN                                            *
      *  Calls: ZLCD, ZNCD, ZDAYS                                     *
      *                                                               *
      *****************************************************************
      *
     C           DAYSAC    BEGSR                           *** DAYSAC ***
      *
      ***  Determine the Start and End Dates for Calculating the number
      ***  of Days in the Period.
      *
      ***  Cum Coupon Trades:
      *
     C           MGEXIN    IFEQ 'C'
      *
      ***  The Start Date is the Last Coupon Date before (or on) the
      ***  Value Date of the Trade.
      *
     C                     Z-ADDMGVDAT    ZECD
     C                     EXSR ZLCD
     C                     Z-ADDZZLCD     ZSDATE
      *
      ***  The End Date is the Value Date of the Trade.
      *
     C                     Z-ADDMGVDAT    ZEDATE
     C                     ENDIF
      *
      ***  Ex-Coupon Trades:
      *
     C           MGEXIN    IFEQ 'X'
      *
      ***  The Start Date is the Value Date of the Trade.
      *
     C                     Z-ADDMGVDAT    ZSDATE
      *
      ***  The End Date is the Next Coupon Date after (or on) the Value
      ***  Date of the Trade.
      *
     C                     Z-ADDMGVDAT    ECD
     C                     EXSR ZNCD
     C                     Z-ADDNCD       ZEDATE
     C                     ENDIF
      *
      ***  Calculate Number of Days from Start Date to End Date.
      *
     C                     EXSR ZDAYS
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/SRPIF                                                     CSE006
      *****************************************************************   CSE006
      *                                                               *   CSE006
      *  SRPIF  - Subroutine to set up Repurchase Information         *   CSE006
      *                                                               *   CSE006
      *  Called By: SRF5IN                                            *   CSE006
      *  Calls:                                                       *   CSE006
      *                                                               *   CSE006
      *****************************************************************   CSE006
      *                                                                   CSE006
     C           SRPIF     BEGSR                           *** SRPIF ***  CSE006
      *                                                                   CSE006
      *** Move data into additional fields in PF/MGF51NPD                 CSE006
      *                                                                   CSE006
     C                     MOVE DPAD      MGARF                           CSE006
     C                     Z-ADDINTR      MGRR                            CSE006
     C                     MOVE MDAT      MGMD                            CSE006
      *                                                                   CSE006
      *** Check if there are Certificate Number for the depot movement    CSE006
      *                                                                   CSE006
     C           DPRN      SETLLSECRTNL1                                  CSE006
     C           DPRN      READESECRTNL1                 30               CSE006
      *                                                                   CSE006
     C           *IN30     IFEQ '0'                                       CSE006
     C                     MOVE 'Y'       MGCRTN                          CSE006
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
     C                     ENDSR                                       *  CSE006
      *                                                                   CSE006
      *****************************************************************   CSE006
      /TITLE SR/SRAUDT
      *****************************************************************
      *                                                               *
      * SRAUDT- Run Control Process                                   *
      *                                                               *
      * Called by: Main Processing                                    *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           SRAUDT    BEGSR
      *
      ***  Write Heading and control information
     C                     WRITEHEADAU
     C                     WRITECONTROL
      *
      ***  If there is a DB Error, Output the DB Error Information.
     C           *INU7     IFEQ '1'
     C                     WRITEDBERROR
     C                     ENDIF
      *
     C                     ENDSR
     C/COPY WNCPYSRC,SE1876C004
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     SETON                     U7U8LR
     C                     EXSR SRAUDT
     C                     DUMP
     C                     ENDIF
      *
     C                     RETRN
      *
     C                     ENDSR
      *
      ********************************************************************
     C/TITLE SR/ZNCD
     C/COPY ZSRSRC,ZNCDZ3
     C/TITLE SR/ZDAYS
     C/COPY ZSRSRC,ZDAYSZ2
      /TITLE SR/ZDATE1
     C/COPY ZSRSRC,ZDATE1Z2
      /TITLE SR/ZDATE2
     C/COPY ZSRSRC,ZDATE2Z2
     C/TITLE SR/ZNCR
     C/COPY ZSRSRC,ZNCRZ1
     C/TITLE SR/ZPRCO
     C/COPY ZSRSRC,ZPRCOZ1
     C/TITLE SR/ZDYYR
     C/COPY ZSRSRC,ZDYYRZ3
     C/TITLE SR/ZYLDO
     C/COPY ZSRSRC,ZYLDOZ1
     C/TITLE SR/ZYLD
     C/COPY ZSRSRC,ZYLDZ2
     C/TITLE SR/ZLCD
     C/COPY ZSRSRC,ZLCDZ1
     C/TITLE SR/ZACCZ
     C/COPY ZSRSRC,ZACCZZ1
     C/TITLE SR/ZCNSD
     C/COPY ZSRSRC,ZCNSDZ1
     C/TITLE SR/ZCALCD
     C/COPY ZSRSRC,ZCALCD
     C/TITLE SR/ZACCR
     C/COPY ZSRSRC,ZACCRZ1
     C/TITLE SR/ZRATE
     C/COPY ZSRSRC,ZRATEZ1
     C/TITLE COMPILE-TIME ARRAYS
      ***
**  CPY@  - Copyright
(c) Finastra International Limited 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**   ZF29 DAY NOS OF FEB 29 TO 2096 (INCLUDING 2000)
0006001521029820444305904073650882610287117481320914670161311759219053
2051421975234362489726358278192928030741322023366335124365853804639507
40968424294389045351
**  POWER8 - ARRAY OF POWERS FOR CURRENCY CONVERSION
00000001
00000010
00000100
00001000
00010000
00100000
01000000
10000000
