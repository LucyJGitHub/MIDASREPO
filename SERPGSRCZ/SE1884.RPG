     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Non-Fung Trades with no Certifcts')           *
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module
     F*                                                               *
     F*  SE1884 - NON-FUNGIBLE TRADES WITH NO CERT NUMBERS REPORT     *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. 226449             Date 24Jun15               *
      *                 259164             Date 02Apr12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CSE071             Date 19Jul05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 BUG3122            Date 17Jul04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      *                 208241             Date 21Aug02               *
      *                 180056             Date 18Oct00               *
      *                 206316             Date 19Jul02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CSE031             Date 09Nov01               *
      *                 180707             Date 25Jul00               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 127566             Date 09May01               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001             DATE 18Jun99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAC005             Date 21Dec98               *
      *                 130862             Date 18Oct98               *
      *                 151945             Date 06Jan99               *
      *                 CSE005             Date 04Feb97               *
      *                 111941             Date 16Dec96               *
     F*                 CAC002             DATE 15SEP96               *
     F*                 109542             DATE 15SEP96               *
     F*                 100017             DATE 18APR96               *
     F*                 092961             DATE 04OCT95               *
     F*                 086491             DATE 15JUN95               *
     F*                 S01446             DATE 04NOV93               *
     F*                 066915             DATE 14FEB94               *
     F*                 S01401             DATE 16JUN93               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  226449 - Recompiled over changes made in /COPY ZDYYRZ3.      *
      *         - Applied for MD034387.                               *
      *  259164 - Database error at 004, file TRADSDY1. Program reads *
      *           deleted record (RECI = '*') in TRADSDL4 and the     *
      *           extension record doesn't exist in LF/TRADSDY1.      *
      *         - Applied for AR943222 (Child:AR943225)               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  BUG3122 - Recompiled due to changes in SDSECSPD.             *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  208241 - Recompiled over changed /CPY ZSDESCZ3.              *
      *  180056 - Recompiled over changed in ZDYYRZ3.                 *
     F*  206316 - Recompiled over changed ZLCDZ1.                     *
     F*  CSE031 - Coupon Fixing for Floating Rate Notes.              *
     F*           (recompilation due to change to ZNCD/ZLCD)          *
     F*  180707 - No Position Settlements generated. Recompile over   *
     F*           changes in /COPY ZNCDZ3.                            *
      *  127566 - Recompiled over changed ZSDESCZ3                    *
     F*  CPB001 - 'Private Banking' Module                            *
      *  CAC005 - PCA-SE Enhancement.                                 *
     F*  130862 - Recompiled over ZLCDZ1 & ZDAYSZ2 changes.           *
     F*  151945 - Recompiled Over changed ZDYYRZ3 .                   *
     F*  CSE005 - Effect of holidays on FRN Coupon Dates              *
     F*           (recompilation due to change to ZNCD/ZLCD)          *
     F*  111941 - Figures Formatted Incorrectly in SE1884P1.          *
     F*  CAC002 - Profit Centre Accounting Development - Phase 2      *
     F*           If Analytical Accounting is available, the profit   *
     F*           centre for each Charge/Commission Code would be     *
     F*           reported.  The trade's Book and Transaction Profit  *
     F*           Centres will also be reported.  If the settlement   *
     F*           currency is not the same as the nominal currency,   *
     F*           the FX Margin Points and Amount will also be        *
     F*           reported.                                           *
     F*  109542 - Add SR/ZLCD and recompile over changes to ZDYYRZ3,  *
     F*           ZLCDZ1 and ZNCDZ3.                                  *
     F*  100017 - Position settlement unable to pick up day adj. on   *
     F*           SECTYD. Change subroutine to use DADJN to stop      *
     F*           interest day adjustment being used for amortisation.*
     F*           Recompile over amended subroutine ZDAYS.            *
     F*  092961 - Recompile over ZDAYSZ2 which was amended by 086491  *
     F*           (Wrong number of days calculated for day basis 1    *
     F*           when start or end day is last day of February)      *
     F*  086491 - Wrong nbr of days calculated for day basis 1        *
     F*           when start or end day is last day of February       *
     F*           Recompiled for ZSRSRC/ZDAYSZ2 changes.              *
     F*  S01446 - CUSTOMER COMMISSIONS (COURTAGE) ENHANCEMENT.        *
     F*           Recompiled due to changes in PF/SECGTD.             *
     F*  066915 - RECOMPILED as CURRENT FACTOR amended from 9,8       *
     F*           to 10,9 by GEMS 052254.                             *
     F*  S01401 - MT5xx SWIFT Messages Feature: New program           *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
      *
     FTRADSDL4IF  E           K        DISK         KINFSR *PSSR
     FTRADSDY1IF  E           K        DISK         KINFSR *PSSR
     FSECTY   IF  E           K        DISK         KINFSR *PSSR
     FSECRTNL1IF  E           K        DISK         KINFSR *PSSR
     FINVTP   IF  E           K        DISK         KINFSR *PSSR
     FSECGT   IF  E           K        DISK         KINFSR *PSSR
      *
     FSE1884P1O   E                    PRINTER      KINFDS SPOOL1     UC
     F                                              KINFSR *PSSR
     FSE1884AUO   E                    PRINTER      KINFDS SPOOLU
     F                                              KINFSR *PSSR
      /TITLE FUNCTION OF INCICATORS
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*    11  - MULTIBRANCHING INDICATOR                             *
     F*    15  - Print file, SE1884P1, has been opened at least once  *
     F*    19  - Analytical accounting is installed                   *   CAC002
     F*    20  - Nominal currency not = Settlement currency           *   CAC002
     F*    21  - Portfolio customer                                   *   CAC002
     F*    39  - Portfolio Management is installed                    *   CAC002
     F*    40  - Private Banking module is installed                  *   CPB001
     F*    50  - END OF INPUT FILE / FILE READ ERROR                  *
     F*                                                               *
     F*    59  - LOKUP OPERATION                                      *
     F*                                                               *
     F*    64  - CHAIN TO TRADSDY1                                    *
     F*    65  - CHAIN TO SECRTNL1                                    *
     F*    67  - CHAIN TO SECTY                                       *
     F*    68  - CHAIN TO INVTP                                       *
     F*    72  - CHAIN TO SECGT                                       *
     F*                                                               *
     F*    88  - AUTO-CHARGES INDICATOR  ON                           *
     F*    98  - Date Format: DDMMYY (off); MMDDYY (on)               *
     F*  90-99 - USED IN STANDARD SUB-ROUTINES                        *
     F*                                                               *
     F*  U7+U8 - Database Error                                       *
     F*                                                               *
     F*****************************************************************
      /TITLE SUBROUTINE INDEX
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E   I N D E X                              *
     F*                                                               *
     F*  INIT   - Initial Processing                                  *
     F*  PROCES - Perform Detail Processing                           *
     F*  FORMAT - Format Output fields                                *
     F*  SCONV   - Converts an amount from one currency to another    *
     F*                                                               *
     F*  *PSSR  - Program error handling routine                      *
     F*                                                               *
     F*  ZDATE1 - Converts date to a day number                       *
     F*  ZDATE2 - Converts day number to a date                       *
     F*  ZDAYS  - Calculates no. days between 2 dates                 *
     F*  ZDYYR  - Calculates no. days in interest year                *
     F*  ZEVCD  - Determines event control date                       *
     F*  ZNCD   - Determines next coupon date                         *
     F*  ZSDESC - Formats security description                        *
     F*  ZSEDIT - Convert amount for output                           *
     F*                                                               *
     F*****************************************************************
     E/TITLE E-SPECIFICATIONS
     E                    CPY@    1   1 80
      ***  Array for Object Copyright Statement.
      *
     E                    SET    11  11  2 0
      ** SETTLEMENT METHOD
      *
     E                    TXT     5  11 14
      ** TEXT FOR SETTLEMENT METHOD
      *
     E                    VAL     6   6  1
      ** CLEARANCE TYPE/PAY CODE
      *
     E                    CTT     3   6 19
      ** TEXT FOR CLEARANCE TYPE
      *
     E                    PCT     3   6 22
      ** TEXT FOR PAY CODE
      *
     E/COPY ZSRSRC,ZDATE2Z1
      *
     E/COPY ZSRSRC,ZDAYSZ1
      *
     E/COPY ZSRSRC,ZPOWER8Z1
      *
     E/COPY ZSRSRC,ZPOWERZ1
      *
     E/COPY ZSRSRC,ZSEDITZ1
      *
     E/COPY ZSRSRC,ZSDESCZ1
      *
     E/COPY ZSRSRC,ZNCDZ1
     I/TITLE I-SPECIFICATIONS
     ITRADSDF
      **   RENAME FIELDS NAMES ON TRADSDL4
     I              LCD                             WWLCD
     I              CHTP                            WWCHTP
     I              TNLU                            WWTNLU
     I*                                                                   CAC002
     ISECGTDF                                                             CAC002
     I              PRFC                            PRFCGT                CAC002
      *
     IRUNDAT      DS
      ***  Data Structure to obtain Multibranching Indicator.
     I                                       13  13 WMBIN
      *
     IDSFDY     E DSDSFDY
      **  Short DS for access programs
      *
     IDSSDY     E DSDSSDY
      **  Long DS for access programs
      *
     ISDBANK    E DSSDBANKPD
      ** Bank details
     I              BJDNWD                          DNWD
      *
     ISDGELR    E DSSDGELRPD
      ** General ledger details
     I              BKAPDT                          APDA
      *
     ISDSTRD    E DSSDSTRDPD
      ** Securities Trading ICD
     I              BVRIDF                          RSFD
      *
     ISDBOOK    E DSSDBOOKPD
      ** Book details
      *
     ISDCURR    E DSSDCURRPD
      ** Currency details
      *
     ISDCUST    E DSSDCUSTPD
      ** Customer details
      *
     ISDDLST    E DSSDDLSTPD
      ** Trade Sub-Types
      *
     ISDNOST    E DSSDNOSTPD
      ** Nostro details
     I              QQACCD                          QQACCX                                    CGL029
      *
     ISDBRCH    E DSSDBRCHPD
      ** Branch details
     I              QQDFAC                          QQDFAX                                    CGL029
      *
     ISDMMOD    E DSSDMMODPD                                              CAC002
      ** MIDAS Modules                                                    CAC002
      *                                                                   CAC002
     ISDSECS    E DSSDSECSPD                                              CAC002
      ** Security Clients                                                 CAC002
     I              QQACCD                          QQACCY                                    CGL029
      *                                                                   CAC002
     IDBERR       DS                            256
      * Data structure for data-base error processing.
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *
     IPSDS       SDS
      ** PROGRAM STATUS DATA STRUCTURE
     I                                      244 253 WSID
     I                                      254 263 USID
      *
     ISPOOL1      DS
      ***  File Information Data Structure for SE1884P1.
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 367 3680LINE1
      *
     ISPOOLU      DS
      ***  File Information Data Structure for SE1884AU.
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
     I                                    B 367 3680LINE2
      *
     I/COPY ZSRSRC,ZSDESCZ2
     I/COPY ZSRSRC,ZSEDITZ2
     I/COPY ZSRSRC,ZNCDZ2
      *
     IPAYDS       DS
      * DATA STRUCTURE FOR PAY FROM/TO FIELD
     I**********                              1  12 WWPAY                                     CGL029
     I                                        1  18 WWPAY                                     CGL029
     I                                        1   6 WWPY06
     I                                        3  12 WWPY10
     I**********                              7  10 WWPY04                                    CGL029
     I**********                             11  12 WWPY02                                    CGL029
     I                                        7  16 WWPY04                                    CGL029
     I                                       17  18 WWPY02                                    CGL029
      *
     IKEY2DS      DS
      * DATA STRUCTURE KEY 2
     I                                        1   6 WWKEY2
     I                                        1   3 NMCY
     I                                        4   6 SITP
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M     S T A R T                                  *
      *================================================================
      *
      ***  Perform Initialisation.
     C                     EXSR INIT
      *
      ***  Perform Detail Processing.
     C                     EXSR PROCES
      *
      *================================================================
      *  P R O G R A M     E N D                                      *
      *                                                               *
     C                     SETON                     LR
     C                     RETRN
      *                                                               *
      *================================================================
      /TITLE SR/INIT
      *****************************************************************
      *                                                               *
      *  INIT   - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls    : ZEVCD                                             *
      *                                                               *
      *****************************************************************
      *
     C           INIT      BEGSR
      *
      ***  Copyright Statement.
     C                     MOVEACPY@      BIS@   80
      *
      ** KLIST FOR INVTP
     C           KL2       KLIST
     C                     KFLD           NMCY
     C                     KFLD           SITP
      *
      ** KLIST FOR SECGT
     C           KL3       KLIST
     C                     KFLD           WWCCY   3
     C                     KFLD           WWCCOM  2
      *
      ** ACCEPT ENTRY PARAMETERS
     C           *ENTRY    PLIST
     C                     PARM           PSEQ    5
     C                     PARM           PBRCA   3
      *                                                                   CAC002
      **  Access MIDAS Module                                             CAC002
      *                                                                   CAC002
     C                     CALL 'AOMMODR0'                                CAC002
     C                     PARM '*MSG   ' WRTCD                           CAC002
     C                     PARM '*FIRST ' WOPTN                           CAC002
     C           SDMMOD    PARM SDMMOD    DSFDY                           CAC002
      *                                                                   CAC002
     C           WRTCD     IFNE *BLANKS                                   CAC002
     C                     MOVEL'010'     DBASE            ************   CAC002
     C                     MOVEL'SDMMODPD'DBFILE           * DBERR 10 *   CAC002
     C                     MOVE *BLANKS   DBKEY            ************   CAC002
     C                     EXSR *PSSR                                     CAC002
     C                     ENDIF                                          CAC002
      *                                                                   CAC002
      **  Set up analytical accounting indicator                          CAC002
      *                                                                   CAC002
     C           BGN0ST    IFEQ 'Y'                                       CAC002
     C                     MOVE '1'       *IN19                           CAC002
     C                     ELSE                                           CAC002
     C                     MOVE '0'       *IN19                           CAC002
     C                     ENDIF                                          CAC002
      *                                                                   CAC002
      **  Set up Portfolio Management indicator                           CAC002
      *                                                                   CAC002
     C           BGPFMG    IFEQ 'Y'                                       CAC002
     C                     MOVE '1'       *IN39                           CAC002
     C                     ELSE                                           CAC002
     C                     MOVE '0'       *IN39                           CAC002
     C                     ENDIF                                          CAC002
      *                                                                   CPB001
      **  Set up Private Banking module indicator                         CPB001
      *                                                                   CPB001
     C           BGN4ST    IFEQ 'Y'                                       CPB001
     C                     MOVE '1'       *IN40                           CPB001
     C                     ELSE                                           CPB001
     C                     MOVE '0'       *IN40                           CPB001
     C                     ENDIF                                          CPB001
      *
      ** Access Bank Details
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           WRTCD     IFNE *BLANKS
     C                     Z-ADD001       DBASE            ***************
     C                     MOVEL'SDBANKPD'DBFILE           * DB ERROR 01 *
     C                     MOVE *BLANKS   DBKEY            ***************
     C                     EXSR *PSSR
     C                     END
      *
      ***  Check System Date Format, DDMMYY (*IN98 off)
      ***                            MMDDYY (*IN98 on).
      *
     C           BJDFIN    IFEQ 'M'
     C                     SETON                     98
     C                     END
      *
      ***  Access RUNDAT Data Area for Multi-Branching Indicator.
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
     C           WMBIN     IFEQ 'Y'
     C                     SETON                     11
     C                     END
      *
     C**********           CALL 'AOGELRR0'                                                    CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM '*MSG   ' WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C********** SDGELR    PARM SDGELR    DSFDY                                               CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
      *
     C           WRTCD     IFNE *BLANKS
     C                     MOVEL'002'     DBASE            **************
     C                     MOVEL'SDGELRPD'DBFILE           * DB ERROR 2 *
     C                     MOVE *BLANKS   DBKEY            **************
     C                     EXSR *PSSR
     C                     END
      *
      ** DETERMINE EVENT CONTROL DATE
     C                     EXSR ZEVCD
      *
     C                     CALL 'AOSTRDR0'
     C                     PARM '*MSG   ' WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDSTRD    PARM SDSTRD    DSSDY
      *
     C           WRTCD     IFNE *BLANKS
     C                     MOVEL'003'     DBASE            **************
     C                     MOVEL'SDSTRDPD'DBFILE           * DB ERROR 3 *
     C                     MOVE *BLANKS   DBKEY            **************
     C                     EXSR *PSSR
     C                     END
      *
      ** Check if CAC005 - PCA-SE Enhancement is on.                      CAC005
      *                                                                   CAC005
     C                     MOVE 'N'       CAC005  1                       CAC005
     C                     CALL 'AOSARDR0'                                CAC005
     C                     PARM *BLANKS   @RTCD   7                       CAC005
     C                     PARM '*VERIFY' @OPTN   7                       CAC005
     C                     PARM 'CAC005'  @SARD   6                       CAC005
      *                                                                   CAC005
     C           @RTCD     IFEQ *BLANK                                    CAC005
     C                     MOVE 'Y'       CAC005                          CAC005
     C                     ENDIF                                          CAC005
      *
      ** CHECK IF AUTO CHARGE PRESENT
     C           BVACOP    IFEQ 'Y'
     C                     MOVE '1'       *IN88
     C                     END
      *
      ** DEFINE WORKFIELDS
     C           *LIKE     DEFN TCSR      WWTCSR
     C           *LIKE     DEFN RRPAYT    WWPAYO
     C                     Z-ADD0         WWCNT   50
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/PROCES
      *****************************************************************
      *                                                               *
      *  PROCES - Subroutine to do the Detail Processing.             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls: FORMAT                                                *
      *                                                               *
      *****************************************************************
      *
     C           PROCES    BEGSR
      *
      ***  If Branch selected then set lower limits
     C           PBRCA     IFNE 'ALL'
     C           PBRCA     SETLLTRADSDL4
     C                     ENDIF
      *
     C                     READ TRADSDL4                 50
      *
      ** If no branch selected then read to end of file;
      ** if branch selected read to change of branch or end of file;
     C           PBRCA     DOWEQ'ALL'
     C           *IN50     ANDEQ'0'
     C           PBRCA     ORNE 'ALL'
     C           *IN50     ANDEQ'0'
     C           TDBA      ANDEQPBRCA
      *                                                                                       259164
      ** Ignore deleted record (RECI = '*') in TRADSDL4                                       259164
      *                                                                                       259164
     C           RECI      IFNE '*'                                                           259164
      *
      ** DETERMINE IF THE TRADE IS NON-FUNGIBLE
     C           TDRF      CHAINTRADSDY1             64
      *
      * IF RECORD NOT FOUND - DATABASE ERROR
     C           *IN64     IFEQ '1'
     C                     MOVEL'004'     DBASE            **************
     C                     MOVEL'TRADSDY1'DBFILE           * DB ERROR 4 *
     C                     MOVELTDRF      DBKEY            **************
     C                     EXSR *PSSR
     C                     END
      *
     C           T1FTID    IFEQ 'N'
      *
      ** ACCESS LF/SECRTNL1 USING OF TRADE REFERENCE
     C           TDRF      CHAINSECRTNL1             65
      *
      ** IF RECORD NOT FOUND - FORMAT AND PRINT DETAILS OF THE TRADE
     C           *IN65     IFEQ '1'
     C                     EXSR FORMAT
     C                     EXSR PRINT
     C                     ADD  1         WWCNT
     C                     END
      *
     C                     END
      *                                                                                       259164
     C                     ENDIF                                                              259164
      *
     C                     READ TRADSDL4                 50
     C                     END
      *
      ** If print file open then right 'End of Report' and close file
     C           *IN15     IFEQ '1'
     C                     WRITETRAILP1
     C                     CLOSESE1884P1
     C                     END
      *
      ** OUTPUT RUN CONTROL REPORT
     C                     EXSR AUDIT
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/FORMAT
      *****************************************************************
      *                                                               *
      *    FORMAT    - FORMAT OUTPUT FOR REPORT                       *
      *                                                               *
      *    CALLED BY - Main Processing                                *
      *    CALLS     - ZSDESC, ZSEDIT, ZDATE2, FMTPFT, ZDAYS, ZNCD    *
      *                ZDYYR, SCONV                                   *
      *                                                               *
      *****************************************************************
      *
     C           FORMAT    BEGSR
      *                                                                   CAC005
      ** If CAC005 is on, retrieve user book from AOBKPCR0.               CAC005
      *                                                                   CAC005
     C           CAC005    IFEQ 'Y'                                       CAC005
     C                     CALL 'AOBKPCR0'                                CAC005
     C                     PARM           PRTCD   7                       CAC005
     C                     PARM '*USER  ' POPTN   7                       CAC005
     C           PMBKCD    PARM *BLANKS   PMBKCD  2                       CAC005
     C           PMPRFC    PARM *BLANKS   PMPRFC  4                       CAC005
     C                     PARM TDBK      PMPSBK  2                       CAC005
      *                                                                   CAC005
     C           PRTCD     IFEQ *BLANKS                                   CAC005
     C                     MOVELPMBKCD    TDBK                            CAC005
     C                     ENDIF                                          CAC005
     C                     ENDIF                                          CAC005
      *
      * FORMAT  CUSTOMER REPORT NAME/TOWN FOR COUNTERPARTY CUSTOMER NO.
      *    IF NO RECORD/RECORD DELETED/ - PRINT BLANK
     C                     MOVELTCNR      WCUST  10
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   WRTCD
     C                     PARM '*KEY   ' WOPTN
     C                     PARM           WCUST  10
     C                     PARM *BLANKS   WKYST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           WRTCD     IFNE *BLANKS
     C                     MOVE *BLANK    RRRNMC
     C                     MOVE *BLANK    RRTWNC
     C                     ELSE
     C                     MOVE BBCRNM    RRRNMC
     C                     MOVE BBCRTN    RRTWNC
     C                     END
      *
      * FORMAT SECURITY SHORTNAME / DESCRIPTION
     C           TDSS      CHAINSECTY                67
     C           *IN67     IFEQ '1'
     C           RECI      OREQ '*'
     C                     MOVEL'005'     DBASE            **************
     C                     MOVEL'SECTY'   DBFILE           * DB ERROR 5 *
     C                     MOVELTDSS      DBKEY            **************
     C                     EXSR *PSSR
     C                     END
      *
      * EXECUTE SR/ZSDESC TO DETERMINE SECURITY REPORT NAME
     C                     MOVE SRPN      ZSRPN
     C                     MOVE *BLANK    ZSFN1
     C                     MOVE *BLANK    ZSFN2
     C                     EXSR ZSDESC
     C                     MOVELZRNME     RRRNME
      *
      * ACCESS INVESTMENT TYPE FOR PROCESSING TYPE
     C           KL2       CHAININVTP                68
     C           *IN68     IFEQ '1'
     C           RECI      OREQ '*'
     C                     MOVEL'006'     DBASE            **************
     C                     MOVEL'INVTP'   DBFILE           * DB ERROR 6 *
     C                     MOVELWWKEY2    DBKEY            **************
     C                     EXSR *PSSR
     C                     END
      *
      * FORMAT BOOK NAMES- ACCESS SDBOOKPD FILE USING KEY -
      *    IF NO RECORD/RECORD DELETED - PRINT BLANK
     C                     CALL 'AOBOOKR0'
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*KEY   ' WOPTN   7
     C                     PARM TDBK      WBOOK   2
     C           SDBOOK    PARM SDBOOK    DSFDY
      *
     C           WRTCD     IFNE *BLANKS
     C                     MOVE *BLANK    RRBOKD
     C                     ELSE
     C                     MOVE BDBKD     RRBOKD
     C                     END
      *
      * FORMAT TRADE SUB TYPE NAME - ACCESS PF/SDDLSTPD
     C                     CALL 'AODLSTR0'
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*KEY   ' WOPTN   7
     C                     PARM TSUB      WDSNB   3
     C           SDDLST    PARM SDDLST    DSFDY
      *
     C           WRTCD     IFNE *BLANKS
     C                     MOVE *BLANK    RRDSNM
     C                     ELSE
     C                     MOVE ASDSTN    RRDSNM
     C                     END
      *
      * FORMAT MARKET INDICATOR TEXT
     C                     MOVE *BLANK    RRLTDM
     C           TDMI      IFEQ 'G'
     C                     MOVEL'GREY'    RRLTDM
     C                     ELSE
     C           TDMI      IFEQ 'P'
     C                     MOVEL'PRIMARY' RRLTDM
     C                     ELSE
     C           TDMI      IFEQ 'S'
     C                     MOVEL'SECOND'  RRLTDM
     C                     MOVE 'ARY'     RRLTDM
     C                     END
     C                     END
     C                     END
      *
      * FORMAT NOMINAL
     C                     Z-ADDTNMD      ZDECS
     C                     Z-ADDNOML      ZFLD15
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RRNOML
      *
      * FORMAT TRADE DATE
     C                     Z-ADDTDDT      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RRTDDT
      *
      * FORMAT VALUE DATE
     C                     Z-ADDTDVD      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RRTDVD
      *
      * FORMAT PRICE
     C***********          Z-ADDPRIC      ZFLD15                          111941
     C                     MOVE PRIC      ZFLD15                          111941
     C                     Z-ADD8         ZDECS
     C                     MOVE 'L'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RRPRIC
      *
      * FORMAT SETTLEMENT TEXT
     C                     MOVEL*BLANK    RRLSMT
     C                     Z-ADD1         X       20
     C           SMTH      LOKUPSET,X                    59
     C           *IN59     IFEQ '1'
     C                     MOVEATXT,X     RRLSMT
     C                     END
      *
      * FORMAT PAY FROM DEPENDING ON SETTLEMENT METHOD
     C                     MOVE 'F'       WWPAYC  1
     C                     MOVE PYFM      WWPAY
     C                     EXSR FMTPFT
     C                     MOVE WWPAYO    RRPAYF
      *
      * FORMAT PAY TO DEPENDING ON SETTLEMENT METHOD
     C                     MOVE 'T'       WWPAYC  1
     C                     MOVE PAYT      WWPAY
     C                     EXSR FMTPFT
     C                     MOVE WWPAYO    RRPAYT
      *
      * FORMAT CLEARANCE TYPE TEXT
     C                     Z-ADD1         X
     C           CLTY      LOKUPVAL,X                    59
     C           *IN59     IFEQ '1'
     C                     MOVEACTT,X     RRCLRX
     C                     END
      *
      * FORMAT DELIVER TO SHORTNAME
      *    READ PF/SDCUSTPD
      *    IF NO RECORD/RECORD DELETED/ - PRINT BLANK
     C                     MOVELDELT      WCUST  10
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   WRTCD
     C                     PARM '*KEY   ' WOPTN
     C                     PARM           WCUST  10
     C                     PARM *BLANKS   WKYST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           WRTCD     IFNE *BLANKS
     C                     MOVE *BLANK    RRNMDT
     C                     ELSE
     C                     MOVE BBCSSN    RRNMDT
     C                     END
      *
      * FORMAT DELIVER FROM SHORTNAME
      *    READ PF/SDCUSTPD
      *    IF NO RECORD/RECORD DELETED/ - PRINT BLANK
     C                     MOVELDELF      WCUST  10
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   WRTCD
     C                     PARM '*KEY   ' WOPTN
     C                     PARM           WCUST  10
     C                     PARM *BLANKS   WKYST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           WRTCD     IFNE *BLANKS
     C                     MOVE *BLANK    RRNMDF
     C                     ELSE
     C                     MOVE BBCSSN    RRNMDF
     C                     END
      *
      * FORMAT PAY CODE TEXT
     C                     Z-ADD1         X
     C           PCOD      LOKUPVAL,X                    59
     C           *IN59     IFEQ '1'
     C                     MOVEAPCT,X     RRPAYX
     C                     END
      *
      * FORMAT PRIORITY CODE TEXT
     C                     MOVE *BLANKS   RRPRCD
     C           PRYC      IFEQ 'R'
     C                     MOVEL'NORMAL'  RRPRCD
     C                     ELSE
     C           PRYC      IFEQ 'P'
     C                     MOVEL'WITH '   RRPRCD
     C                     MOVE 'PRIORITY'RRPRCD
     C                     ELSE
     C           PRYC      IFEQ 'W'
     C                     MOVEL'TOP PRIO'RRPRCD
     C                     MOVE 'RITY'    RRPRCD
     C                     END
     C                     END
     C                     END
      *
      * FORMAT PRICE/DISCOUNT/YIELD
     C                     MOVE 'L'       ZECODE
     C***********          Z-ADDTPDY      ZFLD15                          111941
     C                     MOVE TPDY      ZFLD15                          111941
     C                     Z-ADD8         ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RRTPDY
      *
      * FORMAT COUPON RATE
     C***********          Z-ADDTDCR      ZFLD15                          111941
     C                     MOVE TDCR      ZFLD15                          111941
     C                     Z-ADD7         ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RRTDCR
      *
      * FORMAT DAYS ADJUSTMENT
     C                     Z-ADDDADJ      ZFLD15
     C                     Z-ADD0         ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RRDADJ
      *
      * FORMAT INTEREST ADJUSTMENT
      ** CHAIN TO PF/SDCURRPD USING NOMINAL CURRENCY
     C                     CALL 'AOCURRR0'
     C                     PARM '*MSG   ' WRTCD   7
     C                     PARM '*KEY   ' WOPTN   7
     C                     PARM TNMC      WCURR   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C           WRTCD     IFNE *BLANKS
     C                     MOVEL'007'     DBASE            **************
     C                     MOVEL'SDCURRPD'DBFILE           * DB ERROR 7 *
     C                     MOVELTNMC      DBKEY            **************
     C                     EXSR *PSSR
     C                     END
      *
     C                     MOVE A6SPRT    WWSRI
     C                     MOVE A6NBDP    WWDPI
     C                     MOVE A6MDIN    WWMDI
      *
     C                     Z-ADDTINA      ZFLD15
     C                     MOVE A6NBDP    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RRTINA
      *
      * FORMAT REALLOWANCE
     C***********          Z-ADDRALL      ZFLD15                          111941
     C                     MOVE RALL      ZFLD15                          111941
     C                     Z-ADD5         ZDECS
     C                     MOVE 'L'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RRRALL
      *
      * FORMAT CURRENT FACTOR - USING FACTOR FROM TRADE (NOT SECURITY)
     C***********          Z-ADDTCFC      ZFLD15                          111941
     C***********          Z-ADD8         ZDECS                           111941
     C                     MOVE TCFC      ZFLD15                          111941
     C                     Z-ADD9         ZDECS                           111941
     C                     MOVE 'L'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RRCFCT
      *
      * FORMAT CHARGE/COMMISSION DESCRIPTION
     C                     MOVE TNMC      WWCCY
      *    BROKER COMMISSION
     C                     MOVE TBCC      WWCCOM
     C           KL3       CHAINSECGT                72
     C           *IN72     IFEQ '1'
     C                     MOVE *BLANK    RRNRBC
     C                     ELSE
     C                     MOVE CHNR      RRNRBC
      *                                                                   CAC002
      **  Profit Centre                                                   CAC002
      *                                                                   CAC002
     C                     EXSR SRPRFC                                    CAC002
     C                     MOVE WPRFC     PPRFCA                          CAC002
     C                     END
      *                                                                   CAC002
      *    CUSTOMER COMMISSION
     C                     MOVE TCCC      WWCCOM
     C           KL3       CHAINSECGT                72
     C           *IN72     IFEQ '1'
     C                     MOVE *BLANK    RRNRCC
     C                     ELSE
     C                     MOVE CHNR      RRNRCC
      *                                                                   CAC002
      **  Profit Centre                                                   CAC002
      *                                                                   CAC002
     C                     EXSR SRPRFC                                    CAC002
     C                     MOVE WPRFC     PPRFCB                          CAC002
     C                     END
      *                                                                   CAC002
      *    CUSTOMER CHARGES - 1
     C                     MOVE TCC1      WWCCOM
     C           KL3       CHAINSECGT                72
     C           *IN72     IFEQ '1'
     C                     MOVE *BLANK    RRNRC1
     C                     ELSE
     C                     MOVE CHNR      RRNRC1
      *                                                                   CAC002
      **  Profit Centre                                                   CAC002
      *                                                                   CAC002
     C                     EXSR SRPRFC                                    CAC002
     C                     MOVE WPRFC     PPRFC1                          CAC002
     C                     END
      *                                                                   CAC002
      *    CUSTOMER CHARGES - 2
     C                     MOVE TCC2      WWCCOM
     C           KL3       CHAINSECGT                72
     C           *IN72     IFEQ '1'
     C                     MOVE *BLANK    RRNRC2
     C                     ELSE
     C                     MOVE CHNR      RRNRC2
      *                                                                   CAC002
      **  Profit Centre                                                   CAC002
      *                                                                   CAC002
     C                     EXSR SRPRFC                                    CAC002
     C                     MOVE WPRFC     PPRFC2                          CAC002
     C                     END
      *                                                                   CAC002
      *    CUSTOMER CHARGES - 3
     C                     MOVE TCC3      WWCCOM
     C           KL3       CHAINSECGT                72
     C           *IN72     IFEQ '1'
     C                     MOVE *BLANK    RRNRC3
     C                     ELSE
     C                     MOVE CHNR      RRNRC3
      *                                                                   CAC002
      **  Profit Centre                                                   CAC002
      *                                                                   CAC002
     C                     EXSR SRPRFC                                    CAC002
     C                     MOVE WPRFC     PPRFC3                          CAC002
     C                     END
      *                                                                   CAC002
      *    CUSTOMER CHARGES - 4
     C                     MOVE TCC4      WWCCOM
     C           KL3       CHAINSECGT                72
     C           *IN72     IFEQ '1'
     C                     MOVE *BLANK    RRNRC4
     C                     ELSE
     C                     MOVE CHNR      RRNRC4
      *                                                                   CAC002
      **  Profit Centre                                                   CAC002
      *                                                                   CAC002
     C                     EXSR SRPRFC                                    CAC002
     C                     MOVE WPRFC     PPRFC4                          CAC002
     C                     END
      *                                                                   CAC002
      *    CUSTOMER CHARGES - 5
     C                     MOVE TCC5      WWCCOM
     C           KL3       CHAINSECGT                72
     C           *IN72     IFEQ '1'
     C                     MOVE *BLANK    RRNRC5
     C                     ELSE
     C                     MOVE CHNR      RRNRC5
      *                                                                   CAC002
      **  Profit Centre                                                   CAC002
      *                                                                   CAC002
     C                     EXSR SRPRFC                                    CAC002
     C                     MOVE WPRFC     PPRFC5                          CAC002
     C                     END
      *
      * CARRY OUT CHARGES-6 + 7 + TAX ONLY IF AUTO CHARGES PRESENT
      *
     C           *IN88     IFEQ '1'
      *
      *    CUSTOMER CHARGES - 6
     C                     MOVE TCC6      WWCCOM
     C           KL3       CHAINSECGT                72
     C           *IN72     IFEQ '1'
     C                     MOVE *BLANK    RRNRC6
     C                     ELSE
     C                     MOVE CHNR      RRNRC6
      *                                                                   CAC002
      **  Profit Centre                                                   CAC002
      *                                                                   CAC002
     C                     EXSR SRPRFC                                    CAC002
     C                     MOVE WPRFC     PPRFC6                          CAC002
     C                     END
      *
      *    CUSTOMER CHARGES - 7
     C                     MOVE TCC7      WWCCOM
     C           KL3       CHAINSECGT                72
     C           *IN72     IFEQ '1'
     C                     MOVE *BLANK    RRNRC7
     C                     ELSE
     C                     MOVE CHNR      RRNRC7
      *                                                                   CAC002
      **  Profit Centre                                                   CAC002
      *                                                                   CAC002
     C                     EXSR SRPRFC                                    CAC002
     C                     MOVE WPRFC     PPRFC7                          CAC002
     C                     END
      *
     C                     END
      *
      * FORMAT EXCHANGE RATE
      *                                                                   CAC002
     C           BGN0ST    IFEQ 'Y'                                       CAC002
     C           TMDI      IFEQ 'M'                                       CAC002
     C                     SUB  FXMR      TDER                            CAC002
     C                     ELSE                                           CAC002
     C                     ADD  FXMR      TDER                            CAC002
     C                     ENDIF                                          CAC002
     C                     ENDIF                                          CAC002
      *                                                                   CAC002
     C***********          Z-ADDTDER      ZFLD15                          111941
     C                     MOVE TDER      ZFLD15                          111941
     C                     Z-ADD8         ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RRTDER
      *
      * FORMAT BASE CURRENCY RATE
     C***********          Z-ADDTBCR      ZFLD15                          111941
     C                     MOVE TBCR      ZFLD15                          111941
     C                     Z-ADD8         ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RRTBCR
      *
      ** FORMAT CONSIDERATION IN NOMINAL CURRENCY DEC PLACES
     C                     Z-ADDWWDPI     ZDECS
      *
      ** IF ASSET-BACKED SECURITY, ADJUST FOR CURRENT FACTOR
     C********** PROT      IFEQ '8'                                                           260909
     C********** TCSR      MULT TCFC      WWTCSR                                              260909
     C**********           ELSE                                                               260909
     C                     Z-ADDTCSR      WWTCSR
     C**********           END                                                                260909
      *
     C                     Z-ADDWWTCSR    ZFLD15
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RRTCSR 21
      *
      * FORMAT INTEREST AMOUNT PURCHASED IN NOM CURRENCY DECIMAL PLACES
     C                     Z-ADDWWDPI     ZDECS
     C                     Z-ADDITRA      ZFLD15
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RRITRA
      *
      * FORMAT REALLOWANCE AMOUNT
     C                     Z-ADDRALL      WWRAL1
      *     IF DISCOUNT ..
      *
     C           STBS      IFEQ 'D'
     C           RALL      ANDNE0
     C                     Z-ADDTDVD      ZSDATE
     C                     Z-ADDMATY      ZEDATE
      *
      **     CALCULATE NO. DAYS BETWEEN VALUE AND MATURITY DATES
     C                     EXSR ZDAYS
     C                     Z-ADDZDDAYS    WDDAYS  50
      *
      **     DETERMINE NEXT COUPON DATE
     C                     EXSR ZNCD
      *
      **     CALCULATE NO. DAYS IN INTEREST YEAR
     C                     EXSR ZDYYR
      *
     C           ZINTDD    IFNE 0
     C           RALL      MULT WDDAYS    WPRICE 158
     C           WPRICE    DIV  ZINTDD    WWRALL 158H
      *
     C           WWRALL    IFGE 0
     C           1         SUB  WWRALL    WWRAL1 158
     C                     ELSE
     C                     Z-SUBWWRALL    WWRALL
     C           1         SUB  WWRALL    WWRAL1
     C                     Z-SUBWWRAL1    WWRAL1
     C                     END
      *
     C                     END
     C                     END
      *
      ** INITIALISE REALLOWANCE PARAMETER
     C                     Z-ADD*ZEROS    ZRALL  150
     C                     Z-ADD*ZEROS    ZRALL0 150
     C                     Z-ADD*ZEROS    ZRALL1 151
     C                     Z-ADD*ZEROS    ZRALL2 152
     C                     Z-ADD*ZEROS    ZRALL3 153
      *
      ** MULTIPLY NOMINAL BY REALLOWANCE DEPENDING ON DECIMAL PLACES
     C           WWDPI     IFEQ 0
     C           NOML      MULT WWRAL1    ZRALL0    H
     C                     ELSE
     C           WWDPI     IFEQ 1
     C           NOML      MULT WWRAL1    ZRALL1    H
     C                     ELSE
     C           WWDPI     IFEQ 2
     C           NOML      MULT WWRAL1    ZRALL2    H
     C                     ELSE
     C           NOML      MULT WWRAL1    ZRALL3    H
     C                     END
     C                     END
     C                     END
      *
      * Price basis - divide by 100
      *
     C           SPBS      IFEQ 'P'
     C           WWDPI     IFEQ 0
     C                     DIV  100       ZRALL0
     C                     ELSE
     C           WWDPI     IFEQ 1
     C                     DIV  100       ZRALL1
     C                     ELSE
     C           WWDPI     IFEQ 2
     C                     DIV  100       ZRALL2
     C                     ELSE
     C                     DIV  100       ZRALL3
     C                     END
     C                     END
     C                     END
     C                     END
      *
     C           WWDPI     IFEQ 0
     C                     MOVE ZRALL0    ZRALL
     C                     ELSE
     C           WWDPI     IFEQ 1
     C                     MOVE ZRALL1    ZRALL
     C                     ELSE
     C           WWDPI     IFEQ 2
     C                     MOVE ZRALL2    ZRALL
     C                     ELSE
     C                     MOVE ZRALL3    ZRALL
     C                     END
     C                     END
     C                     END
      *
      *   Allow for nominal decimal places
      *
     C           5         SUB  NMDP      DC      10
     C           ZRALL     MULT POWER8,DC ZRALL
     C                     Z-ADDZRALL     ZFLD15
     C                     MOVE 'J'       ZECODE
     C                     Z-ADDWWDPI     ZDECS
     C                     EXSR ZSEDIT
     C***********          MOVE ZRALL     RRRALL                          111941
     C                     MOVE ZOUT21    RRRAMT
      *
      * FORMAT COMMISSION/CHARGE AMOUNTS
      *   ALL COMMISSIONS/CHARGES
     C                     Z-ADDWWDPI     ZDECS
     C                     MOVE 'J'       ZECODE
      * BROKER COMMISSION
     C                     Z-ADDTBCA      ZFLD15
     C                     Z-ADDZFLD15    ZTBCA  150
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RRTBCA
      * CUST.COMMISSION AMOUNT
     C                     Z-ADDTCCA      ZFLD15
     C                     Z-ADDZFLD15    ZTCCA  150
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RRTCCA
      * CHARGES - 1 AMOUNT
     C                     Z-ADDTCA1      ZFLD15
     C                     Z-ADDZFLD15    ZTCA1  150
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RRTCA1
      * CHARGES - 2 AMOUNT
     C                     Z-ADDTCA2      ZFLD15
     C                     Z-ADDZFLD15    ZTCA2  150
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RRTCA2
      * CHARGES - 3 AMOUNT
     C                     Z-ADDTCA3      ZFLD15
     C                     Z-ADDZFLD15    ZTCA3  150
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RRTCA3
      * CHARGES - 4 AMOUNT
     C                     Z-ADDTCA4      ZFLD15
     C                     Z-ADDZFLD15    ZTCA4  150
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RRTCA4
      * CHARGES - 5 AMOUNT
     C                     Z-ADDTCA5      ZFLD15
     C                     Z-ADDZFLD15    ZTCA5  150
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RRTCA5
      *
      *    CARRY OUT CHARGES 6 + 7, AND TAX, AND REBATES ONLY
      *    WHEN AUTO-CHARGES  PRESENT.
      *
     C           *IN88     IFEQ '1'
      *
      * CHARGES - 6 AMOUNT
     C                     Z-ADDTCA6      ZFLD15
     C                     Z-ADDZFLD15    ZTCA6  150
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RRTCA6
      * CHARGES - 7 AMOUNT
     C                     Z-ADDTCA7      ZFLD15
     C                     Z-ADDZFLD15    ZTCA7  150
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RRTCA7
      * TAX AMOUNT
     C                     Z-ADDTAXA      ZFLD15
     C                     Z-ADDZFLD15    ZTTAX  150
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RRTTAX
      * BROKER COM REBATE
     C                     Z-ADDBCMR      ZFLD15
     C                     Z-ADDZFLD15    ZTBRB  150
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RRTBRB
      * CUST.  COM REBATE
     C                     Z-ADDCCMR      ZFLD15
     C                     Z-ADDZFLD15    ZTCCR  150
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RRTCCR
      * TAX REBATE
     C                     Z-ADDTXRB      ZFLD15
     C                     Z-ADDZFLD15    ZTTXR  150
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RRTTXR
     C                     END
      *
     C                     MOVE WWDPI     ZDECS
     C                     Z-ADDTCTR      ZFLD15
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RRNTOT
      *
      ** TOTAL COMMISSION/CHARGES-CONVERT TO SETTLE CURRENCY VIA SCONV
     C                     CALL 'AOCURRR0'
     C                     PARM '*MSG   ' WRTCD   7
     C                     PARM '*KEY   ' WOPTN   7
     C                     PARM SETC      WCURR   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C           WRTCD     IFNE *BLANKS
     C                     MOVEL'008'     DBASE            ***************
     C                     MOVEL'SDCURRPD'DBFILE           * DB ERROR 8  *
     C                     MOVELSETC      DBKEY            ***************
     C                     EXSR *PSSR
     C                     END
      *
     C                     MOVE A6SPRT    WWSRO
     C                     MOVE A6NBDP    WWDPO
     C                     MOVE A6MDIN    WWMDO
      *
     C                     Z-ADDTCTR      WWAMTI
     C                     EXSR SCONV
      *                                                                   CAC002
     C           BGN0ST    IFEQ 'Y'                                       CAC002
     C                     ADD  MAMT      WWAMTO                          CAC002
     C                     ENDIF                                          CAC002
      *                                                                   CAC002
     C                     Z-ADDWWDPO     ZDECS
     C                     Z-ADDWWAMTO    ZFLD15
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RRSTOT
      *                                                                   CAC002
      **  Assign FX margin points, settlement currency and amount if      CAC002
      **  anal acctg is installed and nominal ccy not = settle ccy        CAC002
      *                                                                   CAC002
     C           BGN0ST    IFEQ 'Y'                                       CAC002
     C           TNMC      ANDNESETC                                      CAC002
      *                                                                   CAC002
     C                     MOVE '1'       *IN20                           CAC002
      *                                                                   CAC002
      **  FX Margin Points                                                CAC002
      *                                                                   CAC002
     C                     MOVE *BLANKS   ZFLD15                          CAC002
     C                     MOVE FXMP      ZFLD15                          CAC002
     C                     Z-ADD2         ZDECS                           CAC002
     C                     MOVE 'L'       ZECODE                          CAC002
     C                     EXSR ZSEDIT                                    CAC002
     C                     MOVE ZOUT21    PFXMP                           CAC002
      *                                                                   CAC002
      **  Settlement Currency                                             CAC002
      *                                                                   CAC002
     C                     MOVE SETC      PSETC                           CAC002
      *                                                                   CAC002
      **  Margin Amount                                                   CAC002
      *                                                                   CAC002
     C                     MOVE *BLANKS   ZFLD15                          CAC002
     C                     MOVE MAMT      ZFLD15                          CAC002
     C                     Z-ADDWWDPO     ZDECS                           CAC002
     C                     MOVE 'J'       ZECODE                          CAC002
     C                     EXSR ZSEDIT                                    CAC002
     C                     MOVE ZOUT21    PMAMT                           CAC002
      *                                                                   CAC002
     C                     ELSE                                           CAC002
     C                     MOVE '0'       *IN20                           CAC002
     C                     ENDIF                                          CAC002
      *                                                                   CAC002
      **  Print the following fields if anal acctg is installed           CAC002
      *                                                                   CAC002
     C           BGN0ST    IFEQ 'Y'                                       CAC002
      *                                                                   CAC002
      **  Transaction Profit Centre only for portfolio customers          CAC002
      *                                                                   CAC002
     C                     MOVE TCNR      WCNUM   6                       CAC002
     C                     CALL 'AOSECSR0'                                CAC002
     C                     PARM *BLANKS   WRTCD                           CAC002
     C                     PARM '*KEY'    WOPTN                           CAC002
     C                     PARM           WCNUM                           CAC002
     C           SDSECS    PARM SDSECS    DSFDY                           CAC002
      *                                                                   CAC002
     C           WRTCD     IFNE *BLANKS                                   CAC002
     C                     MOVEL'011'     DBASE                           CAC002
     C                     MOVEL'SDSECSPD'DBFILE                          CAC002
     C                     MOVELTCNR      DBKEY                           CAC002
     C                     EXSR *PSSR                                     CAC002
     C                     ENDIF                                          CAC002
      *                                                                   CAC002
     C           BFCLAS    IFEQ 'S'                                       CAC002
     C           BFCLAS    OREQ 'D'                                       CAC002
     C                     MOVE TPRC      PTPRC                           CAC002
     C                     MOVE '1'       *IN21                           CAC002
     C                     ELSE                                           CAC002
     C                     MOVE '0'       *IN21                           CAC002
     C                     ENDIF                                          CAC002
      *                                                                   CAC002
      **  Book Profit Centre                                              CAC002
      *                                                                   CAC002
     C                     MOVE BPRC      PBPRC                           CAC002
      *                                                                   CAC002
      **  FX Rate (PM) and mult/div indicator                             CAC002
      *                                                                   CAC002
     C           SPXR      IFNE *ZEROS                                    CAC002
     C                     MOVE *BLANKS   ZFLD15                          CAC002
     C                     MOVE SPXR      ZFLD15                          CAC002
     C                     Z-ADD8         ZDECS                           CAC002
     C                     MOVE 'L'       ZECODE                          CAC002
     C                     EXSR ZSEDIT                                    CAC002
     C                     MOVE ZOUT21    PSPXR                           CAC002
     C                     ELSE                                           CAC002
     C                     MOVE *BLANKS   PSPXR                           CAC002
     C                     ENDIF                                          CAC002
      *                                                                   CAC002
     C                     MOVE SPMD      PSPMD                           CAC002
      *                                                                   CAC002
      **  Portfolio Reference                                             CAC002
      *                                                                   CAC002
     C                     MOVE PTFR      PPTFR                           CAC002
     C*                                                                   CAC002
     C                     ENDIF                                          CAC002
      *
      * FORMAT STATUS
     C                     MOVE *BLANK    RRTINC
     C           TINC      IFEQ 'I'
     C                     MOVEL'IN'      RRTINC
     C                     MOVE 'COMPLETE'RRTINC
     C                     ELSE
      *
     C           TINC      IFEQ 'C'
     C                     MOVEL'COMPLETE'RRTINC
     C                     END
     C                     END
      *
      * FORMAT DETAILS FOR PRINT OUTPUT
     C           WWCHTP    IFEQ 'A'
     C                     MOVEL'AMEND '  RRCHTP
     C                     ELSE
     C           WWCHTP    IFEQ 'I'
     C                     MOVEL'INSERT'  RRCHTP
     C                     ELSE
     C           WWCHTP    IFEQ 'D'
     C                     MOVEL'DELETE'  RRCHTP
     C                     ELSE
     C           WWCHTP    IFEQ 'C'
     C                     MOVEL'CHANGE'  RRCHTP
     C                     END
     C                     END
     C                     END
     C                     END
      *
      * MOVE SWIFT FIELDS TO REPORT FIELDS
     C                     MOVE T1TRTT    RRTRTT
     C                     MOVE T1GDEL    RRGDEL
     C                     MOVE T1RPTY    RRRPTY
     C                     MOVE T1FCOD    RRFCOD
     C                     MOVE T1FTID    RRFTID
     C                     MOVE T1CCID    RRCCID
     C                     MOVE T1SROL    RRSROL
      *
      * FORMAT DATES FOR PRINT OUTPUT
     C                     Z-ADDWWLCD     ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RRALCD
      *
      ** FORMAT REMAINING FIELDS
     C                     MOVE TDRF      RRTDRF
     C                     MOVE TCNR      RRTCNR
     C                     MOVE TDSS      RRTDSS
     C                     MOVE LKRF      RRLKRF
     C                     MOVE TDBA      RRTDBA
     C                     MOVE TDNR      RRTDNR
     C                     MOVE TDBK      RRTDBK
     C                     MOVE TSUB      RRTSUB
     C                     MOVE TDMI      RRTDMI
     C                     MOVE TNMC      RRTNMC
     C                     MOVE ACIN      RRACIN
     C                     MOVE SMTH      RRSMTH
     C                     MOVE ADIN      RRADIN
     C                     MOVE PYFA      RRPYFB
     C                     MOVE PYTA      RRPYTB
     C                     MOVE TDFA      RRTDFA
     C                     MOVE ASNM      RRASNM
     C                     MOVE TBCC      RRTBCC
     C                     MOVE TDSI      RRTDSI
     C                     MOVE TCCC      RRTCCC
     C                     MOVE TCC1      RRTCC1
     C                     MOVE CLTY      RRCLTY
     C                     MOVE TCC2      RRTCC2
     C                     MOVE DELT      RRDELT
     C                     MOVE TCC3      RRTCC3
     C                     MOVE DTFA      RRDTFA
     C                     MOVE TCC4      RRTCC4
     C                     MOVE TCC5      RRTCC5
     C                     MOVE TCC6      RRTCC6
     C                     MOVE TCC7      RRTCC7
     C                     MOVE DELF      RRDELF
     C                     MOVE DFFA      RRDFFA
     C                     MOVE PCOD      RRPCOD
     C                     MOVE NMCY      RRNMCY
     C                     MOVE PRYC      RRPRYC
     C                     MOVE TMDI      RRTMDI
     C                     MOVE SETC      RRSETC
     C                     MOVE SETC      RRSCCY                          111941
     C                     MOVE AUTS      RRAUTS
     C                     MOVE AIIP      RRAIIP
     C                     MOVE TMKC      RRTMKC
     C                     MOVE TCAP      RRTCAP
     C                     MOVE TDID      RRTDID
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/FMTPFT
      *****************************************************************
      *                                                               *
      *    FMTPFT    - FORMAT PAY FROM/TO                             *
      *                                                               *
      *    CALLED BY - INIT                                           *
      *    CALLS     - NONE                                           *
      *                                                               *
      *****************************************************************
      *
     C           FMTPFT    BEGSR
      *
      * FORMAT PAY FROM DEPENDING ON SETTLEMENT METHOD
     C                     MOVE *BLANK    WWPAYO
     C**********           MOVE *BLANK    WWPY12 12                                           CGL029
     C                     MOVE *BLANK    WWPY12 18                                           CGL029
      *
      *    FORMAT NNNNNN-NN-NNNN FOR SETTLEMENT CODE 05
      *    AND FOR SETTLEMENT CODE 15.
     C           SMTH      IFEQ 05
     C           SMTH      OREQ 15
     C                     MOVELWWPY06    WWPY07  7
     C                     MOVE '-'       WWPY07
      *
     C**********           MOVELWWPY04    WWPY05  5                                           CGL029
     C                     MOVELWWPY04    WWPY05 11                                           CGL029
     C                     MOVE '-'       WWPY05
      *
     C                     MOVELWWPY07    WWPY12
     C                     MOVE WWPY05    WWPY12
      *
     C                     MOVELWWPY12    WWPAYO
     C                     MOVE WWPY02    WWPAYO
     C                     ELSE
      *
      *    FORMAT SETTLEMENT METHODS 01 02 07 08 12
     C           SMTH      IFEQ 01
     C           SMTH      OREQ 02
     C           SMTH      OREQ 07
     C           SMTH      OREQ 08
     C           SMTH      OREQ 12
      *
      ** DO NOT ASSUME PAY FROM IS A 2-DIGIT NOSTRO.
     C           WWPAYC    IFEQ 'T'
     C           WWPAYC    OREQ 'F'
     C           WWPY10    ANDEQ*BLANKS
      *
      ** ACCESS PF/SDNOSTPD
     C                     MOVELWWPAY     WWPAY2  2
     C                     CALL 'AONOSTR0'
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*KEY   ' WOPTN   7
     C                     PARM *BLANKS   PWCUST  6          Customer No
     C                     PARM WWPAY2    PWCYCD  3          Currency
     C**********           PARM *BLANKS   PWACCD  4          A/c Code                         CGL029
     C                     PARM *BLANKS   PWACCD 10                                           CGL029
     C                     PARM *BLANKS   PWACSN  2          A/c Seq
     C                     PARM SETC      PWNONB  2          Nostro No
     C                     PARM *BLANKS   PWBRCD  3          Branch Code
     C                     PARM *BLANKS   PWCSSN 10          Cust ShortN
     C                     PARM *BLANKS   PWPNOI  1          Prime Nost In
     C           SDNOST    PARM SDNOST    DSFDY
      *
     C           WRTCD     IFNE *BLANKS
     C                     MOVELWWPAY     WWPAYO
     C                     ELSE
      *
      * FORMAT CUSTOMER SHORT NAME
      *    ACCESS PF/SDCUSTPD
      *    IF NO RECORD/RECORD DELETED/ - PRINT WWPAYNOT SHORTNAME
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   WRTCD
     C                     PARM '*KEY   ' WOPTN
     C                     PARM A7CUST    WCUST  10
     C                     PARM *BLANKS   WKYST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           WRTCD     IFNE *BLANKS
     C                     MOVELWWPAY     WWPAYO
     C                     ELSE
     C                     MOVELWWPAY2    WWPY12
     C                     MOVE BBCSSN    WWPY12
     C                     MOVELWWPY12    WWPAYO
     C                     END
     C                     END
      *
      *    IF WWPAY LONGER THAN 2 CHARS, HAVE A SHORTNAME OR CUST NO.
     C                     ELSE
      *
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   WRTCD
     C                     PARM '*KEY   ' WOPTN
     C                     PARM WWPY06    WCUST  10
     C                     PARM *BLANKS   WKYST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           WRTCD     IFNE *BLANKS
     C                     MOVELWWPAY     WWPAYO
     C                     ELSE
     C                     MOVE BBCSSN    WWPY12
     C                     MOVELWWPY12    WWPAYO
     C                     END
     C                     END
     C                     ELSE
      *
      *    FORMAT SETTLEMENT METHODS 14
     C           WWPAYC    IFEQ 'T'
     C           SMTH      ANDEQ14
     C                     MOVELWWPAY     WWPAYO
     C                     ELSE
      *
      *    CATCH ALL - ANY OTHER SETTLEMENT CODES
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   WRTCD
     C                     PARM '*KEY   ' WOPTN
     C                     PARM WWPAY     WCUST  10
     C                     PARM *BLANKS   WKYST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           WRTCD     IFEQ *BLANKS
     C                     MOVELBBCSSN    WWPAYO
     C                     ELSE
     C                     MOVELWWPAY     WWPAYO
     C                     END
      *
     C                     END
     C                     END
     C                     END
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/PRINT
      *****************************************************************
      *                                                               *
      *    PRINT     - PRINT DETAIL                                   *
      *                                                               *
      *    CALLED BY - PROCES                                         *
      *    CALLS     - NONE                                           *
      *                                                               *
      *****************************************************************
      *
     C           PRINT     BEGSR
      *
      ***  If change of Branch open new print file;
      ***  (if previous print file opened write 'End of Report' and
      ***  close print file before opening new one).
      *
     C           TDBA      IFNE WWLAST
      *
     C           *IN15     IFEQ '1'
     C                     WRITETRAILP1
     C                     CLOSESE1884P1
     C                     END
      *
     C                     OPEN SE1884P1
     C                     MOVE '1'       *IN15
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
     C                     CALL 'ZSFILE'
     C                     PARM           PSEQ
     C                     PARM TDBA      WPARM   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     END
      *
      **  Store old Branch and access new Branch Details.
     C                     MOVE TDBA      WWLAST  3
     C                     MOVE TDBA      RRTDBA
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM '*MSG   ' WRTCD
     C                     PARM '*KEY   ' WOPTN
     C                     PARM TDBA      WBRCA   3
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
      *
     C           WRTCD     IFNE *BLANKS
     C                     MOVEL'009'     DBASE            **************
     C                     MOVEL'SDBRCHPD'DBFILE           * DB ERROR 9 *
     C                     MOVE *BLANKS   DBKEY            **************
     C                     EXSR *PSSR
     C                     END
      *
     C                     MOVE A8BRNM    RRBRNM
      *
      ***  save current branch and name to restore next time if needed
     C                     MOVE RRTDBA    WRTDBA  3
     C                     MOVE RRBRNM    WRBRNM 30
      *
      ***  else not a different branch so restore previous branch/name
     C                     ELSE
     C                     MOVE WRTDBA    RRTDBA  3
     C                     MOVE WRBRNM    RRBRNM 30
     C                     END
      *
      ** PRINT PAGE HEADINGS
     C                     WRITEHEADP1
      *
      ** OUTPUT DETAIL LINE
     C                     WRITEDETAIL
     C                     CLEARDETAIL
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/AUDIT
      *****************************************************************
      *                                                               *
      *    AUDIT     - RUN CONTROL PROCESS                            *
      *                                                               *
      *    CALLED BY - PROCES                                         *
      *    CALLS     - NONE                                           *
      *                                                               *
      *****************************************************************
      *
     C           AUDIT     BEGSR
      *
     C                     Z-ADDSFNUMU    ZSNUM   60
     C                     CALL 'ZSFILE'
     C                     PARM           PSEQ
     C                     PARM *BLANKS   WPARM
     C                     PARM           SFILEU
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     END
      *
      ** WRITE HEADINGS
     C                     WRITEHEADAU
      *
      ** OUTPUT NUMBER OF TRADE SALES LISTED
     C                     Z-ADDWWCNT     RRCNT
     C                     WRITETOTAL
      *
      ***  If there is a DB Error, Output the DB Error Information.
     C           *INU7     IFEQ '1'
     C                     WRITEDBERROR
     C                     ELSE
      *
      ***  Or, if no valid records read, Output "No Details" message.
     C           WWCNT     IFEQ *ZERO
     C                     WRITENONE
     C                     END
     C                     END
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SCONV
      *****************************************************************
      *                                                               *
      *   SCONV - Converts an amount from one currency to another.    *
      *                                                               *
      *   Input Fields - WWAMTI 15/0 amount in input currency         *
      *                  WWSRI  13/8 spot rate for input currency     *
      *                  WWSRO  13/8 spot rate for output currency    *
      *                  WWDPI  1/0  no. of dec for input currency    *
      *                  WWDPO  1/0  no. of dec for output currency   *
      *                  WWMDI  1    mult/div ind for input curr.     *
      *                  WWMDO  1    mult/div ind for output curr.    *
      *                                                               *
      *   Arrays       - POWER  powers of 10                          *
      *                                                               *
      *   Output Fields- WWAMTO 15/0 output amnt converted to new ccy *
      *                                                               *
      *   CALLS    - NONE                                             *
      *                                                               *
      *****************************************************************
      *
     C           SCONV     BEGSR                           **  SCONV    **
      *
      ** DEFINE ALL FIELDS
     C                     Z-ADDWWAMTI    WWAMTI 150
     C                     Z-ADD0         WWAMTO 150
     C           *LIKE     DEFN A6SPRT    WWSRI
     C           *LIKE     DEFN A6SPRT    WWSRO
     C           *LIKE     DEFN A6NBDP    WWDPI
     C           *LIKE     DEFN A6NBDP    WWDPO
     C           *LIKE     DEFN A6MDIN    WWMDI
     C           *LIKE     DEFN A6MDIN    WWMDO
      *
     C           WWDPO     SUB  WWDPI     Z       10
     C           Z         ADD  4         Z
      *
     C           WWMDI     IFNE WWMDO
      *
      ** MULT/DIV INDICATORS DIFFERENT
     C           WWSRI     MULT WWSRO     WWRATE 159H
     C                     ELSE
      *
      ** MULT/DIV INDICATORS THE SAME
     C           WWSRO     IFEQ 0
     C                     GOTO SC0
     C                     END
     C           WWSRI     DIV  WWSRO     WWRATE    H
     C                     END
      *
     C           WWMDI     IFEQ 'D'
     C           WWRATE    IFEQ 0
     C                     GOTO SC0
     C                     END
     C           POWER,Z   DIV  WWRATE    WWRATE    H
     C                     ELSE
     C           POWER,Z   MULT WWRATE    WWRATE    H
     C                     END
      *
     C           WWAMTI    MULT WWRATE    WWAMTO    H
      *
     C           SC0       ENDSR                           **  SC0      **
      *
      *****************************************************************
      /EJECT                                                              CAC002
      *****************************************************************   CAC002
      *                                                               *   CAC002
      *  Subroutine  :  SRPRFC                                        *   CAC002
      *  Function    :  Assign profit centre to print if analytical   *   CAC002
      *                 accounting is installed                       *   CAC002
      *                                                               *   CAC002
      *  Called by   :  Main Routine                                  *   CAC002
      *  Calls       :  -                                             *   CAC002
      *                                                               *   CAC002
      *****************************************************************   CAC002
      *                                                                   CAC002
     C           SRPRFC    BEGSR                           ** SRPRFC **   CAC002
      *                                                                   CAC002
     C                     MOVE *BLANKS   WPRFC   4                       CAC002
      *                                                                   CAC002
      **  Perform this subroutine only if anal acctg is installed         CAC002
      *                                                                   CAC002
     C           BGN0ST    IFEQ 'Y'                                       CAC002
      *                                                                   CAC002
     C                     SELEC                                          CAC002
      *                                                                   CAC002
      **  Transaction profit centre                                       CAC002
      *                                                                   CAC002
     C           BOTR      WHEQ 'T'                                       CAC002
     C                     MOVE TPRC      WPRFC                           CAC002
      *                                                                   CAC002
      **  Book profit centre                                              CAC002
      *                                                                   CAC002
     C           BOTR      WHEQ 'B'                                       CAC002
     C                     MOVE BPRC      WPRFC                           CAC002
      *                                                                   CAC002
      **  Profit centre from LF/SECGT                                     CAC002
      *                                                                   CAC002
     C           BOTR      WHEQ *BLANK                                    CAC002
     C                     MOVE PRFCGT    WPRFC                           CAC002
      *                                                                   CAC002
     C                     ENDSL                                          CAC002
      *                                                                   CAC002
     C                     ENDIF                                          CAC002
      *                                                                   CAC002
     C           SRPRF9    ENDSR                           ** SRPRF9 **   CAC002
      *                                                                   CAC002
      *****************************************************************   CAC002
      /EJECT                                                              CAC002
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *    *PSSR     - PROGRAM ERROR HANDLING ROUTINE                 *
      *                                                               *
      *    CALLED BY -                                                *
      *    CALLS     -                                                *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
      ***  Check to see that *PSSR has not already been called.
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     MOVEL'SE1884'  DBPGM
      *
     C           *NAMVAR   DEFN LDA       WLDA  256
     C           *LOCK     IN   WLDA
     C                     MOVELDBERR     WLDA
     C                     OUT  WLDA
     C                     SETON                     U7U8LR
      *
     C                     EXSR AUDIT
      *
     C                     DUMP
     C                     END
      *
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/ZDATE1
     C/COPY ZSRSRC,ZDATE1Z2
      /TITLE SR/ZDATE2
     C/COPY ZSRSRC,ZDATE2Z4
      /TITLE SR/ZDAYS
     C/COPY ZSRSRC,ZDAYSZ2
      /TITLE SR/ZDYYR
     C/COPY ZSRSRC,ZDYYRZ3
      /TITLE SR/ZEVCD
     C/COPY ZSRSRC,ZEVCDZ1
      /TITLE SR/ZNCD
     C/COPY ZSRSRC,ZNCDZ3
      /TITLE SR/ZLCD                                                      109542
     C/COPY ZSRSRC,ZLCDZ1                                                 109542
      /TITLE SR/ZSDESC
     C/COPY ZSRSRC,ZSDESCZ3
      /TITLE SR/ZSEDIT
     C/COPY ZSRSRC,ZSEDITZ3
      /TITLE COMPILE-TIME ARRAYS
      ***
**  CPY@
(c) Finastra International Limited 2001
**  SET - SETTLEMENT METHOD
0102030405060708121415
**  TXT - TEXT FOR SETTLEMENT METHOD
NOSTRO        CHEQ.SENT     COMPENS.SUSP. PRIME RET.A/C SPECIFIED A/C
COMPUTER SUSP.CHIPS         SWIFT         CHEQ.COLLECTEDRETAIL A/C
NOMINATED A/C
**  VAL - CLEARANCE TYPE/PAY CODE
012345
**  CTT - TEXT FOR CLEARANCE TYPE
UNSPECIFIED        WITHIN EUROCLEAR    OUR DEPOT EUROCLEAR
WITHIN CEDEL       OUR DEPOT CEDEL     SPECIFIED DEPOT
**  PCT - TEXT FOR PAYCODE
AS REQUIRED           AGAINST PAYMENT       PAYT AFTER DELIVERY
FREE                  DELIVERY AFTER PAYMENTAGAINST PAYMENT G'TEED
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**   ZF29 DAY NOS OF FEB 29 TO 2096 (INCLUDING 2000)
0006001521029820444305904073650882610287117481320914670161311759219053
2051421975234362489726358278192928030741322023366335124365853804639507
40968424294389045351
**  POWER8 - ARRAY OF POWERS FOR CURRENCY CONVERSION
00000001
00000010
00000100
00001000
00010000
00100000
01000000
10000000
**  POWER - ARRAY OF POWERS FOR CURRENCY CONVERSION
0000001
0000010
0000100
0001000
0010000
0100000
1000000
