     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas Security Withholding Tax Details Mnt')
     F*****************************************************************
     F*                                                               *
     F*  Midas  Securities Trading Module                         *
     F*                                                               *
     F*  SE1960 - Security Withholding Tax Details Maintenance        *
     F*                                                               *
     F*  Function: This program allows input of Withholding Tax       *
     F*            details for coupon and dividend bearing            *
     F*            securities which attract such a tax.               *
     F*            Created as part of SAR S01447, Withholding Tax     *
     F*                                                               *
     F*  Called By: SEC02 - Input Cycle Processing                    *
     F*                                                               *
     F*  Calls: AOBANKR0 - Access object for Bank I.C.D. details      *
     F*         AOCTRYR0 - Access object for Country Codes            *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD027346           Date 22May20               *     
      *  Prev Amend No. AR397187A          Date 22May20               *
      *                 AR397187           Date 22May20               *
      *                 MD027346           Date 22May20               *
      *                 MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 CRE026             Date 24May06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CSE071             Date 19Jul05               *
      *                 BUG3644            Date 12Jul04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01408             Date 22Aug96               *
      *                 084256             Date 06Sep95               *
     F*                 S01496             DATE 10OCT94               *
     F*                 S01447             DATE 26JAN94               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD027346 - Midas database error due to record cannot be      *
      *             allocated. Monitor for error during CHAIN and     *
      *             issue proper error message if cannot be allocated.*     
      *  AR397187A - Modify previous fix on the validation of user    *
      *              and action code on input screen and add valida-  *
      *              tion on the subfile screen.                      *
      *  AR397187 - Security withholding tax details maintenance      *
      *             issue.  Add validation for user and action code.  *
      *  MD046248 - Finastra Rebranding                               *
      *  CRE026 - Consumer Banking (Recompile)                        *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  BUG3644 - Serious Error in Collateralised Lending (Recompile)*
     F*  S01408 - Core Hook SE1960CCP1 Added                          *
     F*  084256 - Tax rate change should be switchable                *
     F*  S01496 - Incorporation of JYSKE Enhancements (S10982)        *
     F*  S01447 - Created for SE Withholding Tax enhancement.         *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     F*
     FSECTY   IF  E           K        DISK         KINFSR *PSSR
     F*
     F***SESWHTL0UF  E           K        DISK         KINFSR *PSSR A                       MD027346
     FSESWHTL0UF  E           K        DISK                      A    UC                    MD027346
     F                                              KINFDS FILEDS
     F            SESWHTD0                          KRENAMESESWHTDX                         MD027346
     F                                              KCOMIT
     F*
     F**   SCREEN FORMAT
     FSE1960DFCF  E                    WORKSTN      KINFSR *PSSR
     F                                        DDRRN KSFILE SE1960S5
     F*
     F********************************************************************
     F/EJECT
     F********************************************************************
     F**                                                                 *
     F** ID F  C  H  L    FUNCTION OF INDICATORS                         *
     F**                 ------------------------                        *
     F** 26 - END OF MESSAGE SUBFILE                                     *
     F** 27 - ROLLUP                                                     *
     F** 28 - SFLDSP                                                     *
     F** 29 - SFLDSPCTL                                                  *
     F** 30 - SFLCLR                                                     *
     F** 31 - SFLEND                                                     *
     F** 32 - ROLLDOWN                                                   *
     F**       41 - ERROR ON TAX BAND FOR TAX DEDUCTED AT SOURCE         *
     F**       42 - ERROR ON TAX BAND FOR TAX DEDUCTED BY USER           *
     F**       43 - ERROR ON RATE OF TAX DEDUCTED AT SOURCE              *
     F**    51 - FILE INDICATOR ON SESWHTL0                              *
     F**    52 - FILE INDICATOR ON SECTY                                 *
     F**    54 - WRITE INDICATOR ON SESWHTL0                             *
     F**    59 - FILE INDICATOR ON SE1960DF                              *
     F**       60 - SECOND SCREEN DISPLAYED                              *
     F**       61 - ERROR ON ACTION CODE                                 *
     F**       62 - ERROR ON SECURITY                                    *
     F**       63 - ERROR ON RATE OF TAX DEDUCTED AT SOURCE              *
     F**       66 - ERROR ON REVIEW FROM                                 *
     F**       69 - ERROR ON SUBFILE SELECT                              *
     F**          70 - ERROR INDICATOR                                   *
     F**          71 - 'INVALID' INDICATOR                               *
     F**             99 - FILE ERROR INDICATOR                           *
     F**                                                                 *
     F**       90-98 - USED IN STANDARD SUBROUTINE SR/ZALIGN             *
     F********************************************************************
     F/EJECT
     E********************************************************************
     E*
     E** ARRAY FOR COPYRIGHT
     E                    CPY@    1   1 80
     E*
     E** ARRAYS USED IN ZALIGN
     E                    ZA1        16  1
     E                    ZA2        16  1
     E*
     E** ARRAYS USED IN CONVERSION OF DECIMAL AMOUNTS TO CHARACTER
     E                    ZS1        15  1 0
     E                    ZS2        21  1
     E********************************************************************
     E/EJECT
     I********************************************************************
     I*
     I** DATA STRUCTURE FOR COMPILATION - COPYRIGHT INSERTION
     ICPYR@$      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@$$
     I*
     I** PROGRAM STATUS DATA STRUCTURE
     IPSDS       SDS
     I                                      244 246 WSID
     I                                      254 263 USID
     I*
     I** FILE INFORMATION DATA STRUCTURE
     IFILEDS      DS
     I                                     *STATUS  STATUS
     I*
     I**  LOCAL DATA AREA FOR DATA BASE ERRORS
     ILDA        UDS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     IRUNDT       DS
     I*
     I** DATA AREA RUNDAT
     I*
     I                                        1   7 RUNA
     I                                    P   8  100RUND
     I                                       11  11 SSF
     I                                       12  12 DATF
     I                                       13  13 MBIN
     I*
     I**  DATA AREA FOR UPDATE PROCESSING
     IDNATN       DS                              5
     I                                        1   50FNATN
     I*
     I** DATA AREA OF MENU USER
     I*
     IZMUSER      DS                             17
     I                                       11  13 USRBCH
     I** DATA STRUCTURE TO PROVIDE TEXT ON COMIT ENTRY IN JOURNAL
     ICMTTXT      DS                             52
     I                                        1   52NATN
     I                                        6   7 MDID
     I                                        8  15 PGMN
     I                                       16  18 WSIDX
     I                                       19  240MTIME
     I                                       25  25 ACTNX
     I                                       26  35 USIDX
     I                                       43  52 DD1SSN
     I*
     I** ARRAY FOR ZSEDIT
     I            DS
     I                                        1  150WORK15
     I                                        1  150ZS1
     I*
     I** FORMAT ZSEDIT OUTPUT
     IWW21FM      DS
     I                                        1  21 WWFL21
     I                                        1  20 WW20CH
     I                                        1  18 WW18CH                084256
     I                                       21  21 WWSIGN
     ISDCTRY    E DSSDCTRYPD
     ISECNTR    E DSSECNTRPD
     ISCSARD    E DSSCSARDPD                                              084256
     IDSFDY     E DSDSFDY
     I********************************************************************
     C/EJECT
     C********************************************************************
     C*
     C**              START MAINLINE
     C**             ----------------
     C*
     C**   INITIALISATION
     C                     EXSR #A
     C*
     C**   MAIN PROCESSING
     C           *INKC     DOWEQ'0'
     C*
     C                     MOVEL*BLANKS   DD1ACT
     C                     MOVEL*BLANKS   DD1SSN
     C                     MOVEL*BLANKS   DD1RVF
     C                     EXSR #B
     C                     MOVE '1'       *IN71
     C*
     C                     END
     C*
     C**   END PROGRAM
     C                     MOVE '1'       *INLR
     C*
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C**          INDEX TO SUBROUTINES                                   *
     C**         ----------------------                                  *
     C**                                                                 *
     C**       #B     - MAINLINE                                         *
     C**       #BA    - VALIDATION OF SCREEN 1                           *
     C**       #BB    - SUBFILE PROCESSING                               *
     C**       #BBA   - PROCESSES ENTRY FROM SUBFILE                     *
     C**       #ZA    - FILE MAINTENANCE SCREENS                         *
     C**       #ZC    - CMD12 PROCESSING                                 *
     C**       #ZABA  - VALIDATE & FORMAT RATES ON MAINTENANCE SCREEN    *
     C**       #ZAB   - VALIDATION OF FILE MAINTENANCE SCREENS           *
     C**       #A     - INITIALISATION                                   *
     C**       #ZB    - LOADS ONE PAGE OF SUBFILE RECORDS                *
     C**       #BBAA  - VALIDATES SELECTION ON SUBFILE                   *
     C**       #ZAA   - UPDATE PROCESSING                                *
     C**       ZTNLU1 - TRANSACTION NUMBER PROCESSING                    *
     C**       ZASNMS - WRITES A MESSAGE TO MESSAGE SUBFILE              *
     C**       #PSSR  - ERROR HANDLING SUBROUTINE                        *
     C**       ZALIGN - VALIDATE NUMERIC INPUT                           *
     C**       ZSEDIT - FORMAT NUMERIC FOR OUTPUT                        *
     C**                                                                 *
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C** #B        - MAIN PROCESSING                                     *
     C**                                                                 *
     C** CALLS     - #BA, #BB, #ZA                                       *
     C**                                                                 *
     C** CALLED BY - MAINLINE                                            *
     C**                                                                 *
     C********************************************************************
     C           #B        BEGSR
     C*
     C** WHILE NOT END REQUESTED AND INVALID DATA
     C           *INKC     DOWEQ'0'
     C           *IN71     ANDEQ'1'
     C*
     C**  DISPLAY SELECTION SCREEN
     C                     WRITESE1960F1
     C*
     C**  IF ERROR AND NOT HELP, WRITE MESSAGE SUBFILE
     C           *IN70     IFEQ '1'
     C                     WRITESE1960C7
     C                     END
     C*
     C**  READ SELECTION SCREEN
     C                     READ SE1960F1                 59
     C*
     C** IF NOT END REQUESTED
     C** PROCESS HELP OR ENTER AS NECESSARY
     C           *INKC     IFEQ '0'
     C*
     C                     EXSR #BA
     C*
     C                     END
     C*
     C**   IF VALID DATA GET DETAILS AS NECESSARY
     C           *IN71     IFEQ '0'
     C                     MOVE '1'       *IN71
     C*
     C**   DISPLAY NEXT SCREEN AS NECESSARY
     C                     MOVE '1'       *IN60
     C*
     C           WWSFL     IFEQ 'Y'
     C*
     C                     EXSR #BB
     C*
     C                     ELSE
     C*
     C                     EXSR #ZA
     C*
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C** #BA       - THIS SUBROUTINE PERFORMS VALIDATION OF SCREEN 1     *
     C**                                                                 *
     C** CALLS     - ZASNMS                                              *
     C**                                                                 *
     C** CALLED BY - #B                                                  *
     C**                                                                 *
     C********************************************************************
     C           #BA       BEGSR
     C*
     C** CLEAR SCREEN MESSAGE QUEUE
     C                     CALL 'Y2CLMSC'
     C                     PARM           WWTPGQ 10
     C                     PARM           WWPGQR  5
     C*
     C** CLEAR SCREEN ERROR INDICATORS
     C                     MOVEA'000'     *IN,41
     C                     MOVEA'0000000' *IN,61
     C                     MOVEA'00'      *IN,70
     C                     MOVE 'N'       WWIOVR
     C*
     C** VALIDATE ENTRIES AS FOLLOWS:
     C*
     C**   IF ACTION AND SECURITY ARE BLANK
     C**   SET SUBFILE NEEDED FLAG TO 'Y' & STORE REVIEW FROM VALUE
     C           DD1ACT    IFEQ *BLANKS                    B001
     C           DD1SSN    ANDEQ*BLANKS
     C*
     C                     MOVE 'Y'       WWSFL   1
     C*
     C                     ELSE                            X001
     C*
     C                     MOVE 'N'       WWSFL
     C*
     C**   IF REVIEW FROM IS ENTERED THEN ACTION AND SECURITY
     C**   MUST BE BLANK
     C           DD1RVF    IFNE *BLANKS                    B002
     C*
     C           DD1ACT    IFNE *BLANKS                    B003
     C           DD1SSN    ORNE *BLANKS
     C*
     C                     MOVE '1'       *IN66
     C                     MOVEA'11'      *IN,70
     C                     MOVEL'SEM1030' ZAMSID
     C                     EXSR ZASNMS
     C                     END                             E003
     C*
     C                     ELSE                            X002
     C*
     C**   ACTION IS ENTERED
     C**   ACTION MUST BE 'I', 'A', OR 'E'
     C           DD1ACT    IFNE *BLANKS                    B003
     C*
     C           DD1ACT    IFNE 'I'                        B004
     C           DD1ACT    ANDNE'A'
     C           DD1ACT    ANDNE'E'
     C*
     C                     MOVE '1'       *IN61
     C                     MOVEA'11'      *IN,70
     C                     MOVEL'SEM1031' ZAMSID
     C                     EXSR ZASNMS
      *                                                                                     AR397187
      **  Check if User is authorised to the Action Code entered.                           AR397187
      *                                                                                     AR397187
     C                     ELSE                                                             AR397187
     C**********           CALL 'ZVACTU'                                          AR397187 AR397187A
     C**********           PARM DD1ACT    @ZACTN  1                               AR397187 AR397187A
     C**********           PARM           @ERR    10                              AR397187 AR397187A
      *                                                                                     AR397187
     C********** @ERR      IFNE *ZERO                                             AR397187 AR397187A
     C**********           MOVE '1'       *IN61                                   AR397187 AR397187A
     C**********           MOVEA'11'      *IN,70                                  AR397187 AR397187A
     C**********           MOVEL'SEM1019' ZAMSID                                  AR397187 AR397187A
     C**********           EXSR ZASNMS                                            AR397187 AR397187A
     C**********           ENDIF                                                  AR397187 AR397187A
      *                                                                                    AR397187A
     C                     MOVE DD1ACT    ACTCDE  1                                        AR397187A
     C                     EXSR SRVACT                                                     AR397187A
     C                     END                             E004
     C*
     C                     END                             E003
     C*
     C**   ACTION IS BLANK
     C**   SECURITY MUST ALSO BE BLANK
     C           DD1ACT    IFEQ *BLANKS                    B003
     C*
     C           DD1SSN    IFNE *BLANKS                    B004
     C                     MOVE '1'       *IN61
     C                     MOVEA'11'      *IN,70
     C                     MOVEL'SEM1025' ZAMSID
     C                     EXSR ZASNMS
     C                     END                             E004
     C*
     C                     ELSE                            X003
     C*
     C**   ACTION IS NOT BLANK
     C**   SECURITY MUST BE ENTERED
     C*
     C           DD1ACT    IFEQ 'I'                        B004
     C           DD1ACT    OREQ 'A'
     C           DD1ACT    OREQ 'E'
     C*
     C           DD1SSN    IFEQ *BLANKS                    B005
     C                     MOVE '1'       *IN62
     C                     MOVEA'11'      *IN,70
     C                     MOVEL'SEM1024' ZAMSID
     C                     EXSR ZASNMS
     C                     END                             E005
     C*
     C                     END                             E004
     C*
     C                     END                             E003
     C*
     C                     END                             E002
     C*
     C                     END                             E001
     C*
     C**   ACTION AND SECURITY ARE NOT BLANK
     C*
     C           DD1ACT    IFNE *BLANKS                    B001
     C           DD1SSN    ANDNE*BLANKS
     C           *IN71     ANDEQ'0'
     C*
     C**   VALID ACTION CODE ENTERED
     C*
     C           DD1ACT    IFEQ 'I'                        B002
     C*
     C** ACCESS SECURITIES FILE
     C*
     C           DD1SSN    CHAINSECTY                52
     C           *IN52     IFEQ '1'
     C           RECI      ORNE 'D'
     C                     MOVE '1'       *IN62
     C                     MOVEA'11'      *IN,70
     C                     MOVEL'SEM1023' ZAMSID
     C                     EXSR ZASNMS
     C                     ELSE
     C                     MOVE SESN      DD1SSN
     C                     END
     C*
     C*
     C                     MOVE DD1SSN    WWKSSN 10
     C********** WWKSSN    CHAINSESWHTL0             51                                     MD027346
     C           WWKSSN    CHAINSESWHTDX             5153                                   MD027346
      *                                                                                     MD027346
      ** If chain not done due to record being updated by another                           MD027346
      ** workstation send message to screen.                                                MD027346
      *                                                                                     MD027346
     C           *IN53     IFEQ '1'                                                         MD027346
     C                     MOVE '1'       *IN99                                             MD027346
     C                     MOVEA'11'      *IN,70                                            MD027346
     C                     MOVEL'MMM1067' ZAMSID                                            MD027346
     C                     MOVEL'DRSMM   'ZAMSGF                                            MD027346
     C                     EXSR ZASNMS                                                      MD027346
     C                     ENDIF                                                            MD027346
     C*
     C           *IN51     IFEQ '0'                        B003
     C**   ACTION CODE = 'I', RECORD EXISTS  IE. RE-INSERTION ATTEMPTED
     C*
     C**   LIVE RECORD AND THEREFORE CANNOT BE RE-INSERTED
     C                     MOVE '1'       *IN61
     C                     MOVEA'11'      *IN,70
     C                     MOVEL'SEM1029' ZAMSID
     C                     EXSR ZASNMS
     C*
     C                     ELSE                            X003
     C*
     C           *IN71     IFEQ '0'                        B004
     C                     MOVEL*BLANKS   DD2CTA
     C                     MOVEL*BLANKS   DD2TAD
     C                     MOVEL*BLANKS   DD2TAU
     C                     MOVE *BLANKS   DD2RAS
     C***********          MOVE '0.00'    DD2RAS                          S01496
     C***********          MOVE '0.0000'  DD2RAS                    S01496084256
     C                     MOVE 'IA'      WWFMT
     C                     END                             E004
     C*
     C                     END                             E003
     C*
     C                     ELSE                            X002
     C*    ACTION CODE = 'A' OR 'E'
     C*
     C*
     C           DD1SSN    CHAINSECTY                52
     C           *IN52     IFEQ '1'
     C           RECI      ORNE 'D'
     C                     MOVE '1'       *IN62
     C                     MOVEA'11'      *IN,70
     C                     MOVEL'SEM1023' ZAMSID
     C                     EXSR ZASNMS
     C                     ELSE
     C                     MOVE SESN      DD1SSN
     C                     END
     C*
     C*
     C                     MOVE DD1SSN    WWKSSN
     C*
     C********** WWKSSN    CHAINSESWHTL0             51                                     MD027346
     C           WWKSSN    CHAINSESWHTDX             5153                                   MD027346
      *                                                                                     MD027346
      ** If chain not done due to record being updated by another                           MD027346
      ** workstation send message to screen.                                                MD027346
      *                                                                                     MD027346
     C           *IN53     IFEQ '1'                                                         MD027346
     C                     MOVE '1'       *IN99                                             MD027346
     C                     MOVEA'11'      *IN,70                                            MD027346
     C                     MOVEL'MMM1067' ZAMSID                                            MD027346
     C                     MOVEL'DRSMM   'ZAMSGF                                            MD027346
     C                     EXSR ZASNMS                                                      MD027346
     C                     ENDIF                                                            MD027346
     C*
     C           *IN51     IFEQ '1'                        B003
     C**     ACTION CODE 'A' OR 'E' - RECORD NOT ON FILE
     C                     MOVE '1'       *IN41
     C                     MOVE '1'       *IN62
     C                     MOVEA'11'      *IN,70
     C                     MOVEL'SEM1008' ZAMSID
     C                     EXSR ZASNMS
     C*
     C                     ELSE                            X003
     C**     ACTION CODE 'A' OR 'E' - RECORD EXISTS ON FILE
     C*
     C           S1RASO    IFNE *ZEROS
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE S1RASO    ZFLD15
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    WWFL21
     C           S01496    IFEQ 'Y'                                       084256
     C                     MOVE WW20CH    DD2RAS
     C                     ELSE                                           084256
     C                     MOVE WW18CH    DD2RAS                          084256
     C                     END                                            084256
     C                     ELSE
     C                     MOVE *BLANKS   DD2RAS
     C***********          MOVE '0.00'    DD2RAS                          S01496
     C***********          MOVE '0.0000'  DD2RAS                    S01496084256
     C                     END
     C                     MOVELS1TABD    DD2TAD
     C                     MOVELS1TABU    DD2TAU
     C                     MOVELS1CTTA    DD2CTA
     C*
     C**   IF ACTION EQUALS 'A', SET FORMAT FLAG TO 'IA'
     C**   IF ACTION EQUALS 'E', SET FORMAT FLAG TO 'EN'
     C           DD1ACT    IFEQ 'A'                        B004
     C                     MOVEL'IA'      WWFMT
     C                     ELSE                            X004
     C*
     C           DD1ACT    IFEQ 'E'                        B005
     C                     MOVEL'EN'      WWFMT
     C*
     C                     END                             E005
     C*
     C                     END                             E004
     C*
     C                     END                             E003
     C*
     C                     END                             E002
     C*
     C                     END                             E001
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C** #BB       - THIS SUBROUTINE DOES SUBFILE PROCESSING             *
     C**                                                                 *
     C** CALLS     - #BBA, #ZB, #ZC                                      *
     C**                                                                 *
     C** CALLED BY - #B                                                  *
     C**                                                                 *
     C********************************************************************
     C           #BB       BEGSR
     C*
     C** CLEAR SCREEN MESSAGE QUEUE
     C                     CALL 'Y2CLMSC'
     C                     PARM           WWTPGQ 10
     C                     PARM           WWPGQR  5
     C*
     C                     EXSR #ZB
     C*
     C           *INKC     DOWEQ'0'
     C           *INKL     ANDEQ'0'
     C*
     C                     WRITESE1960C5
     C                     WRITESE1960F6
     C*
     C           *IN70     IFEQ '1'
     C                     WRITESE1960C7
     C                     END
     C*
     C                     READ SE1960C5                 59
     C*
     C           *INKC     IFEQ '0'
     C*
     C           *IN27     CASEQ'1'       #ZB
     C           *IN32     CASEQ'1'       #ZB
     C           *INKL     CASEQ'1'       #ZC
     C                     CAS            #BBA
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C** #BBA      - THIS SUBROUTINE PPROCESSES ANY ENTRY FROM SUBFILE   *
     C**                                                                 *
     C** CALLS     - #BBAA, #ZB, #ZC, ZASNMS                             *
     C**                                                                 *
     C** CALLED BY - #BB                                                 *
     C**                                                                 *
     C********************************************************************
     C           #BBA      BEGSR
     C*
     C**   CLEAR SCREEN MESSAGE QUEUE
     C                     CALL 'Y2CLMSC'
     C                     PARM           WWTPGQ
     C                     PARM           WWPGQR
     C*
     C**   SET OF INDICATORS
     C                     MOVEA'00'      *IN,70
     C*
     C**   IF BLANK SUBFILE DISPLAYED - ASSUME REVIEW FROM REQUIRED
     C           *IN28     IFEQ '0'
     C*
     C                     MOVE '1'       *IN59
     C*
     C                     ELSE
     C*
     C**   ELSE IDENTIFY WHETHER 'REVIEW FROM' OR SELECTION REQUIRED
     C                     READCSE1960S5                 59
     C*
     C                     END
     C*
     C**   NO RECORDS CHANGED => 'REVIEW FROM' REQUIRED
     C           *IN59     IFEQ '1'
     C*
     C                     EXSR #ZB
     C*
     C                     ELSE
     C*
     C**   VALIDATE SELECTION
     C                     EXSR #BBAA
     C*
     C**   IF VALID
     C           *IN71     IFEQ '0'
     C*
     C**   LOAD NECESSARY SCREEN
     C                     MOVE '1'       *IN71
     C                     EXSR #ZA
     C*
     C**   CHAIN TO SUBFILE TO GET FIRST CUSTOMER NUMBER/START DATE
     C           1         CHAINSE1960S5             59
     C                     MOVELDD2SSN    DD1RVF
     C*
     C**   REBUILD SUBFILE
     C                     EXSR #ZB
     C*
     C                     ELSE
     C*
     C**   SAVE RRN OF CHANGED SUBFILE RECORD
     C*
     C                     Z-ADDDDRRN     WWSRRN  40
     C                     MOVE DD2SEL    WWSSEL  1
     C*
     C**   REMOVE REVERSE IMAGE FROM SUBFILE
     C                     Z-ADD0         WWCNTR  40
     C*
     C           WWCNTR    DOWLTWWLRRN
     C*
     C                     ADD  1         WWCNTR
     C*
     C           WWCNTR    CHAINSE1960S5             59
     C                     MOVE *BLANKS   DD2SEL
     C                     MOVE '0'       *IN69
     C*
     C                     UPDATSE1960S5
     C*
     C                     END
     C*
     C                     MOVE '1'       *IN69
     C                     MOVEA'11'      *IN,70
     C                     MOVELWWMSGD    ZAMSID
     C                     EXSR ZASNMS
     C           WWSRRN    CHAINSE1960S5             59
     C                     MOVE WWSSEL    DD2SEL
     C                     UPDATSE1960S5
     C*
     C                     END
     C*
     C                     END
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C** #ZA       - THIS SUBROUTINE PROCESSES FILE MAINTENANCE SCREENS  *
     C**                                                                 *
     C** CALLS     - #BCA, #BCB, #ZC                                     *
     C**                                                                 *
     C** CALLED BY - #B, #BBA                                            *
     C**                                                                 *
     C********************************************************************
     C           #ZA       BEGSR
     C*
     C           *INKC     DOWEQ'0'                        B1
     C           *INKL     ANDEQ'0'
     C           *IN71     ANDEQ'1'
     C*
     C           WWFMT     IFEQ 'IA'                       B*2
     C*
     C                     WRITESE1960F2
     C           DD1ACT    IFEQ 'A'                        B**3
     C                     WRITESE1960F8
     C           DD1ACT    IFEQ 'I'                        B***4
     C                     WRITESE1960F7
     C                     END                             E***4
     C                     END                             E**3
     C                     ELSE                            X*2
     C*
     C           WWFMT     IFEQ 'EN'                       B**3
     C                     WRITESE1960F3
     C                     END                             E**3
     C*
     C                     END                             E*2
     C*
     C**  IF ERROR AND NOT HELP, WRITE MESSAGE SUBFILE
     C           *IN70     IFEQ '1'                        B*2
     C                     WRITESE1960C7
     C                     END                             E*2
     C*
     C**  READ SCREEN
     C           WWFMT     IFEQ 'IA'                       B*2
     C                     READ SE1960F2                 59
     C                     ELSE                            X*2
     C*
     C           WWFMT     IFEQ 'EN'                       B**3
     C                     READ SE1960F3                 59
     C                     END                             E**3
     C*
     C                     END                             E*2
     C*
     C** IF NOT END REQUESTED
     C           *INKC     IFEQ '0'                        B*2
     C*
     C           *INKJ     CASEQ'1'       #ZAA             B**3
     C           *INKL     CASEQ'1'       #ZC
     C                     CAS            #ZAB
     C                     END                             E**3
     C*
     C                     END                             E*2
     C*
     C           *INKC     IFEQ '0'                        B*2
     C           *INKL     ANDEQ'0'
     C           *IN71     ANDEQ'0'
     C*
     C           DD1ACT    IFEQ 'I'                        B**3
     C           DD1ACT    OREQ 'A'
     C*
     C                     EXSR #ZAA
     C*
     C                     END                             E**3
     C*
     C                     END                             E*2
     C*
     C                     END                             E1
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C** #ZC       - THIS SUBROUTINE PERFORMS CMD12 PROCESSING           *
     C**                                                                 *
     C** CALLS     - NONE                                                *
     C**                                                                 *
     C** CALLED BY - #BB, #ZA                                            *
     C**                                                                 *
     C********************************************************************
     C           #ZC       BEGSR
     C*
     C** CLEAR S$REEN MESSAGE QUEUE
     C                     CALL 'Y2CLMSC'
     C                     PARM           WWTPGQ 10
     C                     PARM           WWPGQR  5
     C*
     C** CLEAR SCREEN ERROR INDICATORS
     C                     MOVEA'000'     *IN,41
     C                     MOVEA'00000000'*IN,60
     C                     MOVEA'000'     *IN,69
     C*
     C                     MOVEL*BLANKS   DD1ACT
     C                     MOVEL*BLANKS   DD1SSN
     C                     MOVEL*BLANKS   DD1RVF
     C                     MOVEL*BLANKS   DD2CTA
     C                     MOVEL*BLANKS   DD2TAD
     C                     MOVEL*BLANKS   DD2TAU
     C                     MOVEL*BLANKS   DD2RAS
     C***********          MOVE '0.00'    DD2RAS                          S01496
     C***********          MOVE '0.0000'  DD2RAS                    S01496084256
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C** #ZABA     - THIS SUBROUTINE VALIDATES & FORMATS THE RATES ON    *
     C**             MAINTENANCE SCREENS                                 *
     C**                                                                 *
     C** CALLS     - ZALIGN, ZSEDIT                                      *
     C**                                                                 *
     C** CALLED BY - #ZAB                                                *
     C**                                                                 *
     C********************************************************************
     C           #ZABA     BEGSR
     C*
     C                     MOVE 'Y'       WWVALD  1
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE WWFLIN    ZFIELD
     C                     EXSR ZALIGN
     C           *IN99     IFEQ '1'
     C                     MOVE 'N'       WWVALD
     C                     ELSE
     C                     MOVE ZFIELD    WW1500 150
     C           WW1500    IFNE 0
     C***********WW1500    DIV  100       WW1502 152                      S01496
     C           S01496    IFEQ 'Y'                                       084256
     C           WW1500    DIV  10000     WW1504 154                      S01496
     C                     ELSE                                           084256
     C           WW1500    DIV  100       WW1502 152                      084256
     C                     END                                            084256
     C                     ELSE
     C***********          Z-ADD0         WW1502                          S01496
     C                     Z-ADD0         WW1504                          S01496
     C                     Z-ADD0         WW1502                          084256
     C                     END
     C***********WW1502    IFLT 0                                         S01496
     C***********WW1502    ORGT 100                                       S01496
     C           S01496    IFEQ 'Y'                                       084256
     C           WW1504    IFLT 0                                         S01496
     C           WW1504    ORGT 100                                       S01496
     C                     MOVE 'N'       WWVALD
     C                     END
     C                     ELSE                                           084256
     C           WW1502    IFLT 0                                         084256
     C           WW1502    ORGT 100                                       084256
     C                     MOVE 'N'       WWVALD                          084256
     C                     END                                            084256
     C                     END                                            084256
     C                     END
     C*
     C           WWVALD    IFEQ 'N'
     C                     MOVEA'1'       *IN,XX
     C                     MOVEA'11'      *IN,70
     C                     EXSR ZASNMS
     C                     ELSE
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE ZFIELD    ZFLD15
     C           S01496    IFNE 'Y'                                       084256
     C                     Z-ADD2         ZDECS                           084256
     C                     END                                            084256
     C                     EXSR ZSEDIT
     C           S01496    IFNE 'Y'                                       084256
     C                     Z-ADD4         ZDECS                           084256
     C                     END                                            084256
     C                     MOVE ZOUT21    WWFL21
     C                     END
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C** #ZAB      - THIS SUBROUTINE PERFORMS VALIDATION OF FILE         *
     C**             MAINTENANCE SCREENS                                 *
     C**                                                                 *
     C** CALLS     - ZASNMS                                              *
     C**                                                                 *
     C** CALLED BY - #ZA                                                 *
     C**                                                                 *
     C********************************************************************
     C           #ZAB      BEGSR
     C*
     C**   CLEAR SCREEN MESSAGE QUEUE
     C                     CALL 'Y2CLMSC'
     C                     PARM           WWTPGQ
     C                     PARM           WWPGQR
     C*
     C**   SET OF INDICATORS
     C                     MOVE '0'       *IN,59
     C                     MOVEA'000'     *IN,63
     C                     MOVEA'00'      *IN,70
     C                     MOVEA'000'     *IN,41
     C*
     C**   COUNTRY OF TAX IS THE ONLY MANDATORY FIELD. THE OTHER FIELDS
     C**   MUST BE VALID RATES IF ENTERED.
     C*
     C           DD1ACT    IFEQ 'I'                        B1
     C           DD1ACT    OREQ 'A'
     C*
     C*
     C** COUNTRY OF TAX MUST BE A VALID COUNTRY CODE FOUND ON THE
     C** COUNTRY CODES TABLE
     C*
     C           DD2CTA    IFEQ *BLANKS                    B*2
     C                     MOVE '1'       *IN63
     C                     MOVEA'11'      *IN,70
     C                     MOVEL'SEM1016' ZAMSID
     C                     EXSR ZASNMS
     C                     ELSE                            X*2
     C*
     C** CHECK COUNTRY CODE IS VALID
     C*
     C                     CALL 'AOCTRYR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM DD2CTA    @CNCD   2
     C           SDCTRY    PARM SDCTRY    DSFDY
     C*
     C           @RTCD     IFNE *BLANKS                    B**3
     C                     MOVE '1'       *IN63
     C                     MOVEA'11'      *IN,70
     C                     MOVEL'SEM1007' ZAMSID
     C                     EXSR ZASNMS
     C                     ELSE
     C                     MOVE A4CNCD    DD2CTA
     C                     END                             E**3
     C                     END                             E*2
     C*
     C** TAX BAND FOR TAX DEDUCTED AT SOURCE MUST BE BETWEEN 0 AND 5
     C*
     C                     MOVEL*BLANKS   NUMTAD  10
     C*
     C           DD2TAD    IFNE *BLANKS                    B*2
     C                     MOVELDD2TAD    NUMTAD
     C           NUMTAD    IFGT 5                          B**3
     C           NUMTAD    ORLT 0
     C                     MOVE '1'       *IN41
     C                     MOVEA'11'      *IN,70
     C                     MOVEL'SEM1028' ZAMSID
     C                     EXSR ZASNMS
     C*
     C** IF TAX BAND IS BETWEEN 1 AND 5 THEN A VALID RECORD MUST
     C** EXIST ON THE COUNTRY TAX RATES FILE
     C*
     C                     ELSE                            X**3
     C*
     C           NUMTAD    IFGT 0                          B***4
     C                     CALL 'AOSCTXR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM DD2CTA    @CTTA   2
     C                     PARM NUMTAD    @TABD   10
     C           SECNTR    PARM SECNTR    DSFDY
     C*
     C           @RTCD     IFNE *BLANKS                    B**3
     C                     MOVE '1'       *IN63
     C                     MOVEA'11'      *IN,70
     C                     MOVEL'SEM1026' ZAMSID
     C                     EXSR ZASNMS
     C                     END                             E**3
     C*
     C                     END                             E***4
     C                     END                             E**3
     C                     END                             E*2
     C*
     C** TAX BAND FOR TAX DEDUCTED BY USER MUST BE BETWEEN 0 AND 5
     C*
     C                     MOVEL*BLANKS   NUMTAU  10
     C           DD2TAU    IFNE *BLANKS                    B*2
     C                     MOVELDD2TAU    NUMTAU
     C           NUMTAU    IFGT 5                          B**3
     C           NUMTAU    ORLT 0
     C                     MOVE '1'       *IN42
     C                     MOVEA'11'      *IN,70
     C                     MOVEL'SEM1028' ZAMSID
     C                     EXSR ZASNMS
     C                     END                             E**3
     C                     END                             E*2
     C*
     C** RATE OF TAX DEDUCTED AT SOURCE MUST BE IN 5,2 FORMAT
     C*
     C***********DD2RAS    IFEQ '0.00'                     B*2            S01496
     C           DD2RAS    IFEQ '0.0000'                   B*2            S01496
     C***********          MOVE *BLANK    WWRASO  52                      S01496
     C                     MOVE *BLANK    WWRASO  74                      S01496
     C                     ELSE                            X*2
     C***********          MOVE DD2RAS    WWFLIN  6                       S01496
     C                     MOVE DD2RAS    WWFLIN  8                       S01496
     C                     Z-ADD43        XX      20
     C           S01496    IFEQ 'Y'                                       084256
     C                     MOVE 'SEM1057' ZAMSID                          084256
     C                     ELSE                                           084256
     C                     MOVE 'SEM1012' ZAMSID
     C                     END                                            084256
     C                     EXSR #ZABA
     C           WWVALD    IFEQ 'Y'                        B**3
     C***********          Z-ADDWW1502    WWRASO                          S01496
     C           S01496    IFEQ 'Y'                                       084256
     C                     Z-ADDWW1504    WWRASO                          S01496
     C                     ELSE                                           084256
     C                     Z-ADDWW1502    WWRASO                          084256
     C                     END                                            084256
     C                     MOVE WW20CH    DD2RAS
     C                     END                             E**3
     C                     END                             E*2
     C*
     C** IF TAX BAND FOR TAX DEDUCTED AT SOURCE IS NOT BLANK
     C** THEN RATE OF TAX DEDUCTED AT SOURCE MUST NOT BE ENTERED
     C*
     C           NUMTAD    IFGE 1                          B*2
     C           NUMTAD    ANDLE5
     C*
     C           WWRASO    IFNE 0                          B**3
     C                     MOVE '1'       *IN43
     C                     MOVEA'11'      *IN,70
     C                     MOVEL'SEM1027' ZAMSID
     C                     EXSR ZASNMS
     C                     ELSE                            X**3
     C                     MOVE *BLANK    WWRASO
     C                     END                             E**3
     C                     END                             E*2
     C*
     C                     END                             E1
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C** #A         - THIS SUBROUTINE PERFORMS INITIALISATION            *
     C**                                                                 *
     C**  CALLS     - NONE                                               *
     C**                                                                 *
     C**  CALLED BY - MAINLINE                                           *
     C**                                                                 *
     C********************************************************************
     C           #A        BEGSR
     C*
     C*
     C**   INITIALISE DB ERROR FIELDS ETC, AND STORE PROGRAM NAME FOR
     C**   DB ERROR FIELD.
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     MOVE *BLANKS   DBASE
     C                     MOVEL'SE1960'  DBPGM
     C                     OUT  LDA
     C*
     C                     Z-ADD*ZERO     WWDSTN  50
     C                     MOVE '1'       *IN26
     C                     MOVE *BLANKS   ZAMSGF
     C                     MOVEL'SEUSRMSG'ZAMSGF
     C*
     C** IN THE DATAAREA RUNDAT
     C*
     C           *NAMVAR   DEFN RUNDAT    RUNDT
     C           *NAMVAR   DEFN           ZMUSER
     C                     IN   RUNDT
     C                     IN   ZMUSER
     C*
     C**   CHECK SYSTEM DATE FORMAT, DDMMYY OR MMDDYY.
     C*
     C           DATF      COMP 'M'                      98
     C*                                                                   084256
     C**  Access SAR details file to see if Private Banking Securities    084256
     C**  Trading Enhancements switchable feature is installed.           084256
     C*                                                                   084256
     C                     CALL 'AOSARDR0'                                084256
     C                     PARM *BLANKS   @RTCD                           084256
     C                     PARM '*VERIFY' @OPTN                           084256
     C                     PARM 'S01496'  @SARD   6                       084256
     C           SCSARD    PARM SCSARD    DSFDY                           084256
     C*                                                                   084256
     C           @RTCD     IFEQ *BLANK                                    084256
     C                     MOVE 'Y'       S01496  1                       084256
     C                     END                                            084256
     C*
     C***********          Z-ADD2         ZADEC                           S01496
     C***********          Z-ADD2         ZDECS                           S01496
     C*                                                                   S01408
     C/COPY WNCPYSRC,SE1960CCP1                                           S01408
     C*                                                                   S01408
     C           S01496    IFEQ 'Y'                                       084256
     C                     Z-ADD4         ZADEC                           S01496
     C                     ELSE                                           084256
     C                     Z-ADD2         ZADEC                           084256
     C                     END                                            084256
     C                     Z-ADD4         ZDECS                           S01496
     C                     Z-ADD3         ZADIG
     C                     MOVE 'J'       ZECODE
     C*
     C                     MOVE *BLANKS   WWTPGQ
     C                     MOVEL'*'       WWTPGQ
     C                     MOVE *BLANKS   WWPGQR
     C                     MOVEL'*PRV'    WWPGQR
     C*
     C                     MOVE '1'       *IN71
     C*
     C** INITIALISE SUBFILE
     C                     MOVE '1'       *IN30
     C                     WRITESE1960C5
     C                     MOVE '0'       *IN30
     C*
     C** INITIALISE SCREEN MESSAGE QUEUE
     C                     MOVEL'*'       DDPGMQ
     C*
     C                     OPEN SESWHTL0                                                    MD027346
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C** #ZB       - THIS SUBROUTINE LOADS A PAGE OF RECORDS TO SUBFILE  *
     C**                                                                 *
     C** CALLS     - NONE                                                *
     C**                                                                 *
     C** CALLED BY - #BB, #BBA                                           *
     C**                                                                 *
     C********************************************************************
     C           #ZB       BEGSR
     C*
     C                     Z-ADD*ZERO     WWLRRN  40
     C                     Z-ADD*ZERO     DDRRN
     C                     Z-ADD*ZERO     WWCNTR
     C                     MOVEL*BLANKS   DD2SEL
     C                     MOVEA'00'      *IN,28
     C                     MOVE '0'       *IN31
     C                     MOVE '0'       *IN61
     C                     MOVE '0'       *IN69
     C*
     C                     MOVE '1'       *IN30
     C                     WRITESE1960C5
     C                     MOVE '0'       *IN30
     C*
     C           *IN32     IFEQ '1'
     C           *LOVAL    SETLLSESWHTL0
     C                     ELSE
     C                     MOVE DD1RVF    WWKRVF 10
     C           WWKRVF    SETLLSESWHTL0
     C                     END
     C*
     C                     READ SESWHTL0                 51
     C*
     C**  LOOP LOADING SUB-FILE RECORDS TILL FULL OR THERE ARE NO MORE
     C**  TO LOAD
     C*
     C           *IN51     DOWEQ'0'
     C           WWCNTR    ANDLT13
     C*
     C**    CHECK FOR A LIVE SECURITY
     C*
     C           S1SESN    CHAINSECTY                52
     C*
     C**      IF NO SECURITY FOUND THEN THIS IS A DATABASE ERROR
     C*
     C           *IN52     IFEQ '1'
     C                     SETON                     U7U8LR
     C           *LOCK     IN   LDA
     C                     MOVEL'SECTY   'DBFILE           ************
     C                     MOVEL'001'     DBASE            * DBERR 01 *
     C                     MOVELS1SESN    DBKEY            ************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C**    ONLY LOAD RECORDS FOR LIVE SECURITIES
     C*
     C           RECI      IFEQ 'D'
     C*
     C                     MOVELS1SESN    DD2SSN
     C                     MOVELS1CTTA    DD2CTA
     C*
     C           S1RASO    IFNE *ZEROS
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE S1RASO    ZFLD15
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    WWFL21
     C           S01496    IFEQ 'Y'                                       084256
     C                     MOVE WW20CH    DD2RAS
     C                     ELSE                                           084256
     C                     MOVE WW18CH    DD2RAS                          084256
     C                     END                                            084256
     C*
     C                     ELSE
     C                     MOVE *BLANKS   DD2RAS
     C***********          MOVE '0.00'    DD2RAS                          S01496
     C***********          MOVE '0.0000'  DD2RAS                    S01496084256
     C                     END
     C*
     C                     MOVELS1TABD    DD2TAD
     C                     MOVELS1TABU    DD2TAU
     C*
     C                     ADD  1         WWCNTR  40
     C                     ADD  1         DDRRN
     C*
     C                     WRITESE1960S5
     C*
     C                     END
     C*
     C                     READ SESWHTL0                 51
     C*
     C                     END
     C*
     C**  IF NO RECORD WAS WRITTEN TO SUBFILE THEN INDICATE
     C*
     C           WWCNTR    IFLE *ZERO
     C           *IN51     ANDEQ'1'
     C                     MOVE '1'       *IN29
     C                     ELSE
     C                     MOVEA'11'      *IN,28
     C                     END
     C*
     C           *IN51     IFEQ '0'
     C                     MOVELS1SESN    DD1RVF
     C                     EXCPTSESWHT
     C                     ELSE
     C                     MOVE '1'       *IN31
     C                     MOVEL*BLANKS   DD1RVF
     C                     END
     C*
     C                     Z-ADDDDRRN     WWLRRN
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C** #ZAAA     - THIS SETS UP COMMITMENT TEXT                        *
     C**                                                                 *
     C** CALLS     - NONE                                                *
     C**                                                                 *
     C** CALLED BY - #ZAA                                                *
     C**                                                                 *
     C********************************************************************
     C           #ZAAA     BEGSR
     C*
     C**   SET UP COMMITMENT TEXT
     C                     MOVEL'SE'      MDID
     C                     MOVEL'SE1960'  PGMN
     C                     MOVELWSID      WSIDX
     C                     MOVELUSID      USIDX
     C                     MOVE *BLANKS   ACTNX
     C                     TIME           MTIME
     C*
     C           CMTTXT    COMIT
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C** #BBAA     - THIS SUBROUTINE VALIDATES SELECTION ON SUBFILE      *
     C**                                                                 *
     C** CALLS     - NONE                                                *
     C**                                                                 *
     C** CALLED BY - #BBA                                                *
     C**                                                                 *
     C********************************************************************
     C           #BBAA     BEGSR
     C*
     C                     MOVE 'N'       WWIOVR  1
     C*
     C           DD2SEL    IFNE 'I'
     C           DD2SEL    ANDNE'A'
     C           DD2SEL    ANDNE'E'
     C*
     C                     MOVEL'SEM1031' WWMSGD  7
     C                     MOVE '1'       *IN71
     C*
     C                     ELSE
     C*
     C           DD2SEL    IFEQ 'I'
     C*
     C                     MOVEL'SEM1029' WWMSGD
     C                     MOVE '1'       *IN71
     C*
     C                     ELSE                                                            AR397187A
      *                                                                                    AR397187A
      ** Check if user is authorise on the action code.                                    AR397187A
      *                                                                                    AR397187A
     C                     MOVE DD2SEL    ACTCDE                                           AR397187A
     C                     EXSR SRVACT                                                     AR397187A
     C                     END
     C*
     C                     END
     C*
     C**   IF VALID ENTRY
     C           *IN71     IFEQ '0'
     C*
     C                     MOVELDD2SEL    DD1ACT
     C                     MOVELDD2SSN    DD1SSN
     C*
     C**   IF SELECT EQUALS 'I' OR 'A', SET FORMAT FLAG TO 'IA'
     C**   IF SELECT EQUALS 'E', SET FORMAT FLAG TO 'EN'
     C           DD2SEL    IFEQ 'I'
     C                     MOVEL'IA'      WWFMT   2
     C                     MOVE *BLANKS   DD2RAS
     C***********          MOVE '0.00'    DD2RAS                          S01496
     C***********          MOVE '0.0000'  DD2RAS                    S01496084256
     C                     MOVE *BLANKS   DD2TAD
     C                     MOVE *BLANKS   DD2TAU
     C                     MOVE *BLANKS   DD2CTA
     C*
     C                     ELSE
     C*
     C           DD2SEL    IFEQ 'A'
     C                     MOVEL'IA'      WWFMT
     C                     ELSE
     C*
     C           DD2SEL    IFEQ 'E'
     C                     MOVEL'EN'      WWFMT
     C*
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT                                                                               AR397187A
      *****************************************************************                    AR397187A
      *                                                               *                    AR397187A
      *  SRVACT  - Validates user profile if authorise on the action  *                    AR397187A
      *            code entered.                                      *                    AR397187A
      *                                                               *                    AR397187A
      *  Called by: #BA                                               *                    AR397187A
      *             #BBAA                                             *                    AR397187A
      *                                                               *                    AR397187A
      *  Calls: ZVACTU - Validate Action Code using user/pgm          *                    AR397187A
      *         ZVACTBU - Validate Action Code using user/pgm/brch    *                    AR397187A
      *                                                               *                    AR397187A
      *****************************************************************                    AR397187A
      *                                                                                    AR397187A
     C           SRVACT    BEGSR                                                           AR397187A
      *                                                                                    AR397187A
      ** If single branch environment                                                      AR397187A
      *                                                                                    AR397187A
     C           MBIN      IFNE 'Y'                                                        AR397187A
     C                     CALL 'ZVACTU'                                                   AR397187A
     C                     PARM ACTCDE    @ZACTN  1                                        AR397187A
     C                     PARM           @ERR    10                                       AR397187A
      *                                                                                    AR397187A
     C           @ERR      IFNE *ZERO                                                      AR397187A
     C           WWSFL     IFEQ 'N'                                                        AR397187A
     C                     MOVE '1'       *IN61                                            AR397187A
     C                     MOVEA'11'      *IN,70                                           AR397187A
     C                     MOVEL'SEM1019' ZAMSID                                           AR397187A
     C                     EXSR ZASNMS                                                     AR397187A
     C                     ELSE                                                            AR397187A
     C                     MOVEL'SEM1019' WWMSGD                                           AR397187A
     C                     MOVE '1'       *IN71                                            AR397187A
     C                     ENDIF                                                           AR397187A
     C                     ENDIF                                                           AR397187A
      *                                                                                    AR397187A
     C                     ELSE                                                            AR397187A
      *                                                                                    AR397187A
      ** If multibranching environment                                                     AR397187A
      *                                                                                    AR397187A
     C                     CALL 'ZVACTBU'                                                  AR397187A
     C                     PARM ACTCDE    @ZACTN  1                                        AR397187A
     C                     PARM USRBCH    @ZBR    3                                        AR397187A
     C                     PARM           @ERR    10                                       AR397187A
      *                                                                                    AR397187A
     C           @ERR      IFEQ 2                                                          AR397187A
     C           WWSFL     IFEQ 'N'                                                        AR397187A
     C                     MOVE '1'       *IN61                                            AR397187A
     C                     MOVEA'11'      *IN,70                                           AR397187A
     C                     MOVEL'SEM1021' ZAMSID                                           AR397187A
     C                     EXSR ZASNMS                                                     AR397187A
     C                     ELSE                                                            AR397187A
     C                     MOVEL'SEM1021' WWMSGD                                           AR397187A
     C                     MOVE '1'       *IN71                                            AR397187A
     C                     ENDIF                                                           AR397187A
     C                     ENDIF                                                           AR397187A
      *                                                                                    AR397187A
     C           @ERR      IFEQ 1                                                          AR397187A
     C           WWSFL     IFEQ 'N'                                                        AR397187A
     C                     MOVE '1'       *IN61                                            AR397187A
     C                     MOVEA'11'      *IN,70                                           AR397187A
     C                     MOVEL'SEM1020' ZAMSID                                           AR397187A
     C                     EXSR ZASNMS                                                     AR397187A
     C                     ELSE                                                            AR397187A
     C                     MOVEL'SEM1020' WWMSGD                                           AR397187A
     C                     MOVE '1'       *IN71                                            AR397187A
     C                     ENDIF                                                           AR397187A
     C                     ENDIF                                                           AR397187A
      *                                                                                    AR397187A
     C                     ENDIF                                                           AR397187A
      *                                                                                    AR397187A
     C                     ENDSR                                                           AR397187A
      *****************************************************************                    AR397187A
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C** #ZAA      - THIS SUBROUTINE DOES UPDATE PROCESSING              *
     C**                                                                 *
     C** CALLS     - #BCAA, ZASNMS, ZTNLU1, *PSSR                        *
     C**                                                                 *
     C** CALLED BY - #ZA                                                 *
     C**                                                                 *
     C********************************************************************
     C           #ZAA      BEGSR
     C*
     C                     MOVEA'00'      *IN,70
     C                     MOVE 'Y'       WWUPDT  1
     C                     MOVE 'N'       WWINST  1
     C                     MOVE 'N'       WWAMDT  1
     C                     MOVE 'N'       WWDELT  1
     C*
     C                     EXSR ZTNLU1
     C*
     C                     MOVE DD1SSN    WWKSSN
     C********** WWKSSN    CHAINSESWHTL0             51                                     MD027346
     C           WWKSSN    CHAINSESWHTDX             5153                                   MD027346
      *                                                                                     MD027346
      ** If chain not done due to record being updated by another                           MD027346
      ** workstation send message to screen.                                                MD027346
      *                                                                                     MD027346
     C           *IN53     IFEQ '1'                                                         MD027346
     C                     MOVE '1'       *IN99                                             MD027346
     C                     MOVEA'11'      *IN,70                                            MD027346
     C                     MOVEL'MMM1067' ZAMSID                                            MD027346
     C                     MOVEL'DRSMM   'ZAMSGF                                            MD027346
     C                     EXSR ZASNMS                                                      MD027346
     C                     ENDIF                                                            MD027346
     C*
     C           DD1ACT    IFEQ 'I'
     C           DD1ACT    OREQ 'A'
     C** RECORD NOT ON FILE
     C           *IN51     IFEQ '1'
     C*
     C           DD1ACT    IFEQ 'I'
     C*
     C                     MOVELDD1SSN    S1SESN
     C                     Z-ADDWWRASO    S1RASO
     C                     MOVELDD2CTA    S1CTTA
     C                     MOVELDD2TAD    S1TABD
     C                     MOVELDD2TAU    S1TABU
     C*
     C**********           WRITESESWHTD0               54                                   MD027346
     C                     WRITESESWHTDX               54                                   MD027346
     C*
     C**   IF DUPLICATE KEY ERROR
     C           *IN54     IFEQ '1'
     C*
     C           STATUS    IFEQ 01021
     C*
     C                     ROLBK
     C*
     C**   CLEAR MESSAGES CAUSED BY STATUS ERROR
     C                     CALL 'Y2CLMSC'
     C                     PARM           WWTPGQ
     C                     PARM           WWPGQR
     C                     GOTO #ZA0
     C                     ELSE
     C*
     C                     EXSR *PSSR
     C*
     C                     END
     C*
     C                     ELSE
     C*
     C                     EXSR #ZAAA
     C*
     C                     END
     C*
     C                     ELSE
     C*
     C                     GOTO #ZA0
     C                     END
     C** RECORD EXISTS ON FILE
     C                     ELSE
     C*
     C*
     C**   MOVE SCREEN FIELDS ETC TO FILE FIELDS AS NECESSARY
     C           DD1ACT    IFEQ 'A'
     C*
     C                     MOVELDD1SSN    S1SESN
     C                     Z-ADDWWRASO    S1RASO
     C                     MOVELDD2CTA    S1CTTA
     C                     MOVELDD2TAD    S1TABD
     C                     MOVELDD2TAU    S1TABU
     C*
     C**********           UPDATSESWHTD0                                                    MD027346
     C                     UPDATSESWHTDX                                                    MD027346
     C                     ELSE
     C*
     C                     END
     C*
     C                     EXSR #ZAAA
     C*
     C                     END
     C*
     C                     END
     C*
     C                     GOTO #ZA1
     C*
     C           #ZA0      TAG
     C                     MOVEA'11'      *IN,70
     C                     MOVEL'SEM1022' ZAMSID
     C                     EXSR ZASNMS
     C*
     C           #ZA1      ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C** ZTNLU1    - RETRIEVE/UPDATE TRANS NO. OF LAST UPDATE            *
     C**                                                                 *
     C** CALLS     - NONE                                                *
     C**                                                                 *
     C** CALLED BY - #ZAA                                                *
     C**                                                                 *
     C********************************************************************
     C           ZTNLU1    BEGSR                                        **
     C*
     C           *NAMVAR   DEFN MNATN     DNATN
     C           *LOCK     IN   DNATN
     C                     Z-ADDFNATN     WWDSTN
     C                     Z-ADDFNATN     NATN
     C                     ADD  1         FNATN
     C                     OUT  DNATN
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C** ZASNMS    - Send message to program's message queue             *
     C**                                                                 *
     C** CALLS     - Y2SNMGC                                             *
     C**                                                                 *
     C** CALLED BY - #BA, #BBA, #ZAA                                     *
     C**                                                                 *
     C********************************************************************
     C           ZASNMS    BEGSR                           ** ZASNMS    **
     C*
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGM  10
     C                     PARM           ZAPGRL  5
     C                     PARM           ZAMSID  7
     C                     PARM           ZAMSGF 10
     C                     PARM           ZAMSDA132
     C                     PARM           ZAMSTP  7
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C** *PSSR     - PROGRAM ERROR HANDLING ROUTINE                      *
     C**                                                                 *
     C** CALLS     - NONE                                                *
     C**                                                                 *
     C** CALLED BY - #ZAA                                                *
     C**                                                                 *
     C********************************************************************
     C           *PSSR     BEGSR
     C*
     C           @RUN      IFEQ *BLANKS
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
     C*
     C                     ROLBK
     C*
     C                     CALL 'DBERRCTL'
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**
     C**   ZALIGN SR. TO VALIDATE AND RIGHT-ALIGN NUMERIC FIELDS.
     C**
     C           ZALIGN    BEGSR                           *** ZALIGN ***
     C**
     C                     SETOF                     929399
     C**
     C**   SAVE INPUT FIELD IN ARRAY, ZA1.
     C                     MOVEAZFIELD    ZA1
     C**
     C**   CALCULATION TO DEFINE NUMBER STRUCTURE CONTROL FIELDS.
     C                     Z-ADDZADIG     ZADIG   20
     C                     Z-ADDZADEC     ZADEC   10
     C**
     C**   CALCULATIONS TO DEFINE/CLEAR FIELDS.
     C                     MOVE ' '       ZA2
     C                     MOVEAZA2       ZFIELD 16
     C                     Z-ADD0         ZX      20
     C                     Z-ADD0         ZY      20
     C**
     C**   ENSURE REQUIRED STRUCTURE OF FIELD IS VALID.
     C           ZADIG     ADD  ZADEC     ZZ      20
     C           ZZ        COMP 15                   99
     C   99                GOTO ZAEND
     C**
     C**   LOOP TO FIND DECIMAL POINT, BLANKS AND CHARACTERS.
     C           ZATAG1    TAG                             ** ZATAG1 TAG *
     C           ZX        ADD  1         ZX
     C**
     C**   CHECK FOR DECIMAL POINT. ERROR IF SECOND DECIMAL POINT.
     C           ZA1,ZX    COMP '.'                      90
     C   90 93             SETON                     99
     C   99                GOTO ZAEND
     C**
     C**
     C**   CHECK FOR 'M' CONSTANT. ERROR IF NOT LAST CHARACTER ENTERED
     C           ZA1,ZX    COMP 'M'                      96
     C   96                DO
     C           ZX        COMP 15                   99  99
     C   99                GOTO ZAEND
     C           ZX        ADD  1         ZX
     C           ZA1,ZX    COMP ' '                  9999
     C   99                GOTO ZAEND
     C           ZX        SUB  1         ZX
     C           ZX        COMP 9                    99
     C   99                GOTO ZAEND
     C                     END
     C**
     C**   CHECK FOR 'T' CONSTANT. ERROR IF NOT LAST CHARACTER ENTERED
     C           ZA1,ZX    COMP 'T'                      97
     C   97                DO
     C           ZX        COMP 15                   99  99
     C   99                GOTO ZAEND
     C           ZX        ADD  1         ZX
     C           ZA1,ZX    COMP ' '                  9999
     C   99                GOTO ZAEND
     C           ZX        SUB  1         ZX
     C           ZX        COMP 12                   99
     C   99                GOTO ZAEND
     C                     END
     C**
     C**   CLEAR ALPHAMERIC CONSTANT FROM ZA1
     C   96
     COR 97                MOVE ' '       ZA1,ZX
     C**
     C**   CHECK FOR BLANKS. BYPASS FOR FIRST BLANK AFTER A DIGIT.
     C           ZA1,ZX    COMP ' '                      91
     C   91 92             GOTO ZATAG2
     C**
     C**   CHECK FOR NUMERIC, IF NOT '.' OR ' '.
     C  N90N91   ZA1,ZX    COMP '0'                    99
     C  N90N91N99ZA1,ZX    COMP '9'                  99
     C   99                GOTO ZAEND                      NOT NUMERIC
     C**
     C**   STORE DIGITS IN ARRAY AND HOW MANY.
     C**   ZY, TOTAL OF DIGITS IN THE INPUT FIELD.
     C**   ZZ, TOTAL OF DIGITS TO THE LEFT OF DECIMAL POINT.
     C  N90N91   ZY        ADD  1         ZY         92
     C  N90N91             MOVE ZA1,ZX    ZA2,ZY
     C   90                Z-ADDZY        ZZ         93  93
     C**
     C           ZX        COMP 16                     94  CHECK IF ALL
     C   94                GOTO ZATAG1
     C**
     C           ZATAG2    TAG                             ** ZATAG2 TAG *
     C**
     C**   IF 'M' SPECIFIED, MULTIPLY ZA1 BY 1 MILLION
     C   96      ZZ        ADD  6         ZZ
     C**
     C**   IF 'T' SPECIFIED, MULTIPLY ZA1 BY 1 THOUSAND
     C   97      ZZ        ADD  3         ZZ
     C**
     C**   FILL IN ZEROS IN ANY BLANKS LEFT OF DECIMAL POINT
     C   96 93
     COR 97 93   ZZ        DOWGTZY
     C           ZY        ADD  1         ZY
     C                     MOVE '0'       ZA2,ZY
     C                     END
     C**
     C**   IF CONSTANT SPECIFIED WITH NO DECIMAL POINT ZEROISE BLANKS
     C   96N93             DO   6
     C           ZY        ADD  1         ZY
     C                     MOVE '0'       ZA2,ZY
     C                     END
     C**
     C   97N93             DO   3
     C           ZY        ADD  1         ZY
     C                     MOVE '0'       ZA2,ZY
     C                     END
     C**
     C**   IF NO DECIMAL POINT FOUND, SET TOTAL FIELD ZZ.
     C  N93                Z-ADDZY        ZZ
     C**
     C**   CHECK FOR EMBEDDED BLANKS.
     C   91 92             MOVEAZA1,ZX    ZFIELD
     C   91 92   ZFIELD    COMP ' '                  9999
     C   99                GOTO ZAEND                      EMBEDDED BLANK
     C**
     C**   ENSURE THAT NUMBER OF DIGITS ENTERED EITHER SIDE
     C**   OF DECIMAL POINT ARE NOT MORE THAN ALLOWED.
     C           ZZ        COMP ZADIG                99
     C           ZY        SUB  ZZ        ZX
     C  N99      ZADEC     SUB  ZX        ZX           9995
     C   99                GOTO ZAEND
     C   95                GOTO ZATAG4                     NO TRAILING
     C**                                                   BLANKS.
     C**   FILL THE TRAILING BLANKS WITH ZEROS.
     C           ZY        ADD  ZX        ZZ
     C           ZATAG3    TAG                             ** ZATAG3 TAG *
     C           ZY        ADD  1         ZY
     C                     MOVE '0'       ZA2,ZY
     C           ZY        COMP ZZ                     94
     C   94                GOTO ZATAG3
     C**
     C**   RIGHT-ALIGN THE VALUE BY MOVING BACK TO ARRAY, ZA1.
     C           ZATAG4    TAG                             ** ZATAG4 TAG *
     C                     MOVE '0'       ZA1
     C                     Z-ADD16        ZX
     C           ZATAG5    TAG                             ** ZATAG5 TAG *
     C           ZY        COMP 0                        94
     C   94                GOTO ZATAG6
     C                     MOVE ZA2,ZY    ZA1,ZX
     C           ZY        SUB  1         ZY
     C           ZX        SUB  1         ZX
     C                     GOTO ZATAG5
     C           ZATAG6    TAG                             ** ZATAG6 TAG *
     C**
     C**   MOVE VALIDATED AND RIGHT-ALIGNED NUMBER BACK INTO ZFIELD.
     C                     MOVEAZA1       ZFIELD
     C**
     C                     SETOF                     9697
     C**
     C           ZAEND     ENDSR                           * ZAEND ENDSR *
     C**
     C**
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C** ZSEDIT subroutine to insert a decimal point and sign into a     *
     C** numeric field and to blank out leading zeros (optionaly         *
     C** inserting commas).                                              *
     C**     Input fields:   ZFLD15 15/0                                 *
     C**                     ZDECS  1/0                                  *
     C**                     ZECODE 1/  ('J', 'L' or blank)              *
     C**                                                                 *
     C**     Arrays:         ZS1    15/1/0                               *
     C**                     ZS2    21/1/                                *
     C**                                                                 *
     C**     Output fields:  ZOUT21 21                                   *
     C**                                                                 *
     CSR         ZSEDIT    BEGSR                           **  ZSEDIT   *
     C*
     C* Define/Clear fields
     C*
     C                     Z-ADDZFLD15    ZFLD15 150
     C                     Z-ADDZDECS     ZDECS   10
     C                     MOVE ZECODE    ZECODE  1
     C                     MOVE *BLANKS   ZOUT21 21
     C*
     C*       SET UP WORK FIELDS
     C*
     C                     Z-ADD0         ZS1
     C                     MOVE ' '       ZS2
     C*
     C                     Z-ADD15        Z1      20
     C                     Z-ADD20        Z2      20
     C*
     C           15        SUB  ZDECS     ZINTS   20
     C*
     C* Check if numeric field is negative
     C*
     C           ZFLD15    IFLT *ZEROS
     C                     MOVE '-'       ZS2,21
     C                     Z-SUBZFLD15    ZFLD15
     C                     END
     C*
     C                     Z-ADDZFLD15    WORK15
     C*
     C*       CHECK TO SEE IF THERE ARE ANY DECIMAL PLACES
     C*
     C           ZDECS     CABEQ0         ZS10
     C*
     C*       SET UP DECIMALS
     C*
     C                     Z-ADD*ZEROS    ZCNT    10
     C           ZCNT      DOUEQZDECS
     C                     MOVE ZS1,Z1    ZS2,Z2
     C                     SUB  1         Z2
     C                     ADD  1         ZCNT
     C                     SUB  1         Z1
     C                     END
     C*
     C*       PUT IN DECIMAL PLACE
     C*
     C                     MOVE '.'       ZS2,Z2
     C                     SUB  1         Z2
     C*
     C           ZS10      TAG                             ** ZS10 TAG **
     C*
     C*       SET UP INTEGERS
     C*
     C                     Z-ADD*ZEROS    CNT3    10
     C           Z1        DOUEQ*ZEROS
     C                     MOVE ZS1,Z1    ZS2,Z2
     C                     SUB  1         Z1
     C                     SUB  1         Z2
     C*
     C* If edit code is 'J', insert a ',' before each group of three
     C* digits - not if beginning of array reached.
     C*
     C           Z2        IFGT *ZEROS
     C           ZECODE    ANDEQ'J'
     C                     ADD  1         CNT3
     C           CNT3      IFEQ 3
     C                     MOVE ','       ZS2,Z2
     C                     SUB  1         Z2
     C                     Z-ADD*ZEROS    CNT3
     C                     END
     C                     END
     C*
     C                     END
     C*
     C*       PUT IN LEADING BLANKS
     C*
     C                     Z-ADD1         Z2
     C           ZS2,Z2    DOWEQ'0'
     C           ZS2,Z2    OREQ ' '
     C           ZS2,Z2    OREQ ','
     C                     MOVE ' '       ZS2,Z2
     C                     ADD  1         Z2
     C           Z2        CABEQ22        ZS20
     C                     END
     C*
     C*       IF NO INTEGERS PUT IN LEADING ZERO
     C*
     C           ZS20      TAG                             ** ZS20 TAG **
     C*
     C           Z2        IFEQ 22
     C                     SUB  2         Z2
     C                     MOVE '0'       ZS2,Z2
     C                     ELSE
     C*
     C           ZS2,Z2    IFEQ '.'
     C                     SUB  1         Z2
     C                     MOVE '0'       ZS2,Z2
     C                     END
     C                     END
     C*
     C*       SET UP OUTPUT FIELD
     C*
     C                     MOVEAZS2       ZOUT21
     C*
     CSR                   ENDSR
     C*****************************************************************
     C********************************************************************
     C/EJECT
     O* RELEASE RECORD
     O*
     O***SESWHTD0E                SESWHT                                                    MD027346
     OSESWHTDXE                SESWHT                                                       MD027346
     O********************************************************************
     O********************************************************************
     O/EJECT
** CPY@
(c) Finastra International Limited 2001
