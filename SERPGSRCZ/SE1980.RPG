     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas Country to Branch Tax Rates Report')
     F*****************************************************************
     F*                                                               *
     F*  Midas  Securities Trading Module                         *
     F*                                                               *
     F*  SE1980 - Country to Branch Tax Rates Report                  *
     F*                                                               *
     F*  Function: Program produces a report listing Tax Rates by     *
     F*            country.                                           *
     F*            It runs in two modes, Full and Audit.              *
     F*            Created as part of SAR S01447, Withholding Tax     *
     F*                                                               *
     F*  Called By: SEC1980 - Country Tax Rates Report Audit File     *
     F*                                                               *
     F*  Calls: AOBANKR0 - Access object for Bank I.C.D. details      *
     F*         ZSFILE   - Log spool file                             *
     F*         AOCTRYR0 - Access object for Country Codes            *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. CRE026             Date 24May06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 BUG3644            Date 12Jul04               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01408             Date 22Aug96               *
     F*                 084256             Date 06Sep95               *
     F*                081439              DATE 10JAN95               *
     F*                S01496              DATE 11OCT94               *
     F*                S01447              DATE DDMMMYY               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CRE026 - Consumer Banking (Recompile)                        *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  BUG3644 - Serious Error in Collateralised Lending (Recompile)*
      *  CGL029 - Increase Account Code to 10 digits                  *
     F*  084256 - Tax rate change should be switchable                *
     F*  081439 - Wrong update of field tax rate - Recompile          *
     F*  S01496 - Incorporation of JYSKE Enhancements (S10982)        *
     F*           Program recompiled due to changes in PRTF/SE1980P1  *
     F*  S01447 - Created as part of S01447 SE  Withholding Tax       *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     F**
     F**      FILE DEFINITIONS
     F**     ------------------
     F*
     F**   COUNTRY TO BRANCH TAX RATES (COUNTRY SEQUENCE)     - PREFIX TB
     FSECBTXL0IF  E           K        DISK         KINFSR *PSSR
     F            SECBTXD0                          KRENAMESECBTXDA
     F*
     F**   COUNTRY TO BRANCH TAX RATES (LAST CHANGE SEQUENCE) - PREFIX TB
     FSECBTXL1IF  E           K        DISK         KINFSR *PSSR
     F*
     F**   COUNTRY TO BRANCH TAX RATES RUN CONTROLS REPORT
     FSE1980AUO   E             21     PRINTER      KINFDS SPOOLU
     F                                              KINFSR *PSSR
     F*
     F**   COUNTRY TO BRANCH TAX RATES REPORT
     FSE1980P1O   E             22     PRINTER      KINFDS SPOOL      UC
     F                                              KINFSR *PSSR
     F*
     F/EJECT
     F*****************************************************************
     F**                                                              *
     F** ID F  C  H  L    FUNCTION OF INDICATORS                      *
     F**                 ------------------------                     *
     F**                                                              *
     F**    21         - OVERFLOW INDICATOR ON SE1980AU               *
     F**    22         - OVERFLOW INDICATOR ON SE1980P1               *
     F**       30 - FULL LIST INDICATOR FOR PRINTER FILE              *
     F**       37 - MULTIBRANCHING INDICATOR                          *
     F*        38 - Private Banking Securities Trading Enhancements on*   084256
     F**       51 - AUDIT REPORT                                      *
     F**       52 - AUDIT REPORT                                      *
     F**       53 - FULL LIST                                         *
     F**       40      - FIRST TIME THRO' FOR PRINTER FILE INDICATOR  *
     F**       70      - FILE ERROR                                   *
     F**                                                              *
     F*****************************************************************
     F/EJECT
     E********************************************************************
     E*
     E**   ARRAY FOR COPYRIGHT
     E*
     E                    CPY@    1   1 80
     E********************************************************************
     E/EJECT
     E*****************************************************************
     I*
     I**   DATA STRUCTURE FOR COMPILATION - COPYRIGHT INSERTION
     ICPYR@$      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@$$
     I*
     I**   PROGRAM STATUS DATA STRUCTURE
     IPSDS       SDS
     I                                      244 253 JOB
     I                                      254 263 USER
     I*
     I**   FILE INFORMATION DATA STRUCTURE
     ISDBRCH    E DSSDBRCHPD
     I*
     I** BRANCH DETAILS
     I*
     ISDCTRY    E DSSDCTRYPD
     I** EXTERNAL DS FOR COUNTRY DETAILS
     I*
     ISDBANK    E DSSDBANKPD
     I** EXTERNAL DS FOR BANK DETAILS
     I*                                                                   084256
     ISCSARD    E DSSCSARDPD                                              084256
     I** EXTERNAL DS FOR SWITCHABLE FEATURES                              084256
     I*
     IDSFDY     E DSDSFDY
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE
     I**   LOCAL DATA AREA
     ILDA        UDS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     IRUNDT       DS
     I*
     I** DATA AREA RUNDAT
     I*
     I                                        1   7 RUNA
     I                                    P   8  100RUND
     I                                       11  11 SSF
     I                                       12  12 DATF
     I                                       13  13 MBIN
     I**   DATE FORMATTING DATA STRUCTURE
     IUPDATE      DS
     I                                        1   2 TBDAY
     I                                        3   5 TBMON
     I                                        6   7 TBYER
     ISPOOL       DS
     I*
     I**  DS FOR P1 SPOOL FILE NAME
     I*
     I                                       83  92 SFILE
     I                                    B 123 1240SFNUM
     I                                    B 367 3680LINE1
     I**
     ISPOOLU      DS
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *                                                                                       CGL029
     IDSSDY     E DSDSSDY                                                                     CGL029
      *                                                                                       CGL029
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*
     C**              START MAINLINE
     C**             ----------------
     C*
     C**   INPUT PARAMETER LIST
     C*
     C           *ENTRY    PLIST
     C                     PARM           @MODE   1
     C                     PARM           @SEQ    5
     C                     PARM           @ENT    3
     C*
     C**   INITIAL PROCESSING
     C*
     C                     EXSR #A
     C*
     C**   MAIN PROCESSING
     C*
     C                     EXSR #B
     C*
     C**   END PROGRAM
     C*
     C                     MOVE '1'       *INLR
     C                     RETRN
     C*
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**    INDEX TO SUBROUTINES                                      *
     C**   ----------------------                                     *
     C**                                                              *
     C**                                                              *
     C**   #A      - INITIAL PROCESSING                               *
     C**   #B      - MAIN PROCESSING                                  *
     C**   #BA     - COUNTRY TO BRANCH TAX RATES FULL REPORT          *
     C**   #BC     - COUNTRY TO BRANCH TAX RATES AUDIT LIST           *
     C**   #BE     - PROCESSES CHANGE OF BRANCH                       *
     C**   #ZA     - DATABASE ERROR PRINTER OUTPUT                    *
     C**   #ZB     - SETUP OUTPUT FIELDS FOR TAX RATES REPORT         *
     C**   #BB     - FULL LIST RUN CONTROLS                           *
     C**   #BD     - AUDIT LIST RUN CONTROLS                          *
     C**   *PSSR   - PROGRAM ERROR HANDLING SUBROUINE                 *
     C**   RCFAU   - REGISTER AUDIT FILE                              *
     C**                                                              *
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #B        - This subroutine performs Main processing       *
     C**                                                              *
     C**   CALLS     - #BA #BB #BC                                    *
     C**                                                              *
     C**   CALLED BY - MAINLINE                                       *
     C**                                                              *
     C*****************************************************************
     C           #B        BEGSR
     C*
     C           @MODE     IFEQ 'I'
     C           @MODE     OREQ 'F'
     C                     MOVE '1'       *IN53
     C                     END
     C*
     C           @MODE     IFEQ 'A'
     C                     MOVE '1'       *IN52
     C                     END
     C*
     C           @MODE     IFEQ 'U'
     C                     MOVE '1'       *IN51
     C                     END
     C*
     C** CHECK INPUT PARAMETER
     C*
     C           @MODE     IFEQ 'I'                        B1
     C           @MODE     OREQ 'F'
     C*
     C** FULL LIST
     C*
     C                     EXSR #BA
     C*
     C** FULL LIST RUN CONTROLS PROCESSING
     C*
     C           RRPROG    IFNE 0
     C                     EXSR #BB
     C                     END
     C*
     C                     ELSE                            X1
     C           @MODE     IFEQ 'A'                        B*2
     C           @MODE     OREQ 'U'
     C*
     C** AUDIT LIST
     C*
     C                     EXSR #BC
     C*
     C** AUDIT LIST RUN CONTROLS PROCESSING
     C*
     C           RRPROG    IFNE 0
     C                     EXSR #BD
     C                     END
     C*
     C                     ELSE                            X*2
     C*
     C** STANDARD DATABASE ERROR HANDLING
     C*
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE
     C                     MOVEL'@MODE'   DBFILE           ***************
     C                     MOVEL@MODE     DBKEY            * DBERROR 002 *
     C                     EXSR #ZA                        ***************
     C                     EXSR *PSSR
     C                     OUT  LDA
     C                     END                             E*2
     C                     END                             E1
     C*
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #A        - This subroutine performs initial processing    *
     C**                                                              *
     C**   CALLS     -                                                *
     C**                                                              *
     C**   CALLED BY - MAINLINE                                       *
     C**                                                              *
     C*****************************************************************
     C*
     C           #A        BEGSR
     C                     Z-ADD*ZEROS    LINE1
     C                     MOVE '1'       *IN40
     C                     CLOSESE1980P1
     C*
     C** Initialise DB error fields and store program name for DB
     C** error field
     C*
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     MOVE *BLANKS   DBASE
     C                     MOVEL'SE1980'  DBPGM
     C                     OUT  LDA
     C*
     C** IN THE DATAAREA RUNDAT
     C*
     C           *NAMVAR   DEFN RUNDAT    RUNDT
     C                     IN   RUNDT
     C*
     C** Ensure Audit Spool File recorded by RCF
     C*
     C                     EXSR RCFAU
     C*
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C*
     C           BJDFIN    COMP 'M'                      98MMDDYY, 98 ON
     C*
     C           @RTCD     IFNE *BLANK
     C           BJTYLC    OREQ 'D'
     C           *LOCK     IN   LDA
     C                     MOVEL'SDBANKPD'DBFILE           ***************
     C                     MOVEL'001'     DBASE            * DBERROR 001 *
     C                     MOVEL@OPTN     DBKEY            ***************
     C                     MOVE '1'       *IN99
     C                     OUT  LDA
     C                     EXSR #ZA
     C                     EXSR *PSSR
     C                     END
     C*
     C** INITIALISE WORK FIELDS
     C                     MOVE '1'       WWFTT   1
     C*
     C** Initialise run controls work fields
     C                     Z-ADD*ZEROS    RRPROG  50
     C                     Z-ADD*ZEROS    RRPROI  50
     C                     Z-ADD*ZEROS    RRPROA  50
     C                     Z-ADD*ZEROS    RRTOTL  50
     C*
     C           MBIN      IFEQ 'Y'
     C                     MOVE '1'       *IN37
     C                     END
     C*                                                                   084256
     C**  Access SAR details file to see if Private Banking Securities    084256
     C**  Trading Enhancements switchable feature is installed.           084256
     C*                                                                   084256
     C                     CALL 'AOSARDR0'                                084256
     C                     PARM *BLANKS   @RTCD                           084256
     C                     PARM '*VERIFY' @OPTN                           084256
     C                     PARM 'S01496'  @SARD   6                       084256
     C           SCSARD    PARM SCSARD    DSFDY                           084256
     C*                                                                   084256
     C           @RTCD     IFEQ *BLANK                                    084256
     C                     MOVE 'Y'       S01496  1                       084256
     C                     SETON                     38                   084256
     C                     END                                            084256
     C*
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #BA       - COUNTRY TO BRANCH TAX RATES FULL REPORT        *
     C**                                                              *
     C**   CALLS     - #ZB #BE                                        *
     C**                                                              *
     C**   CALLED BY - #B                                             *
     C**                                                              *
     C*****************************************************************
     C*
     C           #BA       BEGSR
     C*
     C                     READ SECBTXL0                 70
     C           *IN70     DOWEQ'0'                         B1
     C*
     C** IF BRANCH REQUESTED IS NOT EQUAL TO BRANCH OF RCD BEING
     C** PROCESSED, THEN GET NEXT RECORD
     C*
     C           MBIN      IFEQ 'Y'                         B*2
     C           @ENT      ANDNETBBRCA
     C           @ENT      ANDNE'ALL'
     C                     READ SECBTXL0                 70
     C                     ELSE                             X*2
     C*
     C** PROCESS CHANGE OF BRANCH
     C*
     C           TBBRCA    IFNE WWBRCH                      B**3
     C                     EXSR #BE
     C                     END                              E**3
     C*
     C           TBRECI    IFEQ 'D'                         B**3
     C*
     C** ADD 1 TO NUMBER OF RECORDS READ
     C*
     C                     ADD  1         RRPROG
     C*
     C** SET UP OUTPUT FIELDS FOR COUNTRY TO BRANCH TAX RATES REPORT
     C*
     C                     EXSR #ZB
     C                     END                              E**3
     C                     READ SECBTXL0                 70
     C*
     C           *IN70     IFEQ '1'                         B**3
     C           TBBRCA    ORNE WWBRCH
     C                     WRITEFOOTING1
     C                     CLOSESE1980P1
     C                     MOVE '1'       WWFTT
     C                     END                              E**3
     C                     END                              E*2
     C                     END                              E1
     C*
     C** NO LIVE RECORDS FOUND ON LF SECBTXL0
     C*
     C           RRPROG    IFEQ 0                           B1
     C                     WRITEHEADER
     C                     WRITENULL1
     C                     ELSE                             X1
     C*
     C           LINE1     IFGT 55                          B*2
     C                     WRITEHEADER1
     C                     WRITEHEADER2
     C                     END                              E*2
     C                     END                              E1
     C*
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #BC       - COUNTRY TO BRANCH TAX RATES AUDIT LIST         *
     C**                                                              *
     C**   CALLS     - #ZB #BE                                        *
     C**                                                              *
     C**   CALLED BY - #B                                             *
     C**                                                              *
     C*****************************************************************
     C*
     C           #BC       BEGSR
     C*
     C                     READ SECBTXL1                 70
     C           *IN70     DOWEQ'0'                        B1
     C           BJRDNB    ANDEQTBLCD
     C*
     C** IF BRANCH REQUESTED IS NOT EQUAL TO BRANCH OF RCD BEING
     C** PROCESSED, THEN GET NEXT RECORD
     C*
     C           MBIN      IFEQ 'Y'                         B*2
     C           @ENT      ANDNETBBRCA
     C           @ENT      ANDNE'ALL'
     C                     READ SECBTXL1                 70
     C                     ELSE                             X*2
     C*
     C** PROCESS CHANGE OF BRANCH
     C*
     C           TBBRCA    IFNE WWBRCH                      B**3
     C                     EXSR #BE
     C                     END                              E**3
     C*
     C** ADD 1 TO NUMBER OF INSERT RECORDS READ
     C*
     C           TBCHTP    IFEQ 'I'                         B**3
     C                     ADD  1         RRPROI
     C                     ADD  1         RRPROG
     C                     END                              E**3
     C*
     C** ADD 1 TO NUMBER OF AMENDMENT RECORDS READ
     C*
     C           TBCHTP    IFEQ 'A'                         B**3
     C                     ADD  1         RRPROA
     C                     ADD  1         RRPROG
     C                     END                              E**3
     C*
     C** SET UP OUTPUT FIELDS FOR COUNTRY TO BRANCH TAX RATES REPORT
     C*
     C                     EXSR #ZB
     C                     READ SECBTXL1                 70
     C*
     C           *IN70     IFEQ '1'                         B**3
     C           TBBRCA    ORNE WWBRCH
     C                     WRITEFOOTING1
     C                     CLOSESE1980P1
     C                     MOVE '1'       WWFTT
     C                     END                              E**3
     C                     END                              E*2
     C                     END                              E1
     C*
     C** NO LIVE RECORDS FOUND ON LF SECBTXL1 WITH LAST CHANGE DATE
     C** EQUAL TO RUN DATE FROM SDBANKPD
     C*
     C           RRPROI    IFEQ 0                          B1
     C           RRPROA    ANDEQ0
     C                     WRITEHEADER
     C                     WRITENULL1
     C                     ELSE                            X1
     C*
     C           LINE1     IFGT 55                         B*2
     C                     WRITEHEADER1
     C                     WRITEHEADER2
     C                     END                             E*2
     C                     END                             E1
     C*
     C                     ENDSR
     C*****************************************************************
     C/EJECT
      *****************************************************************
      *                                                               *
      *    #BE       - THIS SUBROUTINE PROCESSES A CHANGE OF BRANCH   *
      *                                                               *
      *    CALLS     - ZSFILE, *PSSR                                  *
      *                                                               *
      *    CALLED BY - #BA, #BC                                       *
      *                                                               *
      *****************************************************************
     C           #BE       BEGSR
      *
     C                     Z-ADDSFNUM     ZSNUM   60
     C                     Z-ADD0         LINE1
     C*
     C                     OPEN SE1980P1
     C*
     C                     CALL 'ZSFILE'
     C                     PARM @SEQ      SEQ     5
     C                     PARM TBBRCA    SENTY   3
     C                     PARM           SFILE
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      **  ERROR DURING ZSFILE PROCESSING, RETURN TO CALLING PROGRAM
      *
     C           ZSERR     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     Z-ADD3         DBASE
     C                     MOVE *BLANKS   DBKEY
     C                     MOVELSFILE     DBKEY            ***************
     C                     MOVEL'ZSFILE'  DBFILE           * DBERROR 003 *
     C                     MOVEL'SE1980'  DBPGM            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
      *** ACCESS BRANCHES FILE ON CHANGE OF BRANCH
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM TBBRCA    @DSNB   3
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
      *
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD4         DBASE
     C                     MOVELTBBRCA    DBKEY            ***************
     C                     MOVEL'SDBRCHL1'DBFILE           * DBERROR 004 *
     C                     OUT  LDA                        ***************
     C                     EXSR *PSSR
     C                     ELSE
     C                     MOVELTBBRCA    RRBRCA
     C                     MOVELA8BRNM    RRBRNM
     C                     END
      *
      *** Re-initialise work fields used within change of branch
      *
      *
     C                     MOVE TBBRCA    WWBRCH  3
      *
     C                     ENDSR
      *****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #ZA       - SET UP PRINTER DETAILS FOR DATABASE ERRORS     *
     C**                                                              *
     C**   CALLS     - None                                           *
     C**                                                              *
     C**   CALLED BY - #A #B #BB                                      *
     C**                                                              *
     C*****************************************************************
     C*
     C           #ZA       BEGSR
     C*
     C           @MODE     IFEQ 'I'
     C                     MOVE '1'       *IN53
     C                     WRITEHEADER
     C                     WRITEDBERROR
     C                     END
     C*
     C           @MODE     IFEQ 'A'
     C                     MOVE '1'       *IN52
     C                     WRITEHEADER
     C                     WRITEDBERROR
     C                     END
     C*
     C           @MODE     IFEQ 'F'
     C                     MOVE '1'       *IN53
     C                     WRITEHEADER
     C                     WRITEDBERROR
     C                     END
     C*
     C           @MODE     IFEQ 'U'
     C                     MOVE '1'       *IN51
     C                     WRITEHEADER
     C                     WRITEDBERROR
     C                     END
     C*
     C           @MODE     IFNE 'U'
     C           @MODE     ANDNE'A'
     C           @MODE     ANDNE'F'
     C           @MODE     ANDNE'I'
     C                     WRITEHEADER
     C                     WRITEDBERROR
     C                     END
     C*
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #ZB       - SETUP OUTPUT FIELDS FOR TAX RATES REPORT       *
     C**                                                              *
     C**   CALLS     - NONE                                           *
     C**                                                              *
     C**   CALLED BY - #BA #BC                                        *
     C**                                                              *
     C*****************************************************************
     C*
     C           #ZB       BEGSR
     C*
     C                     MOVE TBCTTA    RRCODE
     C                     MOVE TBTABU    RRBAND
     C***********          MOVE TBRASR    RRTAXR                          084256
     C***********          MOVE TBRAUS    RRTAXU                          084256
     C*                                                                   S01408
     C/COPY WNCPYSRC,SE1980CCP1                                           S01408
     C*                                                                   S01408
     C           S01496    IFEQ 'Y'                                       084256
     C                     Z-ADDTBRASR    RRTXR4                          084256
     C                     Z-ADDTBRAUS    RRTXU4                          084256
     C                     ELSE                                           084256
     C                     Z-ADDTBRASR    RRTXR2                          084256
     C                     Z-ADDTBRAUS    RRTXU2                          084256
     C                     END                                            084256
     C                     MOVE TBDLOR    RRDLOR
     C                     MOVE TBTADE    RRDESC
     C*
     C           TBCHTP    IFEQ 'I'
     C                     MOVE 'INSERT'  RRACTN
     C                     END
     C*
     C           TBCHTP    IFEQ 'A'
     C                     MOVE 'AMEND '  RRACTN
     C                     END
     C*
     C           TBCHTP    IFEQ 'D'
     C                     MOVE 'DELETE'  RRACTN
     C                     END
     C*
     C** SET UP LAST ACTIVITY DATE FOR OUTPUT
     C*
     C                     MOVE TBDLUP    TBDAY
     C                     MOVE TBMLUP    TBMON
     C                     MOVE TBYLUP    TBYER
     C                     MOVE UPDATE    RRDATE
     C*
     C** SET UP LAST ACTIVITY TIME FOR OUTPUT
     C*
     C                     MOVE TBTLUP    RRTIME
     C*
     C           TBCTTA    IFNE SXCTTA
     C*
     C** ACCESS COUNTRY CODES FILE VIA ACCESS OBJECT
     C*
     C                     CALL 'AOCTRYR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM TBCTTA    @CNCD   2
     C           SDCTRY    PARM SDCTRY    DSFDY
     C*
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD5         DBASE
     C                     MOVEL'SDCTRYPD'DBFILE           ***************
     C                     MOVELTBCTTA    DBKEY            * DBERROR 005 *
     C                     EXSR #ZA                        ***************
     C                     EXSR *PSSR
     C                     OUT  LDA
     C                     END
     C                     END
     C                     MOVE A4CNNM    RRCBNM
     C                     MOVE TBCTTA    SXCTTA  2
     C*
     C           @MODE     IFEQ 'I'
     C           @MODE     OREQ 'F'
     C                     MOVE '1'       *IN30
     C                     END
     C*
     C           @MODE     IFEQ 'U'
     C                     MOVE '1'       *IN50
     C                     END
     C*
     C           LINE1     IFGT 55
     C           WWFTT     OREQ '1'
     C                     WRITEHEADER1
     C                     WRITEHEADER2
     C                     MOVE '0'       WWFTT
     C                     END
     C*
     C                     WRITEDETAIL1
     C*
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #BB       - PROCESS FULL LIST RUN CONTROLS                 *
     C**                                                              *
     C**   CALLS     -                                                *
     C**                                                              *
     C**   CALLED BY - #B                                             *
     C**                                                              *
     C*****************************************************************
     C*
     C           #BB       BEGSR
     C*
     C** OUTPUT RUN CONTROLS REPORT
     C*
     C           @MODE     IFEQ 'I'
     C           @MODE     OREQ 'F'
     C                     MOVE '1'       *IN53
     C                     WRITEHEADER
     C                     END
     C*
     C                     WRITEDETAILA
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #BD       - PROCESS AUDIT LIST RUN CONTROLS                *
     C**                                                              *
     C**   CALLS     -                                                *
     C**                                                              *
     C**   CALLED BY - #B                                             *
     C**                                                              *
     C*****************************************************************
     C*
     C           #BD       BEGSR
     C*
     C** OUTPUT RUN CONTROLS REPORT
     C*
     C           @MODE     IFEQ 'U'
     C                     MOVE '1'       *IN51
     C                     END
     C           @MODE     IFEQ 'A'
     C                     MOVE '1'       *IN52
     C                     END
     C*
     C                     WRITEHEADER
     C                     WRITEDETAILB
     C*
     C                     ENDSR
     C*****************************************************************
     C**                                                              *
     C**   *PSSR     - Program error handling routine                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR
     C**
     C                     SETON                     U7U8LR
     C*
     C           @RUN      IFEQ *BLANKS
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C*
     C                     END
     C*
     C                     RETRN
     C*
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C****************************************************************
     C*
     C*  RCFAU -- SUBROUTINE TO REGISTER AU PRINTER FILE VIA RCF
     C*
     C*******************************************************************
     C*
     C           RCFAU     BEGSR                            ** RCFAU **
     C*
     C**      ENSURE AUDIT SPOOL FILE RECORDED BY RCF
     C*
     C                     Z-ADDSFNUMU    ZSNUMU  60
     C**
     C                     CALL 'ZSFILE'
     C                     PARM @SEQ      SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
     C*
     C           ZSERR     IFEQ 'Y'
     C*
     C**  ERROR DURING ZSFILE PROCESSING, RETURN TO CALLING PROGRAM
     C*
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     END
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
