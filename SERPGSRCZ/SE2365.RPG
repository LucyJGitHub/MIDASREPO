     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Mature Customer/User Depot Posns')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module
     F*                                                               *
     F*  SE2365  - MATURE USER/CUSTOMER DEPOT POSITIONS               *
     F*                                                               *
     F*  Function: For all Securities maturing today, those live      *
     F*   (COB)    user/customer depot positions that are affected    *
     F*            are accessed and flagged as matured.               *
     F*            A Run Control report is produced.                  *
     F*                                                               *
     F*  Called by: SEC0606I - Mature User/Customer Depot Positions   *
     F*             Frequency:                                        *
     F*             - Daily during COB                                *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 054257             Date 10JAN94               *
     F*                 052254             Date 10JAN94               *
     F*                 E29170             DATE 06NOV91               *
     F*                 S01117             DATE 07JUN91               *
     F*                 E16772             DATE 19/06/89              *
     F*                 E15323             DATE 20/09/88              *
     F*                 E14236             DATE 10/09/88              *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*  054257 - RECOMPILED as PREV CURRENT FACTOR amended from 9,8  *
     F*           to 10,9                                             *
     F*  052254 - RECOMPILED as CURRENT FACTOR amended from 9,8       *
     F*           to 10,9                                             *
     F*  E29170 - Report Control Facility changes                     *
     F*  S01117 - Multibranching                                      *
     F*  E16772 - Recompiled for the Dividend Payment Amount field    *   E16772
     F*           changes.                                            *   E16772
     F*  E15323 - CUSTOMER/DEPOT POSITIONS MATURED DAY EARLY          *   E15323
     F*  E14236 - NEW PROGRAM, TO CLOSE DOWN POSITIONS ON MATURITY    *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F*
     FMATAC   IF  E           K        DISK
     FTABSE   IF  E           K        DISK
     F* ICD 1     TABTB10F                          KIGNORE
     F            TABTB11F                          KIGNORE
     F            TABTB36F                          KIGNORE
     F            TABTB40F                          KIGNORE
     F            TABTG10F                          KIGNORE
     F            TABTG20F                          KIGNORE
     F            TABLETHF                          KIGNORE
     F*********** TABLETJF                          KIGNORE               S01117
     F            TABLETLF                          KIGNORE
     F            TABLETRF                          KIGNORE
     F            TABLETXF                          KIGNORE
     F            TABLET2F                          KIGNORE
     F            TABLET3F                          KIGNORE
     F            TABLET5F                          KIGNORE
     F            TABTE10F                          KIGNORE
     F            TABLETNF                          KIGNORE
     F            TABLETKF                          KIGNORE
     F            TABLETPF                          KIGNORE
     FCDEPS   UF  E           K        DISK
     FUDEPS   UF  E           K        DISK
     FSE2365AUO   E                    PRINTER
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   70 - EOF ON CDEPS/UDEPS                                     *
     F*   80 - EOF ON MATAC                                           *
     F*U7-U8 - DATABASE ERROR                                         *
     F*   U8 - CONTROL TOTALS OUT OF BALANCE                          *
     F*                                                               *
     F*****************************************************************
      /SPACE 3
     F*****************************************************************
     F*                                                               *
     F*  I N D E X   T O   S U B R O U T I N E S                      *
     F*                                                               *
     F*   FIRST  - ACCESSES ICD INFORMATION                           *
     F*   MATURE - MATURES CUSTOMER/USER DEPOT POSITIONS              *
     F*   AUDIT  - OUTPUTS AUDIT RECORD CDEPPA AND CONTROL REPORT     *
     F*   *PSSR  - Error handling                                     *   S01117
     F*   ZSYSTM - OBTAINS ICD1                                       *
     F*                                                               *
     F*****************************************************************
      /EJECT
     E                    CPY@    1   1 80
      /EJECT
     I*
     ILDA         DS                            256                       S01117
     I                                      134 183 DBLOT                 S01117
     I                                      134 141 DBFILE                S01117
     I                                      142 170 DBKEY                 S01117
     I                                      171 180 DBPGM                 S01117
     I                                      181 1830DBASE                 S01117
     I*LDA******* UDS                            256                      S01117
     I***********                           134 177 DBLOT                 S01117
     I***********                           134 138 DBFILE                S01117
     I***********                           139 167 DBKEY                 S01117
     I***********                           168 175 DBPGM                 S01117
     I***********                           176 1770DBASE                 S01117
     I*
     ICPYR@#      DS
     I*
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     I*
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      /EJECT
     C*================================================================
     C*  P R O G R A M     S T A R T
     C*================================================================
     C*
     C** DEFINE KEY FOR CDEPS ACCESS
     C*
     C           CDEPSK    KLIST
     C                     KFLD           SASN
     C*
     C** DEFINE KEY FOR UDEPS ACCESS
     C*
     C           UDEPSK    KLIST
     C                     KFLD           SASN
     C*
     C* ACCESS ICD INFORMATION
     C*
     C           *NAMVAR   DEFN           LDA                             S01117
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVE *BLANK    DBLOT
     C                     Z-ADD0         DBASE
     C                     MOVE 'SE2365  'DBPGM
     C                     OUT  LDA                                       S01117
     C*
     C                     EXSR FIRST
     C*
     C* CHECK FOR DATABASE ERRORS
     C*
     C           *IN99     IFEQ '1'
     C                     GOTO ENDLP
     C                     END
     C*
     C* READ AND PROCESS ALL MATURITY ACTIONS UNTIL EOF
     C*
     C           *IN80     DOUEQ'1'
     C*
     C                     READ MATAC                    80
     C*
     C           *IN80     IFEQ '1'
     C                     GOTO ENDLP
     C                     ELSE
     C                     ADD  1         IREC    50
     C                     END
     C*
     C*  MATURE ONLY IF ACTION IS FOR SECURITY DUE TODAY                  E15323
     C*                                                                   E15323
     C           AKRI      IFEQ 'Y'                                       E15323
     C*                                                                   E15323
     C                     EXSR MATURE
     C*                                                                   E15323
     C                     END                                            E15323
     C*
     C                     END
     C*
     C           ENDLP     TAG
     C*
     C                     EXSR AUDIT
     C*
     C                     SETON                     LR
     C*
     C*****************************************************************
      /EJECT
     C*****************************************************************
     C*                                                               *
     C* FIRST - SUBROUTINE TO ACCESS ICD INFORMATION                  *
     C*                                                               *
     C*****************************************************************
     C*
     C           FIRST     BEGSR                           ** FIRST **
     C*
     C* ACCESS ICD
     C*
     C                     EXSR ZSYSTM
     C           *IN99     IFEQ '1'
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVE 'TABLE'   DBFILE           *****************
     C                     MOVELZTABKY    DBKEY            ** DB ERROR 01 **
     C                     Z-ADD1         DBASE            *****************
     C                     OUT  LDA                                       S01117
     C                     EXSR *PSSR                                     S01117
     C                     END
     C*
     C                     ENDSR
     C*
     C*****************************************************************
      /EJECT
     C*****************************************************************
     C*                                                               *
     C* MATURE - SR TO READ THROUGH ALL LIVE DEPOT POSITIONS ON CDEPPD*
     C*          AND UDEPPD FOR THE SECURITY ACCESSED FROM MATAC      *
     C*          AND TO FLAG THEM AS MATURED WITH A RECORD ID OF 'H'  *
     C*                                                               *
     C*****************************************************************
     C*
     C           MATURE    BEGSR                           ** MATURE **
     C*
     C** SET POINTER TO FIRST CUSTOMER DEPOT FOR THIS MATURED SECURITY
     C*
     C           CDEPSK    SETLLCDEPS
     C*
     C           CDEPSK    READECDEPS                    70
     C*
     C           *IN70     DOWEQ'0'
     C*
     C** MAINTAIN COUNT OF RECORDS CHANGED
     C*
     C                     MOVEL'H'       RECI
     C                     ADD  1         CDEL    50
     C                     EXCPTUCDEPD
     C*
     C           CDEPSK    READECDEPS                    70
     C*
     C                     END
     C*
     C** SET POINTER TO FIRST USER DEPOT FOR THIS MATURED SECURITY
     C*
     C           UDEPSK    SETLLUDEPS
     C*
     C           UDEPSK    READEUDEPS                    70
     C*
     C           *IN70     DOWEQ'0'
     C*
     C** MAINTAIN COUNT OF RECORDS CHANGED
     C*
     C                     MOVEL'H'       RECI
     C                     ADD  1         UDEL    50
     C                     EXCPTUUDEPD
     C*
     C           UDEPSK    READEUDEPS                    70
     C*
     C                     END
     C*
     C                     ENDSR
     C*
     C*****************************************************************
      /EJECT
     C*****************************************************************
     C*                                                               *
     C*  AUDIT - SUBROUTINE TO OUTPUT CONTROL REPORT                  *
     C*                                                               *
     C*****************************************************************
     C*
     C           AUDIT     BEGSR                           ** AUDIT **
     C*
     C* OUTPUT CONTROL REPORT
     C*
     C                     WRITEHEADAU
     C           *INU7     IFEQ '1'
     C                     WRITEERROR
     C                     ELSE
     C*
     C**IF*RECORD*COUNT*IS*ZERO*OUTPUT*NONE****************************   E29170
     C***********                                                         E29170
     C***********IREC      IFEQ 0                                         E29170
     C***********          WRITENONE                                      E29170
     C***********          ELSE                                           E29170
     C                     WRITECONTROL
     C***********          END                                            E29170
     C*
     C                     END
     C***********                                                         E29170
     C***********          WRITETRAILAU                                   E29170
     C*
     C                     ENDSR
     C*
      *****************************************************************   S01117
      /EJECT                                                              S01117
      *****************************************************************   S01117
      *                                                               *   S01117
      *  *PSSR  - Error control subroutine                            *   S01117
      *                                                               *   S01117
      *****************************************************************   S01117
      *                                                                   S01117
     C           *PSSR     BEGSR                           ** *PSSR **    S01117
     C**                                                                  S01117
     C           @RUN      IFEQ *BLANKS                                   S01117
     C                     MOVE 'Y'       @RUN    1                       S01117
     C                     DUMP                                           S01117
     C                     END                                            S01117
     C**                                                                  S01117
     C                     ENDSR                                          S01117
      *                                                                   S01117
     C*****************************************************************
      /EJECT
     C/COPY ZSRSRC,ZSYSTMZ1
      /EJECT
     OCDEPPDF E                UCDEPD
     O                         RECI
     OUDEPPDF E                UUDEPD
     O                         RECI
      ***
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
