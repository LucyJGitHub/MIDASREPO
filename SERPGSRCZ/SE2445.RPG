     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Suppress fields on a/c keys')                 *
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module
     F*                                                               *
     F*  SE2445  - CHECK IF A/C KEY NEEDS FIELDS SUPPRESSED           *
     F*                                                               *
     F*  Function: If the ICD indicates that the Book code or the     *
     F*            Investment type need to be suppressed, update the  *
     F*            A/C key field on SEKEYD making the appropriate     *
     F*            part of the field blank.                           *
     F*                                                               *
     F*  Called by: SEC0610 - Generate A/C Entries - Accruals         *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      *  Last Amend No. AR821740           Date 29Aug11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CGL020             Date 12Dec02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAC005             Date 23Nov98               *
      *                 052254             Date 10Jan94               *
      *                 S01397             Date 10Sep92               *
     F*                 E34692             DATE 16JAN92               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  AR821740 - COB - No stamp tax account keys generated         *
      *             (Recompile)                                       *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*  CGL020 - Midas Compliance Watch - Additional A/C Postings    *
     F*           Information (Recompile)                             *
      *  CAC005 - Profit Centre Accounting - SE Enhancement           *
     F*  052254 - RECOMPILED as CURRENT FACTOR amended from 9,8       *
     F*           to 10,9                                             *
     F*  S01397 - Rel 10 Incorporation of BNZ SARs A46008 and A46013. *
     F*          - Recompiled due to changes to files.                *
     F*  E34692 - New program created to suppress Book codes and      *
     F*           Investment types from a/c key if required by their  *
     F*           appropriate file SDBOOKPD and INVTPD.               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F*
     FSEKEY   UF  E           K        DISK
     FSDBOOKPDIF  E           K        DISK
     FINVTPD  IF  E           K        DISK
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   50 - INVESTMENT TYPE/CCY ON ARRAY (NEED TO BE SUPPRESSED)   *
     F*   51 - BOOK CODE ON ARRAY (NEED TO BE SUPPRESSED)             *
     F*   88 - END OF FILE FOR SDBOOKPD                               *
     F*   89 - END OF FILE FOR INVTPD                                 *
     F*   90 - END OF FILE FOR SEKEY                                  *
     F*                                                               *
     F*****************************************************************
      /SPACE 3
     F*****************************************************************
      /EJECT
     E                    BKAR      200  2
     E                    INVA      500  6
     E                    CPY@    1   1 80
      /SPACE 3
     IWACKEY      DS
     I*
     I* D/S to break down account key on SEKEY To component parts
     I*
     I                                        1   3 WACITY
     I                                        4   4 WACEVC
     I                                        5   5 WACTRT
     I                                        6   6 WACPFI
     I                                        7   8 WACBKC
     I                                        9  10 WACTST
     I                                        9  10 WACPSB
     I                                       11  12 WACCCC
     I                                       13  13 WACMKT
     I                                       14  14 WACCON
     I                                       15  15 WACNEG
     I                                       19  20 WACAMC
     I*
     ICPYR@#      DS
     I*
     I*  Data structure for compilation - copyright insertion
     I*
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     C/EJECT
     C*---------------------------------------------------------------*
     C*       M A I N   P R O C E S S I N G
     C*---------------------------------------------------------------*
      *
      ** Check if CAC005 is installed                                     CAC005
      *                                                                   CAC005
     C                     CALL 'AOSARDR0'                                CAC005
     C                     PARM *BLANKS   PRTCD   7                       CAC005
     C                     PARM '*VERIFY' POPTN   7                       CAC005
     C                     PARM 'CAC005'  PSARD   6                       CAC005
      *                                                                   CAC005
     C           PRTCD     IFEQ *BLANKS                                   CAC005
     C                     MOVE 'Y'       CAC005  1                       CAC005
     C                     ELSE                                           CAC005
     C                     MOVE 'N'       CAC005                          CAC005
     C                     ENDIF                                          CAC005
     C*
     C*  Find and store the books to be suppressed on the a/c key
     C*
     C                     READ SDBOOKPD                 88
     C*
     C                     Z-ADD1         X       30
     C           *IN88     DOWEQ'0'
     C           BDIACK    IFEQ 'N'
     C                     MOVE BDBKCD    BKAR,X
     C                     ADD  1         X
     C                     END
     C*
     C                     READ SDBOOKPD                 88
     C                     END
     C*
     C*  Find and store the investment types to be suppressed on the
     C*  a/c keys
     C*
     C                     READ INVTPD                   89
     C*
     C                     Z-ADD1         Y       30
     C           *IN89     DOWEQ'0'
     C                     MOVE *BLANKS   WRK6
     C           INAK      IFEQ 'N'
     C                     MOVELCCYI      WRK6    6
     C                     MOVE INVT      WRK6
     C                     MOVE WRK6      INVA,Y
     C                     ADD  1         Y
     C                     END
     C*
     C                     READ INVTPD                   89
     C                     END
     C*
     C* Check each a/c key and blank out book code and/or investment
     C* type if necessary
     C*
     C                     READ SEKEY                    90
     C           *IN90     DOWEQ'0'
     C                     SETOF                     5051
     C                     MOVE ACKY      WACKEY
     C                     MOVE *BLANKS   WRKI    6
     C                     MOVE *BLANKS   WRKB    2
     C                     MOVELKCCY      WRKI
     C                     MOVE WACITY    WRKI
     C                     MOVE WACBKC    WRKB
     C           WRKI      LOKUPINVA                     50
     C           *IN50     IFEQ '1'
     C                     MOVE *BLANKS   WACITY
     C                     END
      *                                                                   CAC005
      ** If CAC005 is installed, retrieve user book                       CAC005
      ** Book code from the incoming account key is held as pseudo book   CAC005
      *                                                                   CAC005
     C           CAC005    IFEQ 'Y'                                       CAC005
     C           WRKB      ANDNE*BLANKS                                   CAC005
     C                     CALL 'AOBKPCR0'                                CAC005
     C                     PARM *BLANKS   PRTCD                           CAC005
     C                     PARM '*USER  ' POPTN                           CAC005
     C                     PARM           PBKCD   2                       CAC005
     C                     PARM           PPRFC   4                       CAC005
     C                     PARM WRKB      PPSBK   2                       CAC005
      *                                                                   CAC005
     C           PRTCD     IFEQ *BLANKS                                   CAC005
     C                     MOVELPBKCD     WRKB                            CAC005
     C                     MOVELPBKCD     WACBKC                          CAC005
     C                     ENDIF                                          CAC005
     C                     ENDIF                                          CAC005
     C*
     C           WRKB      LOKUPBKAR                     51
     C           *IN51     IFEQ '1'
     C                     MOVE *BLANKS   WACBKC
     C                     END
     C*
     C                     MOVE WACKEY    ACKY
     C                     UPDATSEKEYDF
     C                     READ SEKEY                    90
     C                     END
     C*
     C                     SETON                     LR
     C*
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
