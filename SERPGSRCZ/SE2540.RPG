     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Drop off customer positions')
/*OVR *: OVRDBF FILE(CHECK) TOFILE(QAFDMBR)                         : *                       095907
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module
     F*                                                               *
     F*  SE2540 - DROP OFF CUSTOMER POSITIONS                         *
     F*                                                               *
     F*  Function: To drop from the files for Cust Positions,         *
     F*   (COB)    - all records no longer needed by back valuation   *
     F*              processing or enquiries                          *
     F*            - all records for which the security has expired   *
     F*            - all records which represent the current position *
     F*              but which are zero and prior to back-valuation   *
     F*              date                                             *
     F*            For backvaluations, the positions as at b/valuation*
     F*            date is required, ie the latest position record    *
     F*            which pre-dates back valuation date;  earlier      *
     F*            positions are required as at end of month, back    *
     F*            to Retention Period for Settled Trades.  Earlier   *
     F*            positions are dropped; the exception is where the  *
     F*            current positons is zero and has been since before *
     F*            b/v date - in this case the position is dropped    *
     F*                                                               *
     F*  Called by: SEC1004 - Drop off Positions - Depot/Cust/Book    *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. 095907             Date 25Jan07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      *                 CSE071             Date 19Jul05               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 207179             Date 23Sep02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 157947             Date 07Jun00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*                 102734             DATE 02MAY96               *
     F*                 052254             DATE 10JAN94               *
     F*                 S01401             DATE 16JUN93               *
     F*                 S01397             DATE 10SEP92               *
     F*                 E29170             DATE 07NOV91               *
     F*                 S01117             DATE 07JUN91               *
     F*                 S01269             DATE 02AUG90               *
     F*                 E22042             DATE 18MAY90               *
     F*                 E20651             DATE 30JAN90               *
     F*                                                               *
     F*---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  095907 -  CPOSO REPLACED BY CPOSOM (FOOB)                    *
      *         -  MODIFICATIONS CALCULATIONS FOR OUTPUT TRAILER      *
      *         -  UPDATES OF RECI WITH '*' REPLACED BY DELETES       *
      *         -  NEW FILE CHECK ADDED TO CORRECT CAPRHA TRAILER     *
      *         -  DROP CAPRHD RECORD IF OLDER THAN RETENTION PERIOD  *
      *         -  IF INTEREST BEARING DONT REMOVE RECS IN CURR.COUP  *
      *            PERIOD                                             *
      *         -  IF RECORDS DELETED FROM CPOSND RETRIEVE ALL RECORDS*
      *            IN CAPRHD FOR THE BRANCH/SECURITY IF SECURITY      *
      *            DELETED                                            *
      *         - ONLY ACCESS SECTYD WHEN CHANGE OF SECURITY          *
      *         - COMPARE FOR TRAILER CPOSNA IS TOTALLY OBSOLETE;     *
      *           ADD NUMBER OF RETAINED RECORDS TO NORE.             *
      *         - USE WORK FIELD OF 6,0 TO OUPTPUT DETAILS FOR CPOSND *
      *           AND CAPRHD ON SE2540AU                              *
      *         - also include fix 243026.
      *  243026 -  In case of rupture on security:                    *
      *            SR/DLTCAP should be called only once               *
      *         -  6,0 is not enough on printer. Increase to 7,0      *
      *         - Work field too short                                *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  207179 - Retain first record past backvalue period if        *
      *           non-zero and delete all others.  Also do not delete *
      *           CAPRHD records unless all CPOSND records are dropped*
     F*  157947 - Customer record with zero position will not be drop *
     F*           from CPOSND. However, with all the records being    *
     F*           dropped from CDEPPD which enable customer to be     *
     F*           deleted from CLINTSE caused DB error in SE1480.     *
     F*           Applied fix 100249.
     F*  102734 - Retain latest position prior to back-value period   *
     F*  052254 - RECOMPILED as CURRENT FACTOR amended from 9,8       *
     F*           to 10,9                                             *
     F*  S01401 - MT5xx SWIFT Messages Feature:                       *
     F*          - Recompiled due to changes to files.                *
     F*  S01397 - Rel 10 Incorporation of BNZ SARs A46008 and A46013. *
     F*          - Recompiled due to changes to files.                *
     F*  E29170  -  Report Control Facility changes                   *
     F*  S01117  -  Multi-branching                                   *
     F*  S01269  -  TRADE AUTHORISATION: RECOMPILED OVER CHANGED      *
     F*             FILE TABSE.                                       *
     F*  E22042  -  WRONG RECORDS WERE BEING FLAGGED TO BE DROPPED.   *
     F*  E20651  -  DB ERRORS USED IN MORE THAN ONE PLACE.            *
     F*             NEW ERROR NUMBERS AS FOLLOWS:                     *
     F*             DB ERROR 01 -> 05                                 *
     F*             DB ERROR 01 -> 06                                 *
     F*                                                               *
     F*****************************************************************
     F*
     F**CPOSO   UP  E           K        DISK                                                 095907
     FCPOSOM  UP  E           K        DISK                                                   095907
     FSECTY   IF  E           K        DISK
     FCPOSNA  UF  E                    DISK
     FCAPRBS  UF  E           K        DISK
     FCAPRHA  UF  E                    DISK
     FCHECK   IF  E                    DISK                                                   095907
     FTABSE   IF  E           K        DISK
     F            TABLETAF                          KIGNORE
     F            TABTB11F                          KIGNORE
     F            TABTE10F                          KIGNORE
     F            TABTE20F                          KIGNORE
     F            TABTG10F                          KIGNORE
     F            TABTG20F                          KIGNORE
     F            TABLETHF                          KIGNORE
     F*********** TABLETJF                          KIGNORE               S01117
     F            TABLETKF                          KIGNORE
     F            TABLETLF                          KIGNORE
     F            TABLETNF                          KIGNORE
     F            TABLETPF                          KIGNORE
     F            TABLETRF                          KIGNORE
     F            TABLETXF                          KIGNORE
     F            TABLET2F                          KIGNORE
     F            TABLET3F                          KIGNORE
     F            TABLET5F                          KIGNORE
     F            TABTB40F                          KIGNORE
     FSE2540AUO   E                    PRINTER
     F/COPY WNCPYSRC,SE2540F001
     F*
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   31       Security not found for this record                 *
     F*   32       Control of live record count - change of branch    *
     F*   33       General work indicator                             *
     F*   34       Record found equal to Key1                         *
     F*   35       End of match with Key1 on Caprbs file              *
     F*   36       End of file on Caprha file                         *
     F*   69       Used to Control first cycle processing             *
     F*   73       Number of records on audit file different from     *
     F*            number of records read                             *
     F*90-99    Used in standard sub-routines                         *
     F*   U7    Database error                                        *
     F*   U8    Database / Control error                              *
     F*   L3 Change of Branch                                         *
     F*   L2 Change of Security                                       *
     F*   L1 Change of Counterparty                                   *
     F*****************************************************************
      /SPACE 3
     F*****************************************************************
     F*                                                               *
     F*  I N D E X   T O   S U B R O U T I N E S                      *
     F*                                                               *
     F*   DLTREC - Flag records as deleted and count total            *
     F*   *PSSR  - Error handling                                     *   S01117
     F*   ZDATE2 - DAYNO - DATE conversion                            *
     F*   ZICDSE - Access ICD record for Securities Trading           *
     F*   ZSEDIT - Insert a decimal point and sign into a numeric fiel*
     F*            and blank out leading zeros                        *
     F*   ZSYSTM - Access ICD record                                  *
     F*                                                               *
     F*****************************************************************
      /EJECT
     E/COPY ZSRSRC,ZDATE2Z1
     E                    CPY@    1   1 80                                S01117
     E*
     E/COPY ZSRSRC,ZSEDITZ1
     E*
      /EJECT
     E/COPY WNCPYSRC,SE2540E001
     ICPOSNDF
     I***********                                   CSBR  L3              S01117
     I                                              CSBA  L3              S01117
     I                                              CSSC  L2
     I                                              CSCN  L1
     I              RECI                            RECIX
     ICAPRHDF
     I              RECI                            RECIY
     I              CSCN                            CSCNY
     I              CSDA                            CSDAY
     I*LDA******* UDS                            256                      S01117
     I***********                           134 177 DBLOT                 S01117
     I***********                           134 138 DBFILE                S01117
     I***********                           139 167 DBKEY                 S01117
     I***********                           168 175 DBPGM                 S01117
     I***********                           176 177 DBASE                 S01117
     ILDA         DS                            256                       S01117
     I                                      134 183 DBLOT                 S01117
     I                                      134 141 DBFILE                S01117
     I                                      142 170 DBKEY                 S01117
     I                                      171 180 DBPGM                 S01117
     I                                      181 1830DBASE                 S01117
     ICPYR@#      DS                                                      S01117
     I*                                                                   S01117
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION            S01117
     I*                                                                   S01117
     I                                        1  80 CPY@                  S01117
     I                                        1  20 CPY@##                S01117
     I/COPY WNCPYSRC,SE2540I001
     I/COPY ZSRSRC,ZSEDITZ2
      /EJECT
     C*
     C**  KEY LIST TO ACCESS CAPRBS FILE
     C*
     C           KEY1      KLIST
     C***********          KFLD           CSBR                            S01117
     C                     KFLD           CSBA                            S01117
     C                     KFLD           CSSC
     C                     KFLD           CSCN                                                207179
     C*
     C           *LIKE     DEFN CSSC      SCSSC                                               095907
     C           *LIKE     DEFN CSSC      TLCSSC                                              243026
     C*================================================================
     C*  P R O G R A M     S T A R T                                  *
     C*================================================================
     C*
     C           *IN69     IFEQ '0'
     C*
     C* Prepare LDA
     C*
     C           *NAMVAR   DEFN           LDA                             S01117
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVE *BLANKS   DBLOT
     C                     MOVE 'SE2540  'DBPGM
     C                     OUT  LDA                                       S01117
     C*
     C* Get Installation Control Data record 1
     C*
     C                     EXSR ZSYSTM
     C*
     C* If error in ZSYSTM, abend program
     C*
     C           *IN99     IFEQ '1'                        ***************
     C           *LOCK     IN   LDA                        ***************S01117
     C***********          MOVE '01'      DBASE            **DB*ERROR*01**E20651
     C***********          MOVE '05'      DBASE            **DB 5**E20651 S01117
     C                     Z-ADD5         DBASE            **DB ERROR 05**S01117
     C                     MOVEL'TABLE'   DBFILE           ***************
     C                     MOVELZTABKY    DBKEY
     C                     OUT  LDA                                       S01117
     C                     EXSR *PSSR                                     S01117
     C                     WRITEHEADAU
     C                     WRITEERROR
     C***********          WRITETRAILAU                                   E29170
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     END
     C*
     C* Get Installation Control Data record for Securities trading
     C*
     C                     EXSR ZICDSE
     C*
     C* If error in ZICDSE, abend program
     C*
     C           *IN99     IFEQ '1'                        ***************
     C           *LOCK     IN   LDA                        ***************S01117
     C***********          MOVE '02'      DBASE            **DB ERROR 02**S01117
     C                     Z-ADD2         DBASE            **DB ERROR 02**S01117
     C                     MOVEL'TABLE'   DBFILE           ***************
     C                     MOVELZTABKY    DBKEY
     C                     OUT  LDA                                       S01117
     C                     EXSR *PSSR                                     S01117
     C                     WRITEHEADAU
     C                     WRITEERROR
     C***********          WRITETRAILAU                                   E29170
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     END
     C*
     C* Calculate Back valuation date
     C*
     C           RUND      SUB  BKVP      BVDATE  50
     C*
     C**Calculate*Retention date                                                              207179
     C**********                                                                              207179
     C********** 31        MULT RTST      WK3N    30                                          207179
     C********** RUND      SUB  WK3N      RTNTDT  50                                          207179
     C           31        MULT RTST      WK3N    50                                          243026
     C           RUND      SUB  WK3N      RTNTDT  50                                          243026
     C*
     C                     SETON                     69
     C                     READ CHECK                    33                                   095907
     C                     Z-ADDMBNRCD    CNORE   50                                          095907
     C                     Z-ADDMBNRCD    CNORE7  70                                          243026
     C/COPY WNCPYSRC,SE2540C001
     C                     END
     C*
      /EJECT
     C*
     C* Change of Branch live record count control
     C*
     C***L3******          SETOF                     32                   E22042
     C   L1                SETOF                     32                   E22042
     C   L1                Z-ADD*ZEROS    ZMTHX                           102734
     C*
     C* Ignore this record if already deleted
     C*
     C           RECIX     IFNE '*'
     C*
     C* Count movements records read
     C*
     C***********          ADD  1         CPRRD                                               095907
     C                     ADD  1         CPRRD   50                                          095907
     C                     ADD  1         CPRRD7                                              243026
     C                     ADD  1         SNORE7                                              243026
     C*
     C* Access Security
     C*
     C           CSSC      IFNE SCSSC                                                         095907
     C                     MOVE CSSC      SCSSC                                               095907
     C           CSSC      CHAINSECTYDF              31
     C  N31      RECI      COMP '*'                      31
     C                     END                                                                095907
     C           *IN31     IFEQ '1'
     C*
      /SPACE 3
     C*
     C* Detail processing
     C*
      /SPACE 2
     C*
     C* If not found, flag position and count as deleted
     C*
     C                     EXSR DLTREC
     C*
     C* Call this routine only once to avoid                                                  243026
     C* unnecessary access                                                                    243026
     C           CSSC      IFNE TLCSSC                                                        243026
     C                     EXSR DLTCAP
     C                     MOVE CSSC      TLCSSC                                              243026
     C                     ENDIF                                                              243026
     C*
     C* Else if security found
     C*
     C                     ELSE
     C*
     C* Get month to determine if first record of new month - retain
     C*
     C                     Z-ADDCSDA      ZDAYNO
     C                     EXSR ZDATE2
     C*
     C* If cposnd date is greater than backvalue date - retain
     C*
     C           CSBA      IFNE WCSBA                                                         207179
     C           CSSC      ORNE WCSSC                                                         207179
     C           CSCN      ORNE WCSCN                                                         207179
     C                     MOVE CSBA      WCSBA   3                                           207179
     C                     MOVE CSSC      WCSSC  10                                           207179
     C**********           MOVE CSCN      WCSCN   60                                   207179 CSD027
     C                     MOVE CSCN      WCSCN   6                                           CSD027
     C                     MOVE *BLANKS   WCAP                                                207179
     C                     ENDIF                                                              207179
      *                                                                                       207179
     C***********CSDA      IFGT BVDATE                                                        095907
     C           CSDA      IFGT RTNTDT                                                        095907
     C           CD01      ORNE *ZERO                                                         095907
     C           CSDA      ANDGELCPN                                                          095907
     C                     MOVE 'Y'       WCAP    1                                           207179
     C***********          ADD  1         STOTO                                               095907
     C                     ADD  1         STOTO   50                                          095907
     C                     ADD  1         STOTO7                                              243026
     C                     Z-ADDZMTH      ZMTHX   20
     C*
     C* Else if cposnd date is less or equal to backvalue date
     C*
     C                     ELSE
     C*
     C* If first record for this branch/security/customer - retain
     C**  This should only be done for customer with position.            157947
     C*                                                                   157947
     C           *IN32     IFEQ '0'                                       157947
     C                     MOVE '0'       *IN44                           157947
     C           CSNT      IFEQ 0                                         157947
     C           CSNV      ANDEQ0                                         157947
     C           CSNS      ANDEQ0                                         157947
     C           CSEX      ANDEQ0                                         157947
     C                     MOVE '1'       *IN32                           157947
     C                     MOVE '1'       *IN44                           157947
     C                     END                                            157947
     C                     END                                            157947
     C*
     C           *IN32     IFEQ '0'
     C                     ADD  1         STOTO
     C                     ADD  1         STOTO7                                              243026
     C                     SETON                     32
     C**********           Z-ADDZMTH      ZMTHX                                               207179
     C**********                                                                              207179
     C**Else*if*cposnd does not meet earlier conditions                                       207179
     C**********                                                                              207179
     C**********           ELSE                                                               207179
     C**********                                                                       157947 207179
     C***If*the*first record show zero customer position and also                      157947 207179
     C***passed*backvalue period then it can be dropped.                               157947 207179
     C**********                                                                       157947 207179
     C********** *IN44     IFEQ '1'                                                    157947 207179
     C**********           Z-ADDZMTH      ZMTHX                                        157947 207179
     C**********           END                                                         157947 207179
     C**********                                                                              207179
     C**If*first record for a new month                                                       207179
     C**********                                                                              207179
     C********** ZMTH      IFNE ZMTHX                                                         207179
     C**********                                                                              207179
     C**And*date is greater than retention date                                               207179
     C**********                                                                              207179
     C********** CSDA      ANDGTRTNTDT                                                        207179
     C**********           ADD  1         STOTO                                               207179
     C**********           Z-ADDZMTH      ZMTHX                                               207179
     C*
     C* Else if record not required
     C*
     C                     ELSE
     C/COPY WNCPYSRC,SE2540C007
     C*
     C* Flag position settlement records and count as deleted
     C*
     C                     EXSR DLTREC
     C*                                                                                       243026
     C* Call this routine only once to avoid                                                  243026
     C* unnecessary access                                                                    243026
     C           CSSC      IFNE TLCSSC                                                        243026
     C                     EXSR DLTCAP                                                        095907
     C                     MOVE CSSC      TLCSSC                                              243026
     C                     ENDIF                                                              243026
     C*                                                                   157947
     C** If all the record in CPOSND is being deleted for a customer,     157947
     C** then need to do the same for CAPRHD.                             157947
     C*                                                                   157947
     C           *IN44     IFEQ '1'                                       157947
     C           WCAP      ANDEQ*BLANKS                                                       207179
     C                     EXSR DLTCAP                                    157947
     C                     MOVE '0'       *IN44                           157947
     C                     END                                            157947
     C*
     C/COPY WNCPYSRC,SE2540C008
     C**********           END                                                                207179
     C                     END
     C                     END
     C                     END
     C*
     C                     END
     C/COPY WNCPYSRC,SE2540C002
     C*
      /SPACE 3
     C*
     C* LR time
     C*
     CLR                   DO
     C/COPY WNCPYSRC,SE2540C003
     C*
     C* If no records read, perform 1st time calcs for audit report
     C*
     CL0         CPRRD     IFEQ *ZEROS
     C*
     C* Prepare LDA
     C*
     CL0                   MOVE *BLANKS   DBLOT
     CL0                   MOVE 'SE2540  'DBPGM
     C*
     C* Get Installation Control Data record 1
     C*
     CL0                   EXSR ZSYSTM
     C*
     C* If error in ZSYSTM, abend program
     C*
     CL0         *IN99     IFEQ '1'                        ***************
     CL0         *LOCK     IN   LDA                                       S01117
     C**L0*******          MOVE '01'      DBASE            **DB*ERROR*01**E20651
     C*L0********          MOVE '06'      DBASE            **DB 6**E20651 S01117
     CL0                   Z-ADD6         DBASE            **DB ERROR 06**S01117
     CL0                   MOVEL'TABLE'   DBFILE           ***************
     CL0                   MOVELZTABKY    DBKEY
     CL0                   OUT  LDA                                       S01117
     CL0                   EXSR *PSSR                                     S01117
     CL0                   WRITEHEADAU
     CL0                   WRITEERROR
     C*0*********          WRITETRAILAU                                   E29170
     CL0                   SETON                     U7U8LR
     CL0                   RETRN
     CL0                   END
     C*
     CL0                   END
     C*
     C* Fetch Depot movements audit file
     C*
     CL0         1         CHAINCPOSNAF              33
     CL0         *IN33     IFEQ '1'                        ***************
     CL0         *LOCK     IN   LDA                        ***************S01117
     C*L0********          MOVEL'03'      DBASE            **DB ERROR 03**S01117
     CL0                   Z-ADD3         DBASE            **DB ERROR 03**S01117
     CL0                   MOVEL'CPOSNA'  DBFILE           ***************
     CL0                   MOVEL'1'       DBKEY
     CL0                   OUT  LDA                                       S01117
     CL0                   EXSR *PSSR                                     S01117
     CL0                   WRITEHEADAU
     CL0                   WRITEERROR
     C*0*********          WRITETRAILAU                                   E29170
     CL0                   SETON                     U7U8
     CL0                   RETRN
     CL0                   ELSE
     C*
     C* Compare control total to number of records read
     C*
     C***L0      NORE      COMP CPRRD                U8U8                                     095907
     C***L0 U8             SETON                     73                                       095907
     C*
     C* Output fields - print only
     C*
     C*0*********          Z-ADDNORE      SNORE                                               095907
     CL0                   Z-ADDNORE      SNORE   50                                          095907
     C*
     CL0                   READ CAPRHA                   36
     C*
     CL0         *IN36     IFEQ '1'
     CL0         *LOCK     IN   LDA                        ***************S01117
     C*L0********          MOVEL'04'      DBASE            **DB ERROR 04**S01117
     CL0                   Z-ADD4         DBASE            **DB ERROR 04**S01117
     CL0                   MOVEL'CAPRHA'  DBFILE           ***************
     CL0                   MOVEL'1'       DBKEY
     CL0                   OUT  LDA                                       S01117
     CL0                   EXSR *PSSR                                     S01117
     CL0                   WRITEHEADAU
     CL0                   WRITEERROR
     C*0*********          WRITETRAILAU                                   E29170
     CL0                   SETON                     U7U8
     CL0                   RETRN
     CL0                   ELSE
     C*
     C* Output fields - print only
     C*
     C***L0                Z-ADDNORE      CNORE                                               095907
     C*0*********CNORE     SUB  CAPCNT    NNORE                                               095907
     CL0         CNORE     SUB  CAPCNT    NNORE   50                                          095907
     CL0         CNORE7    SUB  CAPCN7    NNORE7                                              243026
     C*
     C* Print audit report
     C*
     CL0                   WRITEHEADAU
     CL0                   WRITETOTALS
     C*0*********          WRITETRAILAU                                   E29170
     C*
     C* If totals same, update audit record
     C*
     CL0         *INU8     IFEQ '0'
     CL0                   Z-ADDSTOTO     NORE
     CL0                   UPDATCPOSNAF
     C*
     CL0                   Z-ADDNNORE     NORE
     CL0                   UPDATCAPRHAF
     CL0                   END
     C*
     CL0                   END
     CL0                   END
     C*
     CLR                   END
     C*
     C*****************************************************************
      /EJECT
     C*****************************************************************
     C**                                                              *
     C**   DLTREC SR. TO FLAG RECORDS AND COUNT AS DELETED            *
     C**                                                              *
     C*****************************************************************
     C**
     CSR         DLTREC    BEGSR                           *** DLTREC  ***
     C*
     C* Flag Position record and count as deleted
     C/COPY WNCPYSRC,SE2540C004
     C*
     C****                 MOVE '*'       RECIX                                               095907
     C***                  EXCPTDELPSO                                                        095907
     C                     DELETCPOSNDF                                                       095907
     C***********          ADD  1         CPDEL                                               095907
     C                     ADD  1         CPDEL   50                                          095907
     C                     ADD  1         CPDEL7                                              243026
     C*
     CSR                   ENDSR
     C*
     C*****************************************************************
      /SPACE 3
     C*****************************************************************
     C**                                                              *
     C**   DLTCAP SR. TO FLAG RECORDS AND COUNT AS DELETED FOR CAPRHD *
     C**                                                              *
     C*****************************************************************
     C**
     C           DLTCAP    BEGSR                           *** DLTCAP  ***
     C*
     C* IF RECORD HAS BEEN DELETED FROM CPOSND FILE THEN RETRIEVE
     C* ALL RECORDS ON CAPRHD FOR THE BRANCH/SECURITY
     C*
     C           *IN31     IFEQ '0'                                                           095907
     C           KEY1      SETLLCAPRHDF                  34
     C*
     C           *IN34     IFEQ '1'
     C           *IN35     DOUEQ'1'
     C*
     C           KEY1      READECAPRHDF                  35
     C*
     C* IF NOT END OF MATCH UPDATE THE RECORD AS DELETED AND INCREMENT
     C* COUNT
     C*
     C           *IN35     IFEQ '0'
     C           CSDAY     ANDLTRTNTDT                                                        095907
     C*
     C***********          MOVE '*'       RECIY                                               095907
     C***********          ADD  1         CAPCNT                                              095907
     C                     ADD  1         CAPCNT  50                                          095907
     C                     ADD  1         CAPCN7                                              243026
     C/COPY WNCPYSRC,SE2540C005
     C                     DELETCAPRHDF                                                       095907
     C***********          UPDATCAPRHDF                                                       095907
     C*
     C                     END
     C                     END
     C                     END
     C*
     C                     ELSE                                                               095907
     C* IF RECORD HAS BEEN DELETED FROM CPOSND FILE THEN RETRIEVE                             095907
     C* ALL RECORDS ON CAPRHD FOR THE BRANCH/SECURITY IF SEC. NOT LIVE                        095907
     C*                                                                                       095907
     C           KEY1      SETLLCAPRHDF                  34                                   095907
     C*                                                                                       095907
     C           *IN34     IFEQ '1'                                                           095907
     C           *IN35     DOUEQ'1'                                                           095907
     C*                                                                                       095907
     C           KEY1      READECAPRHDF                  35                                   095907
     C*                                                                                       095907
     C* IF NOT END OF MATCH UPDATE THE RECORD AS DELETED AND INCREMENT                        095907
     C* COUNT                                                                                 095907
     C*                                                                                       095907
     C           *IN35     IFEQ '0'                                                           095907
     C*                                                                                       095907
     C                     ADD  1         CAPCNT                                              095907
     C                     ADD  1         CAPCN7                                              243026
     C                     DELETCAPRHDF                                                       095907
     C*                                                                                       095907
     C                     END                                                                095907
     C                     END                                                                095907
     C                     END                                                                095907
     C                     END                                                                095907
     C*                                                                                       095907
     C                     ENDSR
     C*
      *****************************************************************   S01117
      /EJECT                                                              S01117
      *****************************************************************   S01117
      *                                                               *   S01117
      *  *PSSR  - Error control subroutine                            *   S01117
      *                                                               *   S01117
      *****************************************************************   S01117
      *                                                                   S01117
     C           *PSSR     BEGSR                           ** *PSSR **    S01117
     C**                                                                  S01117
     C           @RUN      IFEQ *BLANKS                                   S01117
     C                     MOVE 'Y'       @RUN    1                       S01117
     C                     DUMP                                           S01117
     C                     END                                            S01117
     C**                                                                  S01117
     C                     ENDSR                                          S01117
      *                                                                   S01117
     C*****************************************************************
     C/COPY WNCPYSRC,SE2540C006
      /EJECT
     C/COPY ZSRSRC,ZDATE2Z4
      /EJECT
     C/COPY ZSRSRC,ZICDSEZ1
      /EJECT
     C/COPY ZSRSRC,ZSEDITZ3
      /EJECT
     C/COPY ZSRSRC,ZSYSTMZ1
     O/EJECT
     O***CPOSNDF E                DELPSO                                                      095907
     O***                      RECIX                                                          095907
      /EJECT
      ***
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  CPY@
(c) Finastra International Limited 2001
