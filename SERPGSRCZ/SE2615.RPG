     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Extract Client Positions')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module
     F*                                                               *
     F*  SE2615  - EXTRACT CLIENT POSITIONS FOR RECONCILIATION        *
     F*                                                               *
     F*  Function: To extract customer positions and customer depot   *
     F*   (COB)    positions to extract file for reconciliation.      *
     F*                                                               *
     F*  Called by: SEC0606J - Run Reconciliation Reports             *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CSE071             Date 19Jul05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 052254             Date 10Jan94               *
      *                 S01401             Date 16Jun93               *
     F*                 S01397             DATE 10SEP92               *
     F*                 E32995             DATE 06NOV91               *
     F*                 S01117             DATE 02NOV90               *
     F*                 E18554             DATE 06JUN89               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
     F*  052254 - RECOMPILED as CURRENT FACTOR amended from 9,8       *
     F*           to 10,9                                             *
     F*  S01401 - MT5xx SWIFT Messages Feature:                       *
     F*          - Recompiled due to changes to files.                *
     F*  S01397 - Rel 10 Incorporation of BNZ SARs A46008 and A46013. *
     F*          - Recompiled due to changes to files.                *
     F*  E32995 - For the grey market trades, the depots are blank,   *
     F*           so no record should be written else COB falls over. *
     F*  S01117 - Multi-branching                                     *
     F*  E18554 - Creation Parameters Added.                          *   E18554
     F*                                                               *   E18554
     F*****************************************************************   E18554
     F*  C R E A T I O N     P A R A M E T E R S                      *   E18554
     F*                                                               *   E18554
     F*                                                               *   E18554
     F*****************************************************************   E18554
     F*
     FCDEPH   IF  E                    DISK
     FCPOSH   IF  E                    DISK
     FSECTY   IF  E           K        DISK                               E32995
     FREC02D  O   E                    DISK
     F*
      /EJECT
     E                    CPY@    1   1 80                                S01117
     ICPOSNDF
     I***********   CSBR                            CDBR                  S01117
     I              CSBA                            CDPA                  S01117
     I              CSSC                            CDSN
     I              CSCN                            CDCN
     I              CSDA                            DDTE
     I              CSEX                            CECN
     I              CSNT                            CDNT
     I              CSNV                            CDNV
     I              CSNS                            CDNS
     ISECTYDF                                                             E32995
     I              RECI                            RECIS                 E32995
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION            S01117
     ICPYR@#      DS                                                      S01117
     I                                        1  80 CPY@                  S01117
     I                                        1  20 CPY@##                S01117
     I*                                                                   S01117
     ILDA         DS                            256                       E32995
     I**  Data Structure for Local Data Area                              E32995
     I*                                                                   E32995
     I                                      134 183 DBLOT                 E32995
     I                                      134 141 DBFILE                E32995
     I                                      142 170 DBKEY                 E32995
     I                                      171 180 DBPGM                 E32995
     I                                      181 1830DBASE                 E32995
     I*                                                                   E32995
      /EJECT
     C*================================================================
     C*  P R O G R A M     S T A R T
     C*================================================================
     C*                                                                   E32995
     C**  Prepare LDA                                                     E32995
     C*                                                                   E32995
     C           *NAMVAR   DEFN           LDA                             E32995
     C           *LOCK     IN   LDA                                       E32995
     C                     MOVEL*BLANK    DBLOT                           E32995
     C                     MOVEL'SE2615'  DBPGM                           E32995
     C                     OUT  LDA                                       E32995
     C*
     C* Set file ID
     C*
     C**NU2******          MOVEL'1'       FLNO                            S01117
     C***U2******          MOVEL'2'       FLNO                            S01117
     C  NU2                MOVEL'1'       FLNI                            S01117
     C   U2                MOVEL'2'       FLNI                            S01117
     C                     MOVEL'ZC'      SOUR
     C*
     C* Output Customer positions
     C*
     C                     READ CPOSH                    10*
     C           *IN10     DOWEQ'0'
     C                     MOVEL*BLANK    CDPT
     C                     MOVEL*BLANK    CDMK
      *                                                                   E32995
     C           CDSN      CHAINSECTY                20                   E32995
     C           *IN20     IFEQ '1'                                       E32995
     C           *LOCK     IN   LDA                                       E32995
     C                     MOVEL'SECTY'   DBFILE           ***************E32995
     C                     MOVELCDSN      DBKEY            ** DB ERROR 01 E32995
     C                     Z-ADD1         DBASE            ***************E32995
     C                     OUT  LDA                                       E32995
     C                     SETON                     U7U8LR               E32995
     C                     EXSR *PSSR                                     E32995
     C                     RETRN                                          E32995
     C                     END                                            E32995
      *                                                                   E32995
     C           MKTI      IFNE 'G'                                       E32995
     C                     WRITEREC02DF
     C                     END                                            E32995
      *                                                                   E32995
     C                     READ CPOSH                    10*
     C                     END
     C*
     C* Output Client Depot positions
     C*
     C                     MOVEL'CD'      SOUR
     C                     READ CDEPH                    10*
     C           *IN10     DOWEQ'0'
      *                                                                   E32995
     C           CDSN      CHAINSECTY                20                   E32995
     C           *IN20     IFEQ '1'                                       E32995
     C           *LOCK     IN   LDA                                       E32995
     C                     MOVEL'SECTY'   DBFILE           ***************E32995
     C                     MOVELCDSN      DBKEY            ** DB ERROR 01 E32995
     C                     Z-ADD2         DBASE            ***************E32995
     C                     OUT  LDA                                       E32995
     C                     SETON                     U7U8LR               E32995
     C                     EXSR *PSSR                                     E32995
     C                     RETRN                                          E32995
     C                     END                                            E32995
      *                                                                   E32995
     C           MKTI      IFNE 'G'                                       E32995
     C                     WRITEREC02DF
     C                     END                                            E32995
      *                                                                   E32995
     C                     READ CDEPH                    10*
     C                     END
     C*
     C                     SETON                     LR
      *****************************************************************   S01117
      /EJECT                                                              S01117
      *****************************************************************   S01117
      *                                                               *   S01117
      *  *PSSR  - Error control subroutine                            *   S01117
      *                                                               *   S01117
      *****************************************************************   S01117
      *                                                                   S01117
     C           *PSSR     BEGSR                           ** *PSSR **    S01117
     C**                                                                  S01117
     C           @RUN      IFEQ *BLANKS                                   S01117
     C                     MOVE 'Y'       @RUN    1                       S01117
     C                     DUMP                                           S01117
     C                     END                                            S01117
     C**                                                                  S01117
     C                     ENDSR                                          S01117
      *                                                                   S01117
     C*****************************************************************   S01117
      /EJECT                                                              S01117
**  CPY@                                                                  S01117
(c) Finastra International Limited 2001
