     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE EXTEL Link Report')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module
     F*                                                               *
     F*  SE3113 - EXTEL LINK REPORT PROGRAM                           *
     F*                                                               *
     F*  Function: Report details of the usage of the link incl:      *
     F*            - Time the process was initiated and the user      *
     F*              responsible                                      *
     F*            - Repeated for each transmission attempted;        *
     F*              Time transmission started                        *
     F*              Time transmission ended                          *
     F*              No of blocks received                            *
     F*              Reason for halt of transmission                  *
     F*            - Time process terminated                          *
     F*            - Reason for termination of process                *
     F*                                                               *
     F*            Frequency:                                         *
     F*             The report will be produced on termination of the *
     F*             Extel receipt process; causes may be as follows:  *
     F*             - Successful completion of a transmission         *
     F*             - Unsuccessful transmission                       *
     F*             - Transmission cancelled by operator              *
     F*                                                               *
     F*  Called by: SEC3113 - Extel Link Report                       *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*  LAST AMEND NO. 052254             DATE 11JAN94               *
     F*  PREV AMEND NO. E29170             DATE 22NOV91               *
     F*                 S01117             DATE 11JUN91               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
     F*  052254 - RECOMPILED as CURRENT FACTOR amended from 9,8       *
     F*           to 10,9                                             *
     F*  E29170 - Report Control Facility changes                     *
     F*  S01117 - Multi-branching                                     *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     FEXLNKD  IF  E                    DISK
     FTABTB10 IF  E                    DISK
     F***SE3113P1O***E                    PRINTER                         E29170
     FSE3113AUO   E                    PRINTER                            E29170
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*  FUNCTION OF INDICATORS                                       *
     F*                                                               *
     F*   55 - To check for Database Errors                           *
     F*   56 - To Display any Extel Link Details                      *
     F*   LR - Program Termination                                    *
     F*                                                               *
     F*****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
     E                    MTH    12  12  3
     E*
     E*  Array containing Month Short Names for Date Reformat
     E*
     I/EJECT
     I*
     IEXSTTD    E DSEXSTAT
     I*
     I*  Externally described Data Structure in the format of
     I*  Physical file EXSTAT
     I*
     ISDAT        DS                              6
     I*
     I*  Data Structure used to convert the System Date Format
     I*
     I                                        1   20SDAT1
     I                                        3   40SDAT2
     I                                        5   60SDAT3
     I*
     IINDAT       DS                              6
     I*
     I*  Data Structures used to convert the Date Format
     I*
     I                                        1   20DAT1
     I                                        3   40DAT2
     I                                        5   60DAT3
     I*
     IOUTDAT      DS                              7
     I                                        1   2 DDAY
     I                                        3   5 DMTH
     I                                        6   7 YRS
     I                                        1   3 MMTH
     I                                        4   5 MDAY
     I*
     I*LDA******* UDS                            256                      S01117
     ILDA         DS                            256                       S01117
     I*
     I*  Defining Local Data Area to display Error Messages
     I*
     I                                      134 141 DBFILE                S01117
     I                                      142 170 DBKEY                 S01117
     I                                      171 180 DBPGM                 S01117
     I                                      181 1830DBASE                 S01117
     I***********                           134 138 FNAM                  S01117
     I***********                           139 170 FKEY                  S01117
     I***********                           171 178 PNAM                  S01117
     I***********                           179 1800ERRNO                 S01117
     I*
     ICPYR@#      DS
     I*
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     I*
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     C/EJECT
     C*
     C*  List of Parameters to be Returned
     C*
     C           *ENTRY    PLIST
     C                     PARM           PDTFMT  3
     C*
     C*  To check for Database Errors
     C*
     C                     READ TABTB10                  55
     C*
     C           *IN55     IFEQ '1'                        **** B001
     C           RECI      ORNE 'D'
     C*
     C*  To print out Database Errors
     C*
     C                     SETON                     U7U8LR
     C*
     C***********          MOVE *BLANKS   TABKEY 12                       S01117
     C***********          MOVEL'01'      TABKEY                          S01117
     C***********          MOVE '10'      TABKEY                          S01117
     C***********          MOVEL'SE3113'  PNAM             ***************S01117
     C***********          MOVELTABKEY    FKEY             * DB ERROR 01 *S01117
     C***********          MOVE 'TABTB'   FNAM             ***************S01117
     C***********          Z-ADD01        ERRNO                           S01117
     C           *NAMVAR   DEFN           LDA                             S01117
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVEL'SE3113'  DBPGM            ***************S01117
     C                     MOVEL'*FIRST'  DBKEY            * DB ERROR 01 *S01117
     C                     MOVE 'TABTB10' DBFILE           ***************S01117
     C                     Z-ADD1         DBASE                           S01117
     C                     OUT  LDA                                       S01117
     C*
     C                     WRITEHEADER
     C                     WRITEERROR
     C***********          WRITEREPORT                                    E29170
     C*
     C***********          DUMP                                           S01117
     C                     EXSR *PSSR                                     S01117
     C*
     C                     RETRN
     C                     END                             **** E001
     C*
     C*  To read records from the Data Structure
     C*
     C           *NAMVAR   DEFN EXSTAT    EXSTTD
     C                     IN   EXSTTD
     C*
     C*  To convert the Date Format (Initiation Date)
     C*
     C                     MOVE LKSD      INDAT
     C                     EXSR DATEF
     C                     MOVE OUTDAT    INDT
     C*
     C*  To convert the Date Format (Termination Date)
     C*
     C                     MOVE LKFD      INDAT
     C                     EXSR DATEF
     C                     MOVE OUTDAT    TEDT
     C*
     C*  To Produce the Link Report
     C*
     C                     WRITEHEADER
     C                     WRITEDETAIL1
     C*
     C*  To print any existing Extel Link Details
     C*
     C                     READ EXLNKD                   56
     C           *IN56     DOWEQ'0'                        **** B001
     C                     WRITEDETAIL2
     C                     READ EXLNKD                   56
     C                     END                             **** E001
     C*
     C***********          WRITEREPORT                                    E29170
     C***********                                                         E29170
     C                     SETON                     LR
     C                     RETRN
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*   Subroutine DATEF - Date Conversion                          *
     C*                                                               *
     C*   Called by :   Detail Process                                *
     C*                                                               *
     C*   Calls     :   None                                          *
     C*                                                               *
     C*****************************************************************
     C*
     C           DATEF     BEGSR
     C*
     C*  Reformat System Date into 'DMY' Format
     C*
     C           PDTFMT    IFEQ 'DMY'                      **** B001
     C                     MOVE SDAT1     DAT1
     C                     MOVE SDAT2     DAT2
     C                     MOVE SDAT3     DAT3
     C                     END                             **** E001
     C*
     C           PDTFMT    IFEQ 'MDY'                      **** B001
     C                     MOVE SDAT2     DAT1
     C                     MOVE SDAT1     DAT2
     C                     MOVE SDAT3     DAT3
     C                     END                             **** E001
     C*
     C           PDTFMT    IFEQ 'YMD'                      **** B001
     C                     MOVE SDAT3     DAT1
     C                     MOVE SDAT2     DAT2
     C                     MOVE SDAT1     DAT3
     C                     END                             **** E001
     C*
     C*  Valid Parameters (System Date Formats)
     C*
     C           PDTFMT    IFEQ 'DMY'                      **** B001
     C           PDTFMT    OREQ 'MDY'
     C           PDTFMT    OREQ 'YMD'
     C*
     C*  Format Output Dates into either 'DDMMMYY' or MMMDDYY'
     C*
     C                     MOVE DAT3      YRS
     C*
     C           DATF      IFEQ 'D'                        **** B002
     C                     MOVE *BLANKS   DMTH
     C                     MOVE DAT1      DDAY
     C           DAT2      IFNE 0                          **** B003
     C                     MOVE MTH,DAT2  DMTH
     C                     END                             **** E003
     C*
     C                     ELSE                            **** X002
     C*
     C                     MOVE DAT1      MDAY
     C                     MOVE *BLANKS   MMTH
     C           DAT2      IFNE 0                          **** B003
     C                     MOVE MTH,DAT2  MMTH
     C                     END                             **** E003
     C*
     C                     END                             **** E002
     C*
     C                     END                             **** E001
     C*
     C*  System Date in 'JUL' Format
     C*
     C           PDTFMT    IFEQ 'JUL'                      **** B001
     C                     END                             **** E001
     C*
     C                     ENDSR
      *****************************************************************   S01117
      /EJECT                                                              S01117
      *****************************************************************   S01117
      *                                                               *   S01117
      *  *PSSR  - Error control subroutine                            *   S01117
      *                                                               *   S01117
      *****************************************************************   S01117
      *                                                                   S01117
     C           *PSSR     BEGSR                           ** *PSSR **    S01117
     C**                                                                  S01117
     C           @RUN      IFEQ *BLANKS                                   S01117
     C                     MOVE 'Y'       @RUN    1                       S01117
     C                     DUMP                                           S01117
     C                     END                                            S01117
     C**                                                                  S01117
     C                     ENDSR                                          S01117
      *                                                                   S01117
     C*****************************************************************   S01117
     C/SPACE 2
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
**    MTH - Month Short Names
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
