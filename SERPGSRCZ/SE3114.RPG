     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE EXTEL Initial Set Up of Files')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module
     F*                                                               *
     F*  SE3114  -  Initial Set Up of EXTEL Files                     *
     F*                                                               *
     F*  Function: To set up data in data area EXSTAT and the trailer *
     F*            files                                              *
     F*                                                               *
     F*  Called by: SEC3114 - Extel Initial Setup                     *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 052254             Date 11Jan94               *
      *                 S01117             Date 11Jun91               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     F*  052254 - RECOMPILED as CURRENT FACTOR amended from 9,8       *
     F*           to 10,9                                             *
     F*  S01117 - Multi-branching                                     *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F*
     FSE3114DFCF  E                    WORKSTN
     FEXCCYZ  O   E                    DISK
     FEXINDZ  O   E                    DISK
     FEXPRCZ  O   E                    DISK
     FEXVALZ  O   E                    DISK
     FHIINDZ  O   E                    DISK
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*  FUNCTION OF INDICATORS                                       *
     F*                                                               *
     F*   01 - F3 TAKEN AT SCREEN                                     *
     F*   88 - DATA AREA ERROR                                        *
     F*   89 - FILE ERROR                                             *
     F*U7-U8 - DATABASE ERROR                                         *
     F*                                                               *
     F*****************************************************************
      /EJECT
     E                    CPY@    1   1 80
     E*
      /EJECT
     I*LDA******* UDS                            256                      S01117
     ILDA         DS                            256                       S01117
     I**     Local Data Area
     I*
     I***********                           134 177 DBLOT                 S01117
     I***********                           134 138 DBFILE                S01117
     I***********                           139 167 DBKEY                 S01117
     I***********                           168 175 DBPGM                 S01117
     I***********                           176 177 DBASE                 S01117
     I                                      134 183 DBLOT                 S01117
     I                                      134 141 DBFILE                S01117
     I                                      142 170 DBKEY                 S01117
     I                                      171 180 DBPGM                 S01117
     I                                      181 1830DBASE                 S01117
     IEXSTTD    E DSEXSTAT
     I**       Data Area  EXSTAT
     I*
     IPSDS       SDS
     I**      Program status data structure used by error processing
     I*
     I                                     *PROGRAM #PGM
     I                                      201 208 #FILE
     I                                      276 277 #MM
     I                                      278 279 #DD
     I                                      280 281 #YY
     IDATE        DS
     I                                        1   2 DD
     I                                        3   4 MM
     I                                        5   6 YY
     I*
     ICPYR@#      DS
     I*
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     C/EJECT
     C*
     C**      Set Up fields (error processing)
     C*
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVE *BLANKS   DBLOT
     C                     MOVEL#PGM      DBPGM
     C                     OUT  LDA                                       S01117
     C**
     C**     Prompt for EXTEL Bank Code & No of transmissions
     C**
     C                     EXFMTSE3114F1               88
     C**
     C**     DB Error
     C**
     C           *IN88     IFEQ '1'
     C           *LOCK     IN   LDA                                       S01117
     C***********          MOVE '01'      DBASE            *************  S01117
     C                     Z-ADD1         DBASE            *************  S01117
     C                     MOVEL'DSPF/'   DBFILE           * DBERR  01 *
     C                     MOVEL'SE3114DF'DBKEY            *************
     C                     SETON                     U7U8
     C                     OUT  LDA                                       S01117
     C                     EXSR *PSSR                                     S01117
     C                     GOTO ENDPGM
     C                     END
     C**
     C**     Drop to end if F3.
     C**
     C           *IN01     CABEQ'1'       ENDPGM
     C**
     C**     Access data area EXSTAT
     C**
     C           *NAMVAR   DEFN EXSTAT    EXSTTD
     C           *LOCK     IN   EXSTTD                 88
     C**
     C**     DB Error if no record found.
     C**
     C           *IN88     IFEQ '1'
     C           *LOCK     IN   LDA                                       S01117
     C***********          MOVE '02'      DBASE            *************  S01117
     C                     Z-ADD2         DBASE            *************  S01117
     C                     MOVEL'DTARA'   DBFILE           * DBERR  02 *
     C                     MOVEL'EXSTAT'  DBKEY            *************
     C                     OUT  LDA                                       S01117
     C                     EXSR *PSSR                                     S01117
     C                     SETON                     U7U8
     C                     ELSE
     C**
     C**      Set Up fields (EXSTAT)
     C**
     C                     MOVE *BLANKS   EXSTTD
     C                     MOVE 'F'       EXST
     C                     MOVE 'N'       EXEF
     C                     Z-ADD1         SQNR
     C                     MOVE #DD       DD
     C                     MOVE #MM       MM
     C                     MOVE #YY       YY
     C                     MOVE DATE      DREC
     C                     MOVE IJBCD     JBCD
     C                     MOVE INORT     NORT    10
     C                     Z-ADDNORT      EXTRAN
     C                     MOVE 'N'       MNTF
     C**
     C                     OUT  EXSTTD
     C**
     C**      Set Up fields (trailers)
     C**
     C                     MOVE 'Z'       RECI
     C**
     C                     Z-ADD0         NORE
     C                     Z-ADD0         NORR
     C                     Z-ADD0         NORA
     C                     Z-ADD0         NINS
     C                     Z-ADD0         NDEL
     C                     Z-ADD0         NAMD
     C                     Z-ADD0         NLRB
     C**
     C**      Write to files
     C**
     C                     WRITEEXCCYZF                89
     C  N89                WRITEEXINDZF                89
     C  N89                WRITEEXPRCZF                89
     C  N89                WRITEEXVALZF                89
     C  N89                WRITEHIINDZF                89
     C**
     C           *IN89     IFEQ '1'
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVEL#FILE     DBFILE
     C***********          MOVE '03'      DBASE            *************  S01117
     C                     Z-ADD3         DBASE            *************  S01117
     C                     OUT  LDA                                       S01117
     C                     EXSR *PSSR                                     S01117
     C                     SETON                     U7U8
     C                     END
     C**
     C                     END
     C**
     C           ENDPGM    TAG
     C***********          OUT  LDA                                       S01117
     C                     SETON                     LR
     C                     RETRN
      *****************************************************************   S01117
      /EJECT                                                              S01117
      *****************************************************************   S01117
      *                                                               *   S01117
      *  *PSSR  - Error control subroutine                            *   S01117
      *                                                               *   S01117
      *****************************************************************   S01117
      *                                                                   S01117
     C           *PSSR     BEGSR                           ** *PSSR **    S01117
     C**                                                                  S01117
     C           @RUN      IFEQ *BLANKS                                   S01117
     C                     MOVE 'Y'       @RUN    1                       S01117
     C                     DUMP                                           S01117
     C                     END                                            S01117
     C**                                                                  S01117
     C                     ENDSR                                          S01117
      *                                                                   S01117
     C*****************************************************************   S01117
     C/EJECT                                                              S01117
**  CPY@
(c) Finastra International Limited 2001
