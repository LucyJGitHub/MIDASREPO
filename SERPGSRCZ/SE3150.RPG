     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE AUTO Cust/Broker Defaults Audit')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module
     F*                                                               *
     F*  SE3150 - CUSTOMER/BROKER DEFAULTS AUDIT LIST                 *
     F*                                                               *
     F*  Function: Provide a list of all charge/commission codes      *
     F*   (I/C)    maintained on the Customer/Broker Default Charges  *
     F*   (COB)    file                                               *
     F*                                                               *
     F*  Called by: SEC0301 - Audit Report -  Input Cycle             *
     F*             SEC0302 - Full Lists -  On Request                *
     F*             SEC0601 - Audit Reports - Close Of Business       *
     F*                                                               *
     F*            Frequency:                                         *
     F*            - Audit report during I/C                          *
     F*            - Audit report during COB                          *
     F*            - List during COB                                  *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01496             Date 27Oct94               *
     F*                 052254             Date 11JAN94               *
     F*                 E29170             DATE 14NOV91               *
     F*                 S01117             DATE 12MAR91               *
     F*                 E21077             DATE 08FEB90               *
     F*                 S01176             DATE 16/08/88              *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*  S01496 - Incorporation of Jyske Enhancements (S10984)        *
     F*  052254 - RECOMPILED as CURRENT FACTOR amended from 9,8       *
     F*           to 10,9                                             *
     F*  E29170 - Report Control Facility changes                     *   S01117
     F*  S01117 - MULTIBRANCHING                                      *   S01117
     F*  E21077 - IF THERE ARE NO DETAILS TO REPORT, DO NOT PRINT     *
     F*           HEADINGS                                            *
     F*  S01176 - SECURITIES TRADING 3.1 (AUSTRALIA)                  *   S01176
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F*
     FCUBRD   IF  E           K        DISK                           UC
     FCUBRL   IF  E           K        DISK                           UC
     F            CUBRDDF                           KRENAMECUBXF3
     FSECUBRL0IF  E           K        DISK                           UC  S01496
     F            CUBRDDF                           KRENAMESCUBRDDF       S01496
     F*                                                                   S01496
     FSECUBLL0IF  E           K        DISK                           UC  S01496
     F            CUBRDDF                           KRENAMESECUBXF3       S01496
     F*TABTB10*IF  E                    DISK                              S01117
     F*CUBRDA IF  E                    DISK                               S01176
     FCUBRDA  UF  E                    DISK                               S01176
     F*CUBRDDXO   E                    DISK                      A    UC  S01176
     F*           CUBRDDF                           KRENAMECUBXF1         S01176
     F*CUBRDAXO   E                    DISK                      A    UC  S01176
     F*           CUBRDAF                           KRENAMECUBXF2         S01176
     FSE3150AUO   E                    PRINTER
     F                                              KINFDS SPOOLU         S01117
     FSE3150P1O   E             60     PRINTER
     F                                              KINFDS SPOOL          S01117
     F*
      /EJECT
     E*
     E*      USED BY SUBROUTINE ZDATE2
     E*
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZSEDITZ1                                               S01176
     E                    CPY@    1   1 80
      /EJECT                                                              S01176
     ICPYR@#      DS
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I*
     I** ERROR DATA STRUCTURE FOR LDA
     I*
     I/COPY ZSRSRC,ZSEDITZ2                                               S01176
     I*                                                                   S01176
     I*LDA******* UDS                            256                      S01117
     ILDA         DS                            256                       S01117
     I**********                            134 138 DBFILE                S01117
     I**********                            139 167 DBKEY                 S01117
     I**********                            168 175 DBPGM                 S01117
     I**********                            176 1770DBASE                 S01117
     I                                      134 141 DBFILE                S01117
     I                                      142 170 DBKEY                 S01117
     I                                      171 180 DBPGM                 S01117
     I                                      181 1830DBASE                 S01117
     I**                                                                  S01117
     I**  DATA STRUCTURE FOR RUN DATE DETAILS                             S01117
     IRUNDT       DS                                                      S01117
     I                                        1   7 RUNA                  S01117
     I                                    P   8  100RUND                  S01117
     I                                       11  11 SSF                   S01117
     I                                       12  12 DATF                  S01117
     I                                       13  13 MBIN                  S01117
     I*  DS FOR LINE NO. TO HANDLE OVERFLOW                               S01117
     ISPOOL       DS                                                      S01117
     I                                       83  92 SFILE                 S01117
     I                                    B 123 1240SFNUM                 S01117
     I**                                                                  S01117
     ISPOOLU      DS                                                      S01117
     I                                       83  92 SFILEU                S01117
     I                                    B 123 1240SFNUMU                S01117
     I**                                                                  S01117
     I**  EXTERNAL DS FOR BANK DETAILS                                    S01117
     ISDBANK    E DSSDBANKPD                                              S01117
     I**                                                                  S01117
     I** DATA STRUCTURE FOR ACCESS OBJECTS, SHORT DATA STRUCTURE          S01117
     ISDSTRD    E DSSDSTRDPD                                              S01496
     I**  Data structure for securities trading details                   S01496
     I*                                                                   S01496
     ISCSARD    E DSSCSARDPD                                              S01496
     I**  Data structure for switchable features file                     S01496
     I*                                                                   S01496
     IDSSDY     E DSDSSDY                                                 S01496
     I**  Long data structure for access objects                          S01496
     I*                                                                   S01496
     IDSFDY     E DSDSFDY                                                 S01117
     I**                                                                  S01117
     I*
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*   MAIN CONTROL                                                *
     C*                                                               *
     C*   CALLS   INIT   - INITIALISATION                             *
     C*           DETAIL - DETAIL RECORD PROCESSING                   *
     C*           TERM   - TERMINATION PROCESSING                     *
     C*           #LCHKF - CHECK IF CHARGE BY CUSTOMER GROUP          *   S01496
     C*                    FEATURE IS ON                              *   S01496
     C*                                                               *
     C*                                                               *
     C*                                                               *
     C*****************************************************************
     C*
     C* RETRIEVE MODE PARAMETER FROM CALLING CL PROGRAM
     C*
     C           *ENTRY    PLIST
     C                     PARM           MODE    1
     C                     PARM           SEQ     5                       S01117
     C*
     C                     EXSR #LCHKF                                    S01496
     C                     EXSR INIT
     C                     EXSR DETAIL
     C                     EXSR TERM
     C*
     C*
     C*****************************************************************
     C/SPACE 3
     C*****************************************************************   S01496
     C*                                                               *   S01496
     C*  #LCHKF - Subroutine to check if Charges by Customer Group    *   S01496
     C*           Feature is on (i.e. S01496 is installed and         *   S01496
     C*           autocharge option is 'Y')                           *   S01496
     C*                                                               *   S01496
     C*  CALLED BY : Main Processing                                  *   S01496
     C*                                                               *   S01496
     C*  CALLS     :                                                  *   S01496
     C*                                                               *   S01496
     C*****************************************************************   S01496
     C*                                                                   S01496
     C           #LCHKF    BEGSR                                          S01496
     C*                                                                   S01496
     C**  Get securities trading details using access object              S01496
     C*                                                                   S01496
     C                     CALL 'AOSTRDR0'                                S01496
     C                     PARM *BLANKS   WLRTCD  7                       S01496
     C                     PARM '*FIRST ' WLOPTN  7                       S01496
     C           SDSTRD    PARM SDSTRD    DSSDY                           S01496
     C*                                                                   S01496
     C**  Check if error occurred                                         S01496
     C*                                                                   S01496
     C           WLRTCD    IFNE *BLANKS                                   S01496
     C           *LOCK     IN   LDA                                       S01496
     C                     Z-ADD3         DBASE            ***************S01496
     C                     MOVE 'SDSTRDPD'DBFILE           * DB ERROR 03 *S01496
     C                     MOVEL'*FIRST'  DBKEY            ***************S01496
     C                     OUT  LDA                                       S01496
     C                     SETON                     U7U8LR               S01496
     C*                                                                   S01496
     C**  Print control report with database error                        S01496
     C*                                                                   S01496
     C                     WRITEHEADAU                                    S01496
     C                     WRITEERROR                                     S01496
     C                     EXSR *PSSR                                     S01496
     C                     RETRN                                          S01496
     C                     END                                            S01496
     C*                                                                   S01496
     C**  Check if S01496 feature is installed using access object        S01496
     C*                                                                   S01496
     C                     CALL 'AOSARDR0'                                S01496
     C                     PARM *BLANKS   WLRTCD                          S01496
     C                     PARM '*VERIFY' WLOPTN                          S01496
     C                     PARM 'S01496'  WLSARD  6                       S01496
     C           SCSARD    PARM SCSARD    DSSDY                           S01496
     C                     MOVE 'N'       S01496  1                       S01496
     C*                                                                   S01496
     C           WLRTCD    IFEQ *BLANKS                                   S01496
     C                     MOVE 'Y'       S01496                          S01496
     C                     ELSE                                           S01496
     C*                                                                   S01496
     C**  Error if return code other than '*NRF'                          S01496
     C*                                                                   S01496
     C           WLRTCD    IFNE '*NRF'                                    S01496
     C           *LOCK     IN   LDA                                       S01496
     C                     Z-ADD4         DBASE            ***************S01496
     C                     MOVE 'SCSARDPD'DBFILE           * DB ERROR 04 *S01496
     C                     MOVEL'*VERIFY' DBKEY            ***************S01496
     C                     OUT  LDA                                       S01496
     C                     SETON                     U7U8LR               S01496
     C*                                                                   S01496
     C**  Print control report with database error                        S01496
     C*                                                                   S01496
     C                     WRITEHEADAU                                    S01496
     C                     WRITEERROR                                     S01496
     C                     EXSR *PSSR                                     S01496
     C                     RETRN                                          S01496
     C                     END                                            S01496
     C*                                                                   S01496
     C                     END                                            S01496
     C*                                                                   S01496
     C**  If S01496 is on and autocharge option is 'Y', seton ind. 55     S01496
     C*                                                                   S01496
     C           S01496    IFEQ 'Y'                                       S01496
     C           BVACOP    ANDEQ'Y'                                       S01496
     C                     MOVE '1'       *IN55                           S01496
     C                     END                                            S01496
     C*                                                                   S01496
     C                     ENDSR                                          S01496
     C*                                                                   S01496
     C*****************************************************************   S01496
     C/EJECT                                                              S01496
     C*****************************************************************
     C*                                                               *
     C*           INIT - INITIALISATION                               *
     C*                                                               *
     C*           CALLED BY : MAIN PROCESSING                         *
     C*                                                               *
     C*           CALLS     : REICD- READ TABTB10 RECORD AND HANDLE   *
     C*                       POSSIBLE DATABASE ERROR.                *
     C*                                                               *
     C*           USES      :                                         *
     C*                       *IN40- REPORT TEXT FOR INPUT CYCLE      *
     C*                              MODE.                            *
     C*                       *IN41- REPORT TEXT FOR C.O.B MODE       *
     C*                       *IN42- REPORT TEXT FOR LIST MODE        *
     C*                       MODE - MODE OF OPERATION OF PROG        *
     C*                              I=INPUT CYCLE                    *
     C*                              A=CLOSE OF BUSINESS              *
     C*                              L=LIST                           *
     C*                       ZETXT - TEXT FOR DATA BASE ERROR        *
     C*                                                               *
     C*****************************************************************
     C*
     C           INIT      BEGSR
     C*
     C*  INITAILISATION OF VARIABLES
     C*
     C                     SETOF                     U7U854
     C*
     C                     Z-ADD0         ZCHG    50
     C                     Z-ADD0         ZCHK    50
     C                     Z-ADD0         ZAMD    40
     C                     Z-ADD0         ZINS    40
     C                     Z-ADD0         ZDEL    40
     C                     Z-ADD0         ZLIVE   50
     C                     Z-ADD0         ZNREP   50
     C           *NAMVAR   DEFN           LDA                             S01117
     C           *LOCK     IN   LDA                                       S01117
     C                     Z-ADD0         DBASE                           S01117
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     MOVE *BLANKS   DBPGM
     C                     MOVE *BLANKS   ZETXT  15
     C*
     C*  SETON *IN60 TO PRINT DETAIL RECORD HEADING FIRST TIME
     C*
     C                     MOVE '1'       *IN60
     C*
     C                     MOVEL'SE3150'  DBPGM
     C                     OUT  LDA                                       S01117
     C*
     C           MODE      IFEQ 'I'                        B1
     C*                                                                   S01496
     C           *IN55     IFEQ '1'                                       S01496
     C                     OPEN SECUBLL0                                  S01496
     C                     ELSE                                           S01496
     C                     OPEN CUBRL
     C                     END                                            S01496
     C*                                                                   S01496
     C                     SETON                     40
     C                     END                             E1
     C*
     C           MODE      IFEQ 'A'                        B1
     C*                                                                   S01496
     C           *IN55     IFEQ '1'                                       S01496
     C                     OPEN SECUBRL0                                  S01496
     C                     ELSE                                           S01496
     C                     OPEN CUBRD
     C                     END                                            S01496
     C*                                                                   S01496
     C**********           OPEN CUBRDDX                                   S01176
     C**********           OPEN CUBRDAX                                   S01176
     C                     SETON                     41
     C                     END                             E1
     C*
     C           MODE      IFEQ 'L'                        B1
     C*                                                                   S01496
     C           *IN55     IFEQ '1'                                       S01496
     C                     OPEN SECUBRL0                                  S01496
     C                     ELSE                                           S01496
     C                     OPEN CUBRD
     C                     END                                            S01496
     C*                                                                   S01496
     C                     SETON                     42
     C                     END                             E1
     C*                                                                   S01117
     C           *NAMVAR   DEFN RUNDAT    RUNDT                           S01117
     C                     IN   RUNDT                                     S01117
     C*
     C                     EXSR REICD
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*           DETAIL - DETAIL PROCESSING                          *
     C*                                                               *
     C*           CALLED BY : MAIN PROCESSING                         *
     C*                                                               *
     C*           CALLS     : ZDATE2                                  *
     C*                                                               *
     C*           USES      :                                         *
     C*                      *IN80 - READ IND ON CUBRL                *
     C*                      *IN81 - READ IND ON CUBRD                *
     C*                      LCD   - LAST CHANGE DATE                 *
     C*                      RUND  - RUN DATE                         *
     C*                      ZCHG  - COUNTER FOR LAST CHAGE DATE=     *
     C*                              RUN DATE                         *
     C*                      ZLIVE - COUNTER FOR LIVE RECS READ       *
     C*                      ZAMD  - COUNTER FOR RECS AMENDED         *
     C*                      ZINS  - COUNTER FOR RECS INSERTED        *
     C*                      ZDEL  - COUNTER FOR RECS DELETED         *
     C*                      CHTP  - RECORD CHANGE TYPE               *
     C*                              'I' INSERTED                     *
     C*                              'A' AMENDED                      *
     C*                              'D' DELETED                      *
     C*                      *IN60 - OVERFLOW INDICATOR ON PRINT      *
     C*                              FILES                            *
     C*                      RECI  - RECORD ID                        *
     C*                      MODE  - MODE OF OPERATION OF PROG        *
     C*                              I=INPUT CYCLE                    *
     C*                              A=CLOSE OF BUSINESS              *
     C*                              L=LIST                           *
     C*                      ZDAYNO - FIVE DIGIT DAY NUMBER           *
     C*                      ZNREP  - DETAILS TO REPORT               *
     C*                               =0 NO DETAILS                   *
     C*                                                               *
     C*****************************************************************
     C*
     C           DETAIL    BEGSR
     C*
     C** INPUT CYCLE MODE
     C*
     C           MODE      IFEQ 'I'                        B1
     C           *IN80     DOUEQ'1'                        B2
     C           LCD       ORNE RUND                       *2
     C*                                                                   S01496
     C           *IN55     IFEQ '1'                                       S01496
     C                     READ SECUBXF3                 80               S01496
     C                     ELSE                                           S01496
     C                     READ CUBXF3                   80
     C                     END                                            S01496
     C*                                                                   S01496
     C           LCD       IFEQ RUND                       B3
     C           *IN80     ANDEQ'0'                        *3
     C                     ADD  1         ZCHG
     C                     ADD  1         ZNREP
     C           CHTP      IFEQ 'A'                        B4
     C                     ADD  1         ZAMD
     C                     END                             E4
     C           CHTP      IFEQ 'I'                        B4
     C                     ADD  1         ZINS
     C                     END                             E4
     C           CHTP      IFEQ 'D'                        B4
     C                     ADD  1         ZDEL
     C                     END                             E4
     C*
     C* TO CHANGE LCD INTO DDMMMYY FORMAT FOR PRINTING
     C*
     C                     Z-ADDLCD       ZDAYNO
     C                     EXSR ZDATE2
     C*                                                                   S01176
     C**  EDIT COMMISSION REBATE PERCENTAGE FOR PRINTING                  S01176
     C*                                                                   S01176
     C                     MOVE CMRP      ZFLD15                          S01176
     C                     Z-ADD3         ZDECS                           S01176
     C                     EXSR ZSEDIT                                    S01176
     C                     MOVE ZOUT21    RCMRP                           S01176
     C*
     C           *IN60     IFEQ '1'                        B4
     C                     WRITESE3150F1
     C                     MOVE '0'       *IN60
     C                     END                             E4
     C                     WRITESE3150F2
     C                     END                             E3
     C                     END                             E2
     C*
     C                     ELSE                            X1
     C*
     C** OTHER MODES
     C*
     C           *IN81     DOUEQ'1'                        B2
     C*                                                                   S01496
     C           *IN55     IFEQ '1'                                       S01496
     C                     READ SCUBRDDF                 81               S01496
     C                     ELSE                                           S01496
     C                     READ CUBRDDF                  81
     C                     END                                            S01496
     C*                                                                   S01496
     C           *IN81     IFEQ '0'                        B3
     C           LCD       IFEQ RUND                       B4
     C           MODE      ANDEQ'A'                        *4
     C           MODE      OREQ 'L'                        *4
     C           MODE      IFEQ 'A'                        B5
     C           CHTP      IFEQ 'A'                        B6
     C                     ADD  1         ZAMD
     C                     END                             E6
     C           CHTP      IFEQ 'I'                        B6
     C                     ADD  1         ZINS
     C                     END                             E6
     C           CHTP      IFEQ 'D'                        B6
     C                     ADD  1         ZDEL
     C                     END                             E6
     C                     ADD  1         ZCHG
     C                     END                             E5
     C                     ADD  1         ZNREP
     C*
     C* TO CHANGE LCD INTO DDMMMYY FORMAT FOR PRINTING
     C*
     C                     Z-ADDLCD       ZDAYNO
     C                     EXSR ZDATE2
     C*                                                                   S01176
     C**  EDIT COMMISSION REBATE PERCENTAGE FOR PRINTING                  S01176
     C*                                                                   S01176
     C                     MOVE CMRP      ZFLD15                          S01176
     C                     Z-ADD3         ZDECS                           S01176
     C                     EXSR ZSEDIT                                    S01176
     C                     MOVE ZOUT21    RCMRP                           S01176
     C*
     C           *IN60     IFEQ '1'                        B5
     C                     WRITESE3150F1
     C                     MOVE '0'       *IN60
     C                     END                             E5
     C                     WRITESE3150F2
     C                     END                             E4
     C*
     C           RECI      IFEQ 'D'                        B4
     C                     ADD  1         ZLIVE
     C                     END                             E4
     C********** MODE      IFEQ 'A'                        B4             S01176
     C********** RECI      ANDEQ'D'                        *4             S01176
     C**********           WRITECUBXF1                                    S01176
     C**********           END                             E4             S01176
     C                     END                             E3
     C                     END                             E2
     C                     END                             E1
     C*
     C           ZNREP     IFNE 0                          B1
     C                     WRITESE3150F6
     C**                                                                  S01117
     C*       ENSURE REPORT SPOOL FILE RECORDED BY RCF                    S01117
     C**                                                                  S01117
     C                     Z-ADDSFNUM     ZSNUM   60                      S01117
     C**                                                                  S01117
     C                     CALL 'ZSFILE'                                  S01117
     C                     PARM           SEQ                             S01117
     C                     PARM *BLANKS   SENTY   3                       S01117
     C                     PARM           SFILE                           S01117
     C                     PARM           ZSNUM                           S01117
     C                     PARM           ZSERR   1                       S01117
     C**                                                                  S01117
     C           ZSERR     IFEQ 'Y'                                       S01117
      **  ERROR DURING ZSFILE PROCESSING, RETURN TO CALLING PROGRAM       S01117
     C                     SETON                     U7U8                 S01117
     C                     RETRN                                          S01117
     C                     END                                            S01117
     C**                                                                  S01117
     C                     END                             E1
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*           TERM - TERMINATION PROCESSING                       *
     C*                                                               *
     C*           CALLED BY : MAIN PROCESSING                         *
     C*                                                               *
     C*           CALLS     :                                         *
     C*                      DBERR - DATABASE ERROR                   *
     C*                                                               *
     C*           USES      :                                         *
     C*                      *IN82 - READ CUBRDA INDICATOR            *
     C*                      NLRB  - NO OF LIVE RECORDS BEFORE        *
     C*                              INPUT CYCLE                      *
     C*                      NINS  - NO OF INSERTIONS                 *
     C*                      NDEL  - NO OF DELETIONS                  *
     C*                      NORE  - NO OF RECORDS FROM CUBRDA        *
     C*                      ZCHK  - TEMPORARY WORK VARIBLE           *
     C*                      ZLIVE - COUNTER FOR LIVE RECS READ       *
     C*                      ZAMD  - COUNTER FOR RECS AMENDED         *
     C*                      ZINS  - COUNTER FOR RECS INSERTED        *
     C*                      ZDEL  - COUNTER FOR RECS DELETED         *
     C*                      *IN60 - OVERFLOW INDICATOR ON PRINT      *
     C*                              FILES                            *
     C*                       MODE - MODE OF OPERATION OF PROG        *
     C*                              I=INPUT CYCLE                    *
     C*                              A=CLOSE OF BUSINESS              *
     C*                              L=LIST                           *
     C*                      ZNREP - DETAILS TO REPORT                *
     C*                              =0 NO DETAILS                    *
     C*                                                               *
     C*****************************************************************
     C*
     C           TERM      BEGSR
     C*
     C           ZNREP     IFEQ 0                          B1
     C                     SETON                     30                   E21077
     C**********           WRITESE3150F1                                  S01117
     C**********           WRITESE3150F5                                  S01117
     C**********           WRITESE3150F6                                  S01117
     C                     END                             E1
     C                     READ CUBRDAF                  82
     C           *IN82     IFEQ '1'                        B1
     C                     EXSR DBERR
     C                     END                             E1
     C**                                                                  S01117
     C*       ENSURE REPORT SPOOL FILE RECORDED BY RCF                    S01117
     C**                                                                  S01117
     C                     Z-ADDSFNUMU    ZSNUMU  60                      S01117
     C**                                                                  S01117
     C                     CALL 'ZSFILE'                                  S01117
     C                     PARM           SEQ                             S01117
     C                     PARM *BLANKS   SENTY                           S01117
     C                     PARM           SFILEU                          S01117
     C                     PARM           ZSNUMU                          S01117
     C                     PARM           ZSERR                           S01117
     C**                                                                  S01117
     C           ZSERR     IFEQ 'Y'                                       S01117
      **  ERROR DURING ZSFILE PROCESSING, RETURN TO CALLING PROGRAM       S01117
     C                     SETON                     U7U8                 S01117
     C                     RETRN                                          S01117
     C                     END                                            S01117
     C**                                                                  S01117
     C*
     C** INPUT CYCLE MODE
     C*
     C           MODE      IFEQ 'I'                        B1
     C                     WRITEHEADAU
     C                     WRITECONTROLI
     C           *IN30     IFEQ '1'                                       S01117
     C                     WRITENONE                                      S01117
     C                     END                                            S01117
     C**********           WRITETRAILAU                                   S01117
     C*                                                                   S01496
     C           *IN55     IFEQ '1'                                       S01496
     C                     CLOSESECUBLL0                                  S01496
     C                     ELSE                                           S01496
     C                     CLOSECUBRL
     C                     END                                            S01496
     C*                                                                   S01496
     C                     END                             E1
     C*
     C** CLOSE OF BUSINESS MODE
     C*
     C           MODE      IFEQ 'A'                        B1
     C           NLRB      ADD  NINS      ZCHK
     C           ZCHK      SUB  NDEL      ZCHK
     C           ZCHK      IFNE ZLIVE                      B2
     C                     SETON                     U8
     C                     END                             E2
     C           NORE      IFNE ZLIVE                      B2
     C                     SETON                     U854
     C                     END                             E2
     C                     WRITEHEADAU
     C                     WRITECONTROLA
     C           *IN30     IFEQ '1'                                       S01117
     C                     WRITENONE                                      S01117
     C                     END                                            S01117
     C**********           WRITETRAILAU                                   S01117
     C           *INU8     IFEQ '0'                        B2
     C*
     C*  INITIALISE VARIABLES BEFORE WRITING TRAILER
     C*
     C                     Z-ADD0         NINS
     C                     Z-ADD0         NDEL
     C                     Z-ADD0         NAMD
     C                     Z-ADDNORE      NLRB
     C**********           WRITECUBXF2                                    S01176
     C                     UPDATCUBRDAF                                   S01176
     C                     END                             E2
     C*                                                                   S01496
     C           *IN55     IFEQ '1'                                       S01496
     C                     CLOSESECUBRL0                                  S01496
     C                     ELSE                                           S01496
     C                     CLOSECUBRD
     C                     END                                            S01496
     C*                                                                   S01496
     C**********           CLOSECUBRDDX                                   S01176
     C**********           CLOSECUBRDAX                                   S01176
     C                     END                             E1
     C*
     C**  LIST MODE
     C*
     C           MODE      IFEQ 'L'                        B1
     C           NORE      IFNE ZLIVE                      B2
     C                     SETON                     U8
     C                     END                             E2
     C                     WRITEHEADAU
     C                     WRITECONTROLL
     C           *IN30     IFEQ '1'                                       S01117
     C                     WRITENONE                                      S01117
     C                     END                                            S01117
     C**********           WRITETRAILAU                                   S01117
     C*                                                                   S01496
     C           *IN55     IFEQ '1'                                       S01496
     C                     CLOSESECUBRL0                                  S01496
     C                     ELSE                                           S01496
     C                     CLOSECUBRD
     C                     END                                            S01496
     C*                                                                   S01496
     C**********           CLOSECUBRDDX                                   S01176
     C**********           CLOSECUBRDAX                                   S01176
     C                     END                             E1
     C*
     C*
     C                     SETON                     LR
     C                     RETRN
     C*
     C                     ENDSR
     C****************************************************************
     C/EJECT
     C****************************************************************
     C*                                                              *
     C***REICD-READ*TABTB10*RECORD************************************    S01117
     C*  REICD-ACCESS SDBANKPD RECORD                                *
     C*                                                              *
     C*  CALLED BY :  INIT                                           *
     C*                                                              *
     C*  CALLS     :                                                 *
     C*                                                              *
     C*  USES      :                                                 *
     C*             MODE  - MODE OF OPERATION OF PROG                *
     C*                     I=INPUT CYCLE                            *
     C*                     A=CLOSE OF BUSINESS                      *
     C*                     L=LIST                                   *
     C*             RECI  - RECORD ID                                *
     C***************IN70*-*CHAIN*ON*TABTB10**************************    S01117
     C*             DATF  - DATE FORMAT                              *
     C*                     =M MMDDYY                                *
     C*                     <>M DDMMYY                               *
     C*             *IN98 - INDICATOR FOR DATE FORMAT                *
     C*             DBFILE - FILE DB ERROR OCCURED ON                *
     C*             DBKEY - KEY TO THE FILE                          *
     C*             DBASE - DB ERROR NUMBER INDICATOR                *
     C*              *IN40- REPORT TEXT FOR INPUT CYCLE              *
     C*                     MODE.                                    *
     C*              *IN41- REPORT TEXT FOR C.O.B MODE               *
     C*              *IN42- REPORT TEXT FOR LIST MODE                *
     C*                                                              *
     C****************************************************************
     C*
     C           REICD     BEGSR
     C*
     C***READ*TABTB10*RECORD                                              S01117
     C** ACCESS SDBANKPD RECORD                                           S01117
     C*
     C********** 1         CHAINTABTB10              70                   S01117
     C                     CALL 'AOBANKR0'                                S01117
     C                     PARM '*MSG   ' @RTCD   7                       S01117
     C                     PARM '*FIRST ' @OPTN   7                       S01117
     C           SDBANK    PARM SDBANK    DSFDY                           S01117
     C*
     C** IF RECORD NOT FOUND/NOT LIVE DATABASE ERROR
     C*
     C********** *IN70     IFEQ '1'                        B1             S01117
     C********** RECI      ORNE 'D'                        *1             S01117
     C           @RTCD     IFNE *BLANKS                                   S01117
     C*
     C*
     C           *LOCK     IN   LDA                                       S01117
     C**********           MOVEL'TABTB'   DBFILE           ***************S01117
     C**********           MOVEL'01'      DBKEY            * DB ERROR 01 *S01117
     C                     MOVEL'SDBANKPD'DBFILE           ***************S01117
     C                     MOVEL'*FIRST'  DBKEY            * DB ERROR 01 *S01117
     C                     Z-ADD1         DBASE            ***************
     C                     OUT  LDA                                       S01117
     C                     SETON                     U7U8LR
     C*
     C* PRINT CONTROL REPORT WITH DATABASE ERROR
     C*
     C                     WRITEHEADAU
     C                     WRITEERROR
     C**********           WRITETRAILAU                                   S01117
     C***********          DUMP                                           S01117
     C                     EXSR *PSSR                                     S01117
     C                     RETRN
     C                     END                             E1
     C*
     C                     MOVELBJURPT    TITL                            S01117
     C*
     C           DATF      IFEQ 'M'                        B1
     C                     SETON                     98
     C                     END                             E1
     C*
     C                     ENDSR
     C****************************************************************
     C/EJECT
     C****************************************************************
     C*                                                              *
     C*  DBERR-ERROR SUBROUTINE FOR DATABASE ERROR                   *
     C*                                                              *
     C*  CALLED BY : TERM                                            *
     C*                                                              *
     C*  CALLS     :                                                 *
     C*                                                              *
     C*                                                              *
     C*  USES      :                                                 *
     C*              MODE - MODE OF OPERATION OF PROG                *
     C*                     I=INPUT CYCLE                            *
     C*                     A=CLOSE OF BUSINESS                      *
     C*                     L=LIST                                   *
     C*              *IN40- REPORT TEXT FOR INPUT CYCLE              *
     C*                     MODE.                                    *
     C*              *IN41- REPORT TEXT FOR C.O.B MODE               *
     C*              *IN42- REPORT TEXT FOR LIST MODE                *
     C*                                                              *
     C*                                                              *
     C*                                                              *
     C****************************************************************
     C*
     C           DBERR     BEGSR
     C*
     C           *LOCK     IN   LDA                                       S01117
     C                     MOVEL'CUBRDA'  DBFILE           ****************
     C                     MOVEL'TRAILER 'ZETXT            *              *
     C                     MOVE 'MISSING' ZETXT            * DB ERROR 02  *
     C                     MOVE ZETXT     DBKEY            *              *
     C                     Z-ADD2         DBASE            ****************
     C                     OUT  LDA                                       S01117
     C                     SETON                     U7U8LR*1
     C*
     C* PRINT CONTROL REPORT WITH DATABASE ERROR
     C*
     C                     WRITEHEADAU
     C                     WRITEERROR
     C                     WRITEERRORP1                                   E29170
     C**********           WRITETRAILAU                                   S01117
     C***********          DUMP                                           S01117
     C                     EXSR *PSSR                                     S01117
     C                     RETRN
     C                     ENDSR
     C*
      *****************************************************************   S01117
      /EJECT                                                              S01117
      *****************************************************************   S01117
      *                                                               *   S01117
      *  *PSSR  - Error control subroutine                            *   S01117
      *                                                               *   S01117
      *****************************************************************   S01117
      *                                                                   S01117
     C           *PSSR     BEGSR                           ** *PSSR **    S01117
     C**                                                                  S01117
     C           @RUN      IFEQ *BLANKS                                   S01117
     C                     MOVE 'Y'       @RUN    1                       S01117
     C                     DUMP                                           S01117
     C                     END                                            S01117
     C**                                                                  S01117
     C                     ENDSR                                          S01117
      *                                                                   S01117
     C*****************************************************************
      /EJECT                                                              S01176
     C/COPY ZSRSRC,ZDATE2Z2                                               S01176
      /EJECT                                                              S01176
     C/COPY ZSRSRC,ZSEDITZ3                                               S01176
      /EJECT                                                              S01176
      ***
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  CPY@
(c) Finastra International Limited 2001
