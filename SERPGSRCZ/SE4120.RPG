     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Repo buy-sell back current valuation')        *
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module                            *
     F*                                                               *
     F*  SE4120 - REPO BUY-SELL BACK CURRENT VALUATION                *
     F*                                                               *
     F*  Function: To report all of the details of Repos, and         *
     F*   (I/C)    Buy-Sell backs and to show the current             *
     F*   (COB)    valuation of the securities involved.              *
     F*                                                               *
     F*  Called by: SEC4120 - Input Cycle or COB on request.          *
     F*                                                               *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD050604           Date 21Feb20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 226449             Date 24Jun15               *
      *                 MD034776           Date 09Jul15               *
      *                 CGL165             Date 15Feb17               *
      *                 CDL096             Date 22Sep14               *
      *                 CDL094             Date 11Jun14               *
      *                 AR1067108          Date 11Dec12               *
      *                 AR1001183          Date 20Nov12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 241662             Date 17Aug06               *
      *                 CSD031             Date 10Apr06               *
      *                 CDL049             Date 11May06               *
      *                 CSD027             Date 09Dec05               *
      *                 CDL038             Date 10May05               *
      *                 CSE071             Date 19Jul05               *
      *                 BUG9169            Date 17Nov05               *
      *                 CSW037A            Date 02May05               *
      *                 229857             Date 07Oct04               *
      *                 226570             Date 27Jun04               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      *                 222727             Date 05Nov03               *
      *                 194370             Date 12Jul01               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CPK015             Date 17Jun01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 188893             Date 17Jan01               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 176443             Date 18Apr00               *
      *                 176403             Date 14Mar00               *
     F*                 176303             Date 09Mar00               *
     F*                 176301             Date 04Mar00               *
      * Midas DBA 3.02 -----------------------------------------------*
     F*                 CSE006  *CREATE    Date 30Jul99               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD050604 - Average price of position changed incorrectly.    *
      *           - Recompiled due to changes in copy source ZCNSDZ1. *
      *  MD046248 - Finastra Rebranding                               *
      *  226449 - Recompiled over changes made in /COPY ZDYYRZ3.      *
      *         - Applied for MD034387.                               *
      *  MD034776 - Reverse AR1001183A to allow negative interest     *
      *             rate. (Recompile)                                 *
      *  CGL165 - Dual Withholding Tax (Recompile)                    *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *           (Recompile)                                         *
      *  CDL094 - Enhance  Receive Settlement Instructions            *
      *           (Recompiled)                                        *
      *  AR1067108 - PMC1140 Component failed during Clean COB        *
      *              Applied fix AR1001183A                           *
      *  AR1001183A - Coupon rate was still used in computation of    *
      *              interest instead of using the details in FRN tab *
      *              (Reopen) (Recompile)                             *
      *  241662 - Recompile over change to field DLAMT1 in SE4120P1.  *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  BUG9169- Put code to handle empty SEDLDCL0.                  *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  229857 - Recompiled due to change in ZYLDZ2.                 *
      *  226570 - Recompiled due to change in ZYLDZ2.                 *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  222727 - Release 5.0 errors  (Recompile)                     *
      *  194370 - Recompile after change on copy sources ZYLDZ2,      *
      *           For securities with divisor basis = '5' and first   *
      *           coupon date > valuation date (rundate + 1), set     *
      *           event control date to first coupon date before      *
      *           processing SR/ZDYYR. This is to ensure that the     *
      *           correct hypothetical last and next coupon dates are *
      *           used in deriving the price/yield rate.              *
      *  CPK015 - Conversion of ZYIELD from PLI to RPG.               *
      *           Recompile over changed ZYLDZ2.                      *
      *  188893 - Printer overflow line on SE4120P1                   *
      *  176443 - Avoid MCH3601 to be sent.                           *
      *           Pass correct parameter to AOCUSTR0.                 *
     F*  176403 - Insert Nominal Currency field for securities        *
     F*  176303 - Value not being calculated correctly for discount/  *
     F*           yield basis securities.                             *
     F*  176301 - Report trailer not strictly correct                 *
     F*  CSE006 - Repurchase Agreements (REPOs) enhancement           *
     F*                                                               *
     F*****************************************************************
      *
     FSE4120P1O   E                    PRINTER                        UC
     F                                              KINFDS SPOOL1
     FSE4120P2O   E                    PRINTER                        UC
     F                                              KINFDS SPOOL2
     FSE4120AUO   E                    PRINTER                        UC
     F                                              KINFDS SPOOLU
     FSECTY   IF  E           K        DISK
     FTRADS   IF  E           K        DISK
     FPRICE   IF  E           K        DISK
     FSEDLDCL0IF  E           K        DISK
     FSEDPMVL3IF  E           K        DISK
     FBSPOSDL1IF  E           K        DISK
      *
      ** Securities Events file for /COPYs                                176303
     FSECEO   IF  E           K        DISK                               176303
     F            SEDEVDF                           KRENAMESEDEVDO        176303
     F            SNDEVDF                           KRENAMESNDEVDO        176303
     FSEDEV   IF  E           K        DISK                               176303
      *
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   30     Used for READ of SEDLDCL0                            *
     F*   31     Condition for no change of branch                    *
     F*   32     Condition for change of branch                       *
     F*   33     Used for READ of SEDPMVL3                            *
     F*   34     Used for READ of SECTY                               *
     F*   35     Used for READ of INVTP                               *
     F*   36     Used for READ of BSPOSDL1                            *
     F*   37     Used for CHAIN  of TRADS                             *
     F*   38     Used for CHAIN of PRICE                              *
     F*                                                               *
     F*   97     USED IN CONDITIONING IF UPDATE FAILS.                *
     F*   99     USED IN THE Z-SUBROUTINES                            *
     F*                                                               *
     F*   U7-U8  DATABASE    OR                                       *
     F*   U8     DIFFERENCE BETWEEN CALC AND FILE TOTALS OF RECORDS   *
     F*                                                               *
     F*****************************************************************
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E   I N D E X                              *
     F*                                                               *
     F*  INIT   -  Initialisation Subroutine                          *
     F*  DDETL  -  GETS DETAILS FOR PRINTER FILE 1                    *
     F*  BSDTL  -  GETS BUY-SELL DETAILS                              *
     F*  FIRST  - CHECK TO SEE IF THIS IS THE FIRST TIME THROUGH      *
     F*  SRDPMV - PRINTS ALL DEPOT MOVEMENTS RELATED TO THE DEALS     *
     F*  ZCNSD  - Calculates Consideration from Nominal and Price     *
     F*  MPRIC  - CALCULATES MARKET PRICE                             *
     F*  BSFST  - CHECK TO SEE IF FIRST TIME THROUGH SR BSDTL         *
     F*  BSPR1  - CALCULATES MARKET PRICE FOR 1ST TRADE               *
     F*  BSPR1  - CALCULATES MARKET PRICE FOR 2ND TRADE               *
     F*  AUDIT  -  PRINTS AUDIT SPOOL FILE                            *
     F*  DBERR  -  SUBROUTINE FOR DB ERROR                            *
     F*  RCFP1  -  SUBROUTINE TO REGISTER P1 PRINTER FILE VIA RCF     *
     F*  RCFP2  -  SUBROUTINE TO REGISTER P2 PRINTER FILE VIA RCF     *
     F*  RCFAU  -  SUBROUTINE TO REGISTER AU PRINTER FILE VIA RCF     *
     F*                                                               *
     F*  ZCONV   -  CONVERTS AMOUNT FROM ONE CCY TO ANOTHER CCY       *
     F*  ZDATE1  -  DATE - DAYNO CONVERSION                           *   176303
     F*  ZPRCI   -  CALCULATE PRICE IN % PRICE BASIS                  *   176303
     F*  ZDATE2  -  DAYNO - DATE CONVERSION                           *
     F*  ZSEDIT  -  PUTS COMMAS, DEC.PL AND SIGN IN NUMERIC FIELDS    *
     F*                                                               *
     F*****************************************************************
      /EJECT
     E                    CPY@    1   1 80
     E/COPY ZSRSRC,ZNCDZ1                                                 176303
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZSEDITZ1
     E/COPY ZSRSRC,ZPOWER8Z1
     E/COPY ZSRSRC,ZPOWERZ1                                               176303
     E/COPY ZSRSRC,ZDAYSZ1                                                176303
     E/COPY ZSRSRC,ZACCRZ0                                                176303
      *
      *
      *  Data Structure for compilation  - Copyright insertion
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *
      *  Local Data-Area data structure used for DB errors
     ILDA         DS                            256
     I                                      134 183 DBLOT
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *
      **  DS for P1 spool file name
     ISPOOL1      DS
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1                037183
     I                                    B 367 3680PRTLN1                037183
      *
      **  DS for P2 spool file name
     ISPOOL2      DS
     I                                       83  92 SFILE2
     I                                    B 123 1240SFNUM2
     I                                    B 188 1890OFLLN2
     I                                    B 367 3680PRTLN2
      *
      **  DS for AU spool file name
     ISPOOLU      DS
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
      ** Data structure for RUNDAT
     IRUNDT       DS
     I                                        1   7 RUNA
     I                                    P   8  100RUND
     I                                       11  11 SSF
     I                                       12  12 DATF
     I                                       13  13 MBIN
     I****:                                  12  12 DFF
     I*****                                  13  13 MBIN
      *
      *
     ISDCURR    E DSSDCURRPD
     I** DS for access to Midas Currency ICD
     I*
     ISDBANK    E DSSDBANKPD
     I** DS for access to Midas Bank details ICD
     I*
     ISDBRCH    E DSSDBRCHPD
     I** DS for access to Midas General Ledger ICD
     I*
     ISDCUST    E DSSDCUSTPD
     I              QQDFAC                          #DFAC1                                    CGL029
     I** DS for access to Midas Customer Details ICD
     I*
     IDSFDY     E DSDSFDY
     I** First DS for access programs, short data structure
     I*
     IDSSDY     E DSDSSDY
     I** First DS for access programs, long data structure
     I*
     I/COPY ZSRSRC,ZSEDITZ2
      *
      *** Named constants for PRINTER 1 file header
      *
     I**************'REPO          '      C         DTYPE1
     I**************'REVERSE REPO  '      C         DTYPE2
     I**************'BOND LENDING  '      C         DTYPE3
     I**************'BOND BORROWING'      C         DTYPE4
     I**************'PLEDGE DEPOSIT'      C         DTYPE5
     I**************'PLEDGE LOAN   '      C         DTYPE6
     I              'REPO/REVERSE REPO'   C         DTYPE1
     I              'REPO/REVERSE REPO'   C         DTYPE2
     I              'BOND BORROW/LEND'    C         DTYPE3
     I              'BOND BORROW/LEND'    C         DTYPE4
     I              'PLEDGE DEPOSIT/LOAN' C         DTYPE5
     I              'PLEDGE DEPOSIT/LOAN' C         DTYPE6
      *
      *** Named constants for PRINTER 2 file header
      *
     I              'BUY-SELL'            C         TTYP21
     I              'SELL-BUY'            C         TTYP22
      *                                                                   176303
     I/COPY ZSRSRC,ZNCDZ2                                                 176303
     I/COPY ZSRSRC,ZYLDZ1                                                 176303
     C/EJECT
      *================================================================
      *  M A I N    P R O C E S S I N G                               *
      *================================================================
      *
      * Initialisation
     C                     EXSR INIT
      *
      * If no DB error
     C                     EXSR DDETL
      *                                                                                      BUG9169
      ** Check if no record in file                                                          BUG9169
      *                                                                                      BUG9169
     C           *IN30     IFEQ *ON                                                          BUG9169
     C           @NOREC    ANDEQ'Y'                                                          BUG9169
     C                     GOTO AUDTAG                                                       BUG9169
     C                     ENDIF                                                             BUG9169
      *
      * If no DB error
     C                     EXSR BSDTL
      *
      * Process AUDIT
     C           AUDTAG    TAG                                                               BUG9169
     C                     EXSR AUDIT
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
      /EJECT
      *****************************************************************
      *                                                               *
      * INIT - INITIAL PROCESSING                                     *
      *                                                               *
      *****************************************************************
      *
     C           INIT      BEGSR
      *
      * Parameter list
      *
     C           *ENTRY    PLIST
     C                     PARM           SEQ     5
     C                     PARM           BRCH    3
      *
      * Clear LDA
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE *BLANK    DBLOT
     C                     MOVE 'SE4120  'DBPGM
     C                     OUT  LDA
      *
      * Access DTAARA/RUNDAT
     C           *NAMVAR   DEFN RUNDAT    RUNDT
     C                     IN   RUNDT
      *
      ***  Check system date format DDMMMYY or MMDDYY.
      *
     C           DATF      IFEQ 'M'
     C                     SETON                     98
     C                     ELSE
     C                     SETOF                     98
     C                     END
      *
      ***  Set up Copyright.
      *
     C                     MOVEACPY@      BIS@   80
      *
      *  Initialise work fields
      *
     C                     MOVEL*BLANKS   @LBRCH  3
     C***********          MOVEL*BLANKS   @LDTYP 15
     C                     MOVEL*BLANKS   @LDTYP 19
     C                     MOVE 'N'       @PRTP1  1
     C                     MOVE 'N'       @PRTP2  1
     C                     MOVE 'N'       @NOREC  1                                          BUG9169
     C                     Z-ADD0         @DL     50
     C                     Z-ADD0         @BS     50
     C                     Z-ADD0         @DP     50
     C                     Z-ADD0         ECD     50                      176303
     C                     MOVE ' '       PROT    1                       176303
      *
      **  Access SDBANKPD for Bank details.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG    'PRTCD   7
     C                     PARM '*FIRST  'POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           PRTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD001       DBASE             ************
     C                     MOVE 'SDBANKPD'DBFILE            * DATABASE *
     C                     MOVEL*BLANKS   DBKEY             * ERROR 001*
     C                     SETON                     U7U8   ************
     C                     EXSR DBERR                                  *
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * DDETL - SUBROUTINE TO GET DD DETAILS  (for printer file 1)    *
      *                                                               *
      *  Called By: Main Processing.                                  *
      *  Calls    : FIRST                                             *
      *             SRDPMV                                            *
      *                                                               *
      *****************************************************************
      *
     C           DDETL     BEGSR                           ** DDETL **
      *
      ** Test whether multibranching is on
      *
     C           MBIN      IFEQ 'Y'
     C                     MOVELBRCH      BRCA
     C           BRCA      SETLLSEDLDCL0
     C                     ELSE
     C           *LOVAL    SETLLSEDLDCL0
     C                     ENDIF
      *
      ** Read REPO and Deals bond lending by branch info
      *
     C                     READ SEDLDCL0                 30
     C                     MOVE BRCA      @BRCH
      *                                                                                      BUG9169
      ** Check if no record in SEDLDCL0                                                      BUG9169
      *                                                                                      BUG9169
     C           *IN30     IFEQ *ON                                                          BUG9169
     C                     MOVE 'Y'       @NOREC                                             BUG9169
     C                     ENDIF                                                             BUG9169
      *                                                                                      BUG9169
     C           *IN30     DOWEQ*OFF
     C           BRCH      IFNE 'ALL'
     C           MBIN      ANDEQ'Y'
     C           BRCA      ANDNEBRCH
     C                     MOVE *ON       *IN30
     C                     ENDIF
      *
      *** If not end of file then set up field DTYPE
      *
     C           *IN30     IFEQ *OFF
     C                     MOVEL*BLANKS   DTYPE
      *
      *** Find out deal type from REPO ID and DTYP
      *
     C           DTYP      IFEQ 'IT'
     C           DTYP      OREQ 'CD'
     C           DTYP      OREQ 'TD'
     C           DRID      IFEQ 'R'
     C                     MOVELDTYPE1    DTYPE
     C                     ENDIF
     C           DRID      IFEQ 'B'
     C                     MOVELDTYPE3    DTYPE
     C                     ENDIF
     C           DRID      IFEQ 'P'
     C                     MOVELDTYPE5    DTYPE
     C                     ENDIF
     C                     ENDIF
     C           DTYP      IFEQ 'IP'
     C           DTYP      OREQ 'CL'
     C           DTYP      OREQ 'TI'
     C           DRID      IFEQ 'R'
     C                     MOVELDTYPE2    DTYPE
     C                     ENDIF
     C           DRID      IFEQ 'B'
     C                     MOVELDTYPE4    DTYPE
     C                     ENDIF
     C           DRID      IFEQ 'P'
     C                     MOVELDTYPE6    DTYPE
     C                     ENDIF
     C                     ENDIF
      *
     C                     EXSR FIRST
     C                     ADD  1         @DL
      *
      **  Access SDCUSTPD for Customer details.
      **  Use Customer number to chain to file
      *
     C                     MOVELCNUM      PCUST
     C                     CALL 'AOCUSTR0'
     C                     PARM '*MSG    'PRTCD
     C                     PARM '*KEY    'POPTN
     C                     PARM           PCUST  10
     C                     PARM *BLANKS   PKYST   7
     C***********SDCUST    PARM SDCUST    DSFDY                           176443
     C           SDCUST    PARM SDCUST    DSSDY                           176443
      *
     C           PRTCD     IFNE *BLANK
     C           *LOCK     IN   LDA                         ************
     C                     Z-ADD002       DBASE             ************
     C                     MOVE 'SDCUSTPD'DBFILE            * DATABASE *
     C                     MOVEL*BLANKS   DBKEY             * ERROR 002*
     C                     SETON                     U7U8   ************
     C                     EXSR DBERR                                  *
     C                     ENDIF
      *
      *** Format printer file fields for SE4120P1
      *
     C                     MOVE DLNO      DLNO1
     C                     MOVE DTYP      DTYP1
     C                     MOVE CNUM      CNUM1
     C                     MOVE BBCSSN    BCSSN1
     C                     MOVE BBCRTN    BCRTN1
      *
      * Format Dates for Print output
     C                     Z-ADDVDAT      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    VDATE1
      *
      * Format Dates for Print output
     C                     Z-ADDMDAT      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    MDATE1
      *
     C                     MOVE CCY       CCY1
      *
      ***  Format Repo Rate
     C                     MOVEL*BLANKS   ZFLD15
     C                     MOVE INTR      ZFLD15
     C                     Z-ADD7         ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    IRATE1
      *
      ***  Format Deal Amount                                             176303
      *                                                                   176303
     C                     CALL 'AOCURRR0'                                176303
     C                     PARM *BLANKS   PRTCD                           176303
     C                     PARM '*KEY   ' POPTN                           176303
     C                     PARM CCY       PCCY    3                       176303
     C           SDCURR    PARM SDCURR    DSSDY                           176303
      *                                                                   176303
      ***  Database Error if not found.                                   176303
      *                                                                   176303
     C           PRTCD     IFNE *BLANK                                    176303
     C           *LOCK     IN   LDA                                       176303
     C                     Z-ADD017       DBASE             ************  176303
     C                     MOVE 'SDCURRPD'DBFILE            * DATABASE *  176303
     C                     MOVELCCY       DBKEY             * ERROR 017*  176303
     C                     SETON                     U7U8   ************  176303
     C                     EXSR DBERR                                  *  176303
     C                     ELSE                                           176303
     C                     MOVELA6NBDP    DPLDNI  10                      176303
     C                     ENDIF                                          176303
      *                                                                   176303
     C                     MOVEL*BLANKS   ZFLD15
     C                     MOVE PCPL      ZFLD15
     C***********          Z-ADD0         ZDECS                           176303
     C                     Z-ADDDPLDNI    ZDECS                           176303
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DLAMT1
      *
      ***  Check end of page,
      *
     C                     Z-ADD7         @LND    20
     C           OFLLN1    SUB  PRTLN1    @LRM    20
     C           @LRM      IFLT @LND
     C                     WRITEHEAD1
     C                     ENDIF
      *
     C                     WRITEHEADD1
      *
      *
      ***  Check end of page,
      *
     C                     Z-ADD4         @LND    20
     C           OFLLN1    SUB  PRTLN1    @LRM    20
     C           @LRM      IFLT @LND
     C                     WRITEHEAD1
     C                     ENDIF
      *
     C                     WRITEHEADD2
      *
      *** Print all the depot movements related to the deals
      *
     C                     EXSR SRDPMV
      *
     C                     ENDIF
      *
      ** Read REPO and Deals bond lending by branch info
      *
     C                     READ SEDLDCL0                 30
      *
      *** End of do loop.
      *
     C                     ENDDO
      *
      *
     C           @PRTP1    IFEQ 'Y'
      *
      *** For multibranch print trail for end of branch.
      *
     C           MBIN      IFEQ 'Y'
     C                     Z-ADD4         @LND                            188893
     C           OFLLN1    SUB  PRTLN1    @LRM                            188893
     C           @LRM      IFLT @LND                                      188893
     C                     WRITEHEAD1                                     188893
     C                     ENDIF                                          188893
     C                     MOVE *ON       *IN32
     C                     WRITETRAIL
     C                     MOVE *OFF      *IN32
     C                     MOVELBRCA      @BRCH
     C***********          ENDIF                                          176301
      *                                                                   176301
     C                     ELSE                                           176301
      *
     C                     Z-ADD4         @LND                            188893
     C           OFLLN1    SUB  PRTLN1    @LRM                            188893
     C           @LRM      IFLT @LND                                      188893
     C                     WRITEHEAD1                                     188893
     C                     ENDIF                                          188893
     C                     MOVE *ON       *IN31
     C                     WRITETRAIL
     C                     MOVE *OFF      *IN31
     C                     MOVE 'N'       @PRTP1
     C                     MOVEL*BLANKS   @LBRCH
     C                     ENDIF                                          176301
      *
      *** Close printer file (P1)
      *
     C                     CLOSESE4120P1
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
     C*****************************************************************
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * FIRST - SUBROUTINE TO CHECK IF THIS IS THE FIRST TIME THROUGH *
      *         OR IF THERE IS A CHANGE OF BRANCH OF DEAL TYPE        *
      *                                                               *
      *  Called By: DDETL                                             *
      *  Calls    : RCFP1                                             *
      *                                                               *
      *                                                               *
      *****************************************************************
      *
     C           FIRST     BEGSR                           ** FIRST **
      *
      *** Set up spool file.
      *
     C           BRCA      IFNE @LBRCH
      *
     C           @PRTP1    IFEQ 'Y'
     C                     MOVE *ON       *IN32
     C                     WRITETRAIL
     C                     MOVE *OFF      *IN32
     C                     MOVELBRCA      @BRCH
      *
      *** Close printer spool file P1
      *
     C                     CLOSESE4120P1
     C                     MOVE 'N'       @PRTP1
     C                     ENDIF
      *
      ***  Open report file, reset page no.
      *
     C           @PRTP1    IFEQ 'N'
     C           @DL       IFEQ 0
     C                     Z-ADD*ZEROS    PAGE
     C                     ENDIF
     C                     OPEN SE4120P1
     C                     EXSR RCFP1
      *
      *** Get branch details
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM '*MSG    'PRTCD   7
     C                     PARM '*FIRST  'POPTN   7
     C                     PARM BRCH      PBRCD   3
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
      *
     C           PRTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD003       DBASE             ************
     C                     MOVE 'SDBRCHPD'DBFILE            * DATABASE *
     C                     MOVEL*BLANKS   DBKEY             * ERROR 003*
     C                     SETON                     U7U8   ************
     C                     EXSR DBERR                                  *
     C                     ENDIF
      *
      ***  Access Currency file for Currency Details
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM CCY       PCCY    3
     C           SDCURR    PARM SDCURR    DSSDY
      *
      ***  Database Error if not found.
      *
     C           PRTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD011       DBASE             ************
     C                     MOVE 'SDCURRPD'DBFILE            * DATABASE *
     C                     MOVEL*BLANKS   DBKEY             * ERROR 011*
     C                     SETON                     U7U8   ************
     C                     EXSR DBERR                                  *
     C                     ELSE                                           176303
     C                     MOVELA6NBDP    DPLDNI  10                      176303
     C                     ENDIF
      *
      *** Write spool file header
      *
     C                     WRITEHEAD1
     C                     MOVE 'Y'       @PRTP1
     C                     MOVELBRCA      @LBRCH
     C                     MOVEL*BLANKS   @LDTYP
     C                     MOVELDTYPE     @LDTYP
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
      *** If deal types are not equal then there is a new deal so
      *** write new header
      *
     C           DTYPE     IFNE @LDTYP
     C                     WRITEHEAD1
     C                     MOVE *BLANKS   @LDTYP
     C                     MOVE DTYPE     @LDTYP
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * SRDPMV - PRINTS ALL DEPOT MOVEMENTS RELATED TO THE DEALS      *
      *                                                               *
      *  Called By: DDETL          .                                  *
      *  Calls    : MPRIC                                             *
      *                                                               *
      *                                                               *
      *****************************************************************
      *
     C           SRDPMV    BEGSR                           ** SRDPMV **
      *
     C           DLNO      SETLLSEDPMVL3
      *
     C                     Z-ADD0         @TVAL1 200
     C                     Z-ADD0         @TMVA1 200
      *
      *** While not end of file read depot movements by loan number fil
      *
     C           DLNO      READESEDPMVL3                 33
      *
     C           *IN33     DOWEQ*OFF
      *
      *** Get security, intestment details from SECTY and INVTP
      *
     C           DPSS      CHAINSECTY                34
      *
     C           *IN34     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD004       DBASE             ************
     C                     MOVE 'SECTY'   DBFILE            * DATABASE *
     C                     MOVEL*BLANKS   DBKEY             * ERROR 004*
     C                     SETON                     U7U8   ************
     C                     EXSR DBERR                                  *
     C                     ENDIF
      *
      *** Save relevant work details to printer file (P1)
      *
     C                     MOVELSRPN      SRPN1
     C                     MOVELSIOI      SIOI1
      *
     C           SIOI      IFEQ 'Y'
     C           SIOI      OREQ *BLANK
      *
      * Format Dates for Print output
     C                     Z-ADDVDAT      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZADATE    SDATE1
     C                     ELSE
      *
      * Format Dates for Print output
     C                     Z-ADDMDAT      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZADATE    SDATE1
     C                     ENDIF
      *
      * Format Nominal Currency of the security                           176403
     C                     MOVE NMCY      SCCY1                           176403
      *                                                                   176403
      ****Find1if*there*is*a*deal*price*on*LF/PRICE.**If*not,*use*the*    176303
      ****price*on*SECTYD.****                                            176303
      ***********                                                         176303
     C***********SESN      CHAINPRICE                38                   176303
      ***********                                                         176303
     C************IN38     IFEQ *OFF                                      176303
     C***********          MOVE PPRC      ZFLD15                          176303
     C***********          ELSE                                           176303
     C***********          MOVE MKPR      ZFLD15                          176303
     C***********          ENDIF                                          176303
      ***********                                                         176303
      ***  Format Price
      *
     C                     MOVEL*BLANKS   ZFLD15                          176303
     C                     MOVE MKTP      ZFLD15                          176303
      *                                                                   176303
     C                     Z-ADD8         ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DPRIC1
      *
      ***  Format Exchange Rate
      *
     C                     MOVEL*BLANKS   ZFLD15
     C                     MOVE DEXRT     ZFLD15
     C                     Z-ADD8         ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    XRATE1
      *
      ***  Get and format Nominal from SEDLDCL0
      *
     C                     MOVEL*BLANKS   ZFLD15
     C                     MOVE DPNA      ZFLD15
     C***********          Z-ADD0         ZDECS                           176303
     C                     Z-ADDNMDP      ZDECS                           176303
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    PNOML1
      *
      *** Get decimal places of currency of the Security                  176303
      *                                                                   176303
     C                     EXSR GETCUR                                    176303
      *
      ****Calculate*depot*movement*value******************************    176303
      *
      *** Calculate depot movement value, convert Price to % format       176303
      *** if necessary (N.B. ZPRCIN set up above; ZPRCOT must be          176303
      *** multiplied by 1 because it is 15,8 and ZPRC is 16,9 and a       176303
      *** MOVE will not do this).                                         176303
      *                                                                   176303
     C                     MOVE MKTP      ZPRCIN                          176303
     C                     Z-ADDVDAT      ZFDATE                          176303
     C                     EXSR ZPRCI                                     176303
     C           ZPRCOT    MULT 1         ZPRC                            176303
      *                                                                   176303
     C***********          Z-ADD0         ZPRC                            176303
     C                     Z-ADD0         ZNOML
     C                     MOVE DPNA      ZNOML
     C***********          Z-ADDPPRC      ZPRC                            176303
     C***********          Z-ADD0         ZCDP                            176303
     C                     Z-ADDDPSECY    ZCDP                            176303
     C                     EXSR ZCNSD
     C***********          MOVE ZCONS     DVAL1                           176303
      ***********                                                         176303
     C***********          MOVE DVAL1     WDVAL1 200                      176303
      *
      *** Convert this amount to the currency of the MM deal              176303
     C           DEXRT     IFLT 1                                         176303
     C           DEXRT     ANDNE*ZEROS                                    176303
     C           1         DIV  DEXRT     ZCRSRT                          176303
     C                     ELSE                                           176303
     C                     Z-ADDDEXRT     ZCRSRT                          176303
     C                     ENDIF                                          176303
     C                     Z-ADDZCONS     ZAMTF                           176303
     C                     Z-ADDDPSECY    ZCDPF                           176303
     C                     Z-ADDDPLDNI    ZCDPT                           176303
     C                     MOVE 'M'       ZCRSMD                          176303
     C                     EXSR ZCCYXR                                    176303
     C                     MOVE ZAMTT     WDVAL1 200                      176303
      *
      ***  Format Depot movement Value
      *
     C                     MOVEL*BLANKS   ZFLD15
     C***********          MOVE DVAL1     ZFLD15                          176303
     C***********          Z-ADD0         ZDECS                           176303
     C                     MOVE ZAMTT     ZFLD15                          176303
     C                     Z-ADDDPLDNI    ZDECS                           176303
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DVAL1
      *
      ***  Get market price
      *
     C                     EXSR MPRIC
      *
      ****Calculate*Market*Value**********************************        176303
      *** Calculate Market Value (convert Price to % format first)        176303
      *
     C           *IN40     IFEQ *ON
      *                                                                   176303
     C                     MOVE PPRC      ZPRCIN                          176303
     C                     Z-ADDVDAT      ZFDATE                          176303
     C                     EXSR ZPRCI                                     176303
     C           ZPRCOT    MULT 1         ZPRC                            176303
      *                                                                   176303
     C***********          Z-ADD0         ZPRC                            176303
     C                     Z-ADD0         ZNOML
     C                     MOVE DPNA      ZNOML
     C***********          Z-ADDMKTP      ZPRC                            176303
     C***********          Z-ADD0         ZCDP                            176303
     C                     Z-ADDDPSECY    ZCDP                            176303
     C                     EXSR ZCNSD
     C***********          MOVE ZCONS     MVAL1                           176303
     C                     MOVE *OFF      *IN40
     C                     ENDIF
      *
     C           *IN41     IFEQ *ON
      *                                                                   176303
     C                     MOVE MKPR      ZPRCIN                          176303
     C                     Z-ADDVDAT      ZFDATE                          176303
     C                     EXSR ZPRCI                                     176303
     C           ZPRCOT    MULT 1         ZPRC                            176303
      *                                                                   176303
     C***********          Z-ADD0         ZPRC                            176303
     C                     Z-ADD0         ZNOML
     C                     MOVE DPNA      ZNOML
     C***********          Z-ADDMKPR      ZPRC                            176303
     C***********          Z-ADD0         ZCDP                            176303
     C                     Z-ADDDPSECY    ZCDP                            176303
     C                     EXSR ZCNSD
     C***********          MOVE ZCONS     MVAL1                           176303
     C                     MOVE *OFF      *IN41
     C                     ENDIF
      *
      *** Convert this amount to the currency of the MM deal              176303
     C***********          MOVE MVAL1     WMVAL1 200                      176303
     C                     Z-ADDZCONS     ZAMTF                           176303
     C                     EXSR ZCCYXR                                    176303
     C                     MOVE ZAMTT     WMVAL1 200                      176303
      *                                                                   176303
      ***  Format Market Value
      *
     C                     MOVEL*BLANKS   ZFLD15
     C***********          MOVE MVAL1     ZFLD15                          176303
     C***********          Z-ADD0         ZDECS                           176303
     C                     MOVE ZAMTT     ZFLD15                          176303
     C                     Z-ADDDPLDNI    ZDECS                           176303
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    MVAL1
      *
      *** Add Dpot movement to total value and market value to total
      *
     C                     ADD  WDVAL1    @TVAL1
     C                     ADD  WMVAL1    @TMVA1
      *
      ***  Check end of page
      *
     C                     Z-ADD3         @LND    20
     C***********          Z-ADD2         @LND    20
     C           OFLLN1    SUB  PRTLN1    @LRM    20
     C           @LRM      IFLT @LND
     C                     WRITEHEAD1
     C                     WRITEHEADD2
     C                     ENDIF
      *
     C                     WRITEDPMVL1
     C                     WRITEDPMVL2
      *
      *** Initialise printer fields
      *
     C                     MOVEL*BLANKS   SRPN1
     C                     MOVEL*BLANKS   SIOI1
     C                     MOVEL*BLANKS   PNOML1
     C                     MOVEL*BLANKS   DPRIC1
     C                     MOVEL*BLANKS   XRATE1
     C                     MOVE *BLANKS   DVAL1
     C                     MOVE *BLANKS   MPRIC1
     C                     MOVE *BLANKS   MVAL1
      *
     C                     ADD  1         @DP
      *
     C           DLNO      READESEDPMVL3                 33
      *
     C                     ENDDO
      *
      ***  Format total fields
      *
     C                     MOVEL*BLANKS   ZFLD15
     C                     Z-ADD@TVAL1    ZFLD15
     C***********          Z-ADD0         ZDECS                           176303
     C                     Z-ADDDPLDNI    ZDECS                           176303
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    TVAL1
      *
     C                     MOVEL*BLANKS   ZFLD15
     C                     Z-ADD@TMVA1    ZFLD15
     C***********          Z-ADD0         ZDECS                           176303
     C                     Z-ADDDPLDNI    ZDECS                           176303
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    TMVAL1
      *
      ***  Check end of page
      *
     C                     Z-ADD5         @LND    20                      176303
     C***********          Z-ADD4         @LND    20                      176303
     C           OFLLN1    SUB  PRTLN1    @LRM    20
     C           @LRM      IFLT @LND
     C                     WRITEHEAD1
     C                     ENDIF
      *
     C                     WRITEDPTOT
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * MPRIC  - CALCULATES MARKET PRICE                              *
      *                                                               *
      *  Called By: SRDPMV         .                                  *
      *  Calls    :                                                   *
      *                                                               *
      *                                                               *
      *****************************************************************
      *
     C           MPRIC     BEGSR                           ** MPRIC **
      *
      *** Find if there is a deal price on LF/PRICE.  If not, use the     176303
      *** price on SECTYD.                                                176303
      *                                                                   176303
     C           SESN      CHAINPRICE                38                   176303
      *                                                                   176303
     C           PPRT      IFEQ 'V'
     C           RECI      ANDEQ'D'
     C***********SDATE1    ANDLERUNDT                                     176303
     C           VDAT      ANDLEBJRDNB                                    176303
     C           *IN38     ANDEQ*OFF                                      176303
      *
      ***  Format Market Price
      *
     C                     MOVE *ON       *IN40
     C                     MOVEL*BLANKS   ZFLD15
     C***********          MOVE MKTP      ZFLD15                          176303
     C                     MOVE PPRC      ZFLD15                          176303
     C                     Z-ADD8         ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    MPRIC1
      *
     C                     ELSE
      *
     C                     MOVE *ON       *IN41
     C                     MOVEL*BLANKS   ZFLD15
     C                     MOVE MKPR      ZFLD15
     C                     Z-ADD8         ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    MPRIC1
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      *****************************************************************
      /EJECT
      *****************************************************************   176303
      *                                                               *   176303
      * GETCUR - GETS CURRENCY DETAILS FROM PF/SDCURRPD AND SET UP    *   176303
      *          ENTRY FIELDS FOR LATER CALL OF SR/ZCCYN              *   176303
      *                                                               *   176303
      *  Called By: SRDPMV                                            *   176303
      *  Calls    : AOCURRR0                                          *   176303
      *                                                               *   176303
      *****************************************************************   176303
      *                                                                   176303
     C           GETCUR    BEGSR                                          176303
      *                                                                   176303
      ***  Access Currency file for Security's Nominal Currency           176303
      *                                                                   176303
     C                     CALL 'AOCURRR0'                                176303
     C                     PARM *BLANKS   PRTCD                           176303
     C                     PARM '*KEY   ' POPTN                           176303
     C                     PARM           NMCY                            176303
     C           SDCURR    PARM SDCURR    DSSDY                           176303
      *                                                                   176303
      ***  Database Error if not found.                                   176303
      *                                                                   176303
     C           PRTCD     IFNE *BLANK                                    176303
     C           *LOCK     IN   LDA                                       176303
     C                     Z-ADD016       DBASE             ************  176303
     C                     MOVE 'SDCURRPD'DBFILE            * DATABASE *  176303
     C                     MOVELNMCY      DBKEY             * ERROR 016*  176303
     C                     SETON                     U7U8   ************  176303
     C                     EXSR DBERR                                  *  176303
     C                     ELSE                                           176303
     C                     MOVELA6NBDP    DPSECY  10                      176303
     C                     ENDIF                                          176303
      *                                                                   176303
     C                     ENDSR                                          176303
      *                                                                   176303
      *****************************************************************   176303
      /EJECT                                                              176303
      *****************************************************************
      *                                                               *
      * BSDTL  - GETS ALL BUY-SELL BACK POSTION DETAILS               *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls    : BSFST                                             *
      *             BSMPRI                                            *
      *                                                               *
      *****************************************************************
      *
     C           BSDTL     BEGSR                           ** BSDTL **
      *
      ** Test whether multibranching is on
      *
     C           MBIN      IFEQ 'Y'
     C                     MOVELBRCH      BSBCH
     C           BSBCH     SETLLBSPOSDL1
     C                     ELSE
     C           *LOVAL    SETLLBSPOSDL1
     C                     ENDIF
      *
      ** Reset 'Last branch' flag                                         176303
     C                     MOVE *BLANKS   @LBRCH                          176303
      *                                                                   176303
      *** While not end of file get Buy-Sell back details
      *
     C                     READ BSPOSDL1                 36
     C                     MOVE BSBCH     @BRCH
     C           *IN36     DOWEQ*OFF
      *
     C           BRCH      IFNE 'ALL'
     C           MBIN      ANDEQ'Y'
     C           BSBCH     ANDNEBRCH
     C                     MOVE *ON       *IN36
     C                     ENDIF
      *
     C           *IN36     IFEQ *OFF
     C           BSRECI    ANDEQ'D'
      *
      *** Check to see if this is first time through SR
      *
     C                     EXSR BSFST
      *
     C                     ADD  1         @BS
      *
      *** Get trade detail using trade reference on file
      *
     C           BSPRF     CHAINTRADS                37
      *
     C           *IN37     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD014       DBASE             ************
     C                     MOVE 'TRADS   'DBFILE            * DATABASE *
     C                     MOVELBSPRF     DBKEY             * ERROR 014*
     C                     SETON                     U7U8   ************
     C                     EXSR DBERR
     C                     ENDIF
      *
      *** Save Buy-Sell date to printer file (P2)
      *** Format Dates for Print output
     C*******              Z-ADDTDDT      ZDAYNO
     C                     Z-ADDTDVD      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZADATE    WBSDA1  7
      *
     C                     Z-ADDTDVD      WTDVD1  50                      176303
     C                     MOVELTDTP      WTTYP1  2
     C                     MOVELTDRF      WTDRF1  6
     C                     MOVELTCNR      WTCNR1  6
     C                     MOVELTDSS      WSRPN1 10
     C                     MOVELSETC      WSETC1  3
     C                     MOVE NOML      WNOML1 11
     C                     Z-ADDPRIC      WPTPR1 158
     C                     MOVELLKRF      WLKRF   6
     C                     MOVE ITRA      WPINT1 15
     C                     MOVE TDER      WPEXR1 13
      *
      ***  Access Currency file for Currency Details
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM TNMC      PCCY    3
     C           SDCURR    PARM SDCURR    DSSDY
      *
      ***  Database Error if not found.
      *
     C           PRTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD013       DBASE             ************
     C                     MOVE 'SDCURRPD'DBFILE            * DATABASE *
     C                     MOVEL*BLANKS   DBKEY             * ERROR 013*
     C                     SETON                     U7U8   ************
     C                     EXSR DBERR                                  *
     C                     ENDIF
      *
      *** Save currency decimal places to work field
      *
     C                     Z-ADDA6NBDP    WNBDP1  10
      *
      *** Find out trade type from TRADS file and TDTP
      *
     C                     SELEC
     C           WTTYP1    WHEQ 'TP'
     C                     MOVELTTYP21    TTYP2
     C           WTTYP1    WHEQ 'TS'
     C                     MOVELTTYP22    TTYP2
     C                     ENDSL
      *
      *** Get second trade detail using link reference on file
      *
     C           LKRF      CHAINTRADS                37
      *
     C           *IN37     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD015       DBASE             ************
     C                     MOVE 'TRADS   'DBFILE            * DATABASE *
     C                     MOVELLKRF      DBKEY             * ERROR 015*
     C                     SETON                     U7U8   ************
     C                     EXSR DBERR
     C                     ENDIF
      *
      *** Save Buy-Sell date to printer file (P2)
      *** Format Dates for Print output
     C***********          Z-ADDTDDT      ZDAYNO
     C                     Z-ADDTDVD      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZADATE    WBSDA2  7
      *
     C                     Z-ADDTDVD      WTDVD2  50                      176303
     C                     MOVELTDTP      WTTYP2  2
     C                     MOVELTDRF      WTDRF2  6
     C                     MOVELTCNR      WTCNR2  6
     C                     MOVELTDSS      WSRPN2 10
     C                     MOVELSETC      WSETC2  3
     C                     MOVE NOML      WNOML2 11
     C                     Z-ADDPRIC      WPTPR2 158
     C                     MOVE ITRA      WPINT2 15
     C                     MOVE TDER      WPEXR2 13
      *
      ***  Access Currency file for Currency Details
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM TNMC      PCCY    3
     C           SDCURR    PARM SDCURR    DSSDY
      *
      ***  Database Error if not found.
      *
     C           PRTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD013       DBASE             ************
     C                     MOVE 'SDCURRPD'DBFILE            * DATABASE *
     C                     MOVEL*BLANKS   DBKEY             * ERROR 013*
     C                     SETON                     U7U8   ************
     C                     EXSR DBERR                                  *
     C                     ENDIF
      *
      *** Save currency decimal places to work field
      *
     C                     Z-ADDA6NBDP    WNBDP2  10
      *
      **  Access SDCUSTPD for Customer details.
      *
     C                     MOVELTCNR      PCUST
     C                     CALL 'AOCUSTR0'
     C                     PARM '*MSG    'PRTCD
     C                     PARM '*KEY    'POPTN
     C                     PARM           PCUST  10
     C                     PARM *BLANKS   PKYST   7
     C***********SDCUST    PARM SDCUST    DSFDY                           176443
     C           SDCUST    PARM SDCUST    DSSDY                           176443
      *
     C           PRTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD007       DBASE             ************
     C                     MOVE 'SDCUSTPD'DBFILE            * DATABASE *
     C                     MOVEL*BLANKS   DBKEY             * ERROR 007*
     C                     SETON                     U7U8   ************
     C                     EXSR DBERR                                  *
     C                     ENDIF
      *
     C                     MOVELBBCSSN    BCSSN2
     C                     MOVELBBCRTN    BCRTN2
      *
      *** Get security, investment details from SECTY
      *
     C           BSSEN     CHAINSECTY                34
      *
     C           *IN34     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD008       DBASE             ************
     C                     MOVE 'SECTY'   DBFILE            * DATABASE *
     C                     MOVEL*BLANKS   DBKEY             * ERROR 008*
     C                     SETON                     U7U8   ************
     C                     EXSR DBERR                                  *
     C                     ENDIF
      *
     C                     MOVELSRPN      SRPN2
      *
      *** Calculate value for 1st trade
      *
     C                     Z-ADD0         ZPRC
     C                     Z-ADD0         ZNOML
     C                     MOVE WNOML1    ZNOML
     C                     Z-ADDWPTPR1    ZPRC
     C***********          Z-ADD0         ZCDP                            176303
     C                     Z-ADDWNBDP1    ZCDP                            176303
     C                     EXSR ZCNSD
     C                     MOVE ZCONS     WBSVA1 20
      *
      ***  Format value for 1st trade
      *
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE WBSVA1    ZFLD15
     C***********          Z-ADD0         ZDECS                           176303
     C                     Z-ADDWNBDP1    ZDECS                           176303
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    WBSVA1
      *
      *** Calculate value for 2nd trade
      *
     C                     Z-ADD0         ZPRC
     C                     Z-ADD0         ZNOML
     C                     MOVE WNOML2    ZNOML
     C                     Z-ADDWPTPR2    ZPRC
     C***********          Z-ADD0         ZCDP                            176303
     C                     Z-ADDWNBDP2    ZCDP                            176303
     C                     EXSR ZCNSD
     C                     MOVE ZCONS     WBSVA2 20
      *
      ***  Format value for 2nd trade
      *
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE WBSVA2    ZFLD15
     C***********          Z-ADD0         ZDECS                           176303
     C                     Z-ADDWNBDP2    ZDECS                           176303
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    WBSVA2
      *
      *** Get market price for 1st trade
      *
     C                     EXSR BSMPR1
      *
      *** Get market price for 2nd trade
      *
     C                     EXSR BSMPR2
      *
      *** Calculate Market Value 1st trade
      *
     C           *IN43     IFEQ *ON
     C                     Z-ADD0         ZPRC
     C                     Z-ADD0         ZNOML
     C                     MOVE WNOML1    ZNOML
     C                     Z-ADDMKTP      ZPRC
     C                     Z-ADD0         ZCDP
     C                     EXSR ZCNSD
     C                     MOVE ZCONS     WPMVA1 20
     C                     MOVE *OFF      *IN43
     C                     ENDIF
      *
     C           *IN44     IFEQ *ON
     C                     Z-ADD0         ZPRC
     C                     Z-ADD0         ZNOML
     C                     MOVE WNOML1    ZNOML
     C                     Z-ADDMKPR      ZPRC
     C                     Z-ADD0         ZCDP
     C                     EXSR ZCNSD
     C                     MOVE ZCONS     WPMVA1 20
     C                     MOVE *OFF      *IN44
     C                     ENDIF
      *
      ***  Format Market Value
      *
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE WPMVA1    ZFLD15
     C                     Z-ADD0         ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    WPMVA1
      *
      *** Calculate Market Value 2nd trade
      *
     C           *IN45     IFEQ *ON
     C                     Z-ADD0         ZPRC
     C                     Z-ADD0         ZNOML
     C                     MOVE WNOML2    ZNOML
     C                     Z-ADDMKTP      ZPRC
     C                     Z-ADD0         ZCDP
     C                     EXSR ZCNSD
     C                     MOVE ZCONS     WPMVA2
     C                     MOVE *OFF      *IN45
     C                     ENDIF
      *
     C           *IN46     IFEQ *ON
     C                     Z-ADD0         ZPRC
     C                     Z-ADD0         ZNOML
     C                     MOVE WNOML2    ZNOML
     C                     Z-ADDMKPR      ZPRC
     C                     Z-ADD0         ZCDP
     C                     EXSR ZCNSD
     C                     MOVE ZCONS     WPMVA2
     C                     MOVE *OFF      *IN46
     C                     ENDIF
      *
      ***  Format Market Value
      *
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE WPMVA2    ZFLD15
     C                     Z-ADD0         ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    WPMVA2 20
      *
      ***  Check end of page
      *
     C                     Z-ADD13        @LND    20
     C           OFLLN2    SUB  PRTLN2    @LRM    20
     C           @LRM      IFLT @LND
     C                     WRITEHEAD2
      *
     C                     ENDIF
      *
      *** Move work fields for 1st trade into printer fields
      *
     C                     MOVELWTCNR1    TCNR2
      *
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE WNOML1    ZFLD15
     C                     Z-ADD0         ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    TNOML2
      *
     C                     MOVELWTDRF1    TDRF2
     C                     MOVELWBSDA1    BSDAT2
     C                     MOVELWSETC1    SETC2
      *
     C                     MOVEL*BLANKS   ZFLD15
     C                     MOVE WPINT1    ZFLD15
     C                     Z-ADDWNBDP1    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    PINTR2
      *
     C                     MOVEL*BLANKS   ZFLD15
     C                     MOVE WPTPR1    ZFLD15
     C                     Z-ADD8         ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    PTPRI2
      *
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE WPEXR1    ZFLD15
     C                     Z-ADD8         ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    PEXR2
      *
     C                     MOVE WBSVA1    BSVAL2
      *
     C                     MOVE WPMPR1    PMPRI2
      *
     C                     MOVE WPMVA1    PMVAL2
      *
     C                     WRITEHEADD3
     C                     WRITEHEADD4
      *
     C                     WRITEBSLINE
      *
      *** Set market price and market val to blank
      *
     C                     MOVEL*BLANKS   PMPRI2
     C                     MOVEL*BLANKS   PMVAL2
      *
      *** Move work fields for 2nd trade into printer fields
      *
     C                     MOVELWTCNR2    TCNR2
      *
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE WNOML2    ZFLD15
     C                     Z-ADD0         ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    TNOML2
      *
     C                     MOVELWTDRF2    TDRF2
     C                     MOVELWBSDA2    BSDAT2
     C                     MOVELWSETC2    SETC2
      *
     C                     MOVEL*BLANKS   ZFLD15
     C                     MOVE WPINT2    ZFLD15
     C                     Z-ADDWNBDP2    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    PINTR2
      *
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE WPTPR2    ZFLD15
     C                     Z-ADD8         ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    PTPRI2
      *
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE WPEXR2    ZFLD15
     C                     Z-ADD8         ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    PEXR2
      *
     C                     MOVE WBSVA2    BSVAL2
      *
     C                     MOVE WPMPR2    PMPRI2
      *
     C                     MOVE WPMVA2    PMVAL2
     C                     WRITEBSLINE
      *
      *** Intialise work fields
      *
     C                     MOVEL*BLANKS   TTYP2
     C                     MOVEL*BLANKS   WTTYP1
     C                     MOVEL*BLANKS   WTDRF1
     C                     MOVEL*BLANKS   WTCNR1
     C                     MOVEL*BLANKS   WSRPN1
     C                     MOVEL*BLANKS   WSETC1
     C                     MOVE *BLANKS   WNOML1
     C                     MOVEL*BLANKS   WLKRF
     C                     MOVE *BLANKS   WPINT1
     C                     MOVE *BLANKS   WPEXR1
     C                     MOVE *BLANKS   WBSVA1
     C                     MOVE *BLANKS   WPMVA1
     C                     MOVE *BLANKS   WPMPR1
      *
     C                     MOVEL*BLANKS   WTTYP2
     C                     MOVEL*BLANKS   WTDRF2
     C                     MOVEL*BLANKS   WTCNR2
     C                     MOVEL*BLANKS   WSRPN2
     C                     MOVEL*BLANKS   WSETC2
     C                     MOVE *BLANKS   WNOML2
     C                     MOVE *BLANKS   WPINT2
     C                     MOVE *BLANKS   WPEXR2
     C                     MOVE *BLANKS   WBSVA2
     C                     MOVE *BLANKS   WPMVA2
     C                     MOVE *BLANKS   WPMPR2
      *
      *** Initialise printer fields
      *
     C                     MOVEL*BLANKS   TTYP2
     C                     MOVEL*BLANKS   TDRF2
     C                     MOVEL*BLANKS   TCNR2
     C                     MOVEL*BLANKS   SRPN2
     C                     MOVEL*BLANKS   SETC2
     C                     MOVE *BLANKS   TNOML2
     C                     MOVE *BLANKS   PINTR2
     C                     MOVE *BLANKS   PEXR2
     C                     MOVE *BLANKS   BSVAL2
     C                     MOVE *BLANKS   PMVAL2
     C                     MOVE *BLANKS   PMPRI2
      *
     C                     ENDIF
      *
      *
     C                     READ BSPOSDL1                 36
      *
     C                     ENDDO
      *
      *** Check for multibranch and print trailer
      *
     C           @PRTP2    IFEQ 'Y'
      *
     C           MBIN      IFEQ 'Y'
     C                     MOVE *ON       *IN32
     C                     WRITETRAIL2
     C                     MOVE *OFF      *IN32
     C                     MOVELBSBCH     @BRCH
     C***********          ENDIF                                          176301
      *                                                                   176301
     C                     ELSE                                           176301
      *
     C                     MOVE *ON       *IN31
     C                     WRITETRAIL2
     C                     MOVE *OFF      *IN31
     C                     MOVE 'N'       @PRTP2
     C***********          CLOSESE4120P1                                  176303
     C                     ENDIF                                          176303
      *                                                                   176303
     C                     CLOSESE4120P2                                  176303
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
      *
      *****************************************************************
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * BSMPR1 - CALCULATES MARKET PRICE                              *
      *                                                               *
      *  Called By: BSDTL                                             *
      *  Calls    : None                                              *
      *                                                               *
      *                                                               *
      *****************************************************************
      *
     C           BSMPR1    BEGSR                           ** BSMPR1 **
      *
      *** Find if there is a deal price on LF/PRICE.  If not, use the     176303
      *** price on SECTYD.                                                176303
      *                                                                   176303
     C           SESN      CHAINPRICE                38                   176303
      *                                                                   176303
     C           RECI      IFEQ 'D'
     C***********BSDAT2    ANDLTRUNDT                                     176303
     C           WTDVD1    ANDLTBJRDNB                                    176303
     C           PPRT      ANDEQ'V'
     C           *IN38     ANDEQ*OFF                                      176303
      *
      ***  Format Market Price
      *
     C                     MOVE *ON       *IN43
     C                     MOVEL*BLANKS   ZFLD15
     C                     MOVE PPRC      ZFLD15                          176303
     C************         MOVE MKTP      ZFLD15                          176303
     C                     Z-ADD8         ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    WPMPR1 15
     C                     ELSE
      *
      ***  Format Market Price
      *
     C                     MOVE *ON       *IN44
     C                     MOVEL*BLANKS   ZFLD15
     C                     MOVE MKPR      ZFLD15
     C                     Z-ADD8         ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    WPMPR1
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * BSMPR2 - CALCULATES MARKET PRICE FOR 2ND TRADE                *
      *                                                               *
      *  Called By: BSDTL          .                                  *
      *  Calls    : None                                              *
      *                                                               *
      *                                                               *
      *****************************************************************
      *
     C           BSMPR2    BEGSR                           ** BSMPR2 **
      *
      *** Find if there is a deal price on LF/PRICE.  If not, use the     176303
      *** price on SECTYD.                                                176303
      *                                                                   176303
     C           SESN      CHAINPRICE                38                   176303
      *                                                                   176303
     C           RECI      IFEQ 'D'                                       176303
     C***********BSDAT2    ANDLTRUNDT                                     176303
     C           WTDVD2    ANDLTBJRDNB                                    176303
     C           PPRT      ANDEQ'V'
     C           *IN38     ANDEQ*OFF                                      176303
      *
      ***  Format Market Price
      *
     C                     MOVE *ON       *IN45
     C                     MOVEL*BLANKS   ZFLD15
     C                     MOVE MKTP      ZFLD15
     C                     Z-ADD8         ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    WPMPR2 15
     C                     ELSE
      *
      ***  Format Market Price
      *
     C                     MOVE *ON       *IN46
     C                     MOVEL*BLANKS   ZFLD15
     C                     MOVE MKPR      ZFLD15
     C                     Z-ADD8         ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    WPMPR2
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * BSFST - SUBROUTINE TO CHECK IF THIS IS THE FIRST TIME THROUGH *
      *         OR IF THERE IS A CHANGE OF BRANCH TO SET UP SPOOL FILE*
      *                                                               *
      *  Called By: BSDTL                                             *
      *  Calls    : RCFP2                                             *
      *                                                               *
      *                                                               *
      *****************************************************************
      *
     C           BSFST     BEGSR                           ** BSFST **
      *
      *** Set up spool file.
      *
     C           BSBCH     IFNE @LBRCH
      *
     C           @PRTP2    IFEQ 'Y'
     C                     MOVE *ON       *IN32
     C                     WRITETRAIL2
     C                     MOVE *OFF      *IN32
     C                     MOVELBSBCH     @BRCH
      *
      ****Close*printer*spool*file*P1**********************************   176303
      *** Close printer spool file P2                                     176303
      *
     C                     CLOSESE4120P2
     C                     MOVE 'N'       @PRTP2
     C                     ENDIF
      *
      ***  Open report file, reset page no.
      *
     C           @PRTP2    IFEQ 'N'
     C           @BS       IFEQ 0
     C                     Z-ADD*ZEROS    PAGE
     C                     ENDIF
     C                     OPEN SE4120P2
     C                     EXSR RCFP2
      *
      *** Get branch details
      *** TO CHECK IF CHANGE OF BRANCH SO DETAILS CAN BE SET UP
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM '*MSG    'PRTCD   7
     C                     PARM '*FIRST  'POPTN   7
     C                     PARM BSBCH     PBRCD
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
      *
     C           PRTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD006       DBASE             ************
     C                     MOVE 'SDBRCHPD'DBFILE            * DATABASE *
     C                     MOVEL*BLANKS   DBKEY             * ERROR 006*
     C                     SETON                     U7U8   ************
     C                     EXSR DBERR                                  *
     C                     ENDIF
      *
      ***  Access Currency file for Currency Details
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM CCY       PCCY    3
     C           SDCURR    PARM SDCURR    DSSDY
      *
      ***  Database Error if not found.
      *
     C           PRTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD012       DBASE             ************
     C                     MOVE 'SDCURRPD'DBFILE            * DATABASE *
     C                     MOVEL*BLANKS   DBKEY             * ERROR 012*
     C                     SETON                     U7U8   ************
     C                     EXSR DBERR                                  *
     C                     ENDIF
      *
      *** Write spool file header
      *
     C                     WRITEHEAD2
     C                     MOVE 'Y'       @PRTP2
     C                     MOVELBSBCH     @LBRCH
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * RCFP1 - SETS UP SPOOL FILE INFORMATION AND CALLS ZSFILE       *
      *                                                               *
      *  Called By: FIRST                                             *
      *  Calls    : None.                                             *
      *                                                               *
      *                                                               *
      *****************************************************************
      *
     C           RCFP1     BEGSR                           ** RCFP1 **
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM1  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           SEQ     5
     C                     PARM BRCH      SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM1
     C                     PARM *BLANKS   ZSERR   1
      *
     C           ZSERR     IFEQ 'Y'
      *
      ***  Error during ZSFILE Processing, Return to Calling Program.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     END
      *
     C                     ENDSR
      *
      *****************************************************************
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * RCFP2 - SETS UP SPOOL FILE INFORMATION AND CALLS ZSFILE       *
      *                                                               *
      *  Called By: Main Processing.                                  *
      *  Calls    : None.                                             *
      *                                                               *
      *                                                               *
      *****************************************************************
      *
     C           RCFP2     BEGSR                           ** RCFP2 **
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM2    ZSNUM2  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           SEQ     5
     C                     PARM BRCH      SENTY   3
     C                     PARM           SFILE2
     C                     PARM           ZSNUM2
     C                     PARM *BLANKS   ZSERR   1
      *
     C           ZSERR     IFEQ 'Y'
      *
      ***  Error during ZSFILE Processing, Return to Calling Program.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     END
      *
     C                     ENDSR
      *
      *****************************************************************
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  AUDIT  - SUBROUTINE TO OUTPUT AUDIT REPORT ONLY WHEN A DB    *
      *           ERROR HAS OCCURRED IN THE PROCESSING OF A RECORD    *
      *                                                               *
      *****************************************************************
      *
     C           AUDIT     BEGSR
      *
      ***  Open SE4120AU and EXSR RCFAU
      ***  Open report file, reset page no.
      *
     C                     Z-ADD*ZEROS    PAGE
     C                     OPEN SE4120AU
      *
      *** RCF Processing for Audit File
      *
     C                     EXSR RCFAU
      *
     C                     WRITEHEADAU
      *
      *** If DB error then write ERROR
      *
     C           *INU7     IFNE '1'
     C           *INU8     ANDNE'1'
     C           @BS       IFEQ 0
     C           @DL       ANDEQ0
     C                     WRITENONE
     C                     ELSE
     C                     MOVEL@DL       DLNORE
     C                     MOVEL@DP       DPNORE
     C                     MOVEL@BS       BSNORE
     C                     WRITEDETLAU
     C                     ENDIF
     C                     ELSE
     C                     WRITEERROR
     C                     ENDIF
      *
      *** Close audit file
      *
     C                     CLOSESE4120AU
      *
     C                     SETON                     LR
      *
     C                     ENDSR
      *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  *DBERR - Error control subroutine                            *
      *                                                               *
      *****************************************************************
      *
     C           DBERR     BEGSR
      **
      *
      *** Close printer files P1 and P2
      *
     C           @PRTP1    IFEQ 'Y'
     C                     CLOSESE4120P1
     C                     ENDIF
      *
     C           @PRTP2    IFEQ 'Y'
     C***********          CLOSESE4120P1                                  176303
     C                     CLOSESE4120P2                                  176303
     C                     ENDIF
      *
     C           @RUN      IFEQ *BLANKS
     C                     MOVE 'Y'       @RUN    1
     C                     ENDIF
      *
      *** Write Audit Error
      *
     C                     EXSR AUDIT
      *
     C                     DUMP
      *
      *** Set on last record indicator  and eror indicator
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
     C/EJECT
      ********************************************************************
      *
      *  RCFAU -- SUBROUTINE TO REGISTER AU PRINTER FILE VIA RCF
      *
      ********************************************************************
      *
     C           RCFAU     BEGSR
      *
      **      Ensure audit spool file recorded by RCF
     C                     Z-ADDSFNUMU    ZSNUMU  60
      **
     C                     CALL 'ZSFILE'
     C                     PARM           SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
     C           ZSERR     IFEQ 'Y'
      *
      **  Error during ZSFILE processing, return to calling program
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
     C/COPY ZSRSRC,ZCNSDZ1
      /EJECT
     C/COPY ZSRSRC,ZDATE2Z4
      /EJECT
     C/COPY ZSRSRC,ZSEDITZ3
      /EJECT
     C/COPY ZSRSRC,ZDATE1Z2                                               176303
      /EJECT                                                              176303
     C/COPY ZSRSRC,ZYLDZ2                                                 176303
      /EJECT                                                              176303
     C/COPY ZSRSRC,ZNCDZ3                                                 176303
      /EJECT                                                              176303
     C/COPY ZSRSRC,ZLCDZ1                                                 176303
      /EJECT                                                              176303
     C/COPY ZSRSRC,ZPRCIZ1                                                176303
      /EJECT                                                              176303
     C/COPY ZSRSRC,ZDAYSZ2                                                176303
      /EJECT                                                              176303
     C/COPY ZSRSRC,ZDYYRZ3                                                176303
      /EJECT                                                              176303
     C/COPY ZSRSRC,ZACCZZ1                                                176303
      /EJECT                                                              176303
     C/COPY ZSRSRC,ZCALCD                                                 176303
      /EJECT                                                              176303
     C/COPY ZSRSRC,ZYLDIZ1                                                176303
      /EJECT                                                              176303
     C/COPY ZSRSRC,ZRATEZ1                                                176303
      /EJECT                                                              176303
     C/COPY ZSRSRC,ZACCRZ1                                                176303
      /EJECT                                                              176303
     C/COPY ZSRSRC,ZCCYXR                                                 176303
      /EJECT                                                              176303
      ***
      ***  ARRA Array entry A.I.B.D. changed to I.S.M.A.
      ** 176303 - Arrays POWER and ZF29 added due to new /COPYs           176303
**  CPY@
(c) Finastra International Limited 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  POWER8 - ARRAY OF POWERS FOR CURRENCY CONVERSION
00000001
00000010
00000100
00001000
00010000
00100000
01000000
10000000
**  POWER - ARRAY OF POWERS FOR CURRENCY CONVERSION
0000001
0000010
0000100
0001000
0010000
0100000
1000000
**   ZF29 DAY NOS OF FEB 29 TO 2096 (INCLUDING 2000)
0006001521029820444305904073650882610287117481320914670161311759219053
2051421975234362489726358278192928030741322023366335124365853804639507
40968424294389045351
