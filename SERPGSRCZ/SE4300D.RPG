     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Movement Status Audit List - Prompt')
      *****************************************************************
      *                                                               *
      *  Midas - SE Module                                            *
      *                                                               *
      *  SE4300D - SE Movement Status Audit List - Prompt             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 234300             Date 29Sep06               *
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 222727             Date 05Nov03               *
      *                 CSE039  *CREATE    Date 25Feb03               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  234300 - Recompiled over changed in PF/HSTTRD.               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  222727 - Release 5.0 errors  (Recompile)                     *
      *  CSE039 - Automatic Settlements of Trades                     *
      *                                                               *
      *****************************************************************
      *  Subroutine Index                                             *
      *                                                               *
      *  #IINIT - Initial processing                                  *
      *  #IINZS - Initialise data                                     *
      *  #IPROC - Main Process                                        *
      *  #IVALD - Validation of the dates                             *
      *  #IENDP - End Program                                         *
      *  ZASNMS - Error messages processing                           *
      *                                                               *
      *****************************************************************
      *                                                               *
      * ID F  C  H  L    FUNCTION OF INDICATORS                       *
      *                                                               *
      *       03      F3  pressed - Exit program                      *
      *       30      Redisplay the screen                            *
      *       50      Error                                           *
      *                                                               *
      *****************************************************************
     FSE4300DFCF  E                    WORKSTN
      ** Display File Screen
      *
     FTRADS   IF  E           K        DISK
      ** Trades File Details
      *
     FSEDVPRL0IF  E           K        DISK
      ** DVP/RVP Movements File Details
      *
     FHSTTR   IF  E           K        DISK
      ** History Trades File Details
      *
     FDPTMV   IF  E           K        DISK
      ** Depot Movements File Details
      *
      *****************************************************************
      /EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
     E/COPY ZSRSRC,ZDATE1Z1
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *
     IRUNDAT    E DSRUNDAT
      * Data Structure to Retrieve Multibranch Indicator
      *
     IA@CPY       DS
      * Copyright array
     I                                        1  80 CPY@
     ILDA       E DSLDA                         256
     I              SPARE                           W24
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IDSSDY     E DSDSSDY
      ** Short Data Structure for Access objects
      *
     IDSFDY     E DSDSFDY
      **  Short data structure for Access objects
      *
     ISDBANK    E DSSDBANKPD
      ** Bank Details
      *
     IPSDS       SDS
      ** Program Status Data Structure - gives Job/Workstation name and
      ** User ID
      *
     I                                     *PROGRAM PGM
     I                                      244 253 JOB
     I                                      254 263 USER
      *
     IPARM        DS                             13
      ***  PARAMETER DATA
     I                                        1   6 TRADE
     I                                        7  12 DEPMV
     I                                       13  13 ALL
      *****************************************************************
     C/EJECT
      *****************************************************************
      *
      *                M A I N  P R O C E S S I N G
      *
      *****************************************************************
      *
     C                     EXSR #IINIT
     C                     EXSR #IPROC
     C                     EXSR #IENDP
      *
      *****************************************************************
      *  #IINIT - Initial Subroutine
      *****************************************************************
      *
     C           #IINIT    BEGSR
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
     C                     MOVE PGM       ZAPGM  10
     C                     MOVE PGM       ##PGM
      *
      **   Define the LDA for error handling
      *
     C           *NAMVAR   DEFN           LDA
      *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
      *
      ** Access Bank Details
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** If error in access program, exit via *PSSR subroutine
      *
     C           @RTCD     IFNE *BLANKS                    - B2
     C           *LOCK     IN   LDA
     C                     Z-ADD001       DBASE            ***************
     C                     MOVEL'SDBANKPD'DBFILE           *DB ERROR 001 *
     C                     MOVEL*BLANKS   DBKEY            ***************
     C                     MOVEL'IBM111 ' DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ELSE                            - X2
      *
      ** Set up Midas Run Date Number & Runday No.
      *
     C                     MOVELBJMRDT    ##MRDT  7
     C                     MOVELBJRDNB    ##RDNB  50
      *
     C                     ENDIF                           - E2
      *
      ** Set Date Format Indicator *IN98  on if date format is MMDDYY
      *
     C           AGDFF     IFEQ 'M'                        - B1
     C                     SETON                     98
     C                     END                             - E1
      *
      ** KLIST to access TRADS
     C           KTRADE    KLIST
     C                     KFLD           KTDRF   6
      *
      ** KLIST to access SEDVPRL0
     C           KSEDV     KLIST
     C                     KFLD           KDREF   6
      *
      ** KLIST to access HSTTR
     C           KHSTTR    KLIST
     C                     KFLD           KTDBA   3
     C                     KFLD           KHTRF   6
      *
      ** KLIST to access DPTMVD
     C           KDPMV     KLIST
     C                     KFLD           KDSEC  10
     C                     KFLD           KDPRN   6
      *
     C           *ENTRY    PLIST
     C                     PARM           RSEQ    5
     C                     PARM           RLEV    1
     C                     PARM           RENT    3
     C                     PARM           PARM   13
     C                     PARM           WWACT   1
     C                     PARM           WWCMD   1
      *
      ** Access SAR Details
     C                     CALL 'AOSARDR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*VERIFY' @OPTN
     C                     PARM 'CSE039'  @SARD   6
     C                     PARM           DSFDY
      *
     C           @RTCD     IFNE *BLANK
     C                     EXSR #IENDP
     C                     ELSE
     C                     MOVEA'000'     *IN,41
     C                     WRITESE4300H0
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      *  #IPROC - Main Process
      *****************************************************************
      *
     C           #IPROC    BEGSR
      *
      ** Continue to redisplay the screen if *IN30 remains off
      *
     C           *IN30     DOWEQ*OFF
      *
      ** If error write messages
      *
     C           *IN50     IFEQ *ON
     C                     WRITE#MSGCTL
     C                     ENDIF
      *
      ** Main format of the display file.
      *
     C                     EXFMTSE4300H0
     C                     EXSR #ICLR
      *
      ** Initialse all indicators
      *
     C                     MOVE *OFF      *IN50
     C                     MOVE *ON       *IN30
      *
     C           *IN03     IFEQ *ON
     C                     EXSR #IEXIT
     C                     END
      *
     C                     EXSR #IVALD
      *
     C                     ENDDO
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      * CLEAR MESSAGE FILE
      *****************************************************************
      *
     C           #ICLR     BEGSR
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGMQ 10
     C                     PARM '*SAME'   ZAPGRL  5
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      * EXIT FROM PROGRAM
      *****************************************************************
      *
     C           #IENDP    BEGSR
      *
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      * EXIT FROM PROGRAM IF F3 HAS BEEN PRESSED
      *****************************************************************
      *
     C           #IEXIT    BEGSR
      *
     C                     MOVE 'Y2U9999' W0RTN   7
     C                     EXSR #IENDP
      *
     C                     ENDSR
      *
      *****************************************************************
      * VALIDATE THE SCREEN
      *****************************************************************
      *
     C           #IVALD    BEGSR
      *
     C                     MOVE *OFF      *IN41
     C                     MOVE *OFF      *IN42
     C                     MOVE *OFF      *IN43
     C                     MOVE *OFF      *IN50
      *
      ** Check what field has been filled in
      *
B1   C           F0TREF    IFNE *BLANKS
     C           F0WREF    ANDEQ*BLANKS
     C           F0AREF    ANDEQ*BLANKS
      *
      ** Access TRADS to see if trade exists
     C                     MOVELF0TREF    KTDRF
     C           KTRADE    CHAINTRADS                60
B2   C           *IN60     IFEQ *OFF
     C           RECI      ANDNE'*'
     C                     MOVE TDRF      TRADE
X2   C                     ELSE
      *
      ** Access SEDVPRL0 to see if trade exists
     C                     MOVELF0TREF    KDREF
     C           KSEDV     CHAINSEDVPRL0             61
B3   C           *IN61     IFEQ *OFF
     C           DVRECI    ANDNE'*'
     C                     MOVE DVREF     TRADE
X3   C                     ELSE                            - X1
      *
      ** Access HSTTR to see if trade exists
     C                     MOVELF0TREF    KHTRF
     C           KHSTTR    CHAINHSTTR                62
B4   C           *IN62     IFEQ *OFF
     C           RECI      ANDNE'*'
     C                     MOVE TDRF      TRADE
X4   C                     ELSE                            - X1
      *
      ** Error: Transaction not found
     C                     MOVE *ON       *IN50
     C                     MOVE *ON       *IN41
     C                     MOVE *OFF      *IN30
     C                     MOVEL'CS03901' ZAMSID
     C                     EXSR ZASNMS
E4   C                     ENDIF
E3   C                     ENDIF
E2   C                     ENDIF
      *
X1   C                     ELSE
      *
B2   C           F0WREF    IFNE *BLANKS                    - B1
     C           F0TREF    ANDEQ*BLANKS                    - B1
     C           F0AREF    ANDEQ*BLANKS                    - B1
      *
      ** Access DPTMV to see if trade exists
     C                     MOVELF0TREF    KDPRN
     C           KDPMV     CHAINDPTMV                63
B3   C           *IN63     IFEQ *OFF
     C           RECI      ANDNE'*'
     C                     MOVE DPRN      DEPMV
X3   C                     ELSE                            - X1
      *
      ** Error: Transaction not found
     C                     MOVE *ON       *IN42
     C                     MOVE *ON       *IN50
     C                     MOVE *OFF      *IN30
     C                     MOVEL'CS03901' ZAMSID
     C                     EXSR ZASNMS
E3   C                     ENDIF
      *
X2   C                     ELSE
      *
B3   C           F0AREF    IFNE *BLANKS
     C           F0TREF    ANDEQ*BLANKS
     C           F0WREF    ANDEQ*BLANKS
      *
B4   C           F0AREF    IFEQ 'X'
     C                     MOVELF0AREF    ALL
      *
X4   C                     ELSE
      ** Error: Entry must be 'X'
     C                     MOVE *ON       *IN43
     C                     MOVE *ON       *IN50
     C                     MOVE *OFF      *IN30
     C                     MOVEL'CS03903' ZAMSID
     C                     EXSR ZASNMS
E4   C                     END
      *
X3   C                     ELSE
      *
      ** Error: Only one field can be entered
     C                     MOVE *ON       *IN50
     C                     MOVE *ON       *IN41
     C                     MOVE *ON       *IN42
     C                     MOVE *ON       *IN43
     C                     MOVE *OFF      *IN30
     C                     MOVEL'CS03902' ZAMSID
     C                     EXSR ZASNMS
E3   C                     END                             - E1
E2   C                     END                             - E1
E1   C                     END                             - E1
      *
     C                     ENDSR
      *
      *****************************************************************
      * Send message to program's message queue.
      *****************************************************************
      *
     C           ZASNMS    BEGSR
      *
     C           ZAMSGF    IFEQ *BLANKS
     C                     MOVEL'GB'      ZAMSGF
     C                     MOVE 'SEUSRMSG'ZAMSGF
     C                     END
      *
     C           ZAPGMQ    IFEQ *BLANK
     C                     MOVEL##PGM     ZAPGMQ
     C                     END
      *
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGMQ 10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message Id.
     C                     PARM           ZAMSGF 10        Message file.
     C                     PARM           ZAMSDA132        Message data.
     C                     PARM           ZAMSTP  7        Message type.
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     SETON                     U7U8LR
     C                     DUMP
      *
     C                     CALL 'DBERRCTL'
      *
     C                     ENDIF
      *
     C                     MOVE 'E'       WWCMD
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      ********************************************************************
      /EJECT
     C/COPY ZSRSRC,ZDATE1Z2
      /EJECT
** CPY@
(c) Misys International Banking Systems Ltd. 2003
** ZYDY - Years in days cumulative, four year span
0366073110961461
** ZMDY - Months in days cumulative through year
000031059090120151181212243273304334365
