     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Unrealised Profit/Loss Report')               *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE4605 - Print Unrealised Profit Loss Analysis               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      *  Last Amend No. AR1067108          Date 11Dec12               *
      *  Prev Amend No. AR1001183A         Date 20Nov12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CPK024             Date 12Apr06               *
      *                 CSE071             Date 19Jul05               *
      *                 CGL029             Date 01Sep03               *
      *                 211407             Date 07Jan03               *
      *                 208241             Date 21Aug02               *
      *                 207543             Date 17JUL02               *
      *                 194370             Date 12Jul01               *
      *                 180056             Date 18OCT00               *
      *                 CSE035             Date 22Apr02               *
      *                 CEU018             Date 27Apr98               *
      *                 CSE036  *CREATED   Date 22Apr02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR1067108 - PMC1140 Component failed during Clean COB        *
      *              Applied fix AR1001183A                           *
      *  AR1001183A - Coupon rate was still used in computation of    *
      *              interest instead of using the details in FRN tab *
      *              (Reopen) (Recompile)                             *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CPK024 - Packaging of MPlus 1.2.1. Copy sources needed by    *
      *           UTC0012.                                            *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  211407 - Recompile after change on copy sources ZYLDZ2,      *
      *           ZACCRZ1 and ZYCEZ1 for NX/NNX processing.           *
      *           The change was to call subroutine ZLCD1 instead     *
      *           of ZLCD in retrieving the PERIOD for NNX processing *
      *           and ZNCD1 is called instead of ZNCD in retrieving   *
      *           the WBENCD for NNX processing. Also rewrite tests   *
      *           for identifying if position is NX,NNX or CUM.       *
      *  208241 - Recompiled over changed /CPY ZSDESCZ3.              *
      *  207543 - CEU018 NOT WORKING ON A PRICE BASIS, MISSING A PART *
      *           OF AUS047. CHECK FOR CEU017, RECOMPILE OVER ZYLDZ2. *
      *  194370 - Recompile after change on copy sources ZYLDZ2,      *
      *           For securities with divisor basis = '5' and first   *
      *           coupon date > valuation date (rundate + 1), set     *
      *           event control date to first coupon date before      *
      *           processing SR/ZDYYR. This is to ensure that the     *
      *           correct hypothetical last and next coupon dates are *
      *           used in deriving the price/yield rate.              *
      *  180056 - Recompiled over changed in ZDYYRZ3.                 *
      *  CSE035 - NX/NNX Coupon Payment Processing.                   *
      *  CEU018 - SE Decimals on Nominal amounts                      *
      *  CSE036 - Unrealised Profit and Loss Report by Trade          *
      *                                                               *
      *****************************************************************
      *                                                               *
     FSETREXV1IF  E           K        DISK
     FSETREXV2IF  E           K        DISK
     F            SETREXP0                          KRENAMESETREXP2
     FSECTY   IF  E           K        DISK
     FINVTP   IF  E           K        DISK
     FPRICED  IF  E           K        DISK
     FSEDEV   IF  E           K        DISK
     FSECEO   IF  E           K        DISK
     F            SEDEVDF                           KRENAMESEDEVDO
     F            SNDEVDF                           KRENAMESNDEVDO
     FBKPHD   UF  E           K        DISK
     FBKPOS   IF  E           K        DISK
     FSEKEYD  O   E           K        DISK                      A
     FSEKEYA  UF  E           K        DISK                      A    UC
     FSEBKPSVDIF  E           K        DISK
     F            BKPOSDF                           KRENAMESEBKPSVF
     FSEBKHSVDIF  E           K        DISK
     F            BKPHDDF                           KRENAMESEBKHSVF
     FSEPCBD  IF  E           K        DISK
     FSE4605P1O   E                    PRINTER      KINFDS SPOOL      UC
     FSE4605P2O   E                    PRINTER      KINFDS SPOOLU     UC
     FSE4605AUO   E                    PRINTER
      *
     F***COPY*ZSRSRC,ZNDECZ1*                                                                 CPK024
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *    20 - First Cycle Indicator                                 *
      *    21 - EOF for SETREXV1 File                                 *
      *    22 - Record Not Found                                      *
      *    23 - EOF for SEDEV File                                    *
      *    24 - Australian Yield Security                             *
      *    25 - Market Type = P                                       *
      *    26 - Market Type = S                                       *
      *    27 - Market Type = G                                       *
      *    29 - Zero Profit & Loss                                    *
      *    30 - New Details Indicator                                 *
      *    31 - Nominal Total < 0                                     *
      *    32 - Settlement Value Total < 0                            *
      *    33 - Market Value Total < 0                                *
      *    34 - Profit & Loss Total < 0                               *
      *    35 - Currency Decimal Places = 0                           *
      *    36 - Currency Decimal Places = 1                           *
      *    37 - Currency Decimal Places = 2                           *
      *    38 - Currency Decimal Places = 3                           *
      *    40 - 'Grand Total' indicator                               *
      *    41 - Investment Type Total < 0                             *
      *    42 - Market Total < 0                                      *
      *    43 - Currency Total < 0                                    *
      *    44 - Book Total < 0                                        *
      *    45 - Nominal Amount < 0                                    *
      *    46 - Settlement Value < 0                                  *
      *    47 - Market Value < 0                                      *
      *    48 - Profit & Loss < 0                                     *
      *    50 - Market Value < 0                                      *
      *    52 - Old & new Unreal. P/L posted either > or <            *
      *    56 - EOF for SEKEYA file                                   *
      *    58 - No records on file PRICED                             *
      *    59 - No price record exists for a security                 *
      *    70 - Multibranching Indicator                              *
      *    75 - Currency break                                        *
      *    80 - Error in Chain for BKPHD or BKPOS                     *
      *                                                               *
      * 90-95 - ZDATE2 Subroutine Indicators                          *
      *    98 - Date Format Indicator                                 *
      *    99 - PF/SDBANKPD Record Error                              *
      *                                                               *
      *    U7 - Database Error                                        *
      *    U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Index to subroutines:                                        *
      *                                                               *
      *  SRINIT - Initial Program Control                             *
      *  SRDETL - Record Detail Processing                            *
      *  SRBRAN - Branch Details Check                                *
      *  SRBOOK - Book Details Check                                  *
      *  SRNCCY - Nominal Currency Details Check                      *
      *  SRMRKT - Market Indicator Check                              *
      *  SRITYP - Investment Type Check                               *
      *  SRSECN - Security Details Check                              *
      *  SRWBTL - Print Book Totals                                   *
      *  SRWCTL - Print Currency Totals                               *
      *  SRWMTL - Print Market Totals                                 *
      *  SRWITL - Print Investment Type Totals                        *
      *  SRWSTL - Print Security Totals                               *
      *  SRWRIT - Write Details                                       *
      *  SROVRF - Check Overflow Line                                 *
      *  SRHEAD - Write Headings                                      *
      *  SRNEGV - Format Negative Value                               *
      *  SRZERO - Process zero position records                       *
      *  SRUNRL - Calculate Unrealised Profit/Loss                    *
      *  SRBKPH - Chain BKPHD                                         *
      *  SRBKPO - Chain BKPOS                                         *
      *  SRWRTD - Generate Account Keys Detail                        *
      *  SRAUDI - Update Account Keys Audit                           *
      *  SRPCHN - Chain to Book Position Profit Centre                *
      *  SRPRIC - Chain to file PF/PRICED                             *
      *  ZSDESC - Format Security Description                         *
      *  ZDATE1 - Convert Date to Day Number                          *
      *  ZDATE2 - Convert Day Number to Date Formats                  *
      *  ZSEDIT - Format Amount                                       *
      *  ZNCD   - Determine Next Coupon Date                          *
      *  ZYCE   - Determine if in Cum- or Ex- Coupon Date Range       *
      *  ZPRCI  - Convert Price to %Price                             *
      *  ZPRCO  - Convert Price to Yield                              *
      *  ZACCZ  - Calculate Accrued Interest                          *
      *  ZDAYS  - Calculate No. of Days between Dates                 *
      *  ZDYYR  - Calculate No. of Days in Interest Year              *
      *  ZYLDO  - Output Price as Price/Discount/Yield                *
      *  ZACCR  - Calculate Accured Int. Between Two Dates            *
      *  ZRATE  - Calculate Effective Interest Rate                   *
      *  XDATE3 - Calculate No. of Working Days Back in Ccy           *
      *  ZYLDI  - Output Yield as %Price                              *
      *  ZD5DYS - Determine No. of Days in Coupon Period (DVBS=5)     *
      *  ZYLD   - Set Up Parameter Values and Call ZYIELD             *
      *  ZLCD   - Calculate Last Coupon Date                          *
      *  ZHASH  - Calculates event amounts as hash value              *
      *  *PSSR  - Exception error routine                             *
      *                                                               *
      *****************************************************************
      *
      **  Copyright Statement
      *
     E                    CPY@    1   1 80
      *
      **  Work Arrays
      *
     E                    AU1        16  1
     E                    AU2        22  1
      *
      **  Total accumulation arrays
      *
     E                    NOM         7 22 0             Nominal
     E                    DSC         7 22 0             Discount
     E                    CAP         7 22 0             Capital Value
     E                    WAY         7 25 8             Weighted Avg Yld
     E                    WAM         7 25 8             WAY MRKT
     E                    MKT         7 22 0             Market Value
     E                    PL          7 22 0             P/L
     E                    PLT         7 22 0             P/L Today
      *
     E/COPY ZSRSRC,ZNCDZ1
     E/COPY ZSRSRC,ZDAYSZ1
     E/COPY ZSRSRC,ZSDESCZ1
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZSEDITZ1
     E/COPY ZSRSRC,ZPOWER8Z1
     E/COPY ZSRSRC,ZDATE6Z1
     E/COPY ZSRSRC,ZHOLE
     E/COPY ZSRSRC,ZHASHZ1                               INT AND DEC
     E***COPY*ZSRSRC,ZNDECZ2*                                                                 CPK024
      *
      **  Rename BRCA since it is already used by SDBRCHPD routines.
      *
     ISEPCBDF
     I              BRCA                            XXBRCA
     ISEBKPSVF
     I              RECI                            AURECI
     I              BPSC                            AUBPSC
     I              BPBA                            AUBPBA
     I              BPBK                            AUBPBK
     I              BPMK                            AUBPMK
     I              BPTV                            AUBPTV
     I              BPPD                            AUBPPD
     I              NPSN                            AUNPSN
     I              ECNP                            AUECNP
     I              APPB                            AUAPPB
     I              RPTP                            AURPTP
     I              PILP                            AUPILP
     I              APNC                            AUAPNC
     I              SNPI                            AUSNPI
     I              SNSI                            AUSNSI
     I              SANP                            AUSANP
     I              SANS                            AUSANS
     I              ANMP                            AUANMP
     I              AAPR                            AUAAPR
     I              ADJI                            AUADJI
     I              TNLU                            AUTNLU
     ISEBKHSVF
     I              RECI                            AURECI
     I              BHSC                            AUBHSC
     I              BHBA                            AUBHBA
     I              BHBK                            AUBHBK
     I              BHMK                            AUBHMK
     I              BHTV                            AUBHTV
     I              LPCD                            AULPCD
     I              LPSD                            AULPSD
     I              FPSD                            AUFPSD
     I              LAVP                            AULAVP
     I              LATD                            AULATD
     I              ARPC                            AUARPC
     I              ICLD                            AUICLD
     I              IACR                            AUIACR
     I              IACP                            AUIACP
     I              INDO                            AUINDO
     I              UPPT                            AUUPPT
     I              DPAP                            AUDPAP
     I              BUDU                            AUBUDU
     I              INVT                            AUINVT
     I              NCRY                            AUNCRY
     I              BHTR                            AUBHTR
     I              BHRN                            AUBHRN
     I              LPRC                            AULPRC
     I              BHDP                            AUBHDP
     I              BHCP                            AUBHCP
     I              BHPP                            AUBHPP
     I              TNLU                            AUTNLU
     I              DPFP                            AUDPFP
     I/COPY ZSRSRC,ZHOLI
      *
      **  Data Structure for Compilation - Copyright Insertion
      *
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *
      ** Data Structure for XDATE3
      *
     I            DS
     I                                        1  75 HCCY
     I                                        1  75 XCCY
      *
      **  Data Structure for LF/INVTP
      *
     I            DS
     I                                        1   6 INKEY
     I                                        1   3 ATNMC
     I                                        4   6 AINVT
      *
      **  Data structure for BKPHD
      *
     I            DS
     I                                        1  17 BKKEY
     I                                        1  10 #ATDSS
     I                                       11  13 #ATDBR
     I                                       14  15 #ATDBK
     I                                       16  16 #ATDMI
     I                                       17  17 TVDB
      *
      **  Data structure for BKPOS
      *
     I            DS
     I                                        1  22 BOKEY
     I                                        1  10 BHSC
     I                                       11  13 BHBA
     I                                       14  15 BHBK
     I                                       16  16 BHMK
     I                                       17  17 TVD2
     I                                       18  220LPSD
     I                                       18  22 LPSA
      *
      ** Data Structure for Sekeyd Account key
      *
     I            DS
     I                                        1  20 ACKY
     I                                        1   3 INVT
     I                                        4   4 EVTP
     I                                        5   5 TRTP
     I                                        6   6 PORT
     I                                        7   8 BOOK
     I                                        9   9 PSBS
     I                                       10  10 FILL
     I                                       11  12 CGTP
     I                                       13  13 MRKT
     I                                       14  14 CNVI
     I                                       15  15 NEGI
     I                                       16  18 FIL2
     I                                       19  20 AMCD
      *
      ** Book Position Profit Centre (PF/SEPCBD)
      *
     IKSEPCR      DS
     I                                        1   2 KBHBK
     I                                        3   5 KBHBA
     I                                        6  15 KBHSC
     I                                       16  16 KBHMK
      *
     I            DS
     I                                        1  12 TABKEY
     I                                        1   20RECT
     I                                        3   8 ZZ006
     I                                        9  11 RCCY
     I                                       12  12 ZZ001
     ISPOOL       DS
      *
      **   File information data structure for main print.
      *
     I                                       83  92 SFILE
     I                                    B 123 1240SFNUM
     ISPOOLU      DS
      *
      **   File information data structure for main print.
      *
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
      **  System Rundate
      *
     IRUNDT       DS
     I                                        1   7 RUNA
     I                                    P   8  100RUND
     I                                       11  11 SSF
     I                                       12  12 DATF
     I                                       13  13 MBIN
      *
      **  Program Status
      *
     IPSDS       SDS
     I                                      244 253 WSID
     I                                      254 263 USRID
      *
      **  Error reporting data structure
      *
     ILDA         DS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *
      ** Data Structures for access objects
      *
     ISDBANK    E DSSDBANKPD
      *
      **  External DS for bank details.
      *
     I              BJCYCD                          BCCY
     I              BJURPT                          TITL
      *
     ISDSTRD    E DSSDSTRDPD
      *
      **  External DS for security details
      *
     I              BVUPP                           UPPS
     I              BVUPB                           UPBS
     I              BVNPAC                          NPAC
      *
     ISDGELR    E DSSDGELRPD
      *
      **  External DS for general ledger details.
      *
     ISDBOOK    E DSSDBOOKPD
      *
      **  External DS for book details.
      *
     ISDCURR    E DSSDCURRPD
      *
      **  External DS for currency details.
      *
     I              A6SPRT                          SPOT
     I              A6MDIN                          MDIN
      *
     ISDBRCH    E DSSDBRCHPD
      *
      **  External DS for SD Branch Codes
      *
     I              A8BRCD                          BRCA
     I              A8BRNM                          BRNM
      *
     IDSFDY     E DSDSFDY
      *
      ** First DS for Access Programs, Short Data Structure
      *
     IDSSDY     E DSDSSDY
      *
      ** Second DS for Access Programs, Long Data Structure
      *
     I/COPY ZSRSRC,ZNCDZ2
     I/COPY ZSRSRC,ZSDESCZ2
     I/COPY ZSRSRC,ZSEDITZ2
     I/COPY ZSRSRC,ZYLDZ1
      *    Hash totalling fields
     I/COPY ZSRSRC,ZHASHZ2
     I              'NZG15FEB01'          C         CONST
      /EJECT
      *
     C                     MOVEACPY@      BIS@   80
      *
      **  RCF sequence number of report
      *
     C           *ENTRY    PLIST
     C                     PARM           PLVL    1
     C                     PARM           PBRC    3
      *
      **  Key Field for LF/SEDEV
      *
     C           SEKEY     KLIST
     C                     KFLD           SDSN
     C                     KFLD           SDED
     C                     KFLD           SDET
      *
      **  Key Field for LF/INVTP
      *
     C           ITKEY     KLIST
     C                     KFLD           ATNMC
     C                     KFLD           AINVT
      *
      **  Key Field for LF/BKPHD
      *
     C           BKHKEY    KLIST
     C                     KFLD           #ATDSS
     C                     KFLD           #ATDBR
     C                     KFLD           #ATDBK
     C                     KFLD           #ATDMI
     C                     KFLD           TVDB             Trade/Value
      *
      **  Key Field for LF/BKPOS
      *
     C           BKOKEY    KLIST
     C                     KFLD           BHSC
     C                     KFLD           BHBA
     C                     KFLD           BHBK
     C                     KFLD           BHMK
     C                     KFLD           TVD2             Trade/Value
     C                     KFLD           LPSD
      *
      **  Key Field for LF/SEBKPSVD
      *
     C           SEOKEY    KLIST
     C                     KFLD           BHSC
     C                     KFLD           BHBA
     C                     KFLD           BHBK
     C                     KFLD           BHMK
     C                     KFLD           TVD2             Trade/Value
     C                     KFLD           AULPSD
      *
      ** Book Position Profit Centre (PF/SEPCBD)
      *
     C           KSEPCB    KLIST
     C                     KFLD           KBHBK
     C                     KFLD           KBHBA
     C                     KFLD           KBHSC
     C                     KFLD           KBHMK
      *
      **  Key field for PF/PRICED
      *
     C           PKEY      KLIST
     C                     KFLD           PSSN
     C                     KFLD           PPRT
     C                     KFLD           PVDT
      *
      **  Initialise fields
      *
     C                     Z-ADD0         OREC
     C                     Z-ADD1         Z
      *
      **  Main Processing
      *
     C           *NAMVAR   DEFN           LDA
     C                     EXSR SRINIT
      *
     C                     MOVE '0'       *IN20
     C                     EXSR SRZERO
     C                     EXSR SRDETL
      *
      **  If database error ocurred, return to calling program. Else, print
      **  report.
      *
     C           *INU7     IFEQ '0'
     C           *INU8     ANDEQ'0'
      *
      **  If LF/SETREXV1 is empty, print 'NO DETAILS TO REPORT'. Else,
      **  print 'END OF REPORT'.
      *
     C           *IN20     IFEQ '0'
     C                     WRITEHEADAU
     C                     WRITENODTL
     C                     EXSR SRAUDI
      *
     C                     ELSE
     C                     EXSR SRUNRL
     C                     EXSR SRWSTL
     C                     EXSR SRWMTL
     C                     EXSR SRWBTL
     C                     EXSR SRWITL
     C                     EXSR SRWCTL
     C                     EXSR SROVRF
     C                     EXSR SROVR2
     C                     WRITEFMTP111
     C                     WRITEFMTP211
     C                     CLOSESE4605P1
     C                     CLOSESE4605P2
     C                     EXSR SRAUDI
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     MOVE '1'       *INLR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  Subroutine: SRINIT - Initial Program Control                 *
      *                                                               *
      *  Called By : MAIN - Main Controlling Routine                  *
      *                                                               *
      *  Calls     : ZSYSTM - Chain to Installation Control Record    *
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR
      *
     C                     MOVE 'SE4605'  DBPGM
      *
      **  Access Run Date
      *
     C           *NAMVAR   DEFN RUNDAT    RUNDT
     C                     IN   RUNDT
      *
      **  Set Up Multibranching Indicator
      *
     C           MBIN      IFEQ 'Y'
     C                     MOVE '1'       *IN70
     C                     ENDIF
      *
      **  Check Date Format
      *
     C           DATF      IFEQ 'M'
     C                     MOVE '1'       *IN98
     C                     ENDIF
      *
      **  Access Bank Details
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      **  If no records are found, DATABASE ERROR.
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'SDBANKPD'DBFILE
     C                     MOVE *BLANKS   DBKEY            ***************
     C                     Z-ADD1         DBASE            * DB ERROR 01 *
     C                     SETON                     U7U8LR***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      **  Access Security Details
      *
     C                     CALL 'AOSTRDR0'
     C                     PARM '*DBERR ' PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDSTRD    PARM SDSTRD    DSFDY
      *
      **  If no records are found, DATABASE ERROR.
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'SDSTRDPD'DBFILE
     C                     MOVE *BLANKS   DBKEY            ***************
     C                     Z-ADD7         DBASE            * DB ERROR 07 *
     C                     SETON                     U7U8LR***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      **  Access General Ledger Details
      *
     C**********           CALL 'AOGELRR0'                                                    CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C********** SDGELR    PARM SDGELR    DSFDY                                               CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
      *
      **  If no records are found, DATABASE ERROR.
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'SDGELRPD'DBFILE
     C                     MOVE *BLANKS   DBKEY            ***************
     C                     Z-ADD14        DBASE            * DB ERROR 14 *
     C                     SETON                     U7U8LR***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Read SEKEYA - Generate Account Keys Audit
      ** Write record to new member UNPL first
      *
     C                     OPEN SEKEYA
     C                     WRITESEKEYAF
     C                     CLOSESEKEYA
      *
     C                     OPEN SEKEYA
     C                     READ SEKEYA                   56
     C           *IN56     IFEQ '0'
     C                     Z-ADDNORE      OREC
     C                     ELSE
     C           *LOCK     IN   LDA
     C                     MOVE 'SEKEYA  'DBFILE
     C                     MOVE *BLANKS   DBKEY            ***************
     C                     Z-ADD10        DBASE            * DB ERROR 10 *
     C                     SETON                     U7U8LR***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      *   Calculate event control date for market price calc
      *
     C                     Z-ADDBKAPDT    ZZAPDT
     C                     Z-ADDBJDNWD    ZZDNWD
     C                     EXSR ZEVCD
     C           BKALDI    IFEQ 'Y'
     C                     Z-ADDECD       @WECD   50
     C                     ELSE
     C           ECD       ADD  1         @WECD   50
     C                     ENDIF
      *                                                                                       CSE035
      ** Check if SAR CSE035 is on                                                            CSE035
      *                                                                                       CSE035
     C                     CALL 'AOSARDR0'                                                    CSE035
     C                     PARM *BLANKS   PRTCD   7                                           CSE035
     C                     PARM '*VERIFY' POPTN   7                                           CSE035
     C                     PARM 'CSE035'  PSARD   6                                           CSE035
      *                                                                                       CSE035
     C           PRTCD     IFEQ *BLANKS                                                       CSE035
     C                     MOVE 'Y'       CSE035  1                                           CSE035
     C                     ELSE                                                               CSE035
     C                     MOVE 'N'       CSE035                                              CSE035
     C                     ENDIF                                                              CSE035
      *                                                                                       207543
      ** Access SAR details file to see if CEU018 is installed                                207543
      *                                                                                       207543
     C                     CALL 'AOSARDR0'                                                    207543
     C                     PARM *BLANKS   PRTCD                                               207543
     C                     PARM '*VERIFY' POPTN                                               207543
     C                     PARM 'CEU018'  PSARD                                               207543
      *                                                                                       207543
     C           PRTCD     IFEQ *BLANK                                                        207543
     C                     MOVE 'Y'       CEU018  1                                           207543
     C                     ELSE                                                               207543
     C                     MOVE 'N'       CEU018                                              207543
     C                     END                                                                207543
      *
      ** Initialize Grand Total field
      *
     C                     Z-ADD0         #ATOTP 150
      *
     C           #AINI9    ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  Subroutine: SRZERO - Zero Position Processing                *
      *                                                               *
      *  Called By : MAIN - Main Controlling Routine                  *
      *                                                               *
      *  Calls     : SRBRAN - Branch Details Check                    *
      *                                                               *
      *****************************************************************
      *
     C           SRZERO    BEGSR
      *
      **  Read Zero positions file
      *
     C                     READ SETREXV2                 77
      *
      **  Perform details processing while not end-of-file and no
      **  database error encountered.
      *
     C           *IN77     DOWEQ'0'
     C           ATDBR     IFNE #ATDBR
     C                     EXSR SRBRAN
     C                     ENDIF
     C           ATNMC     IFNE #ATNMC
     C                     EXSR SRNCCY
     C                     ENDIF
     C           AINVT     IFNE #AINVT
     C                     EXSR SRITYP
     C                     ENDIF
     C           ATDBK     IFNE #ATDBK
     C                     EXSR SRBOOK
     C                     ENDIF
     C           ATDMI     IFNE #ATDMI
     C                     EXSR SRMRKT
     C                     ENDIF
     C           ATDSS     IFNE #ATDSS
     C                     EXSR SRSECN
     C                     ENDIF
      *
     C                     Z-ADD0         #ATOTN
     C                     Z-ADD0         #ATOTS
     C                     Z-ADD0         #ATOTD
     C                     Z-ADD0         #ATOTM
     C                     Z-ADD0         #APTTL
      *
     C           ARECI     IFNE '*'
     C                     EXSR SRWRIT
     C                     ENDIF
     C                     EXSR SRUNRL
      *
     C                     READ SETREXV2                 77
     C                     ENDDO
      *
     C                     Z-ADD0         #ATOTN
     C                     Z-ADD0         #ATOTS
     C                     Z-ADD0         #ATOTD
     C                     Z-ADD0         #ATOTM
     C                     Z-ADD0         #APTTL
      *
     C           SRZER9    ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  Subroutine: SRDETL - Record Detail Processing                *
      *                                                               *
      *  Called By : MAIN - Main Controlling Routine                  *
      *                                                               *
      *  Calls     : SRBRAN - Branch Details Check                    *
      *              SRBOOK - Book Details Check                      *
      *              SRNCCY - Nominal Currency Details Check          *
      *              SRMRKT - Market Indicator Check                  *
      *              SRITYP - Investment Type Check                   *
      *              SRSECN - Security Details Check                  *
      *              SRWBTL - Print Book Totals                       *
      *              SRWCTL - Print Currency Totals                   *
      *              SRWMTL - Print Market Totals                     *
      *              SRWITL - Print Investment Type Totals            *
      *              SRWSTL - Print Security Totals                   *
      *              SROVRF - Check Overflow Line                     *
      *              SRWRIT - Write Details                           *
      *              SRHEAD - Write Headings                          *
      *              ZNCD   - Determine Next Coupon Date              *
      *                                                               *
      *****************************************************************
      *
     C           SRDETL    BEGSR
      *
      **  Read Trade Details File.
      *
     C                     READ SETREXV1                 21
      *
      **  Perform details processing while not end-of-file and no
      **  database error encountered.
      *
     C           *IN21     DOWEQ'0'
     C           *INU7     ANDEQ'0'
     C           *INU8     ANDEQ'0'
      *
      ** If this is the first record read, check details for branch,
      ** book, currency, market, investment type, and security.
      *
     C           *IN20     IFEQ '0'
     C                     EXSR SRBRAN
     C                     EXSR SRBOOK
     C                     EXSR SRNCCY
     C                     EXSR SRMRKT
     C                     EXSR SRITYP
     C                     EXSR SRSECN
      *
      **  If no database error occurred, print headings and details.
      *
     C           *INU7     IFEQ '0'
     C           *INU8     ANDEQ'0'
     C                     OPEN SE4605P1
     C                     OPEN SE4605P2
      *
      **  Processing for spool file by RCF (report)
      *
     C                     Z-ADDSFNUM     PSFNUM  60
     C                     CALL 'ZSFILE'
     C                     PARM *BLANKS   PSEQ    5
     C                     PARM BRCA      PENTY   3
     C                     PARM           SFILE
     C                     PARM           PSFNUM
     C                     PARM           PZSERR  1
      *
      **  If error occurred during ZSFILE processing, return to
      **  calling program.
      *
     C           PZSERR    IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
      **  Processing for spool file by RCF (report)
      *
     C                     Z-ADDSFNUMU    PSFNUM  60
     C                     CALL 'ZSFILE'
     C                     PARM *BLANKS   PSEQ    5
     C                     PARM BRCA      PENTY   3
     C                     PARM           SFILEU
     C                     PARM           PSFNUM
     C                     PARM           PZSERR  1
      *
      **  If error occurred during ZSFILE processing, return to
      **  calling program.
      *
     C           PZSERR    IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     MOVE '1'       *IN30
     C                     EXSR SRHEAD
     C                     MOVE '0'       *IN30
     C                     MOVE '1'       *IN20
     C                     ENDIF
      *
     C                     ENDIF
      *
      **  If a record has already been processed, check for changes in
      **  branch, book, currency, market, investment type or security.
      *
     C           *IN20     IFEQ '1'
      *
      **  If there is a change in branch, print totals for security,
      **  market, book, investment type and currency.
      **  Print '*** END OF REPORT ***' afterwards.
      *
     C           ATDBR     IFNE #ATDBR
     C                     EXSR SRUNRL
     C                     EXSR SRWSTL
     C                     EXSR SRWMTL
     C                     EXSR SRWBTL
     C                     EXSR SRWITL
     C                     EXSR SRWCTL
     C                     WRITEFMTP111
     C                     WRITEFMTP211
      *
      **  Check details for branch, currency, investment type, book
      **  market and security.
      *
     C                     EXSR SRBRAN
     C                     EXSR SRNCCY
     C                     EXSR SRITYP
     C                     EXSR SRBOOK
     C                     EXSR SRMRKT
     C                     EXSR SRSECN
      *
      **  If no database error occurred, close the spool file.
      *
     C           *INU7     IFEQ '0'
     C           *INU8     ANDEQ'0'
     C                     CLOSESE4605P1
     C                     CLOSESE4605P2
      *
      **  If all branches are to printed or branch chosen by user is
      **  equal to the branch read, open new spool file.
      *
     C           PBRC      IFEQ 'ALL'
     C           PBRC      OREQ ATDBR
     C           *IN70     OREQ '0'
     C                     OPEN SE4605P1
     C                     OPEN SE4605P2
     C                     Z-ADD0         PAGE
     C                     Z-ADD0         PAGE2
      *
      **  Processing for spool file by RCF (report)
      *
     C                     Z-ADDSFNUM     PSFNUM  60
     C                     CALL 'ZSFILE'
     C                     PARM *BLANKS   PSEQ    5
     C                     PARM BRCA      PENTY   3
     C                     PARM           SFILE
     C                     PARM           PSFNUM
     C                     PARM           PZSERR  1
      *
      **  If error occurred during ZSFILE processing, return to
      **  calling program.
      *
     C           PZSERR    IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
      **  Processing for spool file by RCF (report)
      *
     C                     Z-ADDSFNUMU    PSFNUM  60
     C                     CALL 'ZSFILE'
     C                     PARM *BLANKS   PSEQ    5
     C                     PARM BRCA      PENTY   3
     C                     PARM           SFILEU
     C                     PARM           PSFNUM
     C                     PARM           PZSERR  1
      *
      **  If error occurred during ZSFILE processing, return to
      **  calling program.
      *
     C           PZSERR    IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
     C                     MOVE '1'       *IN30
     C                     EXSR SRHEAD
     C                     MOVE '0'       *IN30
     C                     ENDIF
     C                     ENDIF
     C                     ELSE
      *
      **  If there is a change in nominal currency, write totals for
      **  security, market, book and investment type.
      *
     C           ATNMC     IFNE #ATNMC
     C                     EXSR SRUNRL
     C                     EXSR SRWSTL
     C                     EXSR SRWMTL
     C                     EXSR SRWBTL
     C                     EXSR SRWITL
      *
      **  Print currency total.
      *
     C                     EXSR SRWCTL
      *
      **  Check details for currency, investment type, book, market and
      **  security.
      *
     C                     EXSR SRNCCY
     C                     EXSR SRITYP
     C                     EXSR SRBOOK
     C                     EXSR SRMRKT
     C                     EXSR SRSECN
      *
      ** If no database error were encountered, write new page with
      ** new currency details.
      *
     C           *INU7     IFEQ '0'
     C           *INU8     ANDEQ'0'
     C                     MOVE '1'       *IN30
     C                     EXSR SRHEAD
     C                     MOVE '0'       *IN30
     C                     ENDIF
     C                     ELSE
      *
      **  If there is a change in the investment type, print totals for
      **  security, market, and book.
      *
     C           AINVT     IFNE #AINVT
     C                     EXSR SRUNRL
     C                     EXSR SRWSTL
     C                     EXSR SRWMTL
     C                     EXSR SRWBTL
      *
      **  Print investment type total.
      *
     C                     EXSR SRWITL
      *
      **  Check details for investment type, book, market and security.
      *
     C                     EXSR SRITYP
     C                     EXSR SRBOOK
     C                     EXSR SRMRKT
     C                     EXSR SRSECN
      *
      ** If no database error occurred, print new investment type line.
      *
     C           *INU7     IFEQ '0'
     C           *INU8     ANDEQ'0'
     C                     Z-ADD57        #ALCTR
     C                     Z-ADD57        #ALCT2
     C                     EXSR SROVRF
     C                     EXSR SROVR2
     C                     ADD  4         #ALCTR
     C                     ADD  4         #ALCT2
     C                     EXSR SROVRF
     C                     WRITEFMTP104
     C                     ADD  3         #ALCTR
     C                     ENDIF
      *
     C                     ELSE
      *
      **  If the book code has changed, print totals for security
      **  and market.
      *
     C           ATDBK     IFNE #ATDBK
     C                     EXSR SRUNRL
     C                     EXSR SRWSTL
     C                     EXSR SRWMTL
      *
      **  Format BOOK TOTAL for output.
      *
     C                     EXSR SRWBTL
      *
      **  Check details for book, market and security.
      *
     C                     EXSR SRBOOK
     C                     EXSR SRMRKT
     C                     EXSR SRSECN
      *
      **  If no database error occurred, print new page with new
      **  book details.
      *
     C           *INU7     IFEQ '0'
     C           *INU8     ANDEQ'0'
     C                     MOVE '1'       *IN30
     C                     EXSR SRHEAD
     C                     MOVE '0'       *IN30
     C                     ENDIF
      *
     C                     ELSE
      *
      **  If the market indicator has changed, print totals for
      **  security.
      *
     C           ATDMI     IFNE #ATDMI
     C                     EXSR SRUNRL
     C                     EXSR SRWSTL
      *
      **  Print market total.
      *
     C                     EXSR SRWMTL
      *
      ** Check details for market, and security.
      *
     C                     EXSR SRMRKT
     C                     EXSR SRSECN
      *
      ** If no database errors occurred, print new page with new
      ** market details.
      *
     C           *INU7     IFEQ '0'
     C           *INU8     ANDEQ'0'
     C                     MOVE '1'       *IN30
     C                     EXSR SRHEAD
     C                     MOVE '0'       *IN30
     C                     ENDIF
      *
     C                     ELSE
      *
      **  If the security shortname has changed, print totals for
      **  security.
      *
     C           ATDSS     IFNE #ATDSS
     C                     EXSR SRUNRL
     C                     EXSR SRWSTL
      *
      **  Check details for security.
      *
     C                     EXSR SRSECN
      *
      **  If no database error was encountered, print new security
      **  line.
      *
     C           *INU7     IFEQ '0'
     C           *INU8     ANDEQ'0'
     C                     EXSR SROVRF
     C                     WRITEFMTP104
     C                     ADD  3         #ALCTR
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
      *
      **  If not database error occurred, calculate next coupon date
      **  using the trade value date as the event control date.
      *
     C           *INU7     IFEQ '0'
     C           *INU8     ANDEQ'0'
     C                     Z-ADDECD       SVECD   50
     C                     Z-ADDATDVD     ECD     50
     C                     EXSR ZNCD
      *
      **  Check if security is an Australian Yield security (trade
      **  basis = 'Y' and yield basis = 'AU'
      **  OR trade basis = 'Y' and yield basis = 'IA').
      *
     C           STBS      IFEQ 'Y'
     C           SYBS      ANDEQ'AU'
     C           STBS      OREQ 'Y'
     C           SYBS      ANDEQ'IA'
     C                     MOVE '1'       *IN24
     C                     ENDIF
      *
      **  Write details. Then, read next record.
      *
     C                     EXSR SRWRIT
     C                     Z-ADDSVECD     ECD
     C                     READ SETREXV1                 21
      *
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           SRDET9    ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  Subroutine: SRBRAN - Branch Details Check                    *
      *                                                               *
      *  Called By : SRDETL - Record Detail Processing                *
      *                                                               *
      *****************************************************************
      *
     C           SRBRAN    BEGSR
      *
      **  Access Branch Details using the branch code as key field.
      *
     C                     MOVE ATDBR     PBRCD
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM '*MSG   ' PRTCD   7
     C                     PARM '*KEY   ' POPTN   7
     C                     PARM           PBRCD   3
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'SDBRCHPD'DBFILE
     C                     MOVELATDBR     DBKEY            ***************
     C                     Z-ADD2         DBASE            * DB ERROR 02 *
     C                     SETON                     U7U8LR***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ELSE
     C                     MOVE ATDBR     #ATDBR  3
     C                     ENDIF
      *
     C           SRBRA9    ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  Subroutine: SRBOOK - Book Details Check                      *
      *                                                               *
      *  Called By : SRDETL - Record Detail Processing                *
      *                                                               *
      *****************************************************************
      *
     C           SRBOOK    BEGSR
      *
      **  If no database error was encountered, chain to Book Details
      **  file.
      *
     C           *INU7     IFEQ '0'
     C           *INU8     ANDEQ'0'
      *
     C                     MOVE ATDBK     PBOOK
     C                     CALL 'AOBOOKR0'
     C                     PARM '*MSG   ' PRTCD   7
     C                     PARM '*KEY   ' POPTN   7
     C                     PARM           PBOOK   2
     C           SDBOOK    PARM SDBOOK    DSFDY
      *
      *
      **  If no record found, DATABASE ERROR.
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'SDBOOKPD'DBFILE
     C                     MOVELATDBK     DBKEY            ***************
     C                     Z-ADD3         DBASE            * DB ERROR 03 *
     C                     SETON                     U7U8LR***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ELSE
     C                     MOVE ATDBK     #ATDBK  2
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           SRBOO9    ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  Subroutine: SRNCCY - Nominal Currency Details Check          *
      *                                                               *
      *  Called By : SRDETL - Record Detail Processing                *
      *                                                               *
      *****************************************************************
      *
     C           SRNCCY    BEGSR
      *
      **  If no database error was encountered before, access the
      **  Currencies file.
      *
     C           *INU7     IFEQ '0'
     C           *INU8     ANDEQ'0'
      *
     C                     MOVE ATNMC     PCURKY
     C                     CALL 'AOCURRR0'
     C                     PARM '*MSG   ' PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM           PCURKY  3
     C           SDCURR    PARM SDCURR    DSFDY
      *
      **  Generate DATABASE ERROR if no record was found.
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'SDCURRPD'DBFILE
     C                     MOVELATNMC     DBKEY            ***************
     C                     Z-ADD4         DBASE            * DB ERROR 04 *
     C                     SETON                     U7U8LR***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ELSE
     C                     MOVE ATNMC     #ATNMC  3
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           SRNCC9    ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  Subroutine: SRMRKT - Market Indicator Check                  *
      *                                                               *
      *  Called By : SRDETL - Record Detail Processing                *
      *                                                               *
      *  Calls     : None                                             *
      *                                                               *
      *****************************************************************
      *
     C           SRMRKT    BEGSR
      *
      **  If there were no previous database errors, check market
      **  indicator whether it is primary, secondary, or grey.
      *
     C           *INU7     IFEQ '0'
     C           *INU8     ANDEQ'0'
      *
     C                     MOVE '0'       *IN25
     C                     MOVE '0'       *IN26
     C                     MOVE '0'       *IN27
      *
     C           ATDMI     IFEQ 'P'
     C                     MOVE '1'       *IN25
     C                     ELSE
      *
     C           ATDMI     IFEQ 'S'
     C                     MOVE '1'       *IN26
     C                     ELSE
      *
     C           ATDMI     IFEQ 'G'
     C                     MOVE '1'       *IN27
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     MOVE ATDMI     #ATDMI  1
      *
     C                     ENDIF
      *
     C           SRMRK9    ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  Subroutine: SRITYP - Investment Type Check                   *
      *                                                               *
      *  Called By : SRDETL - Record Detail Processing                *
      *                                                               *
      *****************************************************************
      *
     C           SRITYP    BEGSR
      *
      **  If no database error occurred previously, chain to Investment
      **  Type Details file.
      *
     C           *INU7     IFEQ '0'
     C           *INU8     ANDEQ'0'
      *
     C           ITKEY     CHAININVTPDF              22
      *
      **  If no equivalent record exists, DATABASE ERROR.
      *
     C           *IN22     IFEQ '1'
     C           RECI      ORNE 'D'
     C           *LOCK     IN   LDA
     C                     MOVE 'INVTP   'DBFILE
     C                     MOVE INKEY     DBKEY            ***************
     C                     Z-ADD05        DBASE            * DB ERROR 05 *
     C                     SETON                     U7U8LR***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ELSE
     C                     MOVE AINVT     #AINVT  3
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           SRITY9    ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  Subroutine: SRSECN - Security Details Check                  *
      *                                                               *
      *  Called By : SRDETL - Record Detail Processing                *
      *                                                               *
      *  Calls     : ZSDESC - Format Security Description             *
      *                                                               *
      *****************************************************************
      *
     C           SRSECN    BEGSR
      *
      **  If there were no previous database errors, chain to Security
      **  Details file.
      *
     C           *INU7     IFEQ '0'
     C           *INU8     ANDEQ'0'
      *
     C           ATDSS     CHAINSECTY                22
      *
      **  Generate DATABASE ERROR if no equivalent record exists. Else,
      **  format corresponding security report name.
      *
     C           *IN22     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE 'SECTY   'DBFILE
     C                     MOVE ATDSS     DBKEY            ***************
     C                     Z-ADD06        DBASE            * DB ERROR 06 *
     C                     SETON                     U7U8LR***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ELSE
     C                     MOVE ATDSS     #ATDSS 10
     C                     MOVE ATDSS     #ATDS2 10
     C                     MOVE SRPN      ZSRPN
     C                     EXSR ZSDESC
     C                     MOVE ZRNME     SECNM  41
     C                     MOVE ZRNME     SECNM2 41
      *
      **  Format MARKET YIELD for output.
      *
     C                     MOVE *BLANKS   ZFLD15
     C                     Z-ADD8         ZDECS
     C                     MOVE 'L'       ZECODE
     C                     MOVE MKPR      ZFLD15
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    #AMYLD
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           SRSEC9    ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  Subroutine: SRWBTL - Write Book Totals                       *
      *                                                               *
      *  Called By : SRDETL - Record Detail Processing                *
      *                                                               *
      *  Calls     : ZSEDIT - Format Amount                           *
      *              SRNEGV - Format Negative Amount                  *
      *              SROVRF - Check Overflow Line                     *
      *                                                               *
      *****************************************************************
      *
     C           SRWBTL    BEGSR
      *
     C                     MOVE '0'       *IN44
      *
      **  If book total is less than zero, set on *IN44 to indicate a
      **  negative amount.
      *
     C           #ABTTL    IFLT *ZERO
     C                     MOVE '1'       *IN44
     C                     Z-SUB#ABTTL    #ABTTL
     C                     ENDIF
      *
      **  Format BOOK TOTAL for output.
      *
     C                     Z-ADD0         #ABTTL 150
     C                     Z-ADD3         Y       20
     C                     CLEAR#TOT
     C                     MOVE 'BOOK'    #TOT   10
     C                     EXSR SRWSTL
      *
     C           SRWBT9    ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  Subroutine: SRWCTL - Write Currency Totals                   *
      *                                                               *
      *  Called By : SRDETL - Record Detail Processing                *
      *                                                               *
      *  Calls     : ZSEDIT - Format Amount                           *
      *              SRNEGV - Format Negative Amount                  *
      *              SROVRF - Check Overflow Line                     *
      *                                                               *
      *****************************************************************
      *
     C           SRWCTL    BEGSR
      *
     C           SRWCT9    ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  Subroutine: SRWMTL - Write Market Totals                     *
      *                                                               *
      *  Called By : SRDETL - Record Detail Processing                *
      *                                                               *
      *  Calls     : ZSEDIT - Format amount                           *
      *              SRNEGV - Format negative amount                  *
      *              SROVRF - Check overflow line                     *
      *                                                               *
      *****************************************************************
      *
     C           SRWMTL    BEGSR
      *
     C                     MOVE '0'       *IN42
      *
      **  Add market total to currency total.
      *
     C                     ADD  #AMTTL    #ACTTL 150
      *
      **  If the market total is less than zero, set on *IN42 to
      **  indicate a negative amount.
      *
     C           #AMTTL    IFLT *ZERO
     C                     MOVE '1'       *IN42
     C                     Z-SUB#AMTTL    #AMTTL
     C                     ENDIF
     C                     Z-ADD0         #AMTTL 150
     C                     Z-ADD4         Y       20
     C                     CLEAR#TOT
     C                     MOVE 'MARKET'  #TOT   10
     C                     EXSR SRWSTL
      *
     C           SRWMT9    ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  Subroutine: SRWITL - Write Investment Type Totals            *
      *                                                               *
      *  Called By : SRDETL - Record Detail Processing                *
      *                                                               *
      *  Calls     : ZSEDIT - Format Amount                           *
      *              SRNEGV - Format Negative Amount                  *
      *              SROVRF - Check Overflow Line                     *
      *                                                               *
      *****************************************************************
      *
     C           SRWITL    BEGSR
      *
     C                     MOVE '0'       *IN41
      *
      **  Add investment type total to market total.
      *
     C                     ADD  #AITTL    #AMTTL
      *
      **  If the investment type total is less than zero, set on *IN41
      **  to indicate a negative amount.
      *
     C           #AITTL    IFLT *ZERO
     C                     MOVE '1'       *IN41
     C                     Z-SUB#AITTL    #AITTL
     C                     ENDIF
      *
      **  Format INVESTMENT TYPE TOTAL for output.
      *
     C                     Z-ADD0         #AITTL 150
     C                     Z-ADD2         Y       20
     C                     CLEAR#TOT
     C                     MOVE '1'       *IN40
     C                     EXSR SRWSTL
     C                     MOVE '0'       *IN40
      *
     C           SRWIT9    ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  Subroutine: SRWSTL - Write Security Totals                   *
      *                                                               *
      *  Called By : SRDETL - Record Detail Processing                *
      *                                                               *
      *  Calls     : ZSEDIT - Format Amount                           *
      *              SRNEGV - Format Negative Amount                  *
      *              SROVRF - Check Overflow Line                     *
      *                                                               *
      *****************************************************************
      *
     C           SRWSTL    BEGSR
      *
      **  Set off indicators.
      *
     C                     SETOF                     3132
     C                     SETOF                     3334
     C                     SETOF                     35
      *
     C           Y         IFEQ 0
     C                     Z-ADD5         Y
     C                     CLEAR#TOT
     C                     MOVE #ATDSS    #TOT
     C                     ENDIF
      *
      **  Add profit & loss total to investment type total.
      *
     C                     ADD  #APTTL    #AITTL
      *
      **  Suppress printing of book and market totals.
      *
     C           Y         IFNE 3
     C           Y         ANDNE4
      *
      **  If the nominal total is less than zero, set on *IN31 to
      **  indicate a negative amount.
      *
     C                     Z-ADDNOM,Y     #ATOTN
     C           #ATOTN    IFLT *ZERO
     C                     MOVE '1'       *IN31
     C                     ENDIF
      *
      **  Format NOMINAL TOTAL for output.
      *
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE #ATOTN    ZFLD15
     C                     MOVE NMDP      ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    #ANTOT
      *
     C                     Z-ADDCAP,Y     #ATOTS
      *
      **  Format SETTLEMENT VALUE TOTAL for output.
      *
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE #ATOTS    ZFLD15
     C                     MOVE A6NBDP    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    #ASTOT
      *
     C                     Z-ADDMKT,Y     #ATOTM
      *
      **  Format MARKET VALUE TOTAL for output.
      *
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE #ATOTM    ZFLD15
     C                     MOVE A6NBDP    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    #AMTOT
      *
      **  If the profit & loss total is less than zero, set on *IN34 to
      **  indicate a negative amount.
      *
     C                     Z-ADDPL,Y      #APTTL
      *
      ** Format PROFIT & LOSS TOTAL for output.
      *
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE #APTTL    ZFLD15
     C                     MOVE A6NBDP    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    #APLTL
      *
      **  If the discount total is less than zero, set on *IN34 to
      **  indicate a negative amount.
      *
     C                     Z-ADDDSC,Y     #ATOTD
      *
      **  Format DISCOUNT TOTAL for output.
      *
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE #ATOTD    ZFLD15
     C                     MOVE A6NBDP    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    #ADTOT
      *
      **  Format WEIGHTED AVERAGE YIELD for output.
      *
     C                     Z-ADDWAY,Y     #WWAY
     C           #ATOTN    IFNE *ZERO
     C           #WWAY     DIV  #ATOTN    #ATOTY 118H
     C                     ELSE
     C                     Z-ADD*ZERO     #ATOTY
     C                     ENDIF
     C           #ATOTY    IFLT *ZERO
     C                     Z-SUB#ATOTY    #ATOTY
     C                     ENDIF
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE #ATOTY    ZFLD15
     C                     MOVE 8         ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    #AYTOT
      *
      ** Subtract new total Unrealised P/L from Unrealised P/L todate
      ** (BKPHDD) for Unrealised P/L if it is not the Grand Total
      *
     C           *IN40     IFEQ '0'
     C           #APTTL    SUB  #AUPPT    #APLTY 150
     C                     ADD  #APLTY    #ATOTP
     C                     ENDIF
      *
      **  If the profit & loss total is less than zero, set on *IN34 to
      **  indicate a negative amount.
      *
     C           #APTTL    IFLT *ZERO
     C                     MOVE '1'       *IN34
     C                     Z-SUB#APTTL    #APTTL
     C                     ENDIF
      *
      **  Format Unreal P/L Today for output
      *
     C                     MOVE *BLANKS   ZFLD15
     C   40                MOVE #ATOTP    ZFLD15
     C  N40                MOVE #APLTY    ZFLD15
     C                     MOVE A6NBDP    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    #APTOT
     C   40                Z-ADD0         #ATOTP
     C           #TOT      IFEQ CONST
     C                     DUMP
     C                     ENDIF
      *
      **  Print totals.
      *
     C                     EXSR SROVRF
     C                     WRITEFMTP106
      *
     C                     MOVE #TOT      EIGHT   8
     C           EIGHT     IFEQ 'SECURITY'
     C                     EXSR SROVR2
     C                     WRITEFMTP204
     C                     ELSE
     C                     EXSR SROVR2
      *
      **  Calculate way market
      *
     C           #ATOTN    IFNE *ZERO
     C           WAM,Y     DIV  #ATOTN    FLD118 118H
     C                     ELSE
     C                     Z-ADD*ZERO     FLD118
     C                     ENDIF
     C           FLD118    IFLT *ZERO
     C                     Z-SUBFLD118    FLD118
     C                     ENDIF
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE FLD118    ZFLD15
     C                     MOVE 8         ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    #AMYLD
      *
     C                     WRITEFMTP206
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ADD  2         #ALCTR
     C                     ADD  2         #ALCT2
     C                     Z-ADD0         #ATOTN 150
     C                     Z-ADD0         #ATOTS 150
     C                     Z-ADD0         #ATOTM 150
     C                     Z-ADD0         #APTTL 150
     C                     Z-ADD0         #ATOTD 150
     C                     Z-ADD0         #WWAY  228
     C                     Z-ADD*ZERO     NOM,Y
     C                     Z-ADD*ZERO     CAP,Y
     C                     Z-ADD*ZERO     MKT,Y
     C                     Z-ADD*ZERO     PL,Y
     C                     Z-ADD*ZERO     DSC,Y
     C                     Z-ADD*ZERO     WAY,Y
     C                     Z-ADD*ZERO     WAM,Y
     C                     Z-ADD*ZERO     PLT,Y
     C                     Z-ADD*ZERO     Y
      *
     C           SRWST9    ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  Subroutine: SRWRIT - Write Details                           *
      *                                                               *
      *  Called By : SRDETL - Record Detail Processing                *
      *                                                               *
      *  Calls     : SROVRF - Check Overflow Line                     *
      *              SRNEGV - Format Negative Amount                  *
      *              ZDATE2 - Convert Day Number to Date Formats      *
      *              ZSEDIT - Format Amount                           *
      *              ZPRCI  - Convert Price To %Price                 *
      *              ZPRCO  - Convert Price to Yield                  *
      *              ZACCZ  - Calculate Accrued Interest              *
      *              ZYCE   - Determine if in CUM- or EX- COUPON      *
      *                       date range                              *
      *                                                               *
      *****************************************************************
      *
     C           SRWRIT    BEGSR
      *
      **  Set off indicators.
      *
     C                     SETOF                     394546
     C                     SETOF                     4748
     C                     SETOF                     49
      *
      **  Format TRADE VALUE DATE for output.
      *
     C           AUTDRF    IFNE 'VDPOSN'
     C                     Z-ADDATDVD     ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZADATE    #ATRAD
     C                     ELSE
     C                     MOVE '1'       *IN39
     C                     ENDIF
      *
      **  Add nominal amount to nominal total.
      *
     C                     ADD  AFNOML    #ATOTN
      *
      **  If the nominal amount is less than zero, set on *IN45 to
      **  indicate a negative amount.
      *
     C           ANOML     IFLT *ZERO
     C                     MOVE '1'       *IN45
     C                     ENDIF
     C                     Z-ADDANOML     #ANOM  150
      *
      **  Format NOMINAL for output.
      *
     C                     MOVE NMDP      ZDECS
     C                     MOVE 'J'       ZECODE
     C                     Z-ADDAFNOML    ZFLD15
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    #ANOML
      *
      **  Initialise factorised nominal to nominal amount.
      *
     C                     Z-ADDAFNOML    #AFNOM 194
      *
      **  CEU018     - To cater for decimal places on nominal...                              CEU018
      *
     C           PROT      IFEQ '1'                                                           CEU018
     C           PROT      OREQ '3'                                                           CEU018
     C           PROT      OREQ '9'                                                           CEU018
     C           5         SUB  NMDP      #DP     10                                          CEU018
     C           #AFNOM    MULT POWER8,#DP#AFNOM                                              CEU018
     C           #ANOM     MULT POWER8,#DP#ANOM                                               CEU018
     C                     ENDIF                                                              CEU018
      *
      **  Convert trade price from %price to yield.
      *
     C                     Z-ADDAPRIC     ZPRICE
     C           AUTDRF    IFEQ 'VDPOSN'
     C                     Z-ADD@WECD     #VDAT   50
     C                     SETON                     66
     C                     ELSE
     C                     Z-ADDATDVD     #VDAT
     C                     ENDIF
     C                     Z-ADDATDVD     ZFDATE
     C                     MOVE 'Y'       #WMODE  1
      *
     C                     MOVE *BLANKS   #WRTCD 10
     C           ANOML     IFEQ *ZEROS
     C                     Z-ADD0         ZPRCOT
     C                     ELSE
     C                     CALL 'ZPRCCAL'
     C                     PARM           ATDSS
     C                     PARM           APRIC
     C                     PARM           #VDAT
     C                     PARM           #WMODE
     C                     PARM           ZPRCOT
     C                     PARM           #WRTCD
     C                     PARM           #ANOM
     C                     PARM 'N'       PDBG    1
     C                     ENDIF
      *
      **  Format DEALT YIELD for output.
      *
     C                     MOVE *BLANKS   ZFLD15
     C                     Z-ADD8         ZDECS
     C                     MOVE 'L'       ZECODE
     C                     MOVE ZPRCOT    ZFLD15
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    #ADYLD
      *
      **   ACCUMULATE YIELD * FACE VALUE for WEIGHTED AVERAGE YIELD calculation
      *
     C           ZPRCOT    MULT AFNOML    #WK228 228
     C                     ADD  #WK228    #WWAY
      *
      **   ACCUMULATE YIELD * FACE VALUE for WAY MARKET
      *
     C           MKPR      MULT AFNOML    WWK228 228
      *
      **  Format MARKET YIELD for output.
      *
     C                     MOVE *BLANKS   ZFLD15
     C                     Z-ADD8         ZDECS
     C                     MOVE 'L'       ZECODE
     C                     MOVE MKPR      ZFLD15
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    #AMYLD
      *
      **  Reset currency decimal places based on the nominal currency.
      *
     C                     MOVEA'0000'    *IN,35
     C           35        ADD  A6NBDP    J       20
     C                     MOVEA'1'       *IN,J
     C                     Z-ADDA6NBDP    NCDP
      *
      **  Initialise fields to contain settlement value.
      *
     C                     Z-ADD*ZEROS    #ASETV 150
     C                     Z-ADD*ZEROS    #ASEV0 150
     C                     Z-ADD*ZEROS    #ASEV1 151
     C                     Z-ADD*ZEROS    #ASEV2 152
     C                     Z-ADD*ZEROS    #ASEV3 153
     C           APRIC     DIV  100       #WPRIC 159H
      *
      **  Settlement Value = Price x Factorised Nominal
      *
     C   35      #WPRIC    MULT #ANOM     #ASEV0    H
     C   36      #WPRIC    MULT #ANOM     #ASEV1    H
     C   37      #WPRIC    MULT #ANOM     #ASEV2    H
     C   38      #WPRIC    MULT #ANOM     #ASEV3    H
      *
     C   35                MOVE #ASEV0    #ASETV
     C   36                MOVE #ASEV1    #ASETV
     C   37                MOVE #ASEV2    #ASETV
     C   38                MOVE #ASEV3    #ASETV
      *
     C                     Z-ADD#ASETV    #ASET  150
      *
      **  Format SETTLEMENT VALUE for output.
      *
     C                     MOVE A6NBDP    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     Z-ADD#ASET     ZFLD15
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    #ASVAL
      *
      **  Add settlement value to settlement total.
      *
     C                     ADD  #ASETV    #ATOTS
      *
      **  Initialise fields to contain premium/discount total.
      *
     C                     Z-ADD*ZEROS    #ADSC  150
     C                     Z-ADD*ZEROS    #ADSC0 150
     C                     Z-ADD*ZEROS    #ADSC1 151
     C                     Z-ADD*ZEROS    #ADSC2 152
     C                     Z-ADD*ZEROS    #ADSC3 153
      *
      **  Discount = Factorised Nominal - Book value
      *
     C   35      #AFNOM    SUB  #ASEV0    #ADSC0    H
     C   36      #AFNOM    SUB  #ASEV1    #ADSC1    H
     C   37      #AFNOM    SUB  #ASEV2    #ADSC2    H
     C   38      #AFNOM    SUB  #ASEV3    #ADSC3    H
      *
     C   35                MOVE #ADSC0    #ADSC
     C   36                MOVE #ADSC1    #ADSC
     C   37                MOVE #ADSC2    #ADSC
     C   38                MOVE #ADSC3    #ADSC
      *
      **  If the discount value is less than zero, set on *IN49 to
      **  indicate a negative amount.
      *
      **  Determine if premium or discount
      *
     C           #ASET     IFGT AFNOML
     C           #ADSC     ANDLT0
     C                     Z-SUB#ADSC     #ADSC            premium(+)
     C                     ENDIF
      *
     C           #ASET     IFLE AFNOML
     C           #ADSC     ANDGT0
     C                     Z-SUB#ADSC     #ADSC            discount(-)
     C                     ENDIF
      *
      **  Add discount value to discount total.
      *
     C                     ADD  #ADSC     #ATOTD
      *
      **  Format DISCOUNT AMOUNT for output.
      *
     C                     MOVE A6NBDP    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     Z-ADD#ADSC     ZFLD15
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    #ADISC
      *
      **  If the market price is zero, zeroise and format MARKET VALUE
      **  for output.
      *
     C           MKPR      IFEQ *ZERO
     C                     Z-ADD*ZERO     #AMARV
     C                     MOVE *BLANKS   ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     MOVE 'L'       ZECODE
     C                     MOVE #AMARV    ZFLD15
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    #AMVAL
     C                     Z-ADD*ZERO     ZPRCOT
     C                     ELSE
      *
      **  If the market price and the nominal are not zero and the
      **  security is an Australian yield security, calculate accrued
      **  interest using the last coupon date, trade value date, and
      **  nominal amount as inputs.
      *
     C           ANOML     IFNE *ZERO
     C           *IN24     ANDEQ'1'
     C                     Z-ADDLCPN      ZDCSDT
     C                     Z-ADDATDVD     ZDCEDT
     C                     Z-ADDANOML     ZAMT
     C                     EXSR ZACCZ
      *
      **  Purchased Interest = Accrued Interest / Nominal Amount
      *
     C           ZDCINT    DIV  ANOML     WRKPI     H
      *
      **  Determine if the trade value date is in the cum- or ex-
      **  coupon date range.
      *
     C                     Z-ADDATDVD     WRKD    50
     C                     EXSR ZYCE
     C                     MOVELSCUMEX    CUMEXF
      *
      **  If it is within the ex-coupon range, calculate accrued
      **  interest using the last coupon date as the start date, the
      **  next coupon date as the end date.
      *
     C           SCUMEX    IFEQ 'X'
     C                     Z-ADDLCPN      ZDCSDT
     C                     Z-ADDNCD       ZDCEDT
     C                     EXSR ZACCZ
      *
      **  Calculate the work purchased interest.
      **  Final Purchased Int. = Purchased Int. - Work Purchased Int.
      *
     C           ZDCINT    DIV  ANOML     #AWKPI 159H
     C           WRKPI     SUB  #AWKPI    WRKPI
     C                     ENDIF
      *
      **  If the trade value date is in the cum- coupon date range,
      **  zeroise the purchased interest.
      *
     C                     ELSE
     C                     MOVE 'C'       SCUMEX
     C                     Z-ADD*ZERO     WRKPI
     C                     ENDIF
      *
      **  Calculate the %price.
      *
     C                     MOVE MKPR      ZPRCIN
     C                     MOVE 'P'       #WMODE  1
      *
     C                     MOVE *BLANKS   #WRTCD 10
     C           ANOML     IFEQ *ZEROS
     C                     Z-ADD0         ZPRCOT
     C                     ELSE
     C                     CALL 'ZPRCCAL'
     C                     PARM           ATDSS
     C                     PARM           ZPRCIN
     C                     PARM           #VDAT
     C                     PARM           #WMODE
     C                     PARM           ZPRCOT
     C                     PARM           #WRTCD
     C                     PARM           #ANOM
     C                     PARM 'N'       PDBG    1
     C                     ENDIF
      *
      **  Initialise fields to contain the market value.
      *
     C                     Z-ADD*ZEROS    #AMARV 150
     C                     Z-ADD*ZEROS    #AMAV0 150
     C                     Z-ADD*ZEROS    #AMAV1 151
     C                     Z-ADD*ZEROS    #AMAV2 152
     C                     Z-ADD*ZEROS    #AMAV3 153
      *
      **  Market Value = Market %Price x Factorised Nominal
      *
     C           ZPRCOT    DIV  100       #WPRIC    H
     C   35      #WPRIC    MULT #ANOM     #AMAV0    H
     C   36      #WPRIC    MULT #ANOM     #AMAV1    H
     C   37      #WPRIC    MULT #ANOM     #AMAV2    H
     C   38      #WPRIC    MULT #ANOM     #AMAV3    H
      *
     C   35                MOVE #AMAV0    #AMARV
     C   36                MOVE #AMAV1    #AMARV
     C   37                MOVE #AMAV2    #AMARV
     C   38                MOVE #AMAV3    #AMARV
      *
     C                     Z-ADD#AMARV    #AMAR  150
      *
      **  Format MARKET VALUE for output.
      *
     C                     MOVE A6NBDP    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     Z-ADD#AMAR     ZFLD15
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    #AMVAL
      *
     C                     ENDIF
      *
      **  Add market value to market total.
      *
     C                     ADD  #AMARV    #ATOTM
      *
      **  If the market price is zero, the profit & loss will be zero
      **  and will be printed as '***'.
      *
     C                     MOVE '0'       *IN29
     C                     EXSR SRPRIC
     C           MKPR      IFEQ *ZEROS
     C           *IN59     ANDEQ'1'
     C                     MOVE '1'       *IN29
     C                     Z-ADD*ZEROS    #APANL
     C                     ELSE
      *
      **  If the market price is not zero,
      **  Profit & Loss = Market Value - Settlement Value
      *
     C           MKPR      IFNE 0
     C           #AMARV    SUB  #ASETV    #APANL 150
     C                     ELSE
     C                     MOVE '1'       *IN29
     C                     Z-ADD0         #APANL
     C                     ENDIF
      *
      **  If the profit & loss is less than zero, set on *IN48 to
      **  indicate a negative amount.
      *
     C                     Z-ADD#APANL    #APAL  150
      *
      **  Format PROFIT & LOSS for output.
      *
     C                     MOVE A6NBDP    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     Z-ADD#APAL     ZFLD15
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    #APRLO
      *
     C                     ENDIF
      *
      **  Access BKPHDD file for Unrealised P/L Posted ToDate figure.
      *
     C                     MOVE 'T'       TVDB
     C                     EXSR SRBKPH
      *
      **  Add profit & loss to profit & loss total.
      *
     C                     ADD  #APANL    #APTTL
      *
      ** Calculation of Unrealised P/L todate should be done during
      ** total calculation time (#AWSTL)
      *
     C                     Z-ADDUPPT      #AUPPT 130
     C                     Z-ADDPUPPT     #PUPPT 130
      *
      **  Write detail line.
      *
     C           *IN77     IFEQ '1'
     C                     EXSR SROVRF
     C                     WRITEFMTP105
     C                     ADD  1         #ALCTR
      *
      **  Accumulate total registers
      *
     C                     Z-ADD1         X       20
     C                     DO   7         X
     C                     ADD  AFNOML    NOM,X
     C           *IN49     IFEQ '1'
     C                     SUB  #ADSC     DSC,X
     C                     ELSE
     C                     ADD  #ADSC     DSC,X
     C                     ENDIF
     C                     ADD  #WK228    WAY,X
     C                     ADD  WWK228    WAM,X
     C                     ADD  #ASETV    CAP,X
     C                     ADD  #AMARV    MKT,X
     C                     ADD  #APANL    PL,X
     C                     ENDDO
     C                     ENDIF
      *
     C           SRWRI9    ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  Subroutine: SROVRF - Check Overflow Line                     *
      *                                                               *
      *  Called By : MAIN   - Main Controlling Routine                *
      *              SRDETL - Record Detail Processing                *
      *              SRWBTL - Print Book Totals                       *
      *              SRWCTL - Print Currency Totals                   *
      *              SRWMTL - Print Market Totals                     *
      *              SRWITL - Print Investment Type Totals            *
      *              SRWSTL - Print Security Totals                   *
      *              SRWRIT - Write Details                           *
      *                                                               *
      *  Calls     : None                                             *
      *                                                               *
      *****************************************************************
      *
     C           SROVRF    BEGSR
      *
      **  If the line overflow, print a new page.
      *
     C           #ALCTR    IFGE 57
     C                     WRITEFMTP101
     C                     WRITEFMTP102
     C                     WRITEFMTP103
      *
     C                     Z-ADD16        #ALCTR  20
     C                     ENDIF
      *
     C           #AOVR9    ENDSR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *  Subroutine: SROVR2 - Check Overflow Line Printer File 2      *
      *                                                               *
      *  Calls     : None                                             *
      *                                                               *
      *****************************************************************
      *
     C           SROVR2    BEGSR
      *
      **  If the line overflow, print a new page.
      *
     C           #ALCT2    IFGE 57
      *
     C                     WRITEFMTP201
     C                     WRITEFMTP202
     C                     WRITEFMTP203
      *
     C                     Z-ADD16        #ALCT2  20
     C                     ENDIF
      *
     C           SROV29    ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  Subroutine: SRHEAD - Write Headings                          *
      *                                                               *
      *  Called By : SRDETL - Record Detail Processing                *
      *                                                               *
      *  Calls     : None                                             *
      *                                                               *
      *****************************************************************
      *
     C           SRHEAD    BEGSR
      *
     C                     WRITEFMTP101
     C                     WRITEFMTP102
     C                     WRITEFMTP103
     C                     WRITEFMTP104
      *
     C                     WRITEFMTP201
     C                     WRITEFMTP202
     C                     WRITEFMTP203
     C                     Z-ADD19        #ALCTR  20
     C                     Z-ADD19        #ALCT2  20
      *
     C           SRHEA9    ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  Subroutine: SRNEGV - Format Negative Value                   *
      *                                                               *
      *  Called By : SRWBTL - Print Book Totals                       *
      *              SRWCTL - Print Currency Totals                   *
      *              SRWMTL - Print Market Totals                     *
      *              SRWITL - Print Investment Type Totals            *
      *              SRWSTL - Print Security Totals                   *
      *              SRWRIT - Write Details                           *
      *                                                               *
      *  Calls     : None                                             *
      *                                                               *
      *****************************************************************
      *
     C           SRNEGV    BEGSR
      *
     C           *LIKE     DEFN AA        #ALEN
     C                     Z-ADD1         AA      20
      *
      **  Check if the length of the work array is 17.
      *
     C           #ALEN     IFEQ 17
      *
      **  Increment array index while it is not yet equal to the array
      **  length and while the element is a blank.
      *
     C           AA        DOWLT#ALEN
     C           AU1,AA    ANDEQ' '
     C                     ADD  1         AA
     C                     ENDDO
      *
      **  Place the bracket before the amount.
      *
     C                     SUB  1         AA
     C                     MOVE '('       AU1,AA
     C                     ENDIF
      *
      **  Check if the length of the work array is 23.
      *
     C           #ALEN     IFEQ 23
      *
      **  Increment array index while it is not yet equal to the array
      **  length and while the element is a blank.
      *
     C           AA        DOWLT#ALEN
     C           AU2,AA    ANDEQ' '
     C                     ADD  1         AA
     C                     ENDDO
      *
      **  Place the bracket before the amount.
      *
     C                     SUB  1         AA
     C                     MOVE '('       AU2,AA
     C                     ENDIF
      *
     C           SRNEG9    ENDSR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * SRUNRL  -  Subroutine to calculate Unrealized Profit/Loss and *
      *              to output Unrealized P/L Account Keys            *
      *                                                               *
      * Called By : SRDETL - Record Detail Pocessing                  *
      *                                                               *
      * Calls     : SRBKPH - Chain BKPHD                              *
      *             SRBKPO - Chain BKPOS                              *
      *             SRWRTD - Generate Account Keys                    *
      *                                                               *
      *****************************************************************
      *
     C           SRUNRL    BEGSR                           **  SRUNRL **
      *
     C                     MOVE *OFF      *IN50
     C                     MOVE 'T'       TVDB
     C                     MOVE 'T'       TVD2
     C                     SETOF                     52
      *
     C           *IN50     DOWNE'1'
      *
      ** Chain BKPHD - Book Position Headers.
      *
     C                     EXSR SRBKPH
      *
      **  Obtain Book Position Profit Centre Details
      *
     C                     EXSR SRPCHN
      *
      ** Check unrealised profit posting and basis before proceeding.
      *
     C           UPPS      IFNE *BLANK
     C           UPBS      IFEQ 'B'
     C           UPBS      OREQ BHTV
      *
      ** Unrealised p/l is set to zero of market price is zero
      *
     C           MKPR      IFEQ 0
     C                     EXSR SRPRIC
     C                     ENDIF
      *
      ** If market price exists but zero, do unrealised P/L
      ** processing.
      *
     C           MKPR      IFEQ 0
     C           *IN59     ANDEQ'1'
     C                     Z-ADD0         UPPT
     C                     ELSE
      *
      ** store unrelised p/l as previous
      *
     C                     Z-ADDUPPT      PUPPT  130
      *
      ** Chain BKPOS(Book Position) and then read previous to
      ** get old previous NPSN.
      *
     C                     EXSR SRBKPO
      *
      ** Access the save BKPOS record to find old nominal position
      *
     C           *LIKE     DEFN NPSN      PNPSN
     C           BKHKEY    CHAINSEBKHSVF             80
     C           *IN80     IFEQ *OFF
     C           SEOKEY    CHAINSEBKPSVF             80
     C           *IN80     IFEQ *ON
     C                     Z-ADD*ZERO     PNPSN
     C                     ELSE
     C                     Z-ADDAUNPSN    PNPSN
     C                     ENDIF
     C                     ELSE
     C                     Z-ADD*ZERO     PNPSN
     C                     ENDIF
      *
      ** Get New UPPT from Profit & Loss Total
      *
     C                     Z-ADD#APTTL    UPPT
      *
      ** Update BKPHD - Book Position Headers.
      *
     C           SUCESS    IFEQ '1'
     C                     EXCPTUPDBKH
     C                     ENDIF
      *
      ** Set common fields for account keys
      *
     C                     MOVE 'A'       EVTP
     C                     MOVE BHTV      PSBS
     C                     MOVE ' '       FILL
      *
      ** Allow negative position accounting for unrealised p/l.
      *
     C           NPSN      IFGE 0
     C           NPAC      OREQ 'N'
     C                     MOVE ' '       NEGI
     C                     ELSE
     C                     MOVE 'N'       NEGI
     C                     ENDIF
      *
      ** Set indicator for new and previous unrealised p/l
      *
     C           UPPT      IFLE 0
     C           PUPPT     ANDLE0
     C                     MOVE '1'       *IN52
     C                     ELSE
     C           UPPT      IFGE 0
     C           PUPPT     ANDGE0
     C                     MOVE '1'       *IN52
     C                     ENDIF
     C                     ENDIF
      *
      **  Produce account keys if new unrealised p/l greater than 0
      **  and posting basis is not 'L' or new unrealised p/l is less
      **  than zero and profit posting is not 'P'
      *
     C           UPPT      IFGT 0
     C           UPPS      ANDNE'L'
     C                     GOTO UNRLKY
     C                     ELSE
     C           UPPT      IFLT 0
     C           UPPS      ANDNE'P'
     C                     GOTO UNRLKY
     C                     ELSE
     C           PUPPT     IFGT 0
     C           UPPS      ANDNE'L'
     C                     GOTO UNRLKY
     C                     ENDIF
     C           PUPPT     IFLT 0
     C           UPPS      ANDNE'P'
     C                     GOTO UNRLKY
     C                     ENDIF
     C                     SETOF                     66
     C                     GOTO SRUNR9
     C                     ENDIF
     C                     ENDIF
      *
     C           UNRLKY    TAG
      *
      ** If negative position on accounting is used....
      ** If position has changed from + to - or vice-versa, produce 2
      ** keys to reverse previous UP/UL and post new UP/UL
      *
     C           NPAC      IFEQ 'Y'
      *
      ** If no previous position found, then there has been no change
      *
     C           NPSN      IFGE 0
     C           PNPSN     ANDLT0
     C           NPSN      ORLT 0
     C           PNPSN     ANDGE0
      *
     C           PNPSN     IFLT 0
     C                     MOVE 'N'       NEGI
     C                     ELSE
     C                     MOVE ' '       NEGI
     C                     ENDIF
     C           PUPPT     IFLT 0
     C                     MOVE 'UL'      AMCD
     C                     Z-ADDPUPPT     EVAM
     C                     ELSE
     C                     MOVE 'UP'      AMCD
     C                     Z-SUBPUPPT     EVAM
     C                     ENDIF
      *
     C           EVAM      IFNE 0
     C                     EXSR SRWRTD
     C                     ENDIF
      *
     C           NPSN      IFLT 0
     C                     MOVE 'N'       NEGI
     C                     ELSE
     C                     MOVE ' '       NEGI
     C                     ENDIF
     C           UPPT      IFLT 0
     C                     MOVE 'UL'      AMCD
     C                     Z-SUBUPPT      EVAM
     C                     ELSE
     C                     MOVE 'UP'      AMCD
     C                     Z-ADDUPPT      EVAM
     C                     ENDIF
      *
     C           EVAM      IFNE 0
     C                     EXSR SRWRTD
     C                     ENDIF
     C                     SETOF                     66
     C                     GOTO SRUNR9
      *
     C                     ENDIF
     C                     ENDIF
      *
      ** -  Produce one key if previous and new unreal. p/l both less
      **     than or both greater than 0
      *
     C           *IN52     IFEQ '1'
      *
     C           UPPT      IFGT 0
      *
      ** Since UPPT may be zero, need to check PUPPT as well in deter-
      ** mining whether this posting is a change to profit or loss.
      *
     C           PUPPT     ORGT 0
     C                     MOVE 'UP'      AMCD
     C                     ELSE
     C                     MOVE 'UL'      AMCD
     C                     ENDIF
      *
     C           UPPT      SUB  PUPPT     EVAM
     C           UPPT      IFLT 0
      *
     C           PUPPT     ORLT 0
      *
      ** If calculating change to UL, have just subtracted a negative
      ** PUPPT; must reverse sign regardless of whether EVAM is +/-ve.
      *
     C           EVAM      MULT -1        EVAM
     C                     ENDIF
      *
     C           EVAM      IFNE 0
     C                     EXSR SRWRTD
     C                     ENDIF
      *
      ** Else produce two keys if trade moves from profit to loss or
      ** from loss  to profit
      *
     C                     ELSE
      *
      ** - Produce  reversal key for unrealised p/l
      *
     C           PUPPT     IFGT 0
     C                     MOVE 'UP'      AMCD
     C                     ELSE
     C                     MOVE 'UL'      AMCD
     C                     ENDIF
      *
     C           PUPPT     IFGT 0
     C           PUPPT     MULT -1        EVAM
     C                     ELSE
     C                     Z-ADDPUPPT     EVAM
     C                     ENDIF
      *
     C           EVAM      IFNE 0
     C                     EXSR SRWRTD
     C                     ENDIF
      *
      ** -  Produce  key for new unrelised p/l
      *
     C           UPPT      IFGT 0
     C                     MOVE 'UP'      AMCD
     C                     ELSE
     C                     MOVE 'UL'      AMCD
     C                     ENDIF
      *
     C           UPPT      IFLT 0
     C           UPPT      MULT -1        EVAM
     C                     ELSE
     C                     Z-ADDUPPT      EVAM
     C                     ENDIF
      *
     C           EVAM      IFNE 0
     C                     EXSR SRWRTD
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
      ** Check Trade/Value date basis
      *
     C           TVDB      IFEQ 'T'
     C           *IN66     ANDEQ'1'
     C                     MOVE 'V'       TVDB
     C                     MOVE 'V'       TVD2
     C                     ELSE
     C                     MOVE *ON       *IN50
     C                     SETOF                     66
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           SRUNR9    ENDSR                           ** SRUNR9 **
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRBKPH - Subroutine to Chain to Book Position Header.        *
      *                                                               *
      *  Called By : SRUNRL - Calculate Unrealised Profit/Loss        *
      *                                                               *
      *****************************************************************
      *
     C           SRBKPH    BEGSR                           *** SRBKPH ***
      *
     C                     MOVE '1'       SUCESS  1
     C           BKHKEY    CHAINBKPHDDF              80
     C           *IN80     IFEQ '1'
     C                     Z-ADD*ZERO     UPPT
     C                     MOVE '0'       SUCESS
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRBKPO - Subroutine to Chain to Book Position File.          *
      *                                                               *
      *  Called By : SRUNRL - Calculate Unrealised Profit/Loss        *
      *                                                               *
      *****************************************************************
      *
     C           SRBKPO    BEGSR                           *** SRBKPO ***
      *
     C           BKOKEY    CHAINBKPOSDF              80
     C           *IN80     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE 'BKPOSD  'DBFILE
     C                     MOVE BOKEY     DBKEY            ***************
     C                     Z-ADD09        DBASE            * DB ERROR 09 *
     C                     SETON                     U7U8LR***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRWRTD - Subroutine to Write record to SEKEYD File.          *
      *                                                               *
      *  Called By : SRUNRL - Calculate Unrealised Profit/Loss        *
      *                                                               *
      *  Calls     : ZHASH  - Calculate event amounts as hash value   *
      *                                                               *
      *****************************************************************
      *
     C           SRWRTD    BEGSR                           *** SRWRTD ***
      *
      ** SET UP KEY FOR SEKEYD RECORD
      *
     C                     MOVE 'D'       RECI
     C                     MOVE SITP      INVT
     C                     MOVE *BLANK    TRTP
     C                     MOVE *BLANK    PORT
     C                     MOVE *BLANK    CGTP
     C                     MOVE BHBK      BOOK
     C           BVCIAK    IFEQ 'Y'
     C           SCVI      ANDEQ'Y'
     C                     MOVE SCVI      CNVI
     C                     ELSE
     C                     MOVE *BLANK    CNVI
     C                     ENDIF
     C                     MOVE BHMK      MRKT
      *
      ** Set up rest of SEKEYD record
      *
     C                     Z-ADD0         TRFR
     C**********           Z-ADDISSR      CSTN                                                CSD027
     C                     MOVE ISSR      CSTN                                                CSD027
     C                     MOVE BHBA      BRHA
     C                     MOVE 00        ASQN
     C                     Z-ADDECD       EVDT
     C                     MOVE NMCY      EVCY
     C                     Z-ADD0         STLT
     C                     MOVE *BLANK    STLA
     C                     MOVE '0'       RVRS
     C**********           Z-ADD0         KCUST                                               CSD027
     C                     MOVE *BLANKS   KCUST                                               CSD027
     C                     Z-ADD0         KNCA
     C                     MOVE NMCY      KCCY
     C                     Z-ADD0         KBCA
     C                     MOVE BHSC      SESH
      *
      ** Output profit centre from Book Position Profit Centre
      *
     C                     MOVE PCBK      PRFC
      *
      ** Set settlement branch to blank
      *
     C                     MOVE *BLANKS   SEBR
      *
     C                     ADD  1         OREC    50
      *
     C                     WRITESEKEYDF                81
      *
     C           *IN81     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE 'SEKEYD  'DBFILE
     C                     MOVE *BLANKS   DBKEY            ***************
     C                     Z-ADD11        DBASE            * DB ERROR 11 *
     C                     SETON                     U7U8LR***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Call hash totalling subroutine
      *
     C                     Z-ADD1         S
     C                     Z-ADDEVAM      ZZAMT
     C                     EXSR ZHASH
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRAUDI - Subroutine to Update Account Keys Audit.            *
      *                                                               *
      *  Called By : Main Routine                                     *
      *                                                               *
      *  Calls     : None                                             *
      *                                                               *
      *****************************************************************
      *
     C           SRAUDI    BEGSR                           *** SRAUDI ***
      *
      ** Update SEKEYA record
      *
     C           *INU8     IFEQ '0'
     C                     Z-ADDOREC      NORE
     C           HRWN      ADD  INT,Z     HRWN
     C           HRDC      ADD  DEC,Z     HRDC
     C                     UPDATSEKEYAF                81
      *
     C           *IN81     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE 'SEKEYA  'DBFILE
     C                     MOVE *BLANKS   DBKEY            ***************
     C                     Z-ADD12        DBASE            * DB ERROR 12 *
     C                     SETON                     U7U8LR***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRPCHN - Subroutine to Chain to Book Position Profit Centre  *
      *                                                               *
      *  Called By : SRUNRL - Unrealised Profit/Loss                  *
      *                                                               *
      *  Calls     : None                                             *
      *                                                               *
      *****************************************************************
      *
     C           SRPCHN    BEGSR                           *** SRPCHN ***
      *
      **  ... if Profit Centres are used and Trade and Book Positions
      **  are to be reconciled.
      *
     C           BKPRCU    IFEQ 'Y'
     C           BVTBRC    ANDEQ'Y'
      *
      ** Set up key fields for chain to PF/SEPCBD
      *
     C                     MOVE BHBK      KBHBK
     C                     MOVE BHBA      KBHBA
     C                     MOVE BHSC      KBHSC
     C                     MOVE BHMK      KBHMK
      *
     C           KSEPCB    CHAINSEPCBDF              80
     C           *IN80     IFEQ '1'
     C           RECI      OREQ '*'
     C           *LOCK     IN   LDA
     C                     MOVE 'SEPBCD  'DBFILE
     C                     MOVE KSEPCR    DBKEY            ***************
     C                     Z-ADD13        DBASE            * DB ERROR 13 *
     C                     SETON                     U7U8LR***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRPRIC - Subroutine to chain PF/PRICED                       *
      *                                                               *
      *****************************************************************
     C           SRPRIC    BEGSR
      *
      ** If market price equal to zero, chain to PF/PRICED
      ** if a price exists.
      *
     C           MKPR      IFEQ 0
     C                     MOVEA'00'      *IN,58
     C                     MOVE #ATDSS    PSSN
     C                     MOVE 'V '      PPRT
     C                     Z-ADDECD       PVDT
      *
      ** Read record from PF/PRICED
      *
     C           PKEY      SETGTPRICED
     C                     READPPRICED                   58
      *
      ** Set off *IN59 if record found in PF/PRICED with market price
      ** equal to zero.
      *
     C           *IN58     IFEQ '0'
     C           PSSN      ANDEQ#ATDSS
     C           PPRT      ANDEQ'V '
     C           PPRC      ANDEQ0
     C                     MOVE '0'       *IN59
     C                     ELSE
     C                     MOVE '1'       *IN59
     C                     ENDIF
     C                     ENDIF
      *
     C           SRPRI9    ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
     C           @RUN      IFEQ *BLANKS
     C                     MOVE 'Y'       @RUN    1
     C                     WRITEHEADAU
     C                     WRITEDBERR
     C                     DUMP
     C                     ENDIF
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
     C/COPY ZSRSRC,ZSDESCZ3
      /EJECT
     C/COPY ZSRSRC,ZDATE1Z2
      /EJECT
     C/COPY ZSRSRC,ZDATE2Z2
      /EJECT
     C/COPY ZSRSRC,ZSEDITZ3
      /EJECT
     C/COPY ZSRSRC,ZNCDZ3
      /EJECT
     C/COPY ZSRSRC,ZYCEZ1
      /EJECT
     C/COPY ZSRSRC,ZPRCIZ1
      /EJECT
     C/COPY ZSRSRC,ZPRCOZ1
      /EJECT
     C/COPY ZSRSRC,ZACCZZ1
      /EJECT
     C/COPY ZSRSRC,ZDAYSZ2
      /EJECT
     C/COPY ZSRSRC,ZDYYRZ3
      /EJECT
     C/COPY ZSRSRC,ZYLDOZ1
      /EJECT
     C/COPY ZSRSRC,ZACCRZ1
      /EJECT
     C/COPY ZSRSRC,ZRATEZ1
      /EJECT
     C/COPY ZSRSRC,ZBKDT
      /EJECT
     C/COPY ZSRSRC,ZACCH
      /EJECT
     C/COPY ZSRSRC,ZYLDIZ1
      /EJECT
     C/COPY ZSRSRC,ZYLDZ2
      /EJECT
     C/COPY ZSRSRC,ZLCDZ1
      /EJECT
SP   C/COPY ZSRSRC,ZHASHZ3
      /EJECT
     C/COPY ZSRSRC,ZEVCDZ1
      /EJECT
     C/COPY ZSRSRC,ZFWDT
      /EJECT
     C/COPY WNCPYSRC,WRKCDSR
      /EJECT
     C***COPY*ZSRSRC,ZNDECZ3*                                                                 CPK024
      /EJECT
     C/COPY ZSRSRC,ZCNSDZ1
      /EJECT
     C/COPY ZSRSRC,ZCALCD
      /EJECT
     OBKPHDDF E                UPDBKH
     O                         UPPT
**   CPY@
(c) Misys International Banking Systems Ltd. 2002
**   ZF29 DAY NOS OF FEB 29 TO 2096 (INCLUDING 2000)
0006001521029820444305904073650882610287117481320914670161311759219053
2051421975234362489726358278192928030741322023366335124365853804639507
40968424294389045351
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  POWER8 - ARRAY OF POWERS FOR CURRENCY CONVERSION
00000001
00000010
00000100
00001000
00010000
00100000
01000000
10000000
