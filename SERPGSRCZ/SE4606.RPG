     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Create Profit/Loss Extract file')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE4606 - Create Profit/Loss Extract File                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      *  Prev Amend No. CSE071             Date 19Jul05               *
      *                 CGL029             Date 01Sep03               *
      *                 CSE036  *CREATE    Date 22Apr02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSE036 - Unrealised Profit and Loss Report by Trade          *
      *                                                               *
      *****************************************************************
     FBKPHDDJ0IP  E           K        DISK
     FTRADSD  IF  E                    DISK
     FSECTY   IF  E           K        DISK
     FSDBANKPDIF  E                    DISK
     FBKPHD   IF  E           K        DISK
     FSETREXPPO   E                    DISK
      *
      *****************************************************************
      * ID F  C  H  L    FUNCTION OF INDICATORS                       *
      *                                                               *
      *    10            Live Record Indicator                        *
      *    11            Chain Fail in Securities File                *
      *    20            First cycle indicator                        *
      *    80            Chain fail on ICD-1                          *
      *    81            End of file on TRADSD                        *
      *    82            SDGELRPD file indicator                      *
      *                                                               *
      *    U7            Database Error                               *
      *    U8            Database Error                               *
      *****************************************************************
      /EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      /SPACE 3
     IDSFDY     E DSDSFDY
      *
      **  Short DS for access programs
      *
     ISDGELR    E DSSDGELRPD
      *
      ** General ledger details
      *
      ** DATA STRUCTURE FOR COMPILATION - COPYRIGHT INSERTION
      *
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *
      **  Error reporting data structure
      *
     ILDA       E DSLDA                         256
     I                                      134 141 @DBFIL
     I                                      142 170 @DBKEY
     I                                      171 180 @DBPGM
     I                                      181 1830@DBASE
      *
      ** Program Status Data Structure
      *
     IPSDS       SDS
     I                                      244 253 WSID
     I                                      254 263 USRID
      *                                                                                       CGL029
     IDSSDY     E DSDSSDY                                                                     CGL029
      *                                                                                       CGL029
      /EJECT
      *
      **  Main Processing
      *
     C                     MOVEACPY@      BIS@   80
     C           *NAMVAR   DEFN           LDA
     C                     SETOF                     1011
     C                     SETOF                     U7U8
     C                     EXSR SRBPRO
     CLR                   EXSR SRTRAD
      *
      *****************************************************************
      *                                                               *
      *  Index to subroutines:                                        *
      *                                                               *
      *   1. SRTRAD - Initial Program Control                         *
      *   2. SRDBER - Databsae Error Handling                         *
      *   3. SRWRIT - Write Details                                   *
      *   4. SRBPRO - Process book positions header file              *
      *   5. SRBWRI - Write Details from book positions               *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRTRAD - Initial Program Control                             *
      *                                                               *
      *  Called by: Main Program                                      *
      *                                                               *
      *  Calls:     SRDBER                                            *
      *             SRWRIT                                            *
      *                                                               *
      *****************************************************************
     C           SRTRAD    BEGSR
      *
     C           *IN20     IFEQ '0'
     C                     READ SDBANKPD                 80
     C           *IN80     IFEQ '1'
     C                     EXSR SRDBER
     C                     ELSE
     C**********           CALL 'AOGELRR0'                                                    CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM '*MSG   ' PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C********** SDGELR    PARM SDGELR    DSFDY                                               CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
     C           PRTCD     IFNE *BLANKS
     C                     SETON                     82
     C                     EXSR SRDBER
     C                     ELSE
     C                     Z-ADDBKAPDT    ZZAPDT
     C                     Z-ADDBJDNWD    ZZDNWD
     C                     EXSR ZEVCD
     C                     MOVE '1'       *IN20
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     SETOF                         81
     C                     READ TRADSD                   81
     C           *IN81     DOUEQ'1'
     C                     MOVE 'D'       ARECI
      *
      **  Determine if the record is live.
      *
     C           RECI      COMP '*'                  1010
      *
      **  Output a record if deal deleted today, and no existing
      **  value date record, so SE4605 can reverse P/L if necessary
      *
     C           *IN10     IFEQ *OFF
     C           LCD       ANDEQBJRDNB
      *
     C           KLBKH     KLIST
     C                     KFLD           TDSS
     C                     KFLD           TDBA
     C                     KFLD           TDBK
     C                     KFLD           TDMI
     C                     KFLD           @TV
      *
     C                     MOVEL'V'       @TV     1
      *
     C           KLBKH     CHAINBKPHD                80
     C           *IN80     IFEQ *ON
     C                     MOVE *ON       *IN10
     C                     MOVE '*'       ARECI
     C                     ENDIF
     C                     ENDIF
      *
     C           *IN10     IFEQ '1'
     C           TDVD      ANDGTECD
     C                     MOVE '1'       *IN10
     C                     ELSE
     C                     MOVE '0'       *IN10
     C                     ENDIF
      *
      **  If it is a live record, chain to Securities File to get the
      **  details.
      *
     C           *IN10     IFEQ '1'
     C           TDSS      CHAINSECTY                11
      *
      **  If no corresponding record was found, DATABASE ERROR.
      *
     C           *IN11     IFEQ '1'
     C                     EXSR SRDBER
     C                     ELSE
      *
      **  If the trade type is 'TS', negate the nominal amount.
      *
     C           TDTP      IFEQ 'TS'
     C                     Z-SUBNOML      NOML
     C                     ENDIF
      *
      **  Flag dummy record
      *
     C           ARECI     IFEQ '*'
     C                     Z-ADD*ZERO     NOML
     C                     ENDIF
      *
      **  Write security details to extract file.
      *
     C                     EXSR SRWRIT
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     READ TRADSD                   81
     C                     ENDDO
      *
     C           SRTRA9    ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  Subroutine: SRDBER - Database Error                          *
      *                                                               *
      *  Called by : SRTRAD                                           *
      *  Calls     : None                                             *
      *                                                               *
      *****************************************************************
     C           SRDBER    BEGSR
      *
     C                     SETON                     U7U8LR
      *
      **  Move values to LDA
      *
     C           *LOCK     IN   LDA
     C                     MOVEL'SE4606'  @DBPGM
      *
     C           *IN11     IFEQ '1'
     C                     Z-ADD01        @DBASE           ***************
     C                     MOVEL'SECTY'   @DBFIL           * DB ERROR 01 *
     C                     MOVELTDSS      @DBKEY           ***************
     C                     ENDIF
      *
     C           *IN80     IFEQ '1'
     C                     Z-ADD02        @DBASE           ***************
     C                     MOVEL'SDBANKPD'@DBFIL           * DB ERROR 02 *
     C                     MOVE *BLANKS   @DBKEY           ***************
     C                     ENDIF
      *
     C           *IN82     IFEQ '1'
     C                     Z-ADD03        @DBASE           ***************
     C                     MOVEL'TABTB11' @DBFIL           * DB ERROR 03 *
     C                     MOVE *BLANKS   @DBKEY           ***************
     C                     ENDIF
      *
     C                     OUT  LDA
     C                     RETRN
      *
     C           SRDBE9    ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  Subroutine: SRWRIT - Write Details                           *
      *                                                               *
      *  Called by : SRTRAD                                           *
      *  Calls     : None                                             *
      *                                                               *
      *****************************************************************
     C           SRWRIT    BEGSR
      *
     C                     MOVE TDRF      AUTDRF
     C                     MOVE TDSS      ATDSS
     C                     MOVE NOML      ANOML
     C                     Z-ADDNOML      AFNOML
     C                     MOVE TNMC      ATNMC
     C                     MOVE TDVD      ATDVD
     C                     MOVE TDBA      ATDBR
     C                     MOVE TDBK      ATDBK
     C                     MOVE TDMI      ATDMI
     C                     MOVE PRIC      APRIC
     C                     MOVE SITP      AINVT
      *
      ** Write new fields added
      ** long/short position
      *
     C           TDTP      IFEQ 'TP'
     C                     MOVE 'L'       ASIGN
     C                     ELSE
     C                     MOVE 'S'       ASIGN
     C                     ENDIF
      *
      ** Maturity date
      *
     C                     Z-ADDMATY      AMATD
      *
      ** Forward/Settle Indicator
      *
     C                     MOVE 'F'       AFWST
     C                     WRITESETREXP0
      *
     C           SRWRI9    ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  Subroutine: SRBPRO - Process book positions file             *
      *                       BKPHJ3V read in as input primary        *
      *                                                               *
      *  Called by : Main Program                                     *
      *  Calls     : SRDBER                                           *
      *              SRBWRI                                           *
      *                                                               *
      *****************************************************************
     C           SRBPRO    BEGSR
      *
     C           *IN20     IFEQ '0'
     C                     READ SDBANKPD                 80
     C           *IN80     IFEQ '1'
     C                     EXSR SRDBER
     C                     ELSE
     C**********           CALL 'AOGELRR0'                                                    CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM '*MSG   ' PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C********** SDGELR    PARM SDGELR    DSFDY                                               CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
     C           PRTCD     IFNE *BLANKS
     C                     SETON                     82
     C                     EXSR SRDBER
     C                     ELSE
     C                     Z-ADDBKAPDT    ZZAPDT
     C                     Z-ADDBJDNWD    ZZDNWD
     C                     EXSR ZEVCD
     C                     MOVE '1'       *IN20
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
      **  Write security details to extract file.
      *
     C                     EXSR SRBWRI
      *
     C           SRBPR9    ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  Subroutine: SRBWRI - Write details from book position file   *
      *                                                               *
      *  Called by : SRBPRO                                           *
      *  Calls     : None                                             *
      *                                                               *
      *****************************************************************
     C           SRBWRI    BEGSR
      *
     C                     MOVE 'D'       ARECI
     C                     MOVEL'VDPOSN'  AUTDRF
     C                     MOVE BHSC      ATDSS
     C                     MOVE NPSN      ANOML
     C                     Z-ADDNPSN      AFNOML
      *
     C                     MOVE NMCY      ATNMC
     C           LATD      IFNE *ZERO
     C                     MOVE LATD      ATDVD
     C                     ELSE
     C                     MOVE LPSD      ATDVD
     C                     ENDIF
     C                     MOVE BHBA      ATDBR
     C                     MOVE BHBK      ATDBK
     C                     MOVE BHMK      ATDMI
     C                     MOVE LAVP      APRIC
     C                     MOVE SITP      AINVT
      *
      ** Write new fields added
      ** Long/short Position (L/S)
      *
     C           NPSN      IFGT 0
     C                     MOVE 'L'       ASIGN
     C                     ELSE
     C                     MOVE 'S'       ASIGN
     C                     ENDIF
      *
      ** Maturity Date
      *
     C                     Z-ADDMATY      AMATD
      *
      ** Forward/Settle Indicator (F/C)
      *
     C                     MOVE 'C'       AFWST
     C                     WRITESETREXP0
      *
     C                     ENDSR
      *****************************************************************
     C/COPY ZSRSRC,ZEVCDZ1
**   CPY@
(c) Misys International Banking Systems Ltd. 2002
