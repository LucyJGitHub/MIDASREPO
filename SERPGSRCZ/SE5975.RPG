     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Extra Security Telekurs Prices Maint')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module                            *
     F*                                                               *
     F*  SE5975 - Extra Security Telekurs Prices Maintenance          *
     F*                                                               *
     F*  Function: This program allows all securities which do not    *
     F*   (I/C)    hold a position to be selected for Telekurs        *
     F*            price update.                                      *
     F*                                                               *
     F*  Called By: SEC5975 - Extra Security Telekurs Prices          *
     F*                       Maintenance (CL)                        *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      *  Prev Amend No. CSE071             Date 19Jul05               *
      *                 CAAA03             Date 03May04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSE009             Date 15Mar99               *
      *                 S01502 *CREATE     Date 08Jul94               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  CAAA03 - Webfacing Changes (recompile).                      *
     F*  CSE009 - Telekurs Changes                                    *
     F*  S01502 - BLI Telekurs Processing.                            *
     F*                                                               *
     F*****************************************************************
     FSE5975DFCF  E                    WORKSTN
     F                                        RCDNBRKSFILE SE5975S1
     FSECTY   IF  E           K        DISK
     F            SECTYDF                           KRENAMESECTYDFF
     FSECTR   IF  E           K        DISK
     F/COPY WNCPYSRC,SE5975F001
     F***STSECPO2IF**E**         K        DISK                                                CSE009
     F***STSTKPF2UF**E**         K        DISK                      A                         CSE009
     FSTSECPO2IF  E           K        DISK                           UC                      CSE009
     FSTSTKPF2UF  E           K        DISK                      A    UC                      CSE009
     F/COPY WNCPYSRC,SE5975F002
     FSETKSEPDIF  E           K        DISK                           UC                      CSE009
     FSETKEXPDUF  E           K        DISK                      A    UC                      CSE009
     FSEMKC   IF  E           K        DISK
     F*
     F*****************************************************************
     F*                                                               *
     F*     F U N C T I O N   O F   I N D I C A T O R S               *
     F*                                                               *
     F*    01            Command 3  end program                       *
     F*    03            Command 6                                    *
     F*    08            Roll up key pressed                          *
     F*    09            Roll down key pressed                        *
     F*    14            Display security details                     *
     F*    16            Display error messages                       *
     F*    18            Display subfile control                      *
     F*    19            Detail screen indicator                      *
     F*    20            Display subfile                              *
     F*    26            SFLEND - Message subfile                     *
     F*    30            Toggle between extra and all securities      *
     F*    78            Character found                              *
     F*    80            CHAIN fail indicator                         *
     F*    81            CHAIN fail indicator                         *
     F*    87            DETSR - Subfile processing loop              *
     F*                                                               *
     F*****************************************************************
     E/EJECT
     E/COPY ZSRSRC,ZSERPT1
     E                    CPY@    1   1 80
     E                    ALPNU  37  37  1
     E                    ALPHA  27  27  1
     E                    INP        20  1
     E                    ERR     1  11 79               MESSAGES ARRAY
     E*
     I/EJECT
     I/COPY WNCPYSRC,SE5975I003
     ISETKSED0                                                                                CSE009
     I              STTELK                          STELK9                                    CSE009
     ISETKEXD0                                                                                CSE009
     I              STTELK                          STELK9                                    CSE009
     I/COPY WNCPYSRC,SE5975I004
     I*
     I** Data structure for telekurs
     I*
     I            DS
     I                                        1  12 T2RF
     I/COPY WNCPYSRC,SE5975I001
     I                                        1   6 TVRF
     I                                        1   60TELKWF
     I/COPY WNCPYSRC,SE5975I002
     I                                        1   9 TVRF2                                     CSE009
     I                                        1   90TELKW2                                    CSE009
     I                                       10  12 SMCO
     I*
     I** Data structure for input field
     I*
     I            DS
     I                                        1  20 SECINP
     I                                        1  20 INP
     I*
     I** Local data area for database error details
     I*
     ILDA         DS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     I** Data area RUNDAT
     I*
     IRUNDT       DS                             13
     I                                       13  13 MBIN
     I*
     I** Data area ZMUSER
     I*
     IZMUSER      DS                             17
     I                                       11  13 USRBCH
     I*
     I** Program status data structure
     I*
     IPSDS       SDS
     I                                      244 253 SWSID
     I                                      254 263 SUSER
     I*                                                                                       CSE009
     I@SARD     E DSSCSARDPD                                                                  CSE009
     I**  Data structure for switchable features.                                             CSE009
     I*
     ISDBANK    E DSSDBANKPD
     I*External data structure for Bank Details
     I*
     IDSFDY     E DSDSFDY
     I*1st DS for access program , short data structure
     I*
     I/EJECT
     C*
     C** MAIN PROCESSING
     C**
     C** First, we clear the subfile, and output the initial screen.
     C** Then, process the input response.  IF F3 is pressed, end
     C** program.  On subsequent cycles also, do roll key processing
     C** Validate SECURITY number/shortname input.
     C*
     C                     MOVEACPY@      BIS@   80
     C*
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL'SE5975  'DBPGM
     C                     OUT  LDA
     C*
     C** Read in ZMUSER
     C*
     C           *NAMVAR   DEFN           ZMUSER
     C                     IN   ZMUSER
     C*
     C** Read in RUNDAT
     C*
     C           *NAMVAR   DEFN RUNDAT    RUNDT
     C                     IN   RUNDT
     C*
     C                     MOVE *BLANKS   ZAMSGF
     C                     MOVEL'SEUSRMSG'ZAMSGF
     C*
     C** Call to access pgm for bank details
     C*
     C                     CALL 'AOBANKR0'
     C                     PARM '*DBERR ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C                     MOVELBJMRDT    SRUNA
     C*                                                                                       CSE009
     C** Check if SAR CSE009 is ON.                                                           CSE009
     C*                                                                                       CSE009
     C                     CALL 'AOSARDR0'                                                    CSE009
     C                     PARM *BLANKS   @RTCD                                               CSE009
     C                     PARM '*VERIFY' @OPTN                                               CSE009
     C                     PARM 'CSE009'  @SARC   6                                           CSE009
     C           @SARD     PARM @SARD     DSFDY                                               CSE009
     C*                                                                                       CSE009
     C**  Set indicator if installed or run date > or = than 26 April 99                      CSE009
     C/COPY WNCPYSRC,SE5975C015
     C*                                                                                       CSE009
     C           @RTCD     IFEQ *BLANKS                    B*1                                CSE009
     C           BJRDNB    ORGE 09978                                                         CSE009
     C                     MOVE 'Y'       CSE009  1                                           CSE009
     C                     MOVE '1'       *IN50                                               CSE009
     C                     OPEN SETKSEPD                                                      CSE009
     C                     OPEN SETKEXPD                                                      CSE009
     C                     ELSE                            X*1                                CSE009
     C                     MOVE 'N'       CSE009                                              CSE009
     C                     MOVE '0'       *IN50                                               CSE009
     C                     OPEN STSECPO2                                                      CSE009
     C                     OPEN STSTKPF2                                                      CSE009
     C                     END                             E*1                                CSE009
     C/COPY WNCPYSRC,SE5975C016
     C*
     C** First cycle - Output initial screen
     C*
     C                     MOVEL'SE5975  'PROGNM  8
     C                     SETON                     1416
     C                     SETOF                     0330
     C                     MOVEL'N'       EXTRA
     C*
     C** Initialise screen message queue
     C*
     C                     MOVEL'*'       DDPGMQ
     C*
     C                     MOVE *BLANKS   WWTPGQ
     C                     MOVEL'*'       WWTPGQ
     C                     MOVE *BLANKS   WWPGQR
     C                     MOVEL'*PRV'    WWPGQR
     C                     MOVEL*BLANKS   @ERR2
     C                     MOVE '1'       *IN26
     C*
     C** Clear subfile
     C*
     C                     SETOF                     1820
     C                     WRITESE5975C1
     C                     MOVEL*BLANKS   SECINP
     C                     MOVEL*BLANKS   ACTION
     C*
     C** Screen processing loop
     C*
     C                     SETON                     87
     C           *IN87     DOWEQ'1'
     C*
     C** Output headings
     C*
     C                     TIME           STIME
     C                     WRITESE5975F1
     C*
     C** If error, write message subfile
     C*
     C           @ERR2     IFEQ 'Y'
     C                     WRITESE5975C2
     C                     MOVEL*BLANKS   @ERR2
     C                     END
     C*
     C** Save last shortname input
     C*
     C                     MOVELSECINP    SECMXX 10
     C*
     C** Blank shortname output
     C*
     C                     MOVEL*BLANKS   SECINP
     C                     MOVEL*BLANKS   ACTION
     C*
     C** Indicator for subfile record
     C*
     C           *IN14     IFEQ '1'
     C                     SETOF                     20
     C                     ELSE
     C                     SETON                     20
     C                     END
     C*
     C** Indicator for subfile control record
     C*
     C                     SETON                     18
     C*
     C** Indicator for HELP function
     C*
     C                     SETOF                     1903
     C                     Z-ADD1         RCDNBR  30
     C                     WRITESE5975C1
     C*
     C** Process details screen
     C*
     C                     SETON                     19
     C           *IN19     DOWEQ'1'
     C                     SETOF                     1980
     C                     READ SE5975C1               8080
     C*
     C                     MOVE *BLANKS   MSG
     C*
     C                     END
     C*
     C** If command 3 then end program
     C*
     C           *IN01     IFEQ '1'
     C                     MOVE '7'       RETNWF  1
     C                     EXSR RETSR
     C                     END
     C*
     C** Clear screen message queue
     C*
     C                     CALL 'Y2CLMSC'
     C                     PARM           WWTPGQ 10
     C                     PARM           WWPGQR  5
     C*
     C** Roll up command
     C*
     C           *IN08     IFEQ '1'
     C                     EXSR ROLLUP
     C                     ELSE
     C*
     C** Roll down command
     C*
     C           *IN09     IFEQ '1'
     C                     EXSR ROLLDN
     C                     ELSE
     C*
     C** Select those with extra details
     C*
     C           *IN03     IFEQ '1'
     C           *IN30     IFEQ '1'
     C                     SETOF                     30
     C                     MOVE *BLANKS   SKEY   20
     C                     MOVEL'A       'SKEY   20
     C                     ELSE
     C                     SETON                     30
     C                     MOVE *BLANKS   SKEY   20
     C                     MOVEL'A       'SKEY   20
     C                     END
     C                     EXSR SNREAD
     C           RCDNBR    IFEQ 0
     C                     SETOF                     30
     C                     MOVE FNAMST    SKEY   20
     C                     EXSR SNREAD
     C                     MOVELERR,11    MSG
     C                     END
     C                     ELSE
     C*
     C** Process input
     C*
     C           SECINP    IFNE *BLANKS
     C                     EXSR SECSR
     C                     ELSE
     C*
     C           ACTION    IFNE *BLANKS
     C                     MOVELERR,10    MSG
     C                     ELSE
     C*
     C** else check if security selected
     C*
     C           *IN20     IFEQ '0'
     C                     MOVELERR,1     MSG
     C                     ELSE
     C                     EXSR DTRQS
     C           MSG       IFEQ *BLANKS
     C           @ERR2     ANDEQ*BLANKS
     C*
     C** If securities updated then reload screen
     C*
     C                     MOVE FNAMST    SKEY   20
     C                     EXSR SNREAD
     C                     END
     C                     END
     C                     END
     C*
     C                     END
     C                     END
     C                     END
     C                     END
     C*
     C           MSG       COMP *BLANKS                  16
     C*
     C*
     C                     END
     C*
     C*****************************************************************
     C*                                                               *
     C* SECSR  - Validate and process input                           *
     C*                                                               *
     C* Calls: RETSR, SNREAD                                          *
     C*                                                               *
     C* Called from: Detail time processing                           *
     C*                                                               *
     C*****************************************************************
     C*
     C           SECSR     BEGSR                           *** SECSR *****
     C*
     C** Test for invalid input
     C*
     C           SECINP    IFEQ *BLANKS
     C                     MOVELERR,1     MSG
     C                     SETON                     14
     C                     ELSE
     C                     MOVELSECINP    WRK1    1
     C           WRK1      LOKUPALPNU                    78
     C*
     C           *IN78     IFEQ '0'
     C                     MOVELERR,2     MSG
     C                     ELSE
     C*
     C** Check if security name or number
     C*
     C                     MOVE SECINP    TEST10 10
     C           WRK1      LOKUPALPHA                    78
     C*
     C** Check if security name exists
     C*
     C           TEST10    IFNE *BLANKS
     C           SECINP    CHAINSECTR                80
     C           *IN80     IFEQ '0'
     C*
     C** If exists then validate action
     C*
     C                     EXSR FILUPD
     C                     ELSE
     C*
     C** If not then load subfile
     C*
     C                     MOVE SECINP    SKEY   20
     C                     EXSR SNREAD
     C                     END
     C                     ELSE
     C*
     C** Check if security number exists
     C*
     C                     MOVELSECINP    SECSKY 10
     C           SECSKY    CHAINSECTY                80
     C           *IN80     IFEQ '0'
     C*
     C** If exists then validate action
     C*
     C                     EXSR FILUPD
     C                     ELSE
     C*
     C** else check if short security name
     C*
     C           SECINP    CHAINSECTR                80
     C           *IN80     IFEQ '0'
     C*
     C** If exists then validate action
     C*
     C                     EXSR FILUPD
     C                     ELSE
     C*
     C** If not then load subfile
     C*
     C                     MOVE SECINP    SKEY   20
     C                     EXSR SNREAD
     C                     END
     C*
     C                     END
     C                     END
     C                     END
     C                     END
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C*                                                               *
     C* FILUPD Subroutine                                             *
     C*                                                               *
     C* Called from: SECSR                                            *
     C*                                                               *
     C*****************************************************************
     C*
     C           FILUPD    BEGSR                              ** FILUPD
     C*
     C** If exists then validate action
     C*
     C           TELKWF    IFLE 0
     C           CSE009    ANDEQ'N'                                                           CSE009
     C           TELKW2    ORLE 0                                                             CSE009
     C           CSE009    ANDEQ'Y'                                                           CSE009
     C                     MOVELERR,8     MSG
     C                     ELSE
     C           ACTION    IFNE 'Y'
     C           ACTION    IFNE 'N'
     C                     MOVELERR,9     MSG
     C                     END
     C                     END
     C*
     C                     MOVE *BLANKS   @ERR2   1
     C*
     C** If ACTION valid, check SPF
     C*
     C           MSG       IFEQ *BLANKS
     C*
     C** For single-branch environment
     C*
     C           MBIN      IFNE 'Y'
     C                     CALL 'ZVACTU'
     C                     PARM ACTION    @ZACTN  1
     C                     PARM           @ERR    10
     C*
     C** If action invalid for user
     C*
     C           @ERR      IFNE *ZERO
     C                     MOVEL'Y'       @ERR2
     C                     MOVEL'SEM1019' ZAMSID
     C                     EXSR ZASNMS
     C                     END
     C                     ELSE
     C*
     C** For multi-branch environment
     C*
     C                     CALL 'ZVACTBU'
     C                     PARM ACTION    @ZACTN  1
     C                     PARM USRBCH    @ZBR    3
     C                     PARM           @ERR    10
     C*
     C**   If action invalid for user
     C*
     C           @ERR      IFEQ 1
     C                     MOVEL'Y'       @ERR2
     C                     MOVEL'SEM1020' ZAMSID
     C                     EXSR ZASNMS
     C                     ELSE
     C           @ERR      IFEQ 2
     C                     MOVEL'Y'       @ERR2
     C                     MOVEL'SEM1021' ZAMSID
     C                     EXSR ZASNMS
     C                     END
     C                     END
     C                     END
     C                     END
     C*
     C                     END
     C*
     C           MSG       IFEQ *BLANKS
     C           @ERR2     ANDEQ' '
     C*
     C** Chain to STSTK to see if security on extra file
     C*
     C           CSE009    IFEQ 'N'                                                           CSE009
     C/COPY WNCPYSRC,SE5975C001
     C           TELKWF    CHAINSTSTKPF2             80
     C                     ELSE                                                               CSE009
     C           TELKW2    CHAINSETKEXPD             80                                       CSE009
     C                     ENDIF                                                              CSE009
     C           *IN80     IFEQ '1'
     C           ACTION    IFEQ 'Y'
     C           CSE009    IFEQ 'N'                                                           CSE009
     C                     Z-ADDTELKWF    STTELK
     C/COPY WNCPYSRC,SE5975C002
     C                     ELSE                                                               CSE009
     C                     Z-ADDTELKW2    STELK9                                              CSE009
     C                     ENDIF                                                              CSE009
     C*
     C** Chain to SEMKC to retrieve telekurs stock exchange code
     C*
     C           SMCC      CHAINSEMKC                80
     C           *IN80     IFEQ '0'
     C           RECI      ANDNE'*'
     C/COPY WNCPYSRC,SE5975C003
     C                     MOVE TKSECD    STBOUR
     C                     ELSE
     C                     MOVE *BLANKS   STBOUR
     C                     END
     C*
     C           CSE009    IFEQ 'N'                                                           CSE009
     C                     WRITESTSTKPFS
     C                     ELSE                                                               CSE009
     C                     WRITESETKEXD0                                                      CSE009
     C                     ENDIF                                                              CSE009
     C                     END
     C                     ELSE
     C           ACTION    IFEQ 'N'
     C           CSE009    IFEQ 'N'                                                           CSE009
     C                     DELETSTSTKPFS
     C/COPY WNCPYSRC,SE5975C004
     C                     ELSE                                                               CSE009
     C                     DELETSETKEXD0                                                      CSE009
     C                     ENDIF                                                              CSE009
     C                     END
     C                     END
     C                     END
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C*                                                               *
     C* SNREAD  Subroutine                                            *
     C*                                                               *
     C* Called from: SECSR                                            *
     C*                                                               *
     C*****************************************************************
     C*
     C           SNREAD    BEGSR                              ** SNREAD
     C*
     C** Clear subfile
     C*
     C                     SETOF                     1820
     C                     WRITESE5975C1
     C                     Z-ADD0         RCDNBR
     C*
     C           SKEY      SETLLSECTR                8080
     C*
     C** End of file
     C*
     C           *IN80     IFEQ '1'
     C                     MOVELERR,5     MSG
     C                     ELSE
     C*
     C                     SETOF                         80
     C           *IN80     DOWEQ'0'
     C                     SETOF                     14
     C*
     C                     READ SECTR                  8080
     C           *IN80     IFEQ '0'
     C*
     C** Format details for output
     C*
     C                     EXSR FORMAT
     C*
     C** Write record to subfile
     C*
     C           *IN30     IFEQ '0'
     C                     ADD  1         RCDNBR
     C                     WRITESE5975S1
     C                     ELSE
     C           EXTRA     IFEQ 'Y'
     C                     ADD  1         RCDNBR
     C                     WRITESE5975S1
     C                     END
     C                     END
     C           RCDNBR    COMP 16                   80  80
     C*
     C** Save key of first record
     C*
     C           RCDNBR    IFEQ 1
     C                     MOVELSRPN      FNAMST 20
     C                     END
     C                     END
     C                     END
     C*
     C                     END
     C*
     C** Save key of last record
     C*
     C           RCDNBR    IFNE 0
     C                     MOVELSRPN      FNAMED 20
     C                     END
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C*                                                               *
     C* ROLLUP  Subroutine                                            *
     C*                                                               *
     C* Called from: Detail time processing                           *
     C*                                                               *
     C*****************************************************************
     C*
     C           ROLLUP    BEGSR                              ** ROLLUP
     C*
     C                     MOVELFNAMED    SKEY
     C                     Z-ADD0         RCDNBR
     C*
     C           SKEY      SETGTSECTR
     C*
     C                     SETOF                         80
     C           *IN80     DOWEQ'0'
     C*
     C                     READ SECTR                  8080
     C*
     C** End of file
     C*
     C           *IN80     IFEQ '1'
     C           RCDNBR    IFEQ 0
     C                     MOVELERR,3     MSG
     C                     END
     C                     END
     C*
     C** If records found then clear subfile
     C*
     C           RCDNBR    IFEQ 0
     C           *IN80     IFEQ '0'
     C*
     C** Clear subfile
     C*
     C                     SETOF                     1820
     C                     WRITESE5975C1
     C                     END
     C                     END
     C           *IN80     IFEQ '0'
     C                     SETOF                     14
     C*
     C** Write record to subfile
     C** Format details for output
     C*
     C                     EXSR FORMAT
     C           *IN30     IFEQ '0'
     C                     ADD  1         RCDNBR
     C                     WRITESE5975S1
     C                     ELSE
     C           EXTRA     IFEQ 'Y'
     C                     ADD  1         RCDNBR
     C                     WRITESE5975S1
     C                     END
     C                     END
     C           RCDNBR    COMP 16                   80  80
     C*
     C** Save key of first shortname
     C*
     C           RCDNBR    IFEQ 1
     C                     MOVELSRPN      FNAMST 20
     C                     END
     C                     END
     C                     END
     C*
     C** Save key of last record
     C*
     C                     MOVELSRPN      FNAMED 20
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C*                                                               *
     C* ROLLDN   Subroutine                                           *
     C*                                                               *
     C* Called from: Detail time processing                           *
     C*                                                               *
     C*****************************************************************
     C*
     C           ROLLDN    BEGSR                              ** ROLLDN
     C*
     C                     MOVELFNAMST    SKEY
     C                     Z-ADD0         RCDNBR
     C*
     C** Clear subfile
     C*
     C                     SETOF                     1820
     C                     WRITESE5975C1
     C*
     C           SKEY      SETLLSECTR
     C*
     C** Read previous 16 records
     C*
     C                     SETOF                         80
     C           *IN80     DOWEQ'0'
     C*
     C                     READPSECTR                    80
     C*
     C** If beginning of file then display message
     C*
     C           *IN80     IFEQ '1'
     C           RCDNBR    IFEQ 0
     C                     MOVELERR,4     MSG
     C                     END
     C                     END
     C           *IN80     IFEQ '0'
     C                     ADD  1         RCDNBR
     C                     MOVELSRPN      FNAMST
     C           RCDNBR    COMP 16                   80  80
     C                     END
     C                     END
     C*
     C** Read forward 16 records
     C*
     C                     MOVELFNAMST    SKEY
     C           SKEY      SETLLSECTR
     C                     Z-ADD0         RCDNBR
     C                     SETOF                         80
     C           *IN80     DOWEQ'0'
     C*
     C                     READ SECTR                    80
     C           *IN80     IFEQ '0'
     C                     SETOF                     14
     C*
     C** Write record to subfile
     C** Format details for output
     C*
     C                     EXSR FORMAT
     C           *IN30     IFEQ '0'
     C                     ADD  1         RCDNBR
     C                     WRITESE5975S1
     C                     ELSE
     C           EXTRA     IFEQ 'Y'
     C                     ADD  1         RCDNBR
     C                     WRITESE5975S1
     C                     END
     C                     END
     C           RCDNBR    COMP 16                   80  80
     C                     END
     C                     END
     C*
     C** Save key of last record
     C*
     C                     MOVELSRPN      FNAMED 20
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C*                                                               *
     C* FORMAT - Format security details for output                   *
     C*                                                               *
     C* Calls (Pgm): ZDATE2, SE5979
     C*                                                               *
     C* Called from: SNREAD, ROLLUP, ROLLDN                           *
     C*                                                               *
     C*****************************************************************
     C*
     C           FORMAT    BEGSR                           ** FORMAT * ***
     C*
     C           CSE009    IFEQ 'N'                                                           CSE009
     C           TVRF      IFEQ *BLANK
     C                     MOVE *ZERO     TELKWF
     C                     END
     C                     ELSE                                                               CSE009
     C           TVRF2     IFEQ *BLANK                                                        CSE009
     C                     MOVE *ZERO     TELKW2                                              CSE009
     C                     END                                                                CSE009
     C                     ENDIF                                                              CSE009
     C*
     C                     MOVE *BLANKS   PRICES
     C                     MOVE 'N'       EXTRA   1
     C           TELKWF    IFGT 0
     C           CSE009    ANDEQ'N'                                                           CSE009
     C           TELKW2    ORGT 0                                                             CSE009
     C           CSE009    ANDEQ'Y'                                                           CSE009
     C                     MOVE 'N'       PRICES
     C           CSE009    IFEQ 'N'                                                           CSE009
     C*****                MOVE TELKWF    TELEKS                                              CSE009
     C                     MOVELTELKWF    TELEKS    P                                         CSE009
     C                     ELSE                                                               CSE009
     C                     MOVE TELKW2    TELEKS    P                                         CSE009
     C                     ENDIF                                                              CSE009
     C*
     C** Chain to STSEC to see if prices currently quated
     C*
     C           CSE009    IFEQ 'N'                                                           CSE009
     C/COPY WNCPYSRC,SE5975C005
     C           TELKWF    CHAINSTSECPO2             80
     C/COPY WNCPYSRC,SE5975C006
     C                     ELSE                                                               CSE009
     C           TELKW2    CHAINSETKSEPD             80                                       CSE009
     C                     ENDIF                                                              CSE009
     C           *IN80     IFEQ '0'
     C                     MOVE 'Y'       PRICES
     C                     END
     C*
     C** Chain to STSTK to see if security on extra file
     C*
     C           CSE009    IFEQ 'N'                                                           CSE009
     C/COPY WNCPYSRC,SE5975C007
     C           TELKWF    CHAINSTSTKPF2             80
     C/COPY WNCPYSRC,SE5975C008
     C                     ELSE                                                               CSE009
     C           TELKW2    CHAINSETKEXPD             80                                       CSE009
     C                     ENDIF                                                              CSE009
     C           *IN80     IFEQ '0'
     C                     MOVE 'Y'       PRICES
     C                     MOVE 'Y'       EXTRA   1
     C                     END
     C*
     C** else if no telekurs number
     C*
     C                     ELSE
     C                     MOVE *BLANKS   TELEKS
     C                     END
     C*
     C                     MOVE *BLANKS   SECS
     C                     MOVELSESN      SECS
     C*
     C           MATY      IFGT 0
     C                     Z-ADDMATY      ZDAYNO
     C                     Z-ADD0         ZDATE
     C                     CALL 'ZDATE2'
     C                     PARM           ZDAYNO  50       DAY NUMBER
     C                     PARM ' '       ZDATFM  1        DATE FORMAT IND
     C                     PARM           ZDATE   60       DATE-DDMMYY
     C                     PARM           WQ0004  7        DATE-DDMMMYY
     C                     Z-ADDZDATE     ZMADAT
     C                     ELSE
     C                     Z-ADD0         ZMADAT
     C                     END
     C                     MOVE *BLANKS   SECNAM
     C                     MOVELSRPN      ZTITLE
     C                     Z-ADDCPNR      ZCPRAT
     C*
     C                     SETOF                     98
     C*
     C** Edit security report name
     C*
     C                     EXSR ZSERPT
     C*
     C                     MOVELZRPNAM    SECNAM
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C*                                                               *
     C* DTRQS  - Validate and process shortname request               *
     C*                                                               *
     C* Calls: RETSR                                                  *
     C*                                                               *
     C* Called from: Detail time processing                           *
     C*                                                               *
     C*****************************************************************
     C**
     C** This subroutine processes security name selects,and if valid
     C** returns the security number to the calling link enquiry.
     C*
     C           DTRQS     BEGSR                           ** DTRQS    ***
     C*
     C** Clear error message field
     C*
     C                     MOVE *BLANKS   MSG
     C                     MOVE *BLANKS   LEOPSF
     C*
     C** Read first changed record
     C*
     C                     SETOF                     81
     C           LEOPSF    DOWEQ' '
     C           *IN81     ANDEQ'0'
     C                     READCSE5975S1                 81
     C                     Z-ADDRCDNBR    RECORD  50
     C                     END
     C*
     C** Not found then display message 1
     C*
     C           *IN81     IFEQ '1'
     C                     MOVELERR,1     MSG
     C                     ELSE
     C*
     C** Reset LEOPSF to blanks
     C*
     C                     MOVE LEOPSF    LEOPXX  1
     C                     MOVE *BLANKS   LEOPSF
     C                     UPDATSE5975S1
     C*
     C** Check that value is equal to Y or N
     C*
     C                     MOVE *BLANKS   MSG
     C           LEOPXX    IFNE 'Y'
     C           LEOPXX    IFNE 'N'
     C                     MOVELERR,6     MSG
     C           *IN81     DOWEQ'0'
     C                     READCSE5975S1                 81
     C                     MOVE *BLANKS   LEOPSF
     C  N81                UPDATSE5975S1
     C                     END
     C                     END
     C                     END
     C           MSG       IFEQ *BLANKS
     C*
     C** Check that telekurs exists for security
     C*
     C           TELEKS    IFEQ *BLANKS
     C                     MOVELERR,8     MSG
     C                     END
     C                     END
     C           MSG       IFEQ *BLANKS
     C*
     C** Save key value
     C*
     C                     MOVE SECS      SECSAV 10
     C*
     C** Check that only one option requested
     C*
     C                     SETOF                     81
     C           LEOPSF    DOWEQ' '
     C           *IN81     ANDEQ'0'
     C                     READCSE5975S1                 81
     C                     END
     C           *IN81     IFEQ '0'
     C                     MOVELERR,7     MSG
     C                     MOVE *BLANKS   LEOPSF
     C                     UPDATSE5975S1
     C           *IN81     DOWEQ'0'
     C                     READCSE5975S1                 81
     C                     MOVE *BLANKS   LEOPSF
     C  N81                UPDATSE5975S1
     C                     END
     C                     ELSE
     C*
     C** If LEOPXX valid, check SPF
     C** For single-branch environment
     C*
     C                     MOVE *BLANKS   @ERR2
     C           MBIN      IFNE 'Y'
     C                     CALL 'ZVACTU'
     C                     PARM LEOPXX    @ZACTN  1
     C                     PARM           @ERR    10
     C*
     C** If action invalid for user
     C*
     C           @ERR      IFNE *ZERO
     C                     MOVEL'Y'       @ERR2
     C                     MOVEL'SEM1019' ZAMSID
     C                     EXSR ZASNMS
     C                     END
     C                     ELSE
     C*
     C** For multi-branch environment
     C*
     C                     CALL 'ZVACTBU'
     C                     PARM LEOPXX    @ZACTN  1
     C                     PARM USRBCH    @ZBR    3
     C                     PARM           @ERR    10
     C*
     C**   If action invalid for user
     C*
     C           @ERR      IFEQ 1
     C                     MOVEL'Y'       @ERR2
     C                     MOVEL'SEM1020' ZAMSID
     C                     EXSR ZASNMS
     C                     ELSE
     C           @ERR      IFEQ 2
     C                     MOVEL'Y'       @ERR2
     C                     MOVEL'SEM1021' ZAMSID
     C                     EXSR ZASNMS
     C                     END
     C                     END
     C                     END
     C*
     C*
     C** Valid - update file if necessary
     C*
     C           LEOPXX    IFEQ 'Y'
     C           @ERR2     ANDEQ' '
     C           PRICES    IFEQ 'N'
     C           CSE009    IFEQ 'N'                                                           CSE009
     C*****                MOVE TELEKS    TELKWF                                              CSE009
     C                     MOVELTELEKS    TELKWF                                              CSE009
     C                     ELSE                                                               CSE009
     C                     MOVE TELEKS    TELKW2                                              CSE009
     C                     ENDIF                                                              CSE009
     C*
     C** Chain to STSEC to see if prices currently quated
     C*
     C           CSE009    IFEQ 'N'                                                           CSE009
     C/COPY WNCPYSRC,SE5975C009
     C           TELKWF    CHAINSTSECPO2             80
     C                     ELSE                                                               CSE009
     C           TELKW2    CHAINSETKSEPD             80                                       CSE009
     C                     ENDIF                                                              CSE009
     C*
     C** Chain to STSTK to see if security on extra file
     C*
     C           CSE009    IFEQ 'N'                                                           CSE009
     C   80      TELKWF    CHAINSTSTKPF2             80
     C                     ELSE                                                               CSE009
     C   80      TELKW2    CHAINSETKEXPD             80                                       CSE009
     C                     ENDIF                                                              CSE009
     C           *IN80     IFEQ '1'
     C           CSE009    IFEQ 'N'                                                           CSE009
     C*****                MOVE TELEKS    TELKWF                                              CSE009
     C                     MOVELTELEKS    TELKWF                                              CSE009
     C                     ELSE                                                               CSE009
     C                     MOVE TELEKS    TELKW2                                              CSE009
     C                     ENDIF                                                              CSE009
     C*
     C           CSE009    IFEQ 'N'                                                           CSE009
     C                     Z-ADDTELKWF    STTELK
     C/COPY WNCPYSRC,SE5975C010
     C                     ELSE                                                               CSE009
     C                     Z-ADDTELKW2    STELK9                                              CSE009
     C                     ENDIF                                                              CSE009
     C*
     C** Chain to SECTYD to retrieve market centre
     C*
     C           SECS      CHAINSECTY                80
     C           *IN80     IFEQ '0'
     C*
     C** Chain to SEMKC to retrieve telekurs stock exchange code
     C*
     C           SMCC      CHAINSEMKC                80
     C           *IN80     IFEQ '0'
     C           RECI      ANDNE'*'
     C/COPY WNCPYSRC,SE5975C011
     C                     MOVE TKSECD    STBOUR
     C                     ELSE
     C                     MOVE *BLANKS   STBOUR
     C                     END
     C           CSE009    IFEQ 'N'                                                           CSE009
     C                     WRITESTSTKPFS
     C/COPY WNCPYSRC,SE5975C012
     C                     ELSE                                                               CSE009
     C                     WRITESETKEXD0                                                      CSE009
     C                     ENDIF                                                              CSE009
     C                     END
     C                     END
     C                     END
     C                     END
     C*
     C           LEOPXX    IFEQ 'N'
     C           @ERR2     ANDEQ' '
     C           PRICES    IFEQ 'Y'
     C           CSE009    IFEQ 'N'                                                           CSE009
     C*****                MOVE TELEKS    TELKWF                                              CSE009
     C                     MOVELTELEKS    TELKWF                                              CSE009
     C                     ELSE                                                               CSE009
     C                     MOVE TELEKS    TELKW2                                              CSE009
     C                     ENDIF                                                              CSE009
     C*
     C** Chain to STSTK and delete if found
     C*
     C           CSE009    IFEQ 'N'                                                           CSE009
     C/COPY WNCPYSRC,SE5975C013
     C           TELKWF    CHAINSTSTKPF2             80
     C  N80                DELETSTSTKPFS
     C/COPY WNCPYSRC,SE5975C014
     C                     ELSE                                                               CSE009
     C           TELKW2    CHAINSETKEXPD             80                                       CSE009
     C  N80                DELETSETKEXD0                                                      CSE009
     C                     ENDIF                                                              CSE009
     C                     END
     C                     END
     C*
     C                     END
     C                     END
     C                     END
     C*
     C** Invalid input display error codes
     C*
     C           MSG       COMP *BLANKS                  16
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C*                                                               *
     C* RETSR  - End program processing                               *
     C*                                                               *
     C* Called from: Detail time processing                           *
     C*                                                               *
     C*****************************************************************
     C*
     C           RETSR     BEGSR                           *** RETSR
     C*
     C                     SETON                         LR
     C                     FREE 'ZDATE2'
     C*
     C                     RETRN
     C*
     C                     ENDSR
     C*****************************************************************
     C**                                                              *
     C** ZASNMS    - Send message to program's message queue          *
     C**                                                              *
     C** CALLS     - Y2SNMGC                                          *
     C**                                                              *
     C** CALLED BY - FILUPD, DTRQS                                    *
     C**                                                              *
     C*****************************************************************
     C           ZASNMS    BEGSR                           ** ZASNMS    **
     C*
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGM  10
     C                     PARM           ZAPGRL  5
     C                     PARM           ZAMSID  7
     C                     PARM           ZAMSGF 10
     C                     PARM           ZAMSDA132
     C                     PARM           ZAMSTP  7
     C*
     C                     ENDSR
     C***************************************************************
     C/COPY ZSRSRC,ZSERPT2
     C/COPY ZSRSRC,ZSERPT3
**  CPY@ - OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
**
A BCDEFGHIJKLMNOPQRSTUVWXYZ0123456789
**
A BCDEFGHIJKLMNOPQRSTUVWXYZ
**
 ENTER START OF SECURITY NAME OR SELECT A SECURITY
 SECURITY NAME INVALID
 ROLLUP NOT VALID,LAST SECURITY NAME DISPLAYED
 ROLLDOWN KEY NOT VALID,FIRST SECURITY NAME DISPLAYED
 SECURITY NAME ENTERED IS AFTER LAST SECURITY NAME ON FILE
 INVALID ENTRY - ENTER Y OR N AGAINST THE REQUIRED SECURITY
 MORE THAN ONE SECURITY SELECTED
 NO TELEKURS NUMBER FOR THIS SECURITY - UPDATE SECURITY
 INVALID OPTION - MUST BE Y OR N
 INVALID OPTION - MUST BE Y OR N,AND A SECURITY NO/NAME ENTERED
 NO EXTRA SECURITIES SELECTED FOR TELEKURS
