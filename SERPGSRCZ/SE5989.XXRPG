     H        1
      *****************************************************************
/*S*D ***RPGBASE*******************************************************                       CSC056
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE5989 - Telekurs Change Take On                             *
      *                                                               *
      *  Function:  This take on program will be run to add 3 leading *
      *             zeros to Telekurs Valoren Reference on existing   *
      *             Securities in order to avoid manual entries to    *
      *             the users.                                        *
      *                                                               *
      *  (phase(s)) Installation                                      *
      *                                                               *
      *  Called By: Switchable Features Initialisation Processing     *
      *             Input Cycle Initiation of the 26th April 1999     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. CSC056  *REDUNDANT Date 06Aug12               *
      *                 CRE073             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      *                 CSE075             Date 17Nov05               *
      *                 CSE071             Date 19Jul05               *
      *                 CAS006             Date 21Jan03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CSD006             Date 16May01               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CSE023             Date 12Jul00               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CAP137             Date 07Feb00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSE009  *CREATE    Date 15Mar99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *             (Recompile)                                       *
      *  CSC056 - COB Restructure - SC COB Optimisation Phase 1       *
      *  CRE073 - Interest Rate Rounding (Recompile)                  *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE075 - US Enhanced Treasury Upgrade to MidasPlus           *
      *           (Recompile)                                         *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  CAS006 - Hedge Accounting Phase B (Recompile)                *
      *  CSD006 - Market Data Feed (Recompiled)                       *
      *  CSE023 - Treaty Withholding Tax (Recompiled)                 *
      *  CAP137 - Conversion of SE Security inputs into modular       *
      *           structure to use as APIs.                           *
      *           Recompiled over changed in PF/SECTYD.               *
      *  CSE009 - Telekurs Changes                                    *
      *                                                               *
      *****************************************************************
      /TITLE F LINES SPECIFICATIONS
      *****************************************************************
      *
     FSECTY   UF  E           K        DISK
     FSE5989P1O   E                    PRINTER      KINFDS SPOOL1
      *
      *****************************************************************
      /TITLE E LINES SPECIFICATIONS
      *****************************************************************
     E                    CPY@    1   1 80               COPYRIGHT  ARRAY
      *****************************************************************
      /TITLE I LINES SPECIFICATIONS
      *****************************************************************
      *
     IPGMDS     ESDSY2PGDSP
      * Program Data Structure
      *
     ISDBANK    E DSSDBANKPD
      * External DS for Bank Details
      *
     IDSSDY     E DSDSSDY
      * DS for access programs
      *
     IDSFDY     E DSDSFDY
      **  First DS for access programs, short DS
      *
     ISPOOL1      DS
      ***  File Information Data Structure for SE5986P1.
      *
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
     I            DS
     I                                        1  12 T2RF
     I                                        1   3 T2RF13
     I                                        1   6 T2RF16
     I                                        4   9 T2RF49
     I                                        7  12 T2RF12
      *****************************************************************
      /EJECT
      *****************************************************************
      *   START PROGRAM : MAINLINE
      *****************************************************************
      *
     C           *ENTRY    PLIST
     C                     PARM           @RTCD   7
     C                     PARM           @MODE   1
      *
      ** Reference Copyright Array
     C                     MOVEACPY@      CPY@@   1
      *
      ** Access Bank details via access program
      ** (database error handling done in access program)
     C                     CALL 'AOBANKR0'
     C                     PARM '*DBERR ' @RTCD
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSSDY
      *
     C           @MODE     IFEQ '1'                        B*1
      *
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
     C                     MOVE 'Y'       WFIRST  1
      *
     C           *LOVAL    SETLLSECTYDF
     C                     READ SECTYDF                  89
      *
     C           *IN89     DOWEQ'0'                        B*2
      *
     C           T2RF      IFNE *BLANKS                    B*3
      *
     C                     MOVE T2RF      ROT2RF
      *
     C           T2RF16    IFNE *BLANKS                    B*4
     C           T2RF12    ANDEQ*BLANKS
     C                     TESTN          T2RF16     80
     C           *IN80     IFEQ '1'                        B*5
     C                     MOVE T2RF16    WT2RF6  6
     C                     MOVE *BLANKS   T2RF
     C                     MOVE '000'     T2RF13
     C                     MOVE WT2RF6    T2RF49
     C                     UPDATSECTYDF
     C                     ENDIF                           E*5
     C                     ENDIF                           E*4
      *
     C                     Z-ADD3         RQDLN1           Check Overflow
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1                     B*4
     C           WFIRST    OREQ 'Y'
     C                     WRITEHEADP1
     C                     MOVE 'N'       WFIRST
     C                     ENDIF                           E*4
      *
     C                     MOVE SESN      RSESN
     C                     MOVE T2RF      RNT2RF
      *
     C           ROT2RF    IFEQ RNT2RF                     B*4
     C                     MOVE '1'       *IN50
     C                     ELSE                            X*4
     C                     MOVE '0'       *IN50
     C                     ENDIF                           E*4
      *
     C                     WRITEDETAIL1
      *
     C                     ENDIF                           E*3
      *
     C                     READ SECTYDF                  89
     C                     ENDDO                           E*2
      *
     C                     Z-ADD3         RQDLN1           Check Overflow
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1                     B*2
     C           WFIRST    OREQ 'Y'
     C                     WRITEHEADP1
     C                     MOVE 'N'       WFIRST
     C                     ENDIF                           E*2
      *
     C                     WRITETRAILP1
      *
     C                     ENDIF                           E*1
      *
     C                     SETON                         LR
     C                     RETRN
      *
      *****************************************************************
      /EJECT
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
