     H
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SC Fee Group Codes Audit List')
     F*****************************************************************
     F**                                                              *
     F**  Midas SWISS TAXES                                       *
     F**                                                              *
     F**  SE6210 - SAFE CUSTODY FEE GROUP CODES AUDIT/LIST            *
     F**                                                              *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F**                                                              *
     F**      FUNCTION:  This Program produces a report, for audit    *
     F**      (COB)      purposes, of all custody fee group codes de- *
     F**      (I/C)      fined to the system. The report runs on re-  *
     F**                 quest in I/C and COB and shows a full list,  *
     F**                 or automatically in COB and audits changes   *
     F**                 made today.                                  *
     F**                                                              *
     F**      CALLED BY: SEC6210 - Safe Custody Fee Group Codes       *
     F**                           Audit Report                       *
     F**                                                              *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD031             Date 10Apr06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 072361             Date 20May96               *
      *                 S01464             Date 04Apr94               *
     F**                   B7871      DC      DATE 15JUL91            *
     F**                                                              *
     F**--------------------------------------------------------------*
     F**                                                              *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
     F**  072361 - Correct B7871 by using the decimal places of the   *
     F**           bank charging currency on safe custody ICD file.   *
     F**  S01464 - Safe Custody Fees                                  *
     F**  B7871  - Add Minimum Commission                             *
     F**                                                              *
     F*****************************************************************
     F** C R E A T I O N     P A R A M E T E R S                      *
     F**                                                              *
     F**                                                              *
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     F**
     F**      FILE DEFINITIONS
     F**     ------------------
     F*
     F**   BANK DETAILS FILE                        - PrefiX BJ
     FSDBANKPDIF  E                    DISK         KINFSR *PSSR
     F*
     F**   CUSTODY FEE GROUPS DEFINITION FILE       - PREFIX SB
     F***SCCFGRPDIF**E***********K********DISK                            S01464
     FSECFGRPDIF  E           K        DISK                               S01464
     F*
     F**   CUSTODY FEE GROUPS TRAILER               - PREFIX SB
     F***SCCFGRZZIF**E********************DISK*********KINFSR**PSSR       S01464
     FSECFGRPAIF  E                    DISK         KINFSR *PSSR          S01464
     F*
     F**   SAFE CUSTODY FEE GROUPS - RUN CONTROLS
     FSE6210AUO   E             21     PRINTER      KINFDS ERF1DS
     F                                              KINFSR *PSSR
     F**   SAFE CUSTODY FEE GROUPS LIST
     F***SE6210P1O***E*************22*****PRINTER******KINFDS*ERF2DS      S01464
     FSE6210P1O   E             22     PRINTER      KINFDS ERF2DS     UC  S01464
     F                                              KINFSR *PSSR
     F*
     F/EJECT
     I*
     F*****************************************************************
     F**                                                              *
     F** ID F  C  H  L    FUNCTION OF INDICATORS                      *
     F**                 ------------------------                     *
     F**                                                              *
     F**    21         - OVERFLOW INDICATOR ON SE6210AU               *
     F**    22         - OVERFLOW INDICATOR ON SE6210P1               *
     F**                                                              *
     F**    10         - PRINT *** DIFFERENCE ***                     *
     F**    70         - FILE ERROR                                   *
     F**                                                              *
     F*****************************************************************
     F/EJECT
     E********************************************************************
     E*
     E**   ARRAY FOR COPYRIGHT
     E*
     E                    CPY@    1   1 80
     E**   ARRAY FOR POWER8                                               072361
     E                    POWER8  1   8  8 4                              072361
     E*
     E********************************************************************
     E/EJECT
     E*****************************************************************
     I*
     I**   DATA STRUCTURE FOR COMPILATION - COPYRIGHT INSERTION
     ICPYR@$      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@$$
     I*
     I**   PROGRAM STATUS DATA STRUCTURE
     IPSDS       SDS
     I                                      244 253 JOB
     I                                      254 263 USER
     I**   FILE INFORMATION DATA STRUCTURE
     IERF1DS      DS
     I                                       83  92 SFILEU                S01464
     I                                    B 123 1240SFNUMU                S01464
     I*
     I**   FOR OVERFLOW - RUN CONTROL
     I                                    B 367 3680LINE
     I*
     IERF2DS      DS
     I                                       83  92 SFILE                 S01464
     I                                    B 123 1240SFNUM                 S01464
     I*
     I**   FOR OVERFLOW - PRINT
     I                                    B 367 3680LINE1
     I*
     I**   LOCAL DATA AREA
     I***LDA********UDS****************************256                    S01464
     ILDA         DS                            256                       S01464
     I**************************************132 141 DBFILE                S01464
     I                                      134 141 DBFILE                S01464
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     I**   DATE FORMATTING DATA STRUCTURE
     IUPDATE      DS
     I                                        1   2 SBDAY
     I                                        3   5 SBMON
     I                                        6   7 SBYER
     ISDSCFE    E DSSDSCFEPD                                              072361
     I** SAFE CUSTODY FEE DETAILS ACCESSED VIA ACCESS PROGRAM             072361
     I**                                                                  072361
     ISDCURR    E DSSDCURRPD                                              072361
     I** CURRENCY CODE DETAILS ACCESSED VIA ACCESS PROGRAM                072361
     I**                                                                  072361
     IDSSDY     E DSDSSDY                                                 072361
     I** SECOND DS FOR ACCESS PROGRAMS, LONG DATA STRUCTURE               072361
     I**                                                                  072361
     C/EJECT
     C*****************************************************************
     C*
     C**              START MAINLINE
     C**             ----------------
     C*
     C**   INPUT PARAMETER LIST
     C*
     C           *ENTRY    PLIST
     C*
     C**   FOR SELECTION OF FULL LIST OR AUDIT LIST
     C*
     C                     PARM           MODE    1
     C                     PARM           @RSEQ   5                       S01464
     C*
     C**   INITIAL PROCESSING
     C*
     C                     EXSR #A
     C*
     C**   MAIN PROCESSING
     C*
     C                     EXSR #B
     C*                                                                   S01464
     C           WWOPEN    IFEQ 'Y'                                       S01464
     C*                                                                   S01464
     C** Call ZSFILE for P1 file                                          S01464
     C*                                                                   S01464
     C                     Z-ADDSFNUM     ZSNUM   60                      S01464
     C                     CALL 'ZSFILE'                                  S01464
     C                     PARM           @RSEQ                           S01464
     C                     PARM *BLANKS   @PARM   3                       S01464
     C                     PARM           SFILE                           S01464
     C                     PARM           ZSNUM                           S01464
     C                     PARM           ZSERR   1                       S01464
     C*                                                                   S01464
     C           ZSERR     IFEQ 'Y'                                       S01464
     C                     MOVE '1'       *INU7                           S01464
     C                     MOVE '1'       *INU8                           S01464
     C                     MOVE '1'       *INLR                           S01464
     C                     RETRN                                          S01464
     C                     END                                            S01464
     C*                                                                   S01464
     C                     CLOSESE6210P1                                  S01464
     C                     END                                            S01464
     C*                                                                   S01464
     C** Call ZSFILE for AU file                                          S01464
     C*                                                                   S01464
     C                     Z-ADDSFNUMU    ZSNUM   60                      S01464
     C                     CALL 'ZSFILE'                                  S01464
     C                     PARM           @RSEQ                           S01464
     C                     PARM *BLANKS   @PARM   3                       S01464
     C                     PARM           SFILEU                          S01464
     C                     PARM           ZSNUM                           S01464
     C                     PARM           ZSERR   1                       S01464
     C*                                                                   S01464
     C           ZSERR     IFEQ 'Y'                                       S01464
     C                     MOVE '1'       *INU7                           S01464
     C                     MOVE '1'       *INU8                           S01464
     C                     MOVE '1'       *INLR                           S01464
     C                     RETRN                                          S01464
     C                     END                                            S01464
     C*
     C**   END PROGRAM
     C*
     C                     MOVE '1'       *INLR
     C                     RETRN
     C*
     C/EJECT
     C*****************************************************************
     C**    INDEX TO SUBROUTINES                                      *
     C**   ----------------------                                     *
     C**                                                              *
     C**                                                              *
     C**   #A      - INITIAL PROCESSING                               *
     C**   #B      - MAIN PROCESSING                                  *
     C**   #BA     - PROCESS FULL SAFE CUSTODY FEE GROUPS LIST        *
     C**   #BB     - PROCESS FULL LIST RUN CONTROLS                   *
     C**   #BC     - PROCESS AUDIT SAFE CUSTODY FEE GROUPS LIST       *
     C**   #BD     - PROCESS AUDIT LIST RUN CONTROLS                  *
     C**   #ZA     - DATABASE ERRORS                                  *
     C**   #ZB     - SETUP OUTPUT FIELDS FOR CUSTODY FEE GROUPS       *
     C**             REPORT                                           *
     C**   *PSSR   - PROGRAM ERROR HANDLING SUBROUINE                 *
     C**                                                              *
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #A        - THIS SUBROUTINE PERFORMS INITIAL PROCESSING    *
     C**                                                              *
     C**   CALLED BY - MAINLINE                                       *
     C**                                                              *
     C*****************************************************************
     C*
     C           #A        BEGSR
     C*
     C                     SETOF                     10
     C                     Z-ADD*ZEROS    LINE
     C                     Z-ADD*ZEROS    LINE1
     C*
     C** Initialise DB error fields annd store program name for DB
     C** error field
     C** Prepare LDA                                                      S01464
     C*
     C           *NAMVAR   DEFN           LDA                             S01464
     C           *LOCK     IN   LDA                                       S01464
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     Z-ADD*ZEROS    DBASE
     C                     MOVEL'SE6210'  DBPGM
     C                     OUT  LDA                                       S01464
     C*
     C** INITIALISE WORK FIELDS
     C                     MOVE '1'       WWFTT   1
     C*
     C** Initialise run controls work fields
     C*
     C                     Z-ADD*ZEROS    RRPROG
     C                     Z-ADD*ZEROS    RRFILE
     C                     MOVE *BLANK    RRACTN
     C*
     C** USE BANK CHARGING CURRENCY FROM SAFE CUSTODY ICD                 072361
     C*                                                                   072361
     C                     CALL 'AOSCFER0'                                072361
     C                     PARM *BLANKS   @RTCD   7                       072361
     C                     PARM '*FIRST'  @OPTN   7                       072361
     C           SDSCFE    PARM SDSCFE    DSSDY                           072361
     C           @RTCD     IFNE *BLANKS                                   072361
     C           *LOCK     IN   LDA                                       072361
     C                     Z-ADD4         DBASE                           072361
     C                     MOVE *BLANKS   DBFILE           ***************072361
     C                     MOVEL'SDSCFEPD'DBFILE           * DBERROR 004 *072361
     C                     MOVE *BLANKS   DBKEY            ***************072361
     C                     OUT  LDA                                       072361
     C                     EXSR #ZA                                       072361
     C                     ENDIF                                          072361
     C*                                                                   072361
     C** USE ACCESS PROGRAM SDCURRPD TO GET DECIMAL PLACES.               072361
     C*                                                                   072361
     C                     CALL 'AOCURRR0'                                072361
     C                     PARM *BLANKS   @RTCD   7                       072361
     C                     PARM '*KEY'    @OPTN   7                       072361
     C                     PARM FBBCGC    @KEYB   3                       072361
     C           SDCURR    PARM SDCURR    DSSDY                           072361
     C           @RTCD     IFNE *BLANKS                                   072361
     C           *LOCK     IN   LDA                                       072361
     C                     Z-ADD5         DBASE                           072361
     C                     MOVE *BLANKS   DBFILE           ***************072361
     C                     MOVEL'SDCURRPD'DBFILE           * DBERROR 005 *072361
     C                     MOVELFBBCGC    DBKEY            ***************072361
     C                     OUT  LDA                                       072361
     C                     EXSR #ZA                                       072361
     C                     ENDIF                                          072361
     C*                                                                   072361
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #B        - This subroutine performs Main processing       *
     C**                                                              *
     C**   CALLS     - #BA #BB #BC #BD #ZA                            *
     C**                                                              *
     C**   CALLED BY - MAINLINE                                       *
     C**                                                              *
     C*****************************************************************
     C           #B        BEGSR
     C** Access Bank details file
     C*
     C           1         SETLLSDBANKPD
     C                     READ SDBANKPD                 70
     C*
     C** If record not found or last change type = 'D' (DELETED)
     C*
     C           *IN70     IFEQ '1'
     C           BJTYLC    OREQ 'D'
     C           *LOCK     IN   LDA                                       S01464
     C                     Z-ADD1         DBASE            ****************
     C                     MOVEL'SDBANKPD'DBFILE           * DBERROR  001 *
     C                     MOVE *BLANKS   DBKEY            ****************
     C                     OUT  LDA                                       S01464
     C                     EXSR #ZA
     C                     END
     C*
     C                     ADD  1         RRFILE
     C*
     C** CHECK INPUT PARAMETER
     C*
     C           MODE      IFEQ '2'
     C*
     C** FULL LIST
     C*
     C                     EXSR #BA
     C*
     C** FULL LIST RUN CONTROLS PROCESSING
     C*
     C                     EXSR #BB
     C                     ELSE
     C           MODE      IFEQ '1'
     C*
     C** AUDIT LIST
     C*
     C                     EXSR #BC
     C*
     C** AUDIT LIST RUN CONTROLS PROCESSING
     C*
     C                     EXSR #BD
     C                     ELSE
     C*
     C** STANDARD DATABASE ERROR HANDLING
     C*
     C           *LOCK     IN   LDA                                       S01464
     C                     Z-ADD3         DBASE
     C                     MOVEL'MODE'    DBFILE           ***************
     C                     MOVELMODE      DBKEY            * DBERROR 003 *
     C                     OUT  LDA                        ***************S01464
     C                     EXSR #ZA                        ***************
     C                     END
     C                     END
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #ZA       - Process Database Errors                        *
     C**                                                              *
     C**   CALLS     - None                                           *
     C**                                                              *
     C**   CALLED BY - #A #B #BB #BD                                  *
     C**                                                              *
     C*****************************************************************
     C*
     C           #ZA       BEGSR
     C*
     C                     WRITEHEADER
     C*
     C* WRITE TITLE FOR FULL LIST OR AUDIT LIST
     C*
     C           MODE      IFEQ '1'
     C                     WRITEHEAD3
     C                     ELSE
     C                     WRITEHEAD2
     C                     END
     C*
     C                     WRITEDBERROR
     C***********          WRITEFOOTING                                   S01464
     C*                                                                   S01464
     C** Call ZSFILE for AU file                                          S01464
     C*                                                                   S01464
     C                     Z-ADDSFNUMU    ZSNUM   60                      S01464
     C                     CALL 'ZSFILE'                                  S01464
     C                     PARM           @RSEQ                           S01464
     C                     PARM *BLANKS   @PARM   3                       S01464
     C                     PARM           SFILEU                          S01464
     C                     PARM           ZSNUM                           S01464
     C                     PARM           ZSERR   1                       S01464
     C*                                                                   S01464
     C           ZSERR     IFEQ 'Y'                                       S01464
     C                     MOVE '1'       *INU7                           S01464
     C                     MOVE '1'       *INU8                           S01464
     C                     MOVE '1'       *INLR                           S01464
     C                     RETRN                                          S01464
     C                     END                                            S01464
     C*
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     RETRN
     C*
     C                     ENDSR
     C/EJECT
     C*
     C*****************************************************************
     C**                                                              *
     C**   #BA       - PROCESS FULL SAFE CUSTODY FEE GROUPS LIST      *
     C**                                                              *
     C**   CALLS     - #ZB                                            *
     C**                                                              *
     C**   CALLED BY - #B                                             *
     C**                                                              *
     C*****************************************************************
     C*
     C           #BA       BEGSR
     C*
     C***********          READ SCCFGRPD                 70               S01464
     C                     READ SECFGRPD                 70               S01464
     C           *IN70     IFEQ '1'
     C*
     C** SETON FLAG TO INDICATE NULL REPORT
     C*
     C                     MOVE '1'       WWEMTF  1
     C***********          WRITEHEADER1                                   S01464
     C***********          WRITEHEADER2                                   S01464
     C***********          WRITEHEADER4                                   S01464
     C*                                                                   S01464
     C** SHOULD NOT PRODUCE RUN CONTROLS ANYMORE                          S01464
     C*                                                                   S01464
     C                     MOVE 'Y'       WWNPRC  1                       S01464
     C                     WRITEHEADER                                    S01464
     C                     WRITEHEAD2                                     S01464
     C                     WRITENULL1
     C***********          END                                            S01464
     C                     ELSE                                           S01464
     C                     OPEN SE6210P1                                  S01464
     C                     MOVE 'Y'       WWOPEN  1                       S01464
     C*
     C** WHILE RECORDS ON FILE
     C*
     C           *IN70     DOWEQ'0'
     C           SBRECI    IFEQ 'D'
     C*
     C** ADD 1 TO NUMBER OF RECORDS READ
     C*
     C                     ADD  1         RRPROG
     C*
     C** OUTPUT DETAILS TO REPORT
     C*
     C                     EXSR #ZB
     C                     END
     C***********          READ SCCFGRPD                 70
     C                     READ SECFGRPD                 70
     C                     END
     C***********                                                         S01464
     C***NO*LIVE*RECORDS*FOUND*ON*FILE*SCCFGRPD                           S01464
     C***********                                                         S01464
     C*                                                                   S01464
     C** No live records found on SECFGRPD                                S01464
     C*                                                                   S01464
     C           RRPROG    IFEQ 0
     C           WWEMTF    ANDNE'1'
     C***********          WRITEHEADER1                                   S01464
     C***********          WRITEHEADER2                                   S01464
     C*                                                                   S01464
     C** SHOULD NOT PRODUCE RUN CONTROLS ANYMORE                          S01464
     C*                                                                   S01464
     C                     MOVE 'Y'       WWNPRC  1                       S01464
     C                     WRITEHEADER                                    S01464
     C                     WRITEHEAD2                                     S01464
     C                     WRITENULL1
     C                     END
     C*
     C           LINE1     IFGT 55
     C                     WRITEHEADER1
     C                     WRITEHEADER2
     C                     WRITEHEADER4
     C                     END
     C                     WRITEFOOTING1
     C*
     C                     END                                            S01464
     C*                                                                   S01464
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #ZB       - SETUP OUTPUT FIELDS FOR TABLE LIST             *
     C**                                                              *
     C**   CALLS     - NONE                                           *
     C**                                                              *
     C**   CALLED BY - #BA #BC                                        *
     C**                                                              *
     C*****************************************************************
     C*
     C           #ZB       BEGSR
     C*
     C           SBCHTP    IFEQ 'I'
     C                     MOVEL'INSERT'  RRACTN
     C                     END
     C*
     C           SBCHTP    IFEQ 'A'
     C                     MOVEL'AMEND '  RRACTN
     C                     END
     C*
     C           SBCHTP    IFEQ 'D'
     C                     MOVEL'DELETE'  RRACTN
     C                     END
     C*
     C** SET UP GROUP TITLE FOR OUTPUT
     C*
     C                     MOVE SBCFGN    RRCFGN
     C*
     C** SET UP GROUP CODE
     C*
     C                     MOVE SBCFGC    RRCFGC
     C*                                                                   B7871
     C** SET UP MINIMUM COMMISSION                                        B7871
     C*                                                                   B7871
     C*****************    MOVE SBMICO    RRMICO                     B7871072361
     C           *LIKE     DEFN SBMICO    WWMICO                          072361
     C                     Z-ADDA6NBDP    DP      30                      072361
     C                     ADD  5         DP                              072361
     C*                                                                   072361
     C*IF DECIMAL PLACES IS EQUAL TO ZERO THE CORRECT IT TO USED EDTWRD   072361
     C*                                                                   072361
     C           A6NBDP    IFEQ *ZEROS                                    072361
     C                     ADD  2         DP                              072361
     C                     ENDIF                                          072361
     C           SBMICO    MULT POWER8,DP WWMICO                          072361
     C                     Z-ADDWWMICO    RRMICO                          072361
     C*
     C** SET UP LAST ACTIVITY DATE FOR OUTPUT
     C*
     C                     MOVE SBDLUP    SBDAY
     C                     MOVE SBMLUP    SBMON
     C                     MOVE SBYLUP    SBYER
     C                     MOVE UPDATE    RRDATE
     C*
     C** SET UP LAST ACTIVITY TIME FOR OUTPUT
     C*
     C                     MOVE SBTLUP    RRTIME
     C*
     C           LINE1     IFGT 55
     C           WWFTT     OREQ '1'
     C                     WRITEHEADER1
     C           MODE      IFEQ '2'
     C                     WRITEHEADER2
     C                     ELSE
     C           MODE      IFEQ '1'
     C                     WRITEHEADER3
     C                     END
     C                     END
     C                     WRITEHEADER4
     C                     MOVE '0'       WWFTT
     C                     END
     C                     WRITEDETAIL1
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #BC       - PROCESS AUDIT SAFE CUSTODY FEE GROUPS LIST     *
     C**                                                              *
     C**   CALLS     - #ZB                                            *
     C**                                                              *
     C**   CALLED BY - #B                                             *
     C**                                                              *
     C*****************************************************************
     C*
     C           #BC       BEGSR
     C*
     C***********          READ SCCFGRPD                 70
     C                     READ SECFGRPD                 70              S01464
     C           *IN70     IFEQ '1'
     C*
     C** SETON FLAG TO INDICATE NULL REPORT
     C*
     C                     MOVE '1'       WWEMTF  1
     C***********          WRITEHEADER1                                   S01464
     C***********          WRITEHEADER3                                   S01464
     C*                                                                   S01464
     C** SHOULD NOT PRODUCE RUN CONTROLS ANYMORE                          S01464
     C*                                                                   S01464
     C                     MOVE 'Y'       WWNPRC  1                       S01464
     C                     WRITEHEADER                                    S01464
     C                     WRITEHEAD3                                     S01464
     C                     WRITENULL1
     C***********          END                                            S01464
     C                     ELSE                                           S01464
     C                     OPEN SE6210P1                                  S01464
     C                     MOVE 'Y'       WWOPEN                          S01464
     C*
     C           *IN70     DOWEQ'0'
     C*
     C** ADD 1 TO NUMBER OF RECORDS READ
     C*
     C                     ADD  1         RRPROG
     C*
     C** ADD 1 TO NUMBER OF INSERTS
     C*
     C           SBCHTP    IFEQ 'I'
     C                     ADD  1         RRINSR
     C                     END
     C*
     C** ADD 1 TO NUMBER OF AMENDS
     C*
     C           SBCHTP    IFEQ 'A'
     C                     ADD  1         RRAMND
     C                     END
     C*
     C** ADD 1 TO NUMBER OF DELETES
     C*
     C           SBCHTP    IFEQ 'D'
     C                     ADD  1         RRDLET
     C                     END
     C*
     C** OUTPUT DETAILS TO REPORT
     C*
     C                     EXSR #ZB
     C***********          READ SCCFGRPD                 70               S01464
     C                     READ SECFGRPD                 70               S01464
     C                     END
     C*
     C***NO*LIVE*RECORDS*FOUND*ON*FILE*SCCFGRPD                           S01464
     C** NO LIVE RECORDS FOUND ON FILE SECFGRPD                           S01464
     C*
     C           RRPROG    IFEQ 0
     C           WWEMTF    ANDNE'1'
     C***********          WRITEHEADER1                                   S01464
     C***********          WRITEHEADER3                                   S01464
     C*                                                                   S01464
     C** SHOULD NOT PRODUCE RUN CONTROLS ANYMORE                          S01464
     C*                                                                   S01464
     C                     MOVE 'Y'       WWNPRC  1                       S01464
     C                     WRITEHEADER                                    S01464
     C                     WRITEHEAD3                                     S01464
     C                     WRITENULL1
     C                     END
     C*
     C           LINE1     IFGT 55
     C                     WRITEHEADER1
     C                     WRITEHEADER3
     C                     WRITEHEADER4
     C                     END
     C                     WRITEFOOTING1
     C*
     C                     END                                            S01464
     C*                                                                   S01464
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #BB       - PROCESS FULL LIST RUN CONTROLS                 *
     C**                                                              *
     C**   CALLS     - #ZA                                            *
     C**                                                              *
     C**   CALLED BY - #B                                             *
     C**                                                              *
     C*****************************************************************
     C*
     C           #BB       BEGSR
     C*
     C***********          READ SCCFGRZZ                 70               S01464
     C                     READ SECFGRPA                 70               S01464
     C           *IN70     IFEQ '1'
     C           SBRECI    ORNE 'Z'
     C*
     C** TRAILER RECORD NOT FOUND - DATABASE ERROR
     C*
     C           *LOCK     IN   LDA                                       S01464
     C                     Z-ADD2         DBASE
     C***********          MOVEL'SCCFGRZZ'DBFILE           ***************S01464
     C                     MOVEL'SECFGRPA'DBFILE           ***************S01464
     C                     MOVEL*BLANKS   DBKEY            * DBERROR 002 *
     C                     OUT  LDA                        ***************S01464
     C                     EXSR #ZA                        ***************
     C                     ELSE
     C           RRPROG    IFNE SBNORE
     C*
     C** RECORDS READ <> RECORDS ON FILE
     C*
     C                     MOVE '1'       *IN10
     C                     MOVE '1'       *INU8
     C                     END
     C                     MOVE SBNORE    RRFILE
     C*
     C** OUTPUT RUN CONTROLS REPORT
     C*
     C           WWNPRC    IFNE 'Y'                                       S01464
     C                     WRITEHEADER
     C                     WRITEHEAD2
     C                     WRITEDETAILA
     C                     END                                            S01464
     C***********          WRITEFOOTING
     C                     END
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   #BD       - PROCESS AUDIT LIST RUN CONTROLS                *
     C**                                                              *
     C**   CALLS     - #ZA                                            *
     C**                                                              *
     C**   CALLED BY - #B                                             *
     C**                                                              *
     C*****************************************************************
     C*
     C           #BD       BEGSR
     C*
     C***********          READ SCCFGRZZ                 70               S01464
     C                     READ SECFGRPA                 70               S01464
     C           *IN70     IFEQ '1'
     C           SBRECI    ORNE 'Z'
     C*
     C** TRAILER RECORD NOT FOUND - DATABASE ERROR
     C*
     C           *LOCK     IN   LDA                                       S01464
     C                     Z-ADD2         DBASE
     C***********          MOVEL'SCCFGRZZ'DBFILE           ***************S01464
     C                     MOVEL'SECFGRPA'DBFILE           ***************S01464
     C                     MOVEL*BLANKS   DBKEY            * DBERROR 002 *
     C                     OUT  LDA                        ***************S01464
     C                     EXSR #ZA                        ***************
     C                     ELSE
     C*
     C** OUTPUT RUN CONTROLS REPORT
     C*
     C           WWNPRC    IFNE 'Y'                                       S01464
     C                     WRITEHEADER
     C                     WRITEHEAD3
     C                     WRITEDETAILB
     C                     END                                            S01464
     C***********          WRITEFOOTING                                   S01464
     C                     END
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   *PSSR     - Program error handling routine                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR
     C                     DUMP
     C*
     C                     ENDSR
     C*****************************************************************
     C/EJECT
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
** POWER8                                                                 072361
00000001
00000010
00000100
00001000
00010000
00100000
01000000
10000000
