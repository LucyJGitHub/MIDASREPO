     H
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas Generated Entries Report')                       *
     F********************************************************************
     F*                                                                  *
     F*    Midas SECURITIES TRADING                                  *
     F*                                                                  *
     F*    SE6290  - GENERATED ENTRIES REPORT                            *
     F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                                  *
     F*  FUNCTION:  This Program produces a report, of all postings      *
     F*  (COB)      generated from accrual or settlement processing      *
     F*             today.                                               *
     F*                                                                  *
     F*  CALLED BY: SEC6290 - Safe Custody Fees Generated Entries        *
     F*                       Report                                     *
     F*                                                                  *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CGL007             Date 26Mar99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 RUEL01             Date 14Jun94               *
      *                 S01464             Date 04Apr94               *
     F*                    R1010              DATE 03DEC90               *
     F*                                                                  *
     F*------------------------------------------------------------------*
     F*                                                                  *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *           (Recompile)                                            *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL007 - Account Postings Enquiry.  Recompile.               *
     F*    RUEL01 - FILE OUT OF BALANCE ON SE6290AU                      *
     F*    S01464 - SAFE CUSTODY FEES                                    *
     F*                                                                  *
     F********************************************************************
     F*                                                               *  *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F***SCGESCL1IF**E***********K********DISK                            S01464
     FSEGESCL1IF  E           K        DISK                               S01464
     F** PREFIX :
     F                                              KINFSR *PSSR
     FSDBANKL1IF  E           K        DISK
     F** PREFIX : BJ
     F                                              KINFSR *PSSR
     FSDCURRL0IF  E           K        DISK
     F** PREFIX : A6
     F                                              KINFSR *PSSR
     FGESCZZ  IF  E           K        DISK
     F** PREFIX :
     F                                              KINFSR *PSSR
     F***SE6290P1O***E*************70*****PRINTER                       UCS01464
     FSE6290P1O   E             70     PRINTER      KINFDS ERF1DS     UC  S01464
     F** PREFIX : RR
     F***SE6290AUO***E********************PRINTER                         S01464
     FSE6290AUO   E                    PRINTER      KINFDS ERF2DS         S01464
     F** PREFIX : RR
     F*
     F*
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F*
     F*       70         OVERFLOW ON SE6290P1
     F*       71         ACCESS ON LF/SDBANKL1
     F*       72         ACCESS ON LF/SDCURRL0
     F*       73         ACCESS ON PF/GESCZZ
     F********74*********ACCESS ON LF/SCGESCL1                            S01464
     F*       74         ACCESS ON LF/SEGESCL1                            S01464
     F*       81         FIRST RECORD READ ON LF/SCGESCL1
     F*       82         FILE OUT OF BALANCE
     F*       88         MULTI-BRANCHING INDICATOR.                       S01464
     F*
     F*       98         DATE IN FORMAT MMDDYY
     F*       99         WORK
     F*
     F*                Unused indicators
     F*                -----------------
     F*       01  02  03  04  05  06  07  08  09  10
     F*       11  12  13  14  15  16  17  18  19  20
     F*       21  22  23  24  25  26  27  28  29  30
     F*       31  32  33  34  35  36  37  38  39  40
     F*       41  42  43  44  45  46  47  48  49  50
     F*       51  52  53  54  55  56  57  58  59  60
     F*       61  62  63  64  65  66  67  68  69  ..
     F*       ..  ..  ..  ..  75  76  77  78  79  80
     F********..  ..  83  84  85  86  87  88  89  90                      S01464
     F*       ..  ..  83  84  85  86  87  ..  89  90                      S01464
     F*       91  92  93  94  95  96  97  ..  ..
     F*
     F*
     F*       U7         DATABASE ERROR
     F*       U8         PROGRAM  ERROR
     F*
     E*=========================================================================
     E*
     E** ARRAY FOR COPYRIGHT
     E*
     E                    CPY@    1   1 80
     E***********                                                         S01464
     E***ARRAY*FOR*QCMDEXC*-*IBM*COMMAND                                  S01464
     E***********                                                         S01464
     E***********         CMD     1   1 39                                S01464
      /COPY ZSRSRC,ZDATE2Z1
     E*
      /COPY ZSRSRC,ZSEDITZ1
     I*
     I** LOCAL DATA AREA FOR DATABASE ERROR DETAILS
     I*
     I***LDA*********DS****************************256                    S01464
     ILDA         DS                            256                       S01464
     I**************************************132 141 DBFILE                S01464
     I                                      134 141 DBFILE                S01464
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     IRUNDT       DS                             13                       S01464
     I** Rundate data area                                                S01464
     I                                       13  13 MBIN                  S01464
     I*                                                                   S01464
     I**   FILE INFORMATION DATA STRUCTURE                                S01464
     IERF1DS      DS                                                      S01464
     I                                       83  92 SFILE                 S01464
     I                                    B 123 1240SFNUM                 S01464
     I*                                                                   S01464
     I**   FOR OVERFLOW - DETAIL                                          S01464
     I                                    B 367 3680LINE                  S01464
     I*                                                                   S01464
     IERF2DS      DS                                                      S01464
     I                                       83  92 SFILEU                S01464
     I                                    B 123 1240SFNUMU                S01464
     I*                                                                   S01464
     I**   FOR OVERFLOW - AUDIT                                           S01464
     I                                    B 367 3680LINE1                 S01464
     I*                                                                   S01464
      /COPY ZSRSRC,ZSEDITZ2
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * INDEX OF SUBROUTINES                                             *
      * MAIN     - MAIN PROCESSING                                       *
      * #INIT    - INITIAL PROCESSING                                    *
      * #P01     - PRINT HEADER DETAILS                                  *
      * #W02     - WRITE GENERATED ENTRY DETAIL LINE                     *
      * #W04     - WRITE SC GENERATED ENTRIES RUN CONTROL REPORT         *
      * *PSSR    - DATA BASE ERROR PROCESSING                            *
      * ZDATE2   - CONVERT MIDAS DAY NUMBER TO DDMMMYY                   *
      * ZSEDIT   - INSERT A DECIMAL POINT AND SIGN INTO A NUMERIC FIE    *
      *                                                                  *
      * EXTERNAL ROUTINES CALLED                                         *
      **QCMDEXC**-*COMMAND*OVERRIDE*PRINTER*FILE                         *S01464
      *                                                                  *
      ********************************************************************
     C*================================================================
     C*
     C************LIKE     DEFN PNAR      WWCMD +13                       S01464
     C************LIKE     DEFN CCY       WWCMDP+ 1                       S01464
     C           *LIKE     DEFN CCY       WWCCY
     C************LIKE     DEFN BRCD      WWBRCD                          S01464
     C           *LIKE     DEFN BRCA      WWBRCD                          S01464
      /EJECT
     C*================================================================
     C*  P R O G R A M     S T A R T
     C*================================================================
      *
      ** PERFORM SETUP OF STANDARD FIELDS
      *
     C                     EXSR #INIT
      *
      ***ACCESS*LF/SCGESCL1                                               S01464
      ** ACCESS LF/SEGESCL1                                               S01464
      *
     C***********          READ SCGESCL1                 74               S01464
     C                     READ SEGESCL1                 74               S01464
      *
     C***        *IN74     IFEQ '1'                        - B1           R1010
     C           *IN74     IFEQ '0'                        - B1           R1010
      *
      *  WRITE HEADER
      *
     C***                  EXSR #P01                                      R1010
      *
      ** WRITE NO DETAILS TO PRINT
      *
     C***                  WRITESE6290F3                                  R1010
      *
     C***                  END                             - E1           R1010
      *
      ** *IN81 SET TO '1' TO SHOW IT IS THE FIRST RECORD READ
      *
     C                     MOVE '1'       *IN81
      *
      ** SAVE BRANCH CODE
      *
     C***********          MOVE BRCD      WWBRCD                          S01464
     C                     MOVE BRCA      WWBRCD                          S01464
      *
      ** SAVE CURRENCY CODE
      *
     C                     MOVE CCY       WWCCY
      *
      ***PROCESS*UNTIL*END*OF*FILE*LF/SCGESCL1                            S01464
      ** PROCESS UNTIL END OF FILE LF/SEGESCL1                            S01464
      *
     C           *IN74     DOWEQ'0'                        - B1
      *
      ** FIRST RECORD READ OR BRANCH CODE HAS CHANGED OR NEW PAGE
      *
     C***********BRCD      IFNE WWBRCD                     - B2           S01464
     C           BRCA      IFNE WWBRCD                     - B2           S01464
     C           *IN81     OREQ '1'
      *
      ** WRITE HEADER
      *
     C                     EXSR #P01
      *
     C                     END                             - E2
     C*                                                                   S01464
     C                     MOVE BRCA      WWBRCD                          S01464
      *
      ** FIRST RECORD READ OR CURRENCY HAS CHANGED
      *
     C           *IN81     IFEQ '1'                        - B2
     C           CCY       ORNE WWCCY
      *
      ** ACCESS LF/SDCURRL0 WITH CURRENCY CODE TO GET NUMBER OF
      ** DECIMAL PLACES
      *
     C           CCY       CHAINSDCURRL0             72
      *
     C           *IN72     IFEQ '1'                        - B3
     C           *LOCK     IN   LDA                                       S01464
     C                     Z-ADD2         DBASE            ***************
     C                     MOVEL'SDCURRL0'DBFILE           **DB ERROR 02**
     C                     MOVE CCY       DBKEY            ***************
     C                     OUT  LDA                                       S01464
     C                     EXSR *PSSR
     C                     END                             - E3
      *
     C                     MOVE CCY       WWCCY
      *
     C                     END                             - E2
      *
      ** ADD 1 TO COUNTER OF RECORD
      *
     C                     ADD  1         RRCNT
      *
      ** CHECK OVERFLOW ON SE6290P1
      *
     C           *IN70     IFEQ '1'                        - B2
      *
     C                     WRITESE6290F1
      *
     C                     MOVE '0'       *IN70
      *
     C                     END                             - E2
      *
      ** WRITE GENERATED ENTRIES
      *
     C                     EXSR #W02
      *
     C***********          READ SCGESCL1                 74               S01464
     C                     READ SEGESCL1                 74               S01464
      *
      ** RESET INDICATOR OF FIRST RECORD READ
      *
     C                     MOVE '0'       *IN81
      *
     C                     END                             - E1
     C*                                                                   S01464
     C           *IN74     IFEQ '1'                                       S01464
      *
      ** WRITE END OF REPORT TRAILER
     C                     WRITESE6290F4
     C                     CLOSESE6290P1                                  S01464
     C                     END                                            S01464
      *
      ** ACCESS PF/GESCZZ
      *
     C                     READ GESCZZ                   73
      *
     C           *IN73     IFEQ '1'                        - B1
     C           *LOCK     IN   LDA                                       S01464
     C                     Z-ADD3         DBASE            ***************
     C                     MOVEL'GESCZZ  'DBFILE           **DB ERROR 03**
     C                     MOVE *BLANKS   DBKEY            ***************
     C                     OUT  LDA                                       S01464
     C                     EXSR *PSSR
     C                     END                             - E3
      *
      ** WRITE GENERATED ENTRIES RUN CONTROL REPORT
      *
     C                     EXSR #W04
      *                                                                   S01464
     C                     ELSE                                           S01464
     C                     WRITEHEADAU                                    S01464
     C                     WRITENULL1                                     S01464
      *
      ** TERMINATE PROGRAM
      *
     C                     END                             - E1            R1010
     C*                                                                   S01464
     C** ENSURE TOTAL SPOOL FILE RECORDED BY RCF                          S01464
     C*                                                                   S01464
     C                     Z-ADDSFNUMU    SFNUM2  60                      S01464
     C*                                                                   S01464
     C                     CALL 'ZSFILE'                                  S01464
     C                     PARM           @RSEQ   5                       S01464
     C                     PARM *BLANK    SENTY   3                       S01464
     C                     PARM           SFILEU                          S01464
     C                     PARM           SFNUM2                          S01464
     C                     PARM           ZSERR   1                       S01464
     C*                                                                   S01464
     C           ZSERR     IFEQ 'Y'                                       S01464
     C                     MOVE '1'       *INU7                           S01464
     C                     MOVE '1'       *INU8                           S01464
     C                     MOVE '1'       *INLR                           S01464
     C                     RETRN                                          S01464
     C                     END                                            S01464
     C*                                                                   S01464
     C                     MOVE '1'       *INLR
     C*================================================================
     C*  P R O G R A M     E N D
     C*================================================================
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #INIT    - INITIAL PROCESSING                                    *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * *PSSR    - DATA BASE ERROR PROCESSING                            *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     - MAIN PROCESSING                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      **WWCMD****-*COMMAND*LINE                                          *S01464
      **WWCMDP***-*END*COMMAND*LINE                                      *S01464
      *                                                                  *
      ********************************************************************
     C           #INIT     BEGSR                           **#INIT BSR**
      *
     C                     MOVEACPY@      BIS@   80
      *
      ** PREPARE LDA
      *
     C           *NAMVAR   DEFN           LDA                             S01464
     C           *LOCK     IN   LDA                                       S01464
     C                     MOVEL'SE6290'  DBPGM
     C                     OUT  LDA                                       S01464
     C*                                                                   S01464
     C** In the data area RUNDAT                                          S01464
     C*                                                                   S01464
     C           *NAMVAR   DEFN RUNDAT    RUNDT                           S01464
     C                     IN   RUNDT                                     S01464
      *
      ** SET EDIT CODE FOR ZSEDIT
      *
     C                     MOVE 'J'       ZECODE
      *
      ** GET INSTALLATION CONTROL DATA RECORD 1
      *
     C           *LOVAL    SETLLSDBANKL1
     C                     READ SDBANKL1                 75
      *
     C           *IN75     IFEQ '1'                        - B1
     C           *LOCK     IN   LDA                                       S01464
     C                     Z-ADD1         DBASE            ***************
     C                     MOVEL'SDBANKL1'DBFILE           **DB ERROR 01**
     C                     MOVE *BLANKS   DBKEY            ***************
     C                     OUT  LDA                                       S01464
     C                     EXSR *PSSR
     C                     END                             - E1
     C*                                                                   S01464
     C** Test for multibranching                                          S01464
     C*                                                                   S01464
     C           MBIN      IFEQ 'Y'                                       S01464
     C                     MOVE '1'       *IN88                           S01464
     C                     ELSE                                           S01464
     C                     MOVE '0'       *IN88                           S01464
     C                     END                                            S01464
      *
      *  CHECK THE FORMAT OF THE DATE
      *
     C           BJDFIN    IFEQ 'M'                        - B1
     C                     MOVE '1'       *IN98
     C                     END                             - E1
      ***********                                                         S01464
      ***BUILD*COMMAND*EXPRESSION*FOR*QCMDEXC                             S01464
      ***********                                                         S01464
     C***********          MOVELCMD,1     WWCMD                           S01464
     C***********          MOVE ')'       WWCMDP                          S01464
      *
      ** INITIALIZE COUNTER
      *
     C                     Z-ADD2         RRCNT                           RUEL01
      *
     C                     ENDSR                           **#INIT ESR**
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #P01     - PRINT HEADER DETAILS                                  *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      **QCMDEXC**-*COMMAND*OVERRIDE*PRINTER*FILE                         *S01464
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     - MAIN PROCESSING                                       *
      * MAIN     - MAIN PROCESSING                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      **WWCMDP***-*END*COMMAND*LINE                                      *S01464
      **WWCMD****-*COMMAND*LINE                                          *S01464
      * WWBRCD   - BRANCH CODE                                           *
      *                                                                  *
      ********************************************************************
     C           #P01      BEGSR                           **#P01  BSR**
      *
      ***CHECK*IF*FIRST*RECORD*READ                                       S01464
      ***********                                                         S01464
     C************IN81     IFEQ '1'                        - B1           S01464
      ***********                                                         S01464
      ***SET*THE*PARAMETER*FOR*QCMDEXC*COMMAND*(BRANCH*CODE)              S01464
      ***********                                                         S01464
     C***********          MOVELBRCD      WWCMDP                          S01464
     C***********          MOVE WWCMDP    WWCMD                           S01464
      ***********                                                         S01464
      ***EXEC*QCMDEXC*COMMAND*(OVERRIDE*THE*PRINTER*FILE*WITH*THE*BRANCH  S01464
      ***CODE)                                                            S01464
      ***********                                                         S01464
     C***********          CALL 'QCMDEXC'                                 S01464
     C***********          PARM WWCMD     QCAA   43                       S01464
     C***********          PARM 43        QCAN   155                      S01464
      ***********                                                         S01464
      ***OPEN*THE*PRINTER*FILE                                            S01464
      ***********                                                         S01464
     C***********          OPEN SE6290P1                                  S01464
      ***********
     C***********          ELSE                            - X1
      *
      ** OTHERWISE, IF THE BRANCH CODE HAS CHANGED
      *
     C           WWOPEN    IFEQ 'Y'                                       S01464
     C                     WRITESE6290F4
      *
     C                     CLOSESE6290P1
      ***********                                                         S01464
      ***SET*THE*PARAMETER*FOR*QCMDEXC*COMMAND*(BRANCH CODE)              S01464
      ***********                                                         S01464
     C***********          MOVELBRCD      WWCMDP                          S01464
     C***********          MOVE WWCMDP    WWCMD                           S01464
      ***********                                                         S01464
      ***EXEC*QCMDEXC*COMMAND*(OVERRIDE*THE*PRINTER*FILE*WITH*THE*BRANCH  S01464
      ***CODE)                                                            S01464
      ***********                                                         S01464
     C***********          CALL 'QCMDEXC'                                 S01464
     C***********          PARM WWCMD     QCAA                            S01464
     C***********          PARM 43        QCAN                            S01464
      ***********                                                         S01464
      ***OPEN*THE*PRINTER*FILE                                            S01464
      ***********                                                         S01464
     C***********          OPEN SE6290P1                                  S01464
      *
     C                     END                             - E1
     C*                                                                   S01464
     C                     OPEN SE6290P1                                  S01464
     C                     Z-ADD*ZERO     PAGE                            S01464
     C                     MOVE 'Y'       WWOPEN  1                       S01464
      *
      ** WRITE THE HEADINGS OF THE REPORT
      *
     C***********          MOVE BRCD      RRBRCD                          S01464
     C                     MOVE BRCA      RRBRCD                          S01464
     C***********          MOVE BRCD      WWBRCD                          S01464
      *
     C                     WRITESE6290F1
     C*                                                                   S01464
     C** ENSURE REPORT SPOOL FILE RECORDED BY RCF                         S01464
     C*                                                                   S01464
     C                     Z-ADDSFNUM     SFNUM1  60                      S01464
     C*                                                                   S01464
     C                     CALL 'ZSFILE'                                  S01464
     C                     PARM           @RSEQ                           S01464
     C                     PARM BRCA      SENTY   3                       S01464
     C                     PARM           SFILE                           S01464
     C                     PARM           SFNUM1                          S01464
     C                     PARM           ZSERR   1                       S01464
     C*                                                                   S01464
     C           ZSERR     IFEQ 'Y'                                       S01464
     C                     MOVE '1'       *INU7                           S01464
     C                     MOVE '1'       *INU8                           S01464
     C                     MOVE '1'       *INLR                           S01464
     C                     RETRN                                          S01464
     C                     END                                            S01464
      *
     C                     ENDSR                           **#P01  ESR**
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #W02     - WRITE GENERATED ENTRY DETAIL LINE                     *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * ZDATE2   - CONVERT MIDAS DAY NUMBER TO DDMMMYY                   *
      * ZSEDIT   - INSERT A DECIMAL POINT AND SIGN INTO A NUMERIC FIE    *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     - MAIN PROCESSING                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           #W02      BEGSR                           **#W02  BSR**
      ** SET CUSTOMER NUMBER
     C                     MOVE CNUM      RRCNUM
      ** SET CURRENCY CODE
     C                     MOVE CCY       RRCCY
      ** SET ACCOUNT CODE
     C                     MOVE ACOD      RRACOD
      ** SET ACCOUNT SEQUENCE
     C                     MOVE ACSQ      RRACSQ
      ** SET RETAIL NUMBER
     C           ACNO      IFEQ *ZEROS                     - B1
     C                     MOVE *BLANKS   RRACNO
     C                     ELSE                            - X1
     C                     MOVE ACNO      RRACNO
     C                     END                             - E1
      ** CONVERT POSTING DATE
     C                     Z-ADDPSTD      ZDAYNO
     C                     EXSR ZDATE2
      ** SET THE POSTING DATE ON REPORT
     C                     MOVE ZADATE    RRPSTD
      ** CONVERT VALUE DATE
     C                     Z-ADDVALD      ZDAYNO
     C                     EXSR ZDATE2
      ** SET THE POSTING DATE ON REPORT
     C                     MOVE ZADATE    RRVALD
      ** SET POSTING NARRATIVE
     C                     MOVE PNAR      RRPNAR
      ** SET POSTING AMOUNT USING ZSEDIT
     C                     Z-ADDPSTA      ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     EXSR ZSEDIT
      *
     C                     MOVE ZOUT21    RRPSTA
      ** SET DEBIT/CREDIT INDICATOR
     C           DRCR      IFEQ *ZEROS                     - B1
     C                     MOVE 'DR'      RRDRCR
     C                     ELSE                            - X1
     C                     MOVE 'CR'      RRDRCR
     C                     END                             - E1
      *
     C                     WRITESE6290F2
      *
     C                     ENDSR                           **#W02  ESR**
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #W04     - WRITE SC GENERATED ENTRIES RUN CONTROL REPORT         *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     - MAIN PROCESSING                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           #W04      BEGSR                           **#W04  BSR**
      *
      ** WRITE REPORT HEADING
      *
     C                     WRITEHEADAU
      *
      **CHECK IF DIFFERENCE BETWEEN RECORD ON FILE AND RECORD IN FILE
      *
     C           RRCNT     IFNE NORE1                      - B1
      *
      ** SETON U8 AND *IN82
      *
     C                     MOVE '1'       *IN82
     C                     MOVE '1'       *INU8
      *
     C                     END                             - E1
      *
      ** WRITE RUN CONTROL
      *
     C                     Z-ADDNORE1     RRNORE
     C                     WRITECONTROL
      *
      ** WRITE END OF REPORT
      *
     C***********          WRITEENDAU                                     S01464
      *
     C                     ENDSR                           **#W04  ESR**
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * *PSSR    - DATA BASE ERROR PROCESSING                            *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     - MAIN PROCESSING                                       *
      * MAIN     - MAIN PROCESSING                                       *
      * #INIT    - INITIAL PROCESSING                                    *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           *PSSR     BEGSR                           ***PSRR BSR**
      *
      ** ROLL BACK CHANGES , DUMP AND RETURN TO THE CALLING PROGRAM
      *
     C                     SETON                     LRU7U8
     C                     WRITEHEADAU
     C                     WRITEERRORAU
     C***********          WRITEENDAU                                     S01464
     C*                                                                   S01464
     C** ENSURE TOTAL SPOOL FILE RECORDED BY RCF                          S01464
     C*                                                                   S01464
     C                     Z-ADDSFNUMU    SFNUM2  60                      S01464
     C*                                                                   S01464
     C                     CALL 'ZSFILE'                                  S01464
     C                     PARM           @RSEQ   5                       S01464
     C                     PARM *BLANK    SENTY   3                       S01464
     C                     PARM           SFILEU                          S01464
     C                     PARM           SFNUM2                          S01464
     C                     PARM           ZSERR   1                       S01464
     C*                                                                   S01464
     C           ZSERR     IFEQ 'Y'                                       S01464
     C                     MOVE '1'       *INU7                           S01464
     C                     MOVE '1'       *INU8                           S01464
     C                     MOVE '1'       *INLR                           S01464
     C                     RETRN                                          S01464
     C                     END                                            S01464
     C                     DUMP
     C                     RETRN
      *
     C                     ENDSR                            ***PSRR ESR**
      ********************************************************************
      /EJECT
      /COPY ZSRSRC,ZDATE2Z2
      /EJECT
      /COPY ZSRSRC,ZSEDITZ3
      /EJECT
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
