     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE ER reference maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE6500 - SE Early Redemptions Reference Maintenance          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CSE071             Date 19Jul05               *
      *                 BUG2701            Date 17May04               *
      *                 CGL029             Date 01Sep03               *
      *                 CSC023             Date 28Jan04               *
      *                 208241             Date 21Aug02               *
      *                 CSE040             Date 13May03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CPK014             Date 14Nov01               *
      *                 CSE016             Date 10Jan01               *
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CSE010             Date 21Jun99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 082496             Date 01Feb95               *
      *                 S01496             Date 07Oct94               *
      *                 S10978             DATE 25FEB93               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  BUG2701 - Make sure correct command is passed on to CMDTXT   *
      *            as part of CSC023 changes.                         *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSC023 - SBMJOB output queue must be *JOBD.                  *
      *  208241 - Recompiled over changed /CPY ZSDESCZ3.              *
      *  CSE040 - Security Statement Production - recompiled          *
      *  CPK014 - Submit jobs with user *JOBD                         *
      *  CSE016 - Extended Security Descriptions                      *
      *  CSE010 - SE Transactions Enhancements                        *
      *  082496 - SE6500 should call FC0040X when processing action P *
      *  S01496 - Incorporation of Jyske Enhancements (S10978)        *
      *  S10978 - JYSZRH: EARLY REDEMPTIONS                           *
      *                                                               *
      *****************************************************************
      /EJECT
     F*
     F*****BJ*-*Bank*Details*-*Rtv*Idx*********************************   S01496
     F***SDBANKL1IF**E           K        DISK                            S01496
     F***********                                   KINFSR *PSSR          S01496
     F*
     F*****BV*-*Securities*Trading*Data*-*Rtv*Idx**********************   S01496
     F***SDSTRDL1IF**E           K        DISK                            S01496
     F***********                                   KINFSR *PSSR          S01496
     F*
     F**   VA - SE Early Redemptions References - Upd Idx
     FSEERRFL0UF  E           K        DISK                      A
     F                                              KINFSR *PSSR
     F                                              KCOMIT
     F*
     F**   VA - SE Early Redemptions References - Rtv Idx
     FSEERRFL1IF  E           K        DISK
     F            SEERRFD0                          KRENAMESEERRFD1
     F                                              KINFSR *PSSR
     F*
     F**   VA - SE Early Redemptions References - Rtv Idx by SESN+EREX+ERRF
     FSEERRFL3IF  E           K        DISK
     F            SEERRFD0                          KRENAMESEERRFD3
     F                                              KINFSR *PSSR
     F*
     F**   VA - SE Early Redemptions References - Rtv Idx by EREX+SESN+ERRF
     FSEERRFL4IF  E           K        DISK
     F            SEERRFD0                          KRENAMESEERRFD4
     F                                              KINFSR *PSSR
     F*
     F**   Securities Details
     FSECTY   IF  E           K        DISK
     F                                              KINFSR *PSSR
     F*
     F**   Investment Types Details
     FINVTP   IF  E           K        DISK
     F                                              KINFSR *PSSR
     F*
     F**   VB - SE Early Redemptions Depots - Rtv Idx
     FSEERDPL1IF  E           K        DISK
     F                                              KINFSR *PSSR
     F*
     F**   SE Security Diary Events
     FSEDEV   IF  E           K        DISK
     F                                              KINFSR *PSSR
     F*
     F*****Table*File*Holidays*****************************************   S01496
     F***FDTABJLLIF**E           K        DISK         KINFSR *PSSR   UC  S01496
     F*
     F**   VC - SE Early Redemptions Allocation - Rtv Idx
     FSEERALL1IF  E           K        DISK
     F            SEERALD0                          KRENAMESEERALDU
     F                                              KINFSR *PSSR
     F*
     F**   VC - SE Early Redemptions Allocation - Upd Idx
     FSEERALL0UF  E           K        DISK                      A
     F                                              KINFSR *PSSR
     F                                              KCOMIT
     F*
     F**   Display file
     FSE6500DFCF  E                    WORKSTN      KINFDS SCRDS
     F                                              KINFSR *PSSR
     F                                        #1RRN KSFILE #SFLNUM
     ******************************************************************
      /EJECT
     *********************************************************************
      *
      * ID F  C  H  L    Function of indicators
      * ---------------------------------------
      *
      *   KC : CMD  3 - End of program
      *   KE : CMD  5 - Reset Screen
      *   KI : CMD  9 - Go to 'Add' or 'Change' Mode
      *   KL : CMD 12 - Previous screen
      *
      *   LR :    Last record
      *   U7/U8 : Database error
      *
      *   27 : Rollup subfile
      *   30 : End of Subfile
      *
      *   32 : Error indicator (Subfile Field)
      *   33 : Error indicator
      *   34 : Error indicator
      *   35 : Error indicator
      *   36 : Error indicator
      *   37 : Error indicator
      *   38 : Error indicator
      *   39 : Error indicator
      *
      *   40 : Confirm REVERSAL
      *   41 : Confirm ALLOCATION
      *   42 : Confirm AUTHORISATION
      *
      *   43 : 'AMEND' Mode if *ON
      *        'ADD  ' Mode if *OFF
      *   44 : Error indicator
      *   45 : "First" Time Processing
      *   46 : Command Key pressed
      *   47 : Confirmation Screen or 'Full Details' Screen
      *   48 : Protect on 'Redemption Price' and 'Redemption Narrative'
      *   49 : Protect on 'Payment Date'
      *
      *   50 : Error indicator (Position Field)
      *   51 : Error indicator
      *   52 : Error indicator
      *
      *   53 : Access by Security Shortname and Ex-date
      *   54 : Access by Ex-date and Security Shortname
      *   55 : Access by Reference Number
      *
      *   56 : Protect 'Position/Selection' Fields
      *   57 : Protect 'Reference Num.', Security Shortname' & 'Correction Ref'
      *   58 : Protect and non-display 'Action', 'Last Final Allocation'
      *   59 : Protect 'Ex-date' field
      *
      *   60 : Error indicator (Selection Field)
      *   61 : Enquiry Mode
      *   62 : Error indicator (Action)
      *   63 : Protect 'Ex-date' & 'Final Allocation' fields
      *   64 : Report Mode
      *   65 : Reverse Image on 'Payment Date'
      *   66 : Reverse Image on 'Redemption Price'
      *   67 : Reverse Image on 'Redemption Narrative'
      *   68 : Protect 'Final Allocation' field
      *
      *   80 : Display Subfile control
      *   81 : Display Subfile
      *   84 : Subfile Next Change
      *
      *   87 : Read Change indicator
      *   88 : Working indicator
      *   89 : Working indicator
      *
      *   90-99 : Used by Standard Subroutines
      *   98 : Indicator used by subroutine ZDATE1
      *   99 : Indicator used by subroutine ZDATE1
      **************************************************************************
      /EJECT
      **************************************************************************
      *
      * Name of the Subroutines
      * -----------------------
      *
      *       P001  - Initial processing
      *       P002  - Main processing
      *       P003  - Termination processing
      *
      *       P004  - Clear Subfile
      *       P005  - Build the subfile for Add Mode
      *       P006  - Build the subfile for Amend/Enquiry Mode
      *       P007  - Validate Position/Selection fields
      *       P008  - Validate subfile records
      *       P008A - Validate 'Action' field
      *       P009  - Process 'Action'
      *       P010  - Process 'Confirmation' screen
      *
      *       P012  - Check if ER completly allocated.
      *       P013  - Validate 'Correction Reference'.
      *       P014  - Initialise error indicators
      *       P015  - Clear Subfile Fields
      *       P016  - Load Subfile Fields
      *       P017  - Reinitialise Position/selection fields
      *       P018  - Process 'Full details' screen
      *       P019  - Validate 'Full details' screen and update file
      *       P020  - Set defaults of full detail fields
      *
      *       R001  - Position into the File
      *       R002  - Read next record in File
      *
      *       *PSSR - "Standard" Database error processing
      *
      * Name of the Standard Subroutines
      * --------------------------------
      *
      *       ZNCD   - Determine Next Coupon Date
      *       ZDATE1 - Dates Validation and Conversion
      *       ZDATE2 - Dates Validation and Conversion
      *       ZASNMS - Send Message to Program's message queue
      ********ZA0830*-*Finds*out*whether*a*particular*day*is*a*holiday*in*S01496
      *****************a*particular*currency******************************S01496
      *       ZEDIT  - To Insert a decimal point into a numeric field and
      *                to blank out leadings zeroes
      *       ZALIGN - To validate and right-align numeric fields
      *
     *********************************************************************
      /EJECT
     *********************************************************************
     E*
     E**   ARRAYS USED BY ZDATE1
     E*
     E                    ZYDY    4   4  4 0             ZDATE1/2 SR.ARRAY
     E                    ZMDY   13  13  3 0             ZDATE1/2 SR.ARRAY
     E                    ZMNM   12  12  3               ZDATE2 SR. ARRAY
     E*
     E**   FOR COMPILATION - COPYRIGHT INSERTION
     E*
     E                    CPY@    1   1 80
     E*
     E**   STORE THE EIGHT GROUP EACH CUSTOMER IS ASSOCIATED
     E*
     E                    CGRP        9  1               CUSTOMER GROUP
     E                    AGRP        9  1               ACCOUNT COMMISS.
     E*
     E                    CD         12  4 0             CD01-12 ARRAY
     E                    CR         12  1               CR01-12 ARRAY
     E*
     E**********          CMD     1   2 80               SBMJOB                               CSC023
     E                    CMD     1   3 80               SBMJOB                               CSC023
     E*
     E                    TXT     1   4 78               COMMAND KEYS
     E*
     E                    ZA1        16  1               ZALIGN
     E                    ZA2        16  1               ZALIGN
     E*
     E****USED*BY*SR.*ZA0830*-*CURRENCY*RECORDS************************   S01496
     E*
     E***********         @CY        50  3                                S01496
     E*
     E                    NARR    1   4 27               NARRATIVE
     E*                                                                   S01496
     E/COPY ZSRSRC,ZHOLE                                                  S01496
     E/COPY ZSRSRC,ZSDESCZ1                                               CSE016
     E*                                                                   S01496
     *********************************************************************
      /EJECT
     *********************************************************************
     I*
     I**   PROGRAM DATA STRUCTURE
     I*
     IPGMDS     ESDSY2PGDSP
     I*
     I**   FILE INFORMATION DATA STRUCTURE
     I*
     IINFDS1    E DSY2I1DSP
     I*
     I**   FILE RECORD DATA STRUCTURE
     I*
     IFIELD     E DSSEERRFPD
     I*
     ISCRDS       DS
     I                                      197 205 DSPNUM
     I                                    B 378 3790CPFPOS
     I*
     I**   DATA STRUCTURE FOR COMPILATION - COPYRIGHT INSERTION
     I*
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I*
     I**   DATA STRUCTURE FOR DATABASE ERROR
     I*
     ILDA         DS                            256
     I                                      132 183 WWLOT
     I                                      132 141 WWFILE
     I                                      142 170 WWKEY
     I                                      171 180 WWPGM
     I                                      181 1830WWASE
     I*
     I              '0123456789'          C         DIGITS
     I*
     ICDDS        DS
     I                                        1   40CD01
     I                                        5   80CD02
     I                                        9  120CD03
     I                                       13  160CD04
     I                                       17  200CD05
     I                                       21  240CD06
     I                                       25  280CD07
     I                                       29  320CD08
     I                                       33  360CD09
     I                                       37  400CD10
     I                                       41  440CD11
     I                                       45  480CD12
     I                                        1  48 CD
     ICRDS        DS
     I                                        1   1 CR01
     I                                        2   2 CR02
     I                                        3   3 CR03
     I                                        4   4 CR04
     I                                        5   5 CR05
     I                                        6   6 CR06
     I                                        7   7 CR07
     I                                        8   8 CR08
     I                                        9   9 CR09
     I                                       10  10 CR10
     I                                       11  11 CR11
     I                                       12  12 CR12
     I                                        1  12 CR
     ILOCKDS      DS
     I                                        1  10 WUSER
     I                                       11  20 WJOBN
     I*
     I****USED*BY*SR.*ZA0830*-*ARRAY*DATA*STRUCTURE********************   S01496
     I*
     I*********** DS                                                      S01496
     I***********                             1 150 @CY                   S01496
     I***********                             1  75 @@CY1                 S01496
     I***********                            76 150 @@CY2                 S01496
     I*
     I**  DATA STRUCTURE FOR COMMAND STRING FOR QCMDEXC
     I*
     I*CMDTXT***  DS                            160                                           CSC023
     ICMDTXT      DS                            240                                           CSC023
     I                                       12  20 @CJOB
     I                                       14  19 @REF
     I                                       20  20 @ACTI
     I                                       84  89 @REFN
     I                                       81 160 @CMDT2                                   BUG2701
     I                                      161 240 XPARM                                     CSC023
     I*                                                                   S01496
     IZMUSER      DS                             17                       S01496
     I** Data area ZMUSER                                                 S01496
     I                                       11  13 USRBCH                S01496
     I*                                                                   S01496
     IRUNDAT    E DS                                                      S01496
     I**  Standard Data Area Layout for System flags and Run Date         S01496
     I*                                                                   S01496
     ISDBANK    E DSSDBANKPD                                              S01496
     I**  Data structure for bank details                                 S01496
     I*                                                                   S01496
     ISDBRCH    E DSSDBRCHPD                                              S01496
     I**  Data structure for branch details                               S01496
     I*                                                                   S01496
     ISDSTRD    E DSSDSTRDPD                                              S01496
     I**  Data structure for securities trading details                   S01496
     I*                                                                   S01496
     IDSSDY     E DSDSSDY                                                 S01496
     I**  Long data structure for access objects                          S01496
     I*                                                                   S01496
     I/COPY ZSRSRC,ZHOLI                                                  S01496
     I/COPY ZSRSRC,ZSDESCZ2                                               CSE016
     I*                                                                   S01496
     *********************************************************************
      /EJECT
     C********************************************************************
     C**    P R O G R A M   S T A R T
     C********************************************************************
     C*
     C**   INITIAL PROCESSING
     C*
     C                     EXSR P001
     C*
     C**   MAIN PROCESSING
     C*
     C                     EXSR P002
     C*
     C**   TERMINATION PROCESSING
     C*
     C                     EXSR P003
     C*
     C********************************************************************
     C**    P R O G R A M     E N D
     C********************************************************************
      /EJECT
     ******************************************************************
     C* P001   : INITIAL PROCESSING                                   *
     ******************************************************************
     C*
     C           P001      BEGSR
     C*                                                                   S01496
     C**  Copyright statement                                             S01496
     C*                                                                   S01496
     C                     MOVEACPY@      ACT@   80                       S01496
     C*                                                                   S01496
     C**  Read in DTAARA/RUNDAT                                           S01496
     C*                                                                   S01496
     C           *NAMVAR   DEFN           RUNDAT                          S01496
     C                     IN   RUNDAT                                    S01496
     C*
     C**   KEY FIELDS
     C*
     C           KEY1      KLIST
     C                     KFLD           KSECN  10
     C                     KFLD           KEXDT   50
     C                     KFLD           KREFN   6
     C*
     C           KEY2      KLIST
     C                     KFLD           KEXDT
     C                     KFLD           KSECN
     C                     KFLD           KREFN
     C*
     C           KEY3      KLIST
     C                     KFLD           KNMCY
     C                     KFLD           KSITP
     C*
     C           KEY4      KLIST
     C                     KFLD           KSECN
     C                     KFLD           KEXDT
     C*
     C           KEY5      KLIST
     C                     KFLD           KSECN
     C                     KFLD           KEXDT
     C                     KFLD           KTYPE   2
     C*
     C**   ENTRY PARAMETERS
     C**   RECEIVE 'ACTION CODE' TO DETERMINE THE PROCESSING MODE
     C**   VALUE CAN BE 'E' FOR ENQUIRY, 'A' FOR UPDATE OR 'R' FOR REPORT
     C*
     C           *ENTRY    PLIST
     C                     PARM           ACTION  1
     C*
     C**   INITIALISE KEY AND WORKING FIELDS
     C*
     C                     MOVE *BLANKS   WWLOT
     C                     MOVEL'SE6500'  DBPGM  10
     C*
     C                     MOVE *BLANKS   #3REFN
     C                     MOVE *BLANKS   #3SECN
     C                     MOVE *BLANKS   #3EXDT
     C                     MOVE *BLANKS   #3STAT
     C*
     C                     Z-ADD0         WWRRN   40       SAVE RCD.NBR.
     C*
     C**   SETUP SCREEN TIME
     C*
     C                     TIME           ##TME   60       Screen time.
     C*
     C**   ACCESS TO BANK DETAILS TO RETRIEVE THE MIDAS RUN DATE
     C*
     C************LOVAL    SETLLSDBANKL1                                  S01496
     C***********          READ SDBANKL1                 89               S01496
     C*                                                                   S01496
     C                     CALL 'AOBANKR0'                                S01496
     C                     PARM *BLANKS   WLRTCD  7                       S01496
     C                     PARM '*FIRST'  WLOPTN  7                       S01496
     C           SDBANK    PARM SDBANK    DSSDY                           S01496
     C*
     C**   IF RECORD NOT FOUND
     C*
     C************IN89     IFEQ '1'                                       S01496
     C           WLRTCD    IFNE *BLANKS                                   S01496
     C                     MOVEL'001'     DBASE   30       *****************
     C***********          MOVEL'SDBANKL1'DBFILE 10        ** DB ERROR 01 S01496
     C                     MOVE 'SDBANKPD'DBFILE 10        * DB ERROR 01 *S01496
     C                     MOVEL*BLANKS   DBKEY  29        *****************
     C                     EXSR *PSSR
     C                     END
     C*
     C                     MOVE BJMRDT    ##MRDT           MIDAS RUN DATE
     C*
     C**   DETERMINE DATE FORMAT
     C*
     C           BJDFIN    IFEQ 'M'
     C                     MOVE '1'       *IN98
     C                     END
     C*
     C**   ACCESS TO SECURITIES TRADING DATA TO RETRIEVE THE BACK VALUE PERIOD
     C*
     C************LOVAL    SETLLSDSTRDL1                                  S01496
     C***********          READ SDSTRDL1                 89               S01496
     C*                                                                   S01496
     C                     CALL 'AOSTRDR0'                                S01496
     C                     PARM *BLANKS   WLRTCD                          S01496
     C                     PARM '*FIRST'  WLOPTN                          S01496
     C           SDSTRD    PARM SDSTRD    DSSDY                           S01496
     C*
     C**   IF RECORD IS NOT FOUND
     C*
     C************IN89     IFEQ '1'                                       S01496
     C           WLRTCD    IFNE *BLANKS                                   S01496
     C                     MOVEL'002'     DBASE            *****************
     C***********          MOVEL'SDSTRDL1'DBFILE           ** DB ERROR 02 S01496
     C                     MOVE 'SDSTRDPD'DBFILE           * DB ERROR 02 *S01496
     C                     MOVEL*BLANKS   DBKEY            *****************
     C                     EXSR *PSSR
     C                     END
     C*                                                                   S01496
     C**  Read in ZMUSER                                                  S01496
     C*                                                                   S01496
     C           *NAMVAR   DEFN           ZMUSER                          S01496
     C                     IN   ZMUSER                                    S01496
     C*                                                                   S01496
     C**  Access SDBRCHPD using the default user branch                   S01496
     C*                                                                   S01496
     C**********           CALL 'AOBRCHR0'                                             S01496 CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   WLRTCD                          S01496
     C                     PARM '*KEY   ' WLOPTN                          S01496
     C                     PARM USRBCH    WLBRCH  3                       S01496
     C           SDBRCH    PARM SDBRCH    DSSDY                           S01496
     C*                                                                   S01496
     C**  Check if error occurred                                         S01496
     C*                                                                   S01496
     C           WLRTCD    IFNE *BLANKS                                   S01496
     C                     Z-ADD7         DBASE            ***************S01496
     C                     MOVE 'SDBRCHPD'DBFILE           * DB ERROR 07 *S01496
     C                     MOVEL*BLANKS   DBKEY            ***************S01496
     C                     MOVELUSRBCH    DBKEY                           S01496
     C                     EXSR *PSSR                                     S01496
     C                     END                                            S01496
     C*
     C**   CALCULATE THE SE BACKVALUE DATE
     C*
     C           BJRDNB    SUB  BVBVP     WBACKD  50
     C*
     C**   IF THE 'ACTION' PARAMETER IS EQUAL TO 'E' OR 'R'
     C**   SETON INDICATOR FOR ENQUIRY MODE OR REPORT MODE
     C*
     C           ACTION    IFEQ 'E'                        B*1
     C           ACTION    OREQ 'R'
     C                     MOVE '1'       *IN61
     C                     MOVEATXT,1     ##CTX1
     C*
     C           ACTION    IFEQ 'R'                        B*2
     C                     MOVE '1'       *IN64
     C                     END                             E*2
     C                     END                             E*1
     C*
     C**   IF THE 'ACTION' PARAMETER IS EQUAL TO 'A'
     C**   SETON INDICATOR FOR 'UPDATE' MODE
     C*
     C           ACTION    IFEQ 'A'                        B*1
     C*
     C**   ACCESS THE ER REFERENCES FILE TO SEE IF THERE IS RECORD IN THE FILE
     C*
     C           *LOVAL    SETLLSEERRFL1
     C                     READ SEERRFL1                 89
     C*
     C**   IF RECORD IS NOT FOUND, SETON THE INDICATOR OF 'ADD MODE'
     C*
     C           *IN89     IFEQ '1'                        B*2
     C                     MOVE '0'       *IN43
     C                     MOVEATXT,2     ##CTX1
     C*
     C**   IF RECORD IS NOT FOUND, SETON THE INDICATOR OF 'AMEND MODE'
     C*
     C                     ELSE                            X*2
     C                     MOVE '1'       *IN43
     C                     MOVEATXT,3     ##CTX1
     C                     END                             E*2
     C                     END                             E*1
     C*
     C**   ONLY TO DECLARE THE FILE IN 'ADD' MODE
     C*
     C           *IN99     IFEQ '0'                        B*1
     C           *IN99     ANDEQ'1'
     C                     WRITESEERALD0
     C                     END                             E*1
     C*
     C**   SELECT THE PROGRAM MSGQ FOR ERROR MSG
     C*
     C                     MOVEL'*'       TOPQ   10
     C                     MOVEL'*PRV'    TOPRQ   5
     C                     MOVEL'SEUSRMSG'PMSGF  10
     C*
     C**   FILL IN FIELDS FOR SUBROUTINE ZASNMS
     C*
     C                     MOVEL'SE6500'  ZAPGM  10        Message queue
     C                     MOVEL'SEUSRMSG'ZAMSGF 10        Message file
     C                     MOVEL*BLANK    ZAPGRL  5        Rel queue
     C                     MOVEL*BLANK    ZAMSID  7        Message Id.
     C                     MOVEL*BLANK    ZAMSDA132        Message data.
     C                     MOVEL*BLANK    ZAMSTP  7        Message type.
     C*
     C**   CLEAR MESSAGES FROM PROGRAM MESSAGE QUEUE.
     C*
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
     C/COPY WNCPYSRC,SE6500C001
      *                                                                   CSE010
      ** Check if CSE010 (Transaction Enhancements) is active             CSE010
      *                                                                   CSE010
     C                     CALL 'AOSARDR0'                                CSE010
     C                     PARM '       ' @RTCD                           CSE010
     C                     PARM '*VERIFY' @OPTN   7                       CSE010
     C                     PARM 'CSE010'  @SARD   6                       CSE010
      *                                                                   CSE010
     C           @RTCD     IFEQ *BLANK                                    CSE010
     C                     MOVE 'Y'       CSE010  1                       CSE010
     C                     ELSE                                           CSE010
     C                     MOVE 'N'       CSE010                          CSE010
      *                                                                   CSE010
      ** Database Error (return code other than *NRF)                     CSE010
      *                                                                   CSE010
     C           @RTCD     IFNE '*NRF   '                                 CSE010
     C           *LOCK     IN   LDA                                       CSE010
     C                     MOVEL'SCSARDPD'DBFILE           ***************CSE010
     C                     MOVEL'CSE010'  DBKEY            * DB ERROR 08 *CSE010
     C                     Z-ADD8         DBASE            ***************CSE010
     C                     OUT  LDA                                       CSE010
     C                     MOVE *ON       *INU7                           CSE010
     C                     MOVE *ON       *INU8                           CSE010
     C                     MOVE *ON       *INLR                           CSE010
     C                     EXSR *PSSR                                     CSE010
     C                     ENDIF                                          CSE010
     C                     ENDIF                                          CSE010
     C*
     C                     WRITE#MSGCTL
     C*
     C                     ENDSR
      *****************************************************************
      /EJECT
     ******************************************************************
     C* P002   : MAIN PROCESSING                                      *
     ******************************************************************
     C*
     C           P002      BEGSR
     C*
     C**   WHILE CMD3 NOT PRESSED
     C*
     C           *INKC     DOWEQ'0'                        B*1
     C*
     C**   IF IT'S THE FIRST TIME PROCESSING OR F5 IS PRESSED
     C*
     C           *IN45     IFEQ '0'                        B*2
     C           *INKE     OREQ '1'
     C*
     C                     MOVE '0'       *IN53
     C                     MOVE '0'       *IN54
     C                     MOVE '0'       *IN55
     C*
     C**   IF IT'S NOT THE ENQUIRY MODE
     C*
     C           *IN61     IFEQ '0'                        B*3
     C*
     C**   IT IT'S THE 'AMEND' MODE, BUILD THE FIRST SUBFILE PAGE
     C*
     C           *IN43     IFEQ '1'                        B*4
     C                     MOVE '0'       *IN45
     C*
     C**   CLEAR SUBFILE
     C*
     C                     EXSR P004
     C*
     C**   PROTECT ON 'POSITION/SELECTION' FIELDS *OFF
     C**   PROTECT/NON-DISPLAY 'ACTION', 'LAST FINAL ALLOCATION' *OFF
     C**   PROTECT ON 'REFERENCE NUM.' 'SECURITY SHORT.' 'CORRECTION REFERENCE'
     C**              SUBFILE FIELDS *ON
     C*
     C                     MOVE '0'       *IN56
     C                     MOVE '0'       *IN57
     C                     MOVE '1'       *IN58
     C*
     C**   INITIALISE ERROR INDICATORS
     C*
     C                     EXSR P014
     C*
     C**   REINITIALISE POSITION/SELECTION FIELDS
     C*
     C                     EXSR P017
     C*
     C**   SET KEY FIELDS WITH BLANKS OR ZEROS
     C*
     C                     MOVE *BLANKS   KREFN
     C                     MOVE *BLANKS   KSECN
     C                     Z-ADD*ZEROS    KEXDT
     C*
     C**   BUILD THE FIRST SUBFILE PAGE
     C*
     C                     EXSR P006
     C                     ELSE                            X*4
     C*
     C**   IT IT'S THE 'ADD' MODE, BUILD A BLANK SUBFILE
     C*
     C                     EXSR P004
     C*
     C**   PROTECT ON 'POSITION/SELECTION' FIELDS *ON
     C**   PROTECT AND NON-DISPLAY 'ACTION', 'LAST FINAL ALLOCATION', 'STATUS'
     C**   PROTECT ON 'REFERENCE NUM.' AND 'SECURITY SHORT.' SUBFILE FIELDS
     C**              'CORRECTION REFERENCE' *OFF
     C*
     C                     MOVE '1'       *IN56
     C                     MOVE '1'       *IN57
     C                     MOVE '0'       *IN58
     C*
     C**   INITIALISE ERROR INDICATORS
     C*
     C                     EXSR P014
     C*
     C**   BUILD A BLANK SUBFILE
     C*
     C                     EXSR P005
     C                     Z-ADD1         #1RRN
     C                     END                             E*4
     C*
     C**   IF IT'S THE ENQUIRY MODE
     C*
     C                     ELSE                            X*3
     C                     MOVE '0'       *IN45
     C*
     C**   CLEAR SUBFILE
     C*
     C                     EXSR P004
     C*
     C**   REINITIALISE POSITION/SELECTION FIELDS
     C*
     C                     EXSR P017
     C*
     C**   INITIALISE ERROR INDICATORS
     C*
     C                     EXSR P014
     C*
     C**   SET KEY FIELDS WITH BLANKS OR ZEROS
     C*
     C                     MOVE *BLANKS   KREFN
     C                     MOVE *BLANKS   KSECN
     C                     Z-ADD*ZEROS    KEXDT
     C*
     C**   BUILD THE FIRST SUBFILE PAGE
     C*
     C                     EXSR P006
     C                     END                             E*3
     C*
     C                     MOVE '1'       *IN45
     C                     END                             E*2
     C*
     C**   IF CMD9 IS PRESSED
     C*
     C           *IN09     IFEQ '1'                        B*2
     C*                                                                   S01496
     C**  Check if user is authorised to action 'Insert'                  S01496
     C*                                                                   S01496
     C           AGMBIN    IFNE 'Y'                                       S01496
     C*                                                                   S01496
     C**  If single branch environment                                    S01496
     C*                                                                   S01496
     C                     CALL 'ZVACTU'                                  S01496
     C                     PARM 'I'       WLACTN  1                       S01496
     C                     PARM *ZERO     WLERR   10                      S01496
     C*                                                                   S01496
     C           WLERR     IFNE *ZERO                                     S01496
     C                     MOVEL'ER66006' ZAMSID                          S01496
     C                     EXSR ZASNMS                                    S01496
     C                     END                                            S01496
     C*                                                                   S01496
     C                     ELSE                                           S01496
     C*                                                                   S01496
     C**  If multibranching environment                                   S01496
     C*                                                                   S01496
     C                     CALL 'ZVACTBU'                                 S01496
     C                     PARM 'I'       WLACTN  1                       S01496
     C                     PARM USRBCH    WLBRCH  3                       S01496
     C                     PARM *ZERO     WLERR   10                      S01496
     C*                                                                   S01496
     C           WLERR     IFEQ 1                                         S01496
     C                     MOVEL'ER66007' ZAMSID                          S01496
     C                     EXSR ZASNMS                                    S01496
     C                     ELSE                                           S01496
     C           WLERR     IFEQ 2                                         S01496
     C                     MOVEL'ER66008' ZAMSID                          S01496
     C                     EXSR ZASNMS                                    S01496
     C                     END                                            S01496
     C                     END                                            S01496
     C*                                                                   S01496
     C                     END                                            S01496
     C*                                                                   S01496
     C           WLERR     IFEQ *ZERO                                     S01496
     C*
     C**   IT IT'S THE 'AMEND' MODE, BUILD A BLANK SUBFILE
     C*
     C           *IN43     IFEQ '1'                        B*3
     C*
     C**   INITIALISE ERROR INDICATORS
     C*
     C                     EXSR P014
     C*
     C**   CLEAR SUBFILE
     C*
     C                     EXSR P004
     C                     MOVEATXT,2     ##CTX1
     C*
     C**   PROTECT ON 'POSITION/SELECTION' FIELDS *ON
     C**   PROTECT AND NON-DISPLAY 'ACTION', 'LAST FINAL ALLOCATION', 'STATUS'
     C**   PROTECT ON 'REFERENCE NUM.' AND 'SECURITY SHORT.' SUBFILE FIELDS
     C**              'CORRECTION REFERENCE' *OFF
     C*
     C                     MOVE '1'       *IN56
     C                     MOVE '1'       *IN57
     C                     MOVE '0'       *IN58
     C*
     C                     MOVE '0'       *IN30
     C                     MOVE '0'       *IN59
     C                     MOVE '0'       *IN63
     C                     MOVE '0'       *IN68
     C*
     C**   BUILD A BLANK SUBFILE
     C*
     C                     EXSR P005
     C                     Z-ADD1         #1RRN
     C                     MOVE '0'       *IN43
     C                     ELSE                            X*3
     C*
     C                     MOVE '0'       *IN53
     C                     MOVE '0'       *IN54
     C                     MOVE '0'       *IN55
     C*
     C**   CLEAR SUBFILE
     C*
     C                     EXSR P004
     C*
     C**   INITIALISE ERROR INDICATORS
     C*
     C                     EXSR P014
     C*
     C**   IT IT'S THE 'ADD' MODE, BUILD A FIRST SUBFILE PAGE
     C**   (BEGINNING AT FIRST INSERTED RECORD)
     C*
     C                     MOVEATXT,3     ##CTX1
     C*
     C**   PROTECT ON 'POSITION/SELECTION' FIELDS *OFF
     C**   PROTECT/NON-DISPLAY 'ACTION', 'LAST FINAL ALLOCATION' *OFF
     C**   PROTECT ON 'REFERENCE NUM.' 'SECURITY SHORT.' 'CORRECTION REFERENCE'
     C**              SUBFILE FIELDS *ON
     C*
     C                     MOVE '0'       *IN56
     C                     MOVE '0'       *IN57
     C                     MOVE '1'       *IN58
     C*
     C                     MOVE '0'       *IN45
     C*
     C**   BUILD THE FIRST SUBFILE PAGE
     C*
     C                     EXSR P006
     C                     MOVE '1'       *IN45
     C                     MOVE '1'       *IN43
     C                     END                             E*3
     C*                                                                   S01496
     C                     END                                            S01496
     C*                                                                   S01496
     C                     END                             E*2
     C*
     C**   IF ROLLUP IS PRESSED
     C*
     C           *IN27     IFEQ '1'                        B*2
     C                     Z-ADDWWRRN     #1RRN
     C*
     C**   REINITIALISE ERROR INDICATORS
     C*
     C                     EXSR P014
     C*
     C**   CLEAR MESSAGES FROM PROGRAM MESSAGE QUEUE.
     C*
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
     C*
     C**   IF IT'S THE 'ADD' MODE ONLY
     C*
     C           *IN43     IFEQ '0'                        B*3
     C           *IN61     ANDEQ'0'
     C*
     C**   BUILD A BLANK SUBFILE PAGE
     C*
     C                     EXSR P005
     C*
     C**   IF IT'S THE 'AMEND' MODE OR THE 'ENQUIRY' MODE
     C*
     C                     ELSE                            X*3
     C*
     C**   SET KEY FIELDS WITH BLANKS OR ZEROS
     C*
     C                     MOVE *BLANKS   KREFN
     C                     MOVE *BLANKS   KSECN
     C                     Z-ADD*ZEROS    KEXDT
     C*
     C**   BUILD THE NEXT SUBFILE PAGE
     C*
     C                     EXSR P006
     C                     END                             E*3
     C                     END                             E*2
     C*
     C                     MOVE '0'       *IN50            Error on Reference
     C                     MOVE '0'       *IN51            Error on Security
     C                     MOVE '0'       *IN52            Error on Ex-date
     C                     MOVE '0'       *IN60            Error Status Select.
     C*
     C**   IF ONE OF THE POSITION/SELECTION FIELDS IS CHANGED
     C*
     C           #REFN     IFNE #3REFN                     B*2
     C           #SECN     ORNE #3SECN
     C           #EXDT     ORNE #3EXDT
     C           #STAT     ORNE #3STAT
     C*
     C**   VALIDATE POSITION/SELECTION FIELDS
     C*
     C                     EXSR P007
     C*
     C**   IF NO ERROR IS FOUND, BUILD THE FIRST SUBFILE PAGE
     C*
     C           *IN50     IFEQ '0'                        B*3
     C           *IN51     ANDEQ'0'
     C           *IN52     ANDEQ'0'
     C           *IN60     ANDEQ'0'
     C*
     C**   CLEAR SUBFILE
     C*
     C                     EXSR P004
     C*
     C**   SET KEY FIELDS WITH BLANKS OR ZEROS
     C*
     C                     MOVE *BLANKS   KREFN
     C                     MOVE *BLANKS   KSECN
     C                     Z-ADD*ZEROS    KEXDT
     C*
     C**   BUILD THE FIRST SUBFILE PAGE
     C*
     C                     EXSR P006
     C*
     C**   REINITIALISE POSITION/SELECTION FIELDS (IDEM FOR HIDDEN FIELDS)
     C*
     C                     MOVE *BLANKS   #REFN
     C                     MOVE *BLANKS   #SECN
     C                     MOVE *BLANKS   #EXDT
     C                     MOVE *BLANKS   #STAT
     C                     MOVE *BLANKS   #3REFN
     C                     MOVE *BLANKS   #3SECN
     C                     MOVE *BLANKS   #3EXDT
     C                     MOVE *BLANKS   #3STAT
     C                     END                             E*3
     C                     END                             E*2
     C*
     C**   SETUP SCREEN TIME
     C*
     C                     TIME           ##TME            Screen time.
     C*
     C**   EXECUTE THE SUBFILE CONTROL RECORD
     C*
     C                     WRITE#MSGCTL
     C                     WRITE#CMDTXT1
     C                     EXFMT#CTLNUM
     C*
     C**   CLEAR MESSAGES FROM PROGRAM MESSAGE QUEUE.
     C*
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
     C*
     C**   IF NO FUNCTION IS PRESSED AND ALL POSITION/SELECTION FIELDS UNCHANGED
     C**      VALIDATE ALL SUBFILE RECORDS
     C*
     C           *IN46     IFEQ '0'                        B*2
     C           #REFN     ANDEQ#3REFN
     C           #SECN     ANDEQ#3SECN
     C           #EXDT     ANDEQ#3EXDT
     C           #STAT     ANDEQ#3STAT
     C           WWCPT     ANDNE*ZEROS
     C*
     C**   REINITIALIZE ERROR INDICATORS
     C*
     C                     EXSR P014
     C*
     C                     EXSR P008
     C                     END                             E*2
     C*
     C                     END                             E*1
     C*
     C                     ENDSR
     C*****************************************************************
      /EJECT
     ******************************************************************
     C* P003   : TERMINATION PROCESSING                               *
     ******************************************************************
     C*
     C           P003      BEGSR
     C*
     C**   END OF PROGRAM
     C*
     C                     MOVE '1'       *INLR
     C                     RETRN
     C*
     C                     ENDSR
     ******************************************************************
      /EJECT
     C*****************************************************************
     C* P004  - TO CLEAR THE SUBFILE                                  *
     C*****************************************************************
     C           P004      BEGSR
     C*
     C**   CLEAR SUBFILE
     C*
     C                     MOVE '1'       *IN80
     C                     WRITE#CTLNUM
     C                     MOVE '0'       *IN80
     C                     MOVE '0'       *IN81
     C                     Z-ADD0         #1RRN
     C*
     C                     ENDSR
     ******************************************************************
      /EJECT
     ******************************************************************
     C* P005   : BUILD THE SUBFILE FOR ADD MODE                       *
     ******************************************************************
     C*
     C           P005      BEGSR
     C*
     C**   REINITIALISE POSITION/SELECTION FIELDS (HIDDEN FIELDS ALSO)
     C*
     C                     EXSR P017
     C*
     C**   CLEAR MESSAGES FROM PROGRAM MESSAGE QUEUE.
     C*
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
     C*
     C**   LOAD A BLANK SUBFILE PAGE
     C*
     C                     MOVE '0'       *IN84            No SFLNXTCHG
     C*
     C**   INIT SCREEN LINE NUMBER
     C*
     C                     Z-ADD0         WWCPT   20
     C*
     C**   LOAD SUBFILE
     C*
     C********** WWCPT     DOWLT15                                        CSE016
     C           WWCPT     DOWLT7                                         CSE016
     C*
     C                     ADD  1         #1RRN      81    SFLRCDNBR
     C                     ADD  1         WWCPT            SCREEN LINE NBR
     C*
     C**   CLEAR SFL FIELDS
     C*
     C                     EXSR P015
     C*
     C**   ADD RECORD IN SUBFILE
     C*
     C                     WRITE#SFLNUM
     C                     END                             E*1
     C*
     C**   SAVE POSITION
     C*
     C                     Z-ADD#1RRN     WWRRN            SAVE SFL.RCD.NBR.
     C*
     C                     ENDSR
     ******************************************************************
      /EJECT
     ******************************************************************
     C* P006   : BUILD THE SUBFILE FOR AMEND/ENQUIRY MODE             *
     ******************************************************************
     C*
     C           P006      BEGSR
     C*
     C**   LOAD A BLANK SUBFILE PAGE
     C*
     C                     MOVE '0'       *IN84            No SFLNXTCHG
     C*
     C**   IF ROLLUP (AMEND/ENQUIRY), SET KEY BY LAST RECORD OF PREVIOUS SCREEN
     C*
     C           *IN27     IFEQ '1'                        B*1
     C           *IN43     ANDEQ'1'
     C           *IN27     OREQ '1'
     C           *IN61     ANDEQ'1'
     C*
     C           #1RRN     CHAIN#SFLNUM              88
     C                     MOVE #1REFN    KREFN
     C                     MOVE #1SECN    KSECN
     c*
     C                     MOVE #1EXDT    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    KEXDT
     C*
     C                     ELSE                            X*1
     C*
     C**   SET KEY FIELDS BY NEW 'SELECT/POSITION TO' FIELD(S)
     C*
     C           #REFN     IFNE *BLANKS                    B*2
     C           #SECN     ORNE *BLANKS
     C           #EXDT     ORNE *BLANKS
     C                     MOVE #REFN     KREFN
     C                     MOVE #SECN     KSECN
     C                     MOVE #EXDT     ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    KEXDT
     C                     END                             E*2
     C                     END                             E*1
     C*
     C**   POSITION INTO ER REFERENCE FILE
     C*
     C                     EXSR R001
     C*
     C**   READ NEXT RECORD OF THE ER REFERENCE FILE
     C*
     C                     EXSR R002
     C*
     C**   INIT SCREEN LINE NUMBER
     C*
     C                     Z-ADD0         WWCPT   20
     C*
     C**   LOAD SUBFILE
     C*
     C           *IN30     DOWEQ'0'                        B*1
     C********** WWCPT     ANDLT15                                        CSE016
     C           WWCPT     ANDLT7                                         CSE016
     C*
     C**   IF ONE OF THE 'SELECTION' FIELD IS NOT BLANK
     C**    CHECK IF THE RECORD MUST BE TAKEN OR NOT IN THE SUBFILE
     C*
     C           #STAT     IFNE *BLANKS                    B*2
     C*
     C           #STAT     IFNE VAERST                     B*3
     C                     GOTO TAG1
     C                     END                             E*3
     C*
     C                     END                             E*2
     C*
     C**   IF AMEND MODE AND CMD 9 NOT PRESSED
     C**   OR IF ADD MODE AND CMD 9 PRESSED
     C*
     C           *IN43     IFEQ '1'                        B*2
     C           *IN09     ANDEQ'0'
     C           *IN43     OREQ '0'
     C           *IN09     ANDEQ'1'
     C*
     C**   IF 'LOCKED BY USER IDENTIFIER' IS NOT BLANK
     C**     PROTECT ALL SUBFILE RECORD FIELDS
     C*
     C           VAUSER    IFNE *BLANKS                    B*3
     C                     MOVE '1'       *IN63
     C                     ELSE                            X*3
     C                     MOVE '0'       *IN63
     C                     END                             E*3
     C*
     C**   IF 'EARLY REDEMPTION STATUS' IS NOT 'INCOMPLETE'
     C**     PROTECT 'EX-DATE' SUBFILE FIELD
     C*
     C           VAERST    IFNE 'I'                        B*3
     C           VACORF    ORNE *BLANKS
     C                     MOVE '1'       *IN59
     C                     ELSE                            X*3
     C                     MOVE '0'       *IN59
     C                     END                             E*3
     C*
     C**   IF 'FINAL ALLOCATION' FIELD IS EQUAL TO 'Y'
     C**   OR 'EARLY REDEMPTION STATUS' IS EQUAL TO 'R', PROTECT THIS FIELD
     C*
     C           VAFAID    IFEQ 'Y'                        B*3
     C           VAERST    OREQ 'R'
     C                     MOVE '1'       *IN68
     C                     ELSE                            X*3
     C                     MOVE '0'       *IN68
     C                     END                             E*3
     C                     END                             E*2
     C*
     C                     ADD  1         #1RRN      81    SFLRCDNBR
     C                     ADD  1         WWCPT            SCREEN LINE NBR
     C*
     C**   CLEAR SFL FIELDS
     C*
     C                     EXSR P015
     C*
     C**   LOAD SFL FIELDS
     C*
     C                     EXSR P016
     C*
     C**   ADD RECORD IN SUBFILE
     C*
     C                     WRITE#SFLNUM
     C*
     C           TAG1      TAG
     C*
     C**   READ NEXT RECORD OF THE ER REFERENCE FILE
     C*
     C                     EXSR R002
     C*
     C**   END OF DO WHILE
     C*
     C                     END                             E*1
     C*
     C**   SAVE POSITION
     C*
     C                     Z-ADD#1RRN     WWRRN            SAVE SFL.RCD.NBR.
     C*
     C**   IF IT'S NOT A ROLLUP
     C*
     C           *IN27     IFEQ '0'                        B*1
     C                     Z-ADD1         #1RRN
     C                     END                             E*1
     C*
     C**   IF NO RECORDS FOUND, DISPLAY ERROR MESSAGE
     C**   SEND MESSAGE 'NO DATA TO DISPLAY'
     C*
     C           WWCPT     IFEQ *ZEROS                     B*1
     C                     MOVEL'ER65903' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     END                             E*1
     C*
     C                     ENDSR
     ******************************************************************
      /EJECT
     ******************************************************************
     C* P007   : VALIDATE POSITION/SELECTION FIELDS                   *
     ******************************************************************
     C*
     C           P007      BEGSR
     C*
     C                     MOVE '0'       *IN53            Access by Sec & Ex-D.
     C                     MOVE '0'       *IN54            Access by Ex-D. & Sec
     C                     MOVE '0'       *IN55            Access by Reference
     C                     MOVE '0'       *IN89            Valid Numeric Field
     C*
     C**   IF REFERENCE NUMBER IS DIFFERENT THEN BLANKS
     C*
     C           #REFN     IFNE *BLANKS                    B*1
     C*
     C**   CHECK IT'S A VALID NUMERIC FIELD
     C*
     C           DIGITS    CHECK#REFN:1   N       10     89
     C*
     C**   IF IT'S NOT A VALID NUMERIC FIELD
     C*
     C           *IN89     IFEQ '1'                        B*2
     C                     MOVE '1'       *IN50            Reverse image
     C                     MOVEL'ER65001' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     END                             E*2
     C*
     C**   IF SECURITY OR EX-DATE IS/ARE DIFFERENT THAN BLANKS
     C*
     C           #SECN     IFNE *BLANKS                    B*2
     C           #EXDT     ORNE *BLANKS
     C                     MOVE '1'       *IN50            Reverse image
     C                     MOVEL'ER65002' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     END                             E*2
     C                     END                             E*1
     C*
     C**   IF EX-DATE IS DIFFERENT THAN BLANKS
     C*
     C           #EXDT     IFNE *BLANKS                    B*1
     C*
     C**   VERIFY IF IT'S A VALID DATE FORMAT
     C*
     C                     MOVE #EXDT     ZDATE
     C                     EXSR ZDATE1
     C*
     C**   IT'S NOT A VALID DATE FORMAT
     C*
     C           *IN99     IFEQ '1'                        B*2
     C                     MOVE '1'       *IN52            Reverse image
     C                     MOVEL'ER65003' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     END                             E*2
     C                     END                             E*1
     C*
     C**   IF 'STATUS' IS DIFFERENT THAN BLANKS
     C*
     C           #STAT     IFNE *BLANKS                    B*1
     C*
     C**   IF 'STATUS' IS NOT A VALID STATUS (I, L, X, C, R)
     C*
     C           #STAT     IFNE 'I'                        B*2
     C           #STAT     ANDNE'L'
     C           #STAT     ANDNE'X'
     C           #STAT     ANDNE'C'
     C           #STAT     ANDNE'R'
     C                     MOVE '1'       *IN60            Reverse image
     C                     MOVEL'ER65021' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     END                             E*2
     C                     END                             E*1
     C*
     C**   IF NO ERROR INDICATOR ON
     C*
     C           *IN50     IFEQ '0'                        B*1
     C           *IN51     ANDEQ'0'
     C           *IN52     ANDEQ'0'
     C           *IN60     ANDEQ'0'
     C*
     C                     MOVE #REFN     #3REFN
     C                     MOVE #STAT     #3STAT
     C                     MOVE #EXDT     #3EXDT
     C/COPY WNCPYSRC,SE6500C002
      *                                                                   CSE010
      ** Check Security Shortname and call Securities Selection           CSE010
      *                                                                   CSE010
     C           CSE010    IFEQ 'Y'                                       CSE010
     C                     MOVEL#SECN     @QMK    1                       CSE010
     C           @QMK      IFEQ '?'                                       CSE010
     C                     MOVEL#SECN     @SESN                           CSE010
     C                     CALL 'SE0043'                                  CSE010
     C                     PARM           @ACDE   1                       CSE010
     C                     PARM           @SESN  10                       CSE010
     C           @SESN     IFNE *BLANKS                                   CSE010
     C                     MOVE @SESN     #SECN                           CSE010
     C                     ELSE                                           CSE010
     C                     MOVE *BLANKS   #SECN                           CSE010
     C                     ENDIF                                          CSE010
     C                     ENDIF                                          CSE010
     C                     ENDIF                                          CSE010
     C*
     C**   IF SECURITY FIELD IS SELECTED
     C*
     C           #SECN     IFNE *BLANKS                    B*2
     C*
     C                     MOVE #SECN     #3SECN
     C*
     C**   SETON AN INDICATOR TO DETERMINE THAN THE ACCESS IN THE ER REFERENCE
     C**   MUST BE DONE BY SECURITY AND EX-DATE AND REFERENCE
     C*
     C                     MOVE '1'       *IN53
     C                     MOVE #EXDT     #3EXDT
     C*
     C**   ELSE, SECURITY FIELD IS LEAVED BLANK
     C*
     C                     ELSE                            X*2
     C                     MOVE *BLANKS   #3SECN
     C*
     C**   IF EX-DATE FIELD IS SELECTED, SETON AN INDICATOR TO DETERMINE
     C**   THAN THE ACCESS IN THE ER REFERENCE FILE MUST BE DONE BY EX-DATE
     C**   AND SECURITY FIELDS
     C*
     C           #EXDT     IFNE *BLANKS                    B*3
     C                     MOVE #EXDT     #3EXDT
     C                     MOVE '1'       *IN54
     C*
     C**   ELSE, SECURITY AND EX-DATE FIELD ARE LEAVED BLANK, SETON AN INDICATOR
     C**   TO DETERMINE THAT THE ACCESS IN THE ER REFERENCE FILE MUST BE DONE
     C**   BY REFERENCE
     C*
     C                     ELSE                            B*3
     C                     MOVE *BLANKS   #3EXDT
     C                     MOVE '1'       *IN55
     C                     END                             E*3
     C*
     C                     END                             E*2
     C                     END                             E*1
     C*
     C                     ENDSR
     C*
     ******************************************************************
      /EJECT
     ******************************************************************
     C* P008   : VALIDATE SUBFILE RECORDS                             *
     ******************************************************************
     C*
     C           P008      BEGSR
     C*
     C                     MOVE 'N'       ERMSG   1
     C*
     C**   CLEAR MESSAGES FROM PROGRAM MESSAGE QUEUE.
     C*
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
     C*
     C**   READ CHANGED SUBFILE RECORD
     C*
     C                     READC#SFLNUM                  87
     C*
     C**   SETUP THE RRN WITH THE CURRENT CPF'S POSITION WITHIN THE SUBFILE
     C*
     C           *IN87     IFEQ '1'                        B*1
     C                     Z-ADDCPFPOS    #1RRN
     C                     END                             E*1
     C*
     C**   DO WHILE END OF FILE IS NOT REACHED AND F12 OR F3 NOT PRESSED
     C*
     C           *IN87     DOWEQ'0'                        B*1
     C           *INKC     ANDEQ'0'
     C           *INKL     ANDEQ'0'
     C*
     C**   REINITIALIZE ERROR INDICATORS
     C*
     C                     EXSR P014
     C*
     C                     MOVE '1'       *IN84            SFLNXTCHG
     C*
     C**   IF 'ADD MODE'
     C*
     C           *IN43     IFEQ '0'                        B*2
     C           *IN61     ANDEQ'0'
     C*
     C**   IF AT LEAST ONE FIELD IS DIFFERENT THAN BLANKS
     C*
     C           #1REFN    IFNE *BLANKS                    B*3
     C           #1SECN    ORNE *BLANKS
     C           #1CORR    ORNE *BLANKS
     C*
     C**   IF 'REFERENCE' IS LEFT BLANK
     C*
     C           #1REFN    IFEQ *BLANKS                    B*4
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN38            Reverse image
     C                     MOVEL'ER65004' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     ELSE                            X*4
     C*
     C**   CHECK IT'S A VALID NUMERIC FIELD
     C*
     C           DIGITS    CHECK#1REFN:1  N       10     89
     C*
     C**   IF IT'S NOT A VALID NUMERIC FIELD
     C*
     C           *IN89     IFEQ '1'                        B*5
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN38            Reverse image
     C                     MOVEL'ER65001' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     ELSE                            X*5
     C*
     C**   ACCESS ER REFERENCE FILE
     C*
     C           #1REFN    CHAINSEERRFL1             89
     C*
     C**   IF A RECORD IS FOUND
     C*
     C           *IN89     IFEQ '0'                        B*6
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN38            Reverse image
     C                     MOVEL'ER65005' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     ELSE                            X*6
     C*
     C**   ACCESS ER ALLOCATION FILE
     C*
     C           #1REFN    CHAINSEERALL1             89
     C*
     C**   IF A RECORD IS FOUND
     C*
     C           *IN89     IFEQ '0'                        B*7
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN38            Reverse image
     C                     MOVEL'ER65038' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     END                             E*7
     C                     END                             E*6
     C                     END                             E*5
     C                     END                             E*4
     C/COPY WNCPYSRC,SE6500C003
      *                                                                   CSE010
      ** Check Security Shortname and call Securities Selection           CSE010
      *                                                                   CSE010
     C           CSE010    IFEQ 'Y'                                       CSE010
     C                     MOVEL#1SECN    @QMK    1                       CSE010
     C           @QMK      IFEQ '?'                                       CSE010
     C                     MOVEL#1SECN    @SESN                           CSE010
     C                     CALL 'SE0043'                                  CSE010
     C                     PARM           @ACDE   1                       CSE010
     C                     PARM           @SESN  10                       CSE010
     C           @SESN     IFNE *BLANKS                                   CSE010
     C                     MOVE @SESN     #1SECN                          CSE010
     C                     ELSE                                           CSE010
     C                     MOVE *BLANKS   #1SECN                          CSE010
     C                     ENDIF                                          CSE010
     C                     ENDIF                                          CSE010
     C                     ENDIF                                          CSE010
     C*
     C**   IF 'SECURITY SHORTNAME' LEFT BLANK
     C*
     C           #1SECN    IFEQ *BLANKS                    B*4
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN39            Reverse image
     C                     MOVEL'ER65006' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     ELSE                            X*4
     C*
     C**   ACCESS SECURITY DETAILS WITH CURRENT SECURITY SHORTNAME
     C*
     C           #1SECN    CHAINSECTY                89
     C*
     C**   IF NO RECORD IS FOUND
     C*
     C           *IN89     IFEQ '1'                        B*5
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN39            Reverse image
     C                     MOVEL'ER65007' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     ELSE                            X*5
     C*
     C**   TRADE BASIS MUST BE A PERCENTAGE
     C*
     C           STBS      IFNE 'P'                        B*6
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN39            Reverse image
     C                     MOVEL'ER65041' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     END                             E*6
     C*
     C**   EARLY REPAY INDICATOR MUST BE EQUAL TO 'Y'
     C*
     C           ERPY      IFNE 'Y'                        B*6
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN39            Reverse image
     C                     MOVEL'ER65042' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     END                             E*6
     C*
     C**   ACCESS INVESTMENT TYPE FILE
     C*
     C                     MOVE NMCY      KNMCY   3
     C                     MOVE SITP      KSITP   3
     C*
     C           KEY3      CHAININVTP                89
     C*
     C**   IF NO RECORD FOUND
     C*
     C           *IN89     IFEQ '1'                        B*6
     C                     MOVELNMCY      WFLD1   6
     C                     MOVE SITP      WFLD1
     C                     MOVEL'003'     DBASE            *****************
     C***********          MOVEL'INVTP   'DBFILE           ** DB ERROR 03 S01496
     C                     MOVE 'INVTP   'DBFILE           * DB ERROR 03 *S01496
     C                     MOVELWFLD1     DBKEY            *****************
     C                     EXSR *PSSR
     C                     END                             E*6
     C*
     C**   PROCESSING TYPE MUST BE '1' OR '3' ('FIXED INTEREST' OR 'RATE NOTES')
     C*
     C           PROT      IFNE '1'                        B*6
     C           PROT      ANDNE'3'
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN39            Reverse image
     C                     MOVEL'ER65008' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     END                             E*6
     C                     END                             E*5
     C                     END                             E*4
     C                     END                             E*3
     C                     END                             E*2
     C*
     C**   IF NOT ENQUIRY MODE
     C*
     C           *IN61     IFEQ '0'                        B*2
     C*
     C**   IF AT LEAST ONE FIELD IS DIFFERENT THAN BLANK
     C*
     C           #1EXDT    IFNE *BLANKS                    B*3
     C           #1REFN    ORNE *BLANKS
     C           #1SECN    ORNE *BLANKS
     C           #1CORR    ORNE *BLANKS
     C*
     C**   IF EX-DATE IS CHANGED
     C*
     C           #1EXDT    IFNE #2EXDT                     B*4
     C           #1EXDT    OREQ *BLANKS
     C*
     C                     MOVE #1EXDT    ZDATE
     C                     EXSR ZDATE1
     C*
     C**   IF EX-DATE IS NOT A VALID DATE FORMAT
     C*
     C           *IN99     IFEQ '1'                        B*5
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN32            Reverse image
     C                     MOVEL'ER65003' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     ELSE                            X*5
     C*
     C**   IF EX-DATE LOWER OR EQUAL THAN SE BACK VALUE DATE
     C*
     C           ZDAYNO    IFLE WBACKD
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN32            Reverse image
     C                     MOVEL'ER65010' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     END                             E*6
     C*
     C**   IF EX-DATE NOT LOWER THAN SECURITY MATURITY DATE
     C*
     C           ZDAYNO    IFGE MATY                       B*6
     C           MATY      ANDNE*ZEROS
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN32            Reverse image
     C                     MOVEL'ER65022' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     END                             E*6
     C                     END                             E*5
     C*
     C**   IF NO ERROR ON SECURITY AND EX-DATE (IN AMEND MODE ONLY)
     C*
     C           *IN39     IFEQ '0'                        B*5
     C           *IN32     ANDEQ'0'
     C           *IN43     ANDEQ'1'
     C*
     C**   SETUP KEY FIELDS
     C*
     C                     MOVE #1SECN    KSECN
     C                     MOVE #1EXDT    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    KEXDT
     C*
     C**   ACCESS ER REFERENCES FILE WITH THE SECURITY SHORTNAME AND EX-DATE
     C*
     C           KEY4      CHAINSEERRFL3             89
     C*
     C**   IF A RECORD IS FOUND
     C*
     C           *IN89     IFEQ '0'                        B*6
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN32            Reverse image
     C                     MOVEL'ER65019' ZAMSID           Message id.
     C                     MOVE *BLANKS   ZAMSDA           Message Data
     C                     MOVELVAERRF    ZAMSDA           Message Data
     C                     EXSR ZASNMS                     Send message
     C                     END                             E*6
     C                     END                             E*5
     C                     END                             E*4
     C*
     C**   IF SECURITY AND EX-DATE ARE VALID AND NOT EQUAL TO BLANKS
     C*
     C           *IN39     IFEQ '0'                        B*4
     C           #1SECN    ANDNE*BLANKS
     C           *IN32     ANDEQ'0'
     C           #1EXDT    ANDNE*BLANKS
     C*
     C**   IF REDEMPTION DATE IS BLANK OR EX-DATE IS CHANGED AND VALID
     C*
     C           #1RDAT    IFEQ *BLANKS                    B*5
     C           #1EXDT    ORNE #2EXDT
     C           *IN32     ANDEQ'0'
     C*
     C**   ACCESS SECURITY DETAILS TO OBTAIN THE SECURITY MATURITY DATE
     C*
     C           #1SECN    CHAINSECTY                89
     C*
     C**   CALCULATE REDEMPTION DATE AS NEXT COUPON PAYMENT DATE AFTER EX-DATE
     C*
     C                     ADD  1         ZDAYNO
     C                     Z-ADD*ZEROS    FCPN
     C                     EXSR ZNCD
     C*
     C                     Z-ADDNCD       ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     #1RDAT
     C*
     C**   IF REDEMPTION DATE NOT LOWER THAN SECURITY MATURITY DATE
     C*
     C           ZDAYNO    IFGE MATY                       B*6
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN33            Reverse image
     C                     MOVEL'ER65023' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     ELSE
     C*
     C**   THE DEFAULT 'PAYMENT DATE' IS FIRST WORKING DAY BEGINNING AT
     C**   'REDEMPTION DATE'. SO CONVERT 'REDEMPTION DATE' IN MIDAS FMT
     C*
     C                     MOVE #1RDAT    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    WCOUNT  50
     C*
     C**   INITIALISE RETURN FIELD
     C*
     C***********          MOVE 'H'       @@HIND                          S01496
     C                     MOVE 'H'       ZIND                            S01496
     C*
     C**   DO WHILE RETURN FIELD EQUAL TO 'H'
     C*
     C***********@@HIND    DOWEQ'H'                        B*1            S01496
     C***********                                                         S01496
     C***********          MOVE *BLANKS   @@HIND                          S01496
     C***********          Z-ADDWCOUNT    @@MNO   50                      S01496
     C***********          MOVE NMCY      @@CCY   3                       S01496
     C***********          EXSR ZA0830                                    S01496
     C*                                                                   S01496
     C           ZIND      DOWEQ'H'                        B*1            S01496
     C*                                                                   S01496
     C                     MOVE *BLANKS   ZIND                            S01496
     C                     Z-ADDWCOUNT    ZDAYNO  50                      S01496
     C                     MOVE NMCY      ZCCY    3                       S01496
     C                     MOVE A8LCCD    ZLOC    3                       S01496
     C                     EXSR ZCHKH                                     S01496
     C*
     C**   IF IT'S A HOLIDAY FOR THE CURRENCY
     C*
     C***********@@HIND    IFEQ 'H'                        B*2            S01496
     C           ZIND      IFEQ 'H'                        B*2            S01496
     C                     ADD  1         WCOUNT
     C                     END                             E*2
     C                     END                             E*1
     C*
     C**   SETUP THE DISPLAY FIELDS
     C*
     C                     Z-ADDWCOUNT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     #1ERPD
     C                     END                             E*6
     C                     END                             E*5
     C*
     C**   SECURITY SHORTNAME OR/AND EX-DATE NOT VALID
     C*
     C                     ELSE                            X*4
     C                     MOVE *BLANKS   #1RDAT
     C                     END                             E*4
     C*
     C**   IF ALL THE FIELDS ARE EQUAL TO BLANKS
     C*
     C                     ELSE                            X*3
     C                     MOVE *BLANKS   #1RDAT
     C                     END                             E*3
     C                     END                             E*2
     C*
     C**   IF 'AMEND MODE' OR 'ENQUIRY' MODE
     C*
     C           *IN43     IFEQ '1'                        B*2
     C           *IN61     OREQ '1'
     C*
     C**   IF FINAL ALLOCATION IS NOT EQUAL TO 'Y' OR 'N'
     C*
     C           #1FINA    IFNE 'Y'                        B*3
     C           #1FINA    ANDNE'N'
     C           *IN61     ANDEQ'0'
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN35            Reverse image
     C                     MOVEL'ER65011' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     END                             E*3
     C*
     C**   IF ACTION INDICATOR IS CHANGED
     C*
     C           #1SEL     IFNE *BLANKS                    B*3
     C                     EXSR P008A
     C                     END                             E*3
     C*
     C**   PROCESS ACTION (ONLY ENQUIRY MODE)
     C*
     C           ERMSG     IFEQ 'N'                        B*3
     C           *IN61     ANDEQ'1'
     C                     EXSR P009
     C                     MOVE '0'       *IN84
     C                     END                             E*3
     C                     END                             E*2
     C*
     C**   IF AN ERROR MESSAGE IS SENT
     C**   OR IF 'ADD MODE'
     C*
     C           ERMSG     IFEQ 'Y'                        B*2
     C           *IN43     OREQ '0'
     C*
     C**   IF 'EARLY REDEMPTION STATUS' IS NOT 'INCOMPLETED'
     C**     PROTECT 'EX-DATE' SUBFILE FIELD (AMEND MODE)
     C*
     C           *IN43     IFEQ '1'                        B*3
     C*
     C           #1STAT    IFNE 'I'                        B*4
     C           #1CORR    ORNE *BLANKS
     C                     MOVE '1'       *IN59
     C                     ELSE
     C                     MOVE '0'       *IN59
     C                     END                             E*4
     C*
     C**   IF 'FINAL ALLOCATION' FIELD IS EQUAL TO 'Y'
     C**   OR 'EARLY REDEMPTION STATUS' IS EQUAL TO 'R', PROTECT THIS FIELD
     C*
     C           #1FINA    IFEQ 'Y'                        B*4
     C           #1STAT    OREQ 'R'
     C                     MOVE '1'       *IN68
     C                     ELSE                            X*4
     C                     MOVE '0'       *IN68
     C                     END                             E*4
     C*
     C**   IF 'LOCKED BY USER IDENTIFIER' IS NOT BLANK
     C**     PROTECT ALL SUBFILE RECORD FIELDS
     C*
     C           #1REFN    CHAINSEERRFL1             89
     C*
     C           VAUSER    IFNE *BLANKS                    B*4
     C                     MOVE '1'       *IN63
     C                     ELSE                            X*4
     C                     MOVE '0'       *IN63
     C                     END                             E*4
     C*
     C                     END                             E*3
     C*
     C**   UPDATE SUBFILE RECORD (FOR REVERSED ERROR FIELDS)
     C*
     C                     UPDAT#SFLNUM
     C*
     C**   GOTO NEXT READ OF THE SUBFILE CHANGING
     C*
     C                     GOTO TAG4
     C                     END                             E*2
     C*
     C**   IF 'AMEND MODE'
     C*
     C           *IN43     IFEQ '1'                        B*2
     C*
     C**   IF EX-DATE OR FINAL ALLOCATION INDICATOR IS/ARE CHANGED
     C*
     C           #1EXDT    IFNE #2EXDT                     B*3
     C           #1FINA    ORNE #2FINA
     C*
     C**   ACCESS EARLY REDEMPTION REFERENCE FILE WITH CURRENT REFERENCE
     C*
     C           #1REFN    CHAINSEERRFL0             89
     C*
     C**   IF RECORD IS DIFFERENT THAN READ AT CHARGING OF SUBFILE
     C*
     C           FIELD     IFNE #FIELD                     B*4
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN32            Reverse image
     C                     MOVE '1'       *IN33            Reverse image
     C                     MOVE '1'       *IN34            Reverse image
     C                     MOVE '1'       *IN35            Reverse image
     C                     MOVE '1'       *IN36            Reverse image
     C                     MOVE '1'       *IN37            Reverse image
     C                     MOVE '1'       *IN38            Reverse image
     C                     MOVE '1'       *IN39            Reverse image
     C                     MOVEL'ER65902' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C*
     C**   UPDATE SUBFILE RECORD (FOR REVERSED ERROR FIELDS)
     C*
     C                     UPDAT#SFLNUM
     C*
     C**   RELEASE THE RECORD
     C*
     C           #1REFN    SETLLSEERRFL0
     C*
     C**   GOTO NEXT READ OF THE SUBFILE CHANGING
     C*
     C                     GOTO TAG4
     C                     ELSE                            X*4
     C*
     C                     MOVE 'D'       VARECI
     C                     Z-ADDBJRDNB    VALCD
     C                     MOVE 'A'       VACHTP
     C                     MOVE ##USR     VALUID
     C*
     C**   IF EX-DATE CHANGED
     C*
     C           #1EXDT    IFNE #2EXDT                     B*5
     C                     MOVE #1EXDT    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    VAEREX
     C*
     C                     MOVE #1RDAT    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    VAERRD
     C                     MOVE #1EXDT    #2EXDT
     C*
     C                     MOVE #1ERPD    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    VAERPD
     C                     END                             E*5
     C*
     C**   IF FINAL ALLOCATION CHANGED
     C*
     C           #1FINA    IFNE #2FINA                     B*5
     C                     MOVE #1FINA    VAFAID
     C                     MOVE #1FINA    #2FINA
     C                     END                             E*5
     C*
     C                     UPDATSEERRFD0
     C                     MOVE VAJOBN    WAJOBN 10
     C                     MOVE VAUSER    WAUSER 10
     C                     Z-ADDVAJNBR    WAJNBR  60
     C                     MOVE *BLANKS   VAJOBN
     C                     MOVE *BLANKS   VAUSER
     C                     Z-ADD*ZEROS    VAJNBR
     C                     MOVE FIELD     #FIELD
     C                     MOVE WAJOBN    VAJOBN
     C                     MOVE WAUSER    VAUSER
     C                     Z-ADDWAJNBR    VAJNBR
     C                     END                             E*4
     C                     END                             E*3
     C*
     C**   EXECUTE COMMITMENT CONTROL
     C*
     C                     COMIT
     C*
     C**   IF ACTION IS CHANGED
     C*
     C           #1SEL     IFNE #2SEL                      B*3
     C*
     C**   ACCESS EARLY REDEMPTION REFERENCE WITH CURRENT REFERENCE
     C*
     C           #1REFN    CHAINSEERRFL0             89
     C*
     C**   IF RECORD IS DIFFERENT THAN READ AT CHARGING OF SUBFILE
     C*
     C           FIELD     IFNE #FIELD                     B*4
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN32            Reverse image
     C                     MOVE '1'       *IN33            Reverse image
     C                     MOVE '1'       *IN34            Reverse image
     C                     MOVE '1'       *IN35            Reverse image
     C                     MOVE '1'       *IN36            Reverse image
     C                     MOVE '1'       *IN37            Reverse image
     C                     MOVE '1'       *IN38            Reverse image
     C                     MOVE '1'       *IN39            Reverse image
     C                     MOVEL'ER65902' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C*
     C**   UPDATE SUBFILE RECORD (FOR REVERSED ERROR FIELDS)
     C*
     C                     UPDAT#SFLNUM
     C*
     C**   RELEASE THE RECORD
     C*
     C           #1REFN    SETLLSEERRFL0
     C*
     C**   GOTO NEXT READ OF THE SUBFILE CHANGING
     C*
     C                     GOTO TAG4
     C                     ELSE                            X*4
     C*
     C                     MOVE ##JOB     VAJOBN
     C                     MOVE ##USR     VAUSER
     C                     Z-ADD##JNO     VAJNBR
     C*
     C**   UPDATE THE RECORD IN EARLY REDEMPTION REFERENCE FILE
     C*
     C                     UPDATSEERRFD0
     C                     MOVE VAJOBN    WAJOBN 10
     C                     MOVE VAUSER    WAUSER 10
     C                     Z-ADDVAJNBR    WAJNBR  60
     C                     MOVE *BLANKS   VAJOBN
     C                     MOVE *BLANKS   VAUSER
     C                     Z-ADD*ZEROS    VAJNBR
     C                     MOVE FIELD     #FIELD
     C                     MOVE WAJOBN    VAJOBN
     C                     MOVE WAUSER    VAUSER
     C                     Z-ADDWAJNBR    VAJNBR
     C*
     C**   EXECUTE THE COMMITMENT INSTRUCTION
     C*
     C                     COMIT
     C*
     C**   PROCESS ACTION
     C*
     C                     EXSR P009
     C                     END                             E*4
     C                     END                             E*3
     C*
     C**   IF 'EARLY REDEMPTION STATUS' IS NOT 'INCOMPLETED'
     C**     PROTECT 'EX-DATE' SUBFILE FIELD
     C*
     C           #1STAT    IFNE 'I'                        B*4
     C           #1CORR    ORNE *BLANKS
     C                     MOVE '1'       *IN59
     C                     ELSE
     C                     MOVE '0'       *IN59
     C                     END                             E*4
     C*
     C**   IF 'FINAL ALLOCATION' FIELD IS EQUAL TO 'Y'
     C**   OR 'EARLY REDEMPTION STATUS' IS EQUAL TO 'R', PROTECT THIS FIELD
     C*
     C           #1FINA    IFEQ 'Y'                        B*4
     C           #1STAT    OREQ 'R'
     C                     MOVE '1'       *IN68
     C                     ELSE                            X*4
     C                     MOVE '0'       *IN68
     C                     END                             E*4
     C*
     C**   IF 'LOCKED BY USER IDENTIFIER' IS NOT BLANK
     C**     PROTECT ALL SUBFILE RECORD FIELDS
     C*
     C           VAUSER    IFNE *BLANKS                    B*4
     C                     MOVE '1'       *IN63
     C                     ELSE                            X*4
     C                     MOVE '0'       *IN63
     C                     END                             E*4
     C*
     C**   IF NO ERROR MESSAGE SENT
     C*
     C           ERMSG     IFEQ 'N'                        B*3
     C                     MOVE '0'       *IN84
     C                     UPDAT#SFLNUM
     C                     ELSE                            X*3
     C                     MOVE '1'       *IN84
     C                     UPDAT#SFLNUM
     C                     END                             E*3
     C                     END                             E*2
     C*
     C           TAG4      TAG
     C*
     C                     WRITE#MSGCTL
     C*
     C**   READ CHANGED SUBFILE RECORD
     C*
     C                     READC#SFLNUM                  87
     C                     END                             E*1
     C*
     C**   IF 'ADD MODE'
     C**   AND IF NO ERROR MESSAGE IS SENT
     C*
     C           *IN43     IFEQ '0'                        B*1
     C           *IN61     ANDEQ'0'
     C           ERMSG     ANDEQ'N'
     C*
     C                     Z-ADD1         #1RRN
     C                     MOVE 'Y'       FINSR   1
     C*
     C**   READ FIRST RECORD OF SUBFILE
     C*
     C                     READC#SFLNUM                  87
     C*
     C**   DO WHILE END OF SUBFILE IS NOT REACHED
     C*
     C           *IN87     DOWEQ'0'                        B*2
     C*
     C**   IF REFERENCE FIELD IS LEFT BLANK (IT MEANS THAT RECORD IS BLANK)
     C*
     C           #1REFN    IFNE *BLANKS                    B*3
     C*
     C**   ACCESS TO ER REFERENCE FILE WITH CURRENT REFERENCE
     C*
     C           #1REFN    CHAINSEERRFL1             88
     C*
     C**   IF A RECORD IS FOUND
     C*
     C           *IN88     IFEQ '0'                        B*4
     C                     MOVE '1'       *IN32            Reverse image
     C                     MOVE '1'       *IN33            Reverse image
     C                     MOVE '1'       *IN34            Reverse image
     C                     MOVE '1'       *IN35            Reverse image
     C                     MOVE '1'       *IN36            Reverse image
     C                     MOVE '1'       *IN37            Reverse image
     C                     MOVE '1'       *IN38            Reverse image
     C                     MOVE '1'       *IN39            Reverse image
     C                     MOVEL'ER65901' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C*
     C**   UPDATE SUBFILE RECORD (FOR REVERSED ERROR FIELDS)
     C*
     C                     UPDAT#SFLNUM
     C*
     C**   ROLLBACK INSTRUCTION TO REVERSE ALL INSERTS
     C*
     C                     ROLBK
     C*
     C**   TERMINATE CURRENT PROCESSING
     C*
     C                     GOTO TAG5
     C                     ELSE                            X*4
     C*
     C**   VALIDATE 'CORRECTION REFERENCE'
     C*
     C                     EXSR P013
     C*
     C**   IF ERROR ON 'CORRECTION REFERENCE'
     C*
     C           *IN37     IFEQ '1'                        B*5
     C*
     C**   UPDATE SUBFILE RECORD (FOR REVERSED ERROR FIELDS)
     C*
     C                     UPDAT#SFLNUM
     C*
     C**   ROLLBACK INSTRUCTION TO REVERSE ALL INSERTS
     C*
     C                     ROLBK
     C*
     C**   TERMINATE CURRENT PROCESSING
     C*
     C                     GOTO TAG5
     C                     END                             E*5
     C*
     C**   SET DEFAULTS OF 'FULL DETAIL' FIELDS
     C*
     C                     EXSR P020
     C*
     C**   WRITE A RECORD IN EARLY REDEMPTION REFERENCE FILE
     C*
     C                     MOVE 'D'       VARECI
     C                     Z-ADDBJRDNB    VALCD
     C                     MOVE 'I'       VACHTP
     C                     MOVE ##USR     VALUID
     C                     MOVE #1REFN    VAERRF
     C                     MOVE #1SECN    VASESN
     C*
     C                     MOVE #1EXDT    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    VAEREX
     C*
     C                     MOVE #1RDAT    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    VAERRD
     C*
     C                     Z-ADD*ZEROS    VAERAD
     C                     MOVE 'N'       VAFAID
     C                     MOVE 'I'       VAERST
     C                     MOVE #1CORR    VACORF
     C*
     C                     MOVE 'N'       VACFRI
     C                     MOVE 'N'       VACFSI
     C                     MOVE 'N'       VAPOSI
     C                     MOVE 'N'       VAEVNI
     C                     MOVE 'N'       VAPM04
     C*
     C                     MOVE *BLANKS   VAJOBN
     C                     MOVE *BLANKS   VAUSER
     C                     Z-ADD*ZEROS    VAJNBR
     C*
     C                     WRITESEERRFD0
     C*
     C**   SAVE THE FIRST INSERTED RECORD FIELDS
     C*
     C           FINSR     IFEQ 'Y'                        B*5
     C                     MOVE VAERRF    KREFN
     C                     MOVE '1'       *IN55
     C                     MOVE 'N'       FINSR
     C                     END                             E*5
     C*
     C**   UPDATE SUBFILE RECORD (FOR REVERSED ERROR FIELDS)
     C*
     C                     UPDAT#SFLNUM
     C                     END                             E*4
     C                     END                             E*3
     C*
     C**   READ FIRST RECORD OF SUBFILE
     C*
     C                     READC#SFLNUM                  87
     C                     END                             E*2
     C*
     C**   EXECUTE COMMITMENT CONTROL TO FIX ALL INSERTED RECORDS
     C*
     C                     COMIT
     C*
     C**   SET KEY FIELDS FROM KLIST TO FIRST INSERTED RECORD
     C*
     C*
     C**   CLEAR SUBFILE
     C*
     C                     Z-ADD0         WWRRN   40       SAVE RCD.NBR.
     C*
     C**   CLEAR SUBFILE
     C*
     C                     EXSR P004
     C*
     C**   SET INDICATOR TO RETURN TO 'AMEND MODE'
     C*
     C                     MOVE '1'       *IN43
     C                     MOVEATXT,3     ##CTX1
     C*
     C**   PROTECT ON 'POSITION/SELECTION' FIELDS *OFF
     C**   PROTECT/NON-DISPLAY 'ACTION', 'LAST FINAL ALLOCATION' *OFF
     C**   PROTECT ON 'REFERENCE NUM.' 'SECURITY SHORT.' 'CORRECTION REFERENCE'
     C**              SUBFILE FIELDS *ON
     C*
     C                     MOVE '0'       *IN56
     C                     MOVE '0'       *IN57
     C                     MOVE '1'       *IN58
     C*
     C                     MOVE '0'       *IN45
     C                     MOVE '0'       *IN30
     C*
     C                     EXSR P006
     C*
     C                     MOVE '1'       *IN45
     C*
     C                     END                             E*1
     C*
     C           TAG5      ENDSR
     ******************************************************************
      /EJECT
     ******************************************************************
     C* P008A  : VALIDATE 'ACTION' FIELD                              *
     ******************************************************************
     C*
     C           P008A     BEGSR
     C*
     C**   IF IT'S NOT THE REPORT MODE
     C*
     C           *IN64     IFEQ '0'                        B*1
     C*
     C**   IF IT'S THE ENQUIRY MODE, THE 'ACTION' MUST BE EQUAL TO 'E' OR 'F'
     C*
     C           *IN61     IFEQ '1'                        B*2
     C*
     C           #1SEL     IFNE 'E'                        B*3
     C           #1SEL     ANDNE'F'
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN62            Reverse image
     C                     MOVEL'ER65033' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     GOTO TAG2
     C                     END                             E*3
     C*
     C                     ELSE                            X*2
     C*
     C**   'ACTION' CODE CAN BE 'A', 'E', 'P', 'L', 'F', 'X' OR 'R'
     C*
     C           #1SEL     IFNE 'A'                        B*3
     C           #1SEL     ANDNE'E'
     C           #1SEL     ANDNE'P'
     C           #1SEL     ANDNE'L'
     C           #1SEL     ANDNE'F'
     C           #1SEL     ANDNE'X'
     C           #1SEL     ANDNE'R'
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN62            Reverse image
     C                     MOVEL'ER65009' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     GOTO TAG2
     C                     END                             E*3
     C                     END                             E*2
     C*
     C**   IF IT'S THE REPORT MODE, THE 'ACTION' MUST BE EQUAL TO 'P'
     C*
     C                     ELSE                            X*1
     C*
     C           #1SEL     IFNE 'P'                        B*2
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN62            Reverse image
     C                     MOVEL'ER65034' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     GOTO TAG2
     C                     END                             E*2
     C                     END                             E*1
     C*
     C**   IF 'EX-DATE' IS *GE THAN 'RUN DATE' AND ACTION IS NOT EQUAL TO 'F'
     C*
     C                     MOVE #1EXDT    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    KEXDT
     C*
     C           KEXDT     IFGE BJRDNB                     B*1
     C           #1SEL     ANDNE'F'
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN62            Reverse image
     C                     MOVEL'ER65036' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     GOTO TAG2
     C                     END                             E*1
     C*
     C**   ACCESS ER REFERENCE FILE
     C*
     C           #1REFN    CHAINSEERRFL1             89
     C*
     C**   IF LOCKED BY USER IDENTIFIER IS NOT BLANK
     C*
     C           VAUSER    IFNE *BLANKS                    B*1
     C                     MOVE VAUSER    WUSER
     C                     MOVE VAJOBN    WJOBN
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN32            Reverse image
     C                     MOVE '1'       *IN33            Reverse image
     C                     MOVE '1'       *IN34            Reverse image
     C                     MOVE '1'       *IN35            Reverse image
     C                     MOVE '1'       *IN36            Reverse image
     C                     MOVE '1'       *IN37            Reverse image
     C                     MOVE '1'       *IN38            Reverse image
     C                     MOVE '1'       *IN39            Reverse image
     C                     MOVELLOCKDS    ZAMSDA
     C                     MOVEL'ER65012' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     GOTO TAG2
     C                     END                             E*1
     C*
     C**   IF 'ER STATUS' IS 'INCOMPLETED' AND 'ACTION' IS 'AUTHORISATION'
     C**     ERROR MESSAGE : AUTHORISATION IS NOT ALLOWED WITH STATUS 'I'
     C*
     C           #1STAT    IFEQ 'I'                        B*1
     C           #1SEL     ANDEQ'X'
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN62            Reverse image
     C                     MOVEL'ER65013' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     GOTO TAG2
     C                     END                             E*1
     C*
     C**   IF 'ER STATUS' IS 'AUTHORISED'
     C**   AND ACTION IS 'ALLOCATION', 'AUTHORISATION'
     C**    ERROR MESSAGE : ALLOCATION AND AUTHORISATION NOT ALLOWED
     C**                    WITH STATUS 'X' (AUTHORISED)
     C*
     C           #1STAT    IFEQ 'X'                        B*1
     C           #1SEL     ANDEQ'L'
     C           #1STAT    OREQ 'X'
     C           #1SEL     ANDEQ'X'
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN62            Reverse image
     C                     MOVEL'ER65014' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     GOTO TAG2
     C                     END                             E*1
     C*
     C**   IF 'ER STATUS' IS 'COMPLETED'
     C**   AND ACTION IS 'AMEND', 'ALLOCATION' OR 'AUTHORISATION'
     C**    ERROR MESSAGE : AMEND, ALLOCATION AND AUTHORISATION
     C**                    NOT ALLOWED WITH STATUS 'C' (COMPLETED)
     C*
     C           #1STAT    IFEQ 'C'                        B*1
     C           #1SEL     ANDEQ'A'
     C           #1STAT    OREQ 'C'
     C           #1SEL     ANDEQ'L'
     C           #1STAT    OREQ 'C'
     C           #1SEL     ANDEQ'X'
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN62            Reverse image
     C                     MOVEL'ER65015' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     GOTO TAG2
     C                     END                             E*1
     C*
     C**   IF 'ER STATUS' IS 'REVERSED' AND ACTION IS NOT 'E', 'F' OR 'P'
     C**    ERROR MESSAGE : ONLY 'E' FOR ENQUIRY OR 'F' FOR FULL DETAIL
     C**                    OR 'P' FOR PRINT DETAILS
     C**                    ARE ALLOWED WHEN STATUS 'R' (REVERSED)
     C*
     C           #1STAT    IFEQ 'R'                        B*1
     C           #1SEL     ANDNE'E'
     C           #1SEL     ANDNE'F'
     C           #1SEL     ANDNE'P'
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN62            Reverse image
     C                     MOVEL'ER65016' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     GOTO TAG2
     C                     END                             E*1
     C*
     C**   IF ACTION IS 'AUTHORISATION'
     C*
     C           #1SEL     IFEQ 'X'                        B*1
     C*
     C**   CHECK IF ALL DEPOT REDEMPTIONS ARE COMPLETELY ALLOCATED
     C*
     C                     EXSR P012
     C*
     C**   IF NEGATIVE RESPONSE
     C*
     C           ERROR     IFEQ 'N'                        B*2
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN62            Reverse image
     C                     MOVEL'ER65017' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     GOTO TAG2
     C                     END                             E*2
     C                     END                             E*1
     C*
     C**   IF 'LAST ALLOCATION INDICATOR' IS 'N' AND IF ACTION IS 'X'
     C*
     C           #1FINA    IFEQ 'N'                        B*1
     C           #1SEL     ANDEQ'X'
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN62            Reverse image
     C                     MOVEL'ER65025' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     GOTO TAG2
     C                     END                             E*1
     C*
     C**   IF LAST ALLOCATION INDICATOR IS EQUAL TO 'Y' AND ACTION IS 'L'
     C*
     C           #1FINA    IFEQ 'Y'                        B*1
     C           #1SEL     ANDEQ'L'
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN62            Reverse image
     C                     MOVEL'ER65039' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     GOTO TAG2
     C                     END                             E*1
     C*
     C**   IF 'ACTION' IS 'ALLOCATION ('L'), TEST IF IT'S POSSIBLE TO RUN
     C**   AN ALLOCATION
     C*
     C           #1SEL     IFEQ 'L'                        B*1
     C*
     C**   ACCESS ER DEPOT TO SEE IF RECORD EXISTS
     C*
     C           #1REFN    CHAINSEERDPL1             89
     C*
     C**   IF NO RECORD IS FOUND
     C*
     C           *IN89     IFEQ '1'                        B*2
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN62            Reverse image
     C                     MOVEL'ER65026' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     GOTO TAG2
     C                     END                             E*2
     C                     END                             E*1
     C*
     C**   IF 'ACTION' IS 'REVERSE' ('R'), TEST IF IT'S POSSIBLE TO REVERSE
     C*
     C           #1SEL     IFEQ 'R'                        B*1
     C*
     C**   ACCESS SECURITY DETAILS TO RETRIEVE SECURITY MATURITY DATE
     C*
     C           #1SECN    CHAINSECTY                89
     C*
     C**   IF 'EX-DATE' IS NOT GREATER THAN CALCULATED 'BACK VALUE DATE'
     C**   OR IF 'RUN DATE' IS NOT LOWER THAN SECURITY 'MATURITY DATE'
     C*
     C           KEXDT     IFLE WBACKD                     B*2
     C           BJRDNB    ORGE MATY
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN62            Reverse image
     C                     MOVEL'ER65043' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     GOTO TAG2
     C                     END                             E*2
     C                     END                             E*1
     C*
     C**   IF 'ACTION' IS 'AMEND' AND IF POSITION SETTLEMENT INDICATOR EQUAL 'Y'
     C*
     C           #1SEL     IFEQ 'A'                        B*1
     C           VAPOSI    ANDEQ'Y'
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN62            Reverse image
     C                     MOVEL'ER65040' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     END                             E*1
     C*                                                                   S01496
     C**  Check if user is authorised to the action code                  S01496
     C*                                                                   S01496
     C**  If single branch environment                                    S01496
     C*                                                                   S01496
     C           AGMBIN    IFNE 'Y'                                       S01496
     C                     CALL 'ZVACTU'                                  S01496
     C                     PARM #1SEL     WLACTN  1                       S01496
     C                     PARM           WLERR   10                      S01496
     C*                                                                   S01496
     C           WLERR     IFNE *ZERO                                     S01496
     C                     MOVE 'Y'       ERMSG                           S01496
     C                     MOVE '1'       *IN62                           S01496
     C                     MOVEL'ER66006' ZAMSID                          S01496
     C                     EXSR ZASNMS                                    S01496
     C                     END                                            S01496
     C*                                                                   S01496
     C                     ELSE                                           S01496
     C*                                                                   S01496
     C**  If multibranching environment                                   S01496
     C*                                                                   S01496
     C                     CALL 'ZVACTBU'                                 S01496
     C                     PARM #1SEL     WLACTN  1                       S01496
     C                     PARM USRBCH    WLBRCH  3                       S01496
     C                     PARM           WLERR   10                      S01496
     C*                                                                   S01496
     C           WLERR     IFEQ 1                                         S01496
     C                     MOVE 'Y'       ERMSG                           S01496
     C                     MOVE '1'       *IN62                           S01496
     C                     MOVEL'ER66007' ZAMSID                          S01496
     C                     EXSR ZASNMS                                    S01496
     C                     ELSE                                           S01496
     C           WLERR     IFEQ 2                                         S01496
     C                     MOVE 'Y'       ERMSG                           S01496
     C                     MOVE '1'       *IN62                           S01496
     C                     MOVEL'ER66008' ZAMSID                          S01496
     C                     EXSR ZASNMS                                    S01496
     C                     END                                            S01496
     C                     END                                            S01496
     C*                                                                   S01496
     C                     END                                            S01496
     C*
     C           TAG2      ENDSR
     ******************************************************************
      /EJECT
     ******************************************************************
     C* P009   : PROCESS 'ACTION'                                     *
     ******************************************************************
     C*
     C           P009      BEGSR
     C*
     C**   IF ACTION IS 'AMEND' CALL 'DEPOT REDEMPTIONS MAINTENANCE' PROGRAM
     C*
     C           #1SEL     IFEQ 'A'                        B*1
     C*
     C                     MOVE *BLANKS   WRTCD
     C*
     C                     MOVE #1EXDT    ZDATE
     C                     EXSR ZDATE1
     C                     MOVE ZDAYNO    WEXDAT
     C*
     C                     MOVE #1RDAT    ZDATE
     C                     EXSR ZDATE1
     C                     MOVE ZDAYNO    WRDAT
     C*
     C                     MOVE *BLANKS   WALLOC
     C*
     C                     CALL 'SE6510'               99
     C                     PARM           WRTCD   1        Return Code
     C                     PARM           #1REFN           ER Reference
     C                     PARM           #1SECN           Security Shortname
     C                     PARM           WEXDAT  5        Ex-Date
     C                     PARM           WRDAT   5        Redemption Date
     C                     PARM           #1STAT           ER Status
     C                     PARM           WALLOC  1        Allocation Updated
     C                     PARM           DPTUPD  1        Depot Updated
     C*
     C**   IR ERROR INDICATOR OF THE PROGRAM CALL IS ON
     C**   OR IF RETURN CODE IS EQUAL TO 'E' (ERROR)
     C**         DATABASE ERROR
     C*
     C           *IN99     IFEQ '1'                        B*2
     C           WRTCD     OREQ 'E'
     C                     MOVEL'004'     DBASE   30       *****************
     C                     MOVEL*BLANKS   DBFILE 10        ** DB ERROR 04 **
     C                     MOVELNARR,2    DBKEY  29        *****************
     C                     EXSR *PSSR
     C                     END                             E*2
     C*
     C           WRTCD     IFNE 'M'                        B*2
     C                     MOVE *BLANKS   #1SEL
     C                     END                             E*2
     C*
     C**   IF RETURN CODE IS EQUAL TO '2', '3' OR 'M'
     C*
     C           WRTCD     IFEQ '2'                        B*2
     C           WRTCD     OREQ '3'
     C           WRTCD     OREQ 'M'
     C*
     C**   ACCESS TO ER REFERENCE FILE WITH CURRENT REFERENCE
     C*
     C           #1REFN    CHAINSEERRFL0             88
     C*
     C                     MOVE *BLANKS   VAJOBN
     C                     MOVE *BLANKS   VAUSER
     C                     Z-ADD*ZEROS    VAJNBR
     C*
     C           WALLOC    IFEQ 'Y'                        B*3
     C           DPTUPD    OREQ 'Y'
     C                     MOVE 'D'       VARECI
     C                     Z-ADDBJRDNB    VALCD
     C                     MOVE 'A'       VACHTP
     C                     MOVE ##USR     VALUID
     C                     END                             E*3
     C*
     C**   IF ALLOCATION UPDATED INDICATOR RETURNED FROM SE6510 IS 'Y'
     C*
     C           WALLOC    IFEQ 'Y'                        B*3
     C                     MOVE 'Y'       VACFRI
     C                     Z-ADDBJRDNB    VAERAD
     C                     Z-ADDBJRDNB    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     #1LASA
     C*
     C           #1STAT    IFNE 'X'                        B*4
     C                     MOVE 'L'       VAERST
     C                     MOVE 'L'       #1STAT
     C                     END                             E*4
     C                     END                             E*3
     C*
     C                     UPDATSEERRFD0
     C                     MOVE FIELD     #FIELD
     C*
     C**   COMMITMENT INSTRUCTION
     C*
     C                     COMIT
     C*
     C           WRTCD     IFEQ '3'                        B*3
     C                     MOVE '1'       *INKC
     C                     END                             E*3
     C*
     C           WRTCD     IFEQ 'M'                        B*3
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN62            Reverse image
     C                     MOVEL'ER65026' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     END                             E*3
     C*
     C                     GOTO TAG3
     C                     END                             E*2
     C                     END                             E*1
     C*
     C**   IF ACTION IS 'ENQUIRY' CALL 'DEPOT REDEMPTIONS ENQUIRY' PROGRAM
     C*
     C           #1SEL     IFEQ 'E'                        B*1
     C*
     C                     MOVE *BLANKS   WRTCD
     C*
     C                     MOVE #1EXDT    ZDATE
     C                     EXSR ZDATE1
     C                     MOVE ZDAYNO    WEXDAT
     C*
     C                     MOVE #1RDAT    ZDATE
     C                     EXSR ZDATE1
     C                     MOVE ZDAYNO    WRDAT
     C*
     C                     CALL 'SE6530'               99
     C                     PARM           WRTCD   1        Return Code
     C                     PARM           #1REFN           ER Reference
     C                     PARM           #1SECN           Security Shortname
     C                     PARM           WEXDAT  5        Ex-Date
     C                     PARM           WRDAT   5        Redemption Date
     C                     PARM           #1STAT           ER Status
     C*
     C**   IR ERROR INDICATOR OF THE PROGRAM CALL IS ON
     C**   OR IF RETURN CODE IS EQUAL TO 'E' (ERROR)
     C**         DATABASE ERROR
     C*
     C           *IN99     IFEQ '1'                        B*2
     C           WRTCD     OREQ 'E'
     C                     MOVEL'005'     DBASE   30       *****************
     C                     MOVEL*BLANKS   DBFILE 10        ** DB ERROR 05 **
     C                     MOVELNARR,3    DBKEY  29        *****************
     C                     EXSR *PSSR
     C                     END                             E*2
     C*
     C           WRTCD     IFNE 'M'                        B*2
     C                     MOVE *BLANKS   #1SEL
     C                     END                             E*2
     C*
     C**   IF RETURN CODE IS EQUAL TO '2', '3' OR 'M'
     C*
     C           WRTCD     IFEQ '2'                        B*2
     C           WRTCD     OREQ '3'
     C           WRTCD     OREQ 'M'
     C*
     C**   ACCESS TO ER REFERENCE FILE WITH CURRENT REFERENCE
     C*
     C           #1REFN    CHAINSEERRFL0             88
     C*
     C                     MOVE *BLANKS   VAJOBN
     C                     MOVE *BLANKS   VAUSER
     C                     Z-ADD*ZEROS    VAJNBR
     C*
     C                     UPDATSEERRFD0
     C                     MOVE FIELD     #FIELD
     C*
     C**   COMMITMENT INSTRUCTION
     C*
     C                     COMIT
     C*
     C           WRTCD     IFEQ '3'                        B*3
     C                     MOVE '1'       *INKC
     C                     END                             E*3
     C*
     C           WRTCD     IFEQ 'M'                        B*3
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN62            Reverse image
     C                     MOVEL'ER65037' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     END                             E*3
     C*
     C                     GOTO TAG3
     C                     END                             E*2
     C*
     C**   IF RETURN CODE IS EQUAL TO BLANK
     C*
     C           WRTCD     IFEQ ' '                        B*2
     C*
     C**   ACCESS TO ER REFERENCE FILE WITH CURRENT REFERENCE
     C*
     C           #1REFN    CHAINSEERRFL0             88
     C*
     C                     MOVE *BLANKS   VAJOBN
     C                     MOVE *BLANKS   VAUSER
     C                     Z-ADD*ZEROS    VAJNBR
     C*
     C                     UPDATSEERRFD0
     C                     MOVE FIELD     #FIELD
     C*
     C**   COMMITMENT INSTRUCTION
     C*
     C                     COMIT
     C*
     C                     END                             E*2
     C                     END                             E*1
     C*
     C**   IF 'ACTION' IS PRINT
     C*
     C           #1SEL     IFEQ 'P'                        B*1
     C*
     C                     MOVE *BLANKS   #1SEL
     C***********          MOVE *BLANKS   WRTCD                           082496
     C***********                                                         082496
     C***********          CALL 'SE6800'               99                 082496
     C***********          PARM           WRTCD            Return Code    082496
     C***********          PARM           #1REFN           ER Reference   082496
     C***********          PARM           WLSEQ   5     Rpt SequenceS01496082496
     C*                                                                   082496
     C                     MOVEL'SE6800'  @RNAM                           082496
     C                     MOVEL'SEC6801' @COTT                           082496
     C                     CALL 'FC0040X'                                 082496
     C                     PARM *BLANKS   @RTCD   7                       082496
     C                     PARM           @RNAM  10                       082496
     C                     PARM           @COTT  10                       082496
     C                     PARM '10001'   @CSEQ   5                       082496
     C                     PARM #1REFN    @PARM 100                       082496
     C                     PARM AGMBIN    @MBRC   1                       082496
     C                     PARM 'S'       @LVL    1                       082496
     C                     PARM *BLANKS   @ETTY   3                       082496
     C*
     C**   IR ERROR INDICATOR OF THE PROGRAM CALL IS ON
     C**   OR IF RETURN CODE IS EQUAL TO 'E' (ERROR)
     C**         DATABASE ERROR
     C*
     C************IN99     IFEQ '1'                        B*2            082496
     C***********WRTCD     OREQ 'E'                                       082496
     C           @RTCD     IFNE *BLANKS                                   082496
     C                     MOVEL'006'     DBASE   30       *****************
     C                     MOVEL*BLANKS   DBFILE 10        ** DB ERROR 06 **
     C                     MOVELNARR,4    DBKEY  29        *****************
     C                     EXSR *PSSR
     C                     END                             E*2
     C*
     C**   IF RETURN CODE IS EQUAL TO BLANK
     C*
     C***********WRTCD     IFEQ ' '                        B*2            082496
     C           @RTCD     IFEQ ' '                        B*2            082496
     C*
     C**   ACCESS TO ER REFERENCE FILE WITH CURRENT REFERENCE
     C*
     C           #1REFN    CHAINSEERRFL0             88
     C*
     C                     MOVE *BLANKS   VAJOBN
     C                     MOVE *BLANKS   VAUSER
     C                     Z-ADD*ZEROS    VAJNBR
     C*
     C                     UPDATSEERRFD0
     C                     MOVE FIELD     #FIELD
     C*
     C**   COMMITMENT INSTRUCTION
     C*
     C                     COMIT
     C*
     C                     END                             E*2
     C                     END                             E*1
     C*
     C**   IF 'ACTION' IS ALLOCATION, AUTHORISATION OR REVERSE
     C*
     C           #1SEL     IFEQ 'L'                        B*1
     C           #1SEL     OREQ 'X'
     C           #1SEL     OREQ 'R'
     C*
     C**   PRODUCE CONFIRMATION SCREEN
     C*
     C                     EXSR P010
     C*
     C**   CLEAR MESSAGES FROM PROGRAM MESSAGE QUEUE.
     C*
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
     C*
     C**   IF CONFIRMATION INDICATOR RETURNED IS NOT EQUAL TO 'Y'
     C*
     C           #1CONF    IFNE 'Y'                        B*2
     C*
     C**   ACCESS TO ER REFERENCE FILE WITH CURRENT REFERENCE
     C*
     C           #1REFN    CHAINSEERRFL0             88
     C*
     C                     MOVE *BLANKS   VAJOBN
     C                     MOVE *BLANKS   VAUSER
     C                     Z-ADD*ZEROS    VAJNBR
     C*
     C                     UPDATSEERRFD0
     C                     MOVE FIELD     #FIELD
     C*
     C                     COMIT
     C*
     C                     MOVE *BLANKS   #1SEL
     C*
     C**   TERMINATE CURRENT PROCESSING
     C*
     C                     GOTO TAG3
     C*
     C                     END                             E*2
     C                     END                             E*1
     C*
     C**   IF 'ACTION' IS ALLOCATION
     C*
     C           #1SEL     IFEQ 'L'                        B*1
     C*
     C**   SUBMIT IN BATCH 'EARLY REDEMPTIONS ALLOCATION' PROGRAM
     C**   WITH ER REFERENCE AS PARAMETER
     C*
     C                     MOVEL*BLANKS   CMDTXT
     C**********           Z-ADD160       CMDLEN 155                                          CSC023
     C                     Z-ADD240       CMDLEN 155                                          CSC023
     C                     MOVELCMD,1     CMDTXT
     C**********           MOVE CMD,2     CMDTXT                                             BUG2701
     C                     MOVE CMD,2     @CMDT2                                             BUG2701
     C                     MOVEL#1REFN    @REF
     C                     MOVEL#1SEL     @ACTI
     C                     MOVEL#1REFN    @REFN
     C                     MOVELCMD,3     XPARM                                               CSC023
     C                     CALL 'QCMDEXC'
     C                     PARM           CMDTXT
     C                     PARM           CMDLEN
     C*
     C                     MOVE *BLANKS   #1SEL
     C                     END                             E*1
     C*
     C**   IF 'ACTION' IS AUTHORISATION
     C*
     C           #1SEL     IFEQ 'X'                        B*1
     C*
     C**   ACCESS EARLY REDEMPTION REFERENCE WITH CURRENT REFERENCE
     C*
     C           #1REFN    CHAINSEERRFL0             88
     C*
     C                     MOVE 'D'       VARECI
     C                     Z-ADDBJRDNB    VALCD
     C                     MOVE 'A'       VACHTP
     C                     MOVE ##USR     VALUID
     C*
     C**   IF EX-DATE CHANGED
     C*
     C           #1EXDT    IFNE #2EXDT                     B*2
     C                     MOVE #1EXDT    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    VAEREX
     C*
     C                     MOVE #1RDAT    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    VAERRD
     C                     MOVE #1EXDT    #2EXDT
     C                     END                             E*2
     C*
     C**   IF FINAL ALLOCATION CHANGED
     C*
     C           #1FINA    IFNE #2FINA                     B*2
     C                     MOVE #1FINA    VAFAID
     C                     MOVE #1FINA    #2FINA
     C                     END                             E*2
     C*
     C                     MOVE 'X'       VAERST
     C                     MOVE 'Y'       VACFRI
     C*
     C                     MOVE *BLANKS   VAJOBN
     C                     MOVE *BLANKS   VAUSER
     C                     Z-ADD*ZEROS    VAJNBR
     C*
     C                     UPDATSEERRFD0
     C*
     C**   UPDATE SUBFILE FIELDS
     C*
     C                     MOVE FIELD     #FIELD
     C                     MOVE *BLANKS   #1SEL
     C                     MOVE 'X'       #1STAT
     C*
     C**   COMMITMENT INSTRUCTION
     C*
     C                     COMIT
     C                     END                             E*1
     C*
     C**   IF 'ACTION' IS REVERSE
     C*
     C           #1SEL     IFEQ 'R'                        B*1
     C*
     C**   ACCESS EARLY REDEMPTION REFERENCE WITH CURRENT REFERENCE
     C*
     C           #1REFN    CHAINSEERRFL0             88
     C*
     C                     MOVE 'D'       VARECI
     C                     Z-ADDBJRDNB    VALCD
     C                     MOVE 'A'       VACHTP
     C                     MOVE ##USR     VALUID
     C*
     C**   IF EX-DATE CHANGED
     C*
     C           #1EXDT    IFNE #2EXDT                     B*2
     C                     MOVE #1EXDT    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    VAEREX
     C*
     C                     MOVE #1RDAT    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    VAERRD
     C                     MOVE #1EXDT    #2EXDT
     C                     END                             E*2
     C*
     C*
     C**   IF FINAL ALLOCATION CHANGED
     C*
     C           #1FINA    IFNE #2FINA                     B*2
     C                     MOVE #1FINA    VAFAID
     C                     MOVE #1FINA    #2FINA
     C                     END                             E*2
     C*
     C**   IF STATUS IS NOT COMPLETED ('C'), SET CONFIRMATION REQUIRED IND.
     C**   TO 'Y'
     C*
     C           VAERST    IFNE 'C'
     C                     MOVE 'Y'       VACFRI
     C                     END
     C*
     C                     MOVE 'R'       VAERST
     C*
     C                     MOVE *BLANKS   VAJOBN
     C                     MOVE *BLANKS   VAUSER
     C                     Z-ADD*ZEROS    VAJNBR
     C*
     C                     UPDATSEERRFD0
     C*
     C**   UPDATE SUBFILE FIELDS
     C*
     C                     MOVE FIELD     #FIELD
     C                     MOVE *BLANKS   #1SEL
     C                     MOVE 'R'       #1STAT
     C*
     C           #1REFN    SETLLSEERALL0
     C                     READ SEERALL0                 89
     C*
     C**   DO WHILE END OF FILE NOT REACHED AND SAME REFERENCE NUMBER
     C*
     C           *IN89     DOWEQ'0'                        B*1
     C           #1REFN    ANDEQVCERRF
     C*
     C                     MOVE '*'       VCRECI
     C                     UPDATSEERALD0
     C*
     C**   ACCESS NEXT RECORD
     C*
     C                     READ SEERALL0                 89
     C                     END                             E*1
     C*
     C**   COMMITMENT INSTRUCTION
     C*
     C                     COMIT
     C                     END                             E*1
     C*
     C**   IF 'ACTION' IS 'FULL DETAILS' ('F')
     C*
     C           #1SEL     IFEQ 'F'                        B*1
     C*
     C**   INITIALISE ERROR INDICATORS
     C*
     C                     MOVE '0'       *IN65
     C                     MOVE '0'       *IN66
     C                     MOVE '0'       *IN67
     C*
     C**   PROCESS 'FULL DETAILS' SCREEN
     C*
     C                     EXSR P018
     C*
     C**   IF F3 OR F12 IS PRESSED OR IF ALL FIELDS PROTECTED
     C*
     C           *INKC     IFEQ '1'                        B*2
     C           *INKL     OREQ '1'
     C           *IN48     OREQ '1'
     C           *IN49     ANDEQ'1'
     C*
     C**   ACCESS EARLY REDEMPTION REFERENCE WITH CURRENT REFERENCE
     C*
     C           #1REFN    CHAINSEERRFL0             88
     C*
     C                     MOVE *BLANKS   VAJOBN
     C                     MOVE *BLANKS   VAUSER
     C                     Z-ADD*ZEROS    VAJNBR
     C*
     C                     UPDATSEERRFD0
     C*
     C**   UPDATE SUBFILE FIELDS
     C*
     C                     MOVE FIELD     #FIELD
     C*
     C**   COMMITMENT INSTRUCTION
     C*
     C                     COMIT
     C                     END                             E*2
     C*
     C                     MOVE *BLANKS   #1SEL
     C                     END                             E*1
     C*
     C           TAG3      ENDSR
     ******************************************************************
      /EJECT
     ******************************************************************
     C* P010   : PROCESS 'CONFIRMATION' SCREEN                        *
     ******************************************************************
     C*
     C           P010      BEGSR
     C*
     C                     MOVE *BLANKS   #1CONF
     C*
     C                     MOVE '0'       *IN40
     C                     MOVE '0'       *IN41
     C                     MOVE '0'       *IN42
     C*
     C                     MOVE '0'       *IN44
     C                     MOVE '1'       *IN47
     C*
     C**   DEPENDING OF THE 'ACTION', SET THE CORRECT NARRATIVE
     C**   *** CONFIRM REVERSAL ***
     C*
     C           #1SEL     IFEQ 'R'                        B*1
     C                     MOVE '1'       *IN40
     C                     ELSE                            X*1
     C*
     C**   *** CONFIRM ALLOCATION ***
     C*
     C           #1SEL     IFEQ 'L'                        B*2
     C                     MOVE '1'       *IN41
     C                     ELSE                            X*2
     C*
     C**   *** CONFIRM AUTHORISATION ***
     C*
     C           #1SEL     IFEQ 'X'                        B*3
     C                     MOVE '1'       *IN42
     C                     END                             E*3
     C                     END                             E*2
     C                     END                             E*1
     C*
     C                     MOVEATXT,4     ##CTX1
     C*
     C**   DO WHILE F3 OR F12 NOT PRESSED AND NO ERROR
     C*
     C           *INKC     DOWEQ'0'                        B*1
     C           *INKL     ANDEQ'0'
     C           #1CONF    ANDNE'Y'
     C           #1CONF    ANDNE'N'
     C*
     C**   SETUP SCREEN TIME
     C*
     C                     TIME           ##TME            Screen time.
     C*
     C**   EXECUTE SCREEN FORMAT
     C*
     C                     WRITE#MSGCTL
     C                     WRITE#CMDTXT1
     C                     EXFMTSCREEN
     C*
     C**   CLEAR MESSAGES FROM PROGRAM MESSAGE QUEUE.
     C*
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
     C*
     C**   IF 'CONFIRMATION INDICATOR' IS NOT EQUAL TO 'Y' OR 'N'
     C*
     C           #1CONF    IFNE 'N'                        B*2
     C           #1CONF    ANDNE'Y'
     C                     MOVE '1'       *IN44            Reverse image
     C                     MOVEL'ER65018' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     ELSE                            X*2
     C                     MOVE '0'       *IN44            Reverse image
     C                     END                             E*2
     C                     END                             E*1
     C*
     C                     MOVEATXT,3     ##CTX1
     C                     MOVE '0'       *IN47
     C*
     C                     ENDSR
     ******************************************************************
      /EJECT
     ******************************************************************
     C* P012   : CHECK IF ER COMPLETELY ALLOCATED                     *
     ******************************************************************
     C*
     C           P012      BEGSR
     C*
     C**   RESET RETURN INDICATOR
     C*
     C                     MOVE *BLANKS   ERROR   1
     C*
     C**   ACCESS ER DEPOTS FILE
     C*
     C           #1REFN    SETLLSEERDPL1
     C                     READ SEERDPL1                 89
     C*
     C           #1REFN    IFNE VBERRF                     B*1
     C                     MOVE '1'       *IN89
     C                     END                             E*1
     C*
     C**   DO WHILE END OF FILE NOT REACHED
     C**   AND EARLY REDEMPTIONS DEPOTS RECORD ARE FOUND
     C**   AND RETURN INDICATOR IS BLANK
     C*
     C           *IN89     DOWEQ'0'                        B*1
     C           ERROR     ANDEQ*BLANKS
     C*
     C**   IF 'NOMINAL TO BE REDEEMED' IS NOT EQUAL TO 'NOMINAL ALLOCATED'
     C**      THEN ER NOT COMPLETELY ALLOCATED
     C*
     C           VBNRDM    IFNE VBNALL                     B*2
     C                     MOVE 'N'       ERROR
     C                     END                             E*2
     C*
     C**   ACCESS NEXT RECORD
     C*
     C                     READ SEERDPL1                 89
     C*
     C**   IF CHANGE OF REFERENCE NUMBER AND NOT END OF FILE
     C*
     C           #1REFN    IFNE VBERRF                     B*2
     C           *IN89     ANDEQ'0'
     C                     MOVE '1'       *IN89
     C                     END                             E*2
     C                     END                             E*1
     C*
     C                     ENDSR
     ******************************************************************
      /EJECT
     ******************************************************************
     C* P013   : VALIDATE 'CORRECTION REFERENCE'                      *
     ******************************************************************
     C*
     C           P013      BEGSR
     C*
     C**   SETUP KEY FIELDS
     C*
     C                     MOVE #1SECN    KSECN
     C                     MOVE #1EXDT    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    KEXDT
     C*
     C**   ACCESS ER REFERENCES FILE WITH THE SECURITY SHORTNAME AND EX-DATE
     C*
     C           KEY4      CHAINSEERRFL3             89
     C*
     C**   IF A RECORD IS FOUND
     C*
     C           *IN89     IFEQ '0'                        B*1
     C*
     C**   IF CORRECTION REFERENCE IS BLANK OR IS NOT EQUAL TO ER REF OF RECORD
     C*
     C           #1CORR    IFEQ *BLANKS                    B*2
     C           #1CORR    ORNE VAERRF
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN37            Reverse image
     C                     MOVEL'ER65019' ZAMSID           Message id.
     C                     MOVE *BLANKS   ZAMSDA           Message Data
     C                     MOVELVAERRF    ZAMSDA           Message Data
     C                     EXSR ZASNMS                     Send message
     C                     END                             E*2
     C*
     C**   IF CORRECTION REFERENCE IS NOT BLANK AND ASSOCIATED REDEMPTION
     C**   IS NOT 'AUTHORISED', 'COMPLETED' OR 'REVERSED'
     C*
     C           #1CORR    IFNE *BLANKS                    B*2
     C           VAERST    ANDNE'X'
     C           VAERST    ANDNE'C'
     C           VAERST    ANDNE'R'
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN37            Reverse image
     C                     MOVEL'ER65024' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     END                             E*2
     C*
     C**   IF NO RECORD IS FOUND
     C*
     C                     ELSE                            X*1
     C*
     C**   IF CORRECTION REFERENCE IS NOT BLANK
     C*
     C           #1CORR    IFNE *BLANKS                    B*2
     C                     MOVE 'Y'       ERMSG
     C                     MOVE '1'       *IN37            Reverse image
     C                     MOVEL'ER65020' ZAMSID           Message id.
     C                     MOVE *BLANKS   ZAMSDA           Message Data
     C                     MOVEL#1CORR    ZAMSDA           Message Data
     C                     EXSR ZASNMS                     Send message
     C                     END                             E*2
     C                     END                             E*1
     C*
     C                     ENDSR
     ******************************************************************
      /EJECT
     ******************************************************************
     C* P014   : REINITIALISE INDICATORS                              *
     ******************************************************************
     C*
     C           P014      BEGSR
     C*
     C**   REINITIALISE INDICATORS
     C*
     C                     MOVE '0'       *IN32            Error on Ex-Date
     C                     MOVE '0'       *IN33            Error on Redemp Date
     C                     MOVE '0'       *IN34            Error on Last Allocat
     C                     MOVE '0'       *IN35            Error on Final Alloc
     C                     MOVE '0'       *IN36            Error on Status
     C                     MOVE '0'       *IN37            Error on Correct Ref
     C                     MOVE '0'       *IN38            Error on Reference
     C                     MOVE '0'       *IN39            Error on Shortname
     C                     MOVE '0'       *IN62            Error on Action
     C*
     C                     ENDSR
     ******************************************************************
      /EJECT
     ******************************************************************
     C* P015   : CLEAR SFL FIELDS                                     *
     ******************************************************************
     C*
     C           P015      BEGSR
     C*
     C**   INITIALISE SUBFILE RECORD
     C*
     C                     MOVE *BLANKS   #1SEL
     C                     MOVE *BLANKS   #1REFN
     C                     MOVE *BLANKS   #1SECN
     C                     MOVE *BLANKS   #1EXDT
     C                     MOVE *BLANKS   #1RDAT
     C                     MOVE *BLANKS   #1LASA
     C                     MOVE *BLANKS   #1FINA
     C                     MOVE *BLANKS   #1STAT
     C                     MOVE *BLANKS   #1CORR
     C                     MOVE *BLANKS   #1SRNM                          CSE016
     C*
     C**   INITIALISE HIDDEN SUBFILE FIELDS
     C*
     C                     MOVE *BLANKS   #FIELD
     C                     MOVE *BLANKS   #2SEL
     C                     MOVE *BLANKS   #2EXDT
     C                     MOVE *BLANKS   #2FINA
     C*
     C                     ENDSR
      ********************************************************************
      /EJECT
     ******************************************************************
     C* P016  : LOAD SFL FIELDS                                       *
     ******************************************************************
     C*
     C           P016      BEGSR
     C*
     C**   INITIALISE SUBFILE RECORD
     C*
     C                     MOVE *BLANKS   #1SEL
     C                     MOVE VAERRF    #1REFN
     C                     MOVE VASESN    #1SECN
     C*
     C                     Z-ADDVAEREX    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     #1EXDT
     C*
     C                     Z-ADDVAERRD    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     #1RDAT
     C*
     C           VAERAD    IFNE *ZEROS
     C                     Z-ADDVAERAD    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     #1LASA
     C                     ELSE
     C                     MOVE *BLANKS   #1LASA
     C                     END
     C*
     C                     MOVE VAFAID    #1FINA
     C                     MOVE VAERST    #1STAT
     C                     MOVE VACORF    #1CORR
      *                                                                   CSE016
     C                     MOVE *BLANKS   #1SRNM                          CSE016
      *                                                                   CSE016
      ** Retrieve security description                                    CSE016
      *                                                                   CSE016
     C           #1SECN    CHAINSECTY                89                   CSE016
     C           *IN89     IFEQ *OFF                                      CSE016
      *                                                                   CSE016
      ** Format security report description                               CSE016
      *                                                                   CSE016
     C           #1SECN    IFNE *BLANKS                                   CSE016
      *                                                                   CSE016
     C                     MOVE BVRIDF    ZRSFD                           CSE016
     C                     MOVELSRPN      ZSRPN                           CSE016
     C                     EXSR ZSDESC                                    CSE016
     C                     MOVELZRNME     #1SRNM                          CSE016
      *                                                                   CSE016
     C                     ENDIF                                          CSE016
      *                                                                   CSE016
     C                     ENDIF                                          CSE016
     C*
     C**   INITIALISE HIDDEN SUBFILE FIELDS
     C*
     C                     MOVE VAJOBN    WAJOBN 10
     C                     MOVE VAUSER    WAUSER 10
     C                     Z-ADDVAJNBR    WAJNBR  60
     C                     MOVE *BLANKS   VAJOBN
     C                     MOVE *BLANKS   VAUSER
     C                     Z-ADD*ZEROS    VAJNBR
     C                     MOVE FIELD     #FIELD
     C                     MOVE WAJOBN    VAJOBN
     C                     MOVE WAUSER    VAUSER
     C                     Z-ADDWAJNBR    VAJNBR
     C                     MOVE *BLANKS   #2SEL
     C                     MOVE #1EXDT    #2EXDT
     C                     MOVE #1FINA    #2FINA
     C*
     C                     ENDSR
      ********************************************************************
      /EJECT
     ******************************************************************
     C* P017  : REINITIALISE POSITION/SELECTION FIELDS                *
     ******************************************************************
     C*
     C           P017      BEGSR
     C*
     C                     MOVE *BLANKS   #REFN
     C                     MOVE *BLANKS   #SECN
     C                     MOVE *BLANKS   #EXDT
     C                     MOVE *BLANKS   #STAT
     C*
     C                     MOVE *BLANKS   #3REFN
     C                     MOVE *BLANKS   #3SECN
     C                     MOVE *BLANKS   #3EXDT
     C                     MOVE *BLANKS   #3STAT
     C*
     C                     MOVE '0'       *IN50
     C                     MOVE '0'       *IN51
     C                     MOVE '0'       *IN52
     C                     MOVE '0'       *IN60
     C*
     C                     ENDSR
     *****************************************************************
      /EJECT
     ******************************************************************
     C* P018  : PROCESS 'FULL DETAILS' SCREEN                         *
     ******************************************************************
     C*
     C           P018      BEGSR
     C*
     C**   SETON INDICATORS TO PROTECT ALL INPUT CAPABLE FIELDS
     C*
     C                     MOVE '1'       *IN48
     C                     MOVE '1'       *IN49
     C*
     C**   IF STATUS IS 'INCOMPLETED' OR 'ALLOCATED' AND IF UPDATE MODE
     C**   IS RUNNING, NON PROTECT THE FIELDS
     C*
     C           #1STAT    IFEQ 'I'                        B*1
     C           *IN43     ANDEQ'1'
     C           #1STAT    OREQ 'L'
     C           *IN43     ANDEQ'1'
     C                     MOVE '0'       *IN48
     C                     MOVE '0'       *IN49
     C                     END                             E*1
     C*
     C**   IF STATUS IS 'AUTHORISED' AND IF UPDATE MODE IS RUNNING,
     C**   NON PROTECT THE FIELD 'PAYMENT DATE'
     C*
     C           #1STAT    IFEQ 'X'                        B*1
     C           *IN43     ANDEQ'1'
     C                     MOVE '0'       *IN49
     C                     END                             E*1
     C*
     C**   SETUP THE DISPLAY FIELDS
     C*
     C                     Z-ADDVAERPD    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     #1ERPD
     C*
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE VAREDP    ZFIELD
     C                     Z-ADD7         ZADEC
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    #1REDP
     C*
     C                     MOVE VARNAR    #1RNAR
     C*
     C                     MOVE '1'       *IN47
     C                     MOVEATXT,4     ##CTX1
     C                     MOVE 'Y'       ERROR
     C                     MOVE '1'       *IN84
     C*
     C           #1SECN    CHAINSECTY                88
     C                     MOVE SFN1      #1SFN1
     C                     MOVE SFN2      #1SFN2
     C*
     C**   DO WHILE F12 OR F3 IS NOT PRESSED AND NO ERRO
     C*
     C           *INKC     DOWEQ'0'                        B*1
     C           *INKL     ANDEQ'0'
     C           ERROR     ANDEQ'Y'
     C*
     C**   SETUP SCREEN TIME
     C*
     C                     TIME           ##TME            Screen time.
     C*
     C**   EXECUTE SCREEN FORMAT
     C*
     C                     WRITE#MSGCTL
     C                     WRITE#CMDTXT1
     C                     EXFMTDETAILS
     C*
     C**   CLEAR MESSAGES FROM PROGRAM MESSAGE QUEUE.
     C*
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
     C*
     C                     MOVE 'N'       ERROR
     C*
     C**   IF ALL INPUT FIELDS ARE NOT PROTECTED
     C*
     C           *IN48     IFEQ '0'                        B*2
     C           *IN49     OREQ '0'
     C*
     C**   VALIDATE DETAILS AND UPDATE FILE IF NO ERROR
     C*
     C                     EXSR P019
     C                     END                             E*2
     C                     END                             E*1
     C*
     C**   SETUP CORRECTLY THE COMMAND KEYS AVAILABLE
     C*
     C           *IN61     IFEQ '1'                        B*1
     C                     MOVEATXT,1     ##CTX1
     C                     ELSE                            X*1
     C                     MOVEATXT,3     ##CTX1
     C                     END                             E*1
     C*
     C                     MOVE '0'       *IN47
     C*
     C**   CLEAR MESSAGES FROM PROGRAM MESSAGE QUEUE.
     C*
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
     C*
     C                     ENDSR
     *****************************************************************
      /EJECT
     ******************************************************************
     C* P019  : VALIDATE 'FULL DETAILS' SCREEN AND UPDATE FILE        *
     ******************************************************************
     C*
     C           P019      BEGSR
     C*
     C**   INITIALISE ERROR INDICATORS
     C*
     C                     MOVE '0'       *IN65
     C                     MOVE '0'       *IN66
     C                     MOVE '0'       *IN67
     C*
     C**   IF 'PAYMENT DATE' IS NOT PROTECTED
     C*
     C           *IN49     IFEQ '0'                        B*1
     C*
     C**   IF 'PAYMENT DATE' IS BLANK
     C*
     C           #1ERPD    IFEQ *BLANKS                    B*2
     C                     MOVE 'Y'       ERROR
     C                     MOVE '1'       *IN65            Reverse image
     C                     MOVEL'ER65027' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     ELSE                            X*2
     C*
     C                     MOVE #1ERPD    ZDATE
     C                     EXSR ZDATE1
     C*
     C**   IF 'PAYMENT DATE IS NOT A VALID DATE FORMAT
     C*
     C           *IN99     IFEQ '1'                        B*3
     C                     MOVE 'Y'       ERROR
     C                     MOVE '1'       *IN65            Reverse image
     C                     MOVEL'ER65028' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     ELSE                            X*3
     C                     Z-ADDZDAYNO    WPDAT   50
     C                     MOVE #1RDAT    ZDATE
     C                     EXSR ZDATE1
     C*
     C**   IF 'PAYMENT DATE' IS NOT GREATER OR EQUAL THAN THE 'REDEMPTION DATE'
     C*
     C           WPDAT     IFLT ZDAYNO                     B*4
     C                     MOVE 'Y'       ERROR
     C                     MOVE '1'       *IN65            Reverse image
     C                     MOVEL'ER65029' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     END                             E*4
     C                     END                             E*3
     C                     END                             E*2
     C                     END                             E*1
     C*
     C**   IF 'REDEMPTION PRICE' IS NOT PROTECTED
     C*
     C           *IN48     IFEQ '0'                        B*1
     C*
     C**   IF 'REDEMPTION PRICE' IS BLANK OR EQUAL TO ZERO
     C*
     C           #1REDP    IFEQ *BLANKS                    B*2
     C                     MOVE 'Y'       ERROR
     C                     MOVE '1'       *IN66            Reverse image
     C                     MOVEL'ER65030' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     ELSE                            X*2
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVEL#1REDP    ZFIELD
     C                     Z-ADD4         ZADIG
     C                     Z-ADD7         ZADEC
     C                     EXSR ZALIGN
     C*
     C**   IF 'REDEMPTION PRICE' IS NOT A VALID NUMERIC FIELD WITH MAXIMUM
     C**   7 DECIMAL PLACES
     C*
     C           *IN99     IFEQ '1'                        B*3
     C                     MOVE 'Y'       ERROR
     C                     MOVE '1'       *IN66            Reverse image
     C                     MOVEL'ER65031' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     ELSE                            X*3
     C*
     C                     Z-ADD*ZEROS    NFIELD 150
     C                     MOVE ZFIELD    NFIELD
     C           NFIELD    IFEQ *ZEROS                     B*4
     C                     MOVE 'Y'       ERROR
     C                     MOVE '1'       *IN66            Reverse image
     C                     MOVEL'ER65030' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     END                             E*4
     C*
     C                     MOVE *BLANKS   WFIELD 15
     C                     MOVE ZFIELD    WFIELD
     C                     Z-ADD7         ZADEC
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    #1REDP
     C                     END                             E*3
     C                     END                             E*2
     C                     END                             E*1
     C*
     C**   IF 'REDEMPTION NARRATIVE' IS NOT PROTECTED
     C*
     C           *IN48     IFEQ '0'                        B*1
     C*
     C**   IF 'REDEMPTION NARRATIVE' IS BLANK
     C*
     C           #1RNAR    IFEQ *BLANKS                    B*2
     C                     MOVE 'Y'       ERROR
     C                     MOVE '1'       *IN67            Reverse image
     C                     MOVEL'ER65032' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     END                             E*2
     C                     END                             E*1
     C*
     C**   IF NO ERROR DETECTED
     C*
     C           ERROR     IFEQ 'N'                        B*1
     C*
     C**   ACCESS TO ER REFERENCE FILE WITH CURRENT REFERENCE
     C*
     C           #1REFN    CHAINSEERRFL0             88
     C*
     C                     MOVE *BLANKS   VAJOBN
     C                     MOVE *BLANKS   VAUSER
     C                     Z-ADD*ZEROS    VAJNBR
     C*
     C**   IF PAYMENT DATE IS NOT PROTECTED
     C*
     C           *IN49     IFEQ '0'                        B*2
     C                     Z-ADDWPDAT     VAERPD
     C                     END                             E*2
     C*
     C**   IF REDEMPTION PRICE AND NARRATIVE ARE NOT PROTECTED
     C*
     C           *IN48     IFEQ '0'                        B*2
     C                     MOVE WFIELD    VAREDP
     C                     MOVE #1RNAR    VARNAR
     C                     END                             E*2
     C*
     C                     UPDATSEERRFD0
     C                     MOVE FIELD     #FIELD
     C*
     C**   ADD COMMITMENT INSTRUCTION
     C*
     C                     COMIT
     C*
     C                     END                             E*1
     C*
     C                     ENDSR
     ******************************************************************
      /EJECT
     ******************************************************************
     C* P020  : SET DEFAULTS OF 'FULL DETAIL' FIELDS                  *
     ******************************************************************
     C*
     C           P020      BEGSR
     C*
     C**   THE DEFAULT 'PAYMENT DATE' IS THE FIRST WORKING DAY BEGINNING AT
     C**   'REDEMPTION DATE'. SO CONVERT 'REDEMPTION DATE' IN THE MIDAS FORMAT.
     C*
     C                     MOVE #1RDAT    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    WCOUNT  50
     C*
     C**   INITIALISE RETURN FIELD
     C*
     C***********          MOVE 'H'       @@HIND                          S01496
     C                     MOVE 'H'       ZIND                            S01496
     C*
     C**   DO WHILE RETURN FIELD EQUAL TO 'H'
     C*
     C***********@@HIND    DOWEQ'H'                        B*1            S01496
     C***********                                                         S01496
     C***********          MOVE *BLANKS   @@HIND                          S01496
     C***********          Z-ADDWCOUNT    @@MNO   50                      S01496
     C***********          MOVE KNMCY     @@CCY   3                       S01496
     C***********          EXSR ZA0830                                    S01496
     C*                                                                   S01496
     C           ZIND      DOWEQ'H'                        B*1            S01496
     C*                                                                   S01496
     C                     MOVE *BLANKS   ZIND                            S01496
     C                     Z-ADDWCOUNT    ZDAYNO  50                      S01496
     C                     MOVE KNMCY     ZCCY    3                       S01496
     C                     MOVE A8LCCD    ZLOC    3                       S01496
     C                     EXSR ZCHKH                                     S01496
     C*
     C**   IF IT'S A HOLIDAY FOR THE CURRENCY
     C*
     C***********@@HIND    IFEQ 'H'                        B*2            S01496
     C           ZIND      IFEQ 'H'                        B*2            S01496
     C                     ADD  1         WCOUNT
     C                     END                             E*2
     C                     END                             E*1
     C*
     C                     Z-ADDWCOUNT    VAERPD
     C*
     C**   SETUP THE REDEMPTION NARRATIVE
     C*
     C                     MOVEANARR,1    WNARR  33
     C                     MOVE #1REFN    WNARR
     C                     MOVELWNARR     VARNAR
     C*
     C**   SETUP THE REDEMPTION PRICE
     C*
     C                     MOVE 'RC'      KTYPE
     C                     MOVE #1SECN    KSECN
     C                     Z-ADDZDAYNO    KEXDT
     C*
     C**   ACCESS TO THE SECURITY DIARY EVENTS FILE
     C*
     C           KEY5      CHAINSEDEV                88
     C*
     C**   IF A RECORD IS FOUND
     C*
     C           *IN88     IFEQ '0'                        B*1
     C                     Z-ADDSDRP      VAREDP
     C                     ELSE                            X*1
     C*
     C**   IF PRICE BASIS IS EQUAL TO 'P'
     C*
     C           SPBS      IFEQ 'P'                        B*2
     C                     Z-ADD100       VAREDP
     C                     ELSE                            X*2
     C                     Z-ADD1         VAREDP
     C                     END                             E*2
     C                     END                             E*1
     C*
     C                     ENDSR
     *****************************************************************
      /EJECT
     ******************************************************************
     C* R001 : POSITION INTO ER REFERENCE FILE                        *
     ******************************************************************
     C*
     C           R001      BEGSR
     C*
     C**   FIRST TIME PROCESSING
     C*
     C           *IN45     IFEQ '0'                        B*1
     C           *LOVAL    SETLLSEERRFL1
     C                     END                             E*1
     C*
     C**   ACCESS MUST BE DONE BY 'REFERENCE'
     C*
     C           *IN55     IFEQ '1'                        B*1
     C*
     C**   IF ROLLUP
     C*
     C           *IN27     IFEQ '1'                        B*2
     C           KREFN     SETGTSEERRFL1
     C                     ELSE                            X*2
     C           KREFN     SETLLSEERRFL1
     C                     END                             E*2
     C*
     C                     ELSE                            X*1
     C*
     C**   ACCESS MUST BE DONE BY 'SECURITY' AND 'EX-DATE'
     C*
     C           *IN53     IFEQ '1'                        B*2
     C*
     C**   IF ROLLUP
     C*
     C           *IN27     IFEQ '1'                        B*3
     C           KEY1      SETGTSEERRFL3
     C                     ELSE                            X*3
     C           KEY1      SETLLSEERRFL3
     C                     END                             E*3
     C*
     C                     ELSE                            X*2
     C*
     C**   ACCESS MUST BE DONE BY 'EX-DATE' AND 'SECURITY'
     C*
     C           *IN54     IFEQ '1'                        B*3
     C*
     C**   IF ROLLUP
     C*
     C           *IN27     IFEQ '1'                        B*4
     C           KEY2      SETGTSEERRFL4
     C                     ELSE                            X*4
     C           KEY2      SETLLSEERRFL4
     C                     END                             E*4
     C*
     C                     ELSE                            X*3
     C*
     C**   IF ROLLUP IN AMEND/ENQUIRY MODE
     C*
     C           *IN27     IFEQ '1'                        B*4
     C*
     C           *IN43     IFEQ '1'                        B*5
     C           *IN61     OREQ '1'
     C           KREFN     SETGTSEERRFL1
     C                     END                             E*5
     C                     END                             E*4
     C                     END                             E*3
     C                     END                             E*2
     C                     END                             E*1
     C*
     C                     ENDSR
     *****************************************************************
      /EJECT
     ******************************************************************
     C* R002 : READ NEXT RECORD OF THE ER REFERENCE FILE              *
     ******************************************************************
     C*
     C           R002      BEGSR
     C*
     C**   ACCESS MUST BE DONE BY 'REFERENCE'
     C*
     C           *IN55     IFEQ '1'                        B*1
     C           *IN45     OREQ '0'
     C                     READ SEERRFL1                 30
     C                     ELSE                            X*1
     C*
     C**   ACCESS MUST BE DONE BY 'SECURITY' AND 'EX-DATE' AND 'REFERENCE'
     C*
     C           *IN53     IFEQ '1'                        B*2
     C                     READ SEERRFL3                 30
     C                     ELSE                            X*2
     C*
     C**   ACCESS MUST BE DONE BY 'EX-DATE' AND 'SECURITY' AND 'REFERENCE'
     C*
     C           *IN54     IFEQ '1'                        B*3
     C                     READ SEERRFL4                 30
     C                     ELSE                            X*3
     C*
     C**   IF ROLLUP IN AMEND/ENQUIRY MODE
     C*
     C           *IN27     IFEQ '1'                        B*4
     C*
     C           *IN43     IFEQ '1'                        B*5
     C           *IN61     OREQ '1'
     C                     READ SEERRFL1                 30
     C                     END                             E*5
     C                     END                             E*4
     C                     END                             E*3
     C                     END                             E*2
     C                     END                             E*1
     C*
     C                     ENDSR
     *****************************************************************
      /EJECT
     ******************************************************************
     C* *PSSR - TO PROCESS ABNORMAL END OF PROGRAM                    *
     ******************************************************************
     C           *PSSR     BEGSR
     C*
     C** UPDATE LDA WITH ERROR INFORMATION
     C*
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVELDBFILE    WWFILE
     C                     MOVELDBKEY     WWKEY
     C                     MOVELDBPGM     WWPGM
     C                     MOVELDBASE     WWASE
     C                     OUT  LDA
     C*
     C**   ROLLBACK CHANGES, PRINT DUMP AND CANCEL PROGRAM
     C*
     C                     ROLBK
     C                     DUMP
     C*
     C**   TRY TO RELEASE EARLY REDEMPTION BY ACCESS TO ER REFERENCE FILE
     C**   IF CURRENT ER REFERENCE IS NOT BLANK
     C*
     C           #1REFN    IFNE *BLANKS                    B*1
     C           #1REFN    SETLLSEERRFL0
     C                     READ SEERRFL0                 89
     C*
     C**   IF THE RECORD EXISTS
     C*
     C           *IN89     IFEQ '0'                        B*2
     C*
     C                     MOVE *BLANKS   VAJOBN
     C                     MOVE *BLANKS   VAUSER
     C                     Z-ADD*ZEROS    VAJNBR
     C*
     C**   UPDATE THE RECORD
     C*
     C                     UPDATSEERRFD0
     C                     MOVE FIELD     #FIELD
     C*
     C**   COMMITMENT INSTRUCTION
     C*
     C                     COMIT
     C                     END                             E*2
     C                     END                             E*1
     C*
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     CALL 'DBERRCTL'                                S01496
     C                     RETRN
     C*
     C                     ENDSR
     ******************************************************************
      /EJECT
     *****************************************************************
     C*  ZASNMS : SEND MESSAGE TO PROGRAM'S MESSAGE QUEUE            *
     *****************************************************************
     C*
     C           ZASNMS    BEGSR
     C*
     C**   message file specified use PMSGF
     C*
     C                     MOVELPMSGF     ZAMSGF
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGM  10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message Id.
     C                     PARM           ZAMSGF 10        Message file.
     C                     PARM           ZAMSDA132        Message data.
     C                     PARM           ZAMSTP  7        Message type.
     C*
     C**   Clear all fields for default mechanism next time.
     C*
     C                     MOVEL*BLANK    ZAPGM            Program queue
     C                     MOVEL*BLANK    ZAPGRL           Rel queue
     C                     MOVEL*BLANK    ZAMSDA           Message data.
     C                     MOVEL*BLANK    ZAMSTP           Message type.
     C                     MOVEL*BLANK    ZAMSGF           Message file
     C                     MOVEL*BLANK    ZAMSID           Message id.
     C*
     C                     ENDSR
     C********************************************************************
      /EJECT
     C********************************************************************
     C**
     C**   ZDATE1 SR. TO VALIDATE DATES SUBMITTED AND
     C**              CONVERT TO A NUMBER OF DAYS.
     C**
     C**   THE YEAR 2000, BEING DIVISIBLE BY 400, IS A LEAP YEAR.
     C**
     C           ZDATE1    BEGSR                           *** ZDATE1 ***
     C**
     C**   CLEAR DAY NUMBER. RESET ERROR INDICATOR
     C                     Z-ADD0         ZDAYNO  50
     C                     SETOF                     99
     C**
     C**   CALCULATION TO DEFINE INPUT DATE FIELD.
     C                     Z-ADDZDATE     ZDATE   60
     C**
     C**   GET INDIVIDUAL DAY, MONTH AND YEAR FIELDS.
     C                     MOVE ZDATE     ZYEAR   20
     C  N98                MOVELZDATE     ZDAY    20
     C   98                MOVELZDATE     ZMTH    20
     C                     MOVE ZDATE     ZWRK4   40
     C  N98                MOVELZWRK4     ZMTH
     C   98                MOVELZWRK4     ZDAY
     C**
     C**   ENSURE MONTH IS VALID. BYPASS IF ERROR
     C           ZMTH      IFLE 0
     C           ZMTH      ORGT 12
     C                     SETON                     99
     C                     GOTO ZDEND1
     C                     END
     C**
     C**   ENSURE DAY IS VALID. BYPASS IF ERROR
     C           ZDAY      IFLE 0
     C                     SETON                     99
     C                     GOTO ZDEND1
     C                     END
     C**
     C**   CHECK FOR 30 DAY MONTHS. BYPASS IF ERROR
     C           ZMTH      IFEQ 4
     C           ZMTH      OREQ 6
     C           ZMTH      OREQ 9
     C           ZMTH      OREQ 11
     C**
     C           ZDAY      IFGT 30
     C                     SETON                     99
     C                     GOTO ZDEND1
     C                     END
     C**
     C                     ELSE
     C**
     C**   CHECK FOR 31 DAY MONTHS (ALL OTHERS BUT FEB). BYPASS IF ERROR
     C           ZDAY      IFGT 31
     C           ZMTH      ANDNE2
     C                     SETON                     99
     C                     GOTO ZDEND1
     C                     END
     C**
     C                     END
     C**
     C**   CHECK FOR LEAP YEAR: REMAINDER WILL BE ZERO IF IT IS A LEAP YR
     C           ZYEAR     ADD  28        ZYEAR
     C           ZYEAR     DIV  4         ZLYR    20
     C                     MVR            ZLY     10
     C**
     C**   IF FEBUARY FURTHER VALIDATE DAY.
     C           ZMTH      IFEQ 2
     C**
     C**   INVALID IF GREATER THAN 28 AND NOT A LEAP YEAR
     C           ZLY       IFGT 0
     C           ZDAY      ANDGT28
     C                     SETON                     99
     C                     GOTO ZDEND1                     BYPASS IF ERROR
     C                     END
     C**
     C**   INVALID IF GREATER THAN 29 AND A LEAP YEAR - BYPASS IF ERROR
     C           ZLY       IFEQ 0
     C           ZDAY      ANDGT29
     C                     SETON                     99
     C                     GOTO ZDEND1
     C                     END
     C**
     C                     END
     C**
     C**   DETERMINE NUMBER OF DAYS FROM 31/12/1971.
     C**
     C**   MULTIPLY NO. OF FOUR-YEAR SPANS, BY NO. OF DAYS IN SPAN
     C           ZLYR      MULT 1461      ZDAYNO
     C**
     C**   IF NOT A LEAP YEAR, ADD APPROPRIATE NO. OF DAYS FOR EXTRA
     C**   YEARS USING REMAINDER FIELD (IE. 1, 2 OR 3 X 356)
     C           ZLY       IFGT 0
     C           ZDAYNO    ADD  ZYDY,ZLY  ZDAYNO
     C                     END
     C**
     C**   IF IT IS A LEAP YEAR, AND LATER THAN FEBRUARY, ADD ONE FOR
     C**   29TH OF FEB
     C           ZLY       IFEQ 0
     C           ZMTH      ANDGT2
     C           ZDAYNO    ADD  1         ZDAYNO
     C                     END
     C**
     C**   ADD APPROPRIATE NUMBER OF DAYS FOR THE MONTH NUMBER
     C           ZDAYNO    ADD  ZMDY,ZMTH ZDAYNO
     C**
     C**   ADD NUMBER OF DAYS IN MONTH
     C           ZDAYNO    ADD  ZDAY      ZDAYNO
     C**
     C           ZDEND1    ENDSR                           * ZDEND1 ENDSR*
     C**
     C********************************************************************
     C**
     C**   ZDATE2 SR. TO CONVERT A DAY NUMBER TO DATE FORMATS.
     C**
     C**   THE YEAR 2000, BEING DIVISIBLE BY 400, IS A LEAP YEAR.
     C**
     C           ZDATE2    BEGSR                           *** ZDATE2 ***
     C**
     C**   CLEAR LEAP YEAR WORK FIELD.
     C                     MOVE 'N'       ZLEAP   1
     C**
     C**   CLEAR DATE FIELDS.
     C                     Z-ADD0         ZDATE   60
     C                     MOVEL'       ' ZADATE  7
     C**
     C**   CALCULATION TO DEFINE INPUT DAY NUMBER.
     C                     Z-ADDZDAYNO    ZDAYNO  50
     C**
     C**   DETERMINE YEAR NUMBER.
     C**
     C**   ADJUST DAY NUMBER IN CASE LAST DAY OF YEAR.
     C           ZDAYNO    SUB  1         ZDAYN1  50
     C           ZDAYN1    IFLT 0
     C                     GOTO ZDEND2
     C                     END
     C**
     C**   CALCULATE NUMBER OF 4 YEAR SPANS SINCE 31/12/1971.
     C           ZDAYN1    DIV  1461      ZLYR    20
     C                     MVR            ZDAYN1           SAVE REMAINING
     C**                                                   DAYS
     C**   CALCULATE NUMBER OF REMAINING YEARS.
     C                     Z-ADD1         ZWRK2   20
     C           ZDTAG1    TAG                             ** ZDTAG1 TAG *
     C           ZDAYN1    IFGE ZYDY,ZWRK2
     C           ZWRK2     ADD  1         ZWRK2
     C                     GOTO ZDTAG1
     C                     END
     C*
     C           ZWRK2     SUB  1         ZWRK2
     C**
     C**   DECREMENT DAY NO. BY DAYS IN REMAINING YEARS.
     C           ZWRK2     IFNE 0
     C           ZDAYN1    SUB  ZYDY,ZWRK2ZDAYN1
     C                     END
     C**
     C**   CALCULATE ACTUAL YEAR NUMBER.
     C           ZLYR      MULT 4         ZWRK3   30
     C           ZWRK3     ADD  72        ZWRK3            BASE IS 1972
     C                     Z-ADDZWRK3     ZYEAR   20
     C           ZYEAR     ADD  ZWRK2     ZYEAR            YEAR
     C**
     C**   DETERMINE MONTH NUMBER.
     C**
     C**   ADJUST DAY NO. IN CASE LAST DAY OF LEAP YEAR FEBRUARY.
     C           ZWRK2     IFEQ 0
     C           ZDAYN1    IFEQ 59
     C                     MOVE 'Y'       ZLEAP
     C                     END
     C           ZDAYN1    IFGE 59
     C           ZDAYN1    SUB  1         ZDAYN1
     C                     END
     C                     END
     C**
     C**   CALCULATE MONTH NUMBER.
     C                     Z-ADD2         ZWRK2
     C           ZDTAG2    TAG                             ** ZDTAG2 TAG *
     C           ZDAYN1    IFGE ZMDY,ZWRK2
     C           ZWRK2     ADD  1         ZWRK2
     C                     GOTO ZDTAG2
     C                     END
     C*
     C           ZWRK2     SUB  1         ZWRK2
     C**
     C                     Z-ADDZWRK2     ZMTH    20       MONTH
     C**
     C**   DETERMINE DAY OF MONTH.
     C**
     C**   ADD BACK LAST DAY OF YEAR ADJUSTMENT.
     C           ZDAYN1    ADD  1         ZDAYN1
     C**
     C**   CALCULATE DAY OF MONTH.
     C           ZDAYN1    SUB  ZMDY,ZWRK2ZDAY    20       DAY
     C**
     C**   ADD BACK LEAP YEAR 29TH FEBUARY ADJUSTMENT, IF REQUIRED.
     C           ZLEAP     IFEQ 'Y'
     C           ZDAY      ADD  1         ZDAY
     C                     END
     C**
     C**   FORMAT DATE, DDMMYY OR MMDDYY.
     C  N98                MOVELZDAY      ZWRK4   40
     C   98                MOVE ZDAY      ZWRK4
     C  N98                MOVE ZMTH      ZWRK4
     C   98                MOVELZMTH      ZWRK4
     C                     MOVELZWRK4     ZDATE
     C                     MOVE ZYEAR     ZDATE
     C**
     C**   FORMAT ALPHA DATE, DDMMMYY.
     C                     MOVELZDAY      ZWRK5   5
     C           ZDAY      IFLT 10
     C                     MOVEL' '       ZWRK5
     C                     END
     C                     MOVE ZMNM,ZMTH ZWRK5
     C                     MOVELZWRK5     ZADATE
     C                     MOVE ZYEAR     ZADATE
     C**
     C           ZDEND2    ENDSR                           * ZDEND2 ENDSR*
     C**
     C**
     C********************************************************************
      /EJECT
     C********************************************************************
     C*    SUBROUTINE- ZNCD
     C*    DETERMINE NEXT COUPON DATE
     C*
     C**
     C**  INPUT - CD ARRAY - COUPON DATES - SECTYD SECURITY FILE
     C**          CR ARRAY - RATE/PAYT INDICS - SECTYD SECURITY FILE
     C**          FCPN - FIRST COUPON DATE - SECTYD SECURITY FILE
     C**          MATY - MATURITY DATE - SECTYD SECURITY FILE
     C**          ECD - EVENT CONTROL DTAE - ZEVCD PROCESS
     C**          *IN98 - DATE FORMAT - FROM TABTC10 RECORD VIA ZSYSTM
     C**
     C**  OUTPUT - NCD (5,0) - NEXT COUPON DATE
     C**
     C**  CALLS  - ZDATE1
     C**           ZDATE2
     C********************************************************************
     C*
     C           ZNCD      BEGSR                           *** ZNCD   ***
     C*
     C                     EXSR ZDATE2
     C                     MOVELZDATE     ZZECD4  40
     C           *IN98     IFEQ '0'
     C                     MOVELZZECD4    ZZWRKD
     C                     MOVE ZZECD4    ZZWRKM
     C                     Z-ADDZZWRKD    ZZECD4
     C                     MOVELZZWRKM    ZZECD4
     C                     END
      *
      * IF DATE FORMAT IS DDMMYY TURN ROUND ARRAY OF COUPON DATES TO MMDD
     C           *IN98     IFEQ '0'
     C                     DO   12        X       20
     C                     MOVE CD,X      ZZWRKM  20
     C                     MOVELCD,X      ZZWRKD  20
     C                     Z-ADDZZWRKD    CD,X
     C                     MOVELZZWRKM    CD,X
     C                     END
     C                     END
      * GET YEAR FOR ECD
     C                     MOVE ZDATE     ZECDYR  20
      *
      * FIND NEXT COUPON DATE FROM ARRAYS -
      *  IF RATE/PAYT INDS ALL BLANK NCD = MATURITY DATE
     C                     Z-ADDMATY      NCD     50
     C           CRDS      IFNE *BLANKS
      *  IF RATE/PAYT INDS NOT ALL BLANK -
      *    SEARCH FOR NEXT HIGHEST COUPON
     C                     MOVE '0'       ZFOUND  1
     C                     DO   12        X
     C           CR,X      IFEQ 'P'
     C           CR,X      OREQ 'C'
     C           ZZECD4    IFLE CD,X
     C                     MOVE '1'       ZFOUND
     C                     GOTO ZNCD00
     C                     END
     C                     END
     C                     END
      *      FOUND - ADD ECD'S YY TO FORM DATE
     C           ZNCD00    TAG                             ZNCD00 TAG
     C           ZFOUND    IFEQ '1'
     C                     Z-ADDZECDYR    ZDATE
     C                     MOVELCD,X      ZDATE
      *      NOT FOUND - USE FIRST COUPON
      *                  ADD NEXT YEAR'S YY TO FORM DATE
     C                     ELSE
     C                     DO   12        X
     C           CR,X      IFEQ 'P'
     C           CR,X      OREQ 'C'
     C                     Z-ADDZECDYR    ZDATE
     C                     ADD  1         ZDATE
     C                     MOVELCD,X      ZDATE
     C                     MOVE '1'       ZFOUND
     C                     GOTO ZNCD01
     C                     END
     C                     END
     C                     END
     C           ZNCD01    TAG                             ZNCD01 TAG
      *
      * CONVERT DATE TO DAY NO USING ZDATE1
      * (NOT DONE IF NO RATE/PAYT IND IS P OR C)
      * IF DDMMYY FORMAT TURN ROUND TO MMDDYY
     C           ZFOUND    IFEQ '1'
     C           *IN98     IFEQ '0'
     C                     MOVELZDATE     ZZWRK   40
     C                     MOVE ZZWRK     ZZWRKM
     C                     MOVELZZWRK     ZZWRKD
     C                     Z-ADDZZWRKD    ZZWRK
     C                     MOVELZZWRKM    ZZWRK
     C                     MOVELZZWRK     ZDATE
     C                     END
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    NCD
     C                     END
      *
      * IF NCD > MATURITY DATE , RESET NCD = MATURITY DATE
     C           NCD       IFGT MATY
     C                     Z-ADDMATY      NCD
     C                     END
     C                     END
     C*
     C* If the First Coupon Date is not zero, then
     C* IF NCD < 1ST COUPON DATE , RESET NCD = 1ST COUPON DATE
     C*
     C           FCPN      IFNE *ZEROS
     C           NCD       ANDLTFCPN
     C                     Z-ADDFCPN      NCD
     C                     END
      *
      * IF DATE FORMAT IS DDMMYY TURN BACK ARRAY OF COUPON DATES TO DD/MM
     C           *IN98     IFEQ '0'
     C                     DO   12        X       20
     C                     MOVELCD,X      ZZWRKM  20
     C                     MOVE CD,X      ZZWRKD  20
     C                     Z-ADDZZWRKM    CD,X
     C                     MOVELZZWRKD    CD,X
     C                     END
     C                     END
     C*
     C                     ENDSR
     C*****************************************************************
      /EJECT
     C***********************************************************************
     C********************************************************************S01496
     C****ZA0830*SR*-*THIS*SUBROUTINE*FINDS*OUT*WHETHER*A*PARTICULAR*DAY  S01496
     C****************IS*A*HOLIDAY*IN*A*PARTICULAR*CURRENCY************   S01496
     C*****************************************************************   S01496
     C***********ZA0830    BEGSR                                          S01496
     C***********                                                         S01496
     C***OPEN*CURRENCY/HOLIDAY*FILE*IF*FIRST*TIME*THROUGH**************   S01496
     C***********@@ONCE    IFNE 'Y'                        B1             S01496
     C***********          OPEN FDTABJLL                   *1             S01496
     C***********          MOVE 'Y'       @@ONCE  1        *1             S01496
     C***********          END                             E1             S01496
     C***********                                                         S01496
     C***BLANK*OUT*ARRAY***********************************************   S01496
     C***********          MOVE *BLANKS   @@CY1                           S01496
     C***********          MOVE *BLANKS   @@CY2                           S01496
     C***********                                                         S01496
     C***ACCESS*TABLE*ON*FIRST*KEY*************************************   S01496
     C***********          MOVEL@@MNO     @@MNO6  6                       S01496
     C***********          MOVE @@MNO6    @@0830 12                       S01496
     C***********          MOVEL'22    '  @@0830                          S01496
     C***********          MOVE '1'       @@0830                          S01496
     C***********@@0830    CHAINTABLETJF             90                   S01496
     C***********                                                         S01496
     C***IF*RECORD*NOT*FOUND*OR*DELETED********************************   S01496
     C************IN90     IFEQ '1'                        B1             S01496
     C***********RECI      OREQ '*'                        *1             S01496
     C***********          MOVE 'W'       @@HIND           *1             S01496
     C***********          GOTO ZA0839                     *1             S01496
     C***********          END                             E1             S01496
     C***********                                                         S01496
     C***MOVE*FIRST*25*CURRENCIES*TO*ARRAY*****************************   S01496
     C***********          MOVE HCCY      @@CY1                           S01496
     C***********                                                         S01496
     C***ACCESS*TABLE*ON*SECOND*KEY************************************   S01496
     C***********          MOVE '2'       @@0830                          S01496
     C***********@@0830    CHAINTABLETJF             90                   S01496
     C***********                                                         S01496
     C***IF*SECOND*RECORD*FOUND*AND*NOT*DELETED************************   S01496
     C************IN90     IFEQ '0'                        B1             S01496
     C***********RECI      ANDNE'*'                        *1             S01496
     C***********          MOVE HCCY      @@CY2            *1             S01496
     C***********          END                             E1             S01496
     C***********                                                         S01496
     C***SEARCH*ARRAY*FOR*INPUT*CURRENCY*******************************   S01496
     C***********@@CCY     LOKUP@CY                      91               S01496
     C***********                                                         S01496
     C***IF*INPUT*CURRENCY*IS*IN*ARRAY*********************************   S01496
     C************IN91     IFEQ '1'                        B1             S01496
     C***********                                                         S01496
     C***IF*INCLUDE/EXCLUDE*INDICATOR*EQ*'E'***************************   S01496
     C***********INEX      IFEQ 'E'                        B*2            S01496
     C***********          MOVE 'W'       @@HIND  1        **2            S01496
     C***********          ELSE                            X*2            S01496
     C***********          MOVE 'H'       @@HIND           **2            S01496
     C***********          END                             E*2            S01496
     C***********          END                             E1             S01496
     C***********                                                         S01496
     C***IF*INPUT*CURRENCY*IS*NOT*IN*ARRAY*****************************   S01496
     C************IN91     IFEQ '0'                        B1             S01496
     C***********                                                         S01496
     C***IF*INCLUDE/EXCLUDE*INDICATOR*EQ*'I'***************************   S01496
     C***********INEX      IFEQ 'I'                        B*2            S01496
     C***********          MOVE 'W'       @@HIND           **2            S01496
     C***********          ELSE                            X*2            S01496
     C***********          MOVE 'H'       @@HIND           **2            S01496
     C***********          END                             E*2            S01496
     C***********          END                             E1             S01496
     C***********ZA0839    ENDSR                                          S01496
     C*****************************************************************
      /EJECT
     C********************************************************************
     C**
     C**   ZALIGN SR. TO VALIDATE AND RIGHT-ALIGN NUMERIC FIELDS.
     C**
     C           ZALIGN    BEGSR                           *** ZALIGN ***
     C**
     C                     SETOF                     929399
     C**
     C**   SAVE INPUT FIELD IN ARRAY, ZA1.
     C                     MOVEAZFIELD    ZA1
     C**
     C**   CALCULATION TO DEFINE NUMBER STRUCTURE CONTROL FIELDS.
     C                     Z-ADDZADIG     ZADIG   20
     C                     Z-ADDZADEC     ZADEC   10
     C**
     C**   CALCULATIONS TO DEFINE/CLEAR FIELDS.
     C                     MOVE ' '       ZA2
     C                     MOVEAZA2       ZFIELD 16
     C                     Z-ADD0         ZX      20
     C                     Z-ADD0         ZY      20
     C**
     C**   ENSURE REQUIRED STRUCTURE OF FIELD IS VALID.
     C           ZADIG     ADD  ZADEC     ZZ      20
     C           ZZ        COMP 15                   99
     C   99                GOTO ZAEND
     C**
     C**   LOOP TO FIND DECIMAL POINT, BLANKS AND CHARACTERS.
     C           ZATAG1    TAG                             ** ZATAG1 TAG *
     C           ZX        ADD  1         ZX
     C**
     C**   CHECK FOR DECIMAL POINT. ERROR IF SECOND DECIMAL POINT.
     C           ZA1,ZX    COMP '.'                      90
     C   90 93             SETON                     99
     C   99                GOTO ZAEND
     C**
     C**
     C**   CHECK FOR 'M' CONSTANT. ERROR IF NOT LAST CHARACTER ENTERED
     C           ZA1,ZX    COMP 'M'                      96
     C   96                DO
     C           ZX        COMP 15                   99  99
     C   99                GOTO ZAEND
     C           ZX        ADD  1         ZX
     C           ZA1,ZX    COMP ' '                  9999
     C   99                GOTO ZAEND
     C           ZX        SUB  1         ZX
     C           ZX        COMP 9                    99
     C   99                GOTO ZAEND
     C                     END
     C**
     C**   CHECK FOR 'T' CONSTANT. ERROR IF NOT LAST CHARACTER ENTERED
     C           ZA1,ZX    COMP 'T'                      97
     C   97                DO
     C           ZX        COMP 15                   99  99
     C   99                GOTO ZAEND
     C           ZX        ADD  1         ZX
     C           ZA1,ZX    COMP ' '                  9999
     C   99                GOTO ZAEND
     C           ZX        SUB  1         ZX
     C           ZX        COMP 12                   99
     C   99                GOTO ZAEND
     C                     END
     C**
     C**   CLEAR ALPHAMERIC CONSTANT FROM ZA1
     C   96
     COR 97                MOVE ' '       ZA1,ZX
     C**
     C**   CHECK FOR BLANKS. BYPASS FOR FIRST BLANK AFTER A DIGIT.
     C           ZA1,ZX    COMP ' '                      91
     C   91 92             GOTO ZATAG2
     C**
     C**   CHECK FOR NUMERIC, IF NOT '.' OR ' '.
     C  N90N91   ZA1,ZX    COMP '0'                    99
     C  N90N91N99ZA1,ZX    COMP '9'                  99
     C   99                GOTO ZAEND                      NOT NUMERIC
     C**
     C**   STORE DIGITS IN ARRAY AND HOW MANY.
     C**   ZY, TOTAL OF DIGITS IN THE INPUT FIELD.
     C**   ZZ, TOTAL OF DIGITS TO THE LEFT OF DECIMAL POINT.
     C  N90N91   ZY        ADD  1         ZY         92
     C  N90N91             MOVE ZA1,ZX    ZA2,ZY
     C   90                Z-ADDZY        ZZ         93  93
     C**
     C           ZX        COMP 16                     94  CHECK IF ALL
     C   94                GOTO ZATAG1
     C**
     C           ZATAG2    TAG                             ** ZATAG2 TAG *
     C**
     C**   IF 'M' SPECIFIED, MULTIPLY ZA1 BY 1 MILLION
     C   96      ZZ        ADD  6         ZZ
     C**
     C**   IF 'T' SPECIFIED, MULTIPLY ZA1 BY 1 THOUSAND
     C   97      ZZ        ADD  3         ZZ
     C**
     C**   FILL IN ZEROS IN ANY BLANKS LEFT OF DECIMAL POINT
     C   96 93
     COR 97 93   ZZ        DOWGTZY
     C           ZY        ADD  1         ZY
     C                     MOVE '0'       ZA2,ZY
     C                     END
     C**
     C**   IF CONSTANT SPECIFIED WITH NO DECIMAL POINT ZEROISE BLANKS
     C   96N93             DO   6
     C           ZY        ADD  1         ZY
     C                     MOVE '0'       ZA2,ZY
     C                     END
     C**
     C   97N93             DO   3
     C           ZY        ADD  1         ZY
     C                     MOVE '0'       ZA2,ZY
     C                     END
     C**
     C**   IF NO DECIMAL POINT FOUND, SET TOTAL FIELD ZZ.
     C  N93                Z-ADDZY        ZZ
     C**
     C**   CHECK FOR EMBEDDED BLANKS.
     C   91 92             MOVEAZA1,ZX    ZFIELD
     C   91 92   ZFIELD    COMP ' '                  9999
     C   99                GOTO ZAEND                      EMBEDDED BLANK
     C**
     C**   ENSURE THAT NUMBER OF DIGITS ENTERED EITHER SIDE
     C**   OF DECIMAL POINT ARE NOT MORE THAN ALLOWED.
     C           ZZ        COMP ZADIG                99
     C           ZY        SUB  ZZ        ZX
     C  N99      ZADEC     SUB  ZX        ZX           9995
     C   99                GOTO ZAEND
     C   95                GOTO ZATAG4                     NO TRAILING
     C**                                                   BLANKS.
     C**   FILL THE TRAILING BLANKS WITH ZEROS.
     C           ZY        ADD  ZX        ZZ
     C           ZATAG3    TAG                             ** ZATAG3 TAG *
     C           ZY        ADD  1         ZY
     C                     MOVE '0'       ZA2,ZY
     C           ZY        COMP ZZ                     94
     C   94                GOTO ZATAG3
     C**
     C**   RIGHT-ALIGN THE VALUE BY MOVING BACK TO ARRAY, ZA1.
     C           ZATAG4    TAG                             ** ZATAG4 TAG *
     C                     MOVE '0'       ZA1
     C                     Z-ADD16        ZX
     C           ZATAG5    TAG                             ** ZATAG5 TAG *
     C           ZY        COMP 0                        94
     C   94                GOTO ZATAG6
     C                     MOVE ZA2,ZY    ZA1,ZX
     C           ZY        SUB  1         ZY
     C           ZX        SUB  1         ZX
     C                     GOTO ZATAG5
     C           ZATAG6    TAG                             ** ZATAG6 TAG *
     C**
     C**   MOVE VALIDATED AND RIGHT-ALIGNED NUMBER BACK INTO ZFIELD.
     C                     MOVEAZA1       ZFIELD
     C**
     C                     SETOF                     9697
     C**
     C           ZAEND     ENDSR                           * ZAEND ENDSR *
     C**
     C**
     C********************************************************************
      /EJECT
     C********************************************************************
     C**                                                                 *
     C**  ZEDIT SUBROUTINE TO INSERT A DECIMAL POINT INTO A NUMERIC FIELD*
     C**  AND TO BLANK OUT LEADING ZEROES.                               *
     C**      INPUT FIELDS:   ZFIELD 16/                                 *
     C*                       ZADEC                                      *
     C*                       ZADIG - NOT ACTUALLY USED                  *
     C*                                                                  *
     C*       ARRAYS    ZA1 ,ZA2   BOTH 16 ELEMENTS, EACH ONE BYTE LONG. *
     C*                                                                  *
     C*       OUTPUT FIELD   ZFIELD                                      *
     C*                                                                  *
     C           ZEDIT     BEGSR                           ***  ZEDIT  ***
     C**
     C**   CALCULATION TO DEFINE NUMBER STRUCTURE CONTROL FIELDS.
     C                     Z-ADDZADIG     ZADIG   20
     C                     Z-ADDZADEC     ZADEC   10
     C**                                                                 *
     C*       SET UP WORK FIELDS                                         *
     C*                                                                  *
     C                     MOVEA' '       ZA1                            *
     C                     MOVEA' '       ZA2                            *
     C*                                                                  *
     C                     Z-ADD16        Z1      20                     *
     C                     Z-ADD16        Z2      20                     *
     C*                                                                  *
     C           16        SUB  ZADEC     ZADEC2  20                     *
     C*                                                                  *
     C*
     C                     MOVEAZFIELD    ZA1                            *
     C*                                                                  *
     C*       CHECK TO SEE IF THERE ARE ANY DECIMAL PLACES               *
     C*                                                                  *
     C           ZADEC     COMP 0                        91              *
     C   91                GOTO ZT20                                     *
     C**                                                                 *
     C           ZT10      TAG                             ** ZT10 TAG ***
     C**                                                                 *
     C*                                                                  *
     C*       SET UP DECIMALS                                            *
     C*                                                                  *
     C                     MOVE ZA1,Z1    ZA2,Z2                         *
     C           Z1        SUB  1         Z1                             *
     C           Z2        SUB  1         Z2                             *
     C*                                                                  *
     C           Z2        COMP 0                        90              *
     C   90                GOTO ZT30                                     *
     C*                                                                  *
     C*       CHECK IF END OF DECIMALS                                   *
     C**                                                                 *
     C           Z2        COMP ZADEC2               90                  *
     C   90                GOTO ZT10                                     *
     C*                                                                  *
     C**                                                                 *
     C*       PUT IN DECIMAL PLACE                                       *
     C*                                                                  *
     C                     MOVE '.'       ZA2,Z2                         *
     C           Z2        SUB  1         Z2                             *
     C*                                                                  *
     C           ZT20      TAG                             ** ZT20 TAG ***
     C*                                                                  *
     C*       SET UP INTEGERS                                            *
     C*                                                                  *
     C                     MOVE ZA1,Z1    ZA2,Z2                         *
     C           Z1        SUB  1         Z1                             *
     C           Z2        SUB  1         Z2                             *
     C**                                                                 *
     C           Z2        COMP 0                    90                  *
     C   90                GOTO ZT20                                     *
     C*                                                                  *
     C*       PUT IN LEADING BLANKS                                      *
     C*                                                                  *
     C           ZT30      TAG                             ** ZT30 TAG ***
     C**                                                                 *
     C                     Z-ADD1         Z2                             *
     C**                                                                 *
     C           ZT40      TAG                             ** ZT40 TAG ***
     C*                                                                  *
     C           ZA2,Z2    COMP '0'                  9090                *
     C   90      ZA2,Z2    COMP ' '                  9090                *
     C*                                                                  *
     C   90                GOTO ZT50                                     *
     C*                                                                  *
     C                     MOVE ' '       ZA2,Z2                         *
     C           Z2        ADD  1         Z2                             *
     C*                                                                  *
     C           Z2        COMP 16                     90                *
     C   90                GOTO ZT40                                     *
     C*                                                                  *
     C*       IF NO INTEGERS PUT IN LEADING ZERO                         *
     C*                                                                  *
     C           ZT50      TAG                             ** ZT50 TAG ***
     C*                                                                  *
     C                     Z-ADDZADEC2    Z2                             *
     C  N91      Z2        SUB  1         Z2                             *
     C           ZA2,Z2    COMP ' '                      90              *
     C   90                MOVE '0'       ZA2,Z2                         *
     C*                                                                  *
     C*       SET UP OUTPUT FIELD                                        *
     C*                                                                  *
     C           ZT60      TAG                             ** ZT60 TAG ***
     C*                                                                  *
     C                     MOVEAZA2       ZFIELD 16                      *
     C**                                                                 *
     CSR         ZEND      ENDSR                           ** ZEND **    *
     C*****************************************************************
      /EJECT
     C/COPY ZSRSRC,ZACCH                                                  S01496
     C*                                                                   S01496
     C/COPY ZSRSRC,ZCHKH                                                  S01496
     C*                                                                   S01496
     C/COPY ZSRSRC,ZSDESCZ3                                               CSE016
     C/EJECT                                                              S01496
     C*****************************************************************
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  CPY@
(c) Finastra International Limited 2001
**  CMD - CALL 'QCMDEXC' TO SBMJOB
SBMJOB JOB(ERnnnnnnx) JOBD(BULKTJOBD) USER(*JOBD) RQSDTA('CALL PGM(SEC6700) PARM              CPK014
(''xxxxxx'')')                  MSGQ(MOPERQ) RTGDTA(*NONE) INLLIBL(*JOBD)
OUTQ(*JOBD)                                                                                   CSC023
**  TXT
F3=Exit  F5=Refresh Screen  Rollup/Rolldown
F3=Exit  F5=Refresh Screen  F9=Go to 'Change' Mode  Rollup/Rolldown
F3=Exit  F5=Refresh Screen  F9=Go to 'Add' Mode  Rollup/Rolldown
F3=Exit  F12=Previous
**  NARR
EARLY REDEMPTION REFERENCE
ERROR CALL PGM 'SE6510'
ERROR CALL PGM 'SE6530'
ERROR CALL PGM 'SE6800'
