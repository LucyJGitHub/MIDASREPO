     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE ER redemptions enquiry')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module                            *
     F*                                                               *
     F*  SE6530 - SE Early Redemptions Enquiry                        *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CSE071             Date 19Jul05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 179197             Date 18May00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001             Date 24Aug99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAC005             Date 23Nov98               *
      *                 S01496             Date 11Oct94               *
      *                 S10978             Date 22Jan93               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
     F*  179197 - Corrected Message IDs                               *
     F*  CPB001 - 'Private Banking' Module                            *
      *  CAC005 - Profit Centre Accounting - SE Enhancement           *
     F*  S01496 - Incorporation of JYSKE Enhancements (S10978)        *
     F*  S10978 - JYSZRH: EARLY REDEMPTIONS                           *
     F*                                                               *
     F*****************************************************************
      /EJECT
     F*
     F*****BANK*DETAILS*FILE.*PREFIX*-*BJ******************************   S01496
     F***SDBANKL1IF**E           K        DISK         KINFSR *PSSR       S01496
     F*
     F*****MODULES*PRESENT*ON*SYSTEM.*PREFIX*-*BG**********************   S01496
     F***SDMMODL1IF**E           K        DISK         KINFSR *PSSR       S01496
     F*
     F*****PM*ICD*******FILE.*PREFIX*-*P9******************************   S01496
     F***SDPORTL1IF**E           K        DISK         KINFSR *PSSR       S01496
     F*
     F**   SECURITIES   FILE. PREFIX - NONE
     FSECTY   IF  E           K        DISK         KINFSR *PSSR
     F*
     F*****CUSTOMER*DETAILS********************************************   S01496
     F***SDCUSTL1IF**E           K        DISK                            S01496
     F***********                                   KINFSR *PSSR          S01496
     F*
     F*****SE*ICD*******FILE.*PREFIX*-*BV******************************   S01496
     F***SDSTRDL1IF**E           K        DISK         KINFSR *PSSR       S01496
     F*
     F**   SE ER DEPOT ALLOCATIONS. PREFIX - VB
     FSEERDPL1IF  E           K        DISK         KINFSR *PSSR
     F*
     F**   SE ER CUSTOMER/BOOK ALLOCATIONS. PREFIX - VC
     FSEERALL1IF  E           K        DISK         KINFSR *PSSR
     F*
     F**   SCREEN FORMAT
     FSE6530DFCF  E                    WORKSTN      KINFSR *PSSR
     F                                        SFLRR2KSFILE SE6530S5
     F*
     F********************************************************************
     F/EJECT
     F********************************************************************
     F**                                                                 *
     F** ID F  C  H  L    FUNCTION OF INDICATORS                         *
     F**                 ------------------------                        *
     F**                                                                 *
     F**       26 - SFLEND    }- MESSAGE SUBFILE                         *
     F**                                                                 *
     F**       27 - ROLLUP    }                                          *
     F**       28 - SFLDSP    }                                          *
     F**       29 - SFLDSPCTL }                                          *
     F**       30 - SFLCLR    }- REVIEW FROM SUBFILE                     *
     F**       31 - SFLEND    }                                          *
     F**                                                                 *
     F**       39 - FILE INDICATOR ON SE6530DF                           *
     F**       41 - READ INDICATOR ON SEERDPL1 FILE                      *
     F**       42 - READ INDICATOR ON SEERALL1 FILE                      *
     F**       43 - SETLL VALIDATION INDICATOR ON SEERDPL1 OR SEERALL1   *
      *  45 - Display text and field for Profit Centre               *    CAC005
     F**       50 - FILE INDICATOR ON ALL OTHER FILES                    *
     F**                                                                 *
     F**       59 - HIGHLIGHTED SF LINE                                  *
     F**                                                                 *
     F**       61 - ERROR ON BRANCH                                      *
     F**       62 - ERROR ON DEPOT                                       *
     F**       63 - ERROR ON BOOK                                        *
     F**       64 - ERROR ON CUSTOMER                                    *
      *  65 - Error on Profit Centre                                 *    CAC005
     F**                                                                 *
     F**       89 - NON NUMERIC CHARACTER IN NUMERIC FIELD               *
     F**                                                                 *
     F**       98 - FORMAT DATE MMDDYY (OFF => DDMMYY)                   *
     F**                                                                 *
     F********************************************************************
      /EJECT
     E/COPY ZSRSRC,ZSEDITZ1
     E/COPY ZSRSRC,ZDATE2Z1
      /EJECT
     E********************************************************************
     E*
     E**   ARRAY FOR COPYRIGHT
     E                    CPY@    1   1 80
     E********************************************************************
      /EJECT
     I********************************************************************
     I*
     I**   DATA STRUCTURE FOR COMPILATION - COPYRIGHT INSERTION
     ICPYR@$      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@$$
     I*
     I**   PROGRAM STATUS DATA STRUCTURE
     IPSDS       SDS
     I**************************************244 253 DDWID                 S01496
     I**************************************254 263 USER                  S01496
     I                                      244 253 SWSID                 S01496
     I                                      254 263 SUSER                 S01496
     I*
     I**   LOCAL DATA AREA FOR DATA BASE ERRORS
     ILDA         DS                            256
     I                                      132 183 WWLOT
     I                                      132 141 WWFILE
     I                                      142 170 WWKEY
     I                                      171 180 WWPGM
     I                                      181 1830WWASE
     I@BANK     E DSSDBANKPD                                              S01496
     I** Dummy record format for access to Bank Details                   S01496
     I*                                                                   S01496
     I@CUST     E DSSDCUSTPD                                              S01496
     I** Dummy record format for access to Customer Details               S01496
     I*                                                                   S01496
     I@STRD     E DSSDSTRDPD                                              S01496
     I** Dummy record format for access to Securities Trading Data        S01496
     I*                                                                   S01496
     I@PORT     E DSSDPORTPD                                              S01496
     I** Dummy record format for access to Portfolio Management ICD       S01496
     I*                                                                   S01496
     I@MMOD     E DSSDMMODPD                                              S01496
     I** Dummy record format for access to Modules File                   S01496
     I*                                                                   S01496
     IDSFDY     E DSDSFDY                                                 S01496
     I** First DS for access programs, short data structure               S01496
     I*                                                                   S01496
     IDSSDY     E DSDSSDY                                                 S01496
     I** Second DS for access programs, long data structure               S01496
     I*
     I              '0123456789'          C         DIGITS
     I/COPY ZSRSRC,ZSEDITZ2
     I*****************************************************************
     C*  PLIST's, KLIST's, DEFN's etc....                             *
     C*****************************************************************
     C*
     C           *ENTRY    PLIST
     C           @PRTCD    PARM @PRTCD    @PRTCD  1
     C                     PARM @PERRF    @PERRF  6
     C                     PARM @PSESN    @PSESN 10
     C                     PARM @PEXDT    @PEXDT  5
     C                     PARM @PVADT    @PVADT  5
     C                     PARM @PERST    @PERST  1
     C*
     C** DEFINE WORK FIELDS
     C           *LIKE     DEFN VBERRF    @KERRF
     C           *LIKE     DEFN VBBRCD    @KBRCD
     C           *LIKE     DEFN VBDDPT    @KDDPT
     C*
     C           *LIKE     DEFN VBERRF    WDERRF
     C           *LIKE     DEFN VBBRCD    WDBRCD
     C           *LIKE     DEFN VBDDPT    WDDDPT
     C*
     C           *LIKE     DEFN VCERTP    @KERTP
     C           *LIKE     DEFN VCBOOK    @KBOOK
     C           *LIKE     DEFN VCCNUM    @KCNUM
     C*
     C           *LIKE     DEFN VCERRF    WAERRF
     C           *LIKE     DEFN VCBRCD    WABRCD
     C           *LIKE     DEFN VCDDPT    WADDPT
     C           *LIKE     DEFN VCERTP    WAERTP
     C           *LIKE     DEFN VCBOOK    WABOOK
     C           *LIKE     DEFN VCCNUM    WACNUM
     C           *LIKE     DEFN VCPTFR    WAPTFR
     C*
     C* Key list for LF/SEERDPL1
     C*
     C           @KEYD1    KLIST
     C                     KFLD           @KERRF
     C                     KFLD           @KBRCD
     C                     KFLD           @KDDPT
     C*
     C* Key list for LF/SEERDPL1 (saved key fields from current record)
     C*
     C           @KEYWD    KLIST
     C                     KFLD           WDERRF
     C                     KFLD           WDBRCD
     C                     KFLD           WDDDPT
     C*
     C* Key list for LF/SEERALL1
     C*
     C           @KEYA1    KLIST
     C                     KFLD           @KERRF
     C                     KFLD           @KBRCD
     C                     KFLD           @KDDPT
     C                     KFLD           @KERTP
     C                     KFLD           @KBOOK
     C                     KFLD           @KCNUM
     C*
     C* Key list for LF/SEERALL1
     C*
     C           @KEYA2    KLIST
     C                     KFLD           VBERRF
     C                     KFLD           VBBRCD
     C                     KFLD           VBDDPT
     C*
     C* Key list for LF/SEERALL1 (saved key fields from current record)
     C*
     C           @KEYWA    KLIST
     C                     KFLD           WAERRF
     C                     KFLD           WABRCD
     C                     KFLD           WADDPT
     C                     KFLD           WAERTP
     C                     KFLD           WABOOK
     C                     KFLD           WACNUM
     C                     KFLD           WAPTFR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C*
     C**              START MAINLINE
     C**             ----------------
     C*
     C**   INITIAL PROCESSING
     C*
     C                     EXSR P001
     C*
     C**   MAIN PROCESSING
     C*
     C**   DO WHILE NOT CMD-3 AND CMD-12
     C*
     C           *INKC     DOWEQ'0'                        *B1
     C           *INKL     ANDEQ'0'
     C*
     C                     READ SE6530C5                 39
     C*
     C           *INKE     IFEQ '1'                        *B2   CMD-5
     C*
     C**   ACCESS LF/SEERDPL1
     C*
     C           @PERRF    SETLLSEERDPL1
     C*
     C                     MOVEL'D'       WWRFIL
     C*
     C**   CLEAR SCREEN 1 SUBFILE
     C*
     C                     MOVEA'001'     *IN,28           OFF 28-29 ON 30
     C                     WRITESE6530C5
     C                     MOVEA'000'     *IN,28           OFF 28-30
     C*
     C**   PREPARE FIRST PAGE OF SUBFILE
     C*
     C                     Z-ADD*ZERO     SFLRR2
     C                     Z-ADD*ZERO     WWSFLC
     C                     EXSR P002
     C*
     C**   BLANK OUT REVIEW FROM FIELDS
     C*
     C                     MOVEL*BLANKS   D1BRCD
     C                     MOVEL*BLANKS   D1DDPT
     C                     MOVEL*BLANKS   D1BOOK
     C                     MOVEL*BLANKS   D1CNUM
     C                     MOVEL*BLANKS   SPRFC                           CAC005
     C*
     C**   SET OFF ERROR INDICATORS
     C*
     C                     MOVE '0'       *IN61
     C                     MOVE '0'       *IN62
     C                     MOVE '0'       *IN63
     C                     MOVE '0'       *IN64
     C                     MOVE *OFF      *IN65                           CAC005
     C*
     C**   CLEAR SCREEN MESSAGE QUEUE
     C*
     C                     CALL 'Y2CLMSC'
     C                     PARM           WWTPGQ
     C                     PARM           WWPGQR
     C*
     C**   WRITE FIRST PAGE OF SUBFILE
     C*
     C                     WRITESE6530C5
     C                     WRITESE6530F6
     C*
     C                     END                             *E2
     C*
     C           *INKC     IFEQ '0'                        *B2   ENTER
     C           *INKL     ANDEQ'0'
     C           *INKE     ANDEQ'0'
     C           *IN27     ANDEQ'0'
     C*
     C**   SET OFF ERROR INDICATORS
     C*
     C                     MOVE '0'       *IN61
     C                     MOVE '0'       *IN62
     C                     MOVE '0'       *IN63
     C                     MOVE '0'       *IN64
     C                     MOVE *OFF      *IN65                           CAC005
     C*
     C**   CLEAR SCREEN MESSAGE QUEUE
     C*
     C                     CALL 'Y2CLMSC'
     C                     PARM           WWTPGQ
     C                     PARM           WWPGQR
     C*
     C** 'REVIEW FROM' DETAILS CHANGED
     C*
     C           D1BRCD    IFNE *BLANKS                    *B3
     C           D1DDPT    ORNE *BLANKS
     C           D1BOOK    ORNE *BLANKS
     C           D1CNUM    ORNE *BLANKS
     C           SPRFC     ORNE *BLANKS                                   CAC005
     C*
     C                     EXSR V001
     C*
     C           WWVALD    IFEQ 'Y'                        *B4
     C*
     C**   CLEAR SCREEN 1 SUBFILE
     C*
     C                     MOVEA'001'     *IN,28           OFF 28-29 ON 30
     C                     WRITESE6530C5
     C                     MOVEA'000'     *IN,28           OFF 28-30
     C*
     C**   PREPARE FIRST PAGE OF SUBFILE
     C*
     C                     Z-ADD*ZERO     SFLRR2
     C                     Z-ADD*ZERO     WWSFLC
     C                     EXSR P002
     C                     WRITESE6530C5
     C                     WRITESE6530F6
     C*
     C**   REDISPLAY CURRENT SCREEN WITH ERRORS
     C                     ELSE                            *X4
     C                     WRITESE6530C5
     C                     WRITESE6530C7
     C                     WRITESE6530F6
     C                     END                             *E4
     C*
     C**   REDISPLAY CURRENT SCREEN UNCHANGED
     C                     ELSE                            *X3
     C                     WRITESE6530C5
     C                     WRITESE6530F6
     C                     END                             *E3
     C*
     C                     END                             *E2
     C*
     C           *IN27     IFEQ '1'                        *B2 ROLLUP
     C*
     C**   PREPARE NEXT/PREVIOUS PAGE OF SUBFILE
     C*
     C                     Z-ADD*ZERO     WWSFLC
     C                     EXSR P002
     C*
     C**   WRITE NEXT/PREVIOUS PAGE OF SUBFILE
     C*
     C                     WRITESE6530C5
     C           D1BRCD    IFNE *BLANKS                    *B3
     C           D1DDPT    ORNE *BLANKS
     C           D1BOOK    ORNE *BLANKS
     C           D1CNUM    ORNE *BLANKS
     C           SPRFC     ORNE *BLANKS                                   CAC005
     C                     WRITESE6530C7
     C                     END                             *E3
     C                     WRITESE6530F6
     C*
     C                     END                             *E2
     C*
     C                     END                             *E1
     C*
     C           *INKC     IFEQ '1'                        *B1   CMD-3
     C                     MOVEL'3'       @PRTCD
     C                     END                             *E1
     C           *INKL     IFEQ '1'                        *B1   CMD-12
     C                     MOVEL'2'       @PRTCD
     C                     END                             *E1
     C*
     C*
     C**   END PROGRAM
     C*
     C                     MOVE '0'       *INKC
     C                     MOVE '0'       *INKL
     C                     MOVE '0'       *INLR
     C                     RETRN
     C*
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C**          INDEX TO SUBROUTINES                                   *
     C**         ----------------------                                  *
     C**                                                                 *
     C**   1.  P001   - INITIAL PROCESSING                               *
     C**   2.  P002   - BUILD NEXT PAGE OF SUBFILE                       *
     C**   3.  V001   - VALIDATE REVIEW FROM DETAILS                     *
     C**   4.  ZASNMS - SEND MESSAGE TO PROGRAMS MESSAGE QUEUE           *
     C**   5.  #PSSR  - PROGRAM ERROR HANDLING ROUTINE                   *
     C**   6.  ZSEDIT - FORMAT SIGNED NUMERIC FIELDS FOR OUTPUT          *
     C**   7.  ZDATE2 - VALIDATE DAY NUMBERS AND CONVERT TO DATES
     C**                                                                 *
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C** P001      - INITIAL PROCESSING                                  *
     C**                                                                 *
     C** CALLS     - P002, ZDATE2                                        *
     C**                                                                 *
     C** CALLED BY - MAINLINE                                            *
     C**                                                                 *
     C********************************************************************
     C           P001      BEGSR
     C*                                                                   S01496
     C**  Copyright statement                                             S01496
     C*                                                                   S01496
     C                     MOVEACPY@      ACT@   80                       S01496
     C*
     C           WWFITI    IFEQ 'N'                        *B1
     C*
     C**   CLEAR SCREEN 1 SUBFILE
     C*
     C                     MOVEA'001'     *IN,28           OFF 28-29 ON 30
     C                     WRITESE6530C5
     C                     MOVEA'000'     *IN,28           OFF 28-30
     C*
     C                     END                             *E1
     C*
     C           WWFITI    IFEQ ' '                        *B1
     C                     MOVEL'N'       WWFITI  1
     C*
     C**   INITIALISE DB ERROR FIELDS ETC, AND STORE PROGRAM NAME FOR
     C**   DB ERROR FIELD.
     C*
     C                     MOVE *BLANKS   WWLOT
     C                     MOVEL'SE6530'  DBPGM  10
     C*
     C                     MOVE '1'       *IN26
     C                     MOVE 'Y'       WWVALD  1
     C*
     C                     MOVE *BLANKS   WWTPGQ 10
     C                     MOVEL'*'       WWTPGQ
     C                     MOVE *BLANKS   WWPGQR  5
     C                     MOVEL'*PRV'    WWPGQR
     C*
     C**   INITIALISE SCREEN MESSAGE QUEUE
     C*
     C                     MOVEL'*'       DDPGMQ
     C*
     C                     MOVE *BLANKS   ZAMSGF
     C                     MOVEL'SEUSRMSG'ZAMSGF
     C*
     C*****ACCESS*ICD*FILE*LF/SDBANKL1*********************************   S01496
     C*
     C************LOVAL    SETLLSDBANKL1                                  S01496
     C***********          READ SDBANKL1                 50               S01496
     C*                                                                   S01496
     C                     CALL 'AOBANKR0'                                S01496
     C                     PARM *BLANKS   @RTCD   7                       S01496
     C                     PARM '*FIRST  '@OPTN   7                       S01496
     C           @BANK     PARM @BANK     DSFDY                           S01496
     C*
     C**   IF RECORD NOT FOUND OR DELETED
     C*
     C************IN50     IFEQ '1'                        *B2            S01496
     C           @RTCD     IFNE *BLANK                                    S01496
     C                     Z-ADD1         DBASE   30
     C                     MOVE *BLANKS   DBFILE 10        ***************
     C***********          MOVEL'SDBANKL1'DBFILE           * DBERROR 001 *S01496
     C                     MOVE 'SDBANKPD'DBFILE                          S01496
     C                     MOVE *BLANKS   DBKEY  29        ***************
     C                     EXSR *PSSR
     C                     END                             *E2
     C*                                                                   S01496
     C**  Rundate in DDMMMYY format                                       S01496
     C*                                                                   S01496
     C                     MOVE BJMRDT    SRUNA                           S01496
     C*
     C**   SET ON/OFF *IN98
     C*
     C           BJDFIN    IFEQ 'M'                        *B2
     C                     MOVEL'1'       *IN98
     C                     END                             *E2
     C*
     C           BJDFIN    IFEQ 'D'                        *B2
     C                     MOVEL'0'       *IN98
     C                     END                             *E2
     C*
     C*****ACCESS*ICD*FILE*LF/SDMMODL1*********************************   S01496
     C*
     C************LOVAL    SETLLSDMMODL1                                  S01496
     C***********          READ SDMMODL1                 50               S01496
     C*                                                                   S01496
     C                     CALL 'AOMMODR0'                                S01496
     C                     PARM *BLANKS   @RTCD   7                       S01496
     C                     PARM '*FIRST  '@OPTN   7                       S01496
     C           @MMOD     PARM @MMOD     DSFDY                           S01496
     C*
     C**   IF RECORD NOT FOUND
     C*
     C************IN50     IFEQ '1'                        *B2            S01496
     C           @RTCD     IFNE *BLANK                                    S01496
     C                     Z-ADD2         DBASE
     C                     MOVE *BLANKS   DBFILE           ***************
     C***********          MOVEL'SDMMODL1'DBFILE           * DBERROR 002 *S01496
     C                     MOVE 'SDMMODPD'DBFILE                          S01496
     C                     MOVE *BLANKS   DBKEY            ***************
     C                     EXSR *PSSR
     C                     END                             *E2
     C*                                                                   S01496
     C** Check multibranching indicator.                                  S01496
     C*                                                                   S01496
     C           BGMBIN    IFEQ 'Y'                                       S01496
     C                     MOVE '1'       *IN40                           S01496
     C                     END                                            S01496
     C*
     C*****ACCESS*PM*ICD*FILE*LF/SDPORTL1******************************   S01496
     C*
     C           BGPFMG    IFEQ 'Y'                        *B2
     C           BGN4ST    OREQ 'Y'                                       CPB001
     C************LOVAL    SETLLSDPORTL1                                  S01496
     C***********          READ SDPORTL1                 50               S01496
     C*                                                                   S01496
     C           BGPFMG    IFEQ 'Y'                                       CPB001
     C                     CALL 'AOPORTR0'                                S01496
     C                     PARM *BLANKS   @RTCD   7                       S01496
     C                     PARM '*FIRST  '@OPTN   7                       S01496
     C           @PORT     PARM @PORT     DSFDY                           S01496
     C*
     C**   IF RECORD NOT FOUND
     C*
     C************IN50     IFEQ '1'                        *B3            S01496
     C           @RTCD     IFNE *BLANK                                    S01496
     C                     Z-ADD3         DBASE
     C                     MOVE *BLANKS   DBFILE           ***************
     C***********          MOVEL'SDPORTL1'DBFILE           * DBERROR 003 *S01496
     C                     MOVE 'SDPORTPD'DBFILE                          S01496
     C                     MOVE *BLANKS   DBKEY            ***************
     C                     EXSR *PSSR
     C                     END                             *E3
     C                     ENDIF                                          CPB001
     C*
     C                     MOVE '1'       *IN44                           S01496
     C                     END                             *E2
     C                     END                             *E1
     C*
     C**   ACCESS FILE LF/SECTY
     C*
     C           @PSESN    CHAINSECTY                50
     C*
     C**   IF RECORD NOT FOUND
     C*
     C           *IN50     IFEQ '1'                        *B1
     C                     Z-ADD4         DBASE
     C                     MOVE *BLANKS   DBFILE           ***************
     C***********          MOVEL'SECTY   'DBFILE           * DBERROR 004 *S01496
     C                     MOVE 'SECTY   'DBFILE                          S01496
     C                     MOVE @PSESN    DBKEY            ***************
     C                     EXSR *PSSR
     C                     END                             *E1
     C*
     C*****ACCESS*FILE*LF/SDSTRDL1*TO*RETRIEVE*DUMMY*CUSTOMER**********   S01496
     C*
     C************LOVAL    SETLLSDSTRDL1                                  S01496
     C***********          READ SDSTRDL1                 50               S01496
     C*                                                                   S01496
     C                     CALL 'AOSTRDR0'                                S01496
     C                     PARM *BLANKS   @RTCD   7                       S01496
     C                     PARM '*FIRST  '@OPTN   7                       S01496
     C           @STRD     PARM @STRD     DSSDY                           S01496
     C*
     C**   IF RECORD NOT FOUND
     C*
     C************IN50     IFEQ '1'                        *B1            S01496
     C           @RTCD     IFNE *BLANK                                    S01496
     C                     Z-ADD5         DBASE
     C                     MOVE *BLANKS   DBFILE           ***************
     C***********          MOVEL'SDSTRDL1'DBFILE           * DBERROR 005 *S01496
     C                     MOVE 'SDSTRDPD'DBFILE                          S01496
     C                     MOVE *BLANKS   DBKEY            ***************
     C                     EXSR *PSSR
     C                     END                             *E1
     C*
     C**********           MOVE BVERCU    WWERCU  60                                          CSD027
     C                     MOVE BVERCU    WWERCU  6                                           CSD027
      *                                                                   CAC005
      ** Access Switchable feature files for CAC005                       CAC005
      *                                                                   CAC005
     C                     CALL 'AOSARDR0'                                CAC005
     C                     PARM *BLANKS   PRTCD   7                       CAC005
     C                     PARM '*VERIFY' POPTN   7                       CAC005
     C                     PARM 'CAC005'  PSARD   6                       CAC005
      *                                                                   CAC005
     C           PRTCD     IFEQ *BLANK                                    CAC005
     C                     MOVE 'Y'       CAC005  1                       CAC005
     C                     MOVE *ON       *IN45                           CAC005
     C                     ELSE                                           CAC005
     C                     MOVE 'N'       CAC005                          CAC005
     C                     MOVE *OFF      *IN45                           CAC005
     C                     ENDIF                                          CAC005
     C*
     C**   FILL PARAMETERS FOR ZSEDIT
     C*
     C                     MOVEL'L'       ZECODE
     C                     Z-ADDNMDP      ZDECS
     C*
     C**   ACCESS LF/SEERDPL1
     C*
     C           @PERRF    SETLLSEERDPL1             43
     C*
     C**   CHECK DETAILS EXIST
     C*
     C           *IN43     IFEQ '0'                        *B1
     C                     READ SEERDPL1                 41
     C*
     C           VBERRF    IFNE @PERRF                     *B2
     C                     MOVEL'1'       *IN43
     C                     ELSE                            *X2
     C           @PERRF    SETLLSEERDPL1
     C                     MOVEL'D'       WWRFIL  1
     C                     END                             *E2
     C*
     C                     END                             *E1
     C*
     C           *IN43     IFEQ '1'                        *B1
     C*
     C**   SUBFILE IS EMPTY, END THE PROGRAM
     C*
     C                     MOVEL'M'       @PRTCD           EMPTY SFL
     C                     RETRN
     C                     END                             *E1
     C*
     C**   PREPARE FIRST PAGE OF SUBFILE
     C*
     C                     Z-ADD*ZERO     SFLRR2  40
     C                     Z-ADD*ZERO     WWSFLC  40
     C                     EXSR P002
     C*
     C**   FILL FIELDS FROM PARAMETERS
     C*
     C                     MOVEL@PERRF    D1REF
     C                     MOVEL@PSESN    D1SEC
     C                     MOVEL@PEXDT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZDATE     D1EXD
     C                     MOVEL@PVADT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZDATE     D1VAD
     C                     MOVEL@PERST    D1STA
     C*
     C**   BLANK OUT REVIEW FROM FIELDS
     C*
     C                     MOVEL*BLANKS   D1BRCD
     C                     MOVEL*BLANKS   D1DDPT
     C                     MOVEL*BLANKS   D1BOOK
     C                     MOVEL*BLANKS   D1CNUM
     C                     MOVEL*BLANKS   SPRFC                           CAC005
     C*
     C**   WRITE FIRST PAGE OF SUBFILE
     C*
     C                     WRITESE6530C5
     C                     WRITESE6530F6
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C** P002      - BUILD NEXT PAGE OF SUBFILE                          *
     C**                                                                 *
     C** CALLS     - NONE                                                *
     C**                                                                 *
     C** CALLED BY - MAINLINE, P001                                      *
     C**                                                                 *
     C********************************************************************
     C           P002      BEGSR
     C*
     C                     MOVE '0'       *IN31            WRITE + IN SFL
     C*
     C** READ FIRST RECORD
     C*
     C                     MOVEL*BLANK    WWLRFI  1        LAST READ FILE
     C*
     C           WWRFIL    IFEQ 'D'                        *B1
     C                     READ SEERDPL1                 41
     C*
     C           *IN41     IFEQ '0'                        *B2
     C                     MOVEL'D'       WWLRFI
     C                     MOVEL'A'       WWRFIL  1        READ FILE
     C           @KEYA2    SETLLSEERALL1
     C                     END                             *E2
     C*
     C                     ELSE                            *X1
     C*
     C** READ FIRST LIVE RECORD FROM LF/SEERALL1
     C*
     C           *IN42     DOUEQ'1'                        *B2
     C           VCRECI    ORNE '*'
     C                     READ SEERALL1                 42
     C                     END                             *E2
     C*
     C           *IN42     IFEQ '1'                        *B2
     C           VCERRF    ORNE @PERRF
     C           VCBRCD    ORNE VBBRCD
     C           VCDDPT    ORNE VBDDPT
     C                     READ SEERDPL1                 41
     C*
     C           *IN41     IFEQ '0'                        *B3
     C                     MOVEL'D'       WWLRFI
     C           @KEYA2    SETLLSEERALL1
     C                     END                             *E3
     C*
     C                     ELSE                            *X2
     C                     MOVEL'A'       WWLRFI
     C                     END                             *E2
     C*
     C                     END                             *E1
     C*
     C           WWLRFI    IFEQ *BLANK                     *B1
     C*
     C                     MOVE '1'       *IN29            SFLDSPCTL
     C                     MOVEL'M'       @PRTCD           EMPTY SFL
     C*
     C**   SUBFILE IS EMPTY, END THE PROGRAM
     C*
     C                     RETRN
     C*
     C                     ELSE                            *X1
     C*
     C                     MOVE '1'       *IN28            SFLDSP
     C                     MOVE '1'       *IN29            SFLDSPCTL
     C*
     C** FILL UP SCREEN
     C*
     C           WWLRFI    DOWNE*BLANK                     *B2
     C           WWSFLC    ANDLT15
     C*
     C           VBERRF    IFEQ @PERRF                     *B2.1
     C*
     C** WRITE DEPOT LINE
     C*
     C           WWLRFI    IFEQ 'D'                        *B3
     C*
     C**  Validate branch.                                                S01496
     C*                                                                   S01496
     C                     CALL 'ZVBBU'                                   S01496
     C                     PARM VBBRCD    BRANCH  3                       S01496
     C                     PARM *ZEROS    ERRCOD  10                      S01496
     C*                                                                   S01496
     C           ERRCOD    IFEQ *ZEROS                     *B4            S01496
     C                     MOVEL'1'       *IN59            HI ON
     C                     MOVELVBBRCD    D2BRCD
     C                     MOVELVBDDPT    D2DDPT
     C*
     C**   ACCESS THE CUSTOMER DETAIL FILE TO RETRIEVE THE SHORTNAME
     C*
     C***********D2DDPT    CHAINSDCUSTL1             50                   S01496
     C*                                                                   S01496
     C                     CALL 'AOCUSTR0'                                S01496
     C                     PARM *BLANKS   @RTCD   7                       S01496
     C                     PARM '*KEY   ' @OPTN   7                       S01496
     C                     PARM D2DDPT    @KEY1  10                       S01496
     C                     PARM *BLANKS   @KYST   7                       S01496
     C           @CUST     PARM @CUST     DSSDY                           S01496
     C*
     C**   IF RECORD NOT FOUND
     C*
     C************IN50     IFEQ '1'                                       S01496
     C           @RTCD     IFNE *BLANK                                    S01496
     C                     Z-ADD6         DBASE
     C                     MOVE *BLANKS   DBFILE           ***************
     C***********          MOVEL'SDCUSTL1'DBFILE           * DBERROR 006 *S01496
     C                     MOVE 'SDCUSTPD'DBFILE                          S01496
     C                     MOVELD2DDPT    DBKEY            ***************
     C                     EXSR *PSSR
     C                     END
     C*
     C                     MOVE BBCSSN    D2DDPN
     C*
     C                     MOVEL*BLANKS   D2BKCU
     C                     MOVELVBDDPT    D2BKCU
     C                     MOVEL*BLANKS   D2PTFR
     C                     Z-ADDVBEXPO    ZFLD15
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    D2POEX
     C                     Z-ADDVBNRDM    ZFLD15
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    D2NRDM
     C*
     C                     ADD  1         WWSFLC
     C                     ADD  1         SFLRR2
     C*
     C                     WRITESE6530S5
     C                     MOVEL'0'       *IN59            HI OFF
     C*
     C                     END                             *E4            S01496
     C                     END                             *E3
     C*
     C** WRITE BOOK/CUSTOMER LINE
     C*
     C           WWLRFI    IFEQ 'A'                        *B3
     C*
     C                     MOVEL*BLANKS   D2BRCD
     C                     MOVEL*BLANKS   D2DDPT
     C                     MOVEL*BLANKS   D2DDPN
     C           VCPTFR    IFEQ '9997'                     *B4
     C           BGN4ST    ANDNE'Y'                                       CPB001
     C***********          MOVELP9R997    D2PTFR                          S01496
     C                     MOVELFCR997    D2PTFR                          S01496
     C                     ELSE                            *X4
     C                     MOVELVCPTFR    D2PTFR
     C                     END                             *E4
     C                     Z-ADDVCEXPO    ZFLD15
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    D2POEX
     C*
     C                     MOVEL*BLANKS   D2BKCU
     C           VCERTP    IFEQ 'B'                        *B4
     C                     MOVELVCBOOK    D2BKCU
      *                                                                   CAC005
      ** If CAC005 is installed, retrieve the corresponding user book     CAC005
      ** and profit centre represented by VCBOOK.                         CAC005
      *                                                                   CAC005
     C           CAC005    IFEQ 'Y'                                       CAC005
     C                     CALL 'AOBKPCR0'                                CAC005
     C                     PARM *BLANKS   PRTCD                           CAC005
     C                     PARM '*USER  ' POPTN                           CAC005
     C                     PARM           PBKCD   2                       CAC005
     C                     PARM           PPRFC   4                       CAC005
     C                     PARM VCBOOK    PPSBK   2                       CAC005
      *                                                                   CAC005
     C           PRTCD     IFEQ *BLANKS                                   CAC005
     C                     MOVELPBKCD     D2BKCU                          CAC005
     C                     MOVE PPRFC     D2BKCU                          CAC005
     C                     ENDIF                                          CAC005
     C                     ENDIF                                          CAC005
     C*
     C                     ELSE                            *X4
     C                     MOVELVCCNUM    D2BKCU
     C           VCCNUM    IFEQ WWERCU                     *B5
     C                     MOVE 'Dmy'     D2BKCU
     C                     MOVEL*BLANKS   D2POEX
     C                     END                             *E5
     C                     END                             *E4
     C*
     C                     Z-ADDVCNOMA    ZFLD15
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    D2NRDM
     C*
     C                     ADD  1         WWSFLC
     C                     ADD  1         SFLRR2
     C*
     C                     WRITESE6530S5
     C*
     C                     END                             *E3
     C*
     C** READ NEXT RECORD, IF THE SUBFILE IS NOT FULL
     C*
     C           WWSFLC    IFLT 15                         *B3.0
     C*
     C                     MOVEL*BLANK    WWLRFI
     C*
     C** READ NEXT LIVE RECORD FROM LF/SEERALL1
     C*
     C           *IN42     DOUEQ'1'                        *B3
     C           VCRECI    ORNE '*'
     C                     READ SEERALL1                 42
     C                     END                             *E3
     C*
     C           *IN42     IFEQ '1'                        *B3
     C           VCERRF    ORNE @PERRF
     C           VCBRCD    ORNE VBBRCD
     C           VCDDPT    ORNE VBDDPT
     C                     READ SEERDPL1                 41
     C*
     C           *IN41     IFEQ '0'                        *B4
     C                     MOVEL'D'       WWLRFI
     C           @KEYA2    SETLLSEERALL1
     C                     END                             *E4
     C*
     C                     ELSE                            *X3
     C                     MOVEL'A'       WWLRFI
     C                     END                             *E3
     C*
     C                     END                             *E3.0
     C*
     C                     ELSE                            *X2.1
     C                     MOVEL*BLANK    WWLRFI
     C                     END                             *E2.1
     C*
     C                     END                             *E2
     C*
     C                     END                             *E1
     C*
     C           WWLRFI    IFEQ *BLANK
     C                     MOVE '1'       *IN31            DON'T WRITE +
     C                     END
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C** V001      - VALIDATE REVIEW FROM DETAILS                        *
     C**                                                                 *
     C** CALLS     - ZASNMS                                              *
     C**                                                                 *
     C** CALLED BY - MAINLINE                                            *
     C**                                                                 *
     C********************************************************************
     C           V001      BEGSR
     C*
     C**   SET ON VALID FLAG
     C*
     C                     MOVE 'Y'       WWVALD
     C*
     C**   IF REVIEW FROM ENTERED
     C*
     C           D1BRCD    IFNE *BLANKS                    *B1
     C*
     C**   CHECK IT'S A VALID NUMERIC FIELD
     C*
     C           DIGITS    CHECKD1BRCD    N       10     89
     C*
     C**   IF IT'S NOT A VALID NUMERIC FIELD
     C*
     C           *IN89     IFEQ '1'                        *B2
     C                     MOVE 'N'       WWVALD
     C                     MOVE '1'       *IN61            Reverse image
     C                     MOVEL'ER65301' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     END                             *E2
     C*
     C                     END                             *E1
     C*
     C           D1DDPT    IFNE *BLANKS                    *B1
     C*
     C**   CHECK IT'S A VALID NUMERIC FIELD
     C*
     C           DIGITS    CHECKD1DDPT    N       10     89
     C*
     C**   IF IT'S NOT A VALID NUMERIC FIELD
     C*
     C           *IN89     IFEQ '1'                        *B2
     C                     MOVE 'N'       WWVALD
     C                     MOVE '1'       *IN62            Reverse image
     C                     MOVEL'ER65302' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     END                             *E2
     C*
     C                     END                             *E1
     C*
     C           D1CNUM    IFNE *BLANKS                    *B1
     C*
     C**   CHECK IT'S A VALID NUMERIC FIELD
     C*
     C           DIGITS    CHECKD1CNUM    N       10     89
     C*
     C**   IF IT'S NOT A VALID NUMERIC FIELD
     C*
     C           *IN89     IFEQ '1'                        *B2
     C                     MOVE 'N'       WWVALD
     C                     MOVE '1'       *IN64            Reverse image
     C                     MOVEL'ER65303' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     END                             *E2
     C*
     C                     END                             *E1
      *                                                                   CAC005
      ** If CAC005 is installed, validate screen-review-book and          CAC005
      ** screen review-profit centre                                      CAC005
      *                                                                   CAC005
     C           CAC005    IFEQ 'Y'                                       CAC005
      *                                                                   CAC005
      ** If screen-review-book is blank and screen-review profit          CAC005
      ** centre is not blank                                              CAC005
      *                                                                   CAC005
     C           D1BOOK    IFEQ *BLANK                                    CAC005
     C           SPRFC     ANDNE*BLANK                                    CAC005
     C                     MOVE 'N'       WWVALD                          CAC005
     C                     MOVE *ON       *IN65                           CAC005
     C***********          MOVEL'PCA0013' ZAMSID                   CAC005 179197
     C                     MOVEL'PCA0014' ZAMSID                          179197
     C                     EXSR ZASNMS                                    CAC005
     C                     ENDIF                                          CAC005
      *                                                                   CAC005
      ** If screen-review-book not blank and screen-review profit         CAC005
      ** centre is blank                                                  CAC005
      *                                                                   CAC005
     C           D1BOOK    IFNE *BLANK                                    CAC005
     C           SPRFC     ANDEQ*BLANK                                    CAC005
     C                     MOVE 'N'       WWVALD                          CAC005
     C                     MOVE *ON       *IN65                           CAC005
     C***********          MOVEL'PCA0014' ZAMSID                   CAC005 179197
     C                     MOVEL'PCA0013' ZAMSID                          179197
     C                     EXSR ZASNMS                                    CAC005
     C                     ENDIF                                          CAC005
      *                                                                   CAC005
     C           D1BOOK    IFNE *BLANK                                    CAC005
     C           SPRFC     ANDNE*BLANK                                    CAC005
      *                                                                   CAC005
      ** Check if book - profit centre combination is valid               CAC005
      *                                                                   CAC005
     C                     CALL 'AOBKPCR0'                                CAC005
     C                     PARM *BLANKS   PRTCD                           CAC005
     C                     PARM '*PSEUDO' POPTN                           CAC005
     C                     PARM D1BOOK    PBKCD   2                       CAC005
     C                     PARM SPRFC     PPRFC   4                       CAC005
     C                     PARM           PPSBK   2                       CAC005
      *                                                                   CAC005
     C           PRTCD     IFNE *BLANKS                                   CAC005
     C                     MOVE *ON       *IN65                           CAC005
     C                     MOVE 'N'       WWVALD                          CAC005
     C                     MOVEL'PCA0012' ZAMSID                          CAC005
     C                     EXSR ZASNMS                                    CAC005
     C                     ENDIF                                          CAC005
     C                     ENDIF                                          CAC005
      *                                                                   CAC005
     C                     ENDIF                                          CAC005
     C*
     C* TEST ON COMBINATION
     C*
     C           WWVALD    IFEQ 'Y'                        *B1
     C                     MOVE 'N'       WWVALD
     C                     MOVEL*BLANKS   @KERTP
     C*
     C** ALL REVIEW FROM FIELDS BLANK
     C*
     C           D1BRCD    IFEQ *BLANKS                    *B2
     C           D1DDPT    ANDEQ*BLANKS
     C           D1BOOK    ANDEQ*BLANKS
     C           D1CNUM    ANDEQ*BLANKS
     C                     MOVE 'Y'       WWVALD
     C                     END                             *E2
     C*
     C** BRANCH ONLY ENTERED
     C*
     C           D1BRCD    IFNE *BLANKS                    *B2
     C           D1DDPT    ANDEQ*BLANKS
     C           D1BOOK    ANDEQ*BLANKS
     C           D1CNUM    ANDEQ*BLANKS
     C                     MOVE 'Y'       WWVALD
     C                     END                             *E2
     C*
     C** BRANCH + DEPOT ENTERED
     C*
     C           D1BRCD    IFNE *BLANKS                    *B2
     C           D1DDPT    ANDNE*BLANKS
     C           D1BOOK    ANDEQ*BLANKS
     C           D1CNUM    ANDEQ*BLANKS
     C                     MOVE 'Y'       WWVALD
     C                     END                             *E2
     C*
     C** BRANCH + DEPOT + BOOK ENTERED
     C*
     C           D1BRCD    IFNE *BLANKS                    *B2
     C           D1DDPT    ANDNE*BLANKS
     C           D1BOOK    ANDNE*BLANKS
     C           D1CNUM    ANDEQ*BLANKS
     C                     MOVE 'Y'       WWVALD
     C                     MOVEL'B'       @KERTP
     C                     END                             *E2
     C*
     C** BRANCH + DEPOT + CUSTOMER ENTERED
     C*
     C           D1BRCD    IFNE *BLANKS                    *B2
     C           D1DDPT    ANDNE*BLANKS
     C           D1BOOK    ANDEQ*BLANKS
     C           D1CNUM    ANDNE*BLANKS
     C                     MOVE 'Y'       WWVALD
     C                     MOVEL'P'       @KERTP
     C                     END                             *E2
     C*
     C**   OK - VALID COMBINATION
     C*
     C           WWVALD    IFEQ 'Y'                        *B2
     C                     MOVEL@PERRF    @KERRF
     C                     MOVELD1BRCD    @KBRCD
     C                     MOVELD1DDPT    @KDDPT
     C                     MOVELD1BOOK    @KBOOK
     C                     MOVELD1CNUM    @KCNUM
      *                                                                   CAC005
      ** If CAC005 is installed and both screen-review-book code and      CAC005
      ** screen-review-profit centre are non blank, retrieve the          CAC005
      ** corresponding pseudo book                                        CAC005
      *                                                                   CAC005
     C           CAC005    IFEQ 'Y'                                       CAC005
     C           D1BOOK    ANDNE*BLANKS                                   CAC005
     C           SPRFC     ANDNE*BLANKS                                   CAC005
     C                     CALL 'AOBKPCR0'                                CAC005
     C                     PARM *BLANKS   PRTCD                           CAC005
     C                     PARM '*PSEUDO' POPTN                           CAC005
     C                     PARM D1BOOK    PBKCD   2                       CAC005
     C                     PARM SPRFC     PPRFC   4                       CAC005
     C                     PARM           PPSBK   2                       CAC005
      *                                                                   CAC005
     C           PRTCD     IFEQ *BLANKS                                   CAC005
     C                     MOVELPPSBK     @KBOOK                          CAC005
     C                     ENDIF                                          CAC005
     C                     ENDIF                                          CAC005
     C*
     C**   Save key flds for current records on SEERDPL1 & SEERALL1
     C*
     C                     MOVELVBERRF    WDERRF
     C                     MOVELVBBRCD    WDBRCD
     C                     MOVELVBDDPT    WDDDPT
     C*
     C                     MOVELVCERRF    WAERRF
     C                     MOVELVCBRCD    WABRCD
     C                     MOVELVCDDPT    WADDPT
     C                     MOVELVCERTP    WAERTP
     C                     MOVELVCBOOK    WABOOK
     C                     MOVELVCCNUM    WACNUM
     C                     MOVELVCPTFR    WAPTFR
     C*
     C**   CHECK DETAILS EXIST
     C*
     C           @KERTP    IFEQ *BLANK                     *B3
     C           @KEYD1    SETLLSEERDPL1             43
     C*
     C           *IN43     IFEQ '0'                        *B4
     C                     READ SEERDPL1                 41
     C*
     C           VBERRF    IFNE @PERRF                     *B5
     C                     MOVEL'1'       *IN43
     C                     ELSE                            *X5
     C           @KEYD1    SETLLSEERDPL1             43
     C                     END                             *E5
     C*
     C                     END                             *E4
     C*
     C                     ELSE                            *X3
     C           @KEYA1    SETLLSEERALL1             43
     C*
     C           *IN43     IFEQ '0'                        *B4
     C*
     C** READ NEXT LIVE RECORD FROM LF/SEERALL1
     C*
     C           *IN42     DOUEQ'1'                        *B5
     C           VCRECI    ORNE '*'
     C                     READ SEERALL1                 42
     C                     END                             *E5
     C*
     C           *IN42     IFEQ '1'                        *B5
     C                     MOVEL'1'       *IN43
     C                     ELSE                            *X5
     C*
     C           VCERRF    IFNE @PERRF                     *B6
     C                     MOVEL'1'       *IN43
     C                     ELSE                            *X6
     C           @KEYA1    SETLLSEERALL1             43
     C                     END                             *E6
     C*
     C                     END                             *E5
     C*
     C                     END                             *E4
     C*
     C                     END                             *E3
     C*
     C**   IF RECORD NOT FOUND
     C*
     C           *IN43     IFEQ '1'                        *B3
     C                     MOVE 'N'       WWVALD
     C                     MOVEL'ER65305' ZAMSID           NO DETAILS
     C                     EXSR ZASNMS
     C*
     C**   RESTORE FILE POINTERS
     C*
     C           @KEYWD    SETLLSEERDPL1
     C                     READ SEERDPL1                 41
     C           @KEYWA    SETLLSEERALL1
     C                     READ SEERALL1                 42
     C*
     C                     ELSE                            *X3
     C           @KERTP    IFNE *BLANK                     *B4
     C           @KEYD1    SETLLSEERDPL1
     C                     READ SEERDPL1                 41
     C                     MOVEL'A'       WWRFIL
     C*
     C                     ELSE                            *X4
     C                     MOVEL'D'       WWRFIL
     C                     END                             *E4
     C                     END                             *E3
     C*
     C                     ELSE                            *X2
     C                     MOVE '1'       *IN61
     C                     MOVE '1'       *IN62
     C                     MOVE '1'       *IN63
     C                     MOVE '1'       *IN64
     C                     MOVEL'ER65304' ZAMSID           INVALID COMB.
     C                     EXSR ZASNMS
     C*
     C                     END                             *E2
     C*
     C                     END                             *E1
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C** ZASNMS    - SEND MESSAGE TO PROGRAM'S MESSAGE QUEUE             *
     C**                                                                 *
     C** CALLS     - Y2SNMGC                                             *
     C**                                                                 *
     C** CALLED BY - V001                                                *
     C**                                                                 *
     C********************************************************************
     C           ZASNMS    BEGSR                           ** ZASNMS    **
     C*
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGM  10
     C                     PARM           ZAPGRL  5
     C                     PARM           ZAMSID  7
     C                     PARM           ZAMSGF 10
     C                     PARM           ZAMSDA132
     C                     PARM           ZAMSTP  7
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C** *PSSR     - PROGRAM ERROR HANDLING ROUTINE                      *
     C**                                                                 *
     C** CALLS     - NONE                                                *
     C**                                                                 *
     C** CALLED BY -                                                     *
     C**                                                                 *
     C********************************************************************
     C           *PSSR     BEGSR
     C*
     C** UPDATE LDA WITH ERROR INFORMATION
     C*
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVELDBFILE    WWFILE
     C                     MOVELDBKEY     WWKEY
     C                     MOVELDBPGM     WWPGM
     C                     MOVELDBASE     WWASE
     C                     OUT  LDA
     C*
     C** PRINT DUMP AND CANCEL PROGRAM
     C*
     C                     DUMP
     C*
     C                     MOVEL'E'       @PRTCD
     C*
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     RETRN
     C*
     C                     ENDSR
     C********************************************************************
      /EJECT
     C/COPY ZSRSRC,ZSEDITZ3
      /EJECT
     C/COPY ZSRSRC,ZDATE2Z2
     C/COPY ZSRSRC,ZDATE2Z3
** CPY@
(c) Finastra International Limited 2001
