     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Pos. Settl. Default Charges Init')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE6561 - Position settlement default charges init.           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Function:  This program initialize (if possible)             *
      *  (I/C)      the field DCCLGR (Class/cust chrg group code)     *
      *                                                               *
      *  Called by:                                                   *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027A            Date 17May06               *
      *  Prev Amend No. BUG3122            Date 17Jul04               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSE026  *CREATE    Date 17Jan01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027A - Conversion of cust. no. to alpha (post 103 build)  *
      *  BUG3122 - Recompiled due to changes in SDSECSPD.             *
      *  CSE026 - Position Settlement Default Charges by Customer     *
      *           Group.                                              *
      *                                                               *
      *****************************************************************
     FSEDFTCL2UF  E           K        DISK         KINFSR *PSSR
      ** Default charges Details
      *
     FSECUGRPDUF  E           K        DISK         KINFSR *PSSR A
      ** Customer group details
      *
     FSECUGRPAUF  E                    DISK         KINFSR *PSSR
      ** Customer group details trailer
      *
      *****************************************************************
      /EJECT
      *****************************************************************
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
      *****************************************************************
      /SPACE 3
      *****************************************************************
     ILDA       E DSLDA                         256
      ** Local data area for database error details
      *
     ISDSECS    E DSSDSECSPD
      ** Securities Clients Details
      *
     IDSSDY     E DSDSSDY
      ** Second DS for access programs, long data structure
      *
     IRUNDAT    E DS
      ** Standard Data Area Layout for System flags and Run Date
      *
     I            DS
     I                                        1   7 CGDAT
     I                                        1   20CGDLUP
     I                                        3   5 CGMLUP
     I                                        6   70CGYLUP
      ** Day, month & year of last update
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Read in Installation Data
      *
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
     C                     Z-ADD0         NBR     40
      *
     C           *LOVAL    SETLLSEDFTCL2
     C                     READ SEDFTCL2                 95
      *
     C           *IN95     DOWEQ*OFF
      *
     C           DCCLGR    IFEQ *BLANKS
      *
     C********** DCCUST    IFNE 0                                                            CSD027A
     C           DCCUST    IFNE *BLANKS                                                      CSD027A
      *
      ** Call access object if customer exists in SDSECSPD.
      *
     C                     MOVELDCCUST    WWCNSN  6
     C                     MOVELWWCNSN    @KEYS   6
      *
     C                     CALL 'AOSECSR0'
     C                     PARM '       ' @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM           @KEYS
     C           SDSECS    PARM SDSECS    DSSDY
      *
      ** If found, retrieve BFPSCG for customer group
      *
     C           @RTCD     IFEQ *BLANKS
     C                     MOVE BFPSCG    DCCLGR
     C                     ENDIF
      *
     C                     ELSE
      *
      ** Else, customer is blank. Initialise customer group field with
      ** the classification field.
      *
     C           DCCLAS    IFNE *BLANK
     C                     MOVE DCCLAS    DCCLGR
     C           DCCLGR    CHAINSECUGRPD             99
     C           *IN99     IFEQ *ON
      *
      ** add record to file SECUGRPD.
      *
     C                     ADD  1         NBR
     C                     MOVE 'D'       CGRECI
     C                     MOVELAGMRDT    CGDAT
     C                     TIME           CGTLUP
     C                     MOVE 'I'       CGCHTP
     C                     Z-ADDAGRDNB    CGLCD
     C                     Z-ADD0         CGTNLU
     C                     MOVELDCCLGR    CGCUGR
     C                     MOVE *BLANKS   CGNARR
     C                     MOVEL'DEFAULT' CGNARR
     C                     CAT  CGCUGR:1  CGNARR
      *
      ** Write record
      *
     C                     WRITESECUGRP0               98
      *
     C           *IN98     IFEQ *ON
     C                     MOVEL'SECUGRPD'DBFILE
     C                     Z-ADD01        DBASE
     C                     MOVEL*BLANK    DBKEY
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     UPDATSEDFTCD0               98
      *
     C           *IN98     IFEQ *ON
     C                     MOVEL'SEDFTCPD'DBFILE
     C                     Z-ADD02        DBASE
     C                     MOVEL*BLANK    DBKEY
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     READ SEDFTCL2                 95
      *
     C                     ENDDO
      *
     C           NBR       IFNE 0
     C           1         CHAINSECUGRPA             99
     C           *IN99     IFEQ *OFF
     C                     ADD  NBR       CGNORE
     C                     UPDATSECUGRZ0               98
      *
     C           *IN98     IFEQ *ON
     C                     MOVEL'SECUGRPD'DBFILE
     C                     Z-ADD03        DBASE
     C                     MOVEL*BLANK    DBKEY
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     MOVE *ON       *INLR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C                     DUMP
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
      *
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
