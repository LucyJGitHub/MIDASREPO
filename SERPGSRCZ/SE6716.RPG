     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE ER Retrieve Specific Block Inds')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module                        *
     F*                                                               *
     F*  SE6716 - SE Retrieve Specific Block Indicators               *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU017             Date 03Avr98               *
     F*                 CSE007             Date 17Dec97               *
     F*                 S01496             Date 03Oct94               *
     F*                 63345              Date 18Nov93               *
     F*                 62813              Date 18Nov93               *
     F*                 E61731             Date 18Nov93               *
     F*                 S10978             Date 13Apr93               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*  CEU017 - Securities Redenomination Euro Changes              *
     F*  CSE007 - Corporate Actions                                   *
     F*  S01496 - Incorporation of JYSKE Enhancements (S10978)        *
     F*  63345  - NEW VERSION OF THE PROGRAM                          *
     F*  62813  - PGM DOES NOT INDICATE SPECIFIC BLOCK/S FOR THE TOTAL*
     F*           PORTFOLIO (9998) OR FOR THE TOTAL CUSTOMER (9999)   *
     F*  E61731 - SPECIFIC BLOCK TO 'Y' ONLY FOR BLOCK NOT MATURED    *
     F*  S10978 - JYSZRH: EARLY REDEMPTIONS                           *
     F*           * GENERAL BLOCKS. . . . . . . . . . . : N           *
     F*           * SPECIFIC BLOCKS ON ALL THE POSITION : Y           *
     F*           * SPECIFIC BLOCKS . . . . . . . . . . : Y           *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
      /EJECT
     F*
     F**   SE BLOCKED POSITION PREFIX - VD
     FSEBKALL3IF  E           K        DISK         KINFSR *PSSR
     F*
     F**   SE BLOCKED POSITION PREFIX - VD
     FSEBKALL4IF  E           K        DISK         KINFSR *PSSR
     F            SEERALD0                          KRENAMESEERALD4
     F            SEBLKPD0                          KRENAMESEBLKPD4
     F            SECOALD0                          KRENAMESECOALD4       CSE007
     F            SECOBKD0                          KRENAMESECOBKD4       CSE007
     F*
     F********************************************************************
     F/EJECT
     F********************************************************************
     F**                                                                 *
     F** ID F  C  H  L    FUNCTION OF INDICATORS                         *
     F**                 ------------------------                        *
     F**                                                                 *
     F**       01 - FORMAT SEERALD0 READ                                 *CSE007
     F**       02 - FORMAT SEERALD4 READ                                 *CSE007
     F**       41 - RECORD FOUND ON SEBKALL3 FILE                        *
     F**                                                                 *
     F********************************************************************
     F/EJECT
     E********************************************************************
     E*
     E**   ARRAY FOR COPYRIGHT
     E                    CPY@    1   1 80
     E********************************************************************
     E/EJECT
     I********************************************************************
     I******SEERALD0                                                      CSE007
     ISEERALD0    01                                                      CSE007
     I              VCBRCD                          XXBRCD
     I              VCCNUM                          XXCNUM
     I              VCPTFR                          XXPTFR
     I              VCERTP                          XXBLTY
     I              VCSESN                          XXSESN
     I              VCBEDT                          XXBEDT
     I*
     ISEBLKPD0
     I              VDBRCD                          XXBRCD
     I              VDCNUM                          XXCNUM
     I              VDPTFR                          XXPTFR
     I              VDBLTY                          XXBLTY
     I              VDSESN                          XXSESN
     I              VDBEDT                          XXBEDT
     I              VDBSTT                          XXBSTT                CSE007
      *                                                                   CSE007
     ISECOALD0                                                            CSE007
     I              MCBRCD                          XXBRCD                CSE007
     I              MCCNUM                          XXCNUM                CSE007
     I              MCPTFR                          XXPTFR                CSE007
     I              MCBLTY                          XXBLTY                CSE007
     I              MCBSEC                          XXSESN                CSE007
     I              MCBEDT                          XXBEDT                CSE007
     I              MCBSTT                          XXBSTT                CSE007
      *                                                                   CSE007
     ISECOBKD0                                                            CSE007
     I              MUBRCD                          XXBRCD                CSE007
     I              MUCNUM                          XXCNUM                CSE007
     I              MUPTFR                          XXPTFR                CSE007
     I              MUBLTY                          XXBLTY                CSE007
     I              MUSESN                          XXSESN                CSE007
     I              MUBEDT                          XXBEDT                CSE007
     I              MUBSTT                          XXBSTT                CSE007
     I*
     I******SEERALD4                                                      CSE007
     ISEERALD4    02                                                      CSE007
     I              VCBRCD                          XXBRCD
     I              VCCNUM                          XXCNUM
     I              VCERTP                          XXBLTY
     I              VCSESN                          XXSESN
     I              VCBEDT                          XXBEDT
     I*
     ISEBLKPD4
     I              VDBRCD                          XXBRCD
     I              VDCNUM                          XXCNUM
     I              VDBLTY                          XXBLTY
     I              VDSESN                          XXSESN
     I              VDBEDT                          XXBEDT
     I              VDBSTT                          XXBSTT                CSE007
      *                                                                   CSE007
     ISECOALD4                                                            CSE007
     I              MCBRCD                          XXBRCD                CSE007
     I              MCCNUM                          XXCNUM                CSE007
     I              MCBLTY                          XXBLTY                CSE007
     I              MCBSEC                          XXSESN                CSE007
     I              MCBEDT                          XXBEDT                CSE007
     I              MCBSTT                          XXBSTT                CSE007
      *                                                                   CSE007
     ISECOBKD4                                                            CSE007
     I              MUBRCD                          XXBRCD                CSE007
     I              MUCNUM                          XXCNUM                CSE007
     I              MUBLTY                          XXBLTY                CSE007
     I              MUSESN                          XXSESN                CSE007
     I              MUBEDT                          XXBEDT                CSE007
     I              MUBSTT                          XXBSTT                CSE007
     I*
     I**   RUNDAT DATA AREA
     IRUNDAT      DS                             13
     I                                    P   8  100AGRDNB
     I*
     I**   DATA STRUCTURE FOR COMPILATION - COPYRIGHT INSERTION
     ICPYR@$      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@$$
     I********************************************************************
     C/EJECT
     C********************************************************************
     C*
     C**              START MAINLINE
     C**             ----------------
     C*
     C**   INITIAL PROCESSING
     C*
     C                     EXSR P001
     C*
     C**   MAIN PROCESSING
     C*
     C                     EXSR P002
     C*
     C**   END PROGRAM
     C*
     C                     MOVE '0'       *INLR
     C                     RETRN
     C*
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C**          INDEX TO SUBROUTINES                                   *
     C**         ----------------------                                  *
     C**                                                                 *
     C**   1.  P001   - INITIAL PROCESSING                               *
     C**   2.  P002   - MAIN PROCESSING                                  *
     C**   3.  #PSSR  - PROGRAM ERROR HANDLING ROUTINE                   *
     C**                                                                 *
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C** P001      - INITIAL PROCESSING                                  *
     C**                                                                 *
     C** CALLS     - NONE                                                *
     C**                                                                 *
     C** CALLED BY - MAINLINE                                            *
     C**                                                                 *
     C********************************************************************
     C           P001      BEGSR
     C*                                                                   S01496
     C**  Copyright statement                                             S01496
     C*                                                                   S01496
     C                     MOVEACPY@      ACT@   80                       S01496
     C*
     C* DEFINE KEY FIELDS
     C*
     C           *LIKE     DEFN XXBRCD    @KBRCD
     C           *LIKE     DEFN XXCNUM    @KCNUM
     C           *LIKE     DEFN XXBLTY    @KBLTY
     C           *LIKE     DEFN XXBEDT    @KBEDT
     C*
     C* Key list for LF/SEBKALL4
     C*
     C           @KEY1     KLIST
     C                     KFLD           @KBRCD
     C                     KFLD           @KCNUM
     C                     KFLD           @PSESN
     C                     KFLD           @KBLTY
     C                     KFLD           @KBEDT
     C*
     C* Key list for LF/SEBKALL3
     C*
     C           @KEY2     KLIST
     C                     KFLD           @KBRCD
     C                     KFLD           @KCNUM
     C                     KFLD           @PSESN
     C                     KFLD           @KBLTY
     C                     KFLD           @PPTFR
     C                     KFLD           @KBEDT
     C*
     C* RECEIVE INPUT PARAMETERS
     C*
     C           *ENTRY    PLIST
     C                     PARM           @PBRCD  3
     C                     PARM           @PCNUM  6
     C                     PARM           @PSESN 10
     C                     PARM           @PPTFR  4
     C                     PARM           @PPSBI  1
     C                     PARM           @PSSBI  1
     C*
     C                     MOVEL'N'       @PPSBI
     C                     MOVEL'N'       @PSSBI
     C*
     C**   CHECK PARAMETERS EXIST
     C*
     C           @PBRCD    IFEQ *BLANK                     *B1
     C           @PCNUM    OREQ *BLANK
     C           @PSESN    OREQ *BLANK
     C                     MOVE '0'       *INLR
     C                     RETRN
     C                     END                             *E1
     C*
     C**   RETRIEVE RUN DAY NUMBER IN DATA AREA RUNDAT
     C*
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C** P002      - MAIN PROCESSING                                     *
     C**                                                                 *
     C** CALLS     - NONE                                                *
     C**                                                                 *
     C** CALLED BY - MAINLINE                                            *
     C**                                                                 *
     C********************************************************************
     C           P002      BEGSR
     C*
     C                     MOVE @PBRCD    @KBRCD
     C                     MOVE @PCNUM    @KCNUM
     C                     Z-ADDAGRDNB    @KBEDT
     C*
     C           @PPTFR    IFEQ *BLANKS                    *B0
     C           @PPTFR    OREQ '9998'
     C           @PPTFR    OREQ '9999'
     C*
     C                     MOVEL'P'       @KBLTY
     C           @KEY1     SETLLSEBKALL4
     C                     MOVEA'00'      *IN,01                          CSE007
     C                     READ SEBKALL4            N    41
      *                                                                   CSE007
      ***  If 02 ON = format SEERALD4 read, reset field XXBSTT: Block     CSE007
      ***  from Date/Blocked Start Date.                                  CSE007
      *                                                                   CSE007
     C           *IN02     IFEQ '1'                                       CSE007
     C                     Z-ADD0         XXBSTT                          CSE007
     C                     ENDIF                                          CSE007
     C*
     C           *IN41     IFEQ '0'                        *B1
     C           XXBRCD    ANDEQ@KBRCD
     C           XXCNUM    ANDEQ@KCNUM
     C           XXSESN    ANDEQ@PSESN
     C           XXBLTY    ANDEQ@KBLTY
     C           XXBSTT    ANDLE@KBEDT                                    CSE007
     C           XXBEDT    ANDGE@KBEDT                                    CSE007
     C                     MOVEL'Y'       @PPSBI
     C                     END                             *E1
     C*
     C                     MOVEL'S'       @KBLTY
     C           @KEY1     SETLLSEBKALL4
     C                     MOVEA'00'      *IN,01                          CSE007
     C                     READ SEBKALL4            N    41
      *                                                                   CSE007
      ***  If 02 ON = format SEERALD4 read, reset field XXBSTT: Block     CSE007
      ***  from Date/Blocked Start Date.                                  CSE007
      *                                                                   CSE007
     C           *IN02     IFEQ '1'                                       CSE007
     C                     Z-ADD0         XXBSTT                          CSE007
     C                     ENDIF                                          CSE007
     C*
     C           *IN41     IFEQ '0'                        *B1
     C           XXBRCD    ANDEQ@KBRCD
     C           XXCNUM    ANDEQ@KCNUM
     C           XXSESN    ANDEQ@PSESN
     C           XXBLTY    ANDEQ@KBLTY
     C           XXBSTT    ANDLE@KBEDT                                    CSE007
     C           XXBEDT    ANDGE@KBEDT                                    CSE007
     C                     MOVEL'Y'       @PSSBI
     C                     END                             *E1
     C*
     C                     ELSE                            *X0
     C*
     C                     MOVEL'P'       @KBLTY
     C           @KEY2     SETLLSEBKALL3
     C                     MOVEA'00'      *IN,01                          CSE007
     C                     READ SEBKALL3            N    41
      *                                                                   CSE007
      ***  If 01 ON = format SEERALD0 read, reset field XXBSTT: Block     CSE007
      ***  from Date/Blocked Start Date.                                  CSE007
      *                                                                   CSE007
     C           *IN01     IFEQ '1'                                       CSE007
     C                     Z-ADD0         XXBSTT                          CSE007
     C                     ENDIF                                          CSE007
     C*
     C           *IN41     IFEQ '0'                        *B1
     C           XXBRCD    ANDEQ@KBRCD
     C           XXCNUM    ANDEQ@KCNUM
     C           XXSESN    ANDEQ@PSESN
     C           XXBLTY    ANDEQ@KBLTY
     C           XXPTFR    ANDEQ@PPTFR
     C           XXBSTT    ANDLE@KBEDT                                    CSE007
     C           XXBEDT    ANDGE@KBEDT                                    CSE007
     C                     MOVEL'Y'       @PPSBI
     C                     END                             *E1
     C*
     C                     MOVEL'S'       @KBLTY
     C           @KEY2     SETLLSEBKALL3
     C                     MOVEA'00'      *IN,01                          CSE007
     C                     READ SEBKALL3            N    41
      *                                                                   CSE007
      ***  If 01 ON = format SEERALD0 read, reset field XXBSTT: Block     CSE007
      ***  from Date/Blocked Start Date.                                  CSE007
      *                                                                   CSE007
     C           *IN01     IFEQ '1'                                       CSE007
     C                     Z-ADD0         XXBSTT                          CSE007
     C                     ENDIF                                          CSE007
     C*
     C           *IN41     IFEQ '0'                        *B1
     C           XXBRCD    ANDEQ@KBRCD
     C           XXCNUM    ANDEQ@KCNUM
     C           XXSESN    ANDEQ@PSESN
     C           XXBLTY    ANDEQ@KBLTY
     C           XXPTFR    ANDEQ@PPTFR
     C           XXBSTT    ANDLE@KBEDT                                    CSE007
     C           XXBEDT    ANDGE@KBEDT                                    CSE007
     C                     MOVEL'Y'       @PSSBI
     C                     END                             *E1
     C*
     C                     END                             *E0
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**                                                                 *
     C** *PSSR     - PROGRAM ERROR HANDLING ROUTINE                      *
     C**                                                                 *
     C** CALLS     - NONE                                                *
     C**                                                                 *
     C** CALLED BY -                                                     *
     C**                                                                 *
     C********************************************************************
     C           *PSSR     BEGSR
     C*
     C                     DUMP
     C*
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C*
     C                     RETRN
     C*
     C                     ENDSR
     C********************************************************************
      /EJECT
** CPY@
(c) Finastra International Limited 2001
