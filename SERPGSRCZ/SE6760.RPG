     H        1                                                           S01496
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE ER Process Matured Blocks')
     H***********                                                         S01496
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module                        *
     F*                                                               *
     F*  SE6760 - SE Process General Matured Blocks                   *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL014             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CEU017             Date 03Avr98               *
      *                 CSE007             Date 17Dec97               *
     F*                 S01496             Date 04Oct94               *
     F*                 S10978             Date 29Apr93               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CGL014 - Collaterals Processing (Recompile)                  *
     F*  CEU017 - Securities Redenomination Euro Changes              *
     F*  CSE007 - Corporate Actions                                   *
     F*  S01496 - Incorporation of JYSKE Enhancements (S10978)        *
     F*  S10978 - JYSZRH: EARLY REDEMPTIONS                           *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
      /EJECT
     F*
     F**   SE BLOCKED POSITION Upd Idx  PREFIX - VD
     FSEBLKPL0UF  E           K        DISK         KINFSR *PSSR
     F            SEBLKPD0                          KRENAMESEBLKPDU
     F*
     F**   SE BLOCKED POSITION          PREFIX - VD
     FSEBLKPPDIF  E           K        DISK         KINFSR *PSSR
      *                                                                   CSE007
      ***  Midas SE Corporate Actions Blocked Positions file - Prefix MU  CSE007
     FSECOBKPDIF  E                    DISK         KINFSR *PSSR          CSE007
      *                                                                   CSE007
      ***  Midas SE Corporate Actions Blocked Positions file - Prefix MU  CSE007
      ***  Update Index.                                                  CSE007
     FSECOBKL0UF  E           K        DISK         KINFSR *PSSR          CSE007
     F            SECOBKD0                          KRENAMESECOBKDU       CSE007
     F*
     F********************************************************************
      /EJECT
     F********************************************************************
     F**                                                                 *
     F** ID F  C  H  L    FUNCTION OF INDICATORS                         *
     F**                 ------------------------                        *
     F**                                                                 *
     F*********41*-*READ**INDICATOR*ON*PF/SEBLKPPD************************CSE007
     F**       41 - Read indicatore on PF/SEBLKPPD and PF/SECOBKPD       *CSE007
     F*********42*-*CHAIN*INDICATOR*ON*LF/SEBLKPL0************************CSE007
     F**       42 - Chain indicator on LF/SEBLKPL0 and LF/SECOBKL0       *CSE007
     F**                                                                 *
     F********************************************************************
      /EJECT
     E********************************************************************
     E*
     E**   ARRAY FOR COPYRIGHT
     E                    CPY@    1   1 80
     E********************************************************************
      /EJECT
     I********************************************************************
     I*
     I**   DATA STRUCTURE FOR COMPILATION - COPYRIGHT INSERTION
     ICPYR@$      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@$$
     I*
     I**   LOCAL DATA AREA FOR DATA BASE ERRORS
     ILDA         DS                            256
     I                                      132 183 WWLOT
     I                                      132 141 WWFILE
     I                                      142 170 WWKEY
     I                                      171 180 WWPGM
     I                                      181 1830WWASE
     I*
     I********************************************************************
      /EJECT
     C********************************************************************
     C*
     C**              START MAINLINE
     C**             ----------------
     C*
     C**   INITIAL PROCESSING
     C                     EXSR P001
     C*
     C**   MAIN PROCESSING
     C                     READ SEBLKPPD                 41
     C*
     C**   DO WHILE NOT END OF FILE
     C           *IN41     DOWEQ'0'                        B*1
     C                     EXSR P002
     C                     END                             E*1
      *                                                                   CSE007
      ***  Read Corporate Actions Blocked Positions file.                 CSE007
      *                                                                   CSE007
     C                     READ SECOBKPD                 41               CSE007
      *                                                                   CSE007
      ***  Do while not end of PF/SECOBKPD file.                          CSE007
      *                                                                   CSE007
     C           *IN41     DOWEQ'0'                                       CSE007
     C                     EXSR P003                                      CSE007
     C                     ENDDO                                          CSE007
     C*
     C**   END PROGRAM
     C                     MOVE '1'       *INLR
     C                     RETRN
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C**                                                                 *
     C**          INDEX TO SUBROUTINES                                   *
     C**         ----------------------                                  *
     C**                                                                 *
     C**   1.  P001   - INITIAL PROCESSING                               *
     C**   2.  P002   - MAIN PROCESSING                                  *
     C**   3.  P003   - Process Corporate Actions Blocked Position.      *CSE007
     C**   5.  #PSSR  - PROGRAM ERROR HANDLING ROUTINE                   *
     C**                                                                 *
     C********************************************************************
      /EJECT
     C********************************************************************
     C**                                                                 *
     C** P001      - INITIAL PROCESSING                                  *
     C**                                                                 *
     C** CALLS     -                                                     *
     C**                                                                 *
     C** CALLED BY - MAINLINE                                            *
     C**                                                                 *
     C********************************************************************
     C           P001      BEGSR
     C*                                                                   S01496
     C**  Copyright statement                                             S01496
     C*                                                                   S01496
     C                     MOVEACPY@      ACT@   80                       S01496
     C*
     C**   INITIALISE DB ERROR FIELDS ETC, AND STORE PROGRAM NAME FOR
     C**   DB ERROR FIELD.
     C                     MOVE *BLANKS   DBFILE 10
     C                     MOVE *BLANKS   DBKEY  29
     C                     MOVE *BLANKS   DBASE   30
     C                     MOVEL'SE6760'  DBPGM  10
     C*
     C**   KEY LIST FOR LF/SEBLKPL0
     C           @KEY1     KLIST
     C                     KFLD           VDBRCD
     C                     KFLD           VDCNUM
     C                     KFLD           VDPTFR
     C                     KFLD           VDBLTY
     C                     KFLD           VDSESN
     C                     KFLD           VDBEDT
     C                     KFLD           VDSEQN
      *                                                                   CSE007
      ***  Key list for LF/SECOBKL0.                                      CSE007
      *                                                                   CSE007
     C           @KEY2     KLIST                                          CSE007
     C                     KFLD           MUBRCD                          CSE007
     C                     KFLD           MUCNUM                          CSE007
     C                     KFLD           MUPTFR                          CSE007
     C                     KFLD           MUBLTY                          CSE007
     C                     KFLD           MUSESN                          CSE007
     C                     KFLD           MUBEDT                          CSE007
     C                     KFLD           MUSEQN                          CSE007
     C*
     C                     ENDSR
     C********************************************************************
      /EJECT
     C********************************************************************
     C**                                                                 *
     C** P002      - MAIN PROCESSING                                     *
     C**                                                                 *
     C** CALLS     - NONE                                                *
     C**                                                                 *
     C** CALLED BY - MAINLINE                                            *
     C**                                                                 *
     C********************************************************************
     C           P002      BEGSR
     C*
     C**   ACCESS FILE LF/SEBLKPL0
     C           @KEY1     CHAINSEBLKPL0             42
     C*
     C**   IF RECORD IS FOUND
     C           *IN42     IFEQ '0'                        B*1
     C                     MOVEL'M'       VDRECI
     C                     UPDATSEBLKPDU
     C                     END                             E*1
     C*
     C**   READ NEXT RECORD
     C                     READ SEBLKPPD                 41
     C*
     C                     ENDSR
     C********************************************************************
      /EJECT                                                              CSE007
      *****************************************************************   CSE007
      *                                                               *   CSE007
      * P003  - Process Corporate Actions Blocked Postion.            *   CSE007
      *                                                               *   CSE007
      * Called By: Main Processing.                                   *   CSE007
      *                                                               *   CSE007
      * Calls: None.                                                  *   CSE007
      *                                                               *   CSE007
      *****************************************************************   CSE007
      *                                                                   CSE007
     C           P003      BEGSR                           ** P003 SR **  CSE007
      *                                                                   CSE007
      ***  Access file LF/SECOBKL0.                                       CSE007
      *                                                                   CSE007
     C           @KEY2     CHAINSECOBKL0             42                   CSE007
      *                                                                   CSE007
      ***  If record is found.                                            CSE007
      *                                                                   CSE007
     C           *IN42     IFEQ '0'                                       CSE007
     C                     MOVEL'M'       MURECI                          CSE007
     C                     UPDATSECOBKDU                                  CSE007
     C                     ENDIF                                          CSE007
      *                                                                   CSE007
      ***  Read next record in PF/SECOBKPD.                               CSE007
      *                                                                   CSE007
     C                     READ SECOBKPD                 41               CSE007
      *                                                                   CSE007
     C                     ENDSR                                          CSE007
      *                                                                   CSE007
      ****************************************************************    CSE007
      /EJECT
     C********************************************************************
     C**                                                                 *
     C** *PSSR     - PROGRAM ERROR HANDLING ROUTINE                      *
     C**                                                                 *
     C** CALLS     - NONE                                                *
     C**                                                                 *
     C** CALLED BY -                                                     *
     C**                                                                 *
     C********************************************************************
     C           *PSSR     BEGSR
     C*
     C**   UPDATE LDA WITH ERROR INFORMATION
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVELDBFILE    WWFILE
     C                     MOVELDBKEY     WWKEY
     C                     MOVELDBPGM     WWPGM
     C                     MOVELDBASE     WWASE
     C                     OUT  LDA
     C*
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C*
     C                     RETRN
     C*
     C                     ENDSR
     C********************************************************************
      /EJECT
     C********************************************************************
** CPY@
(c) Misys International Banking Systems Ltd. 2001
