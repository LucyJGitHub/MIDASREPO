     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE ER Drop of Early Redemptions')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module                        *
     F*                                                               *
     F*  SE6764 - SE Drop of Early Redemptions                        *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01496             Date 04Oct94               *
     F*                 S10978             Date 19MAY93               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*  S01496 - Incorporation of JYSKE Enhancements (S10978)        *
     F*  S10978 - JYSZRH: EARLY REDEMPTIONS                           *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
      /EJECT
     F*
     F*****BJ*-*SD*Bank*Details*-*Rtv*Idx******************************   S01496
     F***SDBANKL1IF**E           K        DISK                            S01496
     F***********                                   KINFSR *PSSR          S01496
     F*
     F*****BV*-*SE*ICD**********-*Rtv*Idx******************************   S01496
     F***SDSTRDL1IF**E           K        DISK                            S01496
     F***********                                   KINFSR *PSSR          S01496
     F*
     F**   VA - SE ER References - Upd Idx
     FSEERRFL0UF  E           K        DISK
     F                                              KINFSR *PSSR
     F*
     F**   VB - SE ER Depot      - Upd Idx
     FSEERDPL0UF  E           K        DISK
     F                                              KINFSR *PSSR
     F*
     F**   VC - SE ER Allocations - Upd Idx
     FSEERALL0UF  E           K        DISK
     F                                              KINFSR *PSSR
     F*
     F**   Printer file
     F***SE6764P1O***E             70     PRINTER                         S01496
     FSE6764P1O   E             70     PRINTER      KINFDS SPOOL      UC  S01496
     F                                              KINFSR *PSSR
     FSE6764AUO   E                    PRINTER      KINFDS SPOOLU         S01496
     F                                              KINFSR *PSSR          S01496
     F/COPY WNCPYSRC,SE6764F001
     ******************************************************************
      /EJECT
     *********************************************************************
      *
      * ID F  C  H  L    Function of indicators
      * ---------------------------------------
      *
      *   70 - Page Overflow
      *
      *   87,88 - Work Indicator
      *
      *   89 - Record not Found
      *
      *   LR :    Last record
      *   U7/U8 : Database error
      *
      *   90-99 : Used by Standard Subroutines
     *********************************************************************
      /EJECT
     *********************************************************************
     C**
     C** Name of the Subroutines
     C** -----------------------
     C**
     C**   P001 - INITIAL PROCESSING
     C**   P002 - MAIN PROCESSING
     C**   P003 - TERMINATION PROCESSING
     C**
     C**   P005 - DELETE ALL DEPOT RECORDS
     C**   P006 - DELETE ALL ALLOCATION RECORDS
     C**
     C**   R001 - OUTPUT DETAILS TO PRINTER FILE
     C**
     C**  *PSSR - STANDARD DATA BASE ERROR PROCESSING
     C**
     C** Name of the Standard Subroutines
     C** --------------------------------
     C**
     C**   ZDATE2 - Dates Validation and Conversion
     C**
     *********************************************************************
      /EJECT
     *********************************************************************
     E*
     E**   For Compilation - Copyright Insertion
     E*
     E                    CPY@    1   1 80
     E*
     E/COPY ZSRSRC,ZDATE2Z1
     *********************************************************************
      /EJECT
     *********************************************************************
     I*
     I**   Data Structure for Compilation - Copyright Insertion
     I*
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I*                                                                   S01496
     ISPOOLU      DS                                                      S01496
     I** File Information Data Structure (Audit Report)                   S01496
     I*                                                                   S01496
     I                                       83  92 SFILEU                S01496
     I                                    B 123 1240SFNUMU                S01496
     ISPOOL       DS                                                      S01496
     I** File Information Data Structure (Detail Report)                  S01496
     I*                                                                   S01496
     I                                       83  92 SFILE                 S01496
     I                                    B 123 1240SFNUM                 S01496
     I*
     I**   Data Structure for Database Error
     I*
     ILDA         DS                            256
     I                                      132 183 WWLOT
     I                                      132 141 WWFILE
     I                                      142 170 WWKEY
     I                                      171 180 WWPGM
     I                                      181 1830WWASE
     I*                                                                   S01496
     IRUNDAT    E DS                                                      S01496
     I** Standard data area layout for system flags and run date          S01496
     I*                                                                   S01496
     I@BANK     E DSSDBANKPD                                              S01496
     I** Dummy record format for access to Bank Details                   S01496
     I*                                                                   S01496
     I@STRD     E DSSDSTRDPD                                              S01496
     I** Dummy record format for access to Securities Trading Data        S01496
     I*                                                                   S01496
     IDSFDY     E DSDSFDY                                                 S01496
     I** First DS for access programs, short data structure               S01496
     I*                                                                   S01496
     IDSSDY     E DSDSSDY                                                 S01496
     I** Second DS for access programs, long data structure               S01496
     I*                                                                   S01496
      ********************************************************************
      /EJECT
     C********************************************************************
     C**    P R O G R A M   S T A R T
     C********************************************************************
     C*
     C**   Initial Processing
     C*
     C                     EXSR P001
     C*
     C**   Main Processing
     C*
     C                     EXSR P002
     C*
     C**   Termination Processing
     C*
     C                     EXSR P003
     C*
     C********************************************************************
     C**    P R O G R A M     E N D
     *********************************************************************
      /EJECT
     *********************************************************************
     C**  P001 - INITIAL PROCESSING
     *********************************************************************
     C           P001      BEGSR
     C*                                                                   S01496
     C**  Copyright statement                                             S01496
     C*                                                                   S01496
     C                     MOVEACPY@      ACT@   80                       S01496
     C*                                                                   S01496
     C** Check multibranching indicator.                                  S01496
     C*                                                                   S01496
     C           *NAMVAR   DEFN           RUNDAT                          S01496
     C                     IN   RUNDAT                                    S01496
     C           AGMBIN    IFEQ 'Y'                                       S01496
     C                     MOVE '1'       *IN39                           S01496
     C                     END                                            S01496
     C*
     C** Initialise DB error fields and store program name
     C*
     C                     MOVE *BLANKS   WWLOT
     C                     MOVEL'SE6764'  DBPGM  10
     C*
     C** Initialise key and work fields
     C*
     C                     Z-ADD0         WWNERD  50       Nbr ER dr
     C*
     C***Access*to*'Bank*Details'*LF/SDBANKL1*to*retrieve*the*MIDAS****   S01496
     C** Access to SDBANKPD to retrieve the MIDAS                         S01496
     C** Run Date and User Report Title
     C*
     C************LOVAL    SETLLSDBANKL1                                  S01496
     C***********          READ SDBANKL1                 89               S01496
     C*                                                                   S01496
     C                     CALL 'AOBANKR0'                                S01496
     C                     PARM '*MSG    '@RTCD   7                       S01496
     C                     PARM '*FIRST  '@OPTN   7                       S01496
     C           @BANK     PARM @BANK     DSFDY                           S01496
     C*
     C** If record not found
     C*
     C************IN89     IFEQ '1'                                       S01496
     C           @RTCD     IFNE *BLANK                                    S01496
     C           BJTYLC    OREQ 'D'
     C                     Z-ADD1         DBASE   30
     C                     MOVEL*BLANKS   DBFILE 10        *****************
     C***********          MOVEL'SDBANKL1'DBFILE           ** DB ERROR 01 S01496
     C                     MOVE 'SDBANKPD'DBFILE                          S01496
     C                     MOVEL*BLANKS   DBKEY  29        *****************
     C***********          WRITESE6764H1                                  S01496
     C***********          WRITESE6764H2                                  S01496
     C                     EXSR *PSSR
     C                     END
     C*
     C** Determine Date Format
     C*
     C           BJDFIN    IFEQ 'M'                        *B1
     C                     MOVE '1'       *IN98
     C                     END                             *E1
     C*
     C***Access*to*ICD*LF/SDSTRDL1*to*retrieve*Retention*Period********   S01496
     C** Access to SDSTRDPD to retrieve Retention Period.                 S01496
     C*
     C************LOVAL    SETLLSDSTRDL1                                  S01496
     C***********          READ SDSTRDL1                 89               S01496
     C*                                                                   S01496
     C                     CALL 'AOSTRDR0'                                S01496
     C                     PARM '*MSG    '@RTCD   7                       S01496
     C                     PARM '*FIRST  '@OPTN   7                       S01496
     C           @STRD     PARM @STRD     DSSDY                           S01496
     C*
     C** If record not found
     C*
     C************IN89     IFEQ '1'                                       S01496
     C           @RTCD     IFNE *BLANK                                    S01496
     C                     Z-ADD2         DBASE
     C                     MOVEL*BLANKS   DBFILE           *****************
     C***********          MOVEL'SDSTRDL1'DBFILE           ** DB ERROR 02 S01496
     C                     MOVE 'SDSTRDPD'DBFILE                          S01496
     C                     MOVEL*BLANKS   DBKEY            *****************
     C***********          WRITESE6764H1                                  S01496
     C***********          WRITESE6764H2                                  S01496
     C                     EXSR *PSSR
     C                     END
     C/COPY WNCPYSRC,SE6764C001
     C*
     C** Calculate Retention Date
     C*
     C           31        MULT BVROST    WWFLD1  50
     C           BJRDNB    SUB  WWFLD1    RTNTDT  50
     C*
     C***Output*title*to*report****************************************   S01496
     C***********                                                         S01496
     C***********          WRITESE6764H1                                  S01496
     C***********          WRITESE6764H2                                  S01496
     C                     EXSR RCFAU                                     S01496
     C*
     C                     ENDSR
     *********************************************************************
      /EJECT
     *********************************************************************
     C**  P002 - MAIN PROCESSING
     *********************************************************************
     C           P002      BEGSR
     C*
     C** Read first record in LF/SEERRFL0
     C*
     C                     READ SEERRFL0                 88
     C*
     C** Do while end of file is not reached
     C*
     C           *IN88     DOWEQ'0'                        *B1
     C*
     C** If Redemption Date is lower than or equal to Retention Date
     C*
     C           VAERRD    IFLE RTNTDT                     *B2
     C*
     C** Delete all Depot records
     C*
     C                     EXSR P005
     C*
     C** Delete all Allocation records
     C*
     C                     EXSR P006
     C*                                                                   S01496
     C**  Open SE6764P1.                                                  S01496
     C*                                                                   S01496
     C           *IN41     IFEQ '0'                                       S01496
     C                     OPEN SE6764P1                                  S01496
     C                     EXSR RCFP1                                     S01496
     C                     WRITESE6764H1                                  S01496
     C                     WRITESE6764H2                                  S01496
     C                     MOVE '1'       *IN41                           S01496
     C                     END                                            S01496
     C*
     C** If the OVERFLOW INDICATOR is set ON output title to report
     C*
     C           *IN70     IFEQ '1'                        *B3
     C                     WRITESE6764H1
     C                     WRITESE6764H2
     C                     MOVE '0'       *IN70
     C                     ENDIF                           *E3
     C*
     C** Output a detail line
     C*
     C                     EXSR R001
     C/COPY WNCPYSRC,SE6764C002
     C*
     C** Delete the Early Redemption Reference record in LF/SEERRFL0
     C*
     C                     DELETSEERRFD0
     C*
     C                     ADD  1         WWNERD
     C*
     C                     ENDIF                           *E2
     C*
     C** Read next record in LF/SEERRFL0
     C*
     C                     READ SEERRFL0                 88
     C*
     C                     ENDDO                           *E1
     C*
     C                     ENDSR
     *********************************************************************
      /EJECT
     *********************************************************************
     C**  P003 - TERMINATION PROCESSING
     *********************************************************************
     C           P003      BEGSR
     C*                                                                   S01496
     C**  Produce Audit List.                                             S01496
     C*                                                                   S01496
     C                     WRITEHEADAU                                    S01496
     C                     Z-ADDWWNERD    PPNERD                          S01496
     C                     WRITEDETAILAU                                  S01496
     C*
     C** If no detail record was printed during detail processing
     C** write empty record
     C*
     C           WWNERD    IFEQ *ZERO
     C***********          WRITESE6764D2                                  S01496
     C                     WRITENODTLS                                    S01496
     C                     ENDIF
     C*
     C** Write end of report
     C*
     C           *IN41     IFEQ '1'                                       S01496
     C***********          Z-ADDWWNERD    PPNERD                          S01496
     C                     WRITESE6764E1
     C                     CLOSESE6764P1                                  S01496
     C                     END                                            S01496
     C/COPY WNCPYSRC,SE6764C003
     C*
     C** End of program
     C*
     C                     MOVE '1'       *INLR
     C                     RETRN
     C*
     C                     ENDSR
     *********************************************************************
      /EJECT
     *********************************************************************
     C**  P005 - DELETE ALL DEPOT RECORDS
     *********************************************************************
     C           P005      BEGSR
     C*
     C** Read first Depot record for current ER Reference
     C** in LF/SEERDPL0
     C*
     C           VAERRF    SETLLSEERDPL0
     C           VAERRF    READESEERDPL0                 87
     C*
     C** Do while end of file and end of ER Reference is not reached
     C*
     C           *IN87     DOWEQ'0'                        *B1
     C*
     C** Delete the Early Redemption Depot record in LF/SEERDPL0
     C*
     C                     DELETSEERDPD0
     C*
     C** Read next Depot record for current ER Reference
     C** in LF/SEERDPL0
     C*
     C           VAERRF    READESEERDPL0                 87
     C*
     C                     ENDDO                           *E1
     C*
     C                     ENDSR
     *********************************************************************
      /EJECT
     *********************************************************************
     C**  P006 - DELETE ALL ALLOCATION RECORDS
     *********************************************************************
     C           P006      BEGSR
     C*
     C** Read first Allocation record for current ER Reference
     C** in LF/SEERALL0
     C*
     C           VAERRF    SETLLSEERALL0
     C           VAERRF    READESEERALL0                 87
     C*
     C** Do while end of file and end of ER Reference is not reached
     C*
     C           *IN87     DOWEQ'0'                        *B1
     C*
     C** Delete the Early Redemption Allocation record in LF/SEERALL0
     C*
     C                     DELETSEERALD0
     C*
     C** Read next Allocation record for current ER Reference
     C** in LF/SEERALL0
     C*
     C           VAERRF    READESEERALL0                 87
     C*
     C                     ENDDO                           *E1
     C*
     C                     ENDSR
     *********************************************************************
      /EJECT
     *********************************************************************
     C**  R001 - OUTPUT DETAILS TO PRINTER FILE
     *********************************************************************
     C           R001      BEGSR
     C*
     C** ER Reference Number
     C                     MOVELVAERRF    PPERRF
     C** Security Shortname
     C                     MOVELVASESN    PPSESN
     C** ER Ex-Date
     C                     MOVE VAEREX    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    PPEREX
     C** ER Redemption Date
     C                     MOVE VAERRD    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    PPERRD
     C** Payment Date
     C                     MOVE VAERPD    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    PPERPD
     C** ER Last Allocation Date
     C                     MOVE VAERAD    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    PPERAD
     C** Final Allocation Indicator
     C                     MOVELVAFAID    PPFAID
     C** Early Redemption Status
     C                     MOVELVAERST    PPERST
     C** ER Correction of Reference
     C                     MOVELVACORF    PPCORF
     C** Redemption Price
     C                     Z-ADDVAREDP    PPREDP
     C** Redemption Narrative
     C                     MOVELVARNAR    PPRNAR
     C*
     C** Write a record into the printer file
     C*
     C                     WRITESE6764D1
     C*
     C                     ENDSR
     *********************************************************************
      /EJECT
     *********************************************************************
     C**  *PSSR  - STANDARD DATA BASE ERROR PROCESSING
     *********************************************************************
     C           *PSSR     BEGSR
     C*
     C** Update LDA with Error Information
     C*
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVELDBFILE    WWFILE
     C                     MOVELDBKEY     WWKEY
     C                     MOVELDBPGM     WWPGM
     C                     MOVELDBASE     WWASE
     C                     OUT  LDA
     C*
     C                     WRITEHEADAU                                    S01496
     C***********          WRITESE6764E2                                  S01496
     C                     WRITEERRORAU                                   S01496
     C*                                                                   S01496
     C           *IN41     IFEQ '1'                                       S01496
     C                     WRITEDBERROR                                   S01496
     C                     END                                            S01496
     C*
     C                     DUMP
     C*
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C*
     C                     RETRN
     C*
     C                     ENDSR
     *********************************************************************
      /EJECT
     C*****************************************************************   S01496
     C*                                                                   S01496
     C*  RCFP1 -- Subroutine to register P1 report via RCF                S01496
     C*                                                                   S01496
     C*****************************************************************   S01496
     C*                                                                   S01496
     C           RCFP1     BEGSR                            ** RCFP1 **   S01496
     C*                                                                   S01496
     C**  Ensure report spool file is recorded by RCF.                    S01496
     C*                                                                   S01496
     C                     Z-ADDSFNUM     ZSNUM   60                      S01496
     C                     CALL 'ZSFILE'                                  S01496
     C                     PARM           SEQ     5                       S01496
     C                     PARM *BLANKS   @PARM   3                       S01496
     C                     PARM           SFILE                           S01496
     C                     PARM           ZSNUM                           S01496
     C                     PARM           ZSERR   1                       S01496
     C*                                                                   S01496
     C**  Error during ZSFILE processing, return to calling program.      S01496
     C*                                                                   S01496
     C           ZSERR     IFEQ 'Y'                                       S01496
     C                     SETON                     U7U8LR               S01496
     C                     RETRN                                          S01496
     C                     END                                            S01496
     C*                                                                   S01496
     C                     ENDSR                                          S01496
     C*                                                                   S01496
     C*****************************************************************   S01496
     C/EJECT                                                              S01496
     C*****************************************************************   S01496
     C*                                                                   S01496
     C*  RCFAU -- Subroutine to register AU report via RCF                S01496
     C*                                                                   S01496
     C*****************************************************************   S01496
     C*                                                                   S01496
     C           RCFAU     BEGSR                            ** RCFAU **   S01496
     C*                                                                   S01496
     C**   Ensure audit spool file is recorded by RCF.                    S01496
     C*                                                                   S01496
     C                     Z-ADDSFNUMU    ZSNUMU  60                      S01496
     C                     CALL 'ZSFILE'                                  S01496
     C                     PARM           SEQ                             S01496
     C                     PARM *BLANKS   @PARM                           S01496
     C                     PARM           SFILEU                          S01496
     C                     PARM           ZSNUMU                          S01496
     C                     PARM           ZSERR                           S01496
     C*                                                                   S01496
     C**  Error during ZSFILE processing, return to calling program.      S01496
     C*                                                                   S01496
     C           ZSERR     IFEQ 'Y'                                       S01496
     C                     SETON                     U7U8LR               S01496
     C                     RETRN                                          S01496
     C                     END                                            S01496
     C*                                                                   S01496
     C                     ENDSR                                          S01496
     C*                                                                   S01496
     C*****************************************************************   S01496
     C/EJECT                                                              S01496
     C********************************************************************
     C/COPY ZSRSRC,ZDATE2Z2
**   CPY@
(c) Finastra International Limited 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
