     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE ER Generate Forward Events Cust/Book')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module                        *
     F*                                                               *
     F*  SE6780 - Generate Forward Events Cust/Book Allocation        *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      *  Last Amend No. 241795             Date 13Sep06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CSE071             Date 19Jul05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAC005             Date 23Nov98               *
      *                 CEU011             Date 11Mar98               *
      *                 S01496             Date 26Oct94               *
     F*                 S10978             Date 28Apr93               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  241795 - Populate ECSS whether CEU011 is switched on or off. *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAC005 - Profit Centre Accounting - SE Enhancement           *
      *  CEU011 - EMU Position/Risk Reporting                         *
     F*  S01496 - Incorporation of Jyske Enhancements (S10978)        *
     F*  S10978 - JYSZRH: EARLY REDEMPTIONS                           *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
      /EJECT
     F*
     F*****BJ*-*Bank*Details*-*Rtv*Idx*********************************   S01496
     F***SDBANKL1IF**E           K        DISK                            S01496
     F***********                                   KINFSR *PSSR          S01496
     F*
     F**   SE Trade Events
     FTREVED  O   E                    DISK
     F                                              KINFSR *PSSR
     F*
     F**   SE Trade Events - Audit
     FTREVEA  UF  E                    DISK                      A
     F                                              KINFSR *PSSR
     F*
     F**   SE Portfolio Customer Trades Events
     FPCTEVD  O   E                    DISK
     F                                              KINFSR *PSSR
     F*
     F**   SE Portfolio Customer Trades Events - Audit
     FPCTEVA  UF  E                    DISK                      A
     F                                              KINFSR *PSSR
     F*
     F**   SE ER Generate Forward Events Cust/Book - Audit
     FSEERRFL2IF  E           K        DISK
     F                                              KINFSR *PSSR
     F*
     F**   SE ER Generate Forward Events Cust/Book - Audit
     FSEERALL1IF  E           K        DISK
     F                                              KINFSR *PSSR
     F*
     F*****Currency*Codes**********************************************   S01496
     F***SDCURRL0IF**E           K        DISK                            S01496
     F***********                                   KINFSR *PSSR          S01496
     F*
     F*****Client*Details**********************************************   S01496
     F***SDCUSTL0IF**E           K        DISK                            S01496
     F***********                                   KINFSR *PSSR          S01496
     F*
     F*****Branch*Codes************************************************   S01496
     F***SDBRCHL0IF**E           K        DISK                            S01496
     F***********                                   KINFSR *PSSR          S01496
     F*
     F*****Securities*Trading*ICD**************************************   S01496
     F***SDSTRDPDIF**E                    DISK                            S01496
     F***********                                   KINFSR *PSSR          S01496
     F*
     F**   Securities
     FSECTY   IF  E           K        DISK
     F                                              KINFSR *PSSR
     F*
     F**   SE ER Generate Forward Events Cust/Book - Audit
     FSE6780AUO   E                    PRINTER
     F                                              KINFSR *PSSR
     F**************************************************************************
      /EJECT
     E**************************************************************************
     E*
     E**   FOR COMPILATION - COPYRIGHT INSERTION
     E*
     E                    CPY@    1   1 80
     E*
     E                    POWER8  1   8  8 4             POWER8 ARRAY
     E**************************************************************************
      /EJECT
     I**************************************************************************
     I*
     I**   DATA STRUCTURE FOR COMPILATION - COPYRIGHT INSERTION
     I*
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I*
     I**   DATA STRUCTURE FOR DATABASE ERROR
     I*
     ILDA         DS                            256
     I                                      132 183 WWLOT
     I                                      132 141 WWFILE
     I                                      142 170 WWKEY
     I                                      171 180 WWPGM
     I                                      181 1830WWASE
     I*                                                                   S01496
     ISDBANK    E DSSDBANKPD                                              S01496
     I**  Data structure for bank details                                 S01496
     I*                                                                   S01496
     ISDBRCH    E DSSDBRCHPD                                              S01496
     I**  Data structure for branch details                               S01496
     I*                                                                   S01496
     ISDSTRD    E DSSDSTRDPD                                              S01496
     I**  Data structure for securities trading details                   S01496
     I*                                                                   S01496
     ISDCURR    E DSSDCURRPD                                              S01496
     I**  Data structure for currency details                             S01496
     I*                                                                   S01496
     ISDCUST    E DSSDCUSTPD                                              S01496
     I              QQDFAC                          QQDFA1                                    CGL029
     I**  Data structure for customer details                             S01496
      *                                                                   CEU011
     ISCSARD    E DSSCSARDPD                                              CEU011
      **  Switchable Features File                                        CEU011
     I*                                                                   S01496
     IDSFDY     E DSDSFDY                                                 CEU011
      ** First DS for access programs, short data structure               CEU011
      *                                                                   CEU011
     IDSSDY     E DSDSSDY                                                 S01496
     I**  Long data structure for access objects                          S01496
     I*                                                                   S01496
     *********************************************************************
      /EJECT
     C********************************************************************
     C**    P R O G R A M   S T A R T
     C********************************************************************
     C*
     C**   INITIAL PROCESSING
     C*
     C                     EXSR P001
     C*
     C**   MAIN PROCESSING
     C*
     C                     EXSR P002
     C*
     C**   TERMINATION PROCESSING
     C*
     C                     EXSR P003
     C*
     C********************************************************************
     C**    P R O G R A M     E N D
     C********************************************************************
      /EJECT
     ******************************************************************
     C* P001   : INITIAL PROCESSING                                   *
     ******************************************************************
     C*
     C           P001      BEGSR
     C*                                                                   S01496
     C**  Copyright statement                                             S01496
     C*                                                                   S01496
     C                     MOVEACPY@      ACT@   80                       S01496
     C*
     C**  INITIALISE KEY AND WORKING FIELDS
     C*
     C                     MOVE *BLANKS   WKSESN 10
     C                     MOVE *BLANKS   WKNMCY  3
     C***********          Z-ADD*ZEROS    WKBRCD  30                      S01496
     C                     MOVE *BLANKS   WKBRCD  3                       S01496
     C                     Z-ADD*ZEROS    OUTRTR  50
     C                     Z-ADD*ZEROS    OUTRPC  50
     C*
     C                     Z-ADD0         WTOTTR 150
     C                     Z-ADD0         WTOTPC 150
     C*
     C                     Z-ADD0         TRHRWN 150       Hash Total (Integer)
     C                     Z-ADD0         TRHRDC  50       Hash Total (Decimal)
     C                     Z-ADD0         PCHRWN 150       Hash Total (Integer)
     C                     Z-ADD0         PCHRDC  50       Hash Total (Decimal)
     C*
     C**  PREPARE LDA FOR THE DATABASE ERROR OUTPUT
     C*
     C                     MOVE *BLANKS   WWLOT
     C                     MOVEL'SE6780'  DBPGM  10
     C*
     C**   ACCESS TO BANK DETAILS TO RETRIEVE THE MIDAS RUN DATE
     C*
     C************LOVAL    SETLLSDBANKL1                                  S01496
     C***********          READ SDBANKL1                 89               S01496
     C*                                                                   S01496
     C                     CALL 'AOBANKR0'                                S01496
     C                     PARM *BLANKS   WLRTCD  7                       S01496
     C                     PARM '*FIRST'  WLOPTN  7                       S01496
     C           SDBANK    PARM SDBANK    DSSDY                           S01496
     C*
     C**   IF RECORD NOT FOUND
     C*
     C************IN89     IFEQ '1'                                       S01496
     C           WLRTCD    IFNE *BLANKS                                   S01496
     C                     MOVEL'001'     DBASE   30       *****************
     C***********          MOVEL'SDBANKL1'DBFILE 10        ** DB ERROR 01 S01496
     C                     MOVE 'SDBANKPD'DBFILE 10        * DB ERROR 01 *S01496
     C                     MOVEL*BLANKS   DBKEY  29        *****************
     C                     EXSR *PSSR
     C                     END
     C*
     C**  WRITE CONTROL REPORT HEADINGS
     C*
     C                     WRITEHEADER
     C*
     C*****ACCESS*TO*BANK*DETAILS*TO*RETRIEVE*THE*MIDAS*RUN*DATE*******   S01496
     C*
     C***********          READ SDSTRDPD                 89               S01496
     C*                                                                   S01496
     C**  Access securities trading details using access object           S01496
     C*                                                                   S01496
     C                     CALL 'AOSTRDR0'                                S01496
     C                     PARM *BLANKS   WLRTCD                          S01496
     C                     PARM '*FIRST'  WLOPTN                          S01496
     C           SDSTRD    PARM SDSTRD    DSSDY                           S01496
     C*
     C**   IF RECORD NOT FOUND
     C*
     C************IN89     IFEQ '1'                                       S01496
     C           WLRTCD    IFNE *BLANKS                                   S01496
     C                     MOVEL'002'     DBASE            *****************
     C***********          MOVEL'SDSTRDPD'DBFILE           ** DB ERROR 02 S01496
     C                     MOVE 'SDSTRDPD'DBFILE           * DB ERROR 02 *S01496
     C                     MOVEL*BLANKS   DBKEY            *****************
     C                     EXSR *PSSR
     C                     END
      *                                                                   CEU011
      ** Call Access pgm AOSARDR0 to check if CEU011 is ON.               CEU011
      *                                                                   CEU011
     C                     MOVE 'N'       CEU011  1                       CEU011
     C                     CALL 'AOSARDR0'                                CEU011
     C                     PARM *BLANKS   @RTCD   7                       CEU011
     C                     PARM '*VERIFY' @OPTN   7                       CEU011
     C                     PARM 'CEU011'  @SARD   6                       CEU011
     C           SCSARD    PARM SCSARD    DSFDY                           CEU011
      *                                                                   CEU011
     C           @RTCD     IFEQ *BLANKS                                   CEU011
     C                     MOVE 'Y'       CEU011                          CEU011
     C                     END                                            CEU011
      *                                                                   CEU011
     C           @RTCD     IFNE *BLANKS                                   CEU011
     C           @RTCD     ANDNE'*NRF   '                                 CEU011
      *                                                                   CEU011
      ** Call to program ended in error                                   CEU011
      *                                                                   CEU011
     C           *LOCK     IN   LDA                                       CEU011
     C                     MOVEL'CEU011'  DBKEY            ************   CEU011
     C                     MOVE 'SCSARDPD'DBFILE           * DBERR 08 *   CEU011
     C                     Z-ADD8         DBASE            ************   CEU011
     C                     MOVEL'SE6780'  DBPGM                           CEU011
     C                     OUT  LDA                                       CEU011
     C                     EXSR *PSSR                                     CEU011
     C                     ENDIF                                          CEU011
      *                                                                   CAC005
      ** Check if CAC005 is installed                                     CAC005
      *                                                                   CAC005
     C                     CALL 'AOSARDR0'                                CAC005
     C                     PARM *BLANKS   PRTCD   7                       CAC005
     C                     PARM '*VERIFY' POPTN   7                       CAC005
     C                     PARM 'CAC005'  PSARD   6                       CAC005
      *                                                                   CAC005
     C           PRTCD     IFEQ *BLANKS                                   CAC005
     C                     MOVE 'Y'       CAC005  1                       CAC005
     C                     ELSE                                           CAC005
     C                     MOVE 'N'       CAC005                          CAC005
     C                     ENDIF                                          CAC005
     C*
     C                     ENDSR
      *****************************************************************
      /EJECT
     ******************************************************************
     C* P002   : MAIN PROCESSING                                      *
     ******************************************************************
     C*
     C           P002      BEGSR
     C*
     C**  ACCESS FIRST RECORD IN ER REFERENCE FILE
     C*
     C           *LOVAL    SETLLSEERRFL2
     C                     READ SEERRFL2                 88
     C*
     C**  DO WHILE END OF FILE IS NOT REACHED
     C*
     C           *IN88     DOWEQ'0'                        B*1
     C*
     C**  BYPASS REVERSED RECORDS
     C*
     C           VAERST    IFNE 'R'                        B*2
     C*
     C**  IF SECURITY SHORTNAME IS NOT EQUAL TO SAVED SECURITY
     C**  EXECUTE SUBROUTINE SECTIN TO RETRIEVE SECURITY RECORD
     C*
     C           VASESN    IFNE WKSESN                     B*3
     C                     EXSR SECTIN
     C                     MOVE VASESN    WKSESN
     C                     END                             E*3
     C*
     C**  PROCESS ALL CUSTOMER/BOOK ALLOCATION RECORDS
     C*
     C                     EXSR P004
     C*
     C                     END                             E*2
     C*
     C**  ACCESS NEXT RECORD IN ER REFERENCE FILE
     C*
     C                     READ SEERRFL2                 88
     C*
     C                     END                             E*1
     C*
     C                     ENDSR
     C*****************************************************************
      /EJECT
     ******************************************************************
     C* P003   : TERMINATION PROCESSING                               *
     ******************************************************************
     C*
     C           P003      BEGSR
     C*
     C**   PROCESS AUDIT PROCESSING
     C*
     C                     EXSR SRLR
     C*
     C**   END OF PROGRAM
     C*
     C                     MOVE '1'       *INLR
     C                     RETRN
     C*
     C                     ENDSR
     ******************************************************************
      /EJECT
     ******************************************************************
     C* P004   : PROCESS ALL ALLOCATION RECORDS                       *
     ******************************************************************
     C*
     C           P004      BEGSR
     C*
     C**  ACCESS FIRST ALLOCATION RECORD
     C*
     C           VAERRF    SETLLSEERALL1
     C                     READ SEERALL1                 87
     C*
     C**  DO WHILE END OF FILE IS NOT REACHED AND SAME ER REFERENCE
     C*
     C           *IN87     DOWEQ'0'                        B*1
     C           VAERRF    ANDEQVCERRF
     C*
     C**  IF RECORD IDENTIFIER IS 'D', ER TYPE 'B' AND NOMINAL ALLOCATED NE 0
     C*
     C           VCRECI    IFEQ 'D'                        B*2
     C           VCERTP    ANDEQ'B'
     C           VCNOMA    ANDNE*ZEROS
     C*
     C**  GENERATE RECORD ON PF/TREVED
     C*
     C                     EXSR W01
     C                     END                             E*2
     C*
     C**  IF RECORD IDENTIFIER IS 'D', ER TYPE 'P' AND NOMINAL ALLOCATED NE 0
     C*
     C           VCRECI    IFEQ 'D'                        B*2
     C           VCERTP    ANDEQ'P'
     C           VCNOMA    ANDNE*ZEROS
     C*
     C**  GENERATE RECORD ON PF/TREVED AND PF/PCTEVD
     C*
     C                     EXSR W02
     C                     END                             E*2
     C*
     C**  ACCESS NEXT ALLOCATION RECORD FOR THE SAME ER REFERENCE
     C*
     C                     READ SEERALL1                 87
     C                     END                             E*1
     C*
     C                     ENDSR
     ******************************************************************
      /EJECT
     ******************************************************************
     C* W01   - GENERATE RECORD ON PF/TREVED                          *
     ******************************************************************
     C           W01       BEGSR
     C*
     C                     MOVE 'D'       TRRI             RECORD ID
     C                     MOVE VAERRF    TRTR             TRADE REFERENCE
     C                     MOVE VASESN    TRSS             SECURITY SHORTNAME
     C*
     C           VCNOMA    IFGT *ZEROS                     B*1
     C                     MOVE 'TS'      TRTT             TRADE TYPE
     C                     MOVE 'I'       TRIO             I/O INDICATOR
     C                     MOVE '31S1'    TREC             EVENT CODE
     C                     ELSE                            X*1
     C                     MOVE 'TP'      TRTT
     C                     MOVE 'O'       TRIO
     C                     MOVE '31P1'    TREC
     C                     Z-SUBVCNOMA    VCNOMA
     C                     END                             E*1
     C*
     C                     Z-ADDVAERRD    TRVD             VALUE DATE
     C**********           Z-ADDVCDDPT    TROD             OUR DEPOT                          CSD027
     C                     MOVE VCDDPT    TROD             OUR DEPOT                          CSD027
     C                     MOVE NMCY      TRNC             NOMINAL CCY
     C                     Z-ADDVCNOMA    TRON             NOMINAL/OUSTANDING
     C*
     C                     Z-ADDVCNOMA    ZNOML
     C                     Z-ADDVAREDP    ZAVPR
     C                     Z-ADDNMDP      ZNMDP
     C                     Z-ADDA6NBDP    ZNMCD
     C                     MOVE SPBS      ZPRCB
     C                     EXSR ZAPNUM
     C                     Z-ADDZVALU     TRVN             C/VALUE - NOMINAL CCY
     C                     Z-ADDZVALU     TRVS             C/VALUE - SETTLEM CCY
     C*
     C                     MOVE NMCY      TRSC             SETTLEMENT CCY
     C                     MOVE *BLANKS   TROI             OVERDUE INDICATOR
     C                     MOVE 'N'       TRPI             PORTFOLIO INDICATOR
     C*
     C**  IF CHANGE OF BRANCH CODE
     C*
     C           VCBRCD    IFNE WKBRCD                     B*1
     C                     EXSR BRCDIN
     C***********          Z-ADDVCBRCD    WKBRCD                          S01496
     C                     MOVE VCBRCD    WKBRCD                          S01496
     C                     END                             E*1
     C*
     C***********A8BICN    CHAINSDCUSTL0             89                   S01496
     C*                                                                   S01496
     C                     CALL 'AOCUSTR0'                                S01496
     C                     PARM *BLANKS   WLRTCD                          S01496
     C                     PARM '*KEY  '  WLOPTN                          S01496
     C                     PARM A8BICN    WLCUST 10                       S01496
     C                     PARM *BLANKS   WLKYST  7                       S01496
     C           SDCUST    PARM SDCUST    DSSDY                           S01496
     C*
     C**  IF RECORD NOT FOUND
     C*
     C************IN89     IFEQ '1'                        B*1            S01496
     C           WLRTCD    IFNE *BLANKS                                   S01496
     C                     MOVEL'003'     DBASE            *****************
     C***********          MOVEL'SDCUSTL0'DBFILE           ** DB ERROR 03 S01496
     C                     MOVE 'SDCUSTPD'DBFILE           * DB ERROR 03 *S01496
     C                     MOVELA8BICN    DBKEY            *****************
     C                     EXSR *PSSR
     C                     END                             E*1
     C*
     C                     MOVE A8BICN    TRPT             COUNTERPARTY
     C*
     C                     MOVE 'F'       TRED             EX-COUPON/DIVIDEND
     C***********          Z-ADDVCBRCD    TRBN             BRANCH CODE    S01496
     C                     MOVE VCBRCD    TRBA             BRANCH CODE    S01496
     C                     MOVE VCBOOK    TRBK             BOOK CODE
     C                     MOVE MKTI      TRMK             MARKET
     C***********          MOVE BVDSTC    TRTS             TRADE SUB-TYPE S01496
     C                     MOVE BVDMKS    TRTS             TRADE SUB-TYPE S01496
     C*
     C                     MOVE BBCRNM    TRCN             C/PARTY REPORT NAME
     C                     MOVE BBCRTN    TRTN             C/PARTY REPORT TOWN
     C*
     C                     Z-ADD*ZEROS    TRSM             SETTLEMENT METHOD
     C                     MOVE *BLANKS   TRSA             SETTLEMENT ACCOUNT
     C***********          Z-ADD*ZEROS    TRSB             SETTLEMENT BRANS01496
     C                     MOVE *BLANKS   TRTA             SETTLEMENT BR. S01496
     C                     MOVE *BLANKS   TRCR             CLEARANCE TYPE
     C                     MOVE *BLANKS   PTFR             PORTFOLIO REFERENCE
     C                     MOVE 'Y'       EARL             DUE TO EARLY REDEMPT
     C*                                                                   S01496
     C**  Originating branch                                              S01496
     C                     MOVE VCBRCD    ORBR                            S01496
     C*                                                                   S01496
     C**  Company of Booking Branch                                       S01496
     C                     MOVE A8CMCD    COBB                            S01496
     C*                                                                   S01496
     C**  Company of Originating Branch                                   S01496
     C                     MOVE A8CMCD    COOB                            S01496
     C*                                                                   S01496
     C**  Entity code                                                     S01496
     C                     MOVE *BLANKS   EXEI                            S01496
     C*                                                                   S01496
     C**  Branch entity level                                             S01496
     C                     MOVE 'B'       ELVB                            S01496
     C*                                                                   S01496
     C**  Company entity level                                            S01496
     C                     MOVE 'C'       ELVC                            S01496
     C*                                                                   S01496
     C**  System entity level                                             S01496
     C                     MOVE 'S'       ELVS                            S01496
     C*                                                                   S01496
     C**  Event branch code                                               S01496
     C                     MOVE VCBRCD    EBRC                            S01496
     C*                                                                   S01496
     C**  Company of event branch                                         S01496
     C                     MOVE A8CMCD    ECPY                            S01496
     C*                                                                   S01496
     C**  Multibranching event                                            S01496
     C                     MOVE 'N'       IBRE                            S01496
     C*                                                                   S01496
     C**  Intercompany event flag                                         S01496
     C                     MOVE 'Y'       ICYE                            S01496
      *                                                                   CEU011
      ** If CEU011 is ON, move the currency sort sequence number to       CEU011
      ** the event currency sort sequence.                                CEU011
      *                                                                   CEU011
     C********** CEU011    IFEQ 'Y'                                                    CEU011 241795
     C                     MOVE A6SSNB    ECSS                            CEU011
     C**********           ENDIF                                                       CEU011 241795
      *                                                                   CAC005
      ** If CAC005 is installed, retrieve the corresponding user book     CAC005
      ** and profit centre.  VCBOOK in the Early-Redemption-Allocation    CAC005
      ** file is being helds as a pseudo book.                            CAC005
      *                                                                   CAC005
     C           CAC005    IFEQ 'Y'                                       CAC005
     C                     CALL 'AOBKPCR0'                                CAC005
     C                     PARM *BLANKS   PRTCD                           CAC005
     C                     PARM '*USER  ' POPTN                           CAC005
     C                     PARM           PBKCD   2                       CAC005
     C                     PARM           PPRFC   4                       CAC005
     C                     PARM VCBOOK    PPSBK   2                       CAC005
      *                                                                   CAC005
     C           PRTCD     IFEQ *BLANKS                                   CAC005
     C                     MOVELPPRFC     PRFC                            CAC005
     C                     ELSE                                           CAC005
     C                     MOVE *BLANKS   PRFC                            CAC005
     C                     ENDIF                                          CAC005
     C                     ENDIF                                          CAC005
     C*
     C                     ADD  1         OUTRTR
     C                     ADD  TRON      WTOTTR
     C                     WRITETREVEDF
     C*
     C                     ENDSR
     ******************************************************************
      /EJECT
     ******************************************************************
     C* W02   - GENERATE RECORD ON PF/TREVED AND PCTEVD               *
     ******************************************************************
     C           W02       BEGSR
     C*
     C                     MOVE 'D'       TRRI             RECORD ID
     C                     MOVE VAERRF    TRTR             TRADE REFERENCE
     C                     MOVE VASESN    TRSS             SECURITY SHORTNAME
     C*
     C           VCNOMA    IFGT *ZEROS                     B*1
     C                     MOVE 'TP'      TRTT             TRADE TYPE
     C                     MOVE 'O'       TRIO             I/O INDICATOR
     C                     ELSE                            X*1
     C                     MOVE 'TS'      TRTT
     C                     MOVE 'I'       TRIO
     C                     Z-SUBVCNOMA    VCNOMA
     C                     END                             E*1
     C*
     C                     Z-ADDVAERRD    TRVD             VALUE DATE
     C**********           Z-ADDVCDDPT    TROD             OUR DEPOT                          CSD027
     C                     MOVE VCDDPT    TROD             OUR DEPOT                          CSD027
     C                     MOVE NMCY      TRNC             NOMINAL CCY
     C                     Z-ADDVCNOMA    TRON             NOMINAL/OUSTANDING
     C*
     C                     Z-ADDVCNOMA    ZNOML
     C                     Z-ADDVAREDP    ZAVPR
     C                     Z-ADDNMDP      ZNMDP
     C                     Z-ADDA6NBDP    ZNMCD
     C                     MOVE SPBS      ZPRCB
     C                     EXSR ZAPNUM
     C                     Z-ADDZVALU     TRVN             C/VALUE - NOMINAL CCY
     C                     Z-ADDZVALU     TRVS             C/VALUE - SETTLEM CCY
     C*
     C                     MOVE NMCY      TRSC             SETTLEMENT CCY
     C                     MOVE *BLANKS   TROI             OVERDUE INDICATOR
     C                     MOVE 'Y'       TRPI             PORTFOLIO INDICATOR
     C*
     C**  IF CHANGE OF BRANCH CODE
     C*
     C           VCBRCD    IFNE WKBRCD                     B*1
     C                     EXSR BRCDIN
     C***********          Z-ADDVCBRCD    WKBRCD                          S01496
     C                     MOVE VCBRCD    WKBRCD                          S01496
     C                     END                             E*1
     C*
     C                     MOVE VCCNUM    KCNUM   6
     C***********KCNUM     CHAINSDCUSTL0             89                   S01496
     C*                                                                   S01496
     C                     CALL 'AOCUSTR0'                                S01496
     C                     PARM *BLANKS   WLRTCD                          S01496
     C                     PARM '*KEY  '  WLOPTN                          S01496
     C                     PARM KCNUM     WLCUST                          S01496
     C                     PARM *BLANKS   WLKYST                          S01496
     C           SDCUST    PARM SDCUST    DSSDY                           S01496
     C*
     C**  IF RECORD NOT FOUND
     C*
     C************IN89     IFEQ '1'                        B*1            S01496
     C           WLRTCD    IFNE *BLANKS                                   S01496
     C                     MOVEL'004'     DBASE            *****************
     C***********          MOVEL'SDCUSTL0'DBFILE           ** DB ERROR 04 S01496
     C                     MOVE 'SDCUSTPD'DBFILE           * DB ERROR 04 *S01496
     C                     MOVELKCNUM     DBKEY            *****************
     C                     EXSR *PSSR
     C                     END                             E*1
     C*
     C                     MOVE VCCNUM    TRPT             COUNTERPARTY
     C*
     C                     MOVE 'F'       TRED             EX-COUPON/DIVIDEND
     C***********          Z-ADDVCBRCD    TRBN             BRANCH CODE    S01496
     C                     MOVE VCBRCD    TRBA             BRANCH CODE    S01496
     C                     MOVE *BLANKS   TRBK             BOOK CODE
     C                     MOVE *BLANKS   PRFC                            CAC005
     C                     MOVE MKTI      TRMK             MARKET
     C                     MOVE *BLANKS   TREC             EVENT CODE
     C***********          MOVE BVDPDS    TRTS             TRADE SUB-TYPE S01496
     C                     MOVE BVDPFS    TRTS             TRADE SUB-TYPE S01496
     C*
     C                     MOVE BBCRNM    TRCN             C/PARTY REPORT NAME
     C                     MOVE BBCRTN    TRTN             C/PARTY REPORT TOWN
     C*
     C                     Z-ADD*ZEROS    TRSM             SETTLEMENT METHOD
     C                     MOVE *BLANKS   TRSA             SETTLEMENT ACCOUNT
     C***********          Z-ADD*ZEROS    TRSB             SETTLEMENT BRANS01496
     C                     MOVE *BLANKS   TRTA             SETTLEMENT BR. S01496
     C                     MOVE *BLANKS   TRCR             CLEARANCE TYPE
     C                     MOVE VCPTFR    PTFR             PORTFOLIO REFERENCE
     C                     MOVE 'Y'       EARL             DUE TO EARLY REDEMPT
     C*                                                                   S01496
     C**  Originating branch                                              S01496
     C                     MOVE VCBRCD    ORBR                            S01496
     C*                                                                   S01496
     C**  Company of Booking Branch                                       S01496
     C                     MOVE A8CMCD    COBB                            S01496
     C*                                                                   S01496
     C**  Company of Originating Branch                                   S01496
     C                     MOVE A8CMCD    COOB                            S01496
     C*                                                                   S01496
     C**  Entity code                                                     S01496
     C                     MOVE *BLANKS   EXEI                            S01496
     C*                                                                   S01496
     C**  Branch entity level                                             S01496
     C                     MOVE 'B'       ELVB                            S01496
     C*                                                                   S01496
     C**  Company entity level                                            S01496
     C                     MOVE 'C'       ELVC                            S01496
     C*                                                                   S01496
     C**  System entity level                                             S01496
     C                     MOVE 'S'       ELVS                            S01496
     C*                                                                   S01496
     C**  Event branch code                                               S01496
     C                     MOVE VCBRCD    EBRC                            S01496
     C*                                                                   S01496
     C**  Company of event branch                                         S01496
     C                     MOVE A8CMCD    ECPY                            S01496
     C*                                                                   S01496
     C**  Multibranching event                                            S01496
     C                     MOVE 'N'       IBRE                            S01496
     C*                                                                   S01496
     C**  Intercompany event flag                                         S01496
      *                                                                   CEU011
      ** If CEU011 is ON, move the currency sort sequence number to       CEU011
      ** the event currency sort sequence.                                CEU011
      *                                                                   CEU011
     C********** CEU011    IFEQ 'Y'                                                    CEU011 241795
     C                     MOVE A6SSNB    ECSS                            CEU011
     C**********           ENDIF                                                       CEU011 241795
      *                                                                   CEU011
     C                     MOVE 'Y'       ICYE                            S01496
     C                     ADD  1         OUTRTR
     C                     ADD  TRON      WTOTTR
     C                     WRITETREVEDF
     C*
     C                     MOVE BVDPBC    TRBK             BOOK CODE
     C*
     C                     ADD  1         OUTRPC
     C                     ADD  TRON      WTOTPC
     C                     WRITEPCTEVDF
     C*
     C                     ENDSR
     ***************************************************************************
      /EJECT
     C**************************************************************************
     C*                                                                        *
     C*  ZAPNUM - CALCULATE AP NUMERATOR                                       *
     C*                                                                        *
     C*  PURPOSE : TO MULTIPLY A NOMINAL POSITION BY THE PRICE OF THE          *
     C*  POSITION TO GIVE A VALUE OF THE POSITION WITH THE CORRECT NUMBER      *
     C*  OF DECIMAL PLACES FOR THE NOMINAL CURRENCY                            *
     C*                                                                        *
     C*  INPUT  FIELDS                                                         *
     C*       ZNOML  11 0     NOMINAL                                          *
     C*       ZAVPR  15 8     AVERAGE PRICE                                    *
     C*       ZNMDP   1 0     NOMINAL DECIMAL PLACES (0-4)                     *
     C*       ZNMCD   1 0     NOMINAL CURRENCY DECIMAL PLACES (0-3)            *
     C*       ZPRCB   1 A     PRICE BASIS (P/U)                                *
     C*                                                                        *
     C*  OUTPUT  FIELDS                                                        *
     C*       ZVALU  15 0     VALUE                                            *
     C*                                                                        *
     C*  ALSO REQUIRED                                                         *
     C*       POWER8 ARRAY OF MULTIPLYING FACTORS FOR NOMINAL DECIMAL PLACES   *
     C*                                                                        *
     C*  WORK FIELDS:                                                          *
     C*       ZVAL0  15 0 - AVERAGE PRICE WITH 0 DECIMAL PLACES                *
     C*       ZVAL1  15 1 - --------------- '' ----------------                *
     C*       ZVAL2  15 2 - --------------- '' ----------------                *
     C*       ZVAL3  15 3 - --------------- '' ----------------                *
     C*       ZI      1 0 - POWER ARRAY                                        *
     C*                                                                        *
     C*  CALLED FROM:                                                          *
     C*                                                                        *
     C*************************************************************************
     C           ZAPNUM    BEGSR                           *** ZAPNUM ***
     C*
     C** DEFINE ALL FIELDS
     C                     Z-ADDZNOML     ZNOML  110
     C                     Z-ADDZAVPR     ZAVPR  158
     C                     Z-ADDZNMDP     ZNMDP   10
     C                     Z-ADDZNMCD     ZNMCD   10
     C                     MOVE ZPRCB     ZPRCB   1
     C                     Z-ADDZVALU     ZVALU  150
     C*
     C** IF NUMBER OF CURRENCY DECIMAL PLACES I = 0
     C           ZNMCD     IFEQ 0
     C           ZNOML     MULT ZAVPR     ZVAL0  150H
     C                     MOVE ZVAL0     ZVALU
     C                     END
     C*
     C** IF NUMBER OF CURRENCY DECIMAL PLACES I = 1
     C           ZNMCD     IFEQ 1
     C           ZNOML     MULT ZAVPR     ZVAL1  151H
     C                     MOVE ZVAL1     ZVALU
     C                     END
     C*
     C** IF NUMBER OF CURRENCY DECIMAL PLACES I = 2
     C           ZNMCD     IFEQ 2
     C           ZNOML     MULT ZAVPR     ZVAL2  152H
     C                     MOVE ZVAL2     ZVALU
     C                     END
     C*
     C** IF NUMBER OF CURRENCY DECIMAL PLACES I = 3
     C           ZNMCD     IFEQ 3
     C           ZNOML     MULT ZAVPR     ZVAL3  153H
     C                     MOVE ZVAL3     ZVALU
     C                     END
     C*
     C** SET INDEX ZI EQUAL TO 5 -. NOMINAL DECIMAL PLACES )
     C           5         SUB  ZNMDP     ZI      10
     C*
     C** MULTIPLY ZVALU BY  POWER8,ZI TO GET AMOUNT WITH NOMINAL DECIMAL PLACES
     C           ZVALU     MULT POWER8,ZI ZVALU
     C*
     C** PRICE BASIS IF = PERCENTAGE
     C           ZPRCB     IFEQ 'P'
     C           ZVALU     DIV  100       ZVALU     H
     C                     END
     C*
     C                     ENDSR
     C*************************************************************************
      /EJECT
     ******************************************************************
     C* SECTIN   - GET SECURITY RECORD                                *
     ******************************************************************
     C*
     C           SECTIN    BEGSR                           ** SECTIN **
     C*
     C**  CHAIN FOR SECURITY RECORD AND DATA BASE ERROR
     C*
     C           VASESN    CHAINSECTY                89
     C*
     C**  IF RECORD NOT FOUND
     C*
     C           *IN89     IFEQ '1'                        B*1
     C           RECI      OREQ '*'
     C                     MOVEL'005'     DBASE            *****************
     C***********          MOVEL'SECTY   'DBFILE           ** DB ERROR 05 S01496
     C                     MOVE 'SECTY   'DBFILE           * DB ERROR 05 *S01496
     C                     MOVELVASESN    DBKEY            *****************
     C                     EXSR *PSSR
     C                     END                             E*1
     C*
     C**  IF CHANGE OF NOMINAL CURRENCY
     C*
     C           NMCY      IFNE WKNMCY                     B*1
     C*
     C                     MOVE NMCY      WKNMCY
     C*
     C***********NMCY      CHAINSDCURRL0             89                   S01496
     C*                                                                   S01496
     C                     CALL 'AOCURRR0'                                S01496
     C                     PARM *BLANKS   WLRTCD                          S01496
     C                     PARM '*KEY  '  WLOPTN                          S01496
     C                     PARM NMCY      WLCYCD  3                       S01496
     C           SDCURR    PARM SDCURR    DSSDY                           S01496
     C*
     C**  IF RECORD NOT FOUND
     C*
     C************IN89     IFEQ '1'                        B*2            S01496
     C           WLRTCD    IFNE *BLANKS                                   S01496
     C                     MOVEL'006'     DBASE            *****************
     C***********          MOVEL'SDCURRL0'DBFILE           ** DB ERROR 06 S01496
     C                     MOVE 'SDCURRPD'DBFILE           * DB ERROR 06 *S01496
     C                     MOVELNMCY      DBKEY            *****************
     C                     EXSR *PSSR
     C                     END                             E*2
     C                     END                             E*1
     C*
     C*
     C                     ENDSR
     C**********************************************************************
      /EJECT
     ******************************************************************
     C* BRCDIN   - GET BRANCH CODE RECORD                             *
     ******************************************************************
     C*
     C           BRCDIN    BEGSR                           ** BRCDIN **
     C*
     C                     MOVE VCBRCD    KBRCD   3
     C*
     C***********KBRCD     CHAINSDBRCHL0             89                   S01496
     C*                                                                   S01496
     C**********           CALL 'AOBRCHR0'                                             S01496 CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   WLRTCD                          S01496
     C                     PARM '*KEY  '  WLOPTN                          S01496
     C                     PARM KBRCD     WLBRCH  3                       S01496
     C           SDBRCH    PARM SDBRCH    DSSDY                           S01496
     C*
     C**  IF RECORD NOT FOUND
     C*
     C************IN89     IFEQ '1'                        B*1            S01496
     C           WLRTCD    IFNE *BLANKS                                   S01496
     C                     MOVEL'007'     DBASE            *****************
     C***********          MOVEL'SDBRCHL0'DBFILE           ** DB ERROR 07 S01496
     C                     MOVE 'SDBRCHPD'DBFILE           * DB ERROR 07 *S01496
     C                     MOVELKBRCD     DBKEY            *****************
     C                     EXSR *PSSR
     C                     END                             E*1
     C*
     C                     ENDSR
     C**********************************************************************
      /EJECT
     ******************************************************************
     C* *PSSR - TO PROCESS ABNORMAL END OF PROGRAM                    *
     ******************************************************************
     C           *PSSR     BEGSR
     C*
     C** UPDATE LDA WITH ERROR INFORMATION
     C*
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVELDBFILE    WWFILE
     C                     MOVELDBKEY     WWKEY
     C                     MOVELDBPGM     WWPGM
     C                     MOVELDBASE     WWASE
     C                     OUT  LDA
     C*
     C                     WRITEERROR
     C***********          WRITEENDRP                                     S01496
     C*
     C**   PRINT DUMP AND CANCEL PROGRAM
     C*
     C                     DUMP
     C*
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     RETRN
     C*
     C                     ENDSR
     ******************************************************************
      /EJECT
     ******************************************************************
     C*  SRLR - E.O.J. - C O N T R O L   P R O C E S S I N G          *
     ******************************************************************
     C*
     C           SRLR      BEGSR
     C*
     C**  IF NO RECORD OUTPUT
     C*
     C           OUTRPC    IFEQ *ZEROS                     B*1
     C           OUTRTR    ANDEQ*ZEROS
     C                     WRITENODATA
     C                     ELSE                            X*1
     C*
     C**  WRITE TRADE EVENTS AUDIT RECORD
     C*
     C           1         SETLLTREVEA
     C                     READ TREVEA                   89
     C*
     C           WTOTTR    DIV  1000      TRHRWN
     C                     MVR            TRHRDC
     C*
     C**  IF RECORD FOUND
     C*
     C           *IN89     IFEQ '0'                        B*2
     C*
     C**  TRAILER BEFORE RUNNING OF THE PROGRAM
     C*
     C                     Z-ADDNORE      TRBEFO
     C                     Z-ADDHRWN      TRBEF1
     C                     Z-ADDHRDC      TRBEF2
     C*
     C**  TRAILER ADDED
     C*
     C                     Z-ADDOUTRTR    TRCALC
     C                     Z-ADDTRHRWN    TRCAL1
     C                     Z-ADDTRHRDC    TRCAL2
     C*
     C                     ADD  OUTRTR    NORE
     C                     ADD  TRHRWN    HRWN
     C           HRDC      ADD  TRHRDC    TRHRDC
     C*
     C           TRHRDC    IFGE 1000                       B*3
     C           TRHRDC    SUB  1000      HRDC
     C                     ADD  1         HRWN
     C                     ELSE                            X*3
     C                     Z-ADDTRHRDC    HRDC
     C                     END                             E*3
     C*
     C**  OLD TRAILER + CALCULATED TRAILER = NEW TRAILER FOR TREVED
     C*
     C                     Z-ADDNORE      TRAFTR
     C                     Z-ADDHRWN      TRAFT1
     C                     Z-ADDHRDC      TRAFT2
     C*
     C                     UPDATTREVEAF
     C*
     C**  IF TRAILER DOES NOT EXIST |||
     C*
     C                     ELSE                            X*2
     C*
     C**  TRAILER BEFORE RUNNING OF THE PROGRAM
     C*
     C                     Z-ADD*ZEROS    TRBEFO
     C                     Z-ADD*ZEROS    TRBEF1
     C                     Z-ADD*ZEROS    TRBEF2
     C*
     C**  TRAILER ADDED
     C*
     C                     Z-ADDOUTRTR    TRCALC
     C                     Z-ADDTRHRWN    TRCAL1
     C                     Z-ADDTRHRDC    TRCAL2
     C*
     C**  NEW TRAILER FOR TREVED (SAME AS TRAILER ADDED)
     C*
     C                     Z-ADDOUTRTR    TRAFTR
     C                     Z-ADDTRHRWN    TRAFT1
     C                     Z-ADDTRHRDC    TRAFT2
     C*
     C                     Z-ADDOUTRTR    NORE
     C                     Z-ADDTRHRWN    HRWN
     C                     Z-ADDTRHRDC    HRDC
     C                     WRITETREVEAF
     C                     END                             E*2
     C*
     C**  WRITE PORTFOLIO CUSTOMER TRADE EVENTS AUDIT RECORD
     C*
     C           1         SETLLPCTEVA
     C                     READ PCTEVA                   89
     C*
     C           WTOTPC    DIV  1000      PCHRWN
     C                     MVR            PCHRDC
     C*
     C           *IN89     IFEQ '0'                        B*2
     C*
     C**  TRAILER BEFORE RUNNING OF THE PROGRAM
     C*
     C                     Z-ADDNORE      PCBEFO
     C                     Z-ADDHRWN      PCBEF1
     C                     Z-ADDHRDC      PCBEF2
     C*
     C**  TRAILER ADDED
     C*
     C                     Z-ADDOUTRPC    PCCALC
     C                     Z-ADDPCHRWN    PCCAL1
     C                     Z-ADDPCHRDC    PCCAL2
     C*
     C                     ADD  OUTRPC    NORE
     C                     ADD  PCHRWN    HRWN
     C           HRDC      ADD  PCHRDC    PCHRDC
     C*
     C           PCHRDC    IFGE 1000                       B*3
     C           PCHRDC    SUB  1000      HRDC
     C                     ADD  1         HRWN
     C                     ELSE                            X*3
     C                     Z-ADDPCHRDC    HRDC
     C                     END                             E*3
     C*
     C**  OLD TRAILER + CALCULATED TRAILER = NEW TRAILER OF PCTEVD
     C*
     C                     Z-ADDNORE      PCAFTR
     C                     Z-ADDHRWN      PCAFT1
     C                     Z-ADDHRDC      PCAFT2
     C*
     C                     UPDATPCTEVAF
     C*
     C**  IF TRAILER DOES NOT EXIST |||
     C*
     C                     ELSE                            X*2
     C*
     C**  TRAILER BEFORE RUNNING OF THE PROGRAM
     C*
     C                     Z-ADD*ZEROS    PCBEFO
     C                     Z-ADD*ZEROS    PCBEF1
     C                     Z-ADD*ZEROS    PCBEF2
     C*
     C**  TRAILER ADDED
     C*
     C                     Z-ADDOUTRPC    PCCALC
     C                     Z-ADDPCHRWN    PCCAL1
     C                     Z-ADDPCHRDC    PCCAL2
     C*
     C**  NEW TRAILER OF PCTEVD (SAME AS ADDED)
     C*
     C                     Z-ADDOUTRPC    PCAFTR
     C                     Z-ADDPCHRWN    PCAFT1
     C                     Z-ADDPCHRDC    PCAFT2
     C*
     C                     Z-ADDOUTRPC    NORE
     C                     Z-ADDPCHRWN    HRWN
     C                     Z-ADDPCHRDC    HRDC
     C                     WRITEPCTEVAF
     C                     END                             E*2
     C*
     C                     WRITECONTROL
     C                     END                             E*1
     C*
     C**  END OF PROGRAM
     C*
     C***********          WRITEENDRP                                     S01496
     C*
     C                     ENDSR
     C**************************************************************************
      /EJECT
     C**************************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
**  POWER8 - ARRAY OF POWERS FOR CURRENCY CONVERSION
00000001
00000010
00000100
00001000
00010000
00100000
01000000
10000000
