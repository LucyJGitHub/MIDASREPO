     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE ER Blocked Positions Audit/Full List')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module                        *
     F*                                                               *
     F*  SE6820 - SE ER Blocked Positions List/Audit                  *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD031             Date 10Apr06               *
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CSE071             Date 19Jul05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001             Date 26Aug99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU017             Date 03Avr98               *
     F*                 CSE007             Date 05Jan98               *
     F*                 S01496             Date 04Oct94               *
     F*                 S10978             Date 29Mar93               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  CGL029 - Increase Account Code to 10 digits                  *
     F*  CPB001 - 'Private Banking' Module                            *
     F*  CEU017 - Securities Redenomination Euro Changes              *
     F*  CSE007 - Corporate Actions                                   *
     F*  S01496 - Incorporation of Jyske Enhancements (S10978)        *
     F*  S10978 - JYSZRH: EARLY REDEMPTIONS                           *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
      /EJECT
     F*
     F*****BJ*-*Bank*Details*-*Rtv*Idx*********************************   S01496
     F***SDBANKL1IF**E           K        DISK                            S01496
     F***********                                   KINFSR *PSSR          S01496
     F*
     F**   VD - SE ER Block Positions
     FSEBLKPL1IF  E           K        DISK
     F                                              KINFSR *PSSR
     F*
     F**   Securities Details
     FSECTY   IF  E           K        DISK
     F                                              KINFSR *PSSR
     F*
     F*****Portfolio*Management*ICD*-*Rtv*Idx**************************   S01496
     F***SDPORTL1IF**E           K        DISK                            S01496
     F***********                                   KINFSR *PSSR          S01496
     F*
     F*****Modules*Details*-*Rtv*Idx***********************************   S01496
     F***SDMMODL1IF**E           K        DISK                            S01496
     F***********                                   KINFSR *PSSR          S01496
     F*
     F**   Printer file - SE ER Blocked Positions List/Audit
     F***SE6820P1O***E             70     PRINTER                         S01496
     FSE6820P1O   E             70     PRINTER      KINFDS SPOOL      UC  S01496
     F                                              KINFSR *PSSR
     F*                                                                   S01496
     F**   Printer file - SE ER Blocked Positions Audit File              S01496
     FSE6820AUO   E                    PRINTER      KINFDS SPOOLU     UC  S01496
     F                                              KINFSR *PSSR          S01496
     *********************************************************************
      /EJECT
     *********************************************************************
      *
      * ID F  C  H  L    Function of indicators
      * ---------------------------------------
      *
      *   45 - Portfolio management indicator                             S01496
      *                                                                   S01496
      *   50 - 0 Decimal Place
      *   51 - 1 Decimal Places
      *   52 - 2 Decimal Places
      *   53 - 3 Decimal Places
      *   54 - 4 Decimal Places
      *
      *   55 - Multibranching indicator                                   S01496
      *   56 - Branch level indicator                                     S01496
      *                                                                   S01496
      *   59 - Feature CSE007 is switched ON.                             CSE007
      *                                                                   CSE007
      *   60 - Blocked Nominal equal to zero
      *
      *   64 - Blocked Nominal equal to *Hival and Block Type equal to 'P'
      *   65 - Block End Date  equal to *Hival
      *   66 - Blocked Nominal equal to *Hival and Block Type equal to 'S'
      *
      *   70 - Page Overflow
      *
      *   87,88 - Work Indicator
      *
      *   89 - Record not Found
      *
      *   LR :    Last record
      *   U7/U8 : Database error
      *
      *   90-99 : Used by Standard Subroutines
      **************************************************************************
      /EJECT
      **************************************************************************
      *
      * Name of the Subroutines
      * -----------------------
      *
      *       P001  - Initial processing
      *       P002  - Main processing
      *       P003  - Termination processing
      *
      *       R001  - Output Details to Printer File
      *
      *       #LBRPR -  Process a change in branch                        S01496
      *       #LZSFL -  Call ZSFILE                                       S01496
      *                                                                   S01496
      *       *PSSR - "Standard" Database error processing
      *
      * Name of the Standard Subroutines
      * --------------------------------
      *
      *       ZDATE2 - Dates Validation and Conversion
      *
     ***************************************************************************
      /EJECT
     *********************************************************************
     E*
     E**   FOR COMPILATION - COPYRIGHT INSERTION
     E*
     E                    CPY@    1   1 80
     E*
     E**   ARRAYS USED BY ZDATE2
     E*
     E                    ZYDY    4   4  4 0             ZDATE1/2 SR.ARRAY
     E                    ZMDY   13  13  3 0             ZDATE1/2 SR.ARRAY
     E                    ZMNM   12  12  3               ZDATE2 SR. ARRAY
     *********************************************************************
      /EJECT
     *********************************************************************
     I*
     I**   DATA STRUCTURE FOR COMPILATION - COPYRIGHT INSERTION
     I*
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I*
     I**   DATA STRUCTURE FOR DATABASE ERROR
     I*
     ILDA         DS                            256
     I                                      132 183 WWLOT
     I                                      132 141 WWFILE
     I                                      142 170 WWKEY
     I                                      171 180 WWPGM
     I                                      181 1830WWASE
     I*                                                                   S01496
     ISPOOL       DS                                                      S01496
     I**  File Information Data Structure - P1 report                     S01496
     I*                                                                   S01496
     I                                       83  92 SFILE1                S01496
     I                                    B 123 1240SFNUM1                S01496
     ISPOOLU      DS                                                      S01496
     I**  File Information Data Structure - AU report                     S01496
     I*                                                                   S01496
     I                                       83  92 SFILEU                S01496
     I                                    B 123 1240SFNUMU                S01496
     I*                                                                   S01496
     ISDBANK    E DSSDBANKPD                                              S01496
     I**  Data structure for bank details                                 S01496
     I*                                                                   S01496
     ISDBRCH    E DSSDBRCHPD                                              S01496
     I**  Data structure for branch details                               S01496
     I*                                                                   S01496
     ISDMMOD    E DSSDMMODPD                                              S01496
     I**  Data structure for module details                               S01496
     I*                                                                   S01496
     ISDPORT    E DSSDPORTPD                                              S01496
     I**  Data structure for portfolio management details                 S01496
     I*                                                                   S01496
     IDSSDY     E DSDSSDY                                                 S01496
     I**  Long data structure for access objects                          S01496
     I*                                                                   S01496
     *********************************************************************
      /EJECT
     C********************************************************************
     C**    P R O G R A M   S T A R T
     C********************************************************************
     C*                                                                   S01496
     C**  Copyright statement                                             S01496
     C*                                                                   S01496
     C                     MOVEACPY@      ACT@   80                       S01496
     C*
     C**   ENTRY PARAMETER
     C           *ENTRY    PLIST
     C                     PARM           WTYPE   1
     C                     PARM           @RSEQ   5                       S01496
     C                     PARM           @RLEV   1                       S01496
     C                     PARM           @RENT   3                       S01496
     C*
     C**   INITIAL PROCESSING
     C*
     C                     EXSR P001
     C*
     C**   MAIN PROCESSING
     C*
     C                     EXSR P002
     C*
     C**   TERMINATION PROCESSING
     C*
     C                     EXSR P003
     C*
     C********************************************************************
     C**    P R O G R A M     E N D
     C********************************************************************
      /EJECT
     ******************************************************************
     C* P001   : INITIAL PROCESSING                                   *
     ******************************************************************
     C*
     C           P001      BEGSR
     C*
     C**   INITIALISE KEY AND WORKING FIELDS
     C*
     C                     MOVE *BLANKS   WWLOT
     C                     MOVEL'SE6820'  DBPGM  10
     C*
     C***********          MOVE *BLANKS   WBRCD   30                      S01496
     C                     MOVE *BLANKS   WBRCD   3                       S01496
     C*
     C                     MOVE 'Y'       WNODET  1
     C*
     C**   IF INPUT CYCLE, THEN 'FULL LIST' (ELSE 'AUDIT')
     C*
     C           WTYPE     IFEQ '1'
     C                     MOVE '1'       *IN80
     C                     END
     C*                                                                   S01496
     C**  Check report level                                              S01496
     C*                                                                   S01496
     C           @RLEV     IFEQ 'B'                                       S01496
     C                     MOVE '1'       *IN56                           S01496
     C                     END                                            S01496
     C*
     C**   ACCESS TO BANK DETAILS TO RETRIEVE THE MIDAS RUN DATE
     C*
     C************LOVAL    SETLLSDBANKL1                                  S01496
     C***********          READ SDBANKL1                 89               S01496
     C*                                                                   S01496
     C                     CALL 'AOBANKR0'                                S01496
     C                     PARM *BLANKS   WLRTCD  7                       S01496
     C                     PARM '*FIRST'  WLOPTN  7                       S01496
     C           SDBANK    PARM SDBANK    DSSDY                           S01496
     C*
     C**   IF RECORD NOT FOUND
     C*
     C************IN89     IFEQ '1'                        B*1            S01496
     C           WLRTCD    IFNE *BLANKS                                   S01496
     C                     MOVEL'001'     DBASE   30       *****************
     C***********          MOVEL'SDBANKL1'DBFILE 10        ** DB ERROR 01 S01496
     C                     MOVE 'SDBANKPD'DBFILE 10        * DB ERROR 01 *S01496
     C                     MOVEL*BLANKS   DBKEY  29        *****************
     C                     EXSR *PSSR
     C                     END                             E*1
     C*
     C**   DETERMINE DATE FORMAT
     C*
     C           BJDFIN    IFEQ 'M'                        B*1
     C                     MOVE '1'       *IN98
     C                     END                             E*1
     C*
     C**   ACCESS TO MODULES DETAILS FILE TO RETRIEVE THE 'PORTFOLIO
     C**   MANAGEMENT INDICATOR'
     C*
     C************LOVAL    SETLLSDMMODL1                                  S01496
     C***********          READ SDMMODL1                 89               S01496
     C*                                                                   S01496
     C                     CALL 'AOMMODR0'                                S01496
     C                     PARM *BLANKS   WLRTCD                          S01496
     C                     PARM '*FIRST'  WLOPTN                          S01496
     C           SDMMOD    PARM SDMMOD    DSSDY                           S01496
     C*
     C**   IF RECORD NOT FOUND
     C*
     C************IN89     IFEQ '1'                        B*1            S01496
     C           WLRTCD    IFNE *BLANKS                                   S01496
     C                     MOVEL'002'     DBASE            *****************
     C***********          MOVEL'SDMMODL1'DBFILE           ** DB ERROR 02 S01496
     C                     MOVE 'SDMMODPD'DBFILE           * DB ERROR 02 *S01496
     C                     MOVEL*BLANKS   DBKEY            *****************
     C                     EXSR *PSSR
     C                     END                             E*1
     C*                                                                   S01496
     C**  Setup multibranching indicator                                  S01496
     C*                                                                   S01496
     C           BGMBIN    IFEQ 'Y'                                       S01496
     C                     MOVE '1'       *IN55                           S01496
     C                     ELSE                                           S01496
     C*                                                                   S01496
     C**  Else, setup appropriate level and entity                        S01496
     C                     MOVE 'S'       @RLEV                           S01496
     C                     MOVE *BLANKS   @RENT                           S01496
     C                     MOVE '0'       *IN56                           S01496
     C                     END                                            S01496
     C*
     C**   IF 'PORTFOLIO MANAGEMENT' PRESENT
     C*
     C           BGPFMG    IFEQ 'Y'                        B*1
     C           BGN4ST    OREQ 'Y'                                       CPB001
     C*                                                                   S01496
     C**  Setup the portfolio management indicator                        S01496
     C*                                                                   S01496
     C                     MOVE '1'       *IN45                           S01496
     C*
     C**   ACCESS TO PORTFOLIO MANAGEMENT FILE TO RETRIEVE THE 'REFERENCE
     C**   ATTACHED TO "9997"'
     C*
     C************LOVAL    SETLLSDPORTL1                                  S01496
     C***********          READ SDPORTL1                 89               S01496
     C*                                                                   S01496
     C           BGPFMG    IFEQ 'Y'                                       CPB001
     C                     CALL 'AOPORTR0'                                S01496
     C                     PARM *BLANKS   WLRTCD                          S01496
     C                     PARM '*FIRST'  WLOPTN                          S01496
     C           SDPORT    PARM SDPORT    DSSDY                           S01496
     C*
     C**   IF RECORD NOT FOUND
     C*
     C************IN89     IFEQ '1'                        B*2            S01496
     C           WLRTCD    IFNE *BLANKS                                   S01496
     C                     MOVEL'003'     DBASE            *****************
     C***********          MOVEL'SDPORTL1'DBFILE           ** DB ERROR 03 S01496
     C                     MOVE 'SDPORTPD'DBFILE           * DB ERROR 03 *S01496
     C                     MOVEL*BLANKS   DBKEY            *****************
     C                     EXSR *PSSR
     C                     END                             E*2
     C                     END                             E*1
     C                     ENDIF                                          CPB001
      *                                                                   CSE007
      ***  Call access program to access Switchable Features Details.     CSE007
      *                                                                   CSE007
     C                     MOVE 'N'       CSE007  1                       CSE007
     C                     MOVE '0'       *IN59                           CSE007
     C                     CALL 'AOSARDR0'                                CSE007
     C                     PARM '       ' @RTCD   7                       CSE007
     C                     PARM '*VERIFY' @OPTN   7                       CSE007
     C                     PARM 'CSE007'  @SARD   6                       CSE007
      *                                                                   CSE007
      ***  If feature CSE007 is switched ON.                              CSE007
      *                                                                   CSE007
     C           @RTCD     IFEQ *BLANKS                                   CSE007
     C                     MOVE 'Y'       CSE007                          CSE007
     C                     MOVE '1'       *IN59                           CSE007
     C                     ENDIF                                          CSE007
      *                                                                   CEU017
      ***  Call access program to access Switchable Features Details.     CEU017
      *                                                                   CEU017
     C                     MOVE 'N'       CEU017  1                       CEU017
     C                     CALL 'AOSARDR0'                                CEU017
     C                     PARM '       ' @RTCD   7                       CEU017
     C                     PARM '*VERIFY' @OPTN   7                       CEU017
     C                     PARM 'CEU017'  @SARD   6                       CEU017
      *                                                                   CEU017
      ***  If feature CEU017 is switched ON.                              CEU017
      *                                                                   CEU017
     C           @RTCD     IFEQ *BLANKS                                   CEU017
     C                     MOVE 'Y'       CEU017                          CEU017
     C                     MOVE '1'       *IN59                           CEU017
     C                     ENDIF                                          CEU017
     C*
     C                     ENDSR
      *****************************************************************
      /EJECT
     ******************************************************************
     C* P002   : MAIN PROCESSING                                      *
     ******************************************************************
     C*
     C           P002      BEGSR
     C*
     C**   ACCESS FIRST RECORD IN ER REFERENCE FILE
     C**   Only if entity is 'ALL' or blank                               S01496
     C*
     C           @RENT     IFEQ 'ALL'                                     S01496
     C           @RENT     OREQ *BLANKS                                   S01496
     C           *LOVAL    SETLLSEBLKPL1
     C                     READ SEBLKPL1                 88
     C                     ELSE                                           S01496
     C*                                                                   S01496
     C**  Else, use the selected branch (entity) as key                   S01496
     C*                                                                   S01496
     C           @RENT     SETLLSEBLKPL1                                  S01496
     C           @RENT     READESEBLKPL1                 88               S01496
     C                     END                                            S01496
     C*
     C**   WHILE END OF FILE IS NOT REACHED
     C*
     C           *IN88     DOWEQ'0'                        B*1
     C*
     C                     MOVE 'N'       WNODET
     C*
     C**   IF 'BRANCH CODE' IS NOT EQUAL TO 'SAVED BRANCH CODE'
     C*
     C           VDBRCD    IFNE WBRCD                      B*2
     C*
     C                     MOVE VDBRCD    BRCDP1
     C*                                                                   S01496
     C**  Process a change in branch                                      S01496
     C*                                                                   S01496
     C                     EXSR #LBRPR                                    S01496
     C*
     C**   OUTPUT HEADER
     C*
     C                     WRITEHEADER
     C*
     C**   MOVE 'BRANCH CODE' INTO 'SAVED BRANCH CODE'
     C*
     C***********          Z-ADDVDBRCD    WBRCD                           S01496
     C                     MOVE VDBRCD    WBRCD                           S01496
     C                     END                             E*2
     C*
     C**   IF OVERFLOW INDICATOR IS ON
     C*
     C           *IN70     IFEQ '1'                        B*2
     C*
     C**   OUTPUT TITLE
     C*
     C                     WRITEHEADER
     C                     MOVE '0'       *IN70
     C                     END                             E*2
     C*
     C**   IF 'SECURITY' IS DIFFERENT THAN 'SAVED SECURITY'
     C*
     C           VDSESN    IFNE WSESN                      B*2
     C*
     C                     MOVE VDSESN    WSESN  10
     C*
     C                     MOVE '0'       *IN50
     C                     MOVE '0'       *IN51
     C                     MOVE '0'       *IN52
     C                     MOVE '0'       *IN53
     C                     MOVE '0'       *IN54
     C*
     C           VDSESN    IFNE *BLANKS                    B*3
     C*
     C**   ACCESS TO SECURITY DETAILS TO RETRIEVE THE NOMINAL DECIMAL PLACES
     C*
     C           VDSESN    CHAINSECTY                89
     C*
     C**   IF RECORD IS NOT FOUND
     C*
     C           *IN89     IFEQ '1'                        B*4
     C                     MOVEL'004'     DBASE            *****************
     C***********          MOVEL'SECTY   'DBFILE           ** DB ERROR 04 S01496
     C                     MOVE 'SECTY   'DBFILE           * DB ERROR 04 *S01496
     C                     MOVELVDSESN    DBKEY            *****************
     C                     EXSR *PSSR
     C                     END                             E*4
     C*
     C**   SETON CORRECT INDICATOR FOLLOWING THE NUMBER OF DECIMAL PLACES
     C*
     C           NMDP      IFEQ 0                          B*4
     C                     MOVE '1'       *IN50
     C                     ELSE                            X*4
     C           NMDP      IFEQ 1                          B*5
     C                     MOVE '1'       *IN51
     C                     ELSE                            X*5
     C           NMDP      IFEQ 2                          B*6
     C                     MOVE '1'       *IN52
     C                     ELSE                            X*6
     C           NMDP      IFEQ 3                          B*7
     C                     MOVE '1'       *IN53
     C                     ELSE                            X*7
     C                     MOVE '1'       *IN54
     C                     END                             E*7
     C                     END                             E*6
     C                     END                             E*5
     C                     END                             E*4
     C                     END                             E*3
     C                     END                             E*2
     C*
     C**   OUTPUT A DETAIL LINE
     C*
     C                     EXSR R001
     C*
     C**   ACCESS NEXT RECORD IN ER REFERENCE FILE
     C*
     C                     READ SEBLKPL1                 88
     C*                                                                   S01496
     C**  Stop processing if branch is not equal to branch selected       S01496
     C**  and entity is not 'ALL' and not blank                           S01496
     C*                                                                   S01496
     C           @RENT     IFNE 'ALL'                                     S01496
     C           @RENT     ANDNE*BLANKS                                   S01496
     C           @RENT     ANDNEVDBRCD                                    S01496
     C                     MOVE '1'       *IN88                           S01496
     C                     END                                            S01496
     C                     END                             E*1
     C*
     C                     ENDSR
     C*****************************************************************
      /EJECT
     ******************************************************************
     C* P003   : TERMINATION PROCESSING                               *
     ******************************************************************
     C*
     C           P003      BEGSR
     C*
     C**   IF NO DETAIL RECORDS ARE PRINTED
     C*
     C           WNODET    IFEQ 'Y'                        B*1
     C*
     C                     MOVE *BLANKS   BRCDP1
     C*                                                                   S01496
     C**  Open audit printer file and output "No details to report"       S01496
     C*                                                                   S01496
     C                     OPEN SE6820AU                                  S01496
     C                     WRITEHEADAU                                    S01496
     C                     WRITENODTLS                                    S01496
     C*                                                                   S01496
     C**  Ensure audit report is recorded by RCF                          S01496
     C*                                                                   S01496
     C                     Z-ADDSFNUMU    ZSNUM                           S01496
     C                     MOVELSFILEU    SFILE                           S01496
     C                     MOVE *BLANKS   SENTY                           S01496
     C                     EXSR #LZSFL                                    S01496
     C                     CLOSESE6820AU                                  S01496
     C*
     C*****OUTPUT*HEADER***********************************************   S01496
     C***********                                                         S01496
     C***********          WRITEHEADER                                    S01496
     C***********                                                         S01496
     C*****WRITE*EMPTY*RECORD******************************************   S01496
     C***********                                                         S01496
     C***********          WRITENODATA                                    S01496
     C                     END                             E*1
     C*
     C**   WRITE END OF REPORT
     C*
     C           WLOPN     IFEQ 'Y'                                       S01496
     C                     WRITEENDRP
     C                     CLOSESE6820P1                                  S01496
     C                     MOVE *BLANK    WLOPN                           S01496
     C                     END                                            S01496
     C*
     C**   END OF PROGRAM
     C*
     C                     MOVE '1'       *INLR
     C                     RETRN
     C*
     C                     ENDSR
     ******************************************************************
     ******************************************************************
     C* R001   : OUTPUT ER DEPOT DETAILS                              *
     ******************************************************************
     C*
     C           R001      BEGSR
     C*
     C                     MOVE '0'       *IN60
     C*
     C********** VDCNUM    IFEQ *ZEROS                     B*1                                CSD027
     C           VDCNUM    IFEQ *BLANKS                    B*1                                CSD027
     C                     MOVE *BLANKS   CNUMP1
     C                     ELSE                            X*1
     C                     MOVE VDCNUM    CNUMP1
     C                     END                             E*1
     C*
     C           VDPTFR    IFEQ '9997'                     B*1
     C           BGN4ST    ANDNE'Y'                                       CPB001
     C***********          MOVE P9R997    PTFRP1                          S01496
     C                     MOVE FCR997    PTFRP1                          S01496
     C                     ELSE                            X*1
     C                     MOVE VDPTFR    PTFRP1
     C                     END                             E*1
     C*
     C                     MOVE VDBLTY    BLTYP1
     C*
     C                     MOVE VDSESN    SESNP1
      *                                                                   CSE007
      ***  If feature CSE007 is switched ON.                              CSE007
      ***  Or feature CEU017 is switched ON.                              CEU017
      *                                                                   CSE007
     C           CSE007    IFEQ 'Y'                                       CSE007
     C           CEU017    OREQ 'Y'                                       CEU017
      *                                                                   CSE007
      ***  Format Block from Date.                                        CSE007
      *                                                                   CSE007
     C                     MOVE *BLANKS   BSTTP1                          CSE007
     C           VDBSTT    IFNE *ZEROS                                    CSE007
     C                     MOVE VDBSTT    ZDAYNO                          CSE007
     C                     EXSR ZDATE2                                    CSE007
     C                     MOVE ZADATE    BSTTP1                          CSE007
     C                     ENDIF                                          CSE007
      *                                                                   CSE007
      ***  Block Error/Warning Indicator.                                 CSE007
      *                                                                   CSE007
     C                     MOVE *BLANKS   EWINP1                          CSE007
     C                     MOVE VDEWIN    EWINP1                          CSE007
     C                     ENDIF                                          CSE007
     C*
     C**   IF END DATE IS EQUAL TO 99999 (*HIVAL)
     C*
     C           VDBEDT    IFEQ *HIVAL                     B*1
     C                     MOVE '1'       *IN65
     C                     ELSE                            X*1
     C                     MOVE '0'       *IN65
     C                     Z-ADDVDBEDT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    ENDDP1
     C                     END                             E*1
     C*
     C                     MOVE VDBNAR    NARRP1
     C*
     C**   IF BLOCKED NOMINAL IS EQUAL TO 99999 (*HIVAL)
     C*
     C           VDNOMB    IFEQ *HIVAL                     B*1
     C*
     C**   IF BLOCK TYPE IS 'P', DISPLAY '+++'
     C*
     C           VDBLTY    IFEQ 'P'                        B*2
     C                     MOVE '1'       *IN64
     C                     MOVE '0'       *IN66
     C*
     C**   IF BLOCK TYPE IS 'S', DISPLAY '---'
     C*
     C                     ELSE                            X*2
     C                     MOVE '0'       *IN64
     C                     MOVE '1'       *IN66
     C                     END                             E*2
     C*
     C**   IF BLOCKED NOMINAL IS NOT EQUAL TO *HIVAL
     C*
     C                     ELSE                            X*1
     C                     MOVE '0'       *IN64
     C                     MOVE '0'       *IN66
     C*
     C**    IF 0 DECIMAL PLACE
     C*
     C           *IN50     IFEQ '1'                        B*2
     C                     MOVE VDNOMB    BLNMP0
     C                     END                             E*2
     C*
     C**    IF 1 DECIMAL PLACE
     C*
     C           *IN51     IFEQ '1'                        B*2
     C                     MOVE VDNOMB    BLNMP1
     C                     END                             E*2
     C*
     C**    IF 2 DECIMAL PLACES
     C*
     C           *IN52     IFEQ '1'                        B*2
     C                     MOVE VDNOMB    BLNMP2
     C                     END                             E*2
     C*
     C**    IF 3 DECIMAL PLACES
     C*
     C           *IN53     IFEQ '1'                        B*2
     C                     MOVE VDNOMB    BLNMP3
     C                     END                             E*2
     C*
     C**    IF 4 DECIMAL PLACES
     C*
     C           *IN54     IFEQ '1'                        B*2
     C                     MOVE VDNOMB    BLNMP4
     C                     END                             E*2
     C*
     C**    IF BLOCKED NOMINAL IS EQUAL TO ZERO
     C*
     C           VDNOMB    IFEQ *ZEROS                     B*2
     C                     MOVE '1'       *IN60
     C                     END                             E*2
     C                     END                             E*2
     C*
     C                     MOVE VDLUID    LUIDP1
     C*
     C**   IF RUN DATE LOWER OR EQUAL THAN END DATE
     C**   AND END DATE LOWER THAN NEXT WORKING DAY
     C*
     C           BJRDNB    IFLE VDBEDT                     B*1
     C           VDBEDT    ANDLTBJDNWD
     C                     MOVE 'Mature'  CHTPP1
     C                     END                             E*1
     C*
     C**   IF LAST CHANGE DATE IS EQUAL TO THE RUN DATE
     C*
     C           VDLCD     IFEQ BJRDNB                     B*1
     C*
     C**   IF CHANGE TYPE IS INSERT
     C*
     C           VDCHTP    IFEQ 'I'                        B*2
     C                     MOVE 'Insert'  CHTPP1
     C                     ELSE                            X*2
     C           VDCHTP    IFEQ 'A'                        B*3
     C                     MOVE 'Amend '  CHTPP1
     C                     ELSE                            X*3
     C                     MOVE 'Delete'  CHTPP1
     C                     END                             E*3
     C                     END                             E*2
     C                     END                             E*1
     C*
     C**   SETUP LAST CHANGE DATE AND LAST USER IDENTIFIER
     C*
     C                     Z-ADDVDLCD     ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     LCDP1
     C*
     C                     MOVE VDLUID    LUIDP1
     C*
     C**   OUTPUT DETAIL
     C*
     C                     WRITEDETAIL
     C*
     C                     MOVE '0'       *IN63
     C*
     C                     ENDSR
     ******************************************************************
      /EJECT
     C*****************************************************************   S01496
     C* #LBRPR - Subroutine to process a change in branch             *   S01496
     C*****************************************************************   S01496
     C*                                                                   S01496
     C           #LBRPR    BEGSR                                          S01496
     C*                                                                   S01496
     C**  If printer file is open and entity is not blank, print footer   S01496
     C*                                                                   S01496
     C           WLOPN     IFEQ 'Y'                                       S01496
     C           @RENT     ANDNE*BLANKS                                   S01496
     C*                                                                   S01496
     C**  Output "End of Report"                                          S01496
     C                     WRITEENDRP                                     S01496
     C*                                                                   S01496
     C**  Close printer file                                              S01496
     C                     CLOSESE6820P1                                  S01496
     C                     MOVE *BLANK    WLOPN   1                       S01496
     C                     END                                            S01496
     C*                                                                   S01496
     C**  Get branch name using access object                             S01496
     C*                                                                   S01496
     C**********           CALL 'AOBRCHR0'                                S01496              CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   WLRTCD                          S01496
     C                     PARM '*KEY   ' WLOPTN                          S01496
     C                     PARM VDBRCD    WLBRCA  3                       S01496
     C           SDBRCH    PARM SDBRCH    DSSDY                           S01496
     C*                                                                   S01496
     C**  Check if error occurred                                         S01496
     C*                                                                   S01496
     C           WLRTCD    IFNE *BLANKS                                   S01496
     C                     MOVE 'SDBRCHPD'DBFILE           ***************S01496
     C                     Z-ADD5         DBASE            * DB ERROR 05 *S01496
     C                     MOVE *BLANKS   DBKEY            ***************S01496
     C                     MOVELVDBRCD    DBKEY                           S01496
     C                     EXSR *PSSR                                     S01496
     C                     END                                            S01496
     C*                                                                   S01496
     C                     MOVE A8BRNM    RRBRNM                          S01496
     C                     MOVE VDBRCD    RRBRCA                          S01496
     C*                                                                   S01496
     C**  Open printer file SE6820P1 if entity is not blank               S01496
     C**  or the printer file is not yet open                             S01496
     C*                                                                   S01496
     C           @RENT     IFNE *BLANKS                                   S01496
     C           WLOPN     ORNE 'Y'                                       S01496
     C                     OPEN SE6820P1                                  S01496
     C                     MOVE 'Y'       WLOPN                           S01496
     C*                                                                   S01496
     C** Ensure Report Spool File recorded by RCF                         S01496
     C*                                                                   S01496
     C           @RENT     IFNE *BLANKS                                   S01496
     C                     MOVE VDBRCD    SENTY                           S01496
     C                     END                                            S01496
     C*                                                                   S01496
     C                     Z-ADDSFNUM1    ZSNUM                           S01496
     C                     MOVELSFILE1    SFILE                           S01496
     C                     EXSR #LZSFL                                    S01496
     C*                                                                   S01496
     C                     END                                            S01496
     C*                                                                   S01496
     C                     ENDSR                                          S01496
     C*****************************************************************   S01496
     C/EJECT                                                              S01496
     C*****************************************************************   S01496
     C* #LZSFL - Subroutine to call ZSFILE                            *   S01496
     C*****************************************************************   S01496
     C*                                                                   S01496
     C           #LZSFL    BEGSR                                          S01496
     C*                                                                   S01496
     C                     CALL 'ZSFILE'                                  S01496
     C                     PARM           @RSEQ                           S01496
     C                     PARM           SENTY   3                       S01496
     C                     PARM           SFILE  10                       S01496
     C                     PARM           ZSNUM   60                      S01496
     C                     PARM           ZSERR   1                       S01496
     C*                                                                   S01496
     C** If an error occurred during ZSFILE processing,                   S01496
     C** return to the calling program.                                   S01496
     C*                                                                   S01496
     C           ZSERR     IFEQ 'Y'                                       S01496
     C                     SETON                     U7U8LR               S01496
     C                     RETRN                                          S01496
     C                     END                                            S01496
     C*                                                                   S01496
     C                     ENDSR                                          S01496
     C*****************************************************************   S01496
     C/EJECT                                                              S01496
     ******************************************************************
     C* *PSSR - TO PROCESS ABNORMAL END OF PROGRAM                    *
     ******************************************************************
     C           *PSSR     BEGSR
     C*
     C**   UPDATE LDA WITH ERROR INFORMATION
     C*
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C***********          MOVELDBFILE    WWFILE                          S01496
     C                     MOVE DBFILE    WWFILE                          S01496
     C                     MOVELDBKEY     WWKEY
     C                     MOVELDBPGM     WWPGM
     C                     MOVELDBASE     WWASE
     C                     OUT  LDA
     C*
     C**   OUTPUT ERROR ON PRINTER FILE
     C*
     C           WLOPN     IFEQ 'Y'                                       S01496
     C                     WRITEERROR
     C                     END                                            S01496
     C*
     C*****WRITE*END*OF*REPORT*****************************************   S01496
     C*
     C***********          WRITEENDRP                                     S01496
     C*                                                                   S01496
     C**  Output database error details in audit printer file             S01496
     C*                                                                   S01496
     C                     OPEN SE6820AU                                  S01496
     C                     WRITEHEADAU                                    S01496
     C                     WRITEDBERROR                                   S01496
     C*                                                                   S01496
     C**  Ensure audit report is recorded by RCF                          S01496
     C*                                                                   S01496
     C                     Z-ADDSFNUMU    ZSNUM                           S01496
     C                     MOVELSFILEU    SFILE                           S01496
     C                     MOVEL*BLANKS   SENTY                           S01496
     C                     EXSR #LZSFL                                    S01496
     C                     CLOSESE6820AU                                  S01496
     C*
     C**   PRINT DUMP AND CANCEL PROGRAM
     C*
     C                     DUMP
     C*
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     RETRN
     C*
     C                     ENDSR
     ******************************************************************
      /EJECT
     C********************************************************************
     C**
     C**   ZDATE2 SR. TO CONVERT A DAY NUMBER TO DATE FORMATS.
     C**
     C**   THE YEAR 2000, BEING DIVISIBLE BY 400, IS A LEAP YEAR.
     C**
     C           ZDATE2    BEGSR                           *** ZDATE2 ***
     C**
     C**   CLEAR LEAP YEAR WORK FIELD.
     C                     MOVE 'N'       ZLEAP   1
     C**
     C**   CLEAR DATE FIELDS.
     C                     Z-ADD0         ZDATE   60
     C                     MOVEL'       ' ZADATE  7
     C**
     C**   CALCULATION TO DEFINE INPUT DAY NUMBER.
     C                     Z-ADDZDAYNO    ZDAYNO  50
     C**
     C**   DETERMINE YEAR NUMBER.
     C**
     C**   ADJUST DAY NUMBER IN CASE LAST DAY OF YEAR.
     C           ZDAYNO    SUB  1         ZDAYN1  50
     C           ZDAYN1    IFLT 0
     C                     GOTO ZDEND2
     C                     END
     C**
     C**   CALCULATE NUMBER OF 4 YEAR SPANS SINCE 31/12/1971.
     C           ZDAYN1    DIV  1461      ZLYR    20
     C                     MVR            ZDAYN1           SAVE REMAINING
     C**                                                   DAYS
     C**   CALCULATE NUMBER OF REMAINING YEARS.
     C                     Z-ADD1         ZWRK2   20
     C           ZDTAG1    TAG                             ** ZDTAG1 TAG *
     C           ZDAYN1    IFGE ZYDY,ZWRK2
     C           ZWRK2     ADD  1         ZWRK2
     C                     GOTO ZDTAG1
     C                     END
     C*
     C           ZWRK2     SUB  1         ZWRK2
     C**
     C**   DECREMENT DAY NO. BY DAYS IN REMAINING YEARS.
     C           ZWRK2     IFNE 0
     C           ZDAYN1    SUB  ZYDY,ZWRK2ZDAYN1
     C                     END
     C**
     C**   CALCULATE ACTUAL YEAR NUMBER.
     C           ZLYR      MULT 4         ZWRK3   30
     C           ZWRK3     ADD  72        ZWRK3            BASE IS 1972
     C                     Z-ADDZWRK3     ZYEAR   20
     C           ZYEAR     ADD  ZWRK2     ZYEAR            YEAR
     C**
     C**   DETERMINE MONTH NUMBER.
     C**
     C**   ADJUST DAY NO. IN CASE LAST DAY OF LEAP YEAR FEBRUARY.
     C           ZWRK2     IFEQ 0
     C           ZDAYN1    IFEQ 59
     C                     MOVE 'Y'       ZLEAP
     C                     END
     C           ZDAYN1    IFGE 59
     C           ZDAYN1    SUB  1         ZDAYN1
     C                     END
     C                     END
     C**
     C**   CALCULATE MONTH NUMBER.
     C                     Z-ADD2         ZWRK2
     C           ZDTAG2    TAG                             ** ZDTAG2 TAG *
     C           ZDAYN1    IFGE ZMDY,ZWRK2
     C           ZWRK2     ADD  1         ZWRK2
     C                     GOTO ZDTAG2
     C                     END
     C*
     C           ZWRK2     SUB  1         ZWRK2
     C**
     C                     Z-ADDZWRK2     ZMTH    20       MONTH
     C**
     C**   DETERMINE DAY OF MONTH.
     C**
     C**   ADD BACK LAST DAY OF YEAR ADJUSTMENT.
     C           ZDAYN1    ADD  1         ZDAYN1
     C**
     C**   CALCULATE DAY OF MONTH.
     C           ZDAYN1    SUB  ZMDY,ZWRK2ZDAY    20       DAY
     C**
     C**   ADD BACK LEAP YEAR 29TH FEBUARY ADJUSTMENT, IF REQUIRED.
     C           ZLEAP     IFEQ 'Y'
     C           ZDAY      ADD  1         ZDAY
     C                     END
     C**
     C**   FORMAT DATE, DDMMYY OR MMDDYY.
     C  N98                MOVELZDAY      ZWRK4   40
     C   98                MOVE ZDAY      ZWRK4
     C  N98                MOVE ZMTH      ZWRK4
     C   98                MOVELZMTH      ZWRK4
     C                     MOVELZWRK4     ZDATE
     C                     MOVE ZYEAR     ZDATE
     C**
     C**   FORMAT ALPHA DATE, DDMMMYY.
     C                     MOVELZDAY      ZWRK5   5
     C           ZDAY      IFLT 10
     C                     MOVEL' '       ZWRK5
     C                     END
     C                     MOVE ZMNM,ZMTH ZWRK5
     C                     MOVELZWRK5     ZADATE
     C                     MOVE ZYEAR     ZADATE
     C**
     C           ZDEND2    ENDSR                           * ZDEND2 ENDSR*
     C**
     C********************************************************************
      /EJECT
     C********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
