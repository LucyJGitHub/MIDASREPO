     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE ER Blocked Positions Report')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module                        *
     F*                                                               *
     F*  SE6860 - SE Blocked Position Report                          *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CSE071             Date 19Jul05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001             Date 26Aug99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU017             Date 03Avr98               *
     F*                 CSE007             Date 19Dec97               *
     F*                 S01496             Date 18Oct94               *
     F*                 S10978             Date 09Apr93               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  CGL029 - Increase Account Code to 10 digits                  *
     F*  CPB001 - 'Private Banking' Module                            *
     F*  CEU017 - Securities Redenomination Euro Changes              *
     F*  CSE007 - Corporate Actions                                   *
     F*  S01496 - Incorporation of Jyske Enhancements (S10978)        *
     F*  S10978 - JYSZRH: EARLY REDEMPTIONS                           *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
      /EJECT
     F*
     F*****BJ*-*Bank*Details*-*Rtv*Idx*********************************   S01496
     F***SDBANKL1IF**E           K        DISK                            S01496
     F***********                                   KINFSR *PSSR          S01496
     F*
     F**   VB - SE Blocked Allocation File
     FSEBKALL0IF  E           K        DISK
     F                                              KINFSR *PSSR
     F            SEERALD0                          KRENAMESEERALF0
     F            SEBLKPD0                          KRENAMESEBLKPF0
     F            SECOALD0                          KRENAMESECOALF0       CSE007
     F            SECOBKD0                          KRENAMESECOBKF0       CSE007
     F*
     F**   VB - SE Blocked Allocation File
     FSEBKALL1IF  E           K        DISK
     F                                              KINFSR *PSSR
     F            SEERALD0                          KRENAMESEERALF1
     F            SEBLKPD0                          KRENAMESEBLKPF1
     F            SECOALD0                          KRENAMESECOALF1       CSE007
     F            SECOBKD0                          KRENAMESECOBKF1       CSE007
     F*
     F*****Portfolio*Management*ICD*-*Rtv*Idx**************************   S01496
     F***SDPORTL1IF**E           K        DISK                            S01496
     F***********                                   KINFSR *PSSR          S01496
     F*
     F*****Modules*Details*-*Rtv*Idx***********************************   S01496
     F***SDMMODL1IF**E           K        DISK                            S01496
     F***********                                   KINFSR *PSSR          S01496
     F*
     F**   Security Details
     FSECTY   IF  E           K        DISK
     F                                              KINFSR *PSSR
     F*
     F**   Printer file
     F***SE6860P1O***E                    PRINTER                         S01496
     FSE6860P1O   E                    PRINTER      KINFDS SPOOL      UC  S01496
     F                                              KINFSR *PSSR
     F*                                                                   S01496
     F**   Printer file - audit report                                    S01496
     FSE6860AUO   E                    PRINTER      KINFDS SPOOLU     UC  S01496
     F                                              KINFSR *PSSR          S01496
     *********************************************************************
      /EJECT
     *********************************************************************
     F** INDICATORS
     *********************************************************************
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F*
     F*       01 - SEBKALL0 - SEERALD0
     F*       02 - SEBKALL0 - SEBLKPD0
     F*       03 - SEBKALL1 - SEERALD1
     F*       04 - SEBKALL1 - SEBLKPD1
     F*       05 - SEBKALL0 - SECOALF0                                    CSE007
     F*       06 - SEBKALL0 - SECOBKF0                                    CSE007
     F*       07 - SEBKALL1 - SECOALF1                                    CSE007
     F*       08 - SEBKALL1 - SECOBKF1                                    CSE007
     F*                                                                   S01496
     F*       43 - PORTFOLIO MANAGEMENT INDICATOR                         S01496
     F*       44 - MULTIBRANCHING INDICATOR                               S01496
     F*       45 - BRANCH LEVEL INDICATOR                                 S01496
     F*                                                                   CSE007
     F*       60 - Feature CSE007 is switched ON.                         CSE007
     F*
     F*       64 - BLOCKED NOMINAL EQUAL TO *HIVAL AND BLOCK TYPE EQUAL TO 'P'
     F*       66 - BLOCKED NOMINAL EQUAL TO *HIVAL AND BLOCK TYPE EQUAL TO 'S'
     F*
     F*       80 - WORK INDICATOR
     F*       88 - PAGE OVERFLOW
     F*       89 - RECORD NOT FOUND
     F*
     F*       LR :    LAST RECORD
     F*       U7/U8 : DATABASE ERROR
     F*
     F*       90-99 : USED BY STANDARD SUBROUTINES
     F*
     F*       KC         F3 =EXIT INDICATOR
     *********************************************************************
      /EJECT
     *********************************************************************
     E** EXTERNALS
     *********************************************************************
     E*
     E**   FOR COMPILATION - COPYRIGHT INSERTION
     E*
     E                    CPY@    1   1 80
     E/COPY ZSRSRC,ZDATE2Z1
     *********************************************************************
      /EJECT
     *********************************************************************
     I** INPUT
     *********************************************************************
     I*
     ISEERALF0    01
     I              VCRECI                          VDRECI
     I              VCCNUM                          VDCNUM
     I              VCPTFR                          VDPTFR
     I              VCERTP                          VDBLTY
     I              VCSESN                          VDSESN
     I              VCNOMA                          VDNOMB
     I              VCBEDT                          VDBEDT
     I              VCLCD                           VDLCD
     I              VCCHTP                          VDCHTP
     I              VCLUID                          VDLUID
     I              VCBRCD                          VDBRCD
     ISEBLKPF0    02
     ISECOALF0    05                                                      CSE007
     I              MCRECI                          VDRECI                CSE007
     I              MCCNUM                          VDCNUM                CSE007
     I              MCPTFR                          VDPTFR                CSE007
     I              MCBLTY                          VDBLTY                CSE007
     I              MCBSEC                          VDSESN                CSE007
     I              MCBEDT                          VDBEDT                CSE007
     I              MCBRCD                          VDBRCD                CSE007
     I              MCBSTT                          VDBSTT                CSE007
     I              MCBEWI                          VDEWIN                CSE007
     I              MCLCD                           VDLCD                 CSE007
     I              MCCHTP                          VDCHTP                CSE007
     I              MCLUID                          VDLUID                CSE007
     ISECOBKF0    06                                                      CSE007
     I              MURECI                          VDRECI                CSE007
     I              MUCNUM                          VDCNUM                CSE007
     I              MUPTFR                          VDPTFR                CSE007
     I              MUBLTY                          VDBLTY                CSE007
     I              MUSESN                          VDSESN                CSE007
     I              MUNOMB                          VDNOMB                CSE007
     I              MUBEDT                          VDBEDT                CSE007
     I              MUBRCD                          VDBRCD                CSE007
     I              MUBSTT                          VDBSTT                CSE007
     I              MUBEWI                          VDEWIN                CSE007
     I              MULCD                           VDLCD                 CSE007
     I              MUCHTP                          VDCHTP                CSE007
     I              MULUID                          VDLUID                CSE007
     I              MUBNAR                          VDBNAR                CSE007
     ISEERALF1    03
     I              VCRECI                          VDRECI
     I              VCCNUM                          VDCNUM
     I              VCPTFR                          VDPTFR
     I              VCERTP                          VDBLTY
     I              VCSESN                          VDSESN
     I              VCNOMA                          VDNOMB
     I              VCBEDT                          VDBEDT
     I              VCLCD                           VDLCD
     I              VCCHTP                          VDCHTP
     I              VCLUID                          VDLUID
     I              VCBRCD                          VDBRCD
     ISEBLKPF1    04
     ISECOALF1    07                                                      CSE007
     I              MCRECI                          VDRECI                CSE007
     I              MCCNUM                          VDCNUM                CSE007
     I              MCPTFR                          VDPTFR                CSE007
     I              MCBLTY                          VDBLTY                CSE007
     I              MCBSEC                          VDSESN                CSE007
     I              MCBEDT                          VDBEDT                CSE007
     I              MCBRCD                          VDBRCD                CSE007
     I              MCBSTT                          VDBSTT                CSE007
     I              MCBEWI                          VDEWIN                CSE007
     I              MCLCD                           VDLCD                 CSE007
     I              MCCHTP                          VDCHTP                CSE007
     I              MCLUID                          VDLUID                CSE007
     ISECOBKF1    08                                                      CSE007
     I              MURECI                          VDRECI                CSE007
     I              MUCNUM                          VDCNUM                CSE007
     I              MUPTFR                          VDPTFR                CSE007
     I              MUBLTY                          VDBLTY                CSE007
     I              MUSESN                          VDSESN                CSE007
     I              MUNOMB                          VDNOMB                CSE007
     I              MUBEDT                          VDBEDT                CSE007
     I              MUBRCD                          VDBRCD                CSE007
     I              MUBSTT                          VDBSTT                CSE007
     I              MUBEWI                          VDEWIN                CSE007
     I              MULCD                           VDLCD                 CSE007
     I              MUCHTP                          VDCHTP                CSE007
     I              MULUID                          VDLUID                CSE007
     I              MUBNAR                          VDBNAR                CSE007
     I*
     I** PROGRAM STATUS DATA STRUCTURE FOR WS/USER ID'S
     I*
     IPGMDS     ESDSY2PGDSP
     I                                      244 253 WSID
     I                                      254 263 USIDXX
     I*
     I**   FILE INFORMATION DATA STRUCTURE
     I*
     IINFDS1    E DSY2I1DSP
     I*
     IFIELD     E DSSEERRFPD
     I*
     I**   DATA STRUCTURE FOR COMPILATION - COPYRIGHT INSERTION
     I*
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I*
     I**   DATA STRUCTURE FOR DATABASE ERROR
     I*
     ILDA         DS                            256
     I                                      132 183 WWLOT
     I                                      132 141 WWFILE
     I                                      142 170 WWKEY
     I                                      171 180 WWPGM
     I                                      181 1830WWASE
     I*                                                                   S01496
     IWLFRMT      DS                             27                       S01496
     I**  Data structure for formatting DDSTIN                            S01496
     I*                                                                   S01496
     I                                        2  27 WLSTIN                S01496
     I*                                                                   S01496
     ISPOOL       DS                                                      S01496
     I**  File Information Data Structure - P1 report                     S01496
     I*                                                                   S01496
     I                                       83  92 SFILE1                S01496
     I                                    B 123 1240SFNUM1                S01496
     ISPOOLU      DS                                                      S01496
     I**  File Information Data Structure - AU report                     S01496
     I*                                                                   S01496
     I                                       83  92 SFILEU                S01496
     I                                    B 123 1240SFNUMU                S01496
     I*                                                                   S01496
     ISDBANK    E DSSDBANKPD                                              S01496
     I**  Data structure for bank details                                 S01496
     I*                                                                   S01496
     ISDBRCH    E DSSDBRCHPD                                              S01496
     I**  Data structure for branch details                               S01496
     I*                                                                   S01496
     ISDMMOD    E DSSDMMODPD                                              S01496
     I**  Data structure for module details                               S01496
     I*                                                                   S01496
     ISDPORT    E DSSDPORTPD                                              S01496
     I**  Data structure for portfolio management details                 S01496
     I*                                                                   S01496
     IDSSDY     E DSDSSDY                                                 S01496
     I**  Long data structure for access objects                          S01496
     I*                                                                   S01496
     ******************************************************************
      /EJECT
     ******************************************************************
     C**
     C** SUBROUTINES
     C**  P001  - INITIAL PROCESSING
     C**  P0021 - PROCESSING FOR MODE '1'  (BRCD/CNUM/PTFR/END DATE)
     C**  P0022 - PROCESSING FOR MODE '2'  (BRCD/CNUM/END DATE)
     C**  P0023 - PROCESSING FOR MODE '3'  (SESN/END DATE)
     C**  P003  - TERMINATION PROCESSING
     C**  P004  - SECURITY DETAILS
     C**
     C**  *PSSR - STANDARD DATA BASE ERROR PROCESSING
     C**
     C**  R001  - OUTPUT TITLE TO PRINTER FILE
     C**  R002  - OUTPUT DETAILS TO PRINTER FILE
     C**  R003  - OUTPUT EMPTY REPORT TO PRINTER FILE
     C**  R004  - OUTPUT END OF REPORT TO PRINTER FILE
     C**  R009  - DATABASE ERROR ON AUDIT REPORT
     C**
     C**  #LBRPR - Process a change in branch                             S01496
     C**  #LZSFL - Call ZSFILE                                            S01496
     C*
     ******************************************************************
      /EJECT
     ******************************************************************
     C**  MAIN - MAIN PROGRAM
     ******************************************************************
     C*
     C**   ENTRY PARAMETERS
     C           *ENTRY    PLIST
     C                     PARM           #LINE   1
     C                     PARM           DDBRCD  3
     C                     PARM           DDCNUM  6
     C                     PARM           DDPTFR  4
     C                     PARM           DDSESN 10
     C                     PARM           DDDATE  5
     C                     PARM           DDSTIN
     C                     PARM           DDRTCD  1
     C                     PARM           @RSEQ   5                       S01496
     C                     PARM           @RLEV   1                       S01496
     C                     PARM           @RENT   3                       S01496
     C*
     C                     MOVE DDDATE    DDDATI  50
     C*
     C**  INITIAL PROCESSING
     C                     EXSR P001
     C*
     C**  MAIN PROCESSING
     C*
     C**  BRCD/CNUM/PTFR/END DATE
     C           #LINE     IFEQ '1'                        B*1
     C                     EXSR P0021
     C                     ELSE                            X*1
     C*
     C**  BRCD/CNUM/END DATE
     C           #LINE     IFEQ '2'                        B*2
     C                     EXSR P0022
     C*
     C**  SESN/END DATE
     C                     ELSE                            X*2
     C                     EXSR P0023
     C                     END                             E*2
     C                     END                             E*1
     C*
     C**  TERMINATION PROCESSING
     C                     EXSR P003
     C*
     ******************************************************************
      /EJECT
     ******************************************************************
     C**  P001 - INITIAL PROCESSING
     ******************************************************************
     C           P001      BEGSR
     C*                                                                   S01496
     C**  Copyright statement                                             S01496
     C*                                                                   S01496
     C                     MOVEACPY@      ACT@   80                       S01496
     C*                                                                   S01496
     C**  Check report level                                              S01496
     C*                                                                   S01496
     C           @RLEV     IFEQ 'B'                                       S01496
     C                     MOVE '1'       *IN45                           S01496
     C                     END                                            S01496
     C*
     C** INITIALISE KEY AND WORKING FIELDS
     C                     MOVE *OFF      #FLAG1  1
     C                     MOVE *BLANKS   WWLOT
     C                     MOVEL'SE6860'  DBPGM
     C*
     C** KEY FOR MODE '1'
     C           KEY1      KLIST
     C***********          KFLD           KBRCD   30                      S01496
     C                     KFLD           KBRCD   3                       S01496
     C**********           KFLD           KCUST   60                                          CSD027
     C                     KFLD           KCUST   6                                           CSD027
     C                     KFLD           KPTFR   4
     C*
     C** KEY FOR MODE '2'
     C           KEY2      KLIST
     C***********          KFLD           KBRCD   30                      S01496
     C                     KFLD           KBRCD   3                       S01496
     C**********           KFLD           KCUST   60                                          CSD027
     C                     KFLD           KCUST   6                                           CSD027
     C*
     C** KEY FOR MODE '3'
     C           KEY3      KLIST
     C                     KFLD           DDSESN
     C                     KFLD           DDDATI
     C*
     C** ACCESS TO BANK DETAILS LF/BANKL1 TO RETRIEVE THE MIDAS
     C** RUN DATE, BANK REPORT NAME AND DATE FORMAT INDICATOR
     C************LOVAL    SETLLSDBANKL1                                  S01496
     C***********          READ SDBANKL1                 89               S01496
     C*                                                                   S01496
     C                     CALL 'AOBANKR0'                                S01496
     C                     PARM *BLANKS   WLRTCD  7                       S01496
     C                     PARM '*FIRST'  WLOPTN  7                       S01496
     C           SDBANK    PARM SDBANK    DSSDY                           S01496
     C*
     C** IF NO REPORT ID FOUND, PRODUCE A STANDARD DATABASE ERROR
     C************IN89     IFEQ *ON                        B*1            S01496
     C           WLRTCD    IFNE *BLANKS                                   S01496
     C                     MOVEL'001'     DBASE            *****************
     C***********          MOVEL'SDBANKL1'DBFILE           ** DB ERROR 01 S01496
     C                     MOVE 'SDBANKPD'DBFILE           * DB ERROR 01 *S01496
     C                     MOVEL*BLANKS   DBKEY            *****************
     C***********          EXSR R001                                      S01496
     C                     EXSR *PSSR
     C                     END                             E*1
     C*
     C           BJDFIN    IFEQ 'M'                        B*1
     C                     MOVE *ON       *IN98
     C                     ENDIF                           E*1
     C*
     C**   ACCESS TO MODULES DETAILS FILE TO RETRIEVE THE 'PORTFOLIO
     C**   MANAGEMENT INDICATOR'
     C************LOVAL    SETLLSDMMODL1                                  S01496
     C***********          READ SDMMODL1                 89               S01496
     C*                                                                   S01496
     C                     CALL 'AOMMODR0'                                S01496
     C                     PARM *BLANKS   WLRTCD                          S01496
     C                     PARM '*FIRST'  WLOPTN                          S01496
     C           SDMMOD    PARM SDMMOD    DSSDY                           S01496
     C*
     C**   IF RECORD NOT FOUND
     C************IN89     IFEQ '1'                        B*1            S01496
     C           WLRTCD    IFNE *BLANKS                                   S01496
     C                     MOVEL'002'     DBASE            *****************
     C***********          MOVEL'SDMMODL1'DBFILE           ** DB ERROR 02 S01496
     C                     MOVE 'SDMMODPD'DBFILE           * DB ERROR 02 *S01496
     C                     MOVEL*BLANKS   DBKEY            *****************
     C***********          EXSR R001                                      S01496
     C                     EXSR *PSSR
     C                     END                             E*1
     C*                                                                   S01496
     C**  Setup multibranching indicator                                  S01496
     C*                                                                   S01496
     C           BGMBIN    IFEQ 'Y'                                       S01496
     C                     MOVE '1'       *IN44                           S01496
     C                     ELSE                                           S01496
     C*                                                                   S01496
     C**  Else, setup appropriate level and entity                        S01496
     C                     MOVE 'S'       @RLEV                           S01496
     C                     MOVE *BLANKS   @RENT                           S01496
     C                     MOVE '0'       *IN45                           S01496
     C                     END                                            S01496
     C*
     C**   IF 'PORTFOLIO MANAGEMENT' PRESENT
     C           BGPFMG    IFEQ 'Y'                        B*1
     C           BGN4ST    OREQ 'Y'                                       CPB001
     C*                                                                   S01496
     C**  Setup the portfolio management indicator                        S01496
     C*                                                                   S01496
     C                     MOVE '1'       *IN43                           S01496
     C*
     C**   ACCESS TO PORTFOLIO MANAGEMENT FILE TO RETRIEVE THE 'REFERENCE
     C**   ATTACHED TO "9997"'
     C************LOVAL    SETLLSDPORTL1                                  S01496
     C***********          READ SDPORTL1                 89               S01496
     C*                                                                   S01496
     C           BGPFMG    IFEQ 'Y'                                       CPB001
     C                     CALL 'AOPORTR0'                                S01496
     C                     PARM *BLANKS   WLRTCD                          S01496
     C                     PARM '*FIRST'  WLOPTN                          S01496
     C           SDPORT    PARM SDPORT    DSSDY                           S01496
     C*
     C**   IF RECORD NOT FOUND
     C************IN89     IFEQ '1'                        B*2            S01496
     C           WLRTCD    IFNE *BLANKS                    ***************S01496
     C                     MOVEL'003'     DBASE            *****************
     C***********          MOVEL'SDPORTL1'DBFILE           ** DB ERROR 03 S01496
     C                     MOVE 'SDPORTPD'DBFILE           * DB ERROR 03 *S01496
     C                     MOVEL*BLANKS   DBKEY            *****************
     C***********          EXSR R001                                      S01496
     C                     EXSR *PSSR
     C                     END                             E*2
     C                     END                             E*1
     C                     ENDIF                                          CPB001
      *                                                                   CSE007
      ***  Call access program to access Switchable Features Details.     CSE007
      *                                                                   CSE007
     C                     MOVE 'N'       CSE007  1                       CSE007
     C                     MOVE '0'       *IN60                           CSE007
     C                     CALL 'AOSARDR0'                                CSE007
     C                     PARM '       ' @RTCD   7                       CSE007
     C                     PARM '*VERIFY' @OPTN   7                       CSE007
     C                     PARM 'CSE007'  @SARD   6                       CSE007
      *                                                                   CSE007
      ***  If feature CSE007 is switched ON.                              CSE007
      *                                                                   CSE007
     C           @RTCD     IFEQ *BLANKS                                   CSE007
     C                     MOVE 'Y'       CSE007                          CSE007
     C                     MOVE '1'       *IN60                           CSE007
     C                     ENDIF                                          CSE007
      *                                                                   CEU017
      ***  Call access program to access Switchable Features Details.     CEU017
      *                                                                   CEU017
     C                     MOVE 'N'       CEU017  1                       CEU017
     C                     CALL 'AOSARDR0'                                CEU017
     C                     PARM '       ' @RTCD   7                       CEU017
     C                     PARM '*VERIFY' @OPTN   7                       CEU017
     C                     PARM 'CEU017'  @SARD   6                       CEU017
      *                                                                   CEU017
      ***  If feature CSE007 is switched ON.                              CEU017
      *                                                                   CEU017
     C           @RTCD     IFEQ *BLANKS                                   CEU017
     C                     MOVE 'Y'       CEU017                          CEU017
     C                     MOVE '1'       *IN60                           CEU017
     C                     ENDIF                                          CEU017
     C*
     C***OUTPUT*TITLE*TO*REPORT****************************************   S01496
     C***********          EXSR R001                                      S01496
     C*                                                                   S01496
     C**  If system level and mode is not 3, remove branch in DDSTIN      S01496
     C*                                                                   S01496
     C           @RLEV     IFEQ 'S'                                       S01496
     C           #LINE     ANDNE'3'                                       S01496
     C                     MOVE DDSTIN    WLFRMT                          S01496
     C                     MOVELWLSTIN    DDSTIN                          S01496
     C                     END                                            S01496
      *                                                                   CSE007
      ***  Define work field for Blocked Nominal.                         CSE007
      *                                                                   CSE007
     C           *LIKE     DEFN VDNOMB    VDNOMW                          CSE007
     C*
     C                     ENDSR
     ******************************************************************
      /EJECT
     ******************************************************************
     C**  P0021 - MAIN PROCESSING
     ******************************************************************
     C           P0021     BEGSR
     C*
     C                     MOVE DDBRCD    KBRCD
     C                     MOVE DDCNUM    KCUST
     C                     MOVE DDPTFR    KPTFR
     C*
     C******               MOVEA'0000'    *IN,01                          CSE007
     C                     MOVEA'00000000'*IN,01                          CSE007
     C*
     C***READ*FIRST*RECORD*IN*LF/SEBKALL0******************************   S01496
     C*                                                                   S01496
     C**  Access first record only if 'ALL' branches selected or          S01496
     C**  system level is requested                                       S01496
     C*                                                                   S01496
     C           @RENT     IFEQ 'ALL'                                     S01496
     C           @RENT     OREQ *BLANKS                                   S01496
     C           *LOVAL    SETLLSEBKALL0                                  S01496
     C                     READ SEBKALL0                 80               S01496
     C                     ELSE                                           S01496
     C           KEY1      SETLLSEBKALL0
     C***********          READ SEBKALL0                 80               S01496
     C                     READESEBKALL0                 80               S01496
     C                     END                                            S01496
     C*                                                                   S01496
     C**  If customer and portfolio reference is not equal to that        S01496
     C**  selected, read next record                                      S01496
     C*                                                                   S01496
     C           *IN80     DOWEQ'0'                                       S01496
     C           VDCNUM    ANDNEKCUST                                     S01496
     C           *IN80     OREQ '0'                                       S01496
     C           VDPTFR    ANDNEKPTFR                                     S01496
     C                     READ SEBKALL0                 80               S01496
     C                     END                                            S01496
     C*
     C***DO*WHILE*END*OF*FILE*IS*NOT*REACHED*FOR*SAME*BRANCH,*CUSTOMER,*POS01496
     C*                                                                   S01496
     C** Do while end of file is not reached                              S01496
     C*                                                                   S01496
     C           *IN80     DOWEQ*OFF                       B*1
     C***********VDBRCD    ANDEQKBRCD                                     S01496
     C***********VDCNUM    ANDEQKCUST                                     S01496
     C*                                                                   S01496
     C           VDCNUM    IFEQ KCUST                                     S01496
     C           VDPTFR    ANDEQKPTFR
     C*
     C** IF CHANGE OF SECURITY
     C           VDSESN    IFNE WSESN                      B*2
     C                     EXSR P004
     C                     END                             E*2
     C*
     C           VDBEDT    IFGE DDDATI                     B*2
     C*                                                                   S01496
     C**  Process a change in branch                                      S01496
     C*                                                                   S01496
     C           VDBRCD    IFNE RRBRCA                                    S01496
     C                     EXSR #LBRPR                                    S01496
     C                     END                                            S01496
     C*
     C** IF THE OVERFLOW INDICATOR IS SET ON
     C           *IN88     IFEQ *ON                        B*3
     C*
     C** OUTPUT TITLE TO REPORT
     C                     EXSR R001
     C                     ENDIF                           E*3
     C*
     C** OUTPUT A DETAIL LINE
     C                     EXSR R002
     C                     END                             E*2
     C*
     C                     END                                            S01496
     C*                                                                   S01496
     C******               MOVEA'0000'    *IN,01                          CSE007
     C                     MOVEA'00000000'*IN,01                          CSE007
     C*
     C** READ NEXT RECORD IN LF/SEBKALL0
     C                     READ SEBKALL0                 80
     C*                                                                   S01496
     C**  Stop processing if branch is not equal to branch selected       S01496
     C**  and entity is not 'ALL' and not blank                           S01496
     C*                                                                   S01496
     C           @RENT     IFNE 'ALL'                                     S01496
     C           @RENT     ANDNE*BLANKS                                   S01496
     C           KBRCD     ANDNEVDBRCD                                    S01496
     C                     MOVE '1'       *IN80                           S01496
     C                     END                                            S01496
     C*                                                                   S01496
     C                     ENDDO                           E*1
     C*
     C                     ENDSR
     ******************************************************************
      /EJECT
     ******************************************************************
     C**  P0022 - MAIN PROCESSING
     ******************************************************************
     C           P0022     BEGSR
     C*
     C                     MOVE DDBRCD    KBRCD
     C                     MOVE DDCNUM    KCUST
     C*
     C******               MOVEA'0000'    *IN,01                          CSE007
     C                     MOVEA'00000000'*IN,01                          CSE007
     C*
     C***READ*FIRST*RECORD*IN*LF/SEBKALL0******************************   S01496
     C*                                                                   S01496
     C**  Access first record only if 'ALL' branches selected or          S01496
     C**  system level is requested                                       S01496
     C*                                                                   S01496
     C           @RENT     IFEQ 'ALL'                                     S01496
     C           @RENT     OREQ *BLANKS                                   S01496
     C           *LOVAL    SETLLSEBKALL0                                  S01496
     C                     READ SEBKALL0                 80               S01496
     C                     ELSE                                           S01496
     C           KEY2      SETLLSEBKALL0
     C***********          READ SEBKALL0                 80               S01496
     C                     READESEBKALL0                 80               S01496
     C                     END                                            S01496
     C*                                                                   S01496
     C**  If customer is not equal to that selected, read next record     S01496
     C*                                                                   S01496
     C           *IN80     DOWEQ'0'                                       S01496
     C           VDCNUM    ANDNEKCUST                                     S01496
     C                     READ SEBKALL0                 80               S01496
     C                     END                                            S01496
     C*
     C***DO*WHILE*END*OF*FILE*IS*NOT*REACHED*FOR*SAME*BRANCH,*CUSTOMER    S01496
     C*                                                                   S01496
     C** Do while end of file is not reached                              S01496
     C*                                                                   S01496
     C           *IN80     DOWEQ*OFF                       B*1
     C***********VDBRCD    ANDEQKBRCD                                     S01496
     C***********VDCNUM    ANDEQKCUST                                     S01496
     C*                                                                   S01496
     C           VDCNUM    IFEQ KCUST                                     S01496
     C*
     C** IF CHANGE OF SECURITY
     C           VDSESN    IFNE WSESN                      B*2
     C                     EXSR P004
     C                     END                             E*2
     C*
     C           VDBEDT    IFGE DDDATI                     B*2
     C*                                                                   S01496
     C**  Process a change in branch                                      S01496
     C*                                                                   S01496
     C           VDBRCD    IFNE RRBRCA                                    S01496
     C                     EXSR #LBRPR                                    S01496
     C                     END                                            S01496
     C*
     C** IF THE OVERFLOW INDICATOR IS SET ON
     C           *IN88     IFEQ *ON                        B*3
     C*
     C** OUTPUT TITLE TO REPORT
     C                     EXSR R001
     C                     ENDIF                           E*3
     C*
     C** OUTPUT A DETAIL LINE
     C                     EXSR R002
     C                     END                             E*2
     C*
     C                     END                                            S01496
     C*                                                                   S01496
     C******               MOVEA'0000'    *IN,01                          CSE007
     C                     MOVEA'00000000'*IN,01                          CSE007
     C*
     C** READ NEXT RECORD IN LF/SEBKALL0
     C                     READ SEBKALL0                 80
     C*                                                                   S01496
     C**  Stop processing if branch is not equal to branch selected       S01496
     C**  and entity is not 'ALL' and not blank                           S01496
     C*                                                                   S01496
     C           @RENT     IFNE 'ALL'                                     S01496
     C           @RENT     ANDNE*BLANKS                                   S01496
     C           KBRCD     ANDNEVDBRCD                                    S01496
     C                     MOVE '1'       *IN80                           S01496
     C                     END                                            S01496
     C*                                                                   S01496
     C                     ENDDO                           E*1
     C*
     C                     ENDSR
     ******************************************************************
      /EJECT
     ******************************************************************
     C**  P0023 - MAIN PROCESSING
     ******************************************************************
     C           P0023     BEGSR
     C*
     C******               MOVEA'0000'    *IN,01                          CSE007
     C                     MOVEA'00000000'*IN,01                          CSE007
     C*
     C** READ FIRST RECORD IN LF/SEBKALL1
     C           KEY3      SETLLSEBKALL1
     C                     READ SEBKALL1                 80
     C*
     C** RETREIVE SECURITY DETAILS
     C           *IN80     IFNE '1'                        B*1
     C                     EXSR P004
     C                     ENDIF                           E*1
     C*
     C** DO WHILE END OF FILE IS NOT REACHED FOR SAME SECURITY
     C           *IN80     DOWEQ*OFF                       B*1
     C           VDSESN    ANDEQDDSESN
     C*
     C           VDBEDT    IFGE DDDATI                     B*2
     C*                                                                   S01496
     C**  Process a change in branch                                      S01496
     C*                                                                   S01496
     C           VDBRCD    IFNE RRBRCA                                    S01496
     C                     EXSR #LBRPR                                    S01496
     C                     END                                            S01496
     C*
     C** IF THE OVERFLOW INDICATOR IS SET ON
     C           *IN88     IFEQ *ON                        B*3
     C*
     C** OUTPUT TITLE TO REPORT
     C                     EXSR R001
     C                     ENDIF                           E*3
     C*
     C** OUTPUT A DETAIL LINE
     C                     EXSR R002
     C                     END                             E*2
     C*
     C******               MOVEA'0000'    *IN,01                          CSE007
     C                     MOVEA'00000000'*IN,01                          CSE007
     C*
     C** READ NEXT RECORD IN LF/SEBKALL1
     C                     READ SEBKALL1                 80
     C                     ENDDO                           E*1
     C*
     C                     ENDSR
     ******************************************************************
      /EJECT
     ******************************************************************
     C**  P004 -
     ******************************************************************
     C           P004      BEGSR
     C*
     C** RESET INDICATORS
     C                     MOVE '0'       *IN50
     C                     MOVE '0'       *IN51
     C                     MOVE '0'       *IN52
     C                     MOVE '0'       *IN53
     C                     MOVE '0'       *IN54
     C*
     C** SAVE CURRENT SECURITY SHORTNAME
     C                     MOVE VDSESN    WSESN  10
     C*
     C           VDSESN    IFNE *BLANKS                    B*0
     C*
     C** ACCESS TO SECURITY DETAILS FILE TO RETRIEVE NUMBER OF DECIMAL PLACES
     C           VDSESN    CHAINSECTY                89
     C*
     C** IF NO REPORT ID FOUND, PRODUCE A STANDARD DATABASE ERROR
     C           *IN89     IFEQ *ON                        B*1
     C                     MOVEL'004'     DBASE            *****************
     C***********          MOVEL'SECTY   'DBFILE           ** DB ERROR 04 S01496
     C                     MOVE 'SECTY   'DBFILE           * DB ERROR 04 *S01496
     C                     MOVELVDSESN    DBKEY            *****************
     C***********          EXSR R001                                      S01496
     C                     EXSR *PSSR
     C                     END                             E*1
     C*
     C**   SETON CORRECT INDICATOR FOLLOWING THE NUMBER OF DECIMAL PLACES
     C           NMDP      IFEQ 0                          B*1
     C                     MOVE '1'       *IN50
     C                     ELSE                            X*1
     C           NMDP      IFEQ 1                          B*2
     C                     MOVE '1'       *IN51
     C                     ELSE                            X*2
     C           NMDP      IFEQ 2                          B*3
     C                     MOVE '1'       *IN52
     C                     ELSE                            X*3
     C           NMDP      IFEQ 3                          B*4
     C                     MOVE '1'       *IN53
     C                     ELSE                            X*4
     C                     MOVE '1'       *IN54
     C                     END                             E*4
     C                     END                             E*3
     C                     END                             E*2
     C                     END                             E*1
     C                     END                             E*0
     C*
     C                     ENDSR
     ******************************************************************
      /EJECT
     ******************************************************************
     C**  P003 - TERMINATING PROCESSING
     ******************************************************************
     C           P003      BEGSR
     C*
     C** IF NO RECORDS ARE PRINTED DURING DETAIL PROCESSING
     C           #FLAG1    IFEQ *OFF                       B*1
     C*
     C** WRITE EMPTY RECORD
     C                     EXSR R003
     C                     ELSE                            X*1
     C*
     C** IF THE OVERFLOW INDICATOR IS SET ON
     C           *IN88     IFEQ *ON                        B*2
     C*
     C** OUTPUT TITLE TO REPORT
     C                     EXSR R001
     C                     ENDIF                           E*2
     C                     ENDIF                           E*1
     C*
     C** WRITE END OF REPORT
     C           WLOPN     IFEQ 'Y'                                       S01496
     C                     EXSR R004
     C                     END                                            S01496
     C*
     C** MOVE *ON TO LR
     C** RETURN OF THE PROGRAM
     C                     MOVE '1'       *INLR
     C*
     C                     ENDSR
     ******************************************************************
      /EJECT
     ******************************************************************
     C**  R001 - OUTPUT TITLE TO PRINTER FILE
     ******************************************************************
     C           R001      BEGSR
     C*
     C** WRITE RECORD INTO THE PRINTER FILE
     C                     WRITESE6860H1
     C           #LINE     IFEQ '3'                                       S01496
     C                     WRITESE6860H3                                  S01496
     C                     ELSE                                           S01496
     C                     WRITESE6860H2
     C                     END                                            S01496
     C*
     C                     ENDSR
     ******************************************************************
      /EJECT
     ******************************************************************
     C**  R002 - OUTPUT DETAILS TO PRINTER FILE
     ******************************************************************
     C           R002      BEGSR
     C*
     C** BRANCH
     C                     MOVEL*BLANKS   PPBRCD
     C                     MOVE VDBRCD    PPBRCD
     C*
     C** CUSTOMER
     C*
     C********** VDCNUM    IFEQ *ZEROS                     B*1                                CSD027
     C           VDCNUM    IFEQ *BLANKS                    B*1                                CSD027
     C                     MOVEL*BLANKS   PPCNUM
     C                     ELSE                            X*1
     C                     MOVE VDCNUM    PPCNUM
     C                     ENDIF                           E*1
     C*
     C** PORTFOLIO
     C           VDPTFR    IFEQ '9997'                     B*1
     C           BGN4ST    ANDNE'Y'                                       CPB001
     C***********          MOVE P9R997    PPPTFR                          S01496
     C                     MOVE FCR997    PPPTFR                          S01496
     C                     ELSE                            X*1
     C                     MOVE VDPTFR    PPPTFR
     C                     END                             E*1
     C*
     C** BLOCK TYPE
     C                     MOVE VDBLTY    PPBLTP
     C*
     C** SECURITY
     C                     MOVEL*BLANKS   PPSESN
     C                     MOVELVDSESN    PPSESN
     C*
     C** BLOCKED NOMINAL
     C                     MOVE *BLANKS   PBLNM0
     C                     MOVE *BLANKS   PBLNM1
     C                     MOVE *BLANKS   PBLNM2
     C                     MOVE *BLANKS   PBLNM3
     C                     MOVE *BLANKS   PBLNM4
     C*
     C                     MOVE '0'       *IN56
      *                                                                   CSE007
      ***  If record is read from SECOALPD.                               CSE007
      *                                                                   CSE007
     C           *IN05     IFEQ '1'                                       CSE007
     C           *IN07     OREQ '1'                                       CSE007
      *                                                                   CSE007
      ***  Format Blocked Nominal work field = Ex-Date Position           CSE007
      *                                                                   CSE007
     C                     Z-ADDMCEXPO    VDNOMW                          CSE007
     C                     ELSE                                           CSE007
     C                     Z-ADDVDNOMB    VDNOMW                          CSE007
     C                     ENDIF                                          CSE007
     C*
     C******     VDNOMB    IFEQ *HIVAL                     B*1            CSE007
     C           VDNOMW    IFEQ *HIVAL                     B*1            CSE007
     C*
     C**   IF BLOCK TYPE IS 'P', DISPLAY '+++'
     C*
     C           VDBLTY    IFEQ 'P'                        B*2
     C                     MOVE '1'       *IN64
     C                     MOVE '0'       *IN66
     C*
     C**   IF BLOCK TYPE IS 'S', DISPLAY '---'
     C*
     C                     ELSE                            X*2
     C                     MOVE '0'       *IN64
     C                     MOVE '1'       *IN66
     C                     END                             E*2
     C*
     C                     ELSE                            X*1
     C                     MOVE '0'       *IN64
     C                     MOVE '0'       *IN66
     C*
     C******     VDNOMB    IFNE *ZEROS                     B*2            CSE007
     C           VDNOMW    IFNE *ZEROS                     B*2            CSE007
     C*
     C**    IF 0 DECIMAL PLACE
     C*
     C           *IN50     IFEQ '1'                        B*3
     C******               MOVE VDNOMB    PBLNM0                          CSE007
     C                     MOVE VDNOMW    PBLNM0                          CSE007
     C                     END                             E*3
     C*
     C**    IF 1 DECIMAL PLACE
     C*
     C           *IN51     IFEQ '1'                        B*3
     C******               MOVE VDNOMB    PBLNM1                          CSE007
     C                     MOVE VDNOMW    PBLNM1                          CSE007
     C                     END                             E*3
     C*
     C**    IF 2 DECIMAL PLACES
     C*
     C           *IN52     IFEQ '1'                        B*3
     C******               MOVE VDNOMB    PBLNM2                          CSE007
     C                     MOVE VDNOMW    PBLNM2                          CSE007
     C                     END                             E*3
     C*
     C**    IF 3 DECIMAL PLACES
     C*
     C           *IN53     IFEQ '1'                        B*3
     C******               MOVE VDNOMB    PBLNM3                          CSE007
     C                     MOVE VDNOMW    PBLNM3                          CSE007
     C                     END                             E*3
     C*
     C**    IF 4 DECIMAL PLACES
     C*
     C           *IN54     IFEQ '1'                        B*3
     C******               MOVE VDNOMB    PBLNM4                          CSE007
     C                     MOVE VDNOMW    PBLNM4                          CSE007
     C                     END                             E*3
     C*
     C** BLOCKED NOMINAL EQUAL TO ZERO
     C                     ELSE                            X*2
     C                     MOVE '1'       *IN56
     C                     END                             E*2
     C                     ENDIF                           E*1
     C*
     C** END DATE
     C                     MOVEL*BLANKS   PPENDT
     C           VDBEDT    IFEQ *HIVAL                     B*1
     C                     MOVEL' +++'    PPENDT
     C                     ELSE                            X*1
     C                     MOVE VDBEDT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    PPENDT
     C                     ENDIF                           E*1
      *                                                                   CSE007
      ***  If feature CSE007 is switched ON.                              CSE007
      ***  or feature CEU017 is switched ON.                              CEU017
      *                                                                   CSE007
     C           CSE007    IFEQ 'Y'                                       CSE007
     C           CEU017    OREQ 'Y'                                       CEU017
      *                                                                   CSE007
      ***  Format Block from Date.                                        CSE007
      *                                                                   CSE007
     C                     MOVE *BLANKS   PPBSTT                          CSE007
     C           VDBSTT    IFNE *ZEROS                                    CSE007
     C                     MOVE VDBSTT    ZDAYNO                          CSE007
     C                     EXSR ZDATE2                                    CSE007
     C                     MOVE ZADATE    PPBSTT                          CSE007
     C                     ENDIF                                          CSE007
      *                                                                   CSE007
      ***  Block Error/Warning Indicator.                                 CSE007
      *                                                                   CSE007
     C                     MOVE *BLANKS   PPBEWI                          CSE007
     C                     MOVE VDEWIN    PPBEWI                          CSE007
     C                     ENDIF                                          CSE007
     C*
     C** NARRATIVE
     C** IF RECORD FROM ALLOCATION FILE (SEERALPD)
     C           *IN01     IFEQ '1'                        B*1
     C           *IN03     OREQ '1'
     C                     MOVE *BLANKS   PPNARR
     C                     MOVE *BLANKS   WWA9    9
     C                     MOVEL'ER'      WWA9
     C                     MOVE VCERRF    WWA9
     C                     MOVE *BLANKS   WWA11  11
     C                     MOVEL' DEP'    WWA11
     C                     MOVE VCDDPT    WWA11
     C                     MOVELWWA9      PPNARR
     C                     MOVE WWA11     PPNARR
     C                     ELSE                            X*1
      *                                                                   CSE007
      ***  If record is read from SECOALPD.                               CSE007
      *                                                                   CSE007
     C           *IN05     IFEQ '1'                                       CSE007
     C           *IN07     OREQ '1'                                       CSE007
     C                     MOVE *BLANKS   PPNARR                          CSE007
     C                     MOVE *BLANKS   WWA9    9                       CSE007
     C                     MOVELMCCOAT    WWA9                            CSE007
     C                     MOVE MCCORF    WWA9                            CSE007
     C                     MOVE *BLANKS   WWA11  11                       CSE007
     C                     MOVEL' DEP'    WWA11                           CSE007
     C                     MOVE MCDDPT    WWA11                           CSE007
     C                     MOVELWWA9      PPNARR                          CSE007
     C                     MOVE WWA11     PPNARR                          CSE007
     C                     ELSE                                           CSE007
     C                     MOVELVDBNAR    PPNARR
     C                     ENDIF                                          CSE007
     C                     ENDIF                           E*1
     C*
     C** TYPE OF LAST CHANGE
     C                     MOVEL*BLANKS   PPLATP
     C                     SELEC                           B*1
     C           VDCHTP    WHEQ 'I'
     C                     MOVEL'Insert ' PPLATP
     C           VDCHTP    WHEQ 'A'
     C                     MOVEL'Amend  ' PPLATP
     C           VDCHTP    WHEQ 'D'
     C                     MOVEL'Delete ' PPLATP
     C                     OTHER                           X*1
     C                     MOVEL'???'     PPLATP
     C                     ENDSL                           E*1
     C*
     C** LAST CHANGE DATE
     C                     MOVE VDLCD     ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    PPLADT
     C*
     C** LAST USER IDENTIFIER
     C                     MOVELVDLUID    PPLAUS
     C*
     C** WRITE RECORD INTO THE PRINTER FILE
     C           #LINE     IFEQ '3'                                       S01496
     C                     WRITESE6860D2                                  S01496
     C                     ELSE                                           S01496
     C                     WRITESE6860D1               88
     C                     END                                            S01496
     C                     MOVE *ON       #FLAG1  1
     C*
     C                     ENDSR
     ******************************************************************
      /EJECT
     ******************************************************************
     C**  R003 - OUTPUT EMPTY REPORT TO PRINTER FILE
     ******************************************************************
     C           R003      BEGSR
     C*
     C***********          WRITESE6860D2                                  S01496
     C*                                                                   S01496
     C**  Open audit printer file and output "No details to report"       S01496
     C*                                                                   S01496
     C                     OPEN SE6860AU                                  S01496
     C                     WRITEHEADAU                                    S01496
     C                     WRITENODTLS                                    S01496
     C*                                                                   S01496
     C**  Ensure audit report is recorded by RCF                          S01496
     C*                                                                   S01496
     C                     Z-ADDSFNUMU    ZSNUM                           S01496
     C                     MOVELSFILEU    SFILE                           S01496
     C                     MOVE *BLANKS   SENTY                           S01496
     C                     EXSR #LZSFL                                    S01496
     C                     CLOSESE6860AU                                  S01496
     C*
     C                     ENDSR
     ******************************************************************
      /EJECT
     ******************************************************************
     C**  R004 - OUTPUT END OF REPORT TO PRINTER FILE
     ******************************************************************
     C           R004      BEGSR
     C*
     C                     WRITESE6860E1
     C                     CLOSESE6860P1                                  S01496
     C                     MOVE *BLANK    WLOPN   1                       S01496
     C*
     C                     ENDSR
     ******************************************************************
      /EJECT
     ******************************************************************
     C**  R009 - DATABASE ERROR ON AUDIT REPORT
     ******************************************************************
     C           R009      BEGSR
     C*
     C           WLOPN     IFEQ 'Y'                                       S01496
     C                     WRITESE6860E2
     C                     END                                            S01496
     C*                                                                   S01496
     C**  Output database error details in audit printer file             S01496
     C*                                                                   S01496
     C                     OPEN SE6860AU                                  S01496
     C                     WRITEHEADAU                                    S01496
     C                     WRITEDBERROR                                   S01496
     C*                                                                   S01496
     C**  Ensure audit report is recorded by RCF                          S01496
     C*                                                                   S01496
     C                     Z-ADDSFNUMU    ZSNUM                           S01496
     C                     MOVELSFILEU    SFILE                           S01496
     C                     MOVEL*BLANKS   SENTY                           S01496
     C                     EXSR #LZSFL                                    S01496
     C                     CLOSESE6860AU                                  S01496
     C*
     C                     ENDSR
     ******************************************************************
      /EJECT
     C*****************************************************************   S01496
     C* #LBRPR - Subroutine to process a change in branch                 S01496
     C*****************************************************************   S01496
     C*                                                                   S01496
     C           #LBRPR    BEGSR                                          S01496
     C*                                                                   S01496
     C**  If printer file is open and entity is not blank, print footer   S01496
     C*                                                                   S01496
     C           WLOPN     IFEQ 'Y'                                       S01496
     C           @RENT     ANDNE*BLANKS                                   S01496
     C*                                                                   S01496
     C**  Output "End of Report"                                          S01496
     C*                                                                   S01496
     C                     EXSR R004                                      S01496
     C*                                                                   S01496
     C                     END                                            S01496
     C*                                                                   S01496
     C**  Get branch name using access object                             S01496
     C*                                                                   S01496
     C**********           CALL 'AOBRCHR0'                                S01496              CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   WLRTCD                          S01496
     C                     PARM '*KEY   ' WLOPTN                          S01496
     C                     PARM VDBRCD    WLBRCA  3                       S01496
     C           SDBRCH    PARM SDBRCH    DSSDY                           S01496
     C*                                                                   S01496
     C**  Check if error occurred                                         S01496
     C*                                                                   S01496
     C           WLRTCD    IFNE *BLANKS                                   S01496
     C                     MOVE 'SDBRCHPD'DBFILE           ***************S01496
     C                     Z-ADD5         DBASE            * DB ERROR 05 *S01496
     C                     MOVE *BLANKS   DBKEY            ***************S01496
     C                     MOVELVDBRCD    DBKEY                           S01496
     C                     EXSR *PSSR                                     S01496
     C                     END                                            S01496
     C*                                                                   S01496
     C                     MOVE A8BRNM    RRBRNM                          S01496
     C                     MOVE VDBRCD    RRBRCA                          S01496
     C*                                                                   S01496
     C**  Open printer file SE6860P1 if entity is not blank               S01496
     C**  or the printer file is not yet open                             S01496
     C*                                                                   S01496
     C           @RENT     IFNE *BLANKS                                   S01496
     C           WLOPN     ORNE 'Y'                                       S01496
     C                     OPEN SE6860P1                                  S01496
     C                     MOVE 'Y'       WLOPN                           S01496
     C*                                                                   S01496
     C** Ensure Report Spool File recorded by RCF                         S01496
     C*                                                                   S01496
     C           @RENT     IFNE *BLANKS                                   S01496
     C                     MOVE VDBRCD    SENTY                           S01496
     C                     END                                            S01496
     C*                                                                   S01496
     C                     Z-ADDSFNUM1    ZSNUM                           S01496
     C                     MOVELSFILE1    SFILE                           S01496
     C                     EXSR #LZSFL                                    S01496
     C*                                                                   S01496
     C**  Output report header                                            S01496
     C*                                                                   S01496
     C                     EXSR R001                                      S01496
     C*                                                                   S01496
     C                     END                                            S01496
     C*                                                                   S01496
     C                     ENDSR                                          S01496
     C*****************************************************************   S01496
     C/EJECT                                                              S01496
     C*****************************************************************   S01496
     C* #LZSFL - Subroutine to call ZSFILE                                S01496
     C*****************************************************************   S01496
     C*                                                                   S01496
     C           #LZSFL    BEGSR                                          S01496
     C*                                                                   S01496
     C                     CALL 'ZSFILE'                                  S01496
     C                     PARM           @RSEQ                           S01496
     C                     PARM           SENTY   3                       S01496
     C                     PARM           SFILE  10                       S01496
     C                     PARM           ZSNUM   60                      S01496
     C                     PARM           ZSERR   1                       S01496
     C*                                                                   S01496
     C** If an error occurred during ZSFILE processing,                   S01496
     C** return to the calling program.                                   S01496
     C*                                                                   S01496
     C           ZSERR     IFEQ 'Y'                                       S01496
     C                     SETON                     U7U8LR               S01496
     C                     RETRN                                          S01496
     C                     END                                            S01496
     C*                                                                   S01496
     C                     ENDSR                                          S01496
     C*****************************************************************   S01496
     C/EJECT                                                              S01496
     ******************************************************************
     C**  *PSSR - STANDARD DATA BASE ERROR PROCESSING
     ******************************************************************
     C           *PSSR     BEGSR
     C*
     C** UPDATE LDA WITH ERROR INFORMATION
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVELDBFILE    WWFILE
     C                     MOVELDBKEY     WWKEY
     C                     MOVELDBPGM     WWPGM
     C                     MOVELDBASE     WWASE
     C                     OUT  LDA
     C*
     C**   OUTPUT ERROR ON PRINTER FILE
     C                     EXSR R009
     C*
     C**   PRINT DUMP AND CANCEL PROGRAM
     C                     DUMP
     C                     MOVE 'E'       DDRTCD
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     RETRN
     C*
     C                     ENDSR
     ******************************************************************
     C/COPY ZSRSRC,ZDATE2Z2
**  CPY@
(c) Finastra International Limited 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
