     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Corporate actions - initialisation')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7500 - Corporate Actions - Initialisation program          *
      *                                                               *
      *  Function:  This program initialises the trailer files        *
      *             with default values.                              *
      *                                                               *
      *  (phase(s)) Installation                                      *
      *                                                               *
      *  Called By:                                                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Last Amend No. CSE020             Date 21Mar00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CEU017             Date 15Mar98               *
      *                 CSE007  *CREATE    Date 12Mar98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE020 - Corporate Actions Domino Effect Processing          *
      *           (Recompiled due to changes in PF/SECORFPD)          *
      *  CEU017 - Securities Redenomination Euro Changes              *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
      /TITLE F LINES SPECIFICATIONS
      *****************************************************************
      *
     FSECOATPAUF  E                    DISK                      A
      *
     FSECOCHPAUF  E                    DISK                      A
      *
     FSECONAPAUF  E                    DISK                      A
      *
     FSECORFPAUF  E                    DISK                      A
      *
     FSECOSIPAUF  E                    DISK                      A
      *
     FSECOFDPAUF  E                    DISK                      A
      *                                                                   CEU017
     FSECORBPAUF  E                    DISK                      A        CEU017
      *
      *****************************************************************
      /TITLE E LINES SPECIFICATIONS
      *****************************************************************
     E                    CPY@    1   1 80               COPYRIGHT  ARRAY
      *****************************************************************
      /TITLE I LINES SPECIFICATIONS
      *****************************************************************
      *
     IPGMDS     ESDSY2PGDSP
      * Program Data Structure
      *
     ISDBANK    E DSSDBANKPD
      * External DS for Bank Details
      *
     I@SARD     E DSSCSARDPD
      **  Data structure for switchable features.
      *
     IDSSDY     E DSDSSDY
      * DS for access programs
      *
     IDSFDY     E DSDSFDY
      **  First DS for access programs, short DS
      *****************************************************************
      /EJECT
      *****************************************************************
      *   START PROGRAM : MAINLINE
      *****************************************************************
      *
     C           *ENTRY    PLIST
     C                     PARM           @RTCD   7
     C                     PARM           @MODE   1
      *
      ** Reference Copyright Array
     C                     MOVEACPY@      CPY@@   1
      *
      ** Access Bank details via access program
      ** (database error handling done in access program)
     C                     CALL 'AOBANKR0'
     C                     PARM '*DBERR ' @RTCD
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSSDY
      *
      ** Check if SAR CSE007 is ON.
     C                     CALL 'AOSARDR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*VERIFY' @OPTN
     C                     PARM 'CSE007'  @SARC   6
     C           @SARD     PARM @SARD     DSFDY
      *
      **  Set indicator if installed.
     C           @RTCD     IFEQ *BLANKS
     C                     MOVE 'Y'       CSE007  1
     C                     ELSE
     C                     MOVE 'N'       CSE007
     C                     END
      *                                                                   CEU017
      ** Check if SAR CEU017 is ON.                                       CEU017
     C                     CALL 'AOSARDR0'                                CEU017
     C                     PARM *BLANKS   @RTCD                           CEU017
     C                     PARM '*VERIFY' @OPTN                           CEU017
     C                     PARM 'CEU017'  @SARC   6                       CEU017
     C           @SARD     PARM @SARD     DSFDY                           CEU017
      *                                                                   CEU017
      **  Set indicator if installed.                                     CEU017
     C           @RTCD     IFEQ *BLANKS                                   CEU017
     C                     MOVE 'Y'       CEU017  1                       CEU017
     C                     ELSE                                           CEU017
     C                     MOVE 'N'       CEU017                          CEU017
     C                     END                                            CEU017
      *
     C           @MODE     IFEQ '1'
     C           @MODE     OREQ '2'
      *
     C           CSE007    IFEQ 'Y'
     C           CEU017    OREQ 'Y'                        CEU017
      *
     C           1         SETLLSECOATPA
     C                     READ SECOATPA                 89
      *
      ** If record not found in the file : FIELDS SET UP & WRITE
     C           *IN89     IFEQ '1'
      *
     C                     MOVE 'Z'       MMRECI
     C                     Z-ADD*ZEROS    MMNORE
      *
     C                     WRITESECOATZ0
     C                     ENDIF
      *
     C           1         SETLLSECORFPA
     C                     READ SECORFPA                 89
      *
      ** If record not found in the file : FIELDS SET UP & WRITE
     C           *IN89     IFEQ '1'
      *
     C                     MOVE 'D'       MXRECI
     C                     Z-ADDBJRDNB    MXLCD
     C                     MOVE 'I'       MXCHTP
     C                     MOVE ##USR     MXLUID
     C                     MOVE '000001'  MXCORF
      *
     C                     WRITESECORFP0
     C                     ENDIF
      *
     C           1         SETLLSECOFDPA
     C                     READ SECOFDPA                 89
      *
      ** If record not found in the file : FIELDS SET UP & WRITE
     C           *IN89     IFEQ '1'
      *
     C                     MOVE 'Z'       MWRECI
     C                     Z-ADD*ZEROS    MWNORE
      *
     C                     WRITESECOFDZ0
     C                     ENDIF
      *
     C           1         SETLLSECOSIPA
     C                     READ SECOSIPA                 89
      *
      ** If record not found in the file : FIELDS SET UP & WRITE
     C           *IN89     IFEQ '1'
      *
     C                     MOVE 'Z'       MORECI
     C                     Z-ADD*ZEROS    MONORE
      *
     C                     WRITESECOSIZ0
     C                     ENDIF
     C                     ENDIF
      *
     C           CSE007    IFEQ 'Y'
      *
     C           1         SETLLSECOCHPA
     C                     READ SECOCHPA                 89
      *
      ** If record not found in the file : FIELDS SET UP & WRITE
     C           *IN89     IFEQ '1'
      *
     C                     MOVE 'Z'       MRRECI
     C                     Z-ADD*ZEROS    MRNORE
      *
     C                     WRITESECOCHZ0
     C                     ENDIF
      *
     C           1         SETLLSECONAPA
     C                     READ SECONAPA                 89
      *
      ** If record not found in the file : FIELDS SET UP & WRITE
     C           *IN89     IFEQ '1'
      *
     C                     MOVE 'Z'       MNRECI
     C                     Z-ADD*ZEROS    MNNORE
      *
     C                     WRITESECONAZ0
     C                     ENDIF
     C                     ENDIF
      *                                                                   CEU017
     C           CEU017    IFEQ 'Y'                                       CEU017
      *                                                                   CEU017
     C           1         SETLLSECORBPA                                  CEU017
     C                     READ SECORBPA                 89               CEU017
      *                                                                   CEU017
      ** If record not found in the file : FIELDS SET UP & WRITE          CEU017
     C           *IN89     IFEQ '1'                                       CEU017
      *                                                                   CEU017
     C                     MOVE 'D'       NBRECI                          CEU017
     C                     Z-ADDBJRDNB    NBLCD                           CEU017
     C                     MOVE 'I'       NBCHTP                          CEU017
     C                     MOVE ##USR     NBLUID                          CEU017
     C                     Z-ADD1         NBBLKR                          CEU017
      *                                                                   CEU017
     C                     WRITESECORBD0                                  CEU017
     C                     ENDIF                                          CEU017
     C                     ENDIF                                          CEU017
      *
     C                     ENDIF
      *
     C                     SETON                         LR
     C                     RETRN
      *
      *****************************************************************
      /EJECT
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
