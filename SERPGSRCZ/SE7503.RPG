     H        1
      *****************************************************************
/*OVRF*: OVRDBF FILE(SECTYZ1) TOFILE(SECTY) SHARE(*NO)              : *
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Corporate actions dividends events')          *
     F*****************************************************************
     F*                                                               *
     F*  Midas - Securities Trading Module                            *
     F*                                                               *
     F*  SE7503 - Corporate Action - Dividends Events                 *
     F*                                                               *
     F*  Function:  This program maintains the Corporate Action type  *
     F*  (I/C)      DV (Dividends Events).                            *
     F*                                                               *
     F*  Called By: SE7599 - Corporate Actions - Options Maintenance  *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. 247764             Date 12Jun07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *  Prev Amend No. CSE071             Date 19Jul05               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 190204             Date 31May01               *
      * Midas DBA 3.04 -----------------------------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CAP137             Date 07Feb00               *
      *                 CSE020             Date 21Mar00               *
      *                 161732             Date 21Mar00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 161592             Date 03Jun99               *
      *                 136935             Date 18Aug98               *
     F*                 137884             Date 20Jul98               *
     F*                 138957             Date 16Jul98               *
     F*                 138962             Date 13Aug98               *
     F*                 135689             Date 02Jun98               *
     F*                 CSE007  *CREATE    Date 01DEC97               *
     F*                                                               *
     F*--------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  247764 - Database Error on Corporate Action Screen.  Apply   *
      *           UBS-MAD local fix 221720.                           *
      *  221720 - Return to General Information screen when F12 is    *
      *           pressed from detail screen and there is only 1      *
      *           option.                                             *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  190204 - Changed Dividend per Unit format from 9N0 to 13N8.  *
      *  CAP137 - Conversion of SE Security inputs into modular       *
      *           structure to use as APIs.                           *
      *  CSE020 - Corporate Actions Domino Effect Processing          *
      *           (Recompiled due to changes in PF/SECORFPD)          *
      *  161732 - Corporate Actions Exercise and Conversion -         *
      *           Part Stock/Keep Rest Calculation                    *
      *           (Recompiled due to changes in PF/SECOEXPD)          *
     F*  161592 - Comp. price should be defaulted from Subs. price    *
     F*           minus Discount rate for Dividend type 'O' and 'S'   *
     F*  136935 - Corporate Actions: Allocation processing amendments:*
     F*           Default Subscription Right Shortname with New Secu- *
     F*           rity instead of Event Security and use Number of    *
     F*           Decimal Places of Event Security for Right Denomi-  *
     F*           nation Multiplier (Stock Dividend Event Security    *
     F*           will now be Exercise and Conversion New Security).  *
     F*  137884 - Corporate Actions: Cash fields can be changed even  *
     F*           if the Corporate action is stock authorised.        *
     F*  138957 - Corporate Actions: Review information passed to     *
     F*           RPG/SE0040 (Security Details Maintenance) for a     *
     F*           Name Change event where the Security Shortname is   *
     F*           being changed (when F15 is pressed).                *
     F*  138962 - Corporate Actions: various errors:                  *
     F*           When Corporate Type Exercises and Conversion linked *
     F*           to Dividend Events or Right Issues was created      *
     F*           using amendments on Dividend Events or Right Issues *
     F*           Option, this new Corporate Exercises and Conversion *
     F*           did not appeared, so refresh subfile with using new *
     F*           parameter transmited between programs.              *
     F*  135689 - Corporate Actions                                   *
     F*           - Error in validation of Dividend per Unit          *
     F*  CSE007 - Corporate Actions                                   *
     F*                                                               *
     F*****************************************************************
     F*
     F**  MA - SE CORPORATE ACTIONS References - Upd Idx
     FSECORFL0UF  E           K        DISK                      A
     F                                              KINFSR *PSSR
     F                                              KCOMIT
      *
      **  MD - SE Corporate Actions - Dividends Events
     FSECODVL0UF  E           K        DISK                      A
     F                                              KINFSR *PSSR
     F                                              KCOMIT
      *
      **  SE Security Details
     FSECTYZ1 IF  E           K        DISK         KINFSR *PSSR
      *
      **  SE Investment Type
     FINVTP   IF  E           K        DISK         KINFSR *PSSR
      *
      **  SE Market Price
     FPRICM   IF  E           K        DISK         KINFSR *PSSR          E27843
      *
      **  SE Corporate Actions - Exercises and Conversions
     FSECOEXL2IF  E           K        DISK         KINFSR *PSSR
      *
      **   MN - SE Option Narrative Code
     FSECONAL0IF  E           K        DISK         KINFSR *PSSR
      *
      **   MM - SE Corporate Action Type
     FSECOATL0IF  E           K        DISK         KINFSR *PSSR
     F/COPY WNCPYSRC,SE7503F001
      *
      **  Display file
     FSE7503DFCF  E                    WORKSTN      KINFSR *PSSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *                  FUNCTION OF INDICATORS                       *
      *                  ----------------------                       *
      *                                                               *
      *       15         Call Security Maintenance program            *
      *       20-54      Fields error in Display file                 *
      *       60         LS = Final Allocation Stock                  *
      *       61         LY = Final Allocation Cash and Stock         *
      ******* 62         S = Stock Authorised                         *   137884
      *******            X = Fully Authorised (Cash and Stock)        *   137884
      *       62         X = Fully Authorised (Cash and Stock)        *   137884
      *       63         S = Stock Authorised                         *   137884
      *       70         0 - Amend or Insert mode                     *
      *                  1 - Enquire mode                             *
      *       75         Record not found.                            *
      *       80-89      Standard RTS subroutines                     *
      *       90-99      Standard MIDAS subroutines                   *
      *       KC         Exit program                                 *
      *       KL         Previous                                     *
      *       LR         End program.                                 *
      *       U7+U8      Database error occurs                        *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  P001 - Initial Processing                                    *
      *  P002 - Main Processing                                       *
      *  P003 - End Processing                                        *
      *  P004 - Clear both screens fields                             *
      *  P005 - Initialise 1st screen fields                          *
      *  P006 - Validate 1st Details entered                          *
      *  P007 - Save screen fields                                    *
      *  P008 - Subscription Rights Tradable Processing               *
      *  P009 - Validate 2nd Details entered                          *
      *  P010 - Initialise 2nd screen fields                          *
      *  *PSSR - Program error handling routine                       *
      *  ZASNMS - Send message to program's message queue             *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      **  Array for Copyright
     E                    CPY@    1   1 80
      *
     E***********         NARR    1   2 27               Narrative        CAP137
     E                    NARR    1   3 27                                CAP137
      *
     E                    TITLE   1   2 16               Screen name
      *
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZALIGNZ1
     E/COPY ZSRSRC,ZSEDITZ1
     E                    POWER8  1   8  8 3             POWER8 ARRAY
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      **  Program Data Structure
     IPGMDS     ESDSY2PGDSP
      *
      **   LOCAL DATA AREA
     ILDA       E DSLDA                         256
      *
      **  Local data area for database error details
      **  *LOCK IN LDA immediately before and OUT LDA immediately
      **  after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *                                                                   135689
      ***  First Data Strusture for Access Programs, Long Data Structure. 135689
     ISDCURR    E DSSDCURRPD                                              135689
      *
      **  Bank Details
     ISDBANK    E DSSDBANKPD
      *                                                                   CAP137
      ** Externally described DS for SAR details                          CAP137
     ISCSARD    E DSSCSARDPD                                              CAP137
      *                                                                   CAP137
      ** DS for Access program, Short data structure                      CAP137
     IDSFDY     E DSDSFDY                                                 CAP137
      *
      **  Second DS for Access Programs, Long Data Structure
     IDSSDY     E DSDSSDY
     I/COPY WNCPYSRC,SE7503I001
      *
     I/COPY ZSRSRC,ZSEDITZ2
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            MAIN   - Main Routine.                             *
      *                                                               *
      * CALLS      P001   - Initial Processing.                       *
      *            P002   - Main Processing.                          *
      *            P003   - End Processing.                           *
      *            SE7509 - Corporate Actions Exercise and Conversion *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
      *
      **  Input Parameter List
      *
     C           *ENTRY    PLIST
     C                     PARM           @CORF   6        Reference
     C                     PARM           @SESN  10        Security Short.
     C                     PARM           @COAT   2        Corporate Type
     C                     PARM           @STAT   1        Corporate Action Stat
     C                     PARM           @FINA   1        Final Allocation Indi
     C                     PARM           @COEX   5        Ex-Date
     C                     PARM           @OPNB   2        Option Number
     C                     PARM           @MODE   1        Mode
     C                     PARM           @RTCD   1        Return Code
     C                     PARM           @WREX   1                       138962
     C/COPY WNCPYSRC,SE7503C005
      *
      **  Initial Processing
     C                     EXSR P001
      *
      **  Main Processing
     C                     EXSR P002
      *
      **  Call SE7509 Corporate Action Maintenance for 'EX'
     C           *INKC     IFEQ '0'
     C           *INKL     ANDEQ'0'
     C           SSBRT     ANDEQ'Y'
     C           SSBRS     ANDNE*BLANKS
     C           SREAT     ANDNE*BLANKS
     C           SREXD     ANDNE*BLANKS
     C           @MODE     ANDNE'E'
      *
      **  ONLY if Corporate Action EX has not already been created
     C           @CORF     CHAINSECOEXL2             89
      *
     C           *IN89     IFEQ '1'
      *
     C           @MODE     IFEQ 'A'
     C           @MODE     OREQ 'I'
     C                     MOVE 'U'       PMODE   1
     C                     ENDIF
      *
     C                     MOVE @CORF     PCORF   6
     C                     MOVE SSBRS     PSBRS  10
     C                     MOVE SREAT     PREAT   2
     C                     MOVE @STAT     PSTAT   1
     C                     MOVE @FINA     PFINA   1
      *
     C                     MOVE SREXD     ZDATE
     C                     EXSR ZDATE1
     C                     MOVE ZDAYNO    PCOEX   5
      *
     C                     MOVE @OPNB     POPNB   1
      *
     C                     MOVE ' '       PRTCD   1
      *
     C                     CALL 'SE7509'               99
     C                     PARM           PCORF            Reference
     C                     PARM           PSBRS            Security Short.
     C                     PARM           PREAT            Corporate Type
     C                     PARM           PSTAT            Corporate Action Stat
     C                     PARM           PFINA            Final Allocation Indi
     C                     PARM           PCOEX            Ex-Date
     C                     PARM           POPNB            Option Number
     C                     PARM           PMODE            Mode
     C                     PARM           PRTCD            Return Code
     C                     PARM           @WREX                           138962
     C/COPY WNCPYSRC,SE7503C006
     C                     ENDIF
      *
     C                     ENDIF
      *
      **  End Processing
     C                     EXSR P003
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            P001   - Initial Processing                        *
      *                                                               *
      * CALLS      *PSSR  - Program error handling routine            *
      *            ZDATE2 - Convert a day number to a date format     *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           P001      BEGSR
      *
      **  Copyright Statement
     C                     MOVEACPY@      MKI@   80
      *
      **  Initialise Key and Working Fields
     C           KEY1      KLIST
     C                     KFLD           @CORF   6        Reference
     C                     KFLD           WOPNB   20       Option Number
      *
      ** Define key list for file INVTP
     C           KEY2      KLIST
     C                     KFLD           CCYI
     C                     KFLD           INVT
      *
     C                     MOVE @OPNB     WOPNB            Option Number
      *
      ** Local Data Area
      *
     C           *NAMVAR   DEFN           LDA
      *
     C                     MOVEL'SE7503'  DBPGM
      *
      **  Setup Screen Time
     C                     TIME           ##TME   60       Screen time.
      *
      **  Access Bank Details
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   WLRTCD  7
     C                     PARM '*FIRST'  WLOPTN  7
     C           SDBANK    PARM SDBANK    DSSDY
      *
      **  If record not found
     C           WLRTCD    IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE
     C                     MOVE 'SDBANKPD'DBFILE
     C                     MOVEL*BLANKS   DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVE BJMRDT    ##MRDT           Midas Run Date
      *
      **  Determine Date Format
     C           BJDFIN    IFEQ 'M'
     C                     MOVE '1'       *IN98
     C                     ENDIF
      *                                                                   CAP137
      ** Check if API Securities New Screen Input is installed.           CAP137
      *                                                                   CAP137
     C                     CALL 'AOSARDR0'                                CAP137
     C                     PARM *BLANKS   WLRTCD                          CAP137
     C                     PARM '*VERIFY' WLOPTN                          CAP137
     C                     PARM 'CAP137'  WLSARD  6                       CAP137
     C           SCSARD    PARM SCSARD    DSFDY                           CAP137
      *                                                                   CAP137
      ** An NRF (No Record Found) return code is valid.                   CAP137
      ** Issue database error only for error return codes.                CAP137
      *                                                                   CAP137
     C           WLRTCD    IFNE *BLANKS                                   CAP137
     C           WLRTCD    ANDNE'*NRF   '                                 CAP137
     C           *LOCK     IN   LDA                                       CAP137
     C                     Z-ADD9         DBASE                           CAP137
     C                     MOVEL'SCSARDPD'DBFILE                          CAP137
     C                     MOVEL'CAP137'  DBKEY                           CAP137
     C                     OUT  LDA                                       CAP137
     C                     EXSR *PSSR                                     CAP137
     C                     ENDIF                                          CAP137
      *                                                                   CAP137
     C           WLRTCD    IFEQ *BLANK                                    CAP137
     C                     MOVEL'Y'       CAP137  1                       CAP137
     C                     MOVEL'SESECS'  PPGM    9                       CAP137
     C                     MOVE 'SIN'     PPGM                            CAP137
     C                     ELSE                                           CAP137
     C                     MOVEL'N'       CAP137                          CAP137
     C                     MOVE *BLANKS   PPGM                            CAP137
     C                     ENDIF                                          CAP137
     C/COPY WNCPYSRC,SEH00008
      *
     C                     MOVE @CORF     SCORF            Reference
     C                     MOVE @SESN     SSESN            Security Short.
     C                     MOVE @COAT     SCOAT            Corporate Type
      *
     C           @COEX     IFNE *BLANKS
     C           @COEX     ANDNE'00000'
     C                     MOVE @COEX     ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     SEXDT            Ex-Date
     C                     ELSE
     C                     MOVE *BLANKS   SEXDT            Ex-Date
     C                     ENDIF
      *
     C                     MOVE @OPNB     SOPNB            Option Number
      *
      **  Retrieve Nominal Currency of the Event Security
     C           @SESN     CHAINSECTYZ1              89
      *
      **  If record not found
     C           *IN89     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE
     C                     MOVE 'SECTYD'  DBFILE
     C                     MOVEL@SESN     DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     Z-ADDNMDP      EVNMDP  10                      136935
      *
      ** Just for definition of the file in 'F' card with a 'A' (=ADD)
      ** Needed because file already open in calling program in Update
      ** Mode with ADD option specified
     C           *IN99     IFEQ '0'                        B*1
     C           *IN99     ANDEQ'1'
     C                     WRITESECORFD0
     C                     ENDIF                           E*1
      *
      **  Fill in Fields for Subroutine ZASNMS
     C                     MOVEL'SE7503'  ZAPGM  10        Message queue
     C                     MOVEL'SEUSRMSG'ZAMSGF 10        Message file
     C                     MOVEL*BLANK    ZAPGRL  5        Rel queue
     C                     MOVEL*BLANK    ZAMSID  7        Message Id.
     C                     MOVEL*BLANK    ZAMSDA132        Message data.
     C                     MOVEL*BLANK    ZAMSTP  7        Message type.
     C/COPY WNCPYSRC,SE7503C007
      *
      **  Clear Messages from Program Message Queue.
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
      *
     C                     WRITESE7503C1
      *
     C/COPY WNCPYSRC,SE7503C001
      *
      **  Determine fields to protect depending of the Status and the Final Allo
     C                     MOVE '0'       *IN60
     C                     MOVE '0'       *IN61
     C                     MOVE '0'       *IN62
     C                     MOVE '0'       *IN63                           137884
      *
      **  LS=Final Allocation Stock          (*IN60)
      **  LY=Final Allocation Cash and Stock (*IN61)
      ****S*=Stock*Authorised****************(*IN62)                      137884
      **  S =Stock Authorised                (*IN63)                      137884
      **  X =Fully Authorised (Cash & Stock) (*IN62)
     C           @STAT     IFEQ 'L'                        B*1
      *
     C           @FINA     IFEQ 'S'                        B*2
     C                     MOVE '1'       *IN60
     C                     ELSE                            X*2
      *
     C           @FINA     IFEQ 'Y'                        B*3
     C                     MOVE '1'       *IN61
     C                     ENDIF                           E*3
      *
     C                     ENDIF                           E*2
      *
     C                     ELSE                            X*1
     C           @STAT     IFEQ 'S'                        B*2
     C           @STAT     OREQ 'X'
     C                     MOVE '1'       *IN62
     C           @STAT     IFEQ 'S'                                       137884
     C                     MOVE '1'       *IN63                           137884
     C                     ENDIF                                          137884
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
     C/COPY WNCPYSRC,SE7503C008
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            P002   - Main Processing                           *
      *                                                               *
      * CALLS      P004   - Clear screen fields                       *
      *            P005   - Initialise 1st screen fields              *
      *            P006   - Validate Details entered                  *
      *            P007   - Save screen fields                        *
      *            P010   - Initialise 2nd screen fields              *
      *            SE0040 - Security detail maintenance               *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           P002      BEGSR
      *
     C                     MOVE 'N'       WWEND   1
      *
      **  Protect fields if the mode is not Insert/Amend
     C           @MODE     IFEQ 'A'                        B*1
     C           @MODE     OREQ 'I'
     C                     MOVE '0'       *IN70
     C                     ELSE                            X*1
     C                     MOVE '1'       *IN70
     C                     ENDIF                           E*1
      *
     C           KEY1      CHAINSECODVL0             75
      *
     C           *IN75     IFEQ '1'                        B*1
      *
      **  Initialise screen fields
     C                     EXSR P004
     C                     ELSE                            X*1
      *
      **  In case of Amend or Enquiry mode, load screen fields with file fields
     C                     EXSR P005
     C                     EXSR P010
     C                     ENDIF                           E*1
      *
      **  Setof error indicators from *IN20 to *IN56
     C                     MOVEA'00000000'*IN,20
     C                     MOVEA'00000000'*IN,28
     C                     MOVEA'00000000'*IN,36
     C                     MOVEA'00000000'*IN,44
     C                     MOVEA'00000'   *IN,52
      *
      **  Clear Messages from Program Message Queue.
     C/COPY WNCPYSRC,SE7503C009
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
     C/COPY WNCPYSRC,SE7503C010
      *
      **  Do while F3, F12 not pressed
     C           *INKC     DOWEQ'0'                        B*1
     C           *INKL     ANDEQ'0'
     C           WWEND     ANDEQ'N'
      *
      **  Enquiry Mode
     C           *IN70     IFEQ '1'                        B*2
     C                     MOVE 'Y'       WWEND
     C                     ENDIF                           E*2
      *
     C                     TIME           ##TME            Screen time.
     C                     MOVE TITLE,1   STITLE
     C/COPY WNCPYSRC,SE7503C011
      *
     C                     WRITESE7503C1
     C                     EXFMTSE7503F1
     C/COPY WNCPYSRC,SE7503C012
      *
      **  If Enquire mode, display the 2nd screen
     C           *IN70     IFEQ '1'
     C           SSBRT     ANDEQ'Y'
     C           *INKC     ANDEQ'0'
     C           *INKL     ANDEQ'0'
     C/COPY WNCPYSRC,SE7503C013
     C                     WRITESE7503C1
     C                     EXFMTSE7503F2
     C/COPY WNCPYSRC,SE7503C014
     C                     ENDIF
      *
      **  Setof error indicators from *IN20 to *IN56
     C                     MOVEA'00000000'*IN,20
     C                     MOVEA'00000000'*IN,28
     C                     MOVEA'00000000'*IN,36
     C                     MOVEA'00000000'*IN,44
     C                     MOVEA'00000'   *IN,52
      *
      **  Clear Messages from Program Message Queue.
     C/COPY WNCPYSRC,SE7503C015
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
     C/COPY WNCPYSRC,SE7503C016
      *
      **  If <enter> pressed, validate screen fields
     C           @MODE     IFEQ 'A'                        B*2
     C           @MODE     OREQ 'I'
      *
     C           *INKC     IFEQ '0'                        B*3
     C           *INKL     ANDEQ'0'
     C           *IN15     ANDEQ'0'
     C                     MOVE 'N'       ERROR   1
     C                     EXSR P006
      *
      **  If no error, update file and return to previous screen
     C           ERROR     IFEQ 'N'                        B*4
     C                     EXSR P007
     C                     MOVE 'Y'       WWEND
     C                     ENDIF                           E*4
     C/COPY WNCPYSRC,SE7503C017
      *
     C                     ENDIF                           E*3
      *
     C                     ENDIF                           E*2
      *
      **  If F15 pressed, Call "Securities Maintenance" program
     C           *IN15     IFEQ '1'                        B*2
      *
      ** Call SE0040 with the action code 'I'
     C           CAP137    IFNE 'Y'                                       CAP137
     C                     CALL 'SE0040'               99
     C                     PARM *BLANKS   #RTRN   7
     C                     PARM 'I'       #ACTC   1
     C                     PARM SNSEC     #SESN  10
     C                     PARM SSESN     #SESO  10
     C                     PARM *BLANKS   #OTHER156                       138957
     C                     ELSE                                           CAP137
     C                     CALL 'SEC023'               99                 CAP137
     C                     PARM           PPGM                            CAP137
     C                     PARM *BLANKS   #RTRN                           CAP137
     C                     PARM 'I'       #ACTC                           CAP137
     C                     PARM SNSEC     #SESN                           CAP137
     C                     PARM SSESN     #SESO                           CAP137
     C                     PARM *BLANKS   #OTHER                          CAP137
     C                     PARM 'Y'       PLINK   1                       CAP137
     C                     ENDIF                                          CAP137
      *
     C           *IN99     IFEQ '1'
     C           *INU7     OREQ '1'
     C           *INU8     OREQ '1'
     C           *LOCK     IN   LDA
     C                     Z-ADD3         DBASE
     C                     MOVEL*BLANKS   DBFILE
     C           CAP137    IFNE 'Y'                                       CAP137
     C                     MOVELNARR,1    DBKEY
     C                     ELSE                                           CAP137
     C                     MOVELNARR,3    DBKEY                           CAP137
     C                     ENDIF                                          CAP137
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDIF                           E*2
     C/COPY WNCPYSRC,SE7503C004
      *
     C                     ENDDO                           E*1
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            P003   - End Processing                            *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           P003      BEGSR
      *
     C           *INKC     IFEQ '1'                        B*1
     C                     MOVE '3'       @RTCD
     C                     ENDIF                           E*1
      *                                                                                       247764
     C           *INKL     IFEQ '1'                                                           247764
     C                     MOVE '2'       @RTCD                                               247764
     C                     ENDIF                                                              247764
      *
      **  End Program
     C                     MOVE '1'       *INLR
     C/COPY WNCPYSRC,SE7503C018
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            P004   - Clear all screen fields                   *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY  P002   - Main Processing                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           P004      BEGSR
      *
      ** 1st screen fields
     C                     MOVE *BLANKS   SNSEC
     C                     MOVE *BLANKS   SSDIT
     C                     MOVE *BLANKS   SDIVU
     C                     MOVE *BLANKS   SSBPR
     C                     MOVE *BLANKS   SDSRT
     C                     MOVE *BLANKS   SNSHA
     C                     MOVE *BLANKS   SOSHA
     C                     MOVE *BLANKS   SDMLT
     C                     MOVE *BLANKS   SDIVR
     C                     MOVE *BLANKS   SRNDN
     C                     MOVE *BLANKS   SRNDS
     C                     MOVE *BLANKS   SCOMP
     C                     MOVE *BLANKS   SOPT1
     C                     MOVE *BLANKS   SNAR1
     C                     MOVE *BLANKS   SOPT2
     C                     MOVE *BLANKS   SNAR2
     C                     MOVE *BLANKS   SOPT3
     C                     MOVE *BLANKS   SNAR3
     C                     MOVE *BLANKS   SOPT4
     C                     MOVE *BLANKS   SNAR4
     C                     MOVE *BLANKS   SOPT5
     C                     MOVE *BLANKS   SNAR5
     C                     MOVE *BLANKS   SDFNR
     C                     MOVE *BLANKS   SOPSH
     C                     MOVE *BLANKS   STXT1
     C                     MOVE *BLANKS   STXT2
     C                     MOVE *BLANKS   STXT3
     C                     MOVE *BLANKS   STXT4
     C                     MOVE *BLANKS   SSUBF
     C                     MOVE *BLANKS   SSUBT
     C                     MOVE *BLANKS   SSBRT
      *
      **  2nd screen fields
     C                     MOVE *BLANKS   SSBRS
     C                     MOVE *BLANKS   SREPR
     C                     MOVE *BLANKS   SREXD
     C                     MOVE *BLANKS   SREXA
     C                     MOVE *BLANKS   SREAT
     C                     MOVE *BLANKS   SRTRF
     C                     MOVE *BLANKS   SRTRT
     C                     MOVE *BLANKS   SRNNS
     C                     MOVE *BLANKS   SRNRI
     C                     MOVE *BLANKS   SRMLT
     C                     MOVE *BLANKS   SRDIV
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            P005   - Initialise 1st screen fields              *
      *                                                               *
      * CALLS      *PSSR  - Program error handling routine            *
      *                                                               *
      * CALLED BY  P002   - Main Processing                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           P005      BEGSR
      *
      **  1st screen fields
      *
     C           MDNSEC    IFEQ *BLANKS                    B*1
     C                     MOVE SSESN     SNSEC
     C                     ELSE                            X*1
     C                     MOVE MDNSEC    SNSEC
     C                     ENDIF                           E*1
      *
      **  Retrieve Nominal Currency of the New Security
     C           SNSEC     CHAINSECTYZ1              89
      *
      **  If record not found
     C           *IN89     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     Z-ADD4         DBASE
     C                     MOVE 'SECTYD'  DBFILE
     C                     MOVELMDNSEC    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVE MDSDIT    SSDIT
      *
     C********** MDDIVU    IFNE *ZEROS                                    190204
     C           MDDIV1    IFNE *ZEROS                                    190204
      *                                                                   135689
     C**********           CALL 'AOCURRR0'                         135689 190204
     C**********           PARM '*MSG   ' WLRTCD                   135689 190204
     C**********           PARM '*KEY   ' WLOPTN                   135689 190204
     C**********           PARM NMCY      WLAJCD  3                135689 190204
     C********** SDCURR    PARM SDCURR    DSSDY                    135689 190204
      *                                                                   135689
     C                     MOVE *BLANKS   ZFIELD
     C**********           MOVE MDDIVU    ZFIELD                          190204
     C                     MOVE MDDIV1    ZFIELD                          190204
     C*****                Z-ADDNMDP      ZADEC                           135689
     C**********           Z-ADDA6NBDP    ZADEC                    135689 190204
     C                     Z-ADD8         ZADEC                           190204
     C                     EXSR ZEDIT
     C                     MOVEL*BLANKS   SDIVU
     C**********           MOVE ZFIELD    WFLD9   9                       190204
     C**********           MOVELWFLD9     SDIVU                           190204
     C                     MOVE ZFIELD    WFLD14 14                       190204
     C                     MOVELWFLD14    SDIVU                           190204
     C                     ELSE                            X*1
     C                     MOVE *BLANKS   SDIVU
     C                     ENDIF                           E*1
      *
     C           MDSBPR    IFNE *ZEROS                     B*1
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE MDSBPR    ZFLD15
     C                     Z-ADD8         ZDECS
     C                     MOVE 'L'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVEL*BLANKS   SSBPR
     C                     MOVE ZOUT21    WFLD17 17
     C                     MOVELWFLD17    SSBPR
     C                     ELSE                            X*1
     C           MDMRKI    IFEQ 'Y'                        B*2
     C                     MOVEL'MARKET'  SSBPR     P
     C                     ELSE                            X*1
     C                     MOVE *BLANKS   SSBPR
     C                     ENDIF                           E*2
     C                     ENDIF                           E*1
      *
     C           MDDSRT    IFNE *ZEROS                     B*1
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE MDDSRT    ZFIELD
     C                     Z-ADD4         ZADEC
     C                     EXSR ZEDIT
     C                     MOVEL*BLANKS   SDSRT
     C                     MOVE ZFIELD    WFLD8   8
     C                     MOVELWFLD8     SDSRT
     C                     ELSE                            X*1
     C                     MOVE *BLANKS   SDSRT
     C                     ENDIF                           E*1
      *
     C           MDNSHA    IFNE *ZEROS                     B*1
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE MDNSHA    ZFLD15
     C                     Z-ADD7         ZDECS
     C                     MOVE 'L'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVEL*BLANKS   SNSHA
     C                     MOVE ZOUT21    WFLD17 17
     C                     MOVELWFLD17    SNSHA
     C                     ELSE                            X*1
     C                     MOVE *BLANKS   SNSHA
     C                     ENDIF                           E*1
      *
     C           MDOSHA    IFNE *ZEROS                     B*1
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE MDOSHA    ZFLD15
     C                     Z-ADD7         ZDECS
     C                     MOVE 'L'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVEL*BLANKS   SOSHA
     C                     MOVE ZOUT21    WFLD17
     C                     MOVELWFLD17    SOSHA
     C                     ELSE                            X*1
     C                     MOVE *BLANKS   SOSHA
     C                     ENDIF                           E*1
      *
     C           MDDMLT    IFNE *ZEROS                     B*1
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE MDDMLT    ZFLD15
     C                     Z-ADDNMDP      ZDECS
     C                     EXSR ZSEDIT
     C                     MOVEL*BLANKS   SDMLT
     C                     MOVE ZOUT21    WFLD17
     C                     MOVELWFLD17    SDMLT
     C                     ELSE                            X*1
     C                     MOVE *BLANKS   SDMLT
     C                     ENDIF                           E*1
      *
     C           MDDIVR    IFNE *ZEROS                     B*1
     C                     Z-ADDMDDIVR    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     SDIVR
     C                     ELSE                            X*1
     C                     MOVE *BLANKS   SDIVR
     C                     ENDIF                           E*1
      *
     C                     MOVE MDRNDN    SRNDN
     C                     MOVE MDRNDS    SRNDS
      *
     C           MDCOMP    IFNE *ZEROS                     B*1
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE MDCOMP    ZFLD15
     C                     Z-ADD8         ZDECS
     C                     EXSR ZSEDIT
     C                     MOVEL*BLANKS   SCOMP
     C                     MOVE ZOUT21    WFLD17
     C                     MOVELWFLD17    SCOMP
     C                     ELSE                            X*1
     C                     MOVE *BLANKS   SCOMP
     C                     ENDIF                           E*1
      *
     C                     MOVE MDOPT1    SOPT1
     C                     MOVE MDNAR1    SNAR1
     C                     MOVE MDOPT2    SOPT2
     C                     MOVE MDNAR2    SNAR2
     C                     MOVE MDOPT3    SOPT3
     C                     MOVE MDNAR3    SNAR3
     C                     MOVE MDOPT4    SOPT4
     C                     MOVE MDNAR4    SNAR4
     C                     MOVE MDOPT5    SOPT5
     C                     MOVE MDNAR5    SNAR5
      *
     C                     MOVE MDDFNR    SDFNR
      *
     C                     MOVE MDOPSH    SOPSH
      *
     C                     MOVE MDTXT1    STXT1
     C                     MOVE MDTXT2    STXT2
     C                     MOVE MDTXT3    STXT3
     C                     MOVE MDTXT4    STXT4
      *
     C           MDSUBF    IFNE *ZEROS                     B*1
     C                     Z-ADDMDSUBF    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     SSUBF
     C                     ELSE                            X*1
     C                     MOVE *BLANKS   SSUBF
     C                     ENDIF                           E*1
      *
     C           MDSUBT    IFNE *ZEROS                     B*1
     C                     Z-ADDMDSUBT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     SSUBT
     C                     ELSE                            X*1
     C                     MOVE *BLANKS   SSUBT
     C                     ENDIF                           E*1
      *
     C                     MOVE MDSBRT    SSBRT
      *
      **  2nd screen fields
      *
     C           MDSBRS    IFEQ *BLANKS                    B*1
     C                     MOVE SSESN     SSBRS
     C                     ELSE                            X*1
     C                     MOVE MDSBRS    SSBRS
     C                     ENDIF                           E*1
      *
      **  Retrieve Nominal Currency of the Event Security
     C           SSBRS     CHAINSECTYZ1              89
      *
      **  If record not found
     C           *IN89     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     Z-ADD5         DBASE
     C                     MOVE 'SECTYD'  DBFILE
     C                     MOVELMDSBRS    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           MDREPR    IFNE *ZEROS
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE MDREPR    ZFLD15
     C                     Z-ADD8         ZDECS
     C                     MOVE 'L'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVEL*BLANKS   SREPR
     C                     MOVE ZOUT21    WFLD17 17
     C                     MOVELWFLD17    SREPR
     C                     ELSE
     C                     MOVE SSBPR     SREPR
     C                     ENDIF
      *
     C           MDREXD    IFNE *ZEROS
     C                     Z-ADDMDREXD    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     SREXD
     C                     ELSE
     C                     MOVE *BLANKS   SREXD
     C                     ENDIF
      *
     C           MDREXA    IFNE *ZEROS
     C                     Z-ADDMDREXA    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     SREXA
     C                     ELSE
     C                     MOVE *BLANKS   SREXA
     C                     ENDIF
      *
     C                     MOVE MDREAT    SREAT
      *
     C           MDRTRF    IFNE *ZEROS
     C                     Z-ADDMDRTRF    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     SRTRF
     C                     ELSE
     C                     MOVE *BLANKS   SRTRF
     C                     ENDIF
      *
     C           MDRTRT    IFNE *ZEROS
     C                     Z-ADDMDRTRT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     SRTRT
     C                     ELSE
     C                     MOVE *BLANKS   SRTRT
     C                     ENDIF
      *
     C           MDRNNS    IFNE *ZEROS
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE MDRNNS    ZFLD15
     C                     Z-ADD7         ZDECS
     C                     MOVE 'L'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVEL*BLANKS   SRNNS
     C                     MOVE ZOUT21    WFLD17 17
     C                     MOVELWFLD17    SRNNS
     C                     ELSE
     C                     MOVE *BLANKS   SRNNS
     C                     ENDIF
      *
     C           MDRNRI    IFNE *ZEROS                     B*1
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE MDRNRI    ZFLD15
     C                     Z-ADD7         ZDECS
     C                     MOVE 'L'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVEL*BLANKS   SRNRI
     C                     MOVE ZOUT21    WFLD17
     C                     MOVELWFLD17    SRNRI
     C                     ELSE                            X*1
     C                     MOVE *BLANKS   SRNRI
     C                     ENDIF                           E*1
      *
     C           MDRMLT    IFNE *ZEROS                     B*1
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE MDRMLT    ZFLD15
     C******               Z-ADDNMDP      ZDECS                           136935
     C                     Z-ADDEVNMDP    ZDECS                           136935
     C                     EXSR ZSEDIT
     C                     MOVEL*BLANKS   SRMLT
     C                     MOVE ZOUT21    WFLD17
     C                     MOVELWFLD17    SRMLT
     C                     ELSE                            X*1
     C                     MOVE *BLANKS   SRMLT
     C                     ENDIF                           E*1
      *
     C           MDRDIV    IFNE *ZEROS                     B*1
     C                     Z-ADDMDRDIV    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     SRDIV
     C                     ELSE                            X*1
     C                     MOVE *BLANKS   SRDIV
     C                     ENDIF                           E*1
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            P006   - Validate 1st Details entered              *
      *                                                               *
      * CALLS      ZASNMS - Send message to program's message queue   *
      *            P008   - Main Processing 2                         *
      *                                                               *
      * CALLED BY  P002   - Main Processing                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           P006      BEGSR
      *
      **  Validate New Security Shortname
     C           SNSEC     IFNE *BLANKS                    B*1
      *
      * Check Security Shortname
      *
     C/COPY WNCPYSRC,SE7503C002
      *
     C           SNSEC     CHAINSECTYZ1              89
      *
     C           *IN89     IFEQ '1'                        B*2
     C                     MOVEL'CO00135' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN20
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*2
      *
     C                     ELSE                            X*1
     C                     MOVE @SESN     SNSEC
     C                     ENDIF                           E*1
      *
      ** Obtain market price
     C                     MOVE SNSEC     WWPSSN
     C                     MOVE MATDVD    WWTDVD
     C                     EXSR P011
      *                                                                   094743
      ** If price found in P011, use this price                           094743
     C           WWCHCK    IFNE 'Y'                        B*1            094743
     C                     Z-ADDMKPR      WWPPRC                          094743
     C                     END                             E*1            094743
      *
      ** Obtain Price Tolerance limit from IMVTPD
     C                     MOVELNMCY      CCYI
     C                     MOVELSITP      INVT
     C           KEY2      CHAININVTPDF              89
     C           *IN89     IFEQ '1'                        B*1
     C           RECI      OREQ '*'
     C                     Z-ADD10        PRTO
     C                     ENDIF                           E*1
      *
      ** If tolerance is not entered, use 10%
     C           PRTO      IFEQ 0                          B*1
     C                     Z-ADD10        PRTO
     C                     ENDIF                           E*1
      *
      **  Validate Stock Dividend Type
     C           SSDIT     IFNE *BLANKS
     C           SSDIT     ANDNE'C'
     C           SSDIT     ANDNE'O'
     C           SSDIT     ANDNE'S'
     C           SSDIT     ANDNE'R'
     C                     MOVEL'CO00148' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN21
     C                     MOVE 'Y'       ERROR
     C                     ENDIF
      *
      **  Validate Dividend per unit
     C           SSDIT     IFEQ 'C'                        B*1
     C           SSDIT     OREQ 'O'
     C           SSDIT     OREQ 'R'                                       135589
      *
     C           SDIVU     IFEQ *BLANKS                    B*2
     C*****                MOVEL'CO00149' ZAMSID                          135689
     C                     MOVEL'CO00582' ZAMSID                          135689
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN22
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
     C           SSDIT     IFEQ 'S'
     C           SDIVU     ANDEQ*BLANKS
     C           SNSHA     ANDEQ*BLANKS
     C           SSDIT     OREQ 'S'
     C           SDIVU     ANDNE*BLANKS
     C           SNSHA     ANDNE*BLANKS
     C*****                MOVEL'CO00149' ZAMSID                          135689
     C                     MOVEL'CO00581' ZAMSID                          135689
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN22
     C                     MOVE '1'       *IN25
     C                     MOVE 'Y'       ERROR
     C                     ENDIF
      *
     C*****      SSDIT     IFEQ 'R'                                       135689
     C*****      SDIVU     ANDEQ*BLANKS                                   135689
     C*****                MOVEL'CO00149' ZAMSID                          135689
     C*****                EXSR ZASNMS                                    135689
     C*****                MOVE '1'       *IN22                           135689
     C*****                MOVE 'Y'       ERROR                           135689
     C*****                ENDIF                                          135689
     C/COPY WNCPYSRC,SE7503C019
      *
     C           SDIVU     IFNE *BLANKS                    B*1
      *                                                                   135689
     C**********           CALL 'AOCURRR0'                         135689 190204
     C**********           PARM '*MSG   ' WLRTCD                   135689 190204
     C**********           PARM '*KEY   ' WLOPTN                   135689 190204
     C**********           PARM NMCY      WLAJCD  3                135689 190204
     C********** SDCURR    PARM SDCURR    DSSDY                    135689 190204
      *                                                                   135689
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE SDIVU     ZFIELD
     C*****      9         SUB  NMDP      ZADIG                           135689
     C*****                Z-ADDNMDP      ZADEC                           135689
     C********** 9         SUB  A6NBDP    ZADIG                    135689 190204
     C**********           Z-ADDA6NBDP    ZADEC                    135689 190204
     C           13        SUB  8         ZADIG                           190204
     C                     Z-ADD8         ZADEC                           190204
     C                     EXSR ZALIGN
      *
     C           *IN99     IFEQ '1'                        B*2
     C**********           MOVEL'CO00150' ZAMSID                          190204
      *                                                                   190204
      **  Dividend per unit must be a 13 numeric field                    190204
      **  with 8 decimal places                                           190204
      *                                                                   190204
     C                     MOVEL'CO00610' ZAMSID                          190204
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN22
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
      **  Validate Subscription Price
      *                                                                   135689
     C           SSDIT     IFEQ 'O'                        B*1            135689
     C           SNSHA     ANDEQ*BLANKS                                   135689
     C           SSBPR     ANDEQ*BLANKS                                   135689
     C           SSDIT     OREQ 'O'                                       135689
     C           SNSHA     ANDNE*BLANKS                                   135689
     C           SSBPR     ANDNE*BLANKS                                   135689
     C                     MOVEL'CO00578' ZAMSID                          135689
     C                     EXSR ZASNMS                                    135689
     C                     MOVE '1'       *IN25                           135689
     C                     MOVE '1'       *IN26                           135689
     C                     MOVE '1'       *IN23                           135689
     C                     MOVE 'Y'       ERROR                           135689
     C                     ENDIF                           E*1            135689
      *                                                                   135689
     C           SSDIT     IFEQ 'R'                        B*1            135689
     C           SSBPR     ANDEQ*BLANKS                                   135689
     C                     MOVEL'CO00579' ZAMSID                          135689
     C                     EXSR ZASNMS                                    135689
     C                     MOVE '1'       *IN23                           135689
     C                     MOVE 'Y'       ERROR                           135689
     C                     ENDIF                           E*1            135689
      *                                                                   135689
     C           SSDIT     IFEQ 'S'                        B*1            135689
     C           SDIVU     ANDNE*BLANKS                                   135689
     C           SSBPR     ANDEQ*BLANKS                                   135689
     C           SSDIT     OREQ 'S'                                       135689
     C           SDIVU     ANDEQ*BLANKS                                   135689
     C           SSBPR     ANDNE*BLANKS                                   135689
     C                     MOVEL'CO00152' ZAMSID                          135689
     C                     EXSR ZASNMS                                    135689
     C                     MOVE '1'       *IN23                           135689
     C                     MOVE 'Y'       ERROR                           135689
     C                     ENDIF                           E*1            135689
      *                                                                   135689
     C           SSDIT     IFEQ 'C'                        B*1            135689
     C           SSBPR     ANDNE*BLANKS                                   135689
     C                     MOVEL'CO00580' ZAMSID                          135689
     C                     EXSR ZASNMS                                    135689
     C                     MOVE '1'       *IN23                           135689
     C                     MOVE 'Y'       ERROR                           135689
     C                     ENDIF                           E*1            135689
      *                                                                   135689
     C*****      SDIVU     IFNE *BLANKS                    B*1            135689
     C*****      SSBPR     ANDEQ*BLANKS                                   135689
     C*****      SDIVU     OREQ *BLANKS                                   135689
     C*****      SSBPR     ANDNE*BLANKS                                   135689
     C*****                MOVEL'CO00152' ZAMSID                          135689
     C*****                EXSR ZASNMS                                    135689
     C*****                MOVE '1'       *IN23                           135689
     C*****                MOVE 'Y'       ERROR                           135689
      *****                                                               135689
     C*****                ELSE                            X*1            135689
      *
     C           SSBPR     IFNE *BLANKS                    B*2
     C           SSBPR     ANDNE'MARKET'
     C           *IN23     ANDEQ'0'                                       135689
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE SSBPR     ZFIELD
     C                     Z-ADD7         ZADIG
     C                     Z-ADD8         ZADEC
     C                     EXSR ZALIGN
      *
     C           *IN99     IFEQ '1'                        B*3
     C                     MOVEL'CO00151' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN23
     C                     MOVE 'Y'       ERROR
     C                     ELSE                            X*3
     C                     MOVE ZFIELD    WWSBPR 158
     C           PRTO      DIV  100       APWRK1 158
     C           WWPPRC    MULT APWRK1    APWRK1
     C           WWPPRC    ADD  APWRK1    APWRK2 158
     C           WWSBPR    IFGT APWRK2                     B*4
     C           ZFIELD    ANDNEWFIELD
     C                     MOVEL'CO00271' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN23
     C                     MOVE 'Y'       ERROR
     C                     MOVE ZFIELD    WFIELD 16
     C                     ENDIF                           E*4
     C                     ENDIF                           E*3
      *
     C                     ENDIF                           E*2
      *
     C*****                ENDIF                           E*1            135689
      *
      **  Validate Subscription Discount Rate
     C           SDSRT     IFNE *BLANKS                    B*1
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE SDSRT     ZFIELD
     C                     Z-ADD3         ZADIG
     C                     Z-ADD4         ZADEC
     C                     EXSR ZALIGN
      *
     C           *IN99     IFEQ '1'                        B*2
     C           SSBPR     OREQ *BLANKS                                   135689
     C                     MOVEL'CO00153' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN24
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
      **  Validate Number of New Shares
     C*****      SSDIT     IFEQ 'C'                        B*1            135689
     C*****      SSDIT     OREQ 'O'                                       135689
      *
     C           SNSHA     IFEQ *BLANKS                    B*2
     C           SSDIT     ANDEQ'C'                                       135689
     C*****                MOVEL'CO00149' ZAMSID                          135689
     C                     MOVEL'CO00583' ZAMSID                          135689
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN25
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*2
      *
     C*****                ENDIF                           E*1            135689
      *****                                                               135689
     C*****      SSDIT     IFEQ 'S'                                       135689
     C*****      SNSHA     ANDEQ*BLANKS                                   135689
     C*****      SDIVU     ANDEQ*BLANKS                                   135689
     C*****                MOVEL'CO00149' ZAMSID                          135689
     C*****                EXSR ZASNMS                                    135689
     C*****                MOVE '1'       *IN25                           135689
     C*****                MOVE 'Y'       ERROR                           135689
     C*****                ENDIF                                          135689
      *
     C           SSDIT     IFEQ 'R'
     C           SNSHA     ANDNE*BLANKS
     C*****                MOVEL'CO00149' ZAMSID                          135689
     C                     MOVEL'CO00584' ZAMSID                          135689
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN25
     C                     MOVE 'Y'       ERROR
     C                     ENDIF
      *
     C           SNSHA     IFNE *BLANKS                    B*1
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE SNSHA     ZFIELD
     C                     Z-ADD8         ZADIG
     C                     Z-ADD7         ZADEC
     C                     EXSR ZALIGN
      *
     C           *IN99     IFEQ '1'                        B*2
     C                     MOVEL'CO00101' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN25
     C                     MOVE 'Y'       ERROR
     C                     ELSE                            X*2
     C           SOSHA     IFEQ *BLANKS                    B*3
     C           *IN25     ANDEQ'0'                                       135689
     C                     MOVEL'CO00270' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN26
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*3
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
      **  Validate Number of Old Shares
     C           SOSHA     IFNE *BLANKS                    B*1
     C           *IN25     ANDEQ'0'                                       135689
      *
     C           SNSHA     IFEQ *BLANKS                    B*2
     C                     MOVEL'CO00102' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN26
     C                     MOVE 'Y'       ERROR
     C                     ELSE                            X*2
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE SOSHA     ZFIELD
     C                     Z-ADD8         ZADIG
     C                     Z-ADD7         ZADEC
     C                     EXSR ZALIGN
      *
     C           *IN99     IFEQ '1'                        B*3
     C                     MOVEL'CO00103' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN26
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*3
      *
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
      **  Validate Denomination Multiplier
     C           SDMLT     IFEQ *BLANKS                    B*1
     C                     Z-ADD*ZEROS    ZFLD15
      *
     C                     Z-ADDNMDP      Y       20
     C                     ADD  4         Y
     C           1         MULT POWER8,Y  ZFLD15
      *
     C                     Z-ADDNMDP      ZDECS
     C                     EXSR ZSEDIT
     C                     MOVEL*BLANKS   SDMLT
     C                     MOVE ZOUT21    WFLD17
     C                     MOVELWFLD17    SDMLT
     C                     ENDIF                           E*1
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE SDMLT     ZFIELD
     C           15        SUB  NMDP      ZADIG
     C                     Z-ADDNMDP      ZADEC
     C                     EXSR ZALIGN
      *
     C           *IN99     IFEQ '1'                        B*3
     C                     MOVEL'CO00107' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN27
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*1
      *
      **  Validate Dividend Right
     C           SDIVR     IFNE *BLANKS                    B*1
     C                     MOVE SDIVR     ZDATE
     C                     EXSR ZDATE1
      *
     C           *IN99     IFEQ '1'                        B*2
     C                     MOVEL'CO00070' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN28
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
      **  Validate Rounding Type (D/R)
     C           SRNDN     IFEQ *BLANKS                    B*1
     C                     MOVE 'D'       SRNDN
     C                     ELSE                            X*1
      *
     C           SRNDN     IFNE 'D'                        B*2
     C           SRNDN     ANDNE'R'
     C           SRNDN     ANDNE'U'
     C                     MOVEL'CO00074' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN29
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
      **  Validate Rounding Settlement In Cash (Y/N)
     C           SRNDS     IFEQ *BLANKS                    B*1
     C                     MOVE 'N'       SRNDS
     C                     ELSE                            X*1
      *
     C           SRNDS     IFNE 'Y'                        B*2
     C           SRNDS     ANDNE'N'
     C                     MOVEL'CO00075' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN30
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
      **  Validate Compensation Price
     C           SRNDS     IFEQ 'N'                        B*1
     C           SCOMP     ANDNE*BLANKS
     C           SRNDS     OREQ 'Y'
     C           SCOMP     ANDEQ*BLANKS
     C           SSDIT     ANDNE'R'                                       137884
     C           SSDIT     ANDNE'S'                                       161592
     C           SSDIT     ANDNE'O'                                       161592
     C                     MOVEL'CO00067' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN31
     C                     MOVE 'Y'       ERROR
     C                     ELSE                            X*2
      *                                                                   137884
     C           SRNDS     IFEQ 'Y'                        B*3            137884
     C           SCOMP     ANDEQ*BLANKS                                   137884
     C           SSDIT     ANDEQ'R'                                       137884
      *                                                                   161592
      *                                                                   161592
     C           SRNDS     OREQ 'Y'                        B*3            161592
     C           SCOMP     ANDEQ*BLANKS                                   161592
     C           SSDIT     ANDEQ'S'                                       161592
      *                                                                   161592
     C           SRNDS     OREQ 'Y'                        B*3            161592
     C           SCOMP     ANDEQ*BLANKS                                   161592
     C           SSDIT     ANDEQ'O'                                       161592
      *                                                                   161592
      *                                                                   161592
     C           *IN23     IFEQ '0'                        B*4            137884
     C           SSBPR     IFEQ 'MARKET'                   B*5            137884
     C                     EXSR P012                                      137884
     C                     ELSE                            X*5            137884
     C                     MOVE WWSBPR    MPRICE                          137884
     C                     ENDIF                           E*5            137884
     C           MPRICE    IFNE *ZEROS                     B*6            137884
     C           SDSRT     IFNE *BLANKS                    B*7            137884
     C                     MOVE *BLANKS   ZFIELD                          137884
     C                     MOVE SDSRT     ZFIELD                          137884
     C                     Z-ADD3         ZADIG                           137884
     C                     Z-ADD4         ZADEC                           137884
     C                     EXSR ZALIGN                                    137884
      *                                                                   137884
     C           *IN99     IFNE '1'                        B*8            137884
     C                     MOVE ZFIELD    WWDISC  74                      137884
      *                                                                   137884
      ***  Calculate Subs. Price = Current Market Price minus Discount.   137884
      *                                                                   137884
     C           MPRICE    MULT WWDISC    WWCOMP 158                      137884
     C           WWCOMP    DIV  100       WWCOMP                          137884
     C           MPRICE    SUB  WWCOMP    MPRICE                          137884
     C                     ENDIF                           E*8            137884
      *                                                                   137884
     C                     ENDIF                           E*7            137884
      *                                                                   137884
     C                     MOVE *BLANKS   ZFLD15                          137884
     C                     MOVE MPRICE    ZFLD15                          137884
     C                     Z-ADD8         ZDECS                           137884
     C                     EXSR ZSEDIT                                    137884
     C                     MOVEL*BLANKS   SCOMP                           137884
     C                     MOVE ZOUT21    WFLD17                          137884
     C                     MOVELWFLD17    SCOMP                           137884
     C                     ELSE                            X*6            137884
     C                     MOVE *BLANKS   SCOMP                           137884
     C                     ENDIF                           E*6            137884
     C                     ENDIF                           E*4            137884
     C           SCOMP     IFEQ *BLANKS                    B*4            137884
     C                     MOVEL'CO00067' ZAMSID                          137884
     C                     EXSR ZASNMS                                    137884
     C                     MOVE '1'       *IN31                           137884
     C                     MOVE 'Y'       ERROR                           137884
     C                     ENDIF                           E*4            137884
     C                     ENDIF                           E*3            137884
      *                                                                   137884
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE SCOMP     ZFIELD
     C                     Z-ADD7         ZADIG
     C                     Z-ADD8         ZADEC
     C                     EXSR ZALIGN
      *
     C           *IN99     IFEQ '1'                        B*2
     C                     MOVEL'CO00108' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN31
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
      **  Validate Customer Option(S)
     C           SOPT1     IFEQ *BLANKS                    B*1
     C                     MOVE 'N'       SOPT1
     C                     ELSE                            X*1
      *
     C           SOPT1     IFNE 'Y'                        B*2
     C           SOPT1     ANDNE'N'
     C                     MOVEL'CO00076' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN32
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
     C           SOPT1     IFEQ 'Y'                        B*1
     C           SSDIT     ANDNE'R'
     C           SSDIT     ANDNE'O'
     C                     MOVEL'CO00157' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN32
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*1
      *
     C           SOPT2     IFEQ *BLANKS                    B*1
     C                     MOVE 'N'       SOPT2
     C                     ELSE                            X*1
      *
     C           SOPT2     IFNE 'Y'                        B*2
     C           SOPT2     ANDNE'N'
     C                     MOVEL'CO00076' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN34
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
     C           SOPT3     IFEQ *BLANKS                    B*1
     C                     MOVE 'N'       SOPT3
     C                     ELSE                            X*1
      *
     C           SOPT3     IFNE 'Y'                        B*2
     C           SOPT3     ANDNE'N'
     C                     MOVEL'CO00076' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN36
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
     C           SOPT4     IFEQ *BLANKS                    B*1
     C                     MOVE 'N'       SOPT4
     C                     ELSE                            X*1
      *
     C           SOPT4     IFNE 'Y'                        B*2
     C           SOPT4     ANDNE'N'
     C                     MOVEL'CO00076' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN38
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
     C           SOPT4     IFEQ 'Y'                        B*1
      *
     C           SOPT2     IFEQ 'Y'                        B*2
     C           SOPT3     OREQ 'Y'
     C                     MOVEL'CO00158' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN38
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
     C           SOPT5     IFEQ *BLANKS                    B*1
     C                     MOVE 'N'       SOPT5
     C                     ELSE                            X*1
      *
     C           SOPT5     IFNE 'Y'                        B*2
     C           SOPT5     ANDNE'N'
     C                     MOVEL'CO00076' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN40
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
     C           SOPT5     IFEQ 'Y'                        B*1
      *
     C           SSDIT     IFNE 'R'                        B*2
     C           SSDIT     ANDNE'O'
     C                     MOVEL'CO00159' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN40
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
      **  Validate Customer Option Narrative(s)
     C           SOPT1     IFEQ 'Y'                        B*1
     C           SNAR1     ANDEQ*BLANKS
     C           SOPT1     ORNE 'Y'
     C           SNAR1     ANDNE*BLANKS
     C                     MOVEL'CO00077' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN33
     C                     MOVE 'Y'       ERROR
     C                     ELSE                            X*1
      *
     C           SNAR1     IFNE *BLANKS                    B*2
     C           SNAR1     CHAINSECONAL0             89
      *
     C           *IN89     IFEQ '1'                        B*3
     C           MNRECI    ORNE 'D'
     C                     MOVEL'CO00078' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN33
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*3
      *
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
     C           SOPT2     IFEQ 'Y'                        B*1
     C           SNAR2     ANDEQ*BLANKS
     C           SOPT2     ORNE 'Y'
     C           SNAR2     ANDNE*BLANKS
     C                     MOVEL'CO00077' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN35
     C                     MOVE 'Y'       ERROR
     C                     ELSE                            X*1
      *
     C           SNAR2     IFNE *BLANKS                    B*2
     C           SNAR2     CHAINSECONAL0             89
      *
     C           *IN89     IFEQ '1'                        B*3
     C           MNRECI    ORNE 'D'
     C                     MOVEL'CO00078' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN35
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*3
      *
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
     C           SOPT3     IFEQ 'Y'                        B*1
     C           SNAR3     ANDEQ*BLANKS
     C           SOPT3     ORNE 'Y'
     C           SNAR3     ANDNE*BLANKS
     C                     MOVEL'CO00077' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN37
     C                     MOVE 'Y'       ERROR
     C                     ELSE                            X*1
      *
     C           SNAR3     IFNE *BLANKS                    B*2
     C           SNAR3     CHAINSECONAL0             89
      *
     C           *IN89     IFEQ '1'                        B*3
     C           MNRECI    ORNE 'D'
     C                     MOVEL'CO00078' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN37
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*3
      *
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
     C           SOPT4     IFEQ 'Y'                        B*1
     C           SNAR4     ANDEQ*BLANKS
     C           SOPT4     ORNE 'Y'
     C           SNAR4     ANDNE*BLANKS
     C                     MOVEL'CO00077' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN39
     C                     MOVE 'Y'       ERROR
     C                     ELSE                            X*1
      *
     C           SNAR4     IFNE *BLANKS                    B*2
     C           SNAR4     CHAINSECONAL0             89
      *
     C           *IN89     IFEQ '1'                        B*3
     C           MNRECI    ORNE 'D'
     C                     MOVEL'CO00078' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN39
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*3
      *
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
     C           SOPT5     IFEQ 'Y'                        B*1
     C           SNAR5     ANDEQ*BLANKS
     C           SOPT5     ORNE 'Y'
     C           SNAR5     ANDNE*BLANKS
     C                     MOVEL'CO00077' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN41
     C                     MOVE 'Y'       ERROR
     C                     ELSE                            X*1
      *
     C           SNAR5     IFNE *BLANKS                    B*2
     C           SNAR5     CHAINSECONAL0             89
      *
     C           *IN89     IFEQ '1'                        B*3
     C           MNRECI    ORNE 'D'
     C                     MOVEL'CO00078' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN41
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*3
      *
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
      **  Validate Default for no reply
     C                     MOVE 'N'       ERROR1  1
     C                     MOVE 'N'       ERROR2  1
      *
     C           SDFNR     IFNE '1'                        B*1
     C           SDFNR     ANDNE'2'
     C           SDFNR     ANDNE'3'
     C           SDFNR     ANDNE'4'
     C           SDFNR     ANDNE' '
     C                     MOVE 'Y'       ERROR1
     C                     ENDIF
      *
     C           SDFNR     IFEQ '1'
     C           SOPT1     ANDNE'Y'
     C           SDFNR     OREQ '2'
     C           SOPT2     ANDNE'Y'
     C           SDFNR     OREQ '3'
     C           SOPT3     ANDNE'Y'
     C           SDFNR     OREQ '4'
     C           SOPT4     ANDNE'Y'
     C                     MOVE 'Y'       ERROR2
     C                     ENDIF
      *
     C           ERROR1    IFEQ 'Y'
     C           ERROR2    OREQ 'Y'
     C                     MOVEL'CO00154' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN42
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*1
      *
      **  Validate Option Shortname - No validation
      *
      **  Validate Narratives - No validation
      *
      **  Validate Subscription From
     C           SSUBF     IFNE *BLANKS                    B*1
      *
     C           SSUBT     IFEQ *BLANKS                    B*2
     C                     MOVE '1'       *IN44
     C                     MOVEL'CO00145' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE 'Y'       ERROR
     C                     ELSE                            X*2
     C                     MOVE SSUBF     ZDATE
     C                     EXSR ZDATE1
      *
     C           *IN99     IFEQ '1'                        B*3
     C                     MOVEL'CO00071' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN43
     C                     MOVE 'Y'       ERROR
     C                     ELSE                            X*3
     C                     Z-ADDZDAYNO    WRK1    50
     C                     ENDIF                           E*3
      *
     C                     ENDIF                           E*2
      *
     C                     MOVE @COEX     WRK2    50
      *
     C           *IN43     IFEQ '0'                        B*1
     C           WRK1      ANDLTWRK2
     C                     MOVEL'CO00155' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN43
     C                     MOVE 'Y'       ERROR
     C                     ENDIF
      *
     C                     ENDIF
      *
      **  Validate Subscription To
     C           SSUBT     IFNE *BLANKS                    B*1
      *
     C           SSUBF     IFEQ *BLANKS                    B*2
     C                     MOVE '1'       *IN43
     C                     MOVEL'CO00147' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE 'Y'       ERROR
     C                     ELSE                            X*2
     C                     MOVE SSUBT     ZDATE
     C                     EXSR ZDATE1
      *
     C           *IN99     IFEQ '1'                        B*3
     C                     MOVEL'CO00072' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN44
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*3
      *
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
     C           *IN43     IFEQ '0'                        B*1
     C           *IN44     ANDEQ'0'
     C           SSUBF     ANDNE*BLANKS
     C           SSUBT     ANDNE*BLANKS
     C           ZDAYNO    ANDLEWRK1                       SSUBT  ANDLTSSUBF
     C                     MOVEL'CO00073' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN44
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*1
      *
      **  Validate Subscription rights tradable
     C           SSBRT     IFEQ *BLANKS                    B*1
     C                     MOVE 'N'       SSBRT
     C                     ELSE                            X*1
      *
     C           SSBRT     IFNE 'Y'                        B*2
     C           SSBRT     ANDNE'N'
     C                     MOVEL'CO00156' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN45
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
      **  If Subscription Rights Tradable is 'Y'
     C           SSBRT     IFEQ 'Y'
     C           ERROR     ANDNE'Y'
     C           MDSBRS    IFEQ *BLANKS                                   136935
     C                     MOVE SNSEC     SSBRS                           136935
     C                     ELSE                                           136935
     C/COPY WNCPYSRC,SE7503C020
     C                     MOVE MDSBRS    SSBRS                           136935
     C/COPY WNCPYSRC,SE7503C021
     C                     ENDIF                                          136935
     C                     EXSR P008
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            P007   - Save screen/s fields                      *
      *                                                               *
      * CALLS      *PSSR  - Program error handling routine            *
      *                                                               *
      * CALLED BY  P002   - Main Processing                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           P007      BEGSR
      *                                                                   137884
     C           KEY1      CHAINSECODVL0             75                   137884
      *
     C           *IN75     IFEQ '0'                        B*1
     C                     MOVE 'A'       MDCHTP
     C                     ELSE                            X*1
     C                     MOVE 'I'       MDCHTP
     C                     ENDIF                           E*1
      *
     C                     MOVE 'D'       MDRECI
     C                     Z-ADDBJRDNB    MDLCD
     C                     MOVE ##USR     MDLUID
     C                     MOVE @CORF     MDCORF
     C                     MOVE SOPNB     MDOPTN
      *
     C                     MOVE SNSEC     MDNSEC
     C                     MOVE SSDIT     MDSDIT
      *
     C           SNSEC     CHAINSECTYZ1              89
      *
     C**********           CALL 'AOCURRR0'                         135689 190204
     C**********           PARM '*MSG   ' WLRTCD                   135689 190204
     C**********           PARM '*KEY   ' WLOPTN                   135689 190204
     C**********           PARM NMCY      WLAJCD  3                135689 190204
     C********** SDCURR    PARM SDCURR    DSSDY                    135689 190204
      *                                                                   135689
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE SDIVU     ZFIELD
     C*****      9         SUB  NMDP      ZADIG                           135689
     C*****                Z-ADDNMDP      ZADEC                           135689
     C********** 9         SUB  A6NBDP    ZADIG                    135689 190204
     C**********           Z-ADDA6NBDP    ZADEC                    135689 190204
     C           13        SUB  8         ZADIG                           190204
     C                     Z-ADD8         ZADEC                           190204
     C                     EXSR ZALIGN
     C**********           MOVE ZFIELD    MDDIVU                          190204
     C                     MOVE ZFIELD    MDDIV1                          190204
      *
     C           SSBPR     IFNE 'MARKET'
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVELSSBPR     ZFIELD
     C                     Z-ADD7         ZADIG
     C                     Z-ADD8         ZADEC
     C                     EXSR ZALIGN
     C                     MOVE ZFIELD    MDSBPR
     C                     MOVE 'N'       MDMRKI
     C                     ELSE
     C                     Z-ADD*ZEROS    MDSBPR
     C                     MOVE 'Y'       MDMRKI
     C                     ENDIF
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVELSDSRT     ZFIELD
     C                     Z-ADD3         ZADIG
     C                     Z-ADD4         ZADEC
     C                     EXSR ZALIGN
     C                     MOVE ZFIELD    MDDSRT
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVELSNSHA     ZFIELD
     C                     Z-ADD8         ZADIG
     C                     Z-ADD7         ZADEC
     C                     EXSR ZALIGN
     C                     MOVE ZFIELD    MDNSHA
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVELSOSHA     ZFIELD
     C                     Z-ADD8         ZADIG
     C                     Z-ADD7         ZADEC
     C                     EXSR ZALIGN
     C                     MOVE ZFIELD    MDOSHA
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVELSDMLT     ZFIELD
     C           15        SUB  NMDP      ZADIG
     C                     Z-ADDNMDP      ZADEC
     C                     EXSR ZALIGN
     C                     MOVE ZFIELD    MDDMLT
      *
     C                     MOVE SDIVR     ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    MDDIVR
      *
     C                     MOVE SRNDN     MDRNDN
     C                     MOVE SRNDS     MDRNDS
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVELSCOMP     ZFIELD
     C                     Z-ADD7         ZADIG
     C                     Z-ADD8         ZADEC
     C                     EXSR ZALIGN
     C                     MOVE ZFIELD    MDCOMP
      *
     C                     MOVE SOPT1     MDOPT1
     C                     MOVE SNAR1     MDNAR1
     C                     MOVE SOPT2     MDOPT2
     C                     MOVE SNAR2     MDNAR2
     C                     MOVE SOPT3     MDOPT3
     C                     MOVE SNAR3     MDNAR3
     C                     MOVE SOPT4     MDOPT4
     C                     MOVE SNAR4     MDNAR4
     C                     MOVE SOPT5     MDOPT5
     C                     MOVE SNAR5     MDNAR5
     C                     MOVE SDFNR     MDDFNR
      *
     C           SOPSH     IFEQ *BLANKS                    B*1
     C                     MOVELNARR,2    WWOPSH 26
     C                     MOVE SOPNB     WWOPSH
     C                     MOVELWWOPSH    MDOPSH
     C                     ELSE                            X*1
     C                     MOVE SOPSH     MDOPSH
     C                     ENDIF                           E*1
      *
     C                     MOVE STXT1     MDTXT1
     C                     MOVE STXT2     MDTXT2
     C                     MOVE STXT3     MDTXT3
     C                     MOVE STXT4     MDTXT4
      *
     C                     MOVE SSUBF     ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    MDSUBF
      *
     C                     MOVE SSUBT     ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    MDSUBT
      *
     C                     MOVE SSBRT     MDSBRT
      *
     C           SSBRT     IFEQ 'Y'
     C                     MOVE SSBRS     MDSBRS
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVELSREPR     ZFIELD
     C                     Z-ADD7         ZADIG
     C                     Z-ADD8         ZADEC
     C                     EXSR ZALIGN
     C                     MOVE ZFIELD    MDREPR
      *
     C                     MOVE SREXD     ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    MDREXD
      *
     C                     MOVE SREXA     ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    MDREXA
      *
     C                     MOVE SREAT     MDREAT
      *
     C                     MOVE SRTRF     ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    MDRTRF
      *
     C                     MOVE SRTRT     ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    MDRTRT
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVELSRNNS     ZFIELD
     C                     Z-ADD8         ZADIG
     C                     Z-ADD7         ZADEC
     C                     EXSR ZALIGN
     C                     MOVE ZFIELD    MDRNNS
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVELSRNRI     ZFIELD
     C                     Z-ADD8         ZADIG
     C                     Z-ADD7         ZADEC
     C                     EXSR ZALIGN
     C                     MOVE ZFIELD    MDRNRI
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVELSRMLT     ZFIELD
     C******     15        SUB  NMDP      ZADIG                           136935
     C******               Z-ADDNMDP      ZADEC                           136935
     C           15        SUB  EVNMDP    ZADIG                           136935
     C                     Z-ADDEVNMDP    ZADEC                           136935
     C                     EXSR ZALIGN
     C                     MOVE ZFIELD    MDRMLT
      *
     C                     MOVE SRDIV     ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    MDRDIV
      *
     C                     ELSE
     C                     MOVE *BLANKS   MDSBRS
     C                     Z-ADD0         MDREPR
     C                     Z-ADD0         MDREXD
     C                     Z-ADD0         MDREXA
     C                     MOVE *BLANKS   MDREAT
     C                     MOVE *BLANKS   MDRTRF
     C                     MOVE *BLANKS   MDRTRT
     C                     Z-ADD0         MDRNNS
     C                     Z-ADD0         MDRNRI
     C                     Z-ADD0         MDRMLT
     C                     Z-ADD0         MDRDIV
     C                     ENDIF
     C/COPY WNCPYSRC,SEH00009
      *
     C           *IN75     IFEQ '0'                        B*1
      *
     C                     UPDATSECODVD0
     C                     MOVE 'U'       @RTCD
     C                     ELSE                            X*1
      *
     C                     WRITESECODVD0
     C                     MOVE 'W'       @RTCD
      *
      **   ACCESS ER REFERENCE FILE
      *
     C           @CORF     CHAINSECORFL0             89
      *
     C           *IN89     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     Z-ADD6         DBASE
     C                     MOVE 'SECORFL0'DBFILE
     C                     MOVEL@CORF     DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVE 'D'       MARECI
     C                     Z-ADDBJRDNB    MALCD
     C                     MOVE 'A'       MACHTP
     C                     MOVE ##USR     MALUID
     C                     ADD  1         MANBOP
     C                     UPDATSECORFD0
     C                     ENDIF                           E*1
      *
      **  Execute Commitment Control to fix all inserted records
     C                     COMIT
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            P008   - Subscription Rights Tradable Processing.  *
      *                                                               *
      * CALLS      P009   - Validate 2nd Details entered              *
      *            P010   - Initialise 2nd screen fields              *
      *            SE0040 - Security detail maintenance               *
      *                                                               *
      * CALLED BY  P006   - Validate 1st Details entered              *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           P008      BEGSR
      *
     C                     MOVE 'N'       WWEND2  1
      *
      **  Clear Messages from Program Message Queue.
     C/COPY WNCPYSRC,SE7503C022
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
     C/COPY WNCPYSRC,SE7503C023
      *
      **  Do while F3, F12 not pressed
     C           *INKC     DOWEQ'0'                        B*1
     C           *INKL     ANDEQ'0'
     C           WWEND2    ANDEQ'N'
      *
      **  Enquiry Mode
     C           *IN70     IFEQ '1'                        B*2
     C                     MOVE 'Y'       WWEND2
     C                     ENDIF                           E*2
      *
     C                     TIME           ##TME            Screen time.
     C                     MOVE TITLE,2   STITLE
     C/COPY WNCPYSRC,SE7503C024
      *
     C                     WRITESE7503C1
     C                     EXFMTSE7503F2
     C/COPY WNCPYSRC,SE7503C025
      *
      **  Setof error indicators from *IN20 to *IN56
     C                     MOVEA'00000000'*IN,20
     C                     MOVEA'00000000'*IN,28
     C                     MOVEA'00000000'*IN,36
     C                     MOVEA'00000000'*IN,44
     C                     MOVEA'00000'   *IN,52
      *
      **  Clear Messages from Program Message Queue.
     C/COPY WNCPYSRC,SE7503C026
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
     C/COPY WNCPYSRC,SE7503C027
      *
      **  If <enter> pressed, validate screen fields
     C           @MODE     IFEQ 'A'                        B*2
     C           @MODE     OREQ 'I'
      *
     C           *INKC     IFEQ '0'                        B*3
     C           *INKL     ANDEQ'0'
     C           *IN15     ANDEQ'0'
     C                     MOVE 'N'       ERROR   1
     C                     EXSR P009
      *
      **  If no error, update file and return to previous screen
     C           ERROR     IFEQ 'N'                        B*4
     C                     MOVE 'Y'       WWEND2
     C                     ENDIF                           E*4
     C/COPY WNCPYSRC,SE7503C028
      *
     C                     ENDIF                           E*3
      *
     C                     ENDIF                           E*2
      *
      **  If F15 pressed, Call "Securities Maintenance" program
     C           *IN15     IFEQ '1'                        B*2
      *
      ** Call SE0040 with the action code 'I'
     C           CAP137    IFNE 'Y'                                       CAP137
     C                     CALL 'SE0040'               99
     C                     PARM *BLANKS   #RTRN   7
     C                     PARM 'I'       #ACTC   1
     C                     PARM SSBRS     #SESN  10
     C                     PARM SSESN     #SESO  10
     C                     PARM *BLANKS   #OTHER156                       138957
     C                     ELSE                                           CAP137
     C                     CALL 'SEC023'               99                 CAP137
     C                     PARM           PPGM                            CAP137
     C                     PARM *BLANKS   #RTRN                           CAP137
     C                     PARM 'I'       #ACTC                           CAP137
     C                     PARM SSBRS     #SESN                           CAP137
     C                     PARM SSESN     #SESO                           CAP137
     C                     PARM *BLANKS   #OTHER                          CAP137
     C                     PARM 'Y'       PLINK                           CAP137
     C                     ENDIF                                          CAP137
      *
     C           *IN99     IFEQ '1'
     C           *INU7     OREQ '1'
     C           *INU8     OREQ '1'
     C           *LOCK     IN   LDA
     C                     Z-ADD7         DBASE
     C                     MOVEL*BLANKS   DBFILE
     C           CAP137    IFNE 'Y'                                       CAP137
     C                     MOVELNARR,1    DBKEY
     C                     ELSE                                           CAP137
     C                     MOVELNARR,3    DBKEY                           CAP137
     C                     ENDIF                                          CAP137
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDIF                           E*2
      *
     C                     ENDDO                           E*1
      *
      **  If F3 or F12 were pressed, dont save the screen fields
     C           *INKC     IFEQ '1'
     C           *INKL     OREQ '1'
     C                     MOVE 'Y'       ERROR
     C                     ENDIF
      *
      **  If F12 was pressed, display the previous screen
     C           *INKL     IFEQ '1'
     C                     MOVE '0'       *INKL
     C                     EXSR P010
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            P009   - Validate 2nd Details entered              *
      *                                                               *
      * CALLS      ZASNMS - Send message to program's message queue   *
      *                                                               *
      * CALLED BY  P008   - Subscription Rights Tradable Processing.  *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           P009      BEGSR
      *
      **  Validate Subscription Right Shortname
     C           SSBRS     IFNE *BLANKS                    B*1
      *
      * Check Security Shortname
      *
     C/COPY WNCPYSRC,SE7503C003
      *
     C           SSBRS     CHAINSECTYZ1              89
      *
     C           *IN89     IFEQ '1'                        B*2
     C                     MOVEL'CO00169' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN46
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*2
      *
     C                     ELSE                            X*1
     C                     MOVE @SESN     SSBRS
     C                     ENDIF                           E*1
      *
      **  Validate Subscription Exercise Price
     C           SREPR     IFEQ *BLANKS                    B*1
     C                     MOVE SSBPR     SREPR
     C                     ELSE                            X*1
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE SREPR     ZFIELD
     C                     Z-ADD7         ZADIG
     C                     Z-ADD8         ZADEC
     C                     EXSR ZALIGN
      *
     C           *IN99     IFEQ '1'                        B*2
     C                     MOVEL'CO00160' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN47
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
      **  Validate Right Exercise Ex-date
     C           SREXD     IFNE *BLANKS                    B*1
     C                     MOVE SREXD     ZDATE
     C                     EXSR ZDATE1
      *
     C           *IN99     IFEQ '1'                        B*2
     C                     MOVEL'CO00166' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN48
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
      **  Validate Rights Exercise allocation effective date
     C           SREXA     IFEQ *BLANKS                    B*1
     C                     MOVE MATY      ZDAYNO
     C                     EXSR ZDATE2
      *
     C           ZDATE     IFEQ 0                          B*2
     C                     MOVE *BLANKS   SREXA
     C                     ELSE                            X*2
     C                     MOVE ZDATE     SREXA
     C                     ENDIF                           E*2
      *
     C                     ELSE                            X*1
     C                     MOVE SREXA     ZDATE
     C                     EXSR ZDATE1
      *
     C           *IN99     IFEQ '1'                        B*2
     C                     MOVEL'CO00167' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN49
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
      **  Validate Rights Exercise Corporate Action Type
     C           SREAT     IFNE *BLANKS                    B*1
     C           SREAT     CHAINSECOATL0             89
      *  NOT AN 'EX'
     C           *IN89     IFEQ '0'                        B*2
     C           MMPTYP    ANDNE'EX'
     C                     MOVEL'CO00162' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN50
     C                     MOVE 'Y'       ERROR
     C                     ELSE                            X*2
      *  DO NOT EXIST
     C           *IN89     IFEQ '1'                        B*3
     C           *IN89     OREQ '0'
     C           MMPTYP    ANDNE'EX'
     C           MMRECI    ANDNE'D'
     C                     MOVEL'CO00161' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN50
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*3
      *
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
     C                     Z-ADD*ZEROS    WRTRF   50
     C                     Z-ADD*ZEROS    WRTRT   50
      *
      **  Validate Trading from Date
     C           SRTRF     IFNE *BLANKS                    B*1
     C                     MOVE SRTRF     ZDATE
     C                     EXSR ZDATE1
      *
     C           *IN99     IFEQ '1'                        B*2
     C                     MOVEL'CO00337' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN55
     C                     MOVE 'Y'       ERROR
     C                     ELSE                            X*2
     C                     Z-ADDZDAYNO    WRTRF
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
      **  Validate Trading from Date
     C           SRTRT     IFNE *BLANKS                    B*1
     C                     MOVE SRTRT     ZDATE
     C                     EXSR ZDATE1
      *
     C           *IN99     IFEQ '1'                        B*2
     C                     MOVEL'CO00338' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN56
     C                     MOVE 'Y'       ERROR
     C                     ELSE                            X*2
     C                     Z-ADDZDAYNO    WRTRT
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
     C           *IN55     IFEQ '0'                        B*1
     C           *IN56     ANDEQ'0'
     C           SRTRF     ANDNE*BLANKS
     C           *IN55     OREQ '0'
     C           *IN56     ANDEQ'0'
     C           SRTRT     ANDNE*BLANKS
      *
     C           SRTRF     IFNE *BLANKS                    B*2
     C           SRTRT     ANDEQ*BLANKS
     C                     MOVEL'CO00339' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN56
     C                     MOVE 'Y'       ERROR
     C                     ELSE                            X*2
      *
     C           SRTRT     IFNE *BLANKS                    B*3
     C           SRTRF     ANDEQ*BLANKS
     C                     MOVEL'CO00340' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN56
     C                     MOVE 'Y'       ERROR
     C                     ELSE                            X*3
     C           WRTRT     IFLE WRTRF                      B*4
     C                     MOVEL'CO00341' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN56
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*4
     C                     ENDIF                           E*3
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
      **  Validate Rights-Number of New Shares
     C           SRNNS     IFNE *BLANKS                    B*1
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE SRNNS     ZFIELD
     C                     Z-ADD8         ZADIG
     C                     Z-ADD7         ZADEC
     C                     EXSR ZALIGN
      *
     C           *IN99     IFEQ '1'                        B*2
     C                     MOVEL'CO00163' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN51
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
      **  Validate Rights-Number of Rights
     C           SRNRI     IFNE *BLANKS                    B*1
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE SRNRI     ZFIELD
     C                     Z-ADD8         ZADIG
     C                     Z-ADD7         ZADEC
     C                     EXSR ZALIGN
      *
     C           *IN99     IFEQ '1'                        B*2
     C                     MOVEL'CO00164' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN52
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
      **  Validate Rights-Denomination Multiplier
     C           SRMLT     IFEQ *BLANKS                    B*1
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE 1         ZFLD15
     C******               Z-ADDNMDP      ZDECS                           136935
     C                     Z-ADDEVNMDP    ZDECS                           136935
     C                     EXSR ZSEDIT
     C                     MOVEL*BLANKS   SRMLT
     C                     MOVE ZOUT21    WFLD17
     C                     MOVELWFLD17    SRMLT
     C                     ENDIF                           E*1
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE SRMLT     ZFIELD
     C******     15        SUB  NMDP      ZADIG                           136935
     C******               Z-ADDNMDP      ZADEC                           136935
     C           15        SUB  EVNMDP    ZADIG                           136935
     C                     Z-ADDEVNMDP    ZADEC                           136935
     C                     EXSR ZALIGN
      *
     C           *IN99     IFEQ '1'                        B*3
     C                     MOVEL'CO00165' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN53
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*1
      *
      **  Validate Rights-Dividend Right
     C           SRDIV     IFNE *BLANKS                    B*1
     C                     MOVE SRDIV     ZDATE
     C                     EXSR ZDATE1
      *
     C           *IN99     IFEQ '1'                        B*2
     C                     MOVEL'CO00168' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN54
     C                     MOVE 'Y'       ERROR
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            P010   - Initialise 2nd screen fields              *
      *                                                               *
      * CALLS      *PSSR  - Program error handling routine            *
      *                                                               *
      * CALLED BY  P002   - Main Processing                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           P010      BEGSR
      *
      **  2nd screen fields
      *
     C           MDSBRS    IFEQ *BLANKS                    B*1
     C                     MOVE SSESN     SSBRS
     C                     ELSE                            X*1
     C                     MOVE MDSBRS    SSBRS
     C                     ENDIF                           E*1
      *
      **  Retrieve Nominal Currency of the Event Security
     C           SSBRS     CHAINSECTYZ1              89
      *
      **  If record not found
     C           *IN89     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     Z-ADD8         DBASE
     C                     MOVE 'SECTYD'  DBFILE
     C                     MOVELMDSBRS    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           MDREPR    IFNE *ZEROS
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE MDREPR    ZFLD15
     C                     Z-ADD8         ZDECS
     C                     MOVE 'L'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVEL*BLANKS   SREPR
     C                     MOVE ZOUT21    WFLD17 17
     C                     MOVELWFLD17    SREPR
     C                     ELSE
     C                     MOVE *BLANKS   SREPR
     C                     ENDIF
      *
     C           MDREXD    IFNE *ZEROS
     C                     Z-ADDMDREXD    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     SREXD
     C                     ELSE
     C                     MOVE *BLANKS   SREXD
     C                     ENDIF
      *
     C           MDREXA    IFNE *ZEROS
     C                     Z-ADDMDREXA    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     SREXA
     C                     ELSE
     C                     MOVE *BLANKS   SREXA
     C                     ENDIF
      *
     C                     MOVE MDREAT    SREAT
      *
     C           MDRTRF    IFNE *ZEROS
     C                     Z-ADDMDRTRF    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     SRTRF
     C                     ELSE
     C                     MOVE *BLANKS   SRTRF
     C                     ENDIF
      *
     C           MDRTRT    IFNE *ZEROS
     C                     Z-ADDMDRTRT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     SRTRT
     C                     ELSE
     C                     MOVE *BLANKS   SRTRT
     C                     ENDIF
      *
     C           MDRNNS    IFNE *ZEROS
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE MDRNNS    ZFLD15
     C                     Z-ADD7         ZDECS
     C                     MOVE 'L'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVEL*BLANKS   SRNNS
     C                     MOVE ZOUT21    WFLD17 17
     C                     MOVELWFLD17    SRNNS
     C                     ELSE
     C                     MOVE *BLANKS   SRNNS
     C                     ENDIF
      *
     C           MDRNRI    IFNE *ZEROS                     B*1
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE MDRNRI    ZFLD15
     C                     Z-ADD7         ZDECS
     C                     MOVE 'L'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVEL*BLANKS   SRNRI
     C                     MOVE ZOUT21    WFLD17
     C                     MOVELWFLD17    SRNRI
     C                     ELSE                            X*1
     C                     MOVE *BLANKS   SRNRI
     C                     ENDIF                           E*1
      *
     C           MDRMLT    IFNE *ZEROS                     B*1
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE MDRMLT    ZFLD15
     C******               Z-ADDNMDP      ZDECS                           136935
     C                     Z-ADDEVNMDP    ZDECS                           136935
     C                     EXSR ZSEDIT
     C                     MOVEL*BLANKS   SRMLT
     C                     MOVE ZOUT21    WFLD17
     C                     MOVELWFLD17    SRMLT
     C                     ELSE                            X*1
     C                     MOVE *BLANKS   SRMLT
     C                     ENDIF                           E*1
      *
     C           MDRDIV    IFNE *ZEROS                     B*1
     C                     Z-ADDMDRDIV    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     SRDIV
     C                     ELSE                            X*1
     C                     MOVE *BLANKS   SRDIV
     C                     ENDIF                           E*1
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
     C********************************************************************
      *
      * P011 - SR to test price within % tolerance of market price
      *
      *   WWPSSN   : SECURITY SHORTNAME
      *   WWTDVD   : VALUE DATE
      *   WWPPRC   : (OUT) MARKET PRICE FOUND
      *   WWCHCK   : (OUT) 'Y'=DO THE CHECK
      *                    'N'=DO NOT THE CHECK, PRICE NOT FOUND
      *
     C*****************************************************************
     C           P011      BEGSR                           ** P011 **
     C*
      ** Klist for market price
     C           KPRICN    KLIST
     C                     KFLD           WWPSSN 10
     C                     KFLD           WWTDVD  50
      *
      ** Find market price with correct date
     C                     MOVE 'N'       WWCHCK  1
      *
     C           KPRICN    SETGTPRICM
     C                     READPPRICM                    89
      *
      ** If record found use this price
     C           *IN89     IFEQ '0'                        B1
     C           PSSN      ANDEQWWPSSN
     C                     MOVE 'Y'       WWCHCK
      *
      ** ..otherwise check for current price
     C                     ELSE                            X1
     C                     Z-ADD*HIVAL    WWTDVD
      *
     C           KPRICN    SETGTPRICM
     C                     READPPRICM                    89
      *
      ** If record found use this price
     C           *IN89     IFEQ '0'                        B2
     C           PSSN      ANDEQWWPSSN
     C                     MOVE 'Y'       WWCHCK
     C                     END                             E2
     C                     END                             E1
      *
     C           WWCHCK    IFEQ 'N'
     C                     Z-ADD0         WWPPRC 158
     C                     ELSE
     C                     Z-ADDPPRC      WWPPRC
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
     C********************************************************************
      *                                                                   137884
      * P012 - Calculate Compensation Price if Subscription Price         137884
      *        entered as Market                                          137884
      *                                                                   137884
     C*****************************************************************   137884
     C           P012      BEGSR                           ** P012 **     137884
      *                                                                   137884
      ***  Find Market Price with correct Date.                           137884
      *                                                                   137884
     C                     MOVE 'N'       WWCHCK  1                       137884
      *                                                                   137884
     C                     MOVE @COEX     WWTDVD                          137884
     C           KPRICN    SETGTPRICM                                     137884
     C                     READPPRICM                    89               137884
      *                                                                   137884
      ***  If record found use this Price.                                137884
      *                                                                   137884
     C           *IN89     IFEQ '0'                                       137884
     C           PSSN      ANDEQWWPSSN                                    137884
     C                     MOVE 'Y'       WWCHCK                          137884
      *                                                                   137884
      ***  Otherwise check for Current Price.                             137884
      *                                                                   137884
     C                     ELSE                                           137884
     C                     Z-ADD*HIVAL    WWTDVD                          137884
      *                                                                   137884
     C           KPRICN    SETGTPRICM                                     137884
     C                     READPPRICM                    89               137884
      *                                                                   137884
      ***  If record found use this price.                                137884
      *                                                                   137884
     C           *IN89     IFEQ '0'                                       137884
     C           PSSN      ANDEQWWPSSN                                    137884
     C                     MOVE 'Y'       WWCHCK                          137884
     C                     ENDIF                                          137884
     C                     ENDIF                                          137884
      *                                                                   137884
     C           WWCHCK    IFEQ 'N'                                       137884
     C                     Z-ADDMKPR      MPRICE 158       Market Price   137884
     C                     ELSE                                           137884
     C                     Z-ADDPPRC      MPRICE                          137884
     C                     ENDIF                                          137884
      *                                                                   137884
     C                     ENDSR                                          137884
      *****************************************************************   137884
      /EJECT                                                              137884
      *****************************************************************
      *                                                               *
      *            *PSSR  - Program error handling routine            *
      *                                                               *
      * CALLS     DBERRCTL- DB error                                  *
      *                                                               *
      * CALLED BY  P001   - Initial Processing                        *
      *            P005   - Initialise 1st screen fields              *
      *            P007   - Save screen fields                        *
      *            P010   - Initialise 2nd screen fields              *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR
      *
      **  Rollback Changes, Print Dump and Cancel Program
     C                     ROLBK
     C                     MOVE 'E'       @RTCD
     C                     DUMP
      *
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C/COPY WNCPYSRC,SE7503C029
     C                     CALL 'DBERRCTL'
     C/COPY WNCPYSRC,SE7503C030
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *  ZASNMS : SEND MESSAGE TO PROGRAM'S MESSAGE QUEUE             *
      *****************************************************************
      *
     C           ZASNMS    BEGSR
     C/COPY WNCPYSRC,SE7503C031
      *
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGM  10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message Id.
     C                     PARM           ZAMSGF 10        Message file.
     C                     PARM           ZAMSDA132        Message data.
     C                     PARM           ZAMSTP  7        Message type.
     C/COPY WNCPYSRC,SE7503C032
      *
      **  Clear all fields for default mechanism next time.
     C                     MOVEL*BLANK    ZAPGM            Program queue
     C                     MOVEL*BLANK    ZAPGRL           Rel queue
     C                     MOVEL*BLANK    ZAMSDA           Message data.
     C                     MOVEL*BLANK    ZAMSTP           Message type.
     C                     MOVEL*BLANK    ZAMSID           Message id.
      *
     C                     ENDSR
     C/COPY WNCPYSRC,SE7503C033
      ********************************************************************
      /EJECT
     C/COPY ZSRSRC,ZDATE1Z2
      /EJECT
     C/COPY ZSRSRC,ZDATE2Z4
      /EJECT
     C/COPY ZSRSRC,ZSEDITZ3
      /EJECT
     C/COPY ZSRSRC,ZEDITZ2
      /EJECT
     C/COPY ZSRSRC,ZALIGNZ2
**  CPY@
(c) Finastra International Limited 2001
**  NARR
ERROR CALLING PGM 'SE0040'
OPTION SHORTNAME NUMBER
ERROR CALLING PGM 'SEC023'
**  TITLE
DIVIDENDS EVENTS
RIGHTS TRADABLE
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  POWER8 - ARRAY OF POWERS FOR CURRENCY CONVERSION
00000001
00000010
00000100
00001000
00010000
00100000
01000000
10000000
