     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Corporate Action Types Audit List')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7530  - Corporate Action Types Audit List                  *
      *                                                               *
      * FUNCTION:  This Program produces a report, for audit          *
      * (COB)      purposes, of all Corporate Action Type defined     *
      * (I/C)      to the system. The report runs on request in I/C   *
      *            and COB and shows a full list, or automatically    *
      *            in COB and audits changes made today.              *
      *                                                               *
      *  Called By: SEC7530 - Corporate Action Types List             *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CSE007  *CREATE    Date 1DEc97                *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     FSECOATPDIF  E           K        DISK
      **  Corporate Action Types
      *
     FSECOATPAIF  E                    DISK         KINFSR *PSSR
      **  Corporate Action Types Trailer
      *
     FSE7530AUO   E             21     PRINTER      KINFDS ERF1DS
     F                                              KINFSR *PSSR
      **  Corporate Action Types Audit
      *
     FSE7530P1O   E             22     PRINTER      KINFDS ERF2DS     UC
     F                                              KINFSR *PSSR
      **  Corporate Action Types Detail
     F/COPY WNCPYSRC,SE7530F001
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  FUNCTION OF INDICATORS                                       *
      * ------------------------                                      *
      *                                                               *
      *  10 - PRINT *** DIFFERENCE ***                                *
      *  21 - OVERFLOW INDICATOR ON SE7530AU                          *
      *  22 - OVERFLOW INDICATOR ON SE7530P1                          *
      *  70 - FILE ERROR                                              *
      *                                                               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  INDEX TO SUBROUTINES                                         *
      * ----------------------                                        *
      *                                                               *
      *  #A     - INITIAL PROCESSING                                  *
      *  #B     - MAIN PROCESSING                                     *
      *  #BA    - PROCESS FULL GROUPS LIST                            *
      *  #BB    - PROCESS FULL LIST RUN CONTROLS                      *
      *  #BC    - PROCESS AUDIT LIST                                  *
      *  #BD    - PROCESS AUDIT LIST RUN CONTROLS                     *
      *  #ZA    - DATABASE ERRORS                                     *
      *  #ZB    - SETUP OUTPUT FIELDS FOR REPORT                      *
      *  *PSSR  - PROGRAM ERROR HANDLING SUBROUINE                    *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
     E                    CPY@    1   1 80
      **  ARRAY FOR COPYRIGHT
     E/COPY WNCPYSRC,SE7530E001
      *
      *****************************************************************
      /EJECT
      *****************************************************************
     ICPYR@$      DS
      **  DATA STRUCTURE FOR COMPILATION - COPYRIGHT INSERTION
      *
     I                                        1  80 CPY@
     I                                        1  20 CPY@$$
      *
     IPSDS       SDS
      **  PROGRAM STATUS DATA STRUCTURE
      *
     I                                      244 253 JOB
     I                                      254 263 USER
      *
     IERF1DS      DS
      **  FILE INFORMATION DATA STRUCTURE
      *
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      **  FOR OVERFLOW - RUN CONTROL
      *
     I                                    B 367 3680LINE
      *
     IERF2DS      DS
      *
     I                                       83  92 SFILE
     I                                    B 123 1240SFNUM
      **  FOR OVERFLOW - PRINT
      *
     I                                    B 367 3680LINE1
      *
     ILDA       E DSLDA                         256
      **  LOCAL DATA AREA
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IUPDATE      DS
      **  DATE FORMATTING DATA STRUCTURE
      *
     I                                        1   2 CADAY
     I                                        3   5 CAMON
     I                                        6   7 CAYER
      *
     ISDBANK    E DSSDBANKPD
      **  BANK DETAILS
      *
     IDSSDY     E DSDSSDY
      ** SECOND DS FOR ACCESS PROGRAMS, LONG DATA STRUCTURE
     I/COPY WNCPYSRC,SE7530I001
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            MAIN   - Main Routine.                             *
      *                                                               *
      * CALLS      #A     - Initial Processing.                       *
      *            #B     - Main Processing.                          *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
      *
      **   INPUT PARAMETER LIST
      *
     C           *ENTRY    PLIST
      *
      **   FOR SELECTION OF FULL LIST OR AUDIT LIST
      *
     C                     PARM           MODE    1
     C                     PARM           @RSEQ   5
      *
      **   INITIAL PROCESSING
      *
     C                     EXSR #A
      *
      **   MAIN PROCESSING
      *
     C                     EXSR #B
     C/COPY WNCPYSRC,SE7530C006
      *
     C           WWOPEN    IFEQ 'Y'
      *
      ** Call ZSFILE for P1 file
      *
     C                     Z-ADDSFNUM     ZSNUM   60
     C                     CALL 'ZSFILE'
     C                     PARM           @RSEQ
     C                     PARM *BLANKS   @PARM   3
     C                     PARM           SFILE
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
     C           ZSERR     IFEQ 'Y'
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     RETRN
     C                     END
      *
     C                     CLOSESE7530P1
     C                     END
      *
      ** Call ZSFILE for AU file
      *
     C                     Z-ADDSFNUMU    ZSNUM   60
     C                     CALL 'ZSFILE'
     C                     PARM           @RSEQ
     C                     PARM *BLANKS   @PARM   3
     C                     PARM           SFILEU
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
     C           ZSERR     IFEQ 'Y'
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     RETRN
     C                     END
      *
      **   END PROGRAM
      *
     C                     MOVE '1'       *INLR
     C                     RETRN
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            #A     - INITIAL PROCESSING                        *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY  MAIN   - Main Routine.                             *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           #A        BEGSR
      *
     C                     SETOF                     10
     C                     Z-ADD*ZEROS    LINE
     C                     Z-ADD*ZEROS    LINE1
      *
      ** Initialise DB error fields annd store program name for DB
      ** error field
      ** Prepare LDA
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     Z-ADD*ZEROS    DBASE
     C                     MOVEL'SE7530'  DBPGM
     C                     OUT  LDA
      *
      ** INITIALISE WORK FIELDS
     C                     MOVE '1'       WWFTT   1
      *
      ** Initialise run controls work fields
      *
     C                     Z-ADD*ZEROS    RRPROG
     C                     Z-ADD*ZEROS    RRFILE
     C                     MOVE *BLANK    RRACTN
     C/COPY WNCPYSRC,SE7530C003
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            #B     - Main Processing                           *
      *                                                               *
      * CALLS      #BA    - Process Full Safe Custody Fee Groups List *
      *            #BB    - Process Full List Run Controls            *
      *            #BC    - Process Audit Safe Custody Fee Groups List*
      *            #BD    - Process Audit List Run Controls           *
      *            #ZA    - Process Database Errors                   *
      *                                                               *
      * CALLED BY  MAIN   - Main Routine.                             *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           #B        BEGSR
      *
      ** Access Bank details file
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*DBERR ' @@RTCD  7
     C                     PARM '*FIRST ' @@OPTN  7
     C           SDBANK    PARM SDBANK    DSSDY
      *
     C           @@RTCD    IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE
     C                     MOVEL'SDBANKPD'DBFILE
     C                     MOVEL'*FIRST'  DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ADD  1         RRFILE
     C/COPY WNCPYSRC,SE7530C002
      *
      ** CHECK INPUT PARAMETER
      *
     C           MODE      IFEQ '2'
      *
      ** FULL LIST
      *
     C                     EXSR #BA
      *
      ** FULL LIST RUN CONTROLS PROCESSING
      *
     C                     EXSR #BB
      *
     C                     ELSE
      *
     C           MODE      IFEQ '1'
      *
      ** AUDIT LIST
      *
     C                     EXSR #BC
      *
      ** AUDIT LIST RUN CONTROLS PROCESSING
      *
     C                     EXSR #BD
      *
     C                     ELSE
      *
      ** STANDARD DATABASE ERROR HANDLING
      *
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE
     C                     MOVEL'MODE'    DBFILE
     C                     MOVELMODE      DBKEY
     C                     OUT  LDA
     C                     EXSR #ZA
     C                     END
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            #ZA    - Process Database Errors                   *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY  #A     - Initial Processing                        *
      *            #B     - Main Processing                           *
      *            #BB    - Process Full List Run Controls            *
      *            #BD    - Process Audit List Run Controls           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           #ZA       BEGSR
      *
     C                     WRITEHEADER
      *
      * WRITE TITLE FOR FULL LIST OR AUDIT LIST
      *
     C           MODE      IFEQ '1'
     C                     WRITEHEAD3
     C                     ELSE
     C                     WRITEHEAD2
     C                     END
      *
     C                     WRITEDBERROR
      *
      ** Call ZSFILE for AU file
      *
     C                     Z-ADDSFNUMU    ZSNUM   60
     C                     CALL 'ZSFILE'
     C                     PARM           @RSEQ
     C                     PARM *BLANKS   @PARM   3
     C                     PARM           SFILEU
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
     C           ZSERR     IFEQ 'Y'
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     RETRN
     C                     END
      *
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            #BA    - PROCESS FULL SAFE CUSTODY FEE GROUPS LIST *
      *                                                               *
      * CALLS      #ZB    - Setup Output Fields for Table List        *
      *                                                               *
      * CALLED BY  #B     - Main Processing                           *
      *                                                               *
      * FLDS USED                                                     *
      *                                                               *
      *****************************************************************
     C           #BA       BEGSR
      *
     C           *LOVAL    SETLLSECOATPD
     C                     READ SECOATPD                 70
      *
     C           *IN70     IFEQ '1'
      *
      ** SETON FLAG TO INDICATE NULL REPORT
      *
     C                     MOVE '1'       WWEMTF  1
      *
      ** SHOULD NOT PRODUCE RUN CONTROLS ANYMORE
      *
     C                     MOVE 'Y'       WWNPRC  1
     C                     WRITEHEADER
     C                     WRITEHEAD2
     C                     WRITENULL1
      *
     C                     ELSE
     C                     OPEN SE7530P1
     C                     MOVE 'Y'       WWOPEN  1
      *
      ** WHILE RECORDS ON FILE
      *
     C           *IN70     DOWEQ'0'
      *
     C           MMRECI    IFEQ 'D'
      *
      ** ADD 1 TO NUMBER OF RECORDS READ
      *
     C                     ADD  1         RRPROG
      *
      ** OUTPUT DETAILS TO REPORT
      *
     C                     EXSR #ZB
     C                     END
      *
     C                     READ SECOATPD                 70
     C                     END
      *
      ** No live records found on SECOATPD
      *
     C           RRPROG    IFEQ 0
     C           WWEMTF    ANDNE'1'
      *
      ** SHOULD NOT PRODUCE RUN CONTROLS ANYMORE
      *
     C                     MOVE 'Y'       WWNPRC  1
     C                     WRITEHEADER
     C                     WRITEHEAD2
     C                     WRITENULL1
     C                     END
      *
     C           LINE1     IFGT 55
     C                     WRITEHEADER1
     C                     WRITEHEADER2
     C                     WRITEHEADER4
     C                     END
      *
     C                     WRITEFOOTING1
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            #ZB    - SETUP OUTPUT FIELDS FOR TABLE LIST        *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY  #BA    - Process Full Safe Custody Fee Groups List *
      *            #BC    - Process Audit Safe Custody Fee Groups List*
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           #ZB       BEGSR
      *
     C           MMCHTP    IFEQ 'I'
     C                     MOVEL'INSERT'  RRACTN
     C                     END
      *
     C           MMCHTP    IFEQ 'A'
     C                     MOVEL'AMEND '  RRACTN
     C                     END
     C*
     C           MMCHTP    IFEQ 'D'
     C                     MOVEL'DELETE'  RRACTN
     C                     END
      *
      ** SET UP CORPORATION ACTION TYPE
      *
     C                     MOVELMMTYPE    RRTYPE
      *
      ** SET UP DESCRIPTION
      *
     C                     MOVELMMDESC    RRDESC
      *
      ** SET UP PROCESSING TYPE
      *
     C                     MOVELMMPTYP    RRPTYP
      *
      ** SET UP C.A. IN PROGRESS ACCOUNT CODE
      *
     C                     MOVELMMACOD    RRACOD
      *
      ** SET UP LAST ACTIVITY DATE FOR OUTPUT
      *
     C                     MOVE MMDLUP    CADAY
     C                     MOVE MMMLUP    CAMON
     C                     MOVE MMYLUP    CAYER
     C                     MOVE UPDATE    RRDATE
     C/COPY WNCPYSRC,SE7530C001
      *
     C           LINE1     IFGT 56
     C           WWFTT     OREQ '1'
     C                     WRITEHEADER1
      *
     C           MODE      IFEQ '2'
     C                     WRITEHEADER2
     C                     ELSE
      *
     C           MODE      IFEQ '1'
     C                     WRITEHEADER3
     C                     END
      *
     C                     END
      *
     C                     WRITEHEADER4
     C                     MOVE '0'       WWFTT
     C                     END
     C/COPY WNCPYSRC,SE7530C004
      *
     C                     WRITEDETAIL1
     C/COPY WNCPYSRC,SE7530C005
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            #BC    - PROCESS AUDIT SAFE CUSTODY FEE GROUPS LIST*
      *                                                               *
      * CALLS      #ZB    - Setup Output Fields for Table List        *
      *                                                               *
      * CALLED BY  #B     - Main Processing                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           #BC       BEGSR
      *
     C           *LOVAL    SETLLSECOATPD
     C                     READ SECOATPD                 70
      *
     C           *IN70     IFEQ '1'
      *
      ** SETON FLAG TO INDICATE NULL REPORT
      *
     C                     MOVE '1'       WWEMTF  1
      *
      ** SHOULD NOT PRODUCE RUN CONTROLS ANYMORE
      *
     C                     MOVE 'Y'       WWNPRC  1
     C                     WRITEHEADER
     C                     WRITEHEAD3
     C                     WRITENULL1
      *
     C                     ELSE
      *
     C                     OPEN SE7530P1
     C                     MOVE 'Y'       WWOPEN
      *
     C           *IN70     DOWEQ'0'
      *
      ** ADD 1 TO NUMBER OF RECORDS READ
      *
     C                     ADD  1         RRPROG
      *
      ** ADD 1 TO NUMBER OF INSERTS
     C*
     C           MMCHTP    IFEQ 'I'
     C                     ADD  1         RRINSR
     C                     END
      *
      ** ADD 1 TO NUMBER OF AMENDS
      *
     C           MMCHTP    IFEQ 'A'
     C                     ADD  1         RRAMND
     C                     END
      *
      ** ADD 1 TO NUMBER OF DELETES
      *
     C           MMCHTP    IFEQ 'D'
     C                     ADD  1         RRDLET
     C                     END
      *
      ** OUTPUT DETAILS TO REPORT
      *
     C                     EXSR #ZB
     C                     READ SECOATPD                 70
      *
     C                     END
      *
      ** NO LIVE RECORDS FOUND ON FILE SECOATPD
      *
     C           RRPROG    IFEQ 0
     C           WWEMTF    ANDNE'1'
      *
      ** SHOULD NOT PRODUCE RUN CONTROLS ANYMORE
      *
     C                     MOVE 'Y'       WWNPRC  1
     C                     WRITEHEADER
     C                     WRITEHEAD3
     C                     WRITENULL1
     C                     END
      *
     C           LINE1     IFGT 56
     C                     WRITEHEADER1
     C                     WRITEHEADER3
     C                     WRITEHEADER4
     C                     END
      *
     C                     WRITEFOOTING1
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            #BB    - PROCESS FULL LIST RUN CONTROLS            *
      *                                                               *
      * CALLS      #ZA    - Process Database Errors                   *
      *                                                               *
      * CALLED BY  #B     - Main Processing                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           #BB       BEGSR
      *
     C           1         SETLLSECOATPA
     C                     READ SECOATPA                 70
      *
     C           *IN70     IFEQ '1'
     C           MMRECI    ORNE 'Z'
      *
      ** TRAILER RECORD NOT FOUND - DATABASE ERROR
      *
     C           *LOCK     IN   LDA
     C                     Z-ADD3         DBASE
     C                     MOVEL'SECOATPA'DBFILE
     C                     MOVEL*BLANKS   DBKEY
     C                     OUT  LDA
     C                     EXSR #ZA
      *
     C                     ELSE
      *
     C           RRPROG    IFNE MMNORE
      *
      ** RECORDS READ <> RECORDS ON FILE
      *
     C                     MOVE '1'       *IN10
     C                     MOVE '1'       *INU8
     C                     END
      *
     C                     MOVE MMNORE    RRFILE
      *
      ** OUTPUT RUN CONTROLS REPORT
      *
     C           WWNPRC    IFNE 'Y'
     C                     WRITEHEADER
     C                     WRITEHEAD2
     C                     WRITEDETAILA
     C                     END
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            #BD    - PROCESS AUDIT LIST RUN CONTROLS           *
      *                                                               *
      * CALLS      #ZA    - Process Database Errors                   *
      *                                                               *
      * CALLED BY  #B     - Main Processing                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           #BD       BEGSR
      *
     C                     READ SECOATPA                 70
      *
     C           *IN70     IFEQ '1'
     C           MMRECI    ORNE 'Z'
      *
      ** TRAILER RECORD NOT FOUND - DATABASE ERROR
      *
     C           *LOCK     IN   LDA
     C                     Z-ADD4         DBASE
     C                     MOVEL'SECOATPA'DBFILE
     C                     MOVEL*BLANKS   DBKEY
     C                     OUT  LDA
     C                     EXSR #ZA
      *
     C                     ELSE
      *
      ** OUTPUT RUN CONTROLS REPORT
      *
     C           WWNPRC    IFNE 'Y'
     C                     WRITEHEADER
     C                     WRITEHEAD3
     C                     WRITEDETAILB
     C                     END
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            *PSSR  - Program error handling routine            *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY  #B     - Main Processing                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR
      *
     C                     DUMP
     C                     ROLBK
     C                     SETON                       U7U8
     C                     MOVE '1'       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
**  CPY@
(c) Finastra International Limited 2001
