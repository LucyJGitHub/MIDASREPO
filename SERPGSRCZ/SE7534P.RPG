     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Corporate Action Allocation Prompt')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7534P - Corporate Action Allocation Report Prompt.         *
      *                                                               *
      *  Function:  Prompt Program for Corporate Action Allocation    *
      *  (I/C)      Report.                                           *
      *                                                               *
      *  Called by: FCC0201A - Standard Report Requests Program for   *
      *                        reports with prompt programs           *
      *  Calls    : AOBANKR0 - Access Program for Bank Details        *
      *             Y2CLMSC  - Program to clear message subfile       *
      *             Y2SNMGC  - Program to send message to subfile     *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. CSE020             Date 21Mar00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSE007 *C *CREATE  Date 1DEc97                *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE020 - Corporate Actions Domino Effect Processing in       *
      *           SECORFPD (recompile)                                *
      *  CSE007- Corporate Actions                                    *
      *                                                               *
      *****************************************************************
     FSE7534DFCF  E                    WORKSTN
      ** Corporate Action Allocation Prompt Screen.
      *
     FSECORFL0IF  E           K        DISK         KINFSR *PSSR
      ** Corporate Action References.
      *
      *****************************************************************
      *                                                               *
      *  F U N C T I O N    O F    I N D I C A T O R S                *
      *                                                               *
      *  01    Reverse Image for Reference Field                      *
      *  02    Reverse Image for Status Field                         *
      *  70    LOKUP indicator                                        *
      *  80    CHAIN indicator                                        *
      *                                                               *
      *  U7-U8 Database error indicator                               *
      *  LR    End of program indicator                               *
      *                                                               *
      *****************************************************************
      /SPACE 3
      ****************************************************************
      *                                                              *
      *  S U B R O U T I N E    I N D E X                            *
      *                                                              *
      *  SRINIT - Initial processing                                 *
      *  SRSET0 - Reset indicators                                   *
      *  SRVAL  - SR to validate screen fields                       *
      *  SRVREF - SR to validate Corporate Action Reference          *
      *  ZASNMS - SR to send message on queue                        *
      *  *PSSR  - Error routine                                      *
      *                                                              *
      ****************************************************************
      /EJECT
      *
     E                    CPY@    1   1 80
      ** Copyright Array
      *
     E                    AREF        6  1
      ** References
      *
     E/COPY ZSRSRC,ZALIGNZ1
      *
      /EJECT
      *
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IPSDS       SDS
      ** Program Status Data Structure
      *
     I                                     *PROGRAM SPGM
     I                                      244 253 SJOB
     I                                      254 263 SUSER
     I                                      282 2870STIME
      *
     IWPARM       DS
      ** Parameters for WPARM - 100 long string passed to RCF
      *
     I                                        1 100 WRPRM
     I                                        1   1 SALCA
     I                                        2   7 SCOAR
      *
     ISDBANK    E DSSDBANKPD
      ** Data Structure for SDBANKPD
      *
     IDSFDY     E DSDSFDY
      ** Short Data Structure
      *
      *****************************************************************
      *                                                               *
      *            MAIN   - Main Routine.                             *
      *                                                               *
      * CALLS      SRINIT - Program Initialisation.                   *
      *            SRSET0 - Initialise Indicators.                    *
      *            SRVAL  - Validate All Fields.                      *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
      *
     C           *ENTRY    PLIST
     C                     PARM           WRSEQ   5
     C                     PARM           WRLEV   1
     C                     PARM           WENT    3
     C                     PARM           WPARM 100
     C                     PARM           WACT    1
     C                     PARM           WCMD    1
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      WCPY   80
      *
      **  Perform Intialisation
      *
     C                     EXSR SRINIT
      *
     C                     EXSR SRSET0
      *
     C           *INKC     DOWEQ'0'
     C           WVALID    ANDEQ0
      *
      ** Read Input Screen
      *
     C                     Z-ADD1         WVALID  10
     C                     WRITESE7534C0
     C                     EXFMTSE7534F1
      *
     C                     EXSR SRSET0
      *
     C           *INKC     IFEQ '1'
     C                     MOVE 'E'       WCMD
     C                     MOVE *BLANKS   SALCA
     C                     MOVE *BLANKS   SCOAR
     C                     GOTO END
     C                     ENDIF
      *
     C                     EXSR SRVAL
      *
     C                     ENDDO
      *
     C           END       TAG
      *
     C           SCOAR     IFNE *BLANKS
     C                     MOVE WSCOAR    SCOAR
     C                     ENDIF
      *
     C                     MOVE '1'       *INLR
     C                     RETRN
      *****************************************************************
      *                                                               *
      *            SRINIT - Initial Processing                        *
      *                                                               *
      * CALLS      *PSSR  - Database Error Processing.                *
      *                                                               *
      * CALLED BY  MAIN   - Main Routine.                             *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           SRINIT    BEGSR                           ** SRINIT **
      *
      ** Read in Installation Data
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE 'SE7534P 'DBPGM
     C                     OUT  LDA
      *
      ** Access the Bank Details
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD001       DBASE
     C                     MOVEL'SDBANKPD'DBFILE
     C                     MOVEL'*FIRST'  DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
     C                     MOVE BJMRDT    SMRDT
     C                     MOVEL'SEUSRMSG'ZAMSGF
     C                     MOVELSPGM      PGMNAM
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            SRSET0 - Reset Indicators.                         *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY  MAIN   - Main Routine.                             *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           SRSET0    BEGSR                           ** SRSET0 **
      *
     C                     MOVE '0'       *IN01
     C                     MOVE '0'       *IN02
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            SRVAL  - Validate Screen Fields.                   *
      *                                                               *
      * CALLS      Y2CLMSC- Clear Subfile.                            *
      *            ZASNMS - Add Error Messages to Message Subfile.    *
      *                                                               *
      * CALLED BY  MAIN   - Main Routine.                             *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           SRVAL     BEGSR                           ** SRVAL **
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM SPGM      ZAPGMQ 10
     C                     PARM '*SAME'   ZAPGRL  5
      *
     C           SALCA     IFEQ *BLANKS
     C           SCOAR     ANDEQ*BLANKS
     C                     MOVEL'CO00202' ZAMSID
     C                     MOVE '1'       *IN02
     C                     EXSR ZASNMS
     C                     GOTO ENDVAL
     C                     ENDIF
      *
     C           SALCA     IFNE 'Y'
     C           SALCA     ANDNE'N'
     C           SALCA     ANDNE*BLANKS
     C                     MOVEL'CO00186' ZAMSID
     C                     MOVE '1'       *IN01
     C                     EXSR ZASNMS
     C                     GOTO ENDVAL
     C                     ENDIF
      *
     C           SALCA     IFNE *BLANKS
     C           SCOAR     ANDNE*BLANKS
     C                     MOVEL'CO00203' ZAMSID
     C                     MOVE '1'       *IN02
     C                     EXSR ZASNMS
     C                     GOTO ENDVAL
     C                     ENDIF
      *
      ** Validate Corporate Action Reference
      *
     C           SCOAR     IFNE *BLANKS
     C                     EXSR SRVREF
     C                     ENDIF
      *
     C           ENDVAL    TAG
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            SRVREF - Validate Corporate Action Reference.      *
      *                                                               *
      * CALLS      SRALL  - Check If Other Fields are Blank.          *
      *            ZASNMS - Send Messages to Queue.                   *
      *                                                               *
      * CALLED BY  SRVAL  - Validate All Screen Fields.               *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           SRVREF    BEGSR                           ** SRVREF **
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE SCOAR     ZFIELD
     C                     Z-ADD0         ZADEC
     C                     Z-ADD6         ZADIG
     C                     EXSR ZALIGN
      *
     C           *IN99     IFEQ '1'
     C                     MOVEL'CO00182' ZAMSID
     C                     MOVE '1'       *IN02
     C                     EXSR ZASNMS
     C                     GOTO ENDREF
     C                     ELSE
     C                     MOVE ZFIELD    WSCOAR  6
     C                     ENDIF
      *
     C           WSCOAR    IFEQ '000000'
     C                     MOVEL'CO00183' ZAMSID
     C                     MOVE '1'       *IN02
     C                     EXSR ZASNMS
     C                     GOTO ENDREF
      *
     C                     ELSE
      *
     C                     MOVEASCOAR     AREF
      *
      ** Check if Reference Number has a decimal point
      *
     C           '.'       LOKUPAREF                     70
      *
     C           *IN70     IFEQ '1'
     C                     MOVEL'CO00182' ZAMSID
     C                     MOVE '1'       *IN02
     C                     EXSR ZASNMS
     C                     GOTO ENDREF
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Check Reference Number if Existing
      *
     C           WSCOAR    CHAINSECORFD0             80
      *
     C           *IN80     IFEQ '1'
     C                     MOVEL'CO00181' ZAMSID
     C                     MOVE '1'       *IN02
     C                     EXSR ZASNMS
     C                     GOTO ENDREF
     C                     ENDIF
      *
     C           MARECI    IFNE 'D'
     C                     MOVEL'CO00259' ZAMSID
     C                     MOVE '1'       *IN02
     C                     EXSR ZASNMS
     C                     ENDIF
      *
     C           ENDREF    TAG
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            ZASNMS - Send Message to Program Message Queue.    *
      *                                                               *
      * CALLS      Y2SNMGC- Send a Message.                           *
      *                                                               *
      * CALLED BY  SRVREF - Validate Corporate Action Reference.      *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           ZASNMS    BEGSR                           ** ZASNMS **
      *
     C           ZAPGMQ    IFEQ *BLANKS
     C                     MOVELSPGM      ZAPGMQ
     C                     ENDIF
      *
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGMQ 10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message Id.
     C                     PARM           ZAMSGF 10        Message file.
     C                     PARM           ZAMSDA132        Message data.
     C                     PARM           ZAMSTP  7        Message type.
     C                     MOVE 0         WVALID
      *
     C           ZASNM9    ENDSR                           ** ZASNM9 **
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            *PSSR  - Program Exception Error Routine.          *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY  SRINIT - Initial Processing.                       *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR                           ** *PSSR **
      *
      **  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
      ** Dump and return to the calling program
      *
     C                     MOVE '1'       *INLR
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     DUMP
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *
     C/COPY ZSRSRC,ZALIGNZ2
      *****************************************************************
      /EJECT
      *
**  CPY@
(c) Finastra International Limited 2001
