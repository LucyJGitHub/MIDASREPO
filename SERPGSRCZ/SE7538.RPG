     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Affected customer positions report')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7538 - Affected Customer Positions - Report                *
      *                                                               *
      * Function:  To print the Affected Customer Position report     *
      * (COB - I/C)                                                   *
      *                                                               *
      *  Called By: SEC7537 - Affected Customer Positions             *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001             Date 07Sep99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 136929             Date 16Jun98               *
      *                 CSE007  *CREATE    Date 30Mar98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CPB001 - 'Private Banking' Module                            *
      *  136929 - Review layout for 'Currency Change' Events (CC)     *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     FSECOAPL0IF  E           K        DISK         KINFSR *PSSR
      ***  CO Affected Positions                             Prefix - 'MX'
      *
     FSE7538AUO   E                    PRINTER      KINFSR *PSSR      UC
      ***  Affected Customer Positions - Audit
     F                                              KINFDS SPOOLU
      *
     FSE7538P1O   E                    PRINTER      KINFSR *PSSR      UC
      ***  Affected Customer Positions - Report
     F                                              KINFDS SPOOL1
      *                                                                   136929
     FSE7538P2O   E                    PRINTER      KINFSR *PSSR      UC  136929
      ***  Affected Customer Positions for 'CC' - Report                  136929
     F                                              KINFDS SPOOL2         136929
      *
      /EJECT
      *
     E/COPY ZSRSRC,ZALIGNZ1
     E/COPY ZSRSRC,ZSEDITZ1                                               136929
     E/COPY ZSRSRC,ZDATE2Z1
      *
     E                    #REPS   1   5 17
      ***  Reply Status
      *
     E                    #SNAR   1   6 11
      ***  Status narrative
      *
     E                    CPY@    1   1 80
      ***  Array containing Copyright statement
      *
      /SPACE 3
     I/COPY ZSRSRC,ZSEDITZ2                                               136929
     ISPOOL1      DS
      ***  File Information Data Structure for SE7538P1.
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
     ISPOOL2      DS                                                      136929
      ***  File Information Data Structure for SE7538P2.                  136929
     I                                       83  92 SFILE2                136929
     I                                    B 123 1240SFNUM2                136929
     I                                    B 188 1890OFLLN2                136929
     I                                    B 367 3680PRTLN2                136929
      *
     ISPOOLU      DS
      ***  File Information Data Structure for SE7538AU.
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     IPSDS       SDS
      ***  Program Status Data Structure
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
     ILDA       E DSLDA                         256
      ***  Local data area for database error details
      *
     ISDACOF    E DSSDACOFPD
      ***  Account Officer details file
      *
     ISDCUST    E DSSDCUSTPD
      ***  Customer details file
      *                                                                   136929
     ISDCURR    E DSSDCURRPD                                              136929
      ***  Currency details                                               136929
      *
     ISDBANK    E DSSDBANKPD
      ***  Bank Details
      *
     ISDPORT    E DSSDPORTPD
      ***  Portfolio ICD
      *
     ISDMMOD    E DSSDMMODPD
      ***  Dummy record format for access to Midas Module
      *
     IDSSDY     E DSDSSDY
      ***  Second DS For Access Programs, Long Data Structure
      *
     IDSFDY     E DSDSFDY
      ***  First DS for access programs, short data structure
      *
     ICPYR@$      DS
      ***  Data structure for compilation - Copyright Insertion
     I                                        1  80 CPY@
     I                                        1  20 CPY@$$
      *
      /EJECT
      *****************************************************************
      *                M A I N   P R O C E S S I N G                  *
      *****************************************************************
      *
      ***  Read file for record
      *
     C                     READ SECOAPL0                 89
      *                                                                   136929
     C                     MOVE *BLANKS   WWCOAT  2                       136929
      *
      ***  Do While End of File not reached.
      *
     C           *IN89     DOWEQ*OFF
      *
     C                     EXSR #REPD1
      *
      ***  Read next record.
      *
     C                     READ SECOAPL0                 89
     C                     ENDDO                           EOF not reached
      *
      ***  If RCOUNT >< 0, Then Print Trailer
      *
     C           RCOUNT    IFNE 0
     C           WWOPEN    ANDEQ'Y'
     C                     Z-ADD3         RQDLN1           Check Overflow 136929
     C           OFLLN1    SUB  PRTLN1    DIFLN1                          136929
     C           DIFLN1    IFGT RQDLN1                                    136929
     C                     WRITETRAILP1
     C                     ENDIF                                          136929
     C                     ENDIF
      *                                                                   136929
      ***  If RCOUN2 >< 0, Then Print Trailer                             136929
      *                                                                   136929
     C           RCOUN2    IFNE 0                                         136929
     C           WOPEN2    ANDEQ'Y'                                       136929
     C                     Z-ADD3         RQDLN2           Check Overflow 136929
     C           OFLLN2    SUB  PRTLN2    DIFLN2                          136929
     C           DIFLN2    IFGT RQDLN2                                    136929
     C                     WRITETRAILP2                                   136929
     C                     ENDIF                                          136929
     C                     ENDIF                                          136929
      *
      ***  Close Output file
      *
     C                     CLOSESE7538P1
     C                     CLOSESE7538P2                                  136929
      *
      ***  Perform audit report
      *
     C                     EXSR AUDIT
      *
      ***  Normal End of Program
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
      *****************************************************************
      *         E N D   O F   M A I N   P R O C E S S I N G           *
      *****************************************************************
      /EJECT
      *****************************************************************
      *  #REPD1 - Process Report Lines.                               *
      *  ------                                                       *
      *  Called By : Main processing                                  *
      *  Calls     :                                                  *
      *****************************************************************
      *
     C           #REPD1    BEGSR
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C           MXPTYP    IFNE 'CC'                                      136929
     C           WWOPEN    IFNE 'Y'
     C                     EXSR RCFP1
     C                     ENDIF
     C                     ELSE                                           136929
     C           WOPEN2    IFNE 'Y'                                       136929
     C                     EXSR RCFP2                                     136929
     C                     ENDIF                                          136929
     C                     ENDIF                                          136929
      *
      ***  If Account officer is not equal to old Account officer
      ***  Reset Account officer printer fields and retrieve hte Account Officer name
      *
     C           MXACOF    IFNE WWACOF
     C           WWFIRT    OREQ 'Y'
     C                     MOVE 'N'       WWFIRT
     C                     MOVE MXACOF    RACOF
     C                     MOVE MXACOF    WWACOF
      *
      ***  Access to Account officer file to retrieve the account officer name
      *
     C                     CALL 'AOACOFR0'
     C                     PARM *BLANKS   WLRTCD  7
     C                     PARM '*KEY   ' WLOPTN  7
     C                     PARM WWACOF    WLACOF  2
     C           SDACOF    PARM SDACOF    DSSDY
      *
     C           WLRTCD    IFEQ *BLANKS
     C                     MOVE AZACON    RACON
     C                     ELSE
     C                     MOVE *BLANKS   RACON
     C                     ENDIF
     C           MXPTYP    IFNE 'CC'                                      136929
     C                     WRITEHEADP1                     Force new page
     C                     ELSE                                           136929
     C                     WRITEHEADP2                     Force new page 136929
     C                     ENDIF                                          136929
     C                     ENDIF
      *
      ***  If Depot is not equal to old Depot
      ***  Reset Depot printer fields and retrieve the Depot Shortname
      *
     C           MXDDPT    IFNE WWDDPT
     C**********           Z-ADDMXDDPT    WWDDPT                                              CSD027
     C                     MOVE MXDDPT    WWDDPT                                              CSD027
     C                     MOVELMXDDPT    WLCUST
      *
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   WLRTCD
     C                     PARM '*KEY  '  WLOPTN
     C                     PARM           WLCUST 10
     C                     PARM *BLANKS   WLKYST  7
     C           SDCUST    PARM SDCUST    DSSDY
      *
      ***  If record not found
      *
     C           WLRTCD    IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE            ***************
     C                     MOVE 'SDCUSTPD'DBFILE           * DB ERROR 01 *
     C                     MOVELWWDDPT    DBKEY     P      ***************
     C                     MOVEL'SE7538'  DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELBBCSSN    RDDPT            Depot Shortname
     C                     ENDIF
      *
     C           MXPTYP    IFNE 'CC'                                      136929
     C                     Z-ADD3         RQDLN1           Check Overflow
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
     C                     ELSE                                           136929
     C                     Z-ADD3         RQDLN2           Check Overflow 136929
     C           OFLLN2    SUB  PRTLN2    DIFLN2                          136929
     C           DIFLN2    IFLE RQDLN2                                    136929
     C                     WRITEHEADP2                                    136929
     C                     ENDIF                                          136929
     C                     ENDIF                                          136929
      *
      ***  Report Details
      *
     C                     MOVE MXCNUM    RCNUM            Customer Number
      *
      ***  If Portfolio Management is ON
      ***  or Private Banking is ON                                       CPB001
      *
     C           BGPFMG    IFEQ 'Y'
     C           BGN4ST    OREQ 'Y'                                       CPB001
      *
      ***  If Portfolio is 9997, Setup with default portfolio
      *
     C           MXPTFR    IFEQ '9997'
     C           BGN4ST    ANDNE'Y'                                       CPB001
     C                     MOVE FCR997    RPTFR            Portfolio
     C                     ELSE
     C                     MOVE MXPTFR    RPTFR            Portfolio
     C                     ENDIF
      *
     C                     ELSE                            Portfolio OFF
     C                     MOVE *BLANKS   RPTFR
     C                     ENDIF
      *
     C                     MOVE MXBRCD    RBRCD            Branch
     C                     MOVELMXSESN    RSESN            Security
     C                     MOVELMXCORF    RCORF            Corporate Reference
     C                     MOVELMXCOAT    RCOAT            Action Type
      *
     C           MXEVDT    IFNE *ZEROS
     C                     MOVE MXEVDT    ZDAYNO           Event Date
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    REVDT            DDMMMYY
     C                     ELSE
     C                     MOVE *BLANKS   REVDT            DDMMMYY
     C                     ENDIF
     C                     MOVE MXNCCY    RNCCY            Nominal Ccy
      *
     C           MXEXPO    IFNE *ZEROS
     C*****                MOVE *BLANKS   ZFIELD                          136929
     C*****                MOVE MXEXPO    ZFIELD           Ex date positio136929
     C*****                MOVE MXNMDP    ZADEC                           136929
     C*****                EXSR ZEDIT                                     136929
     C*****                MOVE ZFIELD    REXPO                           136929
     C                     Z-ADD*ZEROS    ZFLD15                          136929
     C                     Z-ADDMXEXPO    ZFLD15           Ex date positio136929
     C                     Z-ADDMXNMDP    ZDECS                           136929
     C                     MOVE 'J'       ZECODE                          136929
     C                     EXSR ZSEDIT                                    136929
     C                     MOVE ZOUT21    REXPO     P                     136929
     C                     ELSE
     C                     MOVE *BLANKS   REXPO
     C                     ENDIF
      *                                                                   136929
     C           MXPTYP    IFNE 'CC'                                      136929
     C                     MOVE MXOPT1    ROPT1            Reply 1
     C                     MOVE MXOPT2    ROPT2            Reply 2
      *
     C                     SELEC                           Reply Status
     C           MXREST    WHEQ 'R'
      ***  Reply Received
     C                     MOVEL#REPS,1   RREST
     C           MXREST    WHEQ 'S'
      ***  Std Inst Default
     C                     MOVEL#REPS,2   RREST
     C           MXREST    WHEQ 'C'
      ***  Corporate Default
     C                     MOVEL#REPS,3   RREST
     C           MXREST    WHEQ 'M'
      ***  Manually Changed
     C                     MOVEL#REPS,4   RREST
     C           MXREST    WHEQ 'N'
      ***  None
     C                     MOVEL#REPS,5   RREST
     C                     OTHER
     C                     MOVE *BLANKS   RREST
     C                     ENDSL
      *
     C           MXCOAD    IFNE *ZEROS
     C                     MOVE MXCOAD    ZDAYNO           Last Allocation Date
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RCOAD            DDMMMYY
     C                     ELSE
     C                     MOVE *BLANKS   RCOAD
     C                     ENDIF
      *
     C                     SELEC                           Status Narrative
     C           MXSTAT    WHEQ 'I'
      ***  Input
     C                     MOVEL#SNAR,1   RSTAT
     C           MXSTAT    WHEQ 'L'
      ***  Allocated
     C                     MOVEL#SNAR,2   RSTAT
     C           MXSTAT    WHEQ 'S'
      ***  Stock Auth.
     C                     MOVEL#SNAR,3   RSTAT
     C           MXSTAT    WHEQ 'X'
      ***  Authorised
     C                     MOVEL#SNAR,4   RSTAT
     C           MXSTAT    WHEQ 'C'
      ***  Complete
     C                     MOVEL#SNAR,5   RSTAT
     C           MXSTAT    WHEQ 'R'
      ***  Reverse
     C                     MOVEL#SNAR,6   RSTAT
     C                     OTHER
     C                     MOVE *BLANKS   RSTAT
     C                     ENDSL
      *                                                                   136929
     C                     ELSE                                           136929
     C                     MOVE MXNSEC    RNSEC            New Security   136929
     C                     MOVE MXNNCY    RNNCY            New Currency   136929
      *                                                                   136929
     C                     Z-ADD*ZEROS    ZFLD15                          136929
     C                     Z-ADDMXASTA    ZFLD15                          136929
     C                     Z-ADDMXNNDP    ZDECS                           136929
     C                     MOVE 'J'       ZECODE                          136929
     C                     EXSR ZSEDIT                                    136929
     C                     MOVE ZOUT21    RASTA     P                     136929
      *                                                                   136929
     C                     CALL 'AOCURRR0'                                136929
     C                     PARM '*MSG   ' WLRTCD                          136929
     C                     PARM '*KEY   ' WLOPTN                          136929
     C                     PARM MXNNCY    WLAJCD  3                       136929
     C           SDCURR    PARM SDCURR    DSSDY                           136929
      *                                                                   136929
      ***  If record not found, handle Database Error.                    136929
      *                                                                   136929
     C           WLRTCD    IFNE *BLANKS                                   136929
     C           *LOCK     IN   LDA                                       136929
     C                     Z-ADD8         DBASE            ***************136929
     C                     MOVEL'SDCURRPD'DBFILE    P      * DB ERROR 08 *136929
     C                     MOVELMXNNCY    DBKEY     P      ***************136929
     C                     MOVEL'SE7538'  DBPGM     P                     136929
     C                     OUT  LDA                                       136929
     C                     EXSR *PSSR                                     136929
     C                     ENDIF                                          136929
      *                                                                   136929
     C                     Z-ADD*ZEROS    ZFLD15                          136929
     C                     Z-ADDMXACAA    ZFLD15                          136929
     C                     Z-ADDA6NBDP    ZDECS                           136929
     C                     MOVE 'J'       ZECODE                          136929
     C                     EXSR ZSEDIT                                    136929
     C                     MOVE ZOUT21    RACAA     P                     136929
      *                                                                   136929
     C                     ENDIF                                          136929
      *
      ***  Write out Detail Record.
      *
     C           MXPTYP    IFNE 'CC'                                      136929
     C                     WRITEDETAIL1
     C                     ADD  1         RCOUNT
     C                     ELSE                                           136929
     C                     WRITEDETAIL2                                   136929
     C                     ADD  1         RCOUN2  50                      136929
     C                     ENDIF                                          136929
      *
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      *  AUDIT  - Subroutine to Output Audit report and End Program.  *
      *  ------                                                       *
      *  Called By : Main Processing, *PSSR                           *
      *  Calls     : None.                                            *
      *****************************************************************
      *
     C           AUDIT     BEGSR                           *** AUDIT  ***
      *
      ***  Output Report Header and File Controls.
      *
     C                     WRITEHEADAU
      *
      ***  If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
     C                     WRITEDBERROR
     C                     ELSE
      *
      ***  Or, if no records read, Output "No Details" message.
      *
     C           RCOUNT    IFEQ 0
     C           RCOUN2    ANDEQ0                                         136929
     C                     WRITENODTLS
     C                     ELSE
      *
      ***  Normal audit  exit
      *
     C                     ADD  RCOUN2    RCOUNT                          136929
     C                     WRITECONTROL
     C                     ENDIF
      *
     C                     ENDIF                           U7 = *ON
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *  ------                                                       *
      *****************************************************************
      *
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      ***  Open Report file
      *
     C                     CLOSESE7538P1
      *
     C                     OPEN SE7538P1
     C                     MOVE 'Y'       WWOPEN
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           PSEQ    5
     C                     PARM PENT      SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE            ***************
     C                     MOVE 'SE7538P1'DBFILE           * DB ERROR 02 *
     C           'ZSFILE R'CAT  'OUTINE'  DBKEY     P      ***************
     C                     MOVEL'SE7538'  DBPGM     P
     C                     OUT  LDA
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT                                                              136929
      *****************************************************************   136929
      *  RCFP2  - Subroutine to register the P2 Printer File via RCF. *   136929
      *  ------                                                       *   136929
      *****************************************************************   136929
      *                                                                   136929
     C           RCFP2     BEGSR                            *** RCFP2  ***136929
      *                                                                   136929
      ***  Open Report file                                               136929
      *                                                                   136929
     C                     CLOSESE7538P2                                  136929
      *                                                                   136929
     C                     OPEN SE7538P2                                  136929
     C                     MOVE 'Y'       WOPEN2                          136929
      *                                                                   136929
      ***  Ensure Report Spool File recorded by RCF.                      136929
      *                                                                   136929
     C                     Z-ADDSFNUM2    ZSNUM   60                      136929
      *                                                                   136929
     C                     CALL 'ZSFILE'                                  136929
     C                     PARM           PSEQ    5                       136929
     C                     PARM PENT      SENTY   3                       136929
     C                     PARM           SFILE2                          136929
     C                     PARM           ZSNUM                           136929
     C                     PARM           ZSERR   1                       136929
      *                                                                   136929
      ***  If Error occurs during ZSFILE processing, then return to the   136929
      ***  Calling Program.                                               136929
      *                                                                   136929
     C           ZSERR     IFEQ 'Y'                                       136929
     C           *LOCK     IN   LDA                                       136929
     C                     Z-ADD7         DBASE            ***************136929
     C                     MOVE 'SE7538P2'DBFILE           * DB ERROR 07 *136929
     C           'ZSFILE R'CAT  'OUTINE'  DBKEY     P      ***************136929
     C                     MOVEL'SE7538'  DBPGM     P                     136929
     C                     OUT  LDA                                       136929
     C                     MOVE *ON       *INU7                           136929
     C                     MOVE *ON       *INU8                           136929
     C                     MOVE *ON       *INLR                           136929
     C                     RETRN                                          136929
     C                     ENDIF                                          136929
      *                                                                   136929
     C                     ENDSR                                          136929
      /EJECT
      *****************************************************************
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *  ------                                                       *
      *****************************************************************
      *
     C           RCFAU     BEGSR                            *** RCFAU  ***
      *
      ***  Open printer File
      *
     C                     MOVE 'N'       WWRCF            Audit under RCF
     C                     Z-ADD0         RCOUNT
     C                     Z-ADD0         RCOUN2                          136929
     C                     OPEN SE7538AU
      *
      ***  Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           PSEQ
     C                     PARM PENT      SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     Z-ADD3         DBASE            ***************
     C                     MOVE 'SE7538AU'DBFILE           * DB ERROR 03 *
     C           'ZSFILE R'CAT  'OUTINE'  DBKEY     P      ***************
     C                     MOVEL'SE7538'  DBPGM     P
     C                     OUT  LDA
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     MOVE 'Y'       WWRCF   1        Audit under RCF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *   *INZSR - Initial Processing                                 *
      *   ------                                                      *
      *   Called by : Main processing                                 *
      *   Calls     : None.                                           *
      *****************************************************************
     C           *INZSR    BEGSR
      *
     C                     EXSR RCFAU
      *
     C           *ENTRY    PLIST
     C                     PARM           PSEQ    5
     C                     PARM           PENT    3
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     Z-ADD0         DBASE
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     MOVEL'SE7538'  DBPGM     P
     C                     OUT  LDA
      *
      ***  Access Bank details file
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '       ' WLRTCD  7
     C                     PARM '*FIRST ' WLOPTN  7
     C           SDBANK    PARM SDBANK    DSSDY
      *
     C           WLRTCD    IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD4         DBASE            ***************
     C                     MOVEL'SDBANKPD'DBFILE    P      * DB ERROR 04 *
     C                     MOVEL'*FIRST'  DBKEY     P      ***************
     C                     MOVEL'SE7538'  DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ***  Set indicator 98 for date format used in ZDATE2
      *
     C           BJDFIN    IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ENDIF
      *
      ***  Access Midas Module Details
      *
     C                     CALL 'AOMMODR0'
     C                     PARM *BLANKS   WLRTCD  7
     C                     PARM '*FIRST  'WLOPTN  7
     C           SDMMOD    PARM SDMMOD    DSFDY
      *
     C           WLRTCD    IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD5         DBASE            ***************
     C                     MOVEL*BLANKS   DBFILE           * DB ERROR 05 *
     C                     MOVE 'SDMMODPD'DBFILE           ***************
     C                     MOVEL'*FIRST  'DBKEY     P
     C                     MOVEL'SE7538'  DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ***  If Portfolio Management is ON
      *
     C           BGPFMG    IFEQ 'Y'
     C                     MOVE *ON       *IN60
      *
      ***  Access Portfolio Management ICD
      *
     C                     CALL 'AOPORTR0'
     C                     PARM *BLANKS   WLRTCD  7
     C                     PARM '*FIRST  'WLOPTN  7
     C           SDPORT    PARM SDPORT    DSFDY
      *
     C           WLRTCD    IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD6         DBASE            ***************
     C                     MOVEL*BLANKS   DBFILE           * DB ERROR 06 *
     C                     MOVE 'SDPORTPD'DBFILE           ***************
     C                     MOVEL'*FIRST  'DBKEY     P
     C                     MOVEL'SE7538'  DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *                                                                   CPB001
      ***  If Private Banking is ON                                       CPB001
      *                                                                   CPB001
     C           BGN4ST    IFEQ 'Y'                                       CPB001
     C                     MOVE *ON       *IN60                           CPB001
     C                     ENDIF                                          CPB001
      *
      ***  Report Work fields.
      *
     C                     Z-ADD0         RCOUNT
     C                     Z-ADD0         RCOUN2                          136929
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
     C                     Z-ADD0         RQDLN2  30                      136929
     C                     Z-ADD0         DIFLN2  30                      136929
     C                     MOVE 'N'       WWOPEN  1
     C                     MOVE 'N'       WOPEN2  1                       136929
     C                     MOVE 'Y'       WWFIRT  1
     C                     MOVE *BLANKS   WWACOF  2
     C**********           Z-ADD*ZEROS    WWDDPT  60                                          CSD027
     C                     MOVE *BLANKS   WWDDPT  6                                           CSD027
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *  *PSSR  - Error control subroutine                            *
      *  ------                                                       *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
     C                     SETON                     U7U8
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     DUMP
     C                     ENDIF
      *
     C           WWRCF     IFEQ 'Y'
     C                     MOVE 'Y'       WWOPEN  1        To avoid RCFAU in AUDIT
     C                     EXSR AUDIT
     C                     ENDIF
      *
      ***  If *PSSR already run, then just end the program here.
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
     C/COPY ZSRSRC,ZSEDITZ3                                               136929
     C/COPY ZSRSRC,ZDATE2Z2
     C/COPY ZSRSRC,ZALIGNZ2
     C/COPY ZSRSRC,ZEDITZ2
     C/COPY ZSRSRC,ZDATE2Z3
**  #REPS
Reply Received
Std Inst Default
Corporate Default
Manually Changed
None
**  #SNAR
Input
Allocated
Stock Auth.
Authorised
Complete
Reverse
**  CPY@
(c) Finastra International Limited 2001
