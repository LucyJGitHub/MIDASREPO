     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Unauthorised Execution after Ex-Date Rpt')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7539 - Unauthorised Execution after Ex-Date Exception Rpt  *
      *                                                               *
      *  Called By: SEC7539 - Unauthorised Corporate Actions          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CSE020             Date 21Mar00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 135689             Date 03Jun98               *
      *                 CSE007  *CREATE    Date 01Apr98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CSE020 - Corporate Actions Domino Effect Processing          *
      *           SECORFPD (Recompiled)                               *
      *  135689 - Corporate Actions                                   *
      *           - Do not print 'reversed' Corporate Actions         *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     FSECORFL2IF  E           K        DISK         KINFSR *PSSR
      ***  CO Incomplete Corporate Actions File
      *
     FSECODRL0IF  E           K        DISK         KINFSR *PSSR
      ***  CO Depots Reference File
      *
     FSE7539P1O   E                    PRINTER      KINFSR *PSSR      UC
      ***  Unauthorised Execution after Ex-Date - Report
     F                                              KINFDS SPOOL1
      *
     FSE7539AUO   E                    PRINTER      KINFSR *PSSR      UC
      ***  Unauthorised Execution after Ex-Date - Audit Report
     F                                              KINFDS SPOOLU
      *
      /EJECT
      *
     E/COPY ZSRSRC,ZALIGNZ1
     E/COPY ZSRSRC,ZDATE2Z1
      *
     E                    #SNAR   1   6 11
      ***  Status narrative
      *
     E                    CPY@    1   1 80
      ***  Array containing Copyright statement
      *
      /SPACE 3
     ISPOOL1      DS
      ***  File Information Data Structure for SE7539P1.
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      ***  File Information Data Structure for SE7539AU.
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     IPSDS       SDS
      ***  Program Status Data Structure
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
     ILDA       E DSLDA                         256
      ***  Local data area for database error details
      *
     ISDCUST    E DSSDCUSTPD
      ***  Customer details file
      *
     ISDBANK    E DSSDBANKPD
      ***  Bank Details
      *
     IDSSDY     E DSDSSDY
      ***  Second DS For Access Programs, Long Data Structure
      *
     IDSFDY     E DSDSFDY
      ***  First DS for access programs, short data structure
      *
     ICPYR@$      DS
      ***  Data structure for compilation - Copyright Insertion
     I                                        1  80 CPY@
     I                                        1  20 CPY@$$
      *
      /EJECT
      *****************************************************************
      *                M A I N   P R O C E S S I N G                  *
      *****************************************************************
      *
      ***  Read file for record
      *
     C           *LOVAL    SETLLSECORFL2
     C                     READ SECORFL2                 87
      *
      ***  Do While End of File not reached.
      *
     C           *IN87     DOWEQ*OFF
      *
      ***  If Status is not 'X' (Global Status held on PF/SECORFPD)
      ***  and Ex-Date lower or equal than the Run Date
      *
     C           MASTAT    IFNE 'X'
     C           MACOEX    ANDLEBJRDNB
     C           MACOEX    ANDNE*ZEROS
     C           MASTAT    ANDNE'R'                                       135689
      *
     C                     ADD  1         RCOUNT
      *
      ***  Access first record on LF/SECODRL0 with same Corporate Action Reference
      *
     C           MACORF    SETLLSECODRL0
     C           MACORF    READESECODRL0                 88
      *
     C                     MOVE 'Y'       WREFER  1
      *
     C           *IN88     IFEQ '1'
     C                     MOVE 'N'       WDEPOT  1
     C                     EXSR #REPD1
     C                     ENDIF
      *
     C           *IN88     DOWEQ*OFF
      *
     C                     MOVE 'Y'       WDEPOT
     C                     EXSR #REPD1
     C                     MOVE 'N'       WREFER
      *
     C           MACORF    READESECODRL0                 88
     C                     ENDDO
      *
     C                     ENDIF
      *
      ***  Read next record.
      *
     C                     READ SECORFL2                 87
     C                     ENDDO
      *
      ***  If RCOUNT >< 0, Then Print Trailer
      *
     C           RCOUNT    IFNE 0
     C           WWOPEN    ANDEQ'Y'
     C                     WRITETRAILP1
     C                     ENDIF
      *
      ***  Close Output file
      *
     C                     CLOSESE7539P1
      *
      ***  Perform audit report
      *
     C                     EXSR AUDIT
      *
      ***  Normal End of Program
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
      *****************************************************************
      *         E N D   O F   M A I N   P R O C E S S I N G           *
      *****************************************************************
      /EJECT
      *****************************************************************
      *  #REPD1 - Process Report Lines.                               *
      *  ------                                                       *
      *  Called By : Main processing                                  *
      *  Calls     :                                                  *
      *****************************************************************
      *
     C           #REPD1    BEGSR
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C           WWOPEN    IFNE 'Y'
     C                     EXSR RCFP1
     C                     ENDIF
      *
     C                     Z-ADD3         RQDLN1           Check Overflow
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C           WFIRST    OREQ 'Y'
     C                     WRITEHEADP1
     C                     MOVE 'N'       WFIRST
     C                     MOVE 'Y'       WREFER
     C                     ENDIF
      *
     C           WREFER    IFEQ 'Y'
      *
      ***  Report Details
      *
     C                     MOVE MACORF    RCORF            Reference
     C                     MOVE MASESN    RSESN            Security Shortname
     C                     MOVE MACOAT    RCOAT            Action Type
      *
     C                     SELEC                           Status Narrative
     C           MASTAT    WHEQ 'I'
      ***  Input
     C                     MOVEL#SNAR,1   RSTAT
     C           MASTAT    WHEQ 'L'
      ***  Allocated
     C                     MOVEL#SNAR,2   RSTAT
     C           MASTAT    WHEQ 'S'
      ***  Stock Auth.
     C                     MOVEL#SNAR,3   RSTAT
     C           MASTAT    WHEQ 'X'
      ***  Authorised
     C                     MOVEL#SNAR,4   RSTAT
     C           MASTAT    WHEQ 'C'
      ***  Complete
     C                     MOVEL#SNAR,5   RSTAT
     C           MASTAT    WHEQ 'R'
      ***  Reverse
     C                     MOVEL#SNAR,6   RSTAT
     C                     OTHER
     C                     MOVE *BLANKS   RSTAT
     C                     ENDSL
      *
     C           MACOEX    IFNE *ZEROS
     C                     MOVE MACOEX    ZDAYNO           Ex-Date
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RCOEX            DDMMMYY
     C                     ELSE
     C                     MOVE *BLANKS   RCOEX            DDMMMYY
     C                     ENDIF
      *
     C           MATDDT    IFNE *ZEROS
     C                     MOVE MATDDT    ZDAYNO           Trade Date
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RTDDT            DDMMMYY
     C                     ELSE
     C                     MOVE *BLANKS   RTDDT            DDMMMYY
     C                     ENDIF
      *
     C           MAALEF    IFNE *ZEROS
     C                     MOVE MAALEF    ZDAYNO           Allocation Effective Date
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RALEF            DDMMMYY
     C                     ELSE
     C                     MOVE *BLANKS   RALEF            DDMMMYY
     C                     ENDIF
      *
     C           MACOPD    IFNE *ZEROS
     C                     MOVE MACOPD    ZDAYNO           Payment Date
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RCOPD            DDMMMYY
     C                     ELSE
     C                     MOVE *BLANKS   RCOPD            DDMMMYY
     C                     ENDIF
      *
     C           MAEVDT    IFNE *ZEROS
     C                     MOVE MAEVDT    ZDAYNO           Event Date
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    REVDT            DDMMMYY
     C                     ELSE
     C                     MOVE *BLANKS   REVDT            DDMMMYY
     C                     ENDIF
      *
     C                     ELSE
     C                     MOVE *BLANKS   RCORF            Reference
     C                     MOVE *BLANKS   RSESN            Security Shortname
     C                     MOVE *BLANKS   RCOAT            Action Type
     C                     MOVE *BLANKS   RSTAT            Status
     C                     MOVE *BLANKS   RCOEX            Ex-Date
     C                     MOVE *BLANKS   RTDDT            Trade Date
     C                     MOVE *BLANKS   RALEF            Allocation Effective Date
     C                     MOVE *BLANKS   RCOPD            Payment Date
     C                     MOVE *BLANKS   REVDT            Event Date
     C                     ENDIF
      *
     C           WDEPOT    IFEQ 'Y'
      *
      ***  If Depot is not equal to old Depot
      ***  Reset Depot printer fields and retrieve the Depot Shortname
      *
     C           MSDDPT    IFNE WWDDPT
     C**********           Z-ADDMSDDPT    WWDDPT                                              CSD027
     C                     MOVE MSDDPT    WWDDPT                                              CSD027
     C                     MOVELMSDDPT    WLCUST
      *
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   WLRTCD  7
     C                     PARM '*KEY  '  WLOPTN  7
     C                     PARM           WLCUST 10
     C                     PARM *BLANKS   WLKYST  7
     C           SDCUST    PARM SDCUST    DSSDY
      *
      ***  If record not found
      *
     C           WLRTCD    IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE            ***************
     C                     MOVE 'SDCUSTPD'DBFILE           * DB ERROR 01 *
     C                     MOVELWWDDPT    DBKEY     P      ***************
     C                     MOVEL'SE7539'  DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELBBCSSN    RDDPT            Depot Shortname
     C                     ENDIF
      *
     C           MSCOAD    IFNE *ZEROS
     C                     MOVE MSCOAD    ZDAYNO           Last Allocation Date
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RCOAD            DDMMMYY
     C                     ELSE
     C                     MOVE *BLANKS   RCOAD            DDMMMYY
     C                     ENDIF
      *
     C                     SELEC                           Status Narrative
     C           MSCOST    WHEQ 'I'
      ***  Input
     C                     MOVEL#SNAR,1   RCOST
     C           MSCOST    WHEQ 'L'
      ***  Allocated
     C                     MOVEL#SNAR,2   RCOST
     C           MSCOST    WHEQ 'S'
      ***  Stock Auth.
     C                     MOVEL#SNAR,3   RCOST
     C           MSCOST    WHEQ 'X'
      ***  Authorised
     C                     MOVEL#SNAR,4   RCOST
     C           MSCOST    WHEQ 'C'
      ***  Complete
     C                     MOVEL#SNAR,5   RCOST
     C           MSCOST    WHEQ 'R'
      ***  Reverse
     C                     MOVEL#SNAR,6   RCOST
     C                     OTHER
     C                     MOVE *BLANKS   RCOST
     C                     ENDSL
      *
     C                     ELSE
     C                     MOVE *BLANKS   RDDPT            Depot Shortname
     C                     MOVE *BLANKS   RCOAD            Last Alloction Date
     C                     MOVE *BLANKS   RCOST            Depot Status
     C                     ENDIF
      *
      ***  Write out Detail Record.
      *
     C                     WRITEDETAIL1
      *
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      *  AUDIT  - Subroutine to Output Audit report and End Program.  *
      *  ------                                                       *
      *  Called By : Main Processing, *PSSR                           *
      *  Calls     : None.                                            *
      *****************************************************************
      *
     C           AUDIT     BEGSR                           *** AUDIT  ***
      *
      ***  Output Report Header and File Controls.
      *
     C                     WRITEHEADAU
      *
      ***  If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
     C                     WRITEDBERROR
     C                     ELSE
      *
      ***  Or, if no records read, Output "No Details" message.
      *
     C           RCOUNT    IFEQ 0
     C                     WRITENODTLS
     C                     ELSE
      *
      ***  Normal audit  exit
      *
     C                     WRITECONTROL
     C                     ENDIF
      *
     C                     ENDIF                           U7 = *ON
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *  ------                                                       *
      *****************************************************************
      *
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      ***  Open Report file
      *
     C                     CLOSESE7539P1
      *
     C                     OPEN SE7539P1
     C                     MOVE 'Y'       WWOPEN
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           PSEQ    5
     C                     PARM PENT      SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE            ***************
     C                     MOVE 'SE7539P1'DBFILE           * DB ERROR 02 *
     C           'ZSFILE R'CAT  'OUTINE'  DBKEY     P      ***************
     C                     MOVEL'SE7539'  DBPGM     P
     C                     OUT  LDA
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *  ------                                                       *
      *****************************************************************
      *
     C           RCFAU     BEGSR                            *** RCFAU  ***
      *
      ***  Open printer File
      *
     C                     CLOSESE7539AU
     C                     MOVE 'N'       WWRCF            Audit under RCF
     C                     OPEN SE7539AU
      *
      ***  Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           PSEQ
     C                     PARM PENT      SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     Z-ADD3         DBASE            ***************
     C                     MOVE 'SE7539AU'DBFILE           * DB ERROR 03 *
     C           'ZSFILE R'CAT  'OUTINE'  DBKEY     P      ***************
     C                     MOVEL'SE7539'  DBPGM     P
     C                     OUT  LDA
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     MOVE 'Y'       WWRCF   1        Audit under RCF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *   *INZSR - Initial Processing                                 *
      *   ------                                                      *
      *   Called by : Main processing                                 *
      *   Calls     : None.                                           *
      *****************************************************************
     C           *INZSR    BEGSR
      *
     C                     EXSR RCFAU
      *
     C           *ENTRY    PLIST
     C                     PARM           PSEQ    5
     C                     PARM           PENT    3
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     Z-ADD0         DBASE
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     MOVEL'SE7539'  DBPGM     P
     C                     OUT  LDA
      *
      ***  Access Bank details file
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '       ' WWRTCD  7
     C                     PARM '*FIRST ' WWOPTN  7
     C           SDBANK    PARM SDBANK    DSSDY
      *
     C           WWRTCD    IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD4         DBASE            ***************
     C                     MOVEL'SDBANKPD'DBFILE    P      * DB ERROR 04 *
     C                     MOVEL'*FIRST'  DBKEY     P      ***************
     C                     MOVEL'SE7539'  DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ***  Set indicator 98 for date format used in ZDATE2
      *
     C           BJDFIN    IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ENDIF
      *
      ***  Report Work fields.
      *
     C                     Z-ADD0         RCOUNT
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
     C                     MOVE 'N'       WWOPEN  1
     C**********           Z-ADD*ZEROS    WWDDPT  60                                          CSD027
     C                     MOVE *BLANKS   WWDDPT  6                                           CSD027
     C                     MOVE 'Y'       WFIRST  1
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *  *PSSR  - Error control subroutine                            *
      *  ------                                                       *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
     C                     SETON                     U7U8
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     DUMP
     C                     ENDIF
      *
     C           WWRCF     IFEQ 'Y'
     C                     MOVE 'Y'       WWOPEN  1        To avoid RCFAU in AUDIT
     C                     EXSR AUDIT
     C                     ENDIF
      *
      ***  If *PSSR already run, then just end the program here.
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
     C/COPY ZSRSRC,ZDATE2Z2
     C/COPY ZSRSRC,ZALIGNZ2
     C/COPY ZSRSRC,ZEDITZ2
     C/COPY ZSRSRC,ZDATE2Z3
**  #SNAR
Input
Allocated
Stock Auth.
Authorised
Complete
Reverse
**  CPY@
(c) Finastra International Limited 2001
