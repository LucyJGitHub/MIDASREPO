     H        1
      *****************************************************************
/*OVRF*: OVRDBF FILE(SECTYZ1) TOFILE(SECTY) SHARE(*NO)              : *
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Corporate actions - reply handling hist')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7541  - SE Corporate Actions - Reply Handling History Enq. *
      *                                                               *
      *  Called By: SE7514 - Reply Handling Selection                 *
      *             SE7515 - Reply Handling Maintenance               *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CSE071             Date 19Jul05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 161732             Date 21Mar00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAC005             Date 23Nov98               *
      *                 CSE007  *CREATE    Date 01Dec97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  161732 - Corporate Actions Exercise and Conversion -         *
      *           Part Stock/Keep Rest Calculation                    *
      *           (Recompiled due to changes in DSPF/SE7541DF)        *
      *  CAC005 - Profit Centre Accounting - SE Enhancement           *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
      /EJECT
      *
      **   Midas SE Securities - Details
     FSECTYZ1 IF  E           K        DISK
     F                                              KINFSR *PSSR
      *
      **   Midas Corporate Actions -Reference Retrieve index
     FSECORFL1IF  E           K        DISK         KINFSR *PSSR
      *
      **   Midas Corporate Actions -Reply Handling History
     FSECORHL1IF  E           K        DISK         KINFSR *PSSR
      *
      **   Display file
     FSE7541DFCF  E                    WORKSTN      KINFSR *PSSR
     F                                        #1RRN KSFILE SE7541S1
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *                   Function of Indicators                      *
      *                  ------------------------                     *
      *                                                               *
      *  30 - Chain fail on PF/SECORHPD.                              *
      *  31 - Chain fail on PF/SECORFPD.                              *
      *  40 - Display Profit Centre                                   *   CAC005
      *                                                               *
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  *PSSR  - Program Error Handling.                             *
      *  P001   - Initial Processing.                                 *
      *  P002   - Build the Subfile.                                  *
      *                                                               *
      *****************************************************************
      /EJECT
      ********************************************************************
      *
      ** Array for copyright.
      *
     E                    CPY@    1   1 80
      *
      ** Text messages for command keys.
     E                    TXT     1   1 78
      *
     E/COPY ZSRSRC,ZEDITZ1
      *
     E/COPY ZSRSRC,ZDATE2Z1
      *
     E/COPY ZSRSRC,ZSEDITZ1
      *
      /EJECT
      *****************************************************************
      *
      **   Prpgram status data structure
     IPSDS       SDS
     I                                     *PROGRAM ##PGM
     I                                      244 253 JOB
     I                                      254 263 USER
      **   Local data area
     ILDA         DS                            256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      *
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *
     IRUNDAT    E DS
      **  Standard Data Area Layout for System flags and Run Date
      *
     ISDBANK    E DSSDBANKPD
      **  Bank Details
      *
     ISDCUST    E DSSDCUSTPD
      **  Customer Details
      *
     IDSSDY     E DSDSSDY
      ** Second DS for access programs, long data structure
      *
     I/COPY ZSRSRC,ZSEDITZ2
      *
      /EJECT
      *****************************************************************
      * Main Processing
      *****************************************************************
      *
      ** Entry parameters
      *
     C           *ENTRY    PLIST
     C                     PARM           WWCARF  6        CA Reference
     C                     PARM           WWBOOK  2        Book Code
     C                     PARM           WWCUSN  6        Customer Numberame
     C                     PARM           WWBRCH  3        Branch
     C                     PARM           WWPORT  4        Portfolio Reference
     C                     PARM           WWDEPO 10        Depot
     C                     PARM           WWRTCD  1        Return Code
      *
     C           WWCUSN    IFEQ '000000'
     C           WWCUSN    OREQ *BLANKS
     C                     MOVE '1'       *IN60
     C                     MOVE *BLANKS   WWCUSN
     C                     ENDIF
      *
      ** Copyright statement
      *
     C                     MOVEACPY@      MKI@   80
      *
      ** Set 'First Time' flag to 'Y'
      *
     C                     MOVE 'Y'       FIRST   1
      *
      ** Initial processing
      *
     C                     EXSR P001
      *
      ** Clear messages from program message quque.
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
      *
      ** Do until F3 or F12
      *
     C           *INKC     DOUEQ*ON
     C           *INKL     OREQ *ON
      *
      ** If 'FIRST TIME' = 'Y'
      *
     C           FIRST     IFEQ 'Y'                         B*2
      *
      ** Build the subfile
      *
     C                     EXSR P002
      *
      ** Set first time flag to 'N'
     C                     MOVE 'N'       FIRST
      *
      ** Write SE7541DF on screen
      *
     C                     Z-ADD1         #1RRN
      *
     C                     MOVE TXT,1     ##CTX1
     C                     ENDIF
      *
     C                     TIME           ##TME   60       Screen time.
      *
     C                     WRITESE7541C2
     C                     WRITESE7541C1
     C                     EXFMTSE7541F1
      *
      ** Clear messages from program message quque.
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
      *
     C                     ENDDO
      *
     C           *INKC     IFEQ '1'
     C                     MOVE '3'       WWRTCD           Return Code
     C                     ENDIF
      *
     C           *INKL     IFEQ '1'
     C                     MOVE '2'       WWRTCD           Return Code
     C                     ENDIF
      *
      ** Return to calling program
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            P001   - Initial Processing.                       *
      *                                                               *
      * CALLS      *PSSR  - Program error handling routine.           *
      *            AOBANKR0 - Bank ICD Access Object.                 *
      *            AOCUSTR0 - Client Details Access Object.           *
      *            ZDATE2 - To convert a day number to day formats.   *
      *                                                               *
      * CALLED BY  MAIN   - Main Processing.                          *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           P001      BEGSR
      *
      ** Initialise key and work fields
      *
     C                     MOVEL'SE7541  'DBPGM
      *
      ** Setup screen time
      *
     C                     TIME           ##TME   60       Screen time.
      *
      ** System rundate
      *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
      *
      ** Local Data Area
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** If first time program called
      *
     C           FIRST     IFEQ 'Y'
      *
      ** Retrieve bank details via AOBANKR0
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST'  @OPTN   7
     C           SDBANK    PARM SDBANK    DSSDY
      *
      ** If record not found in SDBANKL1
      *
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE
     C                     MOVE 'SDBANKPD'DBFILE
     C                     MOVEL*BLANKS   DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
      *
     C                     ELSE
     C                     MOVELUSER      ##USR
     C                     MOVELJOB       ##JOB
     C                     MOVE BJMRDT    ##MRDT
     C                     ENDIF
      *                                                                   CAC005
      ** Access Switchable feature files for CAC005                       CAC005
      *                                                                   CAC005
     C                     CALL 'AOSARDR0'                                CAC005
     C                     PARM *BLANKS   PRTCD   7                       CAC005
     C                     PARM '*VERIFY' POPTN   7                       CAC005
     C                     PARM 'CAC005'  PSARD   6                       CAC005
      *                                                                   CAC005
     C           PRTCD     IFEQ *BLANK                                    CAC005
     C                     MOVE 'Y'       CAC005  1                       CAC005
     C                     MOVE *ON       *IN40                           CAC005
     C                     ELSE                                           CAC005
     C                     MOVE 'N'       CAC005                          CAC005
     C                     MOVE *OFF      *IN40                           CAC005
     C                     ENDIF                                          CAC005
      *
      ** Retrieve Customer shortname via AOCUSTR0.
      *
     C           WWCUSN    IFNE *BLANKS
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM WWCUSN    @KY10  10
     C                     PARM *BLANKS   @KYST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE
     C                     MOVEL'SDCUSTPD'DBFILE
     C                     MOVELWWCUSN    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
      *
      ** Move customer shortname to screen field.
      *
     C                     ELSE
     C                     MOVELBBCSSN    DDCUST
     C                     ENDIF
     C                     ELSE
     C                     MOVEL*BLANKS   DDCUST
     C                     ENDIF
      *
      ** Setup Screen fields
      *
     C                     MOVE WWBRCH    DDBRCH
     C                     MOVE WWBOOK    DDBOOK
     C                     MOVE WWCARF    DDREFN
      *                                                                   CAC005
      ** If CAC005 is installed, retrieve the corresponding user book     CAC005
      ** and profit centre. WWBOOK from the incoming parameters is        CAC005
      ** being held as pseudo book.                                       CAC005
      *                                                                   CAC005
     C           CAC005    IFEQ 'Y'                                       CAC005
     C                     CALL 'AOBKPCR0'                                CAC005
     C                     PARM *BLANKS   PRTCD                           CAC005
     C                     PARM '*USER  ' POPTN                           CAC005
     C                     PARM           PBKCD   2                       CAC005
     C                     PARM           PPRFC   4                       CAC005
     C                     PARM WWBOOK    PPSBK   2                       CAC005
      *                                                                   CAC005
     C           PRTCD     IFEQ *BLANKS                                   CAC005
     C                     MOVELPBKCD     DDBOOK                          CAC005
     C                     MOVELPPRFC     SPRFC                           CAC005
     C                     ELSE                                           CAC005
     C                     MOVE *BLANKS   SPRFC                           CAC005
     C                     ENDIF                                          CAC005
     C                     ENDIF                                          CAC005
      *
     C           WWCARF    CHAINSECORFL1             31
      *
     C           *IN31     IFEQ *OFF
     C                     MOVELMASESN    DDSECU
     C                     MOVE MACOAT    DDACTP
      *
     C                     MOVE MACOEX    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     DDEXDT
     C                     ENDIF
      *
      ** Retrieve Nominal Currency and Nominal Currency dec. places.
      *
     C           DDSECU    CHAINSECTYZ1              31
      *
      ** No record found
     C           *IN31     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVEL'003'     DBASE            ***************
     C                     MOVE 'SECTYD  'DBFILE           * DB ERROR 03 *
     C                     MOVELDDSECU    DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ELSE
     C                     MOVE NMCY      DDCCY
     C                     Z-ADDNMDP      WNMDP   20
     C                     ENDIF
      *
     C                     MOVEL'*'       TOPQ   10
     C                     MOVEL'*PRV'    TOPRQ   5
     C                     MOVEL'SEUSRMSG'PMSGF  10
      *
     C                     MOVEL'SE7541'  ZAPGM  10        Message queue
     C                     MOVEL'SEUSRMSG'ZAMSGF 10        Message file
     C                     MOVEL*BLANK    ZAPGRL  5        Rel queue
     C                     MOVEL*BLANK    ZAMSID  7        Message Id.
     C                     MOVEL*BLANK    ZAMSDA132        Message data.
     C                     MOVEL*BLANK    ZAMSTP  7        Message type.
      *
     C                     MOVE *OFF      *IN98
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            P002   - Build the subfile.                        *
      *                                                               *
      * CALLS      ZDATE2 - To convert a day number to date formats.  *
      *            ZEDIT  - To insert a decimal point into a numeric  *
      *                     field and blank out leading zeros.        *
      *                                                               *
      * CALLED BY  MAIN   - Main processing.                          *
      *                                                               *
      *****************************************************************
     C           P002      BEGSR
      *
      ** Clear subfile
      *
     C                     MOVE *ON       *IN80
     C                     WRITESE7541C1
     C                     MOVE *OFF      *IN80
     C                     MOVE *OFF      *IN81
      *
     C                     Z-ADD0         #1RRN   40
      *
      ** Read record from LF/SECORHL1 using Corporate Action reference,
      ** Customer number and Portfolio reference.
      *
     C           WWCARF    SETLLSECORHL1
     C           WWCARF    READESECORHL1                 30
      *
      ** Do while records read from LF/SECORHL1
      *
     C           *IN30     DOWEQ*OFF
      *
      ** Check if record read has the same Customer number
      *
     C                     MOVE MPCNUM    MPCNUA  6
      *
     C           MPCNUA    IFEQ WWCUSN
     C           WWCUSN    ANDNE*BLANKS
      *
     C           MPBOOK    OREQ WWBOOK
     C           WWBOOK    ANDNE*BLANKS
      *
      ** Move file fields into subfile fields.
      *
      ** Fomat Reply date
      *
     C                     MOVE MPRHDT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    DDRDAT
      *
      ** Reply time
      *
     C                     MOVE MPRHTI    DDRTIM
      *
      ** Reply option
      *
     C           MPRHLV    IFEQ '1'
     C                     MOVE MPRHRE    DDRLV1
     C                     ELSE
     C                     MOVE *BLANKS   DDRLV1
     C                     ENDIF
      *
      ** Reply customer
      *
     C           MPRHLV    IFEQ '2'
     C                     MOVE MPRHRE    DDRLV2
     C                     ELSE
     C                     MOVE *BLANKS   DDRLV2
     C                     ENDIF
      *
      ** Reply Method
      *
     C                     MOVELMPRHNA    DDRMET
      *
      ** Cash %
      *
     C           MPRHCA    IFNE *ZEROS
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE MPRHCA    ZFIELD
     C                     Z-ADD2         ZADEC
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    DDRCAS
     C                     ELSE
     C                     MOVE *BLANKS   DDRCAS
     C                     ENDIF
      *
      ** Nominal to be taken as cash
      *
     C           MPRHCN    IFNE *ZEROS
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE MPRHCN    ZFLD15
     C                     Z-ADDWNMDP     ZDECS
     C                     MOVE 'L'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE *BLANKS   DDNOMI
     C                     MOVE ZOUT21    WFLD17 17
     C                     MOVELWFLD17    DDNOMI
     C                     ELSE
     C                     MOVE *BLANKS   DDNOMI
     C                     ENDIF
      *
      ** Reply Narrative
      *
     C                     MOVE *BLANKS   DDRNA1
     C                     MOVE *BLANKS   DDRNA2
     C                     MOVE *BLANKS   DDRNA3
     C                     MOVELMPRHN1    DDRNA1
     C                     MOVELMPRHN2    DDRNA2
     C                     MOVELMPRHN3    DDRNA3
      *
     C                     ADD  1         #1RRN      81
     C                     WRITESE7541S1
     C                     ENDIF
      *
      ** Read next record
      *
     C           WWCARF    READESECORHL1                 30
      *
     C                     ENDDO
      *
     C           #1RRN     IFEQ 0
     C                     ADD  1         #1RRN
     C                     WRITESE7541S1
     C                     MOVEL'CO00001' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            *PSSR  - Error Processing.                         *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY  P001   - Initial Processing.                       *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR
      *
     C                     DUMP
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * ZASNMS   - Send message to program message queue              *
      *                                                               *
      * CALLS    Y2SNMSGC                                             *
      *                                                               *
      * CALLED BY                                                     *
      *                                                               *
      *****************************************************************
     C           ZASNMS    BEGSR
      *
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGM            Program queue
     C                     PARM           ZAPGRL           Rel queue
     C                     PARM           ZAMSID           Message Id.
     C                     PARM           ZAMSGF           Message file.
     C                     PARM           ZAMSDA           Message data.
     C                     PARM           ZAMSTP           Message type.
      *
      ** Clear all fields for default mechanism next time.
     C                     MOVEL*BLANK    ZAPGM            Message queue
     C                     MOVEL*BLANK    ZAPGRL           Rel queue
     C                     MOVEL*BLANK    ZAMSDA           Message data.
     C                     MOVEL*BLANK    ZAMSTP           Message type.
      *
     C           ZASNM9    ENDSR                           ZASNM9 TAG
      *
      ********************************************************************
      /EJECT
      ********************************************************************
      *
     C/COPY ZSRSRC,ZEDITZ2
      *
     C/COPY ZSRSRC,ZDATE2Z4
      *
     C/COPY ZSRSRC,ZSEDITZ3
      **
**  CPY@
(c) Finastra International Limited 2001
**
F3=Exit  F5=Refresh Screen  F12=Previous Screen  Rollup/Down
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
