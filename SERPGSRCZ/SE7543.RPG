     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Orders to be executed - Report')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7543 - Orders to be Executed - Report                      *
      *                                                               *
      * Function:  To print the Orders to be Executed report          *
      * (COB - I/C)                                                   *
      *                                                               *
      *  Called By: SEC7542 - Orders to be Executed                   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001             Date 07Sep99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSE007  *CREATE    Date 02Apr98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CPB001 - 'Private Banking' Module                            *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
     FSECOOEL0IF  E           K        DISK         KINFSR *PSSR
      ***  CO Orders to be executed                          Prefix - 'MY'
     F            SECOOED0                          KRENAMESECOOER0
      *
     FSECOOEL1IF  E           K        DISK         KINFSR *PSSR
      ***  CO Orders to be executed                          Prefix - 'MY'
     F            SECOOED0                          KRENAMESECOOER1
      *
     FSE7543AUO   E                    PRINTER      KINFSR *PSSR      UC
      ***  Orders to be executed - Audit
     F                                              KINFDS SPOOLU
      *
     FSE7543P1O   E                    PRINTER      KINFSR *PSSR      UC
      ***  Orders to be Executed - Report
     F                                              KINFDS SPOOL1
      *
      *****************************************************************
      /EJECT
      *****************************************************************
     E/COPY ZSRSRC,ZALIGNZ1
     E/COPY ZSRSRC,ZDATE2Z1
      *
     E                    CPY@    1   1 80
      ***  Array containing Copyright statement
      *
      *****************************************************************
      /SPACE 3
      *****************************************************************
     ISPOOL1      DS
      ***  File Information Data Structure for SE7543P1.
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      ***  File Information Data Structure for SE7543AU.
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     IPSDS       SDS
      ***  Program Status Data Structure
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
     ILDA       E DSLDA                         256
      ***  Local data area for database error details
      *
     ISDACOF    E DSSDACOFPD
      ***  Account Officer details file
      *
     ISDCUST    E DSSDCUSTPD
      ***  Customer details file
      *
     ISDBANK    E DSSDBANKPD
      ***  Bank Details
      *
     ISDPORT    E DSSDPORTPD
      ***  Portfolio ICD
      *
     ISDMMOD    E DSSDMMODPD
      ***  Dummy record format for access to Midas Module
      *
     IDSSDY     E DSDSSDY
      ***  Second DS For Access Programs, Long Data Structure
      *
     IDSFDY     E DSDSFDY
      ***  First DS for access programs, short data structure
      *
     ICPYR@$      DS
      ***  Data structure for compilation - Copyright Insertion
     I                                        1  80 CPY@
     I                                        1  20 CPY@$$
      *****************************************************************
      /EJECT
      *****************************************************************
      *                M A I N   P R O C E S S I N G                  *
      *****************************************************************
      *
      ***  Check the parameter entered from the RCF
      ***  (On System Level or on Account Officer Level)
      *
      *
      ***  If System level requested, than report by security
      *
     C           PLEV      IFEQ 'S'
     C                     MOVE *OFF      *IN61
     C                     EXSR #REPSY
      *
      ***  If Account Officer Level requested, than report by Account Officer
      *
     C                     ELSE
     C                     MOVE *ON       *IN61
     C                     EXSR #REPAC
     C                     ENDIF
      *
      ***  If RCOUNT >< 0, Then Print Trailer
      *
     C           RCOUNT    IFNE 0
     C           WWOPEN    ANDEQ'Y'
     C                     WRITETRAILP1
     C                     ENDIF
      *
      ***  Close Output file
      *
     C                     CLOSESE7543P1
      *
      ***  Perform audit report
      *
     C                     EXSR AUDIT
      *
      ***  Normal End of Program
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
      *****************************************************************
      *         E N D   O F   M A I N   P R O C E S S I N G           *
      *****************************************************************
      /EJECT
      *****************************************************************
      *  #REPAC - Report by Account officer on Account Officer level  *
      *  ------                                                       *
      *  Called By : Main processing                                  *
      *  Calls     :                                                  *
      *****************************************************************
      *
     C           #REPAC    BEGSR
      *
      ***  Read file for record
      *
     C                     READ SECOOEL1                 88
      *
      ***  Do While End of File not reached.
      *
     C           *IN88     DOWEQ*OFF
      *
     C                     EXSR #REPD1
      *
      ***  Read next record.
      *
     C                     READ SECOOEL1                 88
     C                     ENDDO                           EOF not reached
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *  #REPSY - Report by Security on System level                  *
      *  ------                                                       *
      *  Called By : Main processing                                  *
      *  Calls     :                                                  *
      *****************************************************************
      *
     C           #REPSY    BEGSR
      *
      ***  Read file for record
      *
     C                     READ SECOOEL0                 88
      *
      ***  Do While End of File not reached.
      *
     C           *IN88     DOWEQ*OFF
      *
     C                     EXSR #REPD1
      *
      ***  Read next record.
      *
     C                     READ SECOOEL0                 88
     C                     ENDDO                           EOF not reached
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *  #REPD1 - Process Report Lines.                               *
      *  ------                                                       *
      *  Called By : Main processing                                  *
      *  Calls     :                                                  *
      *****************************************************************
      *
     C           #REPD1    BEGSR
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C           WWOPEN    IFNE 'Y'
     C                     EXSR RCFP1
     C                     ENDIF
      *
      ***  If Account officer is not equal to old Account officer
      ***  Reset Account officer printer fields and retrieve hte Account Officer name
      *
     C           PLEV      IFNE 'S'
     C           MYACOF    IFNE WWACOF
     C           WWFIRT    OREQ 'Y'
     C                     MOVE 'N'       WWFIRT
     C                     MOVE MYACOF    RACOF
     C                     MOVE MYACOF    WWACOF
      *
      ***  Access to Account officer file to retrieve the account officer name
      *
     C                     CALL 'AOACOFR0'
     C                     PARM *BLANKS   WLRTCD  7
     C                     PARM '*KEY   ' WLOPTN  7
     C                     PARM WWACOF    WLACOF  2
     C           SDACOF    PARM SDACOF    DSSDY
      *
     C           WLRTCD    IFEQ *BLANKS
     C                     MOVE AZACON    RACON
     C                     ELSE
     C                     MOVE *BLANKS   RACON
     C                     ENDIF
     C                     WRITEHEADP1                     Force new page
     C                     MOVE *BLANKS   RSESN
     C                     MOVE *BLANKS   RCNUM
     C                     ENDIF
     C                     ENDIF
      *
      ***  If Depot is not equal to old Depot
      ***  Reset Depot printer fields and retrieve the Depot Shortname
      *
     C           MYDDPT    IFNE WWDDPT
     C**********           Z-ADDMYDDPT    WWDDPT                                              CSD027
     C                     MOVE MYDDPT    WWDDPT                                              CSD027
     C                     MOVELMYDDPT    WLCUST
      *
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   WLRTCD
     C                     PARM '*KEY  '  WLOPTN
     C                     PARM           WLCUST 10
     C                     PARM *BLANKS   WLKYST  7
     C           SDCUST    PARM SDCUST    DSSDY
      *
      ***  If record not found
      *
     C           WLRTCD    IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE            ***************
     C                     MOVE 'SDCUSTPD'DBFILE           * DB ERROR 01 *
     C                     MOVELWWDDPT    DBKEY     P      ***************
     C                     MOVEL'SE7543'  DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELBBCSSN    RDDPT            Depot Shortname
     C                     ENDIF
      *
     C                     Z-ADD3         RQDLN1           Check Overflow
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     MOVE *BLANKS   RSESN
     C                     MOVE *BLANKS   RCNUM
     C                     ENDIF
      *
      ***  Report Details
      *
     C           MYSESN    IFNE WWSESN
     C                     MOVELMYSESN    RSESN     P      Security
     C                     MOVELMYSESN    WWSESN    P
     C                     ELSE
     C                     MOVE *BLANKS   RSESN
     C                     ENDIF
      *
     C           MYCNUM    IFNE WWCNUM
     C**********           Z-ADDMYCNUM    WWCNUM                                              CSD027
     C                     MOVE MYCNUM    WWCNUM                                              CSD027
     C                     MOVELMYCNUM    WLCUST
      *
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   WLRTCD
     C                     PARM '*KEY  '  WLOPTN
     C                     PARM           WLCUST 10
     C                     PARM *BLANKS   WLKYST  7
     C           SDCUST    PARM SDCUST    DSSDY
      *
      ***  If record not found
      *
     C           WLRTCD    IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE            ***************
     C                     MOVE 'SDCUSTPD'DBFILE           * DB ERROR 02 *
     C                     MOVELWWCNUM    DBKEY     P      ***************
     C                     MOVEL'SE7543'  DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELBBCSSN    RCNUM            Customer Shortname
     C                     ELSE
     C                     MOVE *BLANKS   RCNUM
     C                     ENDIF
      *
      ***  If Portfolio Management is ON
      ***  or Private Banking module is ON                                CPB001
      *
     C           BGPFMG    IFEQ 'Y'
     C           BGN4ST    OREQ 'Y'                                       CPB001
      *
      ***  If Portfolio is 9997, Setup with default portfolio
      ***  default portfolio if private banking is ON                     CPB001
      *
     C           MYPTFR    IFEQ '9997'
     C           BGN4ST    ANDNE'Y'                                       CPB001
     C                     MOVE FCR997    RPTFR            Portfolio
     C                     ELSE
     C                     MOVE MYPTFR    RPTFR            Portfolio
     C                     ENDIF
      *
     C                     ELSE                            Portfolio OFF
     C                     MOVE *BLANKS   RPTFR
     C                     ENDIF
      *
     C                     MOVE MYOPNO    ROPT2            Option Number
     C                     MOVELMYOPNA    ROPNA     P      Option Narrative
     C                     MOVELMYCORF    RCORF            Corporate Reference
     C                     MOVELMYCOAT    RCOAT            Action Type
     C                     MOVE MYCOST    RCOST            Status
      *
     C           MYNOML    IFNE *ZEROS
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE MYNOML    ZFIELD           Nominal
     C                     MOVE MYNMDP    ZADEC
     C                     EXSR ZEDIT
     C                     MOVE *BLANKS   RNOML
     C                     MOVE ZFIELD    RNOML
     C                     ELSE
     C                     MOVE *BLANKS   RNOML
     C                     ENDIF
      *
     C           MYTRAF    IFNE *ZEROS
     C                     MOVE MYTRAF    ZDAYNO           Trading from date
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RTRAF            DDMMMYY
     C                     ELSE
     C                     MOVE *BLANKS   RTRAF            DDMMMYY
     C                     ENDIF
      *
     C           MYTRAT    IFNE *ZEROS
     C                     MOVE MYTRAT    ZDAYNO           Traiding to Date
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RTRAT            DDMMMYY
     C                     ELSE
     C                     MOVE *BLANKS   RTRAT
     C                     ENDIF
      *
      ***  Write out Detail Record.
      *
     C                     WRITEDETAIL1
     C                     ADD  1         RCOUNT
      *
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      *  AUDIT  - Subroutine to Output Audit report and End Program.  *
      *  ------                                                       *
      *  Called By : Main Processing, *PSSR                           *
      *  Calls     : None.                                            *
      *****************************************************************
      *
     C           AUDIT     BEGSR                           *** AUDIT  ***
      *
     C           WWOPEN    IFNE 'Y'                        WHEN NO RECORD READ
     C                     EXSR RCFAU
     C                     ENDIF
      *
      ***  Output Report Header and File Controls.
      *
     C                     WRITEHEADAU
      *
      ***  If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
     C                     WRITEDBERROR
     C                     ELSE
      *
      ***  Or, if no records read, Output "No Details" message.
      *
     C           RCOUNT    IFEQ 0
     C                     WRITENODTLS
     C                     ELSE
      *
      ***  Normal audit  exit
      *
     C                     WRITECONTROL
     C                     ENDIF
      *
     C                     ENDIF                           U7 = *ON
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *  ------                                                       *
      *****************************************************************
      *
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      ***  Open Report file
      *
     C                     CLOSESE7543P1
      *
     C                     OPEN SE7543P1
     C                     MOVE 'Y'       WWOPEN
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           PSEQ    5
     C                     PARM PENT      SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     Z-ADD3         DBASE            ***************
     C                     MOVE 'SE7543P1'DBFILE           * DB ERROR 03 *
     C           'ZSFILE R'CAT  'OUTINE'  DBKEY     P      ***************
     C                     MOVEL'SE7543'  DBPGM     P
     C                     OUT  LDA
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *  ------                                                       *
      *****************************************************************
      *
     C           RCFAU     BEGSR                            *** RCFAU  ***
      *
      ***  Open printer File
      *
     C                     CLOSESE7543AU
     C                     MOVE 'N'       WWRCF            Audit under RCF
     C                     Z-ADD0         RCOUNT
     C                     OPEN SE7543AU
      *
      ***  Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           PSEQ
     C                     PARM PENT      SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     Z-ADD4         DBASE            ***************
     C                     MOVE 'SE7543AU'DBFILE           * DB ERROR 04 *
     C           'ZSFILE R'CAT  'OUTINE'  DBKEY     P      ***************
     C                     MOVEL'SE7543'  DBPGM     P
     C                     OUT  LDA
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     MOVE 'Y'       WWRCF   1        Audit under RCF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *   *INZSR - Initial Processing                                 *
      *   ------                                                      *
      *   Called by : Main processing                                 *
      *   Calls     : None.                                           *
      *****************************************************************
     C           *INZSR    BEGSR
      *
     C                     EXSR RCFAU
      *
     C           *ENTRY    PLIST
     C                     PARM           PSEQ    5
     C                     PARM           PLEV    1
     C                     PARM           PENT    3
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     Z-ADD0         DBASE
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     MOVEL'SE7543'  DBPGM     P
     C                     OUT  LDA
      *
      ***  Access Bank details file
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '       ' WLRTCD  7
     C                     PARM '*FIRST ' WLOPTN  7
     C           SDBANK    PARM SDBANK    DSSDY
      *
     C           WLRTCD    IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD5         DBASE            ***************
     C                     MOVEL'SDBANKPD'DBFILE    P      * DB ERROR 05 *
     C                     MOVEL'*FIRST'  DBKEY     P      ***************
     C                     MOVEL'SE7543'  DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ***  Set indicator 98 for date format used in ZDATE2
      *
     C           BJDFIN    IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ENDIF
      *
      ***  Access Midas Module Details
      *
     C                     CALL 'AOMMODR0'
     C                     PARM *BLANKS   WLRTCD  7
     C                     PARM '*FIRST  'WLOPTN  7
     C           SDMMOD    PARM SDMMOD    DSFDY
      *
     C           WLRTCD    IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD6         DBASE            ***************
     C                     MOVEL*BLANKS   DBFILE           * DB ERROR 06 *
     C                     MOVE 'SDMMODPD'DBFILE           ***************
     C                     MOVEL'*FIRST  'DBKEY     P
     C                     MOVEL'SE7543'  DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ***  If Portfolio Management is ON
      *
     C           BGPFMG    IFEQ 'Y'
     C                     MOVE *ON       *IN60
      *
      ***  Access Portfolio Management ICD
      *
     C                     CALL 'AOPORTR0'
     C                     PARM *BLANKS   WLRTCD  7
     C                     PARM '*FIRST  'WLOPTN  7
     C           SDPORT    PARM SDPORT    DSFDY
      *
     C           WLRTCD    IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD7         DBASE            ***************
     C                     MOVEL*BLANKS   DBFILE           * DB ERROR 07 *
     C                     MOVE 'SDPORTPD'DBFILE           ***************
     C                     MOVEL'*FIRST  'DBKEY     P
     C                     MOVEL'SE7543'  DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *                                                                   CPB001
      ***  If Private Banking Module is ON                                CPB001
      *                                                                   CPB001
     C           BGN4ST    IFEQ 'Y'                                       CPB001
     C                     MOVE *ON       *IN60                           CPB001
     C                     ENDIF                                          CPB001
      *
      ***  Report Work fields.
      *
     C                     Z-ADD0         RCOUNT
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
     C**********           Z-ADD0         WWCNUM  60                                          CSD027
     C                     MOVE *BLANKS   WWCNUM  6                                           CSD027
     C                     MOVE *BLANKS   WWSESN 10
     C                     MOVE 'N'       WWOPEN  1
     C                     MOVE 'Y'       WWFIRT  1
     C                     MOVE *BLANKS   WWACOF  2
     C**********           Z-ADD*ZEROS    WWDDPT  60                                          CSD027
     C                     MOVE *BLANKS   WWDDPT  6                                           CSD027
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *  *PSSR  - Error control subroutine                            *
      *  ------                                                       *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
     C                     SETON                     U7U8
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     DUMP
     C                     ENDIF
      *
     C           WWRCF     IFEQ 'Y'
     C                     MOVE 'Y'       WWOPEN  1        To avoid RCFAU in AUDIT
     C                     EXSR AUDIT
     C                     ENDIF
      *
      ***  If *PSSR already run, then just end the program here.
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
     C/COPY ZSRSRC,ZDATE2Z2
     C/COPY ZSRSRC,ZALIGNZ2
     C/COPY ZSRSRC,ZEDITZ2
     C/COPY ZSRSRC,ZDATE2Z3
**  CPY@
(c) Finastra International Limited 2001
