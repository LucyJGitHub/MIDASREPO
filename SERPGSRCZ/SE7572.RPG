     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE CO Retrieve General Block Sec. Inds')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7572 - Corporate Actions - Retrieve General Block Security *
      *           Indicators.                                         *
      *                                                               *
      *  Function:  This program retrieve Block Security Indicators.  *
      *  (I/C)                                                        *
      *                                                               *
      *  Called By: SE0050 - Trade Detail Maintenance                 *
      *             SE0070 - Depot Movements Maintenance              *
      *             SE0205 - Client Holdings enquiry                  *
      *             SE0215 - Customer Portfolio Valuation enquiry     *
      *             SE3000 - Bulk Trades Input                        *
      *             SE3002 - Review Trades for Bulk Trade Input       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 227267             Date 10Jun04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSE007  *CREATE    Date 18Dec97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  227267 - POSITION BLOCKED ALTHOUGH NO BLOCK FOR ALLOCATION   *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
      /EJECT
      *
      ***  Midas SE Corporate Actions General Blocks/Security - Prefixes VD-MU
      *
     FSECOBPL1IF  E           K        DISK         KINFSR *PSSR
      *
     FSECOAL99IF  E           K        DISK         KINFSR *PSSR                              227267
      /EJECT
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   41  - Record found on SECOBPL1 file.                        *
      *   90  - Record found on SECOAL99 file.                        *                       227267
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *   P001   - Initial processing.                                *
      *   P002   - Detail processing.                                 *
      *   P003   - Termination processing                             *
      *                                                               *
      *   *PSSR  - Program Error Handling Routine.                    *
      *                                                               *
      *****************************************************************
      /EJECT
      *
      ***  Array containing Copyright statement.
      *
     E                    CPY@    1   1 80
      *
      /SPACE 3
      *
     ISECOBKD0
     I              MUBRCD                          VDBRCD
     I              MUBLTY                          VDBLTY
     I              MUSESN                          VDSESN
     I              MUBEDT                          VDBEDT
     I              MUBSTT                          VDBSTT
      *
      ***  Data Area giving Run Day Number.
      *
     IRUNDAT    E DSRUNDAT
      *
      /EJECT
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***  Perform initial processing.
      *
     C                     EXSR P001
      *
      ***  Perform detail processing.
      *
     C                     EXSR P002
      *
      ***  Perform termination processing.
      *
     C                     EXSR P003
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
      /EJECT
      *****************************************************************
      *                                                               *
      *  P001   - Initial processing.                                 *
      *                                                               *
      *  Called By: Main processing.                                  *
      *                                                               *
      *  Calls: P003   - Termination processing                       *
      *                                                               *
      *****************************************************************
      *
     C           P001      BEGSR                           ***  P001  ***
      *
      ***  Set up copyright parameter.
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ***  Define key fields.
      *
     C           *LIKE     DEFN VDBRCD    @KBRCD
     C           *LIKE     DEFN VDBLTY    @KBLTY
     C           *LIKE     DEFN VDBEDT    @KBEDT
      *
      ***  Key list for LF/SECOBPL1.
      *
     C           @KEY1     KLIST
     C                     KFLD           @KBRCD
     C                     KFLD           @PSESN
     C                     KFLD           @KBLTY
     C                     KFLD           @KBEDT
      *                                                                                       227267
      ***  Key list for LF/SECOAL99.                                                          227267
      *                                                                                       227267
     C           @KEY2     KLIST                                                              227267
     C                     KFLD           @KBRCD                                              227267
     C                     KFLD           @PSESN                                              227267
     C                     KFLD           @PCNUM                                              227267
      *
      ***  Receive input parameters.
      *
     C           *ENTRY    PLIST
     C                     PARM           @PBRCD  3
     C                     PARM           @PSESN 10
     C**********           PARM           @PCNUM  60                                   227267 CSD027
     C                     PARM           @PCNUM  6                                           CSD027
     C                     PARM           @PSPGB  1
     C                     PARM           @PSSGB  1
      *
     C                     MOVEL'N'       @PSPGB
     C                     MOVEL'N'       @PSSGB
      *
      ***  If input parameters blanks, exit program.
      *
     C           @PBRCD    IFEQ *BLANK
     C           @PSESN    OREQ *BLANK
     C                     EXSR P003
     C                     ENDIF
      *
      ***  Retrieve Run Day Number in data area RUNDAT.
      *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  P002   - Detail processing.                                  *
      *                                                               *
      *  Called By: Main processing.                                  *
      *                                                               *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           P002      BEGSR                           ***  P002  ***
      *
      ***  Initialize key fields.
      *
     C                     MOVE @PBRCD    @KBRCD           Branch Code.
     C                     MOVEL'P'       @KBLTY           Block Type.
     C                     Z-ADDAGRDNB    @KBEDT           Run Day Number.
      *
     C           @KEY1     SETLLSECOBPL1
     C                     READ SECOBPL1                 41
      *
     C           *IN41     IFEQ '0'
     C           VDBRCD    ANDEQ@KBRCD
     C           VDSESN    ANDEQ@PSESN
     C           VDBLTY    ANDEQ@KBLTY
     C           VDBSTT    ANDLE@KBEDT
     C           VDBEDT    ANDGE@KBEDT
     C                     MOVEL'Y'       @PSPGB
     C                     ENDIF
      *
     C                     MOVEL'S'       @KBLTY
      *
     C           @KEY1     SETLLSECOBPL1
     C                     READ SECOBPL1                 41
      *
     C           *IN41     IFEQ '0'
     C           VDBRCD    ANDEQ@KBRCD
     C           VDSESN    ANDEQ@PSESN
     C           VDBLTY    ANDEQ@KBLTY
     C           VDBSTT    ANDLE@KBEDT
     C           VDBEDT    ANDGE@KBEDT
     C                     MOVEL'Y'       @PSSGB
     C                     ENDIF
      *
     C           @KEY2     SETLLSECOAL99                                                      227267
     C           @KEY2     READESECOAL99                 90                                   227267
      *                                                                                       227267
     C           *IN90     IFEQ '0'                                                           227267
     C           MCBEWI    ANDNE'E'                                                           227267
     C                     MOVEL'N'       @PSPGB                                              227267
     C                     MOVEL'N'       @PSSGB                                              227267
     C                     END                                                                227267
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  P003   - Termination processing.                             *
      *                                                               *
      *  Called By: Main processing.                                  *
      *             P001   - Initial processing.                      *
      *                                                               *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           P003      BEGSR                           ***  P003  ***
      *
     C                     MOVE '0'       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  *PSSR  - Program Error Handling Routine.                     *
      *                                                               *
      *  Called By: None.                                             *
      *                                                               *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ***  *PSSR  ***
      *
     C                     DUMP
      *
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
      *
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
**  CPY@
(c) Finastra International Limited 2001
