     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE CO Retrieve General Block Cust. Inds')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7574 - Corporate Actions - Retrieve General Block Customer *
      *           Indicators.                                         *
      *                                                               *
      *  Function:  This program retrieve Block Customer Indicators.  *
      *  (I/C)                                                        *
      *                                                               *
      *  Called By: SE0050 - Trade Detail Maintenance                 *
      *             SE0070 - Depot Movements Maintenance              *
      *             SE0205 - Client Holdings enquiry                  *
      *             SE0215 - Customer Portfolio Valuation enquiry     *
      *             SE3000 - Bulk Trades Input                        *
      *             SE3002 - Review Trades for Bulk Trade Input       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CSE007  *CREATE    Date 18Dec97               *
      *                                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
      /EJECT
      *
      ***  Midas SE Corporate Actions General Blocks/Customer - Prefixes VD-MU
      *
     FSECOBPL2IF  E           K        DISK         KINFSR *PSSR
      *
      ***  Midas SE Corporate Actions General Blocks/Customer - Prefixes VD-MU
      *
     FSECOBPL3IF  E           K        DISK         KINFSR *PSSR
     F            SEBLKPD0                          KRENAMESEBLKPD3
     F            SECOBKD0                          KRENAMESECOBKD3
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   41  - Record found on SECOBPL1 file.                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *   P001   - Initial processing.                                *
      *   P002   - Detail processing.                                 *
      *   P003   - Termination processing                             *
      *                                                               *
      *   *PSSR  - Program Error Handling Routine.                    *
      *                                                               *
      *****************************************************************
      /EJECT
      *
      ***  Array containing Copyright statement.
      *
     E                    CPY@    1   1 80
      *
      /SPACE 3
      *
     ISECOBKD0
     I              MUBRCD                          VDBRCD
     I              MUBLTY                          VDBLTY
     I              MUCNUM                          VDCNUM
     I              MUPTFR                          VDPTFR
     I              MUBEDT                          VDBEDT
     I              MUBSTT                          VDBSTT
      *
     ISECOBKD3
     I              MUBRCD                          VDBRCD
     I              MUBLTY                          VDBLTY
     I              MUCNUM                          VDCNUM
     I              MUPTFR                          VDPTFR
     I              MUBEDT                          VDBEDT
     I              MUBSTT                          VDBSTT
      *
      ***  Data Area giving Run Day Number.
      *
     IRUNDAT    E DSRUNDAT
      *
      /EJECT
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***  Perform initial processing.
      *
     C                     EXSR P001
      *
      ***  Perform detail processing.
      *
     C                     EXSR P002
      *
      ***  Perform termination processing.
      *
     C                     EXSR P003
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
      /EJECT
      *****************************************************************
      *                                                               *
      *  P001   - Initial processing.                                 *
      *                                                               *
      *  Called By: Main processing.                                  *
      *                                                               *
      *  Calls: P003   - Termination processing                       *
      *                                                               *
      *****************************************************************
      *
     C           P001      BEGSR                           ***  P001  ***
      *
      ***  Set up copyright parameter.
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ***  Define key fields.
      *
     C           *LIKE     DEFN VDBRCD    @KBRCD
     C           *LIKE     DEFN VDCNUM    @KCNUM
     C           *LIKE     DEFN VDBLTY    @KBLTY
     C           *LIKE     DEFN VDBEDT    @KBEDT
      *
      ***  Key list for LF/SECOBPL3.
      *
     C           @KEY1     KLIST
     C                     KFLD           @KBRCD
     C                     KFLD           @KCNUM
     C                     KFLD           @KBLTY
     C                     KFLD           @KBEDT
      *
      ***  Key list for LF/SECOBPL2.
      *
     C           @KEY2     KLIST
     C                     KFLD           @KBRCD
     C                     KFLD           @KCNUM
     C                     KFLD           @KBLTY
     C                     KFLD           @PPTFR
     C                     KFLD           @KBEDT
      *
      ***  Receive input parameters.
      *
     C           *ENTRY    PLIST
     C                     PARM           @PBRCD  3
     C                     PARM           @PCNUM  6
     C                     PARM           @PPTFR  4
     C                     PARM           @PCPGB  1
     C                     PARM           @PCSGB  1
      *
     C                     MOVEL'N'       @PCPGB
     C                     MOVEL'N'       @PCSGB
      *
      ***  If input parameters blanks, exit program.
      *
     C           @PBRCD    IFEQ *BLANK
     C           @PCNUM    OREQ *BLANK
     C                     EXSR P003
     C                     ENDIF
      *
      ***  Retrieve Run Day Number in data area RUNDAT.
      *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  P002   - Detail processing.                                  *
      *                                                               *
      *  Called By: Main processing.                                  *
      *                                                               *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           P002      BEGSR                           ***  P002  ***
      *
     C                     MOVE @PBRCD    @KBRCD
     C                     MOVE @PCNUM    @KCNUM
     C                     Z-ADDAGRDNB    @KBEDT
      *
     C           @PPTFR    IFEQ *BLANKS
      *
     C                     MOVEL'P'       @KBLTY
     C           @KEY1     SETLLSECOBPL3
     C                     READ SECOBPL3                 41
      *
     C           *IN41     IFEQ '0'
     C           VDBRCD    ANDEQ@KBRCD
     C           VDCNUM    ANDEQ@KCNUM
     C           VDBLTY    ANDEQ@KBLTY
     C           VDBSTT    ANDLE@KBEDT
     C           VDBEDT    ANDGE@KBEDT
     C                     MOVEL'Y'       @PCPGB
     C                     ENDIF
      *
     C                     MOVEL'S'       @KBLTY
     C           @KEY1     SETLLSECOBPL3
     C                     READ SECOBPL3                 41
      *
     C           *IN41     IFEQ '0'
     C           VDBRCD    ANDEQ@KBRCD
     C           VDCNUM    ANDEQ@KCNUM
     C           VDBLTY    ANDEQ@KBLTY
     C           VDBSTT    ANDLE@KBEDT
     C           VDBEDT    ANDGE@KBEDT
     C                     MOVEL'Y'       @PCSGB
     C                     ENDIF
      *
     C                     ELSE
      *
     C                     MOVEL'P'       @KBLTY
     C           @KEY2     SETLLSECOBPL2
     C                     READ SECOBPL2                 41
      *
     C           *IN41     IFEQ '0'
     C           VDBRCD    ANDEQ@KBRCD
     C           VDCNUM    ANDEQ@KCNUM
     C           VDBLTY    ANDEQ@KBLTY
     C           VDPTFR    ANDEQ@PPTFR
     C           VDBSTT    ANDLE@KBEDT
     C           VDBEDT    ANDGE@KBEDT
     C                     MOVEL'Y'       @PCPGB
     C                     ENDIF
      *
     C                     MOVEL'S'       @KBLTY
     C           @KEY2     SETLLSECOBPL2
     C                     READ SECOBPL2                 41
      *
     C           *IN41     IFEQ '0'
     C           VDBRCD    ANDEQ@KBRCD
     C           VDCNUM    ANDEQ@KCNUM
     C           VDBLTY    ANDEQ@KBLTY
     C           VDPTFR    ANDEQ@PPTFR
     C           VDBSTT    ANDLE@KBEDT
     C           VDBEDT    ANDGE@KBEDT
     C                     MOVEL'Y'       @PCSGB
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  P003   - Termination processing.                             *
      *                                                               *
      *  Called By: Main processing.                                  *
      *             P001   - Initial processing.                      *
      *                                                               *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           P003      BEGSR                           ***  P003  ***
      *
     C                     MOVE '0'       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  *PSSR  - Program Error Handling Routine.                     *
      *                                                               *
      *  Called By: None.                                             *
      *                                                               *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ***  *PSSR  ***
      *
     C                     DUMP
      *
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
      *
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
