     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE CO Retrieve Specific Block Inds')             *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7576 - Corporate Actions - Retrieve Specific Block         *
      *           Indicators.                                         *
      *                                                               *
      *  Function:  This program retrieve Specific Block Indicators.  *
      *  (I/C)                                                        *
      *                                                               *
      *  Called By: SE0205 - Client Holdings enquiry                  *
      *             SE0215 - Customer Portfolio Valuation enquiry     *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CSW210             Date 04May10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSE007  *CREATE    Date 18Dec97               *
      *                                    Date                       *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSW210 - SWIFT 2010 Changes (Recompile)                      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
      /EJECT
      *
      ***  Midas SE Corporate Actions Specific Blocks - Prefixes VC-VD-MC-MU
      *
     FSECOBAL2IF  E           K        DISK         KINFSR *PSSR
      *
      ***  Midas SE Corporate Actions Specific Blocks - Prefixes VC-VD-MC-MU
      *
     FSECOBAL3IF  E           K        DISK         KINFSR *PSSR
     F            SEERALD0                          KRENAMESEERALD3
     F            SEBLKPD0                          KRENAMESEBLKPD3
     F            SECOALD0                          KRENAMESECOALD3
     F            SECOBKD0                          KRENAMESECOBKD3
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   01  - Format SEERALD0 read.                                 *
      *   02  - Format SEERALD3 read.                                 *
      *                                                               *
      *   41  - Record found on SECOBAL2 file.                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *   P001   - Initial processing.                                *
      *   P002   - Detail processing.                                 *
      *   P003   - Termination processing                             *
      *                                                               *
      *   *PSSR  - Program Error Handling Routine.                    *
      *                                                               *
      *****************************************************************
      /EJECT
      *
      ***  Array containing Copyright statement.
      *
     E                    CPY@    1   1 80
      *
      /SPACE 3
      *
     ISEERALD0    01
     I              VCBRCD                          XXBRCD
     I              VCCNUM                          XXCNUM
     I              VCPTFR                          XXPTFR
     I              VCERTP                          XXBLTY
     I              VCSESN                          XXSESN
     I              VCBEDT                          XXBEDT
      *
     ISEBLKPD0
     I              VDBRCD                          XXBRCD
     I              VDCNUM                          XXCNUM
     I              VDPTFR                          XXPTFR
     I              VDBLTY                          XXBLTY
     I              VDSESN                          XXSESN
     I              VDBEDT                          XXBEDT
     I              VDBSTT                          XXBSTT
      *
     ISECOALD0
     I              MCBRCD                          XXBRCD
     I              MCCNUM                          XXCNUM
     I              MCPTFR                          XXPTFR
     I              MCBLTY                          XXBLTY
     I              MCBSEC                          XXSESN
     I              MCBEDT                          XXBEDT
     I              MCBSTT                          XXBSTT
      *
     ISECOBKD0
     I              MUBRCD                          XXBRCD
     I              MUCNUM                          XXCNUM
     I              MUPTFR                          XXPTFR
     I              MUBLTY                          XXBLTY
     I              MUSESN                          XXSESN
     I              MUBEDT                          XXBEDT
     I              MUBSTT                          XXBSTT
      *
     ISEERALD3    02
     I              VCBRCD                          XXBRCD
     I              VCCNUM                          XXCNUM
     I              VCERTP                          XXBLTY
     I              VCSESN                          XXSESN
     I              VCBEDT                          XXBEDT
      *
     ISEBLKPD3
     I              VDBRCD                          XXBRCD
     I              VDCNUM                          XXCNUM
     I              VDBLTY                          XXBLTY
     I              VDSESN                          XXSESN
     I              VDBEDT                          XXBEDT
     I              VDBSTT                          XXBSTT
      *
     ISECOALD3
     I              MCBRCD                          XXBRCD
     I              MCCNUM                          XXCNUM
     I              MCBLTY                          XXBLTY
     I              MCBSEC                          XXSESN
     I              MCBEDT                          XXBEDT
     I              MCBSTT                          XXBSTT
      *
     ISECOBKD3
     I              MUBRCD                          XXBRCD
     I              MUCNUM                          XXCNUM
     I              MUBLTY                          XXBLTY
     I              MUSESN                          XXSESN
     I              MUBEDT                          XXBEDT
     I              MUBSTT                          XXBSTT
      *
      ***  Data Area giving Run Day Number.
      *
     IRUNDAT    E DSRUNDAT
      *
      /EJECT
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***  Perform initial processing.
      *
     C                     EXSR P001
      *
      ***  Perform detail processing.
      *
     C                     EXSR P002
      *
      ***  Perform termination processing.
      *
     C                     EXSR P003
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
      /EJECT
      *****************************************************************
      *                                                               *
      *  P001   - Initial processing.                                 *
      *                                                               *
      *  Called By: Main processing.                                  *
      *                                                               *
      *  Calls: P003   - Termination processing                       *
      *                                                               *
      *****************************************************************
      *
     C           P001      BEGSR                           ***  P001  ***
      *
      ***  Set up copyright parameter.
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ***  Define key fields.
      *
     C           *LIKE     DEFN XXBRCD    @KBRCD
     C           *LIKE     DEFN XXCNUM    @KCNUM
     C           *LIKE     DEFN XXBLTY    @KBLTY
     C           *LIKE     DEFN XXBEDT    @KBEDT
      *
      ***  Key list for LF/SECOBAL3.
      *
     C           @KEY1     KLIST
     C                     KFLD           @KBRCD
     C                     KFLD           @KCNUM
     C                     KFLD           @PSESN
     C                     KFLD           @KBLTY
     C                     KFLD           @KBEDT
      *
      ***  Key list for LF/SECOBAL2
      *
     C           @KEY2     KLIST
     C                     KFLD           @KBRCD
     C                     KFLD           @KCNUM
     C                     KFLD           @PSESN
     C                     KFLD           @KBLTY
     C                     KFLD           @PPTFR
     C                     KFLD           @KBEDT
      *
      ***  Receive input parameters.
      *
     C           *ENTRY    PLIST
     C                     PARM           @PBRCD  3
     C                     PARM           @PCNUM  6
     C                     PARM           @PSESN 10
     C                     PARM           @PPTFR  4
     C                     PARM           @PPSBI  1
     C                     PARM           @PSSBI  1
      *
     C                     MOVEL'N'       @PPSBI
     C                     MOVEL'N'       @PSSBI
      *
      ***  If input parameters blanks, exit program.
      *
     C           @PBRCD    IFEQ *BLANK
     C           @PCNUM    OREQ *BLANK
     C           @PSESN    OREQ *BLANK
     C                     EXSR P003
     C                     ENDIF
      *
      ***  Retrieve Run Day Number in data area RUNDAT.
      *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  P002   - Detail processing.                                  *
      *                                                               *
      *  Called By: Main processing.                                  *
      *                                                               *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           P002      BEGSR                           ***  P002  ***
      *
     C                     MOVE @PBRCD    @KBRCD
     C                     MOVE @PCNUM    @KCNUM
     C                     Z-ADDAGRDNB    @KBEDT
      *
     C           @PPTFR    IFEQ *BLANKS
     C           @PPTFR    OREQ '9998'
     C           @PPTFR    OREQ '9999'
      *
     C                     MOVEL'P'       @KBLTY
     C           @KEY1     SETLLSECOBAL3
     C                     MOVEA'00'      *IN,01
     C                     READ SECOBAL3            N    41
      *
      ***  If 02 ON = format SEERALD4 read, reset field XXBSTT: Block
      ***  from Date/Blocked Start Date.
      *
     C           *IN02     IFEQ '1'
     C                     Z-ADD0         XXBSTT
     C                     ENDIF
      *
     C           *IN41     IFEQ '0'
     C           XXBRCD    ANDEQ@KBRCD
     C           XXCNUM    ANDEQ@KCNUM
     C           XXSESN    ANDEQ@PSESN
     C           XXBLTY    ANDEQ@KBLTY
     C           XXBSTT    ANDLE@KBEDT
     C           XXBEDT    ANDGE@KBEDT
     C                     MOVEL'Y'       @PPSBI
     C                     ENDIF
      *
     C                     MOVEL'S'       @KBLTY
     C           @KEY1     SETLLSECOBAL3
     C                     MOVEA'00'      *IN,01
     C                     READ SECOBAL3            N    41
      *
      ***  If 02 ON = format SEERALD4 read, reset field XXBSTT: Block
      ***  from Date/Blocked Start Date.
      *
     C           *IN02     IFEQ '1'
     C                     Z-ADD0         XXBSTT
     C                     ENDIF
      *
     C           *IN41     IFEQ '0'
     C           XXBRCD    ANDEQ@KBRCD
     C           XXCNUM    ANDEQ@KCNUM
     C           XXSESN    ANDEQ@PSESN
     C           XXBLTY    ANDEQ@KBLTY
     C           XXBSTT    ANDLE@KBEDT
     C           XXBEDT    ANDGE@KBEDT
     C                     MOVEL'Y'       @PSSBI
     C                     ENDIF
      *
     C                     ELSE
      *
     C                     MOVEL'P'       @KBLTY
     C           @KEY2     SETLLSECOBAL2
     C                     MOVEA'00'      *IN,01
     C                     READ SECOBAL2            N    41
      *
      ***  If 01 ON = format SEERALD0 read, reset field XXBSTT: Block
      ***  from Date/Blocked Start Date.
      *
     C           *IN01     IFEQ '1'
     C                     Z-ADD0         XXBSTT
     C                     ENDIF
      *
     C           *IN41     IFEQ '0'
     C           XXBRCD    ANDEQ@KBRCD
     C           XXCNUM    ANDEQ@KCNUM
     C           XXSESN    ANDEQ@PSESN
     C           XXBLTY    ANDEQ@KBLTY
     C           XXPTFR    ANDEQ@PPTFR
     C           XXBSTT    ANDLE@KBEDT
     C           XXBEDT    ANDGE@KBEDT
     C                     MOVEL'Y'       @PPSBI
     C                     END
      *
     C                     MOVEL'S'       @KBLTY
     C           @KEY2     SETLLSECOBAL2
     C                     MOVEA'00'      *IN,01
     C                     READ SECOBAL2            N    41
      *
      ***  If 01 ON = format SEERALD0 read, reset field XXBSTT: Block
      ***  from Date/Blocked Start Date.
      *
     C           *IN01     IFEQ '1'
     C                     Z-ADD0         XXBSTT
     C                     ENDIF
      *
     C           *IN41     IFEQ '0'
     C           XXBRCD    ANDEQ@KBRCD
     C           XXCNUM    ANDEQ@KCNUM
     C           XXSESN    ANDEQ@PSESN
     C           XXBLTY    ANDEQ@KBLTY
     C           XXPTFR    ANDEQ@PPTFR
     C           XXBSTT    ANDLE@KBEDT
     C           XXBEDT    ANDGE@KBEDT
     C                     MOVEL'Y'       @PSSBI
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  P003   - Termination processing.                             *
      *                                                               *
      *  Called By: Main processing.                                  *
      *             P001   - Initial processing.                      *
      *                                                               *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           P003      BEGSR                           ***  P003  ***
      *
     C                     MOVE '0'       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  *PSSR  - Program Error Handling Routine.                     *
      *                                                               *
      *  Called By: None.                                             *
      *                                                               *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ***  *PSSR  ***
      *
     C                     DUMP
      *
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
      *
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
**  CPY@
(c) Finastra International Limited 2001
