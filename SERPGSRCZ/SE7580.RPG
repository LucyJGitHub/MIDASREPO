     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Drop off Corporate Action Events')            *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7580 - SE Drop of Corporate Action Events                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER020             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 190204             Date 31May01               *
      * Midas DBA 3.04 -----------------------------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CSE020             Date 21Mar00               *
      *                 161732             Date 21Mar00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 136428             Date 15Jun98               *
      *                 CEU017             Date 19Mar98               *
      *                 CSE007  *CREATE    Date 01Dec97               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER020 - Enhanced Rollover Window: Upgrade FGE089 to         *
      *           Midas Plus (Recompile)                              *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  190204 - Recompiled due to changes in PF/SECODVPD.           *
      *  CSE020 - Corporate Actions Domino Effect Processing          *
      *           (Recompiled due to changes in PF/SECORFPD)          *
      *  161732 - Corporate Actions Exercise and Conversion -         *
      *           Part Stock/Keep Rest Calculation                    *
      *           (Recompiled due to changes in PF/SECOEXPD)          *
      *  136428 - Recompile due to addition of Days Basis and Divisor *
      *           Basis field in SECORBPD.                            *
      *  CEU017 - Securities Redenomination Euro Changes              *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
      /EJECT
     FSECORFL0UF  E           K        DISK
     F                                              KINFSR *PSSR
      ** SE Corporate Action Reference File
      *
     FSECODRL0UF  E           K        DISK
     F                                              KINFSR *PSSR
      ** SE Corporate Action Depot Reference File
      *
     FSECODPL0UF  E           K        DISK
     F                                              KINFSR *PSSR
      ** SE Corporate Action - Depot
      *
     FSECOALL0UF  E           K        DISK
     F                                              KINFSR *PSSR
      ** SE Corporate Action Allocations
      *
     FSECOREL0UF  E           K        DISK
     F                                              KINFSR *PSSR
      ** SE Corporate Actions Reply Handling
      *
     FSECORHL0UF  E           K        DISK
     F                                              KINFSR *PSSR
      ** SE Corporate Actions Reply Handling History
      *
     FSECOBKL1UF  E           K        DISK
     F                                              KINFSR *PSSR
      ** SE Corporate Action Blocked Positions
      *
     FSECOEXL0UF  E           K        DISK
     F                                              KINFSR *PSSR
      ** SE Corporate Action Exercises and Conventions
      *
     FSECOFRL0UF  E           K        DISK
     F                                              KINFSR *PSSR
      ** SE Corporate Actions Free Allocations
      *
     FSECOCEL0UF  E           K        DISK
     F                                              KINFSR *PSSR
      ** SE Corporate Actions Capital Changes
      *
     FSECONFL0UF  E           K        DISK
     F                                              KINFSR *PSSR
      ** SE Corporate Actions Non Financial Events
      *
     FSECONCL0UF  E           K        DISK
     F                                              KINFSR *PSSR
      ** SE Corporate Actions Name Changes
      *
     FSECODVL0UF  E           K        DISK
     F                                              KINFSR *PSSR
      ** SE Corporate Actions Dividend Events
      *
     FSECORGL0UF  E           K        DISK
     F                                              KINFSR *PSSR
      ** SE Corporate Actions Rights Issues
      *
     FSECOOFL0UF  E           K        DISK
     F                                              KINFSR *PSSR
      ** SE Corporate Actions Offers
      *
     FSECOCRL0UF  E           K        DISK
     F                                              KINFSR *PSSR
      ** SE Corporate Actions Corporate Reorganisation
      *
     FSECOCCL0UF  E           K        DISK                               CEU017
     F                                              KINFSR *PSSR          CEU017
      ** SE Corporate Actions Currency Changes                            CEU017
      *                                                                   CEU017
     FSECORBL0UF  E           K        DISK         KINFSR *PSSR      UC  CEU017
      ** SE Securities Redenomination Bulk Definitions                    CEU017
      *                                                                   CEU017
     FSECORSL0UF  E           K        DISK         KINFSR *PSSR      UC  CEU017
      ** SE Securities Redenomination Securities                          CEU017
      *                                                                   CEU017
     FSE7580P1O   E                    PRINTER      KINFDS SPOOL1     UC
     F                                              KINFSR *PSSR
      **  Detail Printer File
      *
     FSE7580P2O   E                    PRINTER      KINFDS SPOOL2     UC  CEU017
     F                                              KINFSR *PSSR          CEU017
      **  Detail Printer File - Drop of Bulk Definitions record           CEU017
      *                                                                   CEU017
     FSE7580AUO   E                    PRINTER      KINFDS SPOOLU
     F                                              KINFSR *PSSR
      **  Audit Printer File
      *
     F/COPY WNCPYSRC,SE7580F001
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *                   Function of indicators                      *
      *                   ----------------------                      *
      *                                                               *
      *  41    - Indicator use for Opening/Closing Printer file       *
      *  70    - Page Overflow                                        *
      *  87-88 - Work Indicator                                       *
      *  89    - Record not Found                                     *
      *  LR    - Last record                                          *
      *  U7/U8 - Database error                                       *
      *  90-99 - Used by Standard Subroutines                         *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *  -------------------------------                              *
      *                                                               *
      *  SRINIT - INITIAL PROCESSING                                  *
      *  SRMAIN - MAIN PROCESSING                                     *
      *  SRENDP - END PROCESSING                                      *
      *                                                               *
      *  SRDPRR _ DELETE ALL DEPOT REFERENCE RECORDS                  *
      *  SRALRD - DELETE ALL ALLOCATION RECORDS                       *
      *  SRDPRD - DELETE ALL DEPOT RECORDS                            *
      *  SRRHRD - DELETE ALL REPLY HANDLING RECORDS                   *
      *  SRBLKP - DELETE ALL BLOCKING RECORDS                         *
      *  SRCARD - DELETE ALL OPTION RECORDS                           *
      *                                                               *
      *  SRODPF - OUTPUT DETAILS TO PRINTER FILE                      *
      *                                                               *
      *  *PSSR  - STANDARD DATA BASE ERROR PROCESSING                 *
      *                                                               *
      *  ZDATE2 - Dates Validation and Conversion                     *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
     E/COPY ZSRSRC,ZDATE2Z1
      *
     E                    CPY@    1   1 80
      **   For Compilation - Copyright Insertion
      *
      ******************************************************************
      /EJECT
      ******************************************************************
     ISPOOLU      DS
      ** File Information Data Structure (Audit Report)
      *
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     ISPOOL1      DS
      ** File Information Data Structure (Detail Report)
      *
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOL2      DS                                                      CEU017
      ** File Information Data Structure (Detail Report)                  CEU017
      *                                                                   CEU017
     I                                       83  92 SFILE2                CEU017
     I                                    B 123 1240SFNUM2                CEU017
     I                                    B 188 1890OFLLN2                CEU017
     I                                    B 367 3680PRTLN2                CEU017
      *                                                                   CEU017
     ILDA       E DSLDA                         256
      **   Data Structure for Database Error
      *
     IRUNDAT    E DS
      ** Standard data area layout for system flags and run date
      *
     I@BANK     E DSSDBANKPD
      ** Dummy record format for access to Bank Details
      *
     I@STRD     E DSSDSTRDPD
      ** Dummy record format for access to Securities Trading Data
      *
     ISCSARD    E DSSCSARDPD                                              CEU017
      ** Data structure for switchable features file                      CEU017
      *                                                                   CEU017
     IDSFDY     E DSDSFDY
      ** First DS for access programs, short data structure
      *
     IDSSDY     E DSDSSDY
      ** Second DS for access programs, long data structure
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            START  - Main Routine                              *
      *                                                               *
      *  CALLS     SRINIT - Initial Processing                        *
      *            SRMAIN - Main Processing                           *
      *            SRENDP - End Processing                            *
      *                                                               *
      *****************************************************************
      *
      **   Initial Processing
      *
     C                     EXSR SRINIT
      *
      **   Main Processing
      *
     C                     EXSR SRMAIN
      *
      **   End Processing
      *
     C                     EXSR SRENDP
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            SRINIT - INITIAL PROCESSING                        *
      *                                                               *
      *  CALLS     RCFAU  - Subroutine to register AU report via RCF  *
      *            *PSSR  - StandardData Base Error Processing        *
      *                                                               *
      *  CALLED BY START  - Main Routine                              *
      *                                                               *
      *  FLDS USED        -                                           *
      *                                                               *
      *****************************************************************
     C           SRINIT    BEGSR
      *
      **  Copyright statement
      *
     C                     MOVEACPY@      MKI@   80
      *
      ** Check multibranching indicator.
      *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     Z-ADD0         DBASE
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     MOVEL'SE7580'  DBPGM     P
     C                     OUT  LDA
      *
     C           AGMBIN    IFEQ 'Y'                        B*1
     C                     MOVE '1'       *IN39
     C                     END                             E*1
      *
      ** Initialise key and work fields
      *
     C                     Z-ADD0         WWNCAD  50       Nbr ER dr
     C                     Z-ADD0         WWNBLK  50       Nbr ER dr      CEU017
      *
     C                     EXSR RCFAU
      *
      ** Access to SDBANKPD to retrieve the MIDAS
      ** Run Date and User Report Title
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '        '@RTCD   7
     C                     PARM '*FIRST  '@OPTN   7
     C           @BANK     PARM @BANK     DSFDY
      *
      ** If record not found
      *
     C           @RTCD     IFNE *BLANK                     B*1
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE            ***************
     C                     MOVEL*BLANKS   DBFILE           * DB ERROR 01 *
     C                     MOVE 'SDBANKPD'DBFILE           ***************
     C                     MOVEL'*FIRST'  DBKEY     P
     C                     MOVEL'SE7580'  DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF                           E*1
      *
      ** Determine Date Format
      *
     C           BJDFIN    IFEQ 'M'                        B*1
     C                     MOVE '1'       *IN98
     C                     END                             E*1
      *
      ** Access to SDSTRDPD to retrieve Retention Period.
      *
     C                     CALL 'AOSTRDR0'
     C                     PARM '        '@RTCD   7
     C                     PARM '*FIRST  '@OPTN   7
     C           @STRD     PARM @STRD     DSSDY
      *
      ** If record not found
      *
     C           @RTCD     IFNE *BLANK                     B*1
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE            ***************
     C                     MOVEL*BLANKS   DBFILE           * DB ERROR 02 *
     C                     MOVE 'SDSTRDPD'DBFILE           ***************
     C                     MOVEL'*FIRST'  DBKEY     P
     C                     MOVEL'SE7580'  DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF                           E*1
      *                                                                   CEU017
      ** Check if feature "Securities Redenomination Euro Changes" is on  CEU017
      *                                                                   CEU017
     C                     CALL 'AOSARDR0'                                CEU017
     C                     PARM *BLANKS   @RTCD                           CEU017
     C                     PARM '*VERIFY' @OPTN                           CEU017
     C                     PARM 'CEU017'  @SARD   6                       CEU017
     C           SCSARD    PARM SCSARD    DSSDY                           CEU017
      *                                                                   CEU017
     C           @RTCD     IFEQ *BLANKS                                   CEU017
     C                     MOVE 'Y'       CEU017                          CEU017
     C                     MOVE *ON       *IN50                           CEU017
     C                     OPEN SECORBL0                                  CEU017
     C                     OPEN SECORSL0                                  CEU017
     C                     ELSE                                           CEU017
     C                     MOVE 'N'       CEU017  1                       CEU017
     C                     MOVE *OFF      *IN50                           CEU017
     C                     ENDIF                                          CEU017
     C/COPY WNCPYSRC,SE7580C001
      *
      ** Calculate Retention Date
      *
     C           31        MULT BVROST    WWFLD1  50
     C           BJRDNB    SUB  WWFLD1    RTNTDT  50
      *
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
     C                     Z-ADD0         RQDLN2  30                      CEU017
     C                     Z-ADD0         DIFLN2  30                      CEU017
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            SRMAIN - Main Processing                           *
      *                                                               *
      *  CALLS     SRDPRR - Delete All Depot Reference Records        *
      *            SRALRD - Delete All Allocation Records             *
      *            SRDPRD - Delete All Depot Records                  *
      *            SRRHRD - Delete All Reply Handling Records         *
      *            SRBLKP - Delete All Blocking Records               *
      *            SRCARD - Delete All Corporate Action Records       *
      *            RCFP1  - Subroutine to Register P1 report via RCF  *
      *            SRODPF - Output Details to Printer File            *
      *                                                               *
      *  CALLED BY START  - Main Routine                              *
      *                                                               *
      *  FLDS USED        -                                           *
      *                                                               *
      *****************************************************************
     C           SRMAIN    BEGSR
      *
      ** Read first record in LF/SECORFL0
      *
     C           *LOVAL    SETLLSECORFL0
     C                     READ SECORFL0                 88
      *
      ** Do while end of file is not reached
      *
     C           *IN88     DOWEQ'0'                        B*1
      *
      ** If Allocation Effective Date is lower than or equal to
      ** Retention Date
      *
     C           MAALEF    IFLE RTNTDT                     B*2
     C           MAALEF    ANDNE*ZEROS
      *
      ** Delete all Depot Reference records
      *
     C                     EXSR SRDPRR
      *
      ** Delete all Allocation records
      *
     C                     EXSR SRALRD
      *
      ** Delete all Depot Reference records
      *
     C                     EXSR SRDPRD
      *
      ** Delete all Reply Handling records
      *
     C                     EXSR SRRHRD
      *
      ** Delete all Blcoking records
      *
     C                     EXSR SRBLKP
      *
      ** Delete all Corporate Action records
      *
     C                     EXSR SRCARD
      *
      **  Open SE7580P1.
      *
     C           *IN41     IFEQ '0'                        B*3
     C                     OPEN SE7580P1
     C                     EXSR RCFP1
     C                     WRITEHEADP1
     C                     MOVE '1'       *IN41
     C                     ENDIF                           E*3
      *
     C                     Z-ADD3         RQDLN1           Check Overflow
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ** Output a detail line
      *
     C                     EXSR SRODPF
      *
      ** Delete the Corporate Action Reference record
      *
     C                     DELETSECORFD0
     C/COPY WNCPYSRC,SE7580C002
      *
     C                     ADD  1         WWNCAD
      *
     C                     ENDIF                           E*2
      *
      ** Read next record
      *
     C                     READ SECORFL0                 88
      *
     C                     ENDDO                           E*1
      *                                                                   CEU017
      ** If Feature CEU017 is ON                                          CEU017
      ** Delete all Bulk Definition record                                CEU017
      *                                                                   CEU017
     C           CEU017    IFEQ 'Y'                                       CEU017
      *                                                                   CEU017
      ** Read first record in LF/SECORBL0                                 CEU017
      *                                                                   CEU017
     C           *LOVAL    SETLLSECORBL0                                  CEU017
     C                     READ SECORBL0                 88               CEU017
      *                                                                   CEU017
      ** Do while end of file is not reached                              CEU017
      *                                                                   CEU017
     C           *IN88     DOWEQ*OFF                       B*1            CEU017
      *                                                                   CEU017
      ** If Allocation Effective Date is lower than or equal to           CEU017
      ** Retention Date                                                   CEU017
      *                                                                   CEU017
     C           NBALEF    IFLE RTNTDT                     B*2            CEU017
     C           NBALEF    ANDNE*ZEROS                                    CEU017
      *                                                                   CEU017
      ** Delete all Securities Redenomination Securities                  CEU017
     C                     EXSR SRBLKD                                    CEU017
      *                                                                   CEU017
      **  Open SE7580P2.                                                  CEU017
      *                                                                   CEU017
     C           *IN42     IFEQ *OFF                       B*3            CEU017
     C                     OPEN SE7580P2                                  CEU017
     C                     EXSR RCFP2                                     CEU017
     C                     WRITEHEADP2                                    CEU017
     C                     MOVE *ON       *IN42                           CEU017
     C                     ENDIF                           E*3            CEU017
      *                                                                   CEU017
     C                     Z-ADD3         RQDLN2           Check Overflow CEU017
     C           OFLLN2    SUB  PRTLN2    DIFLN2                          CEU017
     C           DIFLN2    IFLE RQDLN2                                    CEU017
     C                     WRITEHEADP2                                    CEU017
     C                     ENDIF                                          CEU017
      *                                                                   CEU017
      ** Output a detail line                                             CEU017
      *                                                                   CEU017
     C                     EXSR SRODP2                                    CEU017
      *                                                                   CEU017
      ** Delete the Bulk Definition Reference record                      CEU017
      *                                                                   CEU017
     C                     DELETSECORBD0                                  CEU017
      *                                                                   CEU017
     C                     ADD  1         WWNBLK                          CEU017
     C                     ENDIF                           E*2            CEU017
      *                                                                   CEU017
      ** Read next record                                                 CEU017
      *                                                                   CEU017
     C                     READ SECORBL0                 88               CEU017
      *                                                                   CEU017
     C                     ENDDO                           E*1            CEU017
     C                     ENDIF                           CEU017='Y'     CEU017
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            SRENDP - End Processing.                           *
      *                                                               *
      *  CALLS            -                                           *
      *                                                               *
      *  CALLED BY  START - Main Routine                              *
      *                                                               *
      *  FLDS USED        -                                           *
      *                                                               *
      *****************************************************************
     C           SRENDP    BEGSR
      *
      **  Produce Audit List.
      *
     C                     EXSR AUDIT
      *
      ** Write end of report
      *
     C           *IN41     IFEQ '1'                        B*1
     C                     WRITETRAILP1
     C                     CLOSESE7580P1
     C                     ENDIF                           E*1
      *                                                                   CEU017
      ** Write end of report                                              CEU017
      *                                                                   CEU017
     C           *IN42     IFEQ *ON                        B*1            CEU017
     C                     WRITETRAILP2                                   CEU017
     C                     CLOSESE7580P2                                  CEU017
     C                     ENDIF                           E*1            CEU017
      *
      ** End of program
      *
     C                     MOVE '1'       *INLR
     C                     RETRN
      *
     C                     ENDSR
     ******************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            SRDPRR - Delete All Depot Reference Records.       *
      *                                                               *
      *  CALLS            -                                           *
      *                                                               *
      *  CALLED BY SRMAIN - Main Processing                           *
      *                                                               *
      *  FLDS USED        -                                           *
      *                                                               *
      *****************************************************************
     C           SRDPRR    BEGSR
      *
      ** Read first Depot Reference record for current CA Reference
      ** in LF/SECODRL0
      *
     C           MACORF    SETLLSECODRL0
     C           MACORF    READESECODRL0                 87
      *
      ** Do while end of file and end of CA Reference is not reached
      *
     C           *IN87     DOWEQ'0'                        B*1
      *
      ** Delete the Depot Reference record in LF/SECODRL0
      *
     C                     DELETSECODRD0
      *
      ** Read next Depot Reference record for current CA Reference
      ** in LF/SECODRL0
      *
     C           MACORF    READESECODRL0                 87
      *
     C                     ENDDO                           E*1
      *
     C                     ENDSR
     ******************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            SRDPRD - Delete All Depot Records.                 *
      *                                                               *
      *  CALLS            -                                           *
      *                                                               *
      *  CALLED BY SRMAIN - Main Processing                           *
      *                                                               *
      *  FLDS USED        -                                           *
      *                                                               *
      *****************************************************************
     C           SRDPRD    BEGSR
      *
      ** Read first Depot record for current CA Reference
      ** in LF/SECODPL0
      *
     C           MACORF    SETLLSECODPL0
     C           MACORF    READESECODPL0                 87
      *
      ** Do while end of file and end of CA Reference is not reached
      *
     C           *IN87     DOWEQ'0'                        B*1
      *
      ** Delete the Corporate Action Depot record in LF/SECODPL0
      *
     C                     DELETSECODPD0
      *
      ** Read next Depot record for current CA Reference
      ** in LF/SECODPL0
      *
     C           MACORF    READESECODPL0                 87
      *
     C                     ENDDO                           E*1
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            SRALRD - Delete All Allocation Records.            *
      *                                                               *
      *  CALLS            -                                           *
      *                                                               *
      *  CALLED BY SRMAIN - Main Processing                           *
      *                                                               *
      *  FLDS USED        _                                           *
      *                                                               *
      *****************************************************************
     C           SRALRD    BEGSR
      *
      ** Read first Allocation record for current CA Reference
      ** in LF/SECOALL0
      *
     C           MACORF    SETLLSECOALL0
     C           MACORF    READESECOALL0                 87
      *
      ** Do while end of file and end of CA Reference is not reached
      *
     C           *IN87     DOWEQ'0'                        B*1
      *
      ** Delete the Corporate Action Allocation record in LF/SEERALL0
      *
     C                     DELETSECOALD0
      *
      ** Read next Allocation record for current CA Reference
      ** in LF/SECOALL0
      *
     C           MACORF    READESECOALL0                 87
      *
     C                     ENDDO                           E*1
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            SRRHRD - Delete All Reply Handling Records.        *
      *                                                               *
      *  CALLS            -                                           *
      *                                                               *
      *  CALLED BY SRMAIN - Main Processing                           *
      *                                                               *
      *  FLDS USED        -                                           *
      *                                                               *
      *****************************************************************
     C           SRRHRD    BEGSR
      *
      ** Read first Reply Handling record for current CA Reference
      ** in LF/SECOREL0
      *
     C           MACORF    SETLLSECOREL0
     C           MACORF    READESECOREL0                 87
      *
      ** Do while end of file and end of CA Reference is not reached
      *
     C           *IN87     DOWEQ'0'                        B*1
      *
      ** Delete the Reply Handling record in LF/SECOREL0
      *
     C                     DELETSECORED0
      *
      ** Read next Reply Handling record for current CA Reference
      ** in LF/SECOREL0
      *
     C           MACORF    READESECOREL0                 87
      *
     C                     ENDDO                           E*1
      *
      ** Read first Reply Handling History record for current CA Reference
      ** in LF/SECORHL0
      *
     C           MACORF    SETLLSECORHL0
     C           MACORF    READESECORHL0                 87
      *
      ** Do while end of file and end of CA Reference is not reached
      *
     C           *IN87     DOWEQ'0'                        B*1
      *
      ** Delete the Reply Handling History record in LF/SECORHL0
      *
     C                     DELETSECORHD0
      *
      ** Read next Reply Handling History record for current CA Reference
      ** in LF/SECORHL0
      *
     C           MACORF    READESECORHL0                 87
      *
     C                     ENDDO                           E*1
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            SRBLKP - Delete All Blocking Records.              *
      *                                                               *
      *  CALLS            -                                           *
      *                                                               *
      *  CALLED BY SRMAIN - Main Processing                           *
      *                                                               *
      *  FLDS USED        -                                           *
      *                                                               *
      *****************************************************************
     C           SRBLKP    BEGSR
      *
      ** Read first Blocking record for current CA Reference
      ** in LF/SECOBKL1
      *
     C           MACORF    SETLLSECOBKL1
     C           MACORF    READESECOBKL1                 87
      *
      ** Do while end of file and end of CA Reference is not reached
      *
     C           *IN87     DOWEQ'0'                        B*1
      *
      ** Delete the Blocking record in LF/SECOBKL1
      *
     C                     DELETSECOBKD0
      *
      ** Read next Blocking record for current CA Reference
      ** in LF/SECOBKL1
      *
     C           MACORF    READESECOBKL1                 87
      *
     C                     ENDDO                           E*1
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            SRCARD - Delete All Corporate Action Records.      *
      *                                                               *
      *  CALLS            -                                           *
      *                                                               *
      *  CALLED BY SRMAIN - Main Processing                           *
      *                                                               *
      *  FLDS USED        -                                           *
      *                                                               *
      *****************************************************************
     C           SRCARD    BEGSR
      *
     C           MAPTYP    IFNE 'NC'                       B*0
     C           MAPTYP    ANDNE'NF'
     C           MAPTYP    ANDNE'CC'                                      CEU017
      *
      ** Read first Corporate Action for current CA Reference having
      ** the same type
      *
     C                     SELEC
      *
     C           MAPTYP    WHEQ 'EX'                       B*1
     C           MACORF    SETLLSECOEXL0
     C           MACORF    READESECOEXL0                 87
      *
     C           MAPTYP    WHEQ 'FR'                       B*1
     C           MACORF    SETLLSECOFRL0
     C           MACORF    READESECOFRL0                 87
      *
     C           MAPTYP    WHEQ 'CE'                       B*1
     C           MACORF    SETLLSECOCEL0
     C           MACORF    READESECOCEL0                 87
      *
     C           MAPTYP    WHEQ 'DV'                       B*1
     C           MACORF    SETLLSECODVL0
     C           MACORF    READESECODVL0                 87
      *
     C           MAPTYP    WHEQ 'RG'                       B*1
     C           MACORF    SETLLSECORGL0
     C           MACORF    READESECORGL0                 87
      *
     C           MAPTYP    WHEQ 'OF'                       B*1
     C           MACORF    SETLLSECOOFL0
     C           MACORF    READESECOOFL0                 87
      *
     C           MAPTYP    WHEQ 'CR'                       B*1
     C           MACORF    SETLLSECOCRL0
     C           MACORF    READESECOCRL0                 87
      *
     C                     ENDSL                           E*1
      *
      ** Do while end of file and end of CA Reference is not reached
      *
     C           *IN87     DOWEQ'0'                        B*1
      *
      ** Delete the Corporate Action record of the current CA Reference
      *
     C                     SELEC
      *
     C           MAPTYP    WHEQ 'EX'                       B*2
     C                     DELETSECOEXD0
      *
     C           MAPTYP    WHEQ 'FR'                       B*2
     C                     DELETSECOFRD0
      *
     C           MAPTYP    WHEQ 'CE'                       B*2
     C                     DELETSECOCED0
      *
     C           MAPTYP    WHEQ 'DV'                       B*2
     C                     DELETSECODVD0
      *
     C           MAPTYP    WHEQ 'RG'                       B*2
     C                     DELETSECORGD0
      *
     C           MAPTYP    WHEQ 'OF'                       B*2
     C                     DELETSECOOFD0
      *
     C           MAPTYP    WHEQ 'CR'                       B*2
     C                     DELETSECOCRD0
      *
     C                     ENDSL                           E*2
      *
      ** Read next Corporate Action record for current CA Reference
      *
     C                     SELEC
      *
     C           MAPTYP    WHEQ 'EX'                       B*2
     C           MACORF    READESECOEXL0                 87
      *
     C           MAPTYP    WHEQ 'FR'                       B*2
     C           MACORF    READESECOFRL0                 87
      *
     C           MAPTYP    WHEQ 'CE'                       B*2
     C           MACORF    READESECOCEL0                 87
      *
     C           MAPTYP    WHEQ 'DV'                       B*2
     C           MACORF    READESECODVL0                 87
      *
     C           MAPTYP    WHEQ 'RG'                       B*2
     C           MACORF    READESECORGL0                 87
      *
     C           MAPTYP    WHEQ 'OF'                       B*2
     C           MACORF    READESECOOFL0                 87
      *
     C           MAPTYP    WHEQ 'CR'                       B*2
     C           MACORF    READESECOCRL0                 87
      *
     C                     ENDSL                           E*1
      *
     C                     ENDDO                           E*1
      *
     C                     ELSE                            X*0
      *
     C                     SELEC
      *
     C           MAPTYP    WHEQ 'NF'                       B*1
     C           MACORF    CHAINSECONFL0             87
     C           *IN87     IFEQ '0'                        B*2
     C                     DELETSECONFD0
     C                     ENDIF                           E*2
      *
     C           MAPTYP    WHEQ 'NC'                       B*1
     C           MACORF    CHAINSECONCL0             87
     C           *IN87     IFEQ '0'                        B*2
     C                     DELETSECONCD0
     C                     ENDIF                           E*2
      *
     C           MAPTYP    WHEQ 'CC'                       B*1            CEU017
     C           MACORF    CHAINSECOCCL0             87                   CEU017
     C           *IN87     IFEQ '0'                        B*2            CEU017
     C                     DELETSECOCCD0                                  CEU017
     C                     ENDIF                           E*2            CEU017
      *                                                                   CEU017
     C                     ENDSL                           E*1
     C                     ENDIF                           E*0
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            SRODPF - Output Details to Printer File.           *
      *                                                               *
      *  CALLS            -                                           *
      *                                                               *
      *  CALLED BY SRMAIN - Main Processing                           *
      *                                                               *
      *  FLDS USED        -                                           *
      *                                                               *
      *****************************************************************
     C           SRODPF    BEGSR
      *
      ** CA Reference Number
      *
     C                     MOVELMACORF    PPREFR
      *
      ** Security Shortname
      *
     C                     MOVELMASESN    PPSESN
      *
      ** CA Ex-Date
      *
     C                     MOVE MACOEX    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    PPEXDT
      *
      ** CA Allocation Effective Date
      *
     C                     MOVE MAALEF    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    PPAEDT
      *
      ** Payment Date
      *
     C                     MOVE MACOPD    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    PPPMDT
      *
      ** Corporate Action Type
      *
     C                     MOVE MACOAT    PPTYPE
      *
      ** Write a record into the printer file
      *
     C                     WRITEDETAIL
      *
     C                     ENDSR
      *****************************************************************   CEU017
      /EJECT                                                              CEU017
      *****************************************************************   CEU017
      *                                                               *   CEU017
      *            SRBLKD - Delete All Securities (PF/SECORSPD)       *   CEU017
      *                                                               *   CEU017
      *  CALLS            -                                           *   CEU017
      *                                                               *   CEU017
      *  CALLED BY SRMAIN - Main Processing                           *   CEU017
      *                                                               *   CEU017
      *  FLDS USED        -                                           *   CEU017
      *                                                               *   CEU017
      *****************************************************************   CEU017
     C           SRBLKD    BEGSR                                          CEU017
      *                                                                   CEU017
      ***  Delete all record qith same Bulk Reference                     CEU017
      *                                                                   CEU017
     C           NBBLKR    SETLLSECORSL0                                  CEU017
     C           NBBLKR    READESECORSL0                 87               CEU017
      *                                                                   CEU017
      ** Do while end of file and end of Bulk Reference is not reached    CEU017
      *                                                                   CEU017
     C           *IN87     DOWEQ*OFF                       B*1            CEU017
      *                                                                   CEU017
      ** Delete the Securities record of the current Bulk Reference       CEU017
      *                                                                   CEU017
     C                     DELETSECORSD0                                  CEU017
      *                                                                   CEU017
      ** Read next Securities record for current Bulk Reference           CEU017
      *                                                                   CEU017
     C           NBBLKR    READESECORSL0                 87               CEU017
     C                     ENDDO                           E*1            CEU017
      *                                                                   CEU017
     C                     ENDSR                                          CEU017
      *****************************************************************   CEU017
      /EJECT                                                              CEU017
      *****************************************************************   CEU017
      *                                                               *   CEU017
      *            SRODP2 - Output Details to Printer File (P2)       *   CEU017
      *                                                               *   CEU017
      *  CALLS            -                                           *   CEU017
      *                                                               *   CEU017
      *  CALLED BY SRMAIN - Main processing                           *   CEU017
      *                                                               *   CEU017
      *  FLDS USED        -                                           *   CEU017
      *                                                               *   CEU017
      *****************************************************************   CEU017
     C           SRODP2    BEGSR                                          CEU017
      *                                                                   CEU017
      ** Bulk Reference                                                   CEU017
      *                                                                   CEU017
     C                     MOVELNBBLKR    P2REFR                          CEU017
      *                                                                   CEU017
      ** Narrative                                                        CEU017
      *                                                                   CEU017
     C                     MOVELNBBLKN    P2NARR                          CEU017
      *                                                                   CEU017
      ** CA Ex-Date                                                       CEU017
      *                                                                   CEU017
     C                     MOVE NBCOEX    ZDAYNO                          CEU017
     C                     EXSR ZDATE2                                    CEU017
     C                     MOVE ZADATE    P2EXDT                          CEU017
      *                                                                   CEU017
      ** CA Allocation Effective Date                                     CEU017
      *                                                                   CEU017
     C                     MOVE NBALEF    ZDAYNO                          CEU017
     C                     EXSR ZDATE2                                    CEU017
     C                     MOVE ZADATE    P2AEDT                          CEU017
      *                                                                   CEU017
      ** Payment Date                                                     CEU017
      *                                                                   CEU017
     C                     MOVE NBCOPD    ZDAYNO                          CEU017
     C                     EXSR ZDATE2                                    CEU017
     C                     MOVE ZADATE    P2PMDT                          CEU017
      *                                                                   CEU017
      ** Event Date                                                       CEU017
      *                                                                   CEU017
     C                     MOVE NBEVDT    ZDAYNO                          CEU017
     C                     EXSR ZDATE2                                    CEU017
     C                     MOVE ZADATE    P2EVDT                          CEU017
      *                                                                   CEU017
      ** Corporate Action Type                                            CEU017
      *                                                                   CEU017
     C                     MOVE NBCOAT    P2TYPE                          CEU017
      *                                                                   CEU017
      ** Write a record into the printer file                             CEU017
      *                                                                   CEU017
     C                     WRITEDETAIL2                                   CEU017
      *                                                                   CEU017
     C                     ENDSR                                          CEU017
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            AUDIT  - Subroutine to Output Audit report         *
      *                                                               *
      *  CALLS            -                                           *
      *                                                               *
      *  CALLED BY        - Main processing, *PSSR                    *
      *                                                               *
      *  FLDS USED        -                                           *
      *                                                               *
      *****************************************************************
      *
     C           AUDIT     BEGSR                           *** AUDIT  ***
      *
      ***  Output Report Header
      *
     C                     WRITEHEADAU
      *
      ***  If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
     C                     WRITEDBERROR
     C                     ELSE
      *
      ***  Or, if no records read, Output "No Details" message.
      *
     C           WWNCAD    IFEQ *ZERO
     C           WWNBLK    ANDEQ*ZERO                                     CEU017
     C                     WRITENODTLS                     E*1
     C                     ELSE
      *
      ***  Normal audit  exit
      *
     C                     Z-ADDWWNCAD    PPNCAD
     C                     Z-ADDWWNBLK    PPNBLK                          CEU017
     C                     WRITECONTROL
     C                     ENDIF
      *
     C                     ENDIF                           U7 = *ON
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            *PSSR  - Standard Data Base Error Processing.      *
      *                                                               *
      *  CALLS            -                                           *
      *                                                               *
      *  CALLED BY SRINIT - Initial Processing                        *
      *                                                               *
      *  FLDS USED        -                                           *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR
      *
     C                     SETON                     U7U8
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     EXSR RCFAU
     C                     MOVE 'Y'       WRUN    1
     C                     DUMP
     C                     ENDIF
      *
     C                     EXSR AUDIT
      *
      ***  If *PSSR already run, then just end the program here.
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            RCFP1  - Subroutine to register P1 report via RCF  *
      *                                                               *
      *  CALLS     ZSFILE - Report Spool File                         *
      *                                                               *
      *  CALLED BY SRMAIN - Main Processing                           *
      *                                                               *
      *  FLDS USED        -                                           *
      *                                                               *
      *****************************************************************
     C           RCFP1     BEGSR
      *
      **  Ensure report spool file is recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
     C                     CALL 'ZSFILE'
     C                     PARM           SEQ     5
     C                     PARM *BLANKS   @PARM   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      **  Error during ZSFILE processing, return to calling program.
      *
     C           ZSERR     IFEQ 'Y'                        B*1
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     END                             E*1
      *
     C                     ENDSR
      *****************************************************************   CEU017
      /EJECT                                                              CEU017
      *****************************************************************   CEU017
      *                                                               *   CEU017
      *            RCFP2  - Subroutine to register P2 report via RCF  *   CEU017
      *                                                               *   CEU017
      *  CALLS     ZSFILE - Report Spool File                         *   CEU017
      *                                                               *   CEU017
      *  CALLED BY SRMAIN - Main Processing                           *   CEU017
      *                                                               *   CEU017
      *  FLDS USED        -                                           *   CEU017
      *                                                               *   CEU017
      *****************************************************************   CEU017
     C           RCFP2     BEGSR                                          CEU017
      *                                                                   CEU017
      **  Ensure report spool file is recorded by RCF.                    CEU017
      *                                                                   CEU017
     C                     Z-ADDSFNUM2    ZSNUM   60                      CEU017
     C                     CALL 'ZSFILE'                                  CEU017
     C                     PARM           SEQ     5                       CEU017
     C                     PARM *BLANKS   @PARM   3                       CEU017
     C                     PARM           SFILE2                          CEU017
     C                     PARM           ZSNUM                           CEU017
     C                     PARM           ZSERR   1                       CEU017
      *                                                                   CEU017
      **  Error during ZSFILE processing, return to calling program.      CEU017
      *                                                                   CEU017
     C           ZSERR     IFEQ 'Y'                        B*1            CEU017
     C                     SETON                     U7U8LR               CEU017
     C                     RETRN                                          CEU017
     C                     END                             E*1            CEU017
      *                                                                   CEU017
     C                     ENDSR                                          CEU017
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            RCFAU  - Subroutine to register AU report via RCF  *
      *                                                               *
      *  CALLS     ZSFILE - Audit Spool File                          *
      *                                                               *
      *  CALLED BY SRINIT - Initial Processing                        *
      *                                                               *
      *  FLDS USED        -                                           *
      *                                                               *
      *****************************************************************
     C           RCFAU     BEGSR
      *
      **   Ensure audit spool file is recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
     C                     CALL 'ZSFILE'
     C                     PARM           SEQ
     C                     PARM *BLANKS   @PARM
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM           ZSERR
      *
      **  Error during ZSFILE processing, return to calling program.
      *
     C           ZSERR     IFEQ 'Y'                        B*1
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF                           E*1
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
     C/COPY ZSRSRC,ZDATE2Z2
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
