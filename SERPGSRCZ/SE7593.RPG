     H        1
      *****************************************************************
/*OVRF*: OVRDBF FILE(SECTYZ1) TOFILE(SECTY) SHARE(*NO)              : *
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Corporate actions - depot maintenance')       *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7593  - SE Corporate Actions - Depot Maintenance           *
      *                                                               *
      *  Function:  Used to maintain details of allocations per       *
      *  (I/C)      depot.                                            *
      *                                                               *
      *  BE CARREFUL: All amendments of calculations made in this     *
      *               program must obligatorily be done in RPG/       *
      *               SE7550: Midas SE Corporate Actions Automatic    *
      *               Allocation Processing |                         *
      *               Some of those amendments must perhaps be done   *
      *               in RPG/SE7513: Midas SE Corporate Actions       *
      *               Customer/Book by Depot Maintenance, depending   *
      *               on calculations used in this program |          *
      *                                                               *
      *  Called By: SE7593  - Midas SE Corporate Actions - Depot      *
      *                       Selection.                              *
      *             SE7592  - Midas SE Corporate Actions - Depot      *
      *                       Select Option.                          *
      *                                                               *
      *  Calls: SE0040   - Midas SE Security detail maintenance.      *
      *         SE7513   - Midas SE CO Customer/Book by Depot         *
      *                    Maintenance.                               *
      *         Y2CLMSC  - Midas ABS Clear specific program message   *
      *                    queue.                                     *
      *         Y2SNMGC  - Midas ABS Send a message SNDxxxMSG.        *
      *         AOBANKR0 - Midas AO Bank ICD access object.           *
      *         AOCUSTR0 - Midas AO Client details access object.     *
      *         AOCURRR0 - Midas AO Currency codes access object.     *
      *         AOSTRDR0 - Access Program for Securities Trading      *   136935
      *                    Details.                                   *   136935
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CSE071             Date 19Jul05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 190204             Date 31May01               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CAP137             Date 07Feb00               *
      *                 CSE020             Date 21Mar00               *
      *                 161732             Date 21Mar00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 155856             Date 23Feb99               *
      *                 153731             Date 21Jan99               *
      *                 138957             Date 16Jul98               *
      *                 137884             Date 14Jul98               *
      *                 136935             Date 29Jun98               *
      *                 136930             Date 17Jun98               *
      *                 135690             Date 08Jun98               *
      *                 135686             Date 05Jun98               *
      *                 135685             Date 28May98               *
      *                 CEU017             Date 05Mar98               *
      *                 CSE007  *CREATE    Date 01Dec97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  190204 - Changed Dividend per Unit format from 9N0 to 13N8.  *
      *  CAP137 - Conversion of SE Security inputs into modular       *
      *           structure to use as APIs.                           *
      *  CSE020 - Corporate Actions Domino Effect Processing          *
      *  161732 - Corporate Actions Exercise and Conversion -         *
      *           Part Stock/Keep Rest Calculation                    *
      *  155856 - Dbase Error 025 File SECOALL7 when cash amount is   *
      *           amended.                                            *
      *  153731 - Corporate Actions: Incorrect Record Number in       *
      *           Subfile SFLRCDNBR field                             *
      *  138957 - Corporate Actions: Review information passed to     *
      *           RPG/SE0040 (Security Details Maintenance) for a     *
      *           Name Change event where the Security Shortname is   *
      *           being changed (when F15 is pressed).                *
      *  137884 - Corporate Actions: Array Index Error when F5 pressed*
      *  136935 - Corporate Actions: Allocation processing amendments:*
      *           1. Amend calculations for Offers.                   *
      *           2. Input Balancing Difference on Dummy Customer if  *
      *              Currency Change or depending if field 'Allocate  *
      *              Difference to Dummy' = 'Y' on Securities Trading *
      *              ICD.                                             *
      *           3. For Reinvestment of Dividend, if Compensation    *
      *              Price entered, use Compensation Price instead of *
      *              Subscription Price, otherwise, calculate Sub-    *
      *              scription Price = Subscription Price minus Dis-  *
      *              count Rate.                                      *
      *  136930 - EMU and Corporate Actions: various errors:          *
      *           - Blink on screen Ex-Date Position if different     *
      *             from Allocation detail file and Depot Position    *
      *             file.                                             *
      *  135690 - Various errors for Corporate Actions:               *
      *           1. If Processing Type = Name Changes, New Security  *
      *              always equal Event Security, therefore, if       *
      *              Security Shortname entered on CO detail input,   *
      *              no Detail was retrieved from Depot allocation    *
      *              file and New Security displayed and transmitted  *
      *              to RPG/SE7513 was Event Security and no detail   *
      *              was retrieve on Allocation detail file on SE7513.*
      *              Security from CO detail file entered, process    *
      *              with this Security.                              *
      *           2. Due to Fix 135689: Error in validation of        *
      *              Dividend per Unit, amend number of decimal       *
      *              places of Dividend per Unit for Dividend Events, *
      *              from New Currency instead of New Security.       *
      *  135686 - Corporate Actions and EMU                           *
      *           1. Due database error, don't check if Actual Stock  *
      *              Allocation entered is on Denomination Multiplier *
      *              precision if processing type = CE, NC or NF      *
      *           2. Amend wrong calculation on Actual Stock          *
      *              Allocation: if with Denomination Multiplier      *
      *              precision, no Decimal Places used, so retrieve   *
      *              New Security Decimal Places.                     *
      *  135685 - It's not feasible to enter the Compensation Price   *
      *           at the Bulk level as the Bulk will have many        *
      *           Securities all giving a cash compensation at a      *
      *           different level. Therefore, allow the entry of      *
      *           'MARKET' which will use the Market Price of the     *
      *           Security at allocation effective date.              *
      *  CEU017 - Securities Redenomination Euro Changes              *
      *  CSE007 - Corporate Actions                                   *
      *                                                               *
      *****************************************************************
      *
      ***  Midas SE Securities                                           - Prefixe
      *
     FSECTYZ1 IF  E           K        DISK         KINFSR *PSSR
      *
      ***  Midas SE Corporate Actions - Depots live records              - Prefixe MB
      *
     FSECODPL3UF  E           K        DISK         KINFSR *PSSR A
     F                                              KCOMIT
      *
      ***  Midas SE Corporate Actions - Depots Upd idx                   - Prefixe MS
      *
     FSECODRL0UF  E           K        DISK         KINFSR *PSSR A
     F                                              KCOMIT
      *
      ***  Midas SE Corporate Actions - Dividends Events                 - Prefixe MD
      *
     FSECODVL1IF  E           K        DISK         KINFSR *PSSR
      *
      ***  Midas SE Corporate Actions - Free Allocations                 - Prefixe ME
      *
     FSECOFRL1IF  E           K        DISK         KINFSR *PSSR
      *
      ***  Midas SE Corporate Actions - Rights                           - Prefixe MF
      *
     FSECORGL1IF  E           K        DISK         KINFSR *PSSR
      *
      ***  Midas SE Corporate Actions - Capital Events                   - Prefixe MG
      *
     FSECOCEL1IF  E           K        DISK         KINFSR *PSSR
      *
      ***  Midas SE Corporate Actions - Corp. Reorganisation             - Prefixe MH
      *
     FSECOCRL1IF  E           K        DISK         KINFSR *PSSR
      *
      ***  Midas SE Corporate Actions - Exchange Offers                  - Prefixe MI
      *
     FSECOOFL1IF  E           K        DISK         KINFSR *PSSR
      *
      ***  Midas SE Corporate Actions - Exercice & Conversion            - Prefixe MJ
      *
     FSECOEXL1IF  E           K        DISK         KINFSR *PSSR
      *                                                                   135690
      ***  Midas SE Corporate Actions - Exercice & Conversion             135690
      ***                                                    - Prefixe ML 135690
      *                                                                   135690
     FSECONCL1IF  E           K        DISK         KINFSR *PSSR          135690
      *                                                                   CEU017
      ***  Midas SE Corporate Actions - Currency Changes                  CEU017
      ***                                                    - Prefixe NA CEU017
      *                                                                   CEU017
     FSECOCCL2IF  E           K        DISK         KINFSR *PSSR          CEU017
      *
      ***  Midas SE Market Prices file.                                  - Prefixe
      *
     FPRICM   IF  E           K        DISK         KINFSR *PSSR
      *                                                                   135685
      ***  MA - SE CORPORATE ACTIONS References - Rtv Idx                 135685
      *                                                                   135685
     FSECORFL1IF  E           K        DISK                               135685
     F                                              KINFSR *PSSR          135685
      *                                                                   CSE020
      ** Midas SE Corporate Actions - References Upd Idx                  CSE020
      *                                                                   CSE020
     FSECORFL9IF  E           K        DISK                               CSE020
     F            SECORFD0                          KRENAMESECORFX0       CSE020
     F                                              KINFSR *PSSR          CSE020
      *                                                                   136930
      ***  Midas SE Depot Positions - Current and Historic                136930
      *                                                                   136930
     FSEDPOHL0IF  E           K        DISK                               136930
     F                                              KINFSR *PSSR          136930
      *                                                                   136935
      ***  SE Corpotrate Actions - Allocat. Live Records     - Prefixe MC 136935
      *                                                                   136935
     FSECOALL7UF  E           K        DISK         KINFSR *PSSR          136935
     F                                              KCOMIT                136935
      *                                                                   CSE020
      ** SE Corporate Actions - Stock Allocations                         CSE020
      *                                                                   CSE020
     FSECOALL5IF  E           K        DISK                               CSE020
     F            SECOALD0                          KRENAMESECOALD5       CSE020
     F                                              KINFSR *PSSR          CSE020
      *
      ***  Display file.
      *
     FSE7593DFCF  E                    WORKSTN      KINFSR *PSSR
     F                                              KINFDS SCRDS
     F                                        #1RRN KSFILE SE7593S1
      *
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *  KC    F3: Exit pressed.                               (*ON)  *
      *  KE    F5: Refresh pressed.                            (*ON)  *
      *  KL    F12: Previous screen pressed.                   (*ON)  *
      *                                                               *
      *  20    F20: 'Exit without saving' pressed.             (*ON)  *
      *  25    - Rollup pressed on subfile,                    (*ON)  *
      *        - used in program message subfile.                     *
      *  30    Chain failed on LF/SECTY.                       (*ON)  *
      *  31    - Chain failed on LF/SECODPL3,                  (*ON)  *
      *          subfile end (SE7593C1),                              *
      *        - Allow Rollup.                                 (*OFF) *
      *  33    Chain failed on LF/SECODPL3.                    (*ON)  *
      *  34    Record changed on subfile SE7593S1.             (*ON)  *
      *  35    Chain failed on LF/SECODRL0.                    (*ON)  *
      *  36    Chain failed on LF/SECODPL3.                    (*ON)  *
      *        or on SECOALL7.                                 (*ON)  *   136935
      *  40    - Enquiry mode (protect all screen fields,      (*ON)  *
      *        - Amend mode.                                   (*OFF) *
      *  44    Record not found on LF/PRICM.                   (*ON)  *
      *  45    - 'CASH' line on sufile SE7593S1:               (*ON)  *
      *          display fields Actual 1 and Bal.Dif.1,               *
      *          non protect field Actual 3,                          *
      *          non display fields Actual 4, Bal.Dif.2-3-4.          *
      *        - New Security line on sufile SE7593S1:         (*OFF) *
      *          non display fields Actual 1 and Bal.Dif.1,           *
      *          protect field Actual 3,                              *
      *          display fields Actual 4, Bal.Dif.2-3-4.              *
      *  58    Field DDACN3: Action Code in error on subfile   (*ON)  *
      *        SE7593S1.                                              *
      *  59    Field DDACT2: Actual 2 in error on subfile      (*ON)  *
      *        SE7593S1, field DDEXIT: 'Confirm exit without          *
      *        saving' in error on record format SE7593F2.            *
      *  60    Field DDACT3: Actual 3 in error on subfile      (*ON)  *
      *        SE7593S1.                                              *
      *  61    Field DDACT4: Actual 4 in error on subfile      (*ON)  *
      *        SE7593S1.                                              *
      *  62    Blink Ex-Date Position on screen if different   (*ON)  *   136930
      *        from Depot Position and Allocation.                    *   136930
      *  82    - Clear subfile SE7593S1                        (*ON)  *
      *        - Display subfile control SE7593C1.             (*OFF) *
      *  83    Display subfile SE7593S1 = record number > 0.   (*ON)  *
      *  85    SFLNXTCHG on subfile.                           (*ON)  *
      *  89    Record not found on file SECOxxL1 (where xx =   (*ON)  *
      *        'DV', 'FR', 'RG', 'CE', 'CR', 'OF' or 'EX'),           *
      *        SECOCCL2 or on subfile SE7593S1.                       *
      *                                                               *
      *  90    Used in standart subroutine ZDATE2 and ZASIGN.         *
      *  91    Used in standart subroutine ZDATE2 and ZASIGN.         *
      *  92    Used in standart subroutine ZDATE2 and ZASIGN.         *
      *  93    Used in standart subroutine ZDATE2 and ZASIGN.         *
      *  94    Used in standart subroutine ZDATE2.                    *
      *  95    Used in standart subroutine ZDATE2.                    *
      *  96    Used in standart subroutine ZASIGN.                    *
      *  97    Used in standart subroutine ZASIGN.                    *
      *  98    Date in format DDMMYY,                          (*OFF) *
      *        Date in formt MMDDYY,                           (*ON)  *
      *        (used in standart subroutines ZDATE1 and ZDATE2).      *
      *  99    - Used in standart subroutine ZASIGN = field           *
      *          not numeric,                                  (*ON)  *
      *        - Used in standart subroutines ZDATE1 and ZDATE2.       *
      *                                                               *
      *  LR    End Program indicator.                          (*ON)  *
      *  U7-U8 Database error indicator.                       (*ON)  *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  P001   - Initial processing.                                 *
      *  P002   - Detail processing.                                  *
      *  P003   - Termination processing.                             *
      *  P004   - Build the subfile (Amend Mode).                     *
      *  P005   - Validation of the subfile (Amend Mode).             *
      *  P006   - Validate and perform Action taken (Amend Mode).     *
      *  P007   - Process Action.                                     *
      *  P008   - Update/write LF/SECODRL0 (Amend Mode).              *
      *  P009   - Move file fields to screen fields for New Security. *
      *  P010   - Move file fields to screen fields for Security      *
      *           '**CASH**'.                                         *
      *  V001   - Validate details input (Amend Mode).                *
      *  REASTR - Recalculate Actual Stock Rounding for New Security  *
      *           on Security line.                                   *
      *  REACAR - Recalculate Actual Cash Rounding for New Security   *
      *           on Security line.                                   *
      *  REACAA - Recalculate Actual Cash Allocation for Security     *
      *           '**CASH**' on Security line.                        *
      *  U001   - Load field for file SECODRL0.                       *
      *  U002   - Load fields for file SECODPL3 with all amounts = 0. *
      *  ACCSEC - Access Security details file.                       *
      *  *PSSR  - Error processing.                                   *
      *  ZASNMS - Send message to program message queue.              *
      *  SETNDP - Set amount from num field 30.9 to num field 15.0    *
      *           with Number of Decimal Places from a Currency.      *
      *  RTVNDP - Set amount from num field 15.0 to num field 30.9    *
      *           with Retrieve Number of Decimal Places from a Ccy.  *
      *  ROUNDD - Perform rounding to the Number of Decimal Places.   *
      *  CSBPR  - Calculate Subscription Price if Subscription Price  *
      *           entered as Market for Dividend Events.              *
      *  ZDATE2 - Standart subroutine to convert a Day Number to      *
      *           date formats.                                       *
      *  ZSEDIT - Standart subroutine to insert a decimal point into  *
      *           a numeric field and to blank out leading zeroes.    *
      *  ZASIGN - Standart subroutine to validate and right-align     *
      *           signed numeric fields.                              *
      *  ZDATE1 - Standart subroutine to validate dates submitted and *
      *           convert to a number of days.                        *
      *                                                               *
      *****************************************************************
      /EJECT
      *
      ***  Array containing text messages for command keys.
      *
     E                    TXT     1   5 78
      *
      ***  Array containing titles for subfile lines.
      *
     E                    CNSARR  1   6 17
      *
      ***  Array for Security Shortnames.
      *
     E                    SSARR      20 10
      *
      ***  Array to retrieve New Securities (20 potential) from SECOCRL1.
      *
     E                    TSEC       20 10
      *
      ***  Array to retrieve Number of New Shares (20 potential) from SECOCRL1.
      *
     E                    TNSH       20 16
      *
      ***  Array to retrieve Rounding (20 potential) from SECOCRL1.
      *
     E                    TNDN       20  1
      *
      ***  Array to retrieve Rounding Settlement in Cash (20 potential) from SECOCRL1.
      *
     E                    TNDS       20  1
      *
      ***  Array to retrieve Compensation Price (20 potential) from SECOCRL1.
      *
     E                    TOPA       20 16
      *
      ***  Arrays to retrieve Denomination Multiplier (20 potential) from SECOCRL1.
      *
     E                    TDML       20 16
      *
      ***  Array containing error narrative when calling PGM.
      *
     E***********         NARR    1   2 29                                CAP137
     E                    NARR    1   3 29                                CAP137
      *
      ***  Arrays ZS1 and ZS2 used by standart subroutine ZSEDIT.
      *
     E/COPY ZSRSRC,ZSEDITZ1
      *
      ***  Arrays used by standart subroutine ZDATE2:
      ***    ZYDY containing Years in days cumulative, four year span,
      ***    ZMDY containing Months in days cumulative through year,
      ***    ZMNM containing Months short name.
      *
     E/COPY ZSRSRC,ZDATE2Z1
      *
      ***  Arrays ZS3, ZS4 and ZS5 used by standart subroutine ZASIGN.
      *
     E/COPY ZSRSRC,ZASIGNZ1
      *
      ***  Array containing Copyright statement.
      *
     E                    CPY@    1   1 80
      /SPACE 3
      *                                                                   CSE020
     ISECOALD5                                                            CSE020
      ** Rename fields of SECOALL5: Midas SE CA - Stock Allocations       CSE020
      *                                                                   CSE020
     I              MCRECI                          M5RECI                CSE020
     I              MCLCD                           M5LCD                 CSE020
     I              MCCHTP                          M5CHTP                CSE020
     I              MCLUID                          M5LUID                CSE020
     I              MCCORF                          M5CORF                CSE020
     I              MCOPTN                          M5OPTN                CSE020
     I              MCBRCD                          M5BRCD                CSE020
     I              MCDDPT                          M5DDPT                CSE020
     I              MCCOTP                          M5COTP                CSE020
     I              MCBOOK                          M5BOOK                CSE020
     I              MCCNUM                          M5CNUM                CSE020
     I              MCPTFR                          M5PTFR                CSE020
     I              MCEXPO                          M5EXPO                CSE020
     I              MCSESN                          M5SESN                CSE020
     I              MCOALR                          M5OALR                CSE020
     I              MCOTSA                          M5OTSA                CSE020
     I              MCOSTR                          M5OSTR                CSE020
     I              MCOCAR                          M5OCAR                CSE020
     I              MCCAOP                          M5CAOP                CSE020
     I              MCASTA                          M5ASTA                CSE020
     I              MCHOST                          M5HOST                CSE020
     I              MCHOCA                          M5HOCA                CSE020
     I              MCACAA                          M5ACAA                CSE020
     I              MCASTR                          M5ASTR                CSE020
     I              MCACAR                          M5ACAR                CSE020
     I              MCOPT1                          M5OPT1                CSE020
     I              MCOPT2                          M5OPT2                CSE020
     I              MCREST                          M5REST                CSE020
     I              MCBSEC                          M5BSEC                CSE020
     I              MCBEDT                          M5BEDT                CSE020
     I              MCBSTT                          M5BSTT                CSE020
     I              MCBLTY                          M5BLTY                CSE020
     I              MCBEWI                          M5BEWI                CSE020
     I              MCCOAT                          M5COAT                CSE020
     I              MCTRRF                          M5TRRF                CSE020
     I              MCTRR2                          M5TRR2                CSE020
     I              MCTRR3                          M5TRR3                CSE020
     I              MCRVRF                          M5RVRF                CSE020
     I              MCRVR2                          M5RVR2                CSE020
     I              MCRVR3                          M5RVR3                CSE020
     I              MCTNL1                          M5TNL1                CSE020
     I              MCLTRQ                          M5LTRQ                CSE020
     I              MCTOTF                          M5TOTF                CSE020
     I              MCTOTT                          M5TOTT                CSE020
      *
      ***  Rename fields of SECOOFL1: Midas SE Corporate Actions - Offers.
      *
     ISECOOFD0
     I              MIRECI                          MDRECI
     I              MINSHA                          MDNSHA
     I              MIOSHA                          MDOSHA
     I              MIRNDN                          MDRNDN
     I              MIRNDS                          MDRNDS
     I              MICOMP                          MDCOMP
     I              MIDMLT                          MDDMLT
      *
      ***  Rename fields of SECORGL1: Midas SE Corporate Actions - Rights Issues.
      *
     ISECORGD0
     I              MFRECI                          MDRECI
     I              MFNSHA                          MDNSHA
     I              MFOSHA                          MDOSHA
     I              MFRNDN                          MDRNDN
     I              MFRNDS                          MDRNDS
     I              MFCOMP                          MDCOMP
     I              MFDMLT                          MDDMLT
      *
      ***  Rename fields of SECOEXL1: Midas SE Corporate Actions - Exercices and Conversions.
      *
     ISECOEXD0
     I              MJRECI                          MDRECI
     I              MJNSHA                          MDNSHA
     I              MJOSHA                          MDOSHA
     I              MJRNDN                          MDRNDN
     I              MJRNDS                          MDRNDS
     I              MJCOMP                          MDCOMP
     I              MJSUPR                          MDSBPR
     I              MJDMLT                          MDDMLT
     I              MJRCPR                          MDRCPR                161732
      *
      ***  Rename fields of SECOCRL1: Midas SE Corporate Actions - Corporate Reorganisation.
      *
     ISECOCRD0
     I              MHRECI                          MDRECI
     I              MHOSHA                          MDOSHA
      *
      ***  Rename fields of SECOFRL1: Midas SE Corporate Actions - Free Allocation.
      *
     ISECOFRD0
     I              MERECI                          MDRECI
     I              MENSHA                          MDNSHA
     I              MEOSHA                          MDOSHA
     I              MERNDN                          MDRNDN
     I              MERNDS                          MDRNDS
     I              MECOMP                          MDCOMP
     I              MEDMLT                          MDDMLT
      *
      ***  Rename fields of SECOCEL1: Midas SE Corporate Actions - Capital Changes.
      *
     ISECOCED0
     I              MGRECI                          MDRECI
      *                                                                   CEU017
      ***  Rename fields of SECOCCL2: Midas SE Corporate Actions -        CEU017
      ***                             Currency Changes.                   CEU017
      *                                                                   CEU017
     ISECOCCD0                                                            CEU017
     I              NARECI                          MDRECI                CEU017
     I              NANNSZ                          MDNSHA                CEU017
     I              NAONSZ                          MDOSHA                CEU017
     I              NARNDN                          MDRNDN                CEU017
     I              NARNDS                          MDRNDS                CEU017
     I              NACOMP                          MDCOMP                CEU017
     I              NADMLT                          MDDMLT                CEU017
      *                                                                   CSE020
      ** Rename fields of SECORFL9: Corporate Actions Reference File      CSE020
      *                                                                   CSE020
     ISECORFX0                                                            CSE020
     I              MARECI                          MNRECI                CSE020
     I              MALCD                           MNLCD                 CSE020
     I              MACHTP                          MNCHTP                CSE020
     I              MALUID                          MNLUID                CSE020
     I              MACORF                          MNCORF                CSE020
     I              MAJOBN                          MNJOBN                CSE020
     I              MAUSER                          MNUSER                CSE020
     I              MAJNBR                          MNJNBR                CSE020
     I              MASESN                          MNSESN                CSE020
     I              MACOAT                          MNCOAT                CSE020
     I              MAPTYP                          MNPTYP                CSE020
     I              MACOAN                          MNCOAN                CSE020
     I              MACOEX                          MNCOEX                CSE020
     I              MAALEF                          MNALEF                CSE020
     I              MANBOP                          MNNBOP                CSE020
     I              MATDVD                          MNTDVD                CSE020
     I              MACOPD                          MNCOPD                CSE020
     I              MATDDT                          MNTDDT                CSE020
     I              MAEVDT                          MNEVDT                CSE020
     I              MACPNB                          MNCPNB                CSE020
     I              MASHMD                          MNSHMD                CSE020
     I              MALTBS                          MNLTBS                CSE020
     I              MAOCPV                          MNOCPV                CSE020
     I              MANCPV                          MNNCPV                CSE020
     I              MAONML                          MNONML                CSE020
     I              MANNML                          MNNNML                CSE020
     I              MACUPC                          MNCUPC                CSE020
     I              MAEXPC                          MNEXPC                CSE020
     I              MACLRD                          MNCLRD                CSE020
     I              MACLRT                          MNCLRT                CSE020
     I              MADLRD                          MNDLRD                CSE020
     I              MADLRT                          MNDLRT                CSE020
     I              MABLOS                          MNBLOS                CSE020
     I              MABLOP                          MNBLOP                CSE020
     I              MABFRD                          MNBFRD                CSE020
     I              MABEND                          MNBEND                CSE020
     I              MAREFO                          MNREFO                CSE020
     I              MACONR                          MNCONR                CSE020
     I              MADFNR                          MNDFNR                CSE020
     I              MADFCP                          MNDFCP                CSE020
     I              MADFSP                          MNDFSP                CSE020
     I              MALREQ                          MNLREQ                CSE020
     I              MALPRO                          MNLPRO                CSE020
     I              MALTSI                          MNLTSI                CSE020
     I              MASTAT                          MNSTAT                CSE020
     I              MAPREF                          MNPREF                CSE020
      *
      ***  Display File Status Data Structure.
      *
     ISCRDS       DS
     I                                    B 378 3790CPFPOS
      *
      ***  Data structure for compilation - Copyright insertion.
      *
     ICPYR@$      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@$$
      *
      ***  Program status data structure.
      *
     IPSDS       SDS
     I                                     *PROGRAM ##PGM
     I                                      244 253 JOB
     I                                      254 263 USER
      *
      ***  Local data area.
      ***  Local data area for database error details
      ***  *LOCK IN LDA immediately before and OUT LDA immediately
      ***  after each database error handling.
      *
     ILDA         DS                            256
     I                                      134 141 WWFILE
     I                                      142 170 WWKEY
     I                                      171 180 WWPGM
     I                                      181 1830WWASE
      *
      ***  External data structures for Bank Details                     - Prefixe BJ.
      *
     ISDBANK    E DSSDBANKPD
      *
      ***  External data structures for Customer Details                 - Prefixe BB.
      *
     ISDCUST    E DSSDCUSTPD
      *
      ***  External data structure for Currency details                  - Prefixe A6.
      *
     ISDCURR    E DSSDCURRPD
      *                                                                   CAP137
      ** Externally described DS for SAR details                          CAP137
     ISCSARD    E DSSCSARDPD                                              CAP137
      *
      ***  First Data Strusture for Access Programs, Long Data Structure.
      *
     IDSSDY     E DSDSSDY
      *
      ***  Second Data Strusture for Access Programs, Short Data Structure.
      *
     IDSFDY     E DSDSFDY
      *                                                                   136935
      ***  Data structure for Securities Trading Details.    - Prefixe BV 136935
      *                                                                   136935
     ISDSTRD    E DSSDSTRDPD                                              136935
      *
      ***  Data structure for fields WORK15 used by standart subroutine ZSEDIT.
      *
     I/COPY ZSRSRC,ZSEDITZ2
      /EJECT
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***  Initial processing.
      *
     C                     EXSR P001
      *
      ***  Detail processing.
      *
     C                     EXSR P002
      *
      ***  Termination processing.
      *
     C                     EXSR P003
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
      /EJECT
      *****************************************************************
      *                                                               *
      *  P001   - Initial processing.                                 *
      *                                                               *
      *  Called by: Main processing.                                  *
      *                                                               *
      *  Calls: ACCSEC   - Access Security details file.              *
      *         *PSSR    - Error processing.                          *
      *         ZDATE2   - Standart subroutine to convert a Day       *
      *                    Number to date formats.                    *
      *         AOBANKR0 - Midas AO Bank ICD access object.           *
      *         AOCUSTR0 - Midas AO Client details access object.     *
      *         AOCURRR0 - Midas AO Currency codes access object.     *
      *                                                               *
      *****************************************************************
      *
     C           P001      BEGSR                           ** P001 SR **
      *
      ***  Setup Copyright statement.
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ***  Entry parameters.
      *
     C           *ENTRY    PLIST
     C                     PARM           WWREFN  6        CO Reference
     C                     PARM           WWSECN 10        Security Shortname
     C                     PARM           WWCOAT  2        Corporate Action Type
     C                     PARM           WWPTYP  2        Processing Type
     C                     PARM           WWEXDT  5        Ex-Date
     C                     PARM           WWOPTN  2        Option Number
     C                     PARM           WWACTN  1        Action 'A' or 'E'
     C                     PARM           WWBRCD  3        Branch
     C                     PARM           WWDEPT  6        Depot
     C                     PARM           WWEXPO 17        Ex-date position
     C                     PARM           WWCOAD  6        Last Allocation Date
     C                     PARM           WWSTAT  1        Corporate Action Stat
     C                     PARM           WWFINA  1        Final Allocation Indi
     C                     PARM           WWNCCY  3        Nominal Currency
     C                     PARM           WWUPDT  1        Update done ?
     C                     PARM           WWRTCD  1        Return Code
      *
      ***  Setup key list to LF/SECODRL0.
      *
     C           KEY1      KLIST
     C                     KFLD           WWREFN           CO Reference (Parameter)
     C                     KFLD           WWDSBA  3        Branch
     C**********           KFLD           WWDDPT  60       Depot (parameter, num field)       CSD027
     C                     KFLD           WWDDPT  6        Depot (parameter, num field)       CSD027
      *
      ***  Setup key list to LF/SECODPL3.
      *
     C           KEY2      KLIST
     C                     KFLD           WWREFN           CO Reference (parameter)
     C                     KFLD           WWOPNB           Option Number (parameter, num field)
     C                     KFLD           WWBRCA  3        Branch
     C**********           KFLD           WWDPOT  60       Depot                              CSD027
     C                     KFLD           WWDPOT  6        Depot                              CSD027
     C                     KFLD           WWSECS 10        Security
      *
      ***  Setup key list to LF/SECOxxL1.
      *
     C           KEY3      KLIST
     C                     KFLD           WWREFN           CO Reference (parameter)
     C                     KFLD           WWOPNB           Option Number (parameter, num field)
      *
      ***  Setup key list to LF/PRICM.
      *
     C           KEY4      KLIST
     C                     KFLD           MDNSEC           New Security from SECODVL1
     C                     KFLD           KPVDT   50       Value Date
      *                                                                   136930
      ***  Setup key list to LF/SEDPOHL0.                                 136930
      *                                                                   136930
     C           KEY6      KLIST                                          136930
     C                     KFLD           WWBRCD                          136930
     C                     KFLD           WWDDPT                          136930
     C                     KFLD           WWSECN                          136930
     C                     KFLD           KEXDT   50                      136930
      *                                                                   136935
      ***  Setup key list to LF/SECOALL7.                                 136935
      *                                                                   136935
     C           KEY7      KLIST                                          136935
     C                     KFLD           WWREFN           CO Reference   136935
     C                     KFLD           WWOPNB           Option Number  136935
     C                     KFLD           WWBRCD           Branch         136935
     C                     KFLD           WWDDPT           Depot          136935
     C                     KFLD           WWSECS           Security       136935
     C                     KFLD           WWCOTP  1        Cust./Book Ind.136935
     C                     KFLD           WWBOOK  2        Book           136935
     C**********           KFLD           WWERCU  60       Dummy Cust.                 136935 CSD027
     C                     KFLD           WWERCU  6        Dummy Cust.                        CSD027
     C                     KFLD           WWPTFR  4        Portfolio      136935
      *                                                                   CSE020
      ** Setup key list to LF/SECOALL5.                                   CSE020
      *                                                                   CSE020
     C           KEY8      KLIST                                          CSE020
     C                     KFLD           MAPREF                          CSE020
     C                     KFLD           DSBA                            CSE020
     C                     KFLD           DDPT                            CSE020
      *
      ***  Setup work and key fields.
      *
     C                     MOVEL'SE7593  'DBPGM  10        PGM name for database error proc.
     C                     MOVE 'L'       ZECODE           Edit code used in SR ZSEDIT
     C                     MOVE 'N'       WWUPDT           Update done ?
     C**********           MOVE WWDEPT    WWDDPT  60       Depot (parameter, num field)       CSD027
     C                     MOVE WWDEPT    WWDDPT  6        Depot (parameter, num field)       CSD027
     C                     MOVE WWEXDT    KEXDT            Ex-Date        136930
     C                     MOVE 'C'       WWCOTP           Cust./Book Ind.136935
     C                     MOVE *BLANKS   WWBOOK           Book           136935
     C                     MOVE *BLANKS   WWPTFR           Portfolio      136935
      *
      ***  If Action Mode = 'E': Enquiry, setup indicator *IN40 to *ON to protect all screen fields.
      *
     C           WWACTN    IFEQ 'E'                        Enquiry Mode
     C                     MOVE '1'       *IN40
     C                     ELSE                            Amend Mode
     C                     MOVE '0'       *IN40
     C                     ENDIF
      *
      ***  If Option Number = '??', retrieve it depending on Processing Type. not for Processing
      ***  Type Name Changes, Non Financial Events where Option Number always = 01.
      ***  And not for Currency Changes.                                  CEU017
      *
     C           WWOPTN    IFEQ '??'
      *
     C                     SELEC
     C           WWPTYP    WHEQ 'DV'                       Dividend Events
     C           WWREFN    SETLLSECODVL1
     C           WWREFN    READESECODVL1                 89
     C                     MOVE MDOPTN    WWOPTN
     C                     MOVEL'SECODVL1'DBFILS
      *
     C           WWPTYP    WHEQ 'FR'                       Free Allocation
     C           WWREFN    SETLLSECOFRL1
     C           WWREFN    READESECOFRL1                 89
     C                     MOVE MEOPTN    WWOPTN
     C                     MOVEL'SECOFRL1'DBFILS
      *
     C           WWPTYP    WHEQ 'CE'                       Capital Changes
     C           WWREFN    SETLLSECOCEL1
     C           WWREFN    READESECOCEL1                 89
     C                     MOVE MGOPTN    WWOPTN
     C                     MOVEL'SECOCEL1'DBFILS
      *
     C           WWPTYP    WHEQ 'CR'                       Corporate Reorganisation
     C           WWREFN    SETLLSECOCRL1
     C           WWREFN    READESECOCRL1                 89
     C                     MOVE MHOPTN    WWOPTN
     C                     MOVEL'SECOCRL1'DBFILS
      *
     C           WWPTYP    WHEQ 'RG'                       Rights Issues
     C           WWREFN    SETLLSECORGL1
     C           WWREFN    READESECORGL1                 89
     C                     MOVE MFOPTN    WWOPTN
     C                     MOVEL'SECORGL1'DBFILS
      *
     C           WWPTYP    WHEQ 'OF'                       Offers
     C           WWREFN    SETLLSECOOFL1
     C           WWREFN    READESECOOFL1                 89
     C                     MOVE MIOPTN    WWOPTN
     C                     MOVEL'SECOOFL1'DBFILS
      *
     C           WWPTYP    WHEQ 'EX'                       Exercices and Conversions
     C           WWREFN    SETLLSECOEXL1
     C           WWREFN    READESECOEXL1                 89
     C                     MOVE MJOPTN    WWOPTN
     C                     MOVEL'SECOEXL1'DBFILS
      *
      ***  If record not found, handle Database Error.
      *
     C           *IN89     IFEQ '1'
     C                     Z-ADD001       DBASE   30       ***************
     C                     MOVELDBFILS    DBFILE 10        * DB ERROR 01 *
     C                     MOVELWWREFN    DBKEY  29        ***************
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ***  If Processing Type = Non Financial Events or Name Chamges, set Option Number to 01.
      ***  For Currency Changes, set it to 01.                            CEU017
      *
     C           WWPTYP    WHEQ 'NC'                       Name Changes
     C                     MOVE '01'      WWOPTN
      *
     C           WWPTYP    WHEQ 'NF'
     C                     MOVE '01'      WWOPTN           Non Financial Events
      *                                                                   CEU017
     C           WWPTYP    WHEQ 'CC'                                      CEU017
     C                     MOVE '01'      WWOPTN           Ccy Changes    CEU017
     C                     ENDSL
      *
     C                     ENDIF
      *
      ***  Setup Option Number retrieve or received as parameter for key field.
      *
     C                     MOVE WWOPTN    WWOPNB  20
      *
      ***  Setup common screen fields.
      *
     C                     MOVE WWREFN    DDREF3           CO Reference
     C                     MOVE WWSECN    DDSEC3           Security Shortname
     C                     MOVE WWCOAT    DDATP3           Corporate Action Type
     C                     MOVE WWOPNB    DDOPT3           Option Number
     C                     MOVELWWDDPT    DDDEP3           Depot
     C                     MOVE WWEXPO    DDEDP3           Ex-date position
     C                     MOVE WWCOAD    DDLTA3           Last Allocation Date
     C                     MOVE WWSTAT    DDSTS3           Corporate Action Status
     C                     MOVE WWFINA    DDFLA3           Final Allocation Indicator
     C                     MOVE WWNCCY    DDNCC3           Nominal Currency
     C                     MOVE WWBRCD    DDBRC3           Branch
     C                     TIME           ##TME   60       Screen time.
      *
     C           WWEXDT    IFEQ *BLANKS
     C           WWEXDT    OREQ *ZEROS
     C                     MOVE *BLANKS   DDEXD3           Ex-Date
     C                     ELSE
     C                     MOVE WWEXDT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     DDEXD3           Ex-Date
     C                     ENDIF
      *
      ***  Access Customer details file to retrieve Depot Name.
      *
     C                     MOVE *BLANKS   WLKEYC
     C                     MOVELWWDDPT    WLKEYC
     C                     CALL 'AOCUSTR0'
     C                     PARM '*DBERR'  WLRTCD
     C                     PARM '*KEY   ' WLOPTN
     C                     PARM           WLKEYC 10
     C                     PARM *BLANKS   WLKYST  7
     C           SDCUST    PARM SDCUST    DSSDY
      *
      ***  If record not found, handle Database Error.
      *
     C           WLRTCD    IFNE *BLANKS
     C                     Z-ADD002       DBASE            ***************
     C                     MOVEL'SDCUSTPD'DBFILE           * DB ERROR 02 *
     C                     MOVELWWDDPT    DBKEY            ***************
     C                     EXSR *PSSR
     C                     ENDIF
     C                     MOVELBBCSSN    DDDEPS           Depot Name
      *
     C                     MOVE TXT,3     ##CTX1           Command keys
      *
      ***  Setup Action Code to blank and reset error indicators of subfile record.
      *
     C                     MOVE *BLANKS   DDACN3
     C                     MOVEA'0000'    *IN,58
      *
      ***  Access Bank Details file.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   WLRTCD  7
     C                     PARM '*FIRST'  WLOPTN  7
     C           SDBANK    PARM SDBANK    DSSDY
      *
      ***  If record not found, handle Database Error.
      *
     C           WLRTCD    IFNE *BLANKS
     C                     Z-ADD003       DBASE            ***************
     C                     MOVEL'SDBANKPD'DBFILE           * DB ERROR 03 *
     C                     MOVEL*BLANKS   DBKEY            ***************
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELUSER      USIDXX           User Id.
     C                     MOVELJOB       WSID             Work Station Name
     C                     MOVE BJMRDT    ##MRDT           Midas run Date
      *
      ***  Check System Date Format, DDMMYY (*IN98 off)
      ***                            MMDDYY (*IN98 on).
      *
     C                     MOVE '0'       *IN98
     C           BJDFIN    IFEQ 'M'
     C                     MOVE '1'       *IN98
     C                     ENDIF
      *                                                                   136935
      ***  Call Access Program for Securities Trading Details to retrieve 136935
      ***  field 'Allocate Difference to Dummy' and Dummy Customer.       136935
      *                                                                   136935
     C                     CALL 'AOSTRDR0'                                136935
     C                     PARM *BLANKS   WLRTCD                          136935
     C                     PARM '*FIRST'  WLOPTN                          136935
     C           SDSTRD    PARM SDSTRD    DSSDY                           136935
      *                                                                   136935
      ***  Handle Database Error.                                         136935
      *                                                                   136935
     C           WLRTCD    IFNE *BLANKS                                   136935
     C                     Z-ADD023       DBASE            ***************136935
     C                     MOVEL'SDSTRDPD'DBFILE           * DB ERROR 23 *136935
     C                     MOVEL*BLANKS   DBKEY            ***************136935
     C                     EXSR *PSSR                                     136935
     C                     ENDIF                                          136935
     C********** BVALDC    IFEQ 'Y'                                136935 155856
     C           WWPTYP    IFEQ 'CC'                                      155856
     C           WWPTYP    ORNE 'CC'                                      155856
     C           BVALDC    ANDEQ'Y'                                       155856
     C                     MOVE BVERCU    WWERCU                          136935
     C                     ENDIF                                          136935
      *
      ***  Retrieve Event Currency, Event Security Decimal Places and Event Price Basis.
      *
     C                     MOVE WWSECN    KSECN  10
     C                     EXSR ACCSEC
     C                     MOVE NMCY      WNMCYO  3        Event Currency
     C                     MOVE NMCY      DDNCC3           Event Currency for screen
     C                     Z-ADDNMDP      WNMDPO  10       Event Security Decimal Places
     C                     MOVE SPBS      WSPBSO  1        Event Price Basis
      *
      ***  Retrieve Event Currency Decimal Places by calling access objet.
      *
     C                     CALL 'AOCURRR0'
     C                     PARM '*MSG   ' WLRTCD
     C                     PARM '*KEY   ' WLOPTN
     C                     PARM NMCY      WLAJCD  3
     C           SDCURR    PARM SDCURR    DSSDY
      *
      ***  If record not found, handle Database Error.
      *
     C           WLRTCD    IFNE *BLANKS
     C                     Z-ADD004       DBASE            ***************
     C                     MOVEL'SDCURRPD'DBFILE           * DB ERROR 04 *
     C                     MOVELWLAJCD    DBKEY            ***************
     C                     EXSR *PSSR
     C                     ENDIF
     C                     Z-ADDA6NBDP    NMDPCO  10       Event Currency Decimal Places
      *                                                                   136930
     C           WWREFN    CHAINSECORFL1             89                   136930
      *                                                                   136930
     C           *IN89     IFEQ '1'                                       136930
     C                     Z-ADD022       DBASE            ***************136930
     C                     MOVEL'SECORFL1'DBFILE           * DB ERROR 22 *136930
     C                     MOVELWWREFN    DBKEY            ***************136930
     C                     EXSR *PSSR                                     136930
     C                     ENDIF                                          136930
      *                                                                   CSE020
      ** If CA is a child, obtain the Security from the Originating CA    CSE020
      *                                                                   CSE020
     C           MAPREF    IFNE *BLANKS                                   CSE020
     C           MAPREF    CHAINSECORFL9             70                   CSE020
      *                                                                   CSE020
     C           *IN70     IFEQ *ON                                       CSE020
     C           *LOCK     IN   LDA                                       CSE020
     C                     Z-ADD027       DBASE            ***************CSE020
     C                     MOVEL'SECORFL9'DBFILE           * DB ERROR 027*CSE020
     C                     MOVELMAPREF    DBKEY            ***************CSE020
     C                     OUT  LDA                                       CSE020
     C                     EXSR *PSSR                                     CSE020
     C                     ENDIF                                          CSE020
      *                                                                   CSE020
     C           MNPREF    DOWNE*BLANKS                                   CSE020
     C           MNPREF    CHAINSECORFL9             70                   CSE020
     C           *IN70     IFEQ *ON                                       CSE020
     C           *LOCK     IN   LDA                                       CSE020
     C                     Z-ADD028       DBASE            ***************CSE020
     C                     MOVEL'SECORFL9'DBFILE           * DB ERROR 028*CSE020
     C                     MOVELMNPREF    DBKEY            ***************CSE020
     C                     OUT  LDA                                       CSE020
     C                     EXSR *PSSR                                     CSE020
     C                     ENDIF                                          CSE020
     C                     ENDDO                                          CSE020
     C                     MOVE MNSESN    WOSESN 10                       CSE020
      *                                                                   CSE020
      ** Save Security of the Child CA. Use Security obtained from the    CSE020
      ** originating CA instead.                                          CSE020
      *                                                                   CSE020
     C                     MOVE WWSECN    WKSESN 10                       CSE020
     C                     MOVE WOSESN    WWSECN                          CSE020
     C                     ENDIF                                          CSE020
      *                                                                   136930
      ***  SETGT on LF/SEDPOHL0 with Branch, Depot, Security and Ex-Date. 136930
      *                                                                   136930
     C           KEY6      SETGTSEDPOHL0                                  136930
      *                                                                   136930
      ***  Read previous record on LF/SEDPOHL0                            136930
      *                                                                   136930
     C                     READPSEDPOHL0                 89               136930
      *                                                                   136360
      ***  If record read...                                              136930
      *                                                                   136930
     C           *IN89     IFEQ '0'                                       136930
      *                                                                   136930
      ***  For the same Security, Branch and Depot                        136930
      *                                                                   136930
     C           DSSC      IFEQ WWSECN                                    136930
     C           DSBA      ANDEQWWBRCD                                    136930
     C           DDPT      ANDEQWWDDPT                                    136930
     C                     MOVE *BLANKS   ZFLD15                          136930
      *                                                                   CSE020
      ** If CA is a child, override Ex-date position with the sum of      CSE020
      ** the Actual Stock Allocation of the Parent CA.                    CSE020
      *                                                                   CSE020
     C           MAPREF    IFNE *BLANKS                                   CSE020
     C                     MOVE WKSESN    WWSECN                          CSE020
     C                     Z-ADD*ZERO     WKASTA 150                      CSE020
     C           KEY8      SETLLSECOALL5                                  CSE020
     C           KEY8      READESECOALL5                 70               CSE020
     C           *IN70     DOWEQ*OFF                                      CSE020
     C           M5SESN    IFEQ WWSECN                                    CSE020
     C                     ADD  M5ASTA    WKASTA                          CSE020
     C                     ENDIF                                          CSE020
     C           KEY8      READESECOALL5                 70               CSE020
     C                     ENDDO                                          CSE020
     C                     Z-ADDWKASTA    ZFLD15                          CSE020
      *                                                                   CSE020
     C                     ELSE                                           CSE020
      *                                                                   CSE020
     C           MATDVD    IFEQ 'V'                                       136930
     C                     MOVE DSNV      ZFLD15                          136930
     C                     ELSE                                           136930
     C***********MATDVD    IFEQ 'T'                                136930 CSE020
     C                     MOVE DSNT      ZFLD15                          136960
     C                     ENDIF                                          136930
     C                     ENDIF                                          CSE020
      *                                                                   CSE020
     C                     Z-ADDWNMDPO    ZDECS                           136930
     C                     MOVE 'L'       ZECODE                          136930
     C                     EXSR ZSEDIT                                    136930
     C                     MOVE ZOUT21    WUEXPO 17                       136930
     C***********          ENDIF                                   136930 CSE020
     C                     ENDIF                                          136930
     C                     ENDIF                                          136930
      *                                                                   136930
      ***  If Ex-Date Position different from Depot Position file and     136930
      ***  Allocation file, blink on screen.                              136930
      *                                                                   136930
     C                     MOVE *OFF      *IN62                           CSE020
     C           WWEXPO    IFNE WUEXPO                                    136930
     C                     MOVE '1'       *IN62                           136930
     C                     ENDIF                                          136930
      *
     C                     MOVE *BLANKS   WUNSEC 10
      *
      ***  If Processing Type not Corporate Reorganisation.
      *
     C           WWPTYP    IFNE 'CR'
      *
      ***  Retrieve New Security Shortname on Corporate Actions Detail file, not for Processing
      ***  Type Capital Changes, where New Security = Event Security.
      *
     C                     SELEC
     C           WWPTYP    WHEQ 'DV'                       Divedend Events
     C           KEY3      SETGTSECODVL1
     C           KEY3      REDPESECODVL1                 89
     C                     MOVE MDNSEC    WUNSEC
     C                     MOVEL'SECODVL1'DBFILS
      *
     C           WWPTYP    WHEQ 'FR'                       Free Allocation
     C           KEY3      SETGTSECOFRL1
     C           KEY3      REDPESECOFRL1                 89
     C                     MOVE MENSEC    WUNSEC
     C                     MOVEL'SECOFRL1'DBFILS
      *
     C           WWPTYP    WHEQ 'RG'                       Rights Issues
     C           KEY3      SETGTSECORGL1
     C           KEY3      REDPESECORGL1                 89
     C                     MOVE MFNSEC    WUNSEC
     C                     MOVEL'SECORGL1'DBFILS
      *
     C           WWPTYP    WHEQ 'OF'                       Offers
     C           KEY3      SETGTSECOOFL1
     C           KEY3      REDPESECOOFL1                 89
     C                     MOVE MINSEC    WUNSEC
     C                     MOVEL'SECOOFL1'DBFILS
      *
     C           WWPTYP    WHEQ 'EX'                       Exercices and Conversions
     C           KEY3      SETGTSECOEXL1
     C           KEY3      REDPESECOEXL1                 89
     C                     MOVE MJNSEC    WUNSEC
     C                     MOVEL'SECOEXL1'DBFILS
      *
     C           WWPTYP    WHEQ 'NC'                       Name Changes   135690
     C           WWREFN    SETGTSECONCL1                                  135690
     C           WWREFN    REDPESECONCL1                 89               135690
     C                     MOVE MLSESN    WUNSEC                          135690
     C                     MOVEL'SECONCL1'DBFILS                          135690
      *                                                                   135690
     C           WWPTYP    WHEQ 'CC'                       Currency       CEU017
     C           WWREFN    SETGTSECOCCL2                   Changes        CEU017
     C           WWREFN    REDPESECOCCL2                 89               CEU017
     C                     MOVE NANSEC    WUNSEC                          CEU017
     C                     MOVEL'SECOCCL2'DBFILS                          CEU017
      *                                                                   CEU017
      ***  If record not found, handle Database Error.
      *
     C           *IN89     IFEQ '1'
     C                     Z-ADD005       DBASE            ***************
     C                     MOVELDBFILS    DBFILE           * DB ERROR 05 *
     C           WWPTYP    IFEQ 'CC'                       ***************
     C                     MOVELWWREFN    DBKEY
     C                     ELSE
     C                     MOVELWWREFN    DBKEY1
     C                     MOVE WWOPNB    DBKEY1
     C                     MOVELDBKEY1    DBKEY
     C                     ENDIF
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDSL
      *
     C           WUNSEC    IFNE *BLANKS
      *
      ***  If Processing Type with New Security entered (else, New Security = Event Security),
      ***  retrieve New Currency.
      *
     C                     MOVE WUNSEC    KSECN  10
     C                     EXSR ACCSEC
      *
      ***  Retrieve Number of Decimal Places of New Currency by calling access objet.
      *
     C                     CALL 'AOCURRR0'
     C                     PARM '*MSG   ' WLRTCD
     C                     PARM '*KEY   ' WLOPTN
     C                     PARM NMCY      WLAJCD  3
     C           SDCURR    PARM SDCURR    DSSDY
      *
      ***  If record not found, handle Database Error.
      *
     C           WLRTCD    IFNE *BLANKS
     C                     Z-ADD006       DBASE            ***************
     C                     MOVEL'SDCURRPD'DBFILE           * DB ERROR 06 *
     C                     MOVELWLAJCD    DBKEY            ***************
     C                     EXSR *PSSR
     C                     ENDIF
     C                     Z-ADDA6NBDP    NMDPCN  10       New Currency Decimal Places
     C                     ELSE
      *
      ***  If Processing Type with no New Security entered (New Security = Event Security),
      ***  set New Currency Decimal Places to Event Currency Decimal Places.
      *
     C                     Z-ADDNMDPCO    NMDPCN  10       New Currency Decimal Places
     C                     ENDIF
     C                     ENDIF
      *
      ***  Select the program MSGQ for error messages.
      *
     C                     MOVEL'*'       TOPQ   10
     C                     MOVEL'*PRV'    TOPRQ   5
     C                     MOVEL'SEUSRMSG'PMSGF  10
      *
      ***  Setup fields for subroutine ZASNMS.
      *
     C                     MOVEL'SE7593 ' ZAPGM  10        Message queue
     C                     MOVEL'SEUSRMSG'ZAMSGF 10        Message file
     C                     MOVEL*BLANK    ZAPGRL  5        Rel queue
     C                     MOVEL*BLANK    ZAMSID  7        Message Id.
     C                     MOVEL*BLANK    ZAMSDA132        Message data.
     C                     MOVEL*BLANK    ZAMSTP  7        Message type.
      *                                                                   CAP137
      ** Check if API Securities New Screen Input is installed.           CAP137
      *                                                                   CAP137
     C                     CALL 'AOSARDR0'                                CAP137
     C                     PARM *BLANKS   WLRTCD                          CAP137
     C                     PARM '*VERIFY' WLOPTN                          CAP137
     C                     PARM 'CAP137'  PSARD   6                       CAP137
     C           SCSARD    PARM SCSARD    DSFDY                           CAP137
      *                                                                   CAP137
      ** An NRF (No Record Found) return code is valid.                   CAP137
      ** Issue database error only for error return codes.                CAP137
      *                                                                   CAP137
     C           WLRTCD    IFNE *BLANKS                                   CAP137
     C           WLRTCD    ANDNE'*NRF   '                                 CAP137
     C           *LOCK     IN   LDA                                       CAP137
     C                     Z-ADD27        DBASE                           CAP137
     C                     MOVEL'SCSARDPD'DBFILE                          CAP137
     C                     MOVEL'CAP137'  DBKEY                           CAP137
     C                     OUT  LDA                                       CAP137
     C                     EXSR *PSSR                                     CAP137
     C                     ENDIF                                          CAP137
      *                                                                   CAP137
     C           WLRTCD    IFEQ *BLANK                                    CAP137
     C                     MOVEL'Y'       CAP137  1                       CAP137
     C                     MOVEL'SESECS'  PPGM    9                       CAP137
     C                     MOVE 'SIN'     PPGM                            CAP137
     C                     ELSE                                           CAP137
     C                     MOVEL'N'       CAP137                          CAP137
     C                     MOVE *BLANKS   PPGM                            CAP137
     C                     ENDIF                                          CAP137
     C/COPY WNCPYSRC,SEH00033
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  P002   - Detail processing.                                  *
      *                                                               *
      *  Called by: Main processing.                                  *
      *                                                               *
      *  Calls: P004     - Build the subfile (Amend Mode).            *
      *         P005     - Validation of the subfile (Amend Mode).    *
      *         ZASNMS   - Send message to program message queue.     *
      *         Y2CLMSC  - Midas ABS Clear specific program message   *
      *                    queue.                                     *
      *                                                               *
      *****************************************************************
      *
     C           P002      BEGSR                           ** P002 SR **
      *
      ***  Set 'First Time' flag to 'Y'.
      *
     C                     MOVE 'Y'       FIRSTA  1
      *
      ***  Do until F3: Exit or F12: Previous screen is pressed.
      *
     C           *INKC     DOUEQ'1'
     C           *INKL     OREQ '1'
      *
      ***  Check if First Time or F5: Refresh is pressed.
      *
     C           FIRSTA    IFEQ 'Y'
     C           *INKE     OREQ '1'
      *
      ***  Initialise subfile.
      *
     C                     MOVE '1'       *IN82
     C                     WRITESE7593C1
     C                     MOVEA'00'      *IN,82
     C                     Z-ADD0         #1RRN
     C                     Z-ADD0         #1RRNS  40
      *
      ***  Build the first subfile page.
      *
     C                     MOVE '0'       *IN85
     C                     EXSR P004
     C                     Z-ADD#1RRN     #1RRNS
      *
     C           FIRSTA    IFEQ 'Y'
     C                     MOVE '1'       *INKE
     C                     ENDIF
      *
     C                     MOVE 'N'       FIRSTA
     C                     Z-ADD3         CTR2
     C                     Z-ADD1         #1RRN
      *
     C                     ENDIF
      *
      ***  If Roll Up is pressed and no error.
      *
     C           *IN25     IFEQ '1'
     C                     Z-ADD#1RRNS    #1RRN
     C                     Z-ADD#1RRNS    WWRRN   40
      *
      ***  Build the next subfile page.
      *
     C                     MOVE '0'       *IN85
     C                     EXSR P004
     C                     Z-ADD#1RRN     #1RRNS
      *                                                                   153731
     C           WWPTYP    IFEQ 'CR'                                      153731
     C           WNEWPG    ANDEQ'Y'                                       153731
     C           WWPTYP    ORNE 'CR'                                      153731
     C           WWRRN     ADD  1         #1RRN
     C                     ELSE                                           153731
     C                     Z-ADDW1RRN     #1RRN                           153731
     C                     ENDIF                                          153731
      *                                                                   153731
     C                     ENDIF
      *
      ***  Display subfile on screen.
      *
     C                     WRITE#MSGCTL                    Program messages
     C                     WRITESE7593F0                   Command keys
     C                     EXFMTSE7593C1                   Subfile
      *
     C                     Z-ADD#1RRN     W1RRN   40
      *
      ***  Clear messages from program message quque.
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
      *
      ***  If F20: Exit without saving taken.
      *
     C           *IN20     IFEQ '1'
      *
     C                     MOVE TXT,5     ##CTX1           Setup command keys
     C                     MOVE *BLANKS   DDEXIT           Confirmation fields
     C                     MOVE '0'       *IN59            Error indicator
      *
      ***  Do while no error.
      *
     C           DDEXIT    DOWNE'Y'
     C           DDEXIT    ANDNE'N'
      *
      ***  Setup screen time.
      *
     C                     TIME           ##TME            Screen time.
      *
      ***  Execute screen confirmation format.
      *
     C                     WRITE#MSGCTL                    Program messages
     C                     WRITESE7593F0                   Command keys
     C                     EXFMTSE7593F2                   Confirmation screen
      *
      ***  Clear messages from program message queue.
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
      *
      ***  Confirmation field must equal 'Y' or 'N'.
      *
     C           DDEXIT    IFNE 'N'
     C           DDEXIT    ANDNE'Y'
     C                     MOVE '1'       *IN59            Reverse image
     C                     MOVEL'CO00032' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     ELSE
     C                     MOVE '0'       *IN59            Reverse image
     C                     ENDIF
     C                     ENDDO
      *
      ***  Setup INKC = F3: Exit.
      *
     C                     MOVE '1'       *INKC
      *
      ***  If Exit without saving confirmed, rollback changed records.
      *
     C           DDEXIT    IFEQ 'Y'
     C                     MOVE 'N'       WWUPDT
     C                     ROLBK
      *
      ***  If Exit without saving not confirmed, comit changed records.
      *
     C                     ELSE
     C                     COMIT
     C                     ENDIF
     C                     ENDIF
      *
      ***  If ENTER taken.
      *
     C           *INKC     IFEQ '0'                        Not F3: Exit
     C           *INKL     ANDEQ'0'                        Not F12: Previous screen
     C           *INKE     ANDEQ'0'                        Not F5: Refresh
     C           *IN20     ANDEQ'0'                        Not F20: Exit without saving
     C           *IN25     ANDEQ'0'                        Not Rollup
      *
      ***  Validate subfile records.
      *
     C                     EXSR P005
     C                     ENDIF
      *
     C                     ENDDO
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  P003   - Termination processing.                             *
      *                                                               *
      *  Called by: Main processing.                                  *
      *                                                               *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           P003      BEGSR                           ** P003 SR **
      *
      ***  If F3: Exit taken.
      *
     C           *INKC     IFEQ '1'
     C                     MOVE '3'       WWRTCD
      *
      ***  Comit changed records.
      *
     C                     COMIT
     C                     ENDIF
      *
      ***  If F12: Previous screen taken.
      *
     C           *INKL     IFEQ '1'
     C                     MOVE '2'       WWRTCD
      *
      ***  Comit changed records.
      *
     C                     COMIT
     C                     ENDIF
      *
      ***  Return to previous program.
      *
     C                     RETRN
     C                     MOVE '1'       *INLR
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  P004   - Build the subfile (Amend Mode).                     *
      *                                                               *
      *  Called by: P002   - Detail processing.                       *
      *                                                               *
      *  Calls: P009     - Move file fields to screen fields for      *
      *                    New Security.                              *
      *         P010     - Move file fields to screen fields for      *
      *                    Security '**CASH**'.                       *
      *         U002     - Load fields for file SECODPL3 with all     *
      *                    amounts = 0.                               *
      *         ACCSEC   - Access Security details file.              *
      *         CSBPR    - Calculate Subscription Price if            *
      *                    Subscription Price entered as Market for   *
      *                    Dividend Events.                           *
      *         *PSSR    - Error processing.                          *
      *         AOCURRR0 - Midas AO Currency codes access object.     *
      *                                                               *
      *****************************************************************
      *
     C           P004      BEGSR                           ** P004 SR **
      *
      ***  If Rollup taken.
      *
     C           *IN25     IFEQ '1'
     C                     ADD  3         CTR2    20
     C                     ENDIF
      *
      ***  Setup key fields.
      *
     C                     MOVELDDBRC3    WWBRCA           Branch
     C                     MOVELDDDEP3    WWDPOT           Depot
      *
      ***  If first time or F5: Refresh taken, first subfile record must be for Cash Line.
      *
     C           FIRSTA    IFEQ 'Y'
     C           *INKE     OREQ '1'
     C                     MOVEL'**CASH**'WWSECS    P      Security
      *
      ***  Process Cash Line.
      *
     C           KEY2      CHAINSECODPL3             31
      *
     C           *IN31     IFEQ '1'
     C                     EXSR U002
     C                     WRITESECODPD0
     C                     ENDIF
      *
     C                     MOVELCNSARR,1  DDTXT1           title 1st line = 'Cash'
     C                     MOVELCNSARR,2  DDTXT2           title 2nd line = 'Cash Opt/Allocat'
     C                     MOVELCNSARR,3  DDTXT3           title 3rd line = 'Cash Rounding'
     C                     MOVE *BLANKS   DDTXT4           title 4th line = blank
     C                     MOVE *BLANKS   DDORG4           original 4th line =  blank
     C                     MOVE *BLANKS   DDACT4           actual 4th line = blank
     C                     MOVE *BLANKS   DDAC4H           actual 4th line hidden field = blank
     C                     MOVE *BLANKS   DDBLD4           Balancing Diff. 4th line = blank
     C                     MOVE *BLANKS   DDNSEC           New Security = blank
      *
      ***  Move values for Cash Line record.
      *
     C                     Z-ADD1         CTR1    20       Set meter to 1
      *
      ***  If Processing Type not Corporate Reorganisation.
      *
     C           WWPTYP    IFNE 'CR'
      *
      ***  Retrieve New Security Shortname on Corporate Actions Detail file, not for Processing
      ***  Type Capital Changes, where New Security = Event Security.
      *
     C                     MOVE *BLANKS   WUNSEC
     C                     SELEC
     C           WWPTYP    WHEQ 'DV'                       Divedend Events
     C           KEY3      SETGTSECODVL1
     C           KEY3      REDPESECODVL1                 89
     C                     MOVE MDNSEC    WUNSEC
     C                     MOVEL'SECODVL1'DBFILS
      *
     C           WWPTYP    WHEQ 'FR'                       Free Allocation
     C           KEY3      SETGTSECOFRL1
     C           KEY3      REDPESECOFRL1                 89
     C                     MOVE MENSEC    WUNSEC
     C                     MOVEL'SECOFRL1'DBFILS
      *
     C           WWPTYP    WHEQ 'RG'                       Rights Issues
     C           KEY3      SETGTSECORGL1
     C           KEY3      REDPESECORGL1                 89
     C                     MOVE MFNSEC    WUNSEC
     C                     MOVEL'SECORGL1'DBFILS
      *
     C           WWPTYP    WHEQ 'OF'                       Offers
     C           KEY3      SETGTSECOOFL1
     C           KEY3      REDPESECOOFL1                 89
     C                     MOVE MINSEC    WUNSEC
     C                     MOVEL'SECOOFL1'DBFILS
      *
     C           WWPTYP    WHEQ 'EX'                       Exercices and Conversions
     C           KEY3      SETGTSECOEXL1
     C           KEY3      REDPESECOEXL1                 89
     C                     MOVE MJNSEC    WUNSEC
     C                     MOVEL'SECOEXL1'DBFILS
      *
     C           WWPTYP    WHEQ 'NC'                       Name Changes   135690
     C           WWREFN    SETGTSECONCL1                                  135690
     C           WWREFN    REDPESECONCL1                 89               135690
     C                     MOVE MLSESN    WUNSEC                          135690
     C                     MOVEL'SECONCL1'DBFILS                          135690
      *                                                                   135690
     C           WWPTYP    WHEQ 'CC'                       Currency       CEU017
     C           WWREFN    SETGTSECOCCL2                   Changes        CEU017
     C           WWREFN    REDPESECOCCL2                 89               CEU017
     C                     MOVE NANSEC    WUNSEC                          CEU017
     C                     MOVEL'SECOCCL2'DBFILS                          CEU017
      *                                                                   CEU017
      ***  If record not found, handle Database Error.
      *
     C           *IN89     IFEQ '1'
     C                     Z-ADD020       DBASE            ***************
     C                     MOVELDBFILS    DBFILE           * DB ERROR 20 *
     C           WWPTYP    IFEQ 'CC'                       ***************
     C           WWPTYP    OREQ 'NC'                                      135690
     C                     MOVELWWREFN    DBKEY
     C                     ELSE
     C                     MOVELWWREFN    DBKEY1
     C                     MOVE WWOPNB    DBKEY1
     C                     MOVELDBKEY1    DBKEY
     C                     ENDIF
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDSL
      *
      ***  If New Security retrieve not blank.
      *
     C           WUNSEC    IFNE *BLANKS
      *
      ***  Retrieve New Currency, New Security Decimal Places and New Price Basis.
      *
     C                     MOVE WUNSEC    KSECN
     C                     EXSR ACCSEC
      *
     C                     MOVE NMCY      WNMCYN           New Currency
     C                     Z-ADDNMDP      WNMDPN           New Security Decimal Places
     C                     MOVE SPBS      WSPBSN  1        Price Basis
      *
      ***  Retrieve Number of Decimal Places of New Currency by calling access objet.
      *
     C                     CALL 'AOCURRR0'
     C                     PARM '*MSG   ' WLRTCD
     C                     PARM '*KEY   ' WLOPTN
     C                     PARM NMCY      WLAJCD
     C           SDCURR    PARM SDCURR    DSSDY
      *
      ***  If record not found, handle Database Error.
      *
     C           WLRTCD    IFNE *BLANKS
     C                     Z-ADD019       DBASE            ***************
     C                     MOVEL'SDCURRPD'DBFILE           * DB ERROR 19 *
     C                     MOVELWLAJCD    DBKEY            ***************
     C                     EXSR *PSSR
     C                     ENDIF
     C                     Z-ADDA6NBDP    NMDPCN  10       New Currency Decimal Places
      *
      ***  Else, if New Security retrieve is blank or No access done (= New Security = Event
      ***  Security).
      *
     C                     ELSE
      *
      ***  Set New Currency, New Security and Currency Decimal Places and New Price Basis to Event
      ***  Values.
      *
     C                     MOVE WNMCYO    WNMCYN
     C                     Z-ADDWNMDPO    WNMDPN
     C                     MOVE WSPBSO    WSPBSN
     C                     Z-ADDNMDPCO    NMDPCN
     C                     ENDIF
     C                     ENDIF
      *
      ***  Move file fields to subfile fields (**CASH**).
      *
     C                     EXSR P010
      *
      ***  Write a record to the subfile.
      *
     C/COPY WNCPYSRC,SEH00034
     C                     MOVE '1'       *IN45            Display 'CASH' fields
     C                     ADD  1         #1RRN      83
     C                     WRITESE7593S1
     C/COPY WNCPYSRC,SEH00035
      *
     C                     ENDIF
      *
      ***  Process New Security lines.
      *
      ***  If Processing Type = Corporate Reorganisation.
      *
     C           WWPTYP    IFEQ 'CR'
      *
     C                     MOVE 'N'       WNEWPG  1
     C                     MOVE '0'       *IN31
      *
      ***  Retrieve the 20 potential New Security.
      *
     C           KEY3      CHAINSECOCRL1             89
      *
      ***  If record not found, handle Database Error.
      *
     C           *IN89     IFEQ '1'
     C                     Z-ADD007       DBASE            ***************
     C                     MOVEL'SECOCRL1'DBFILE           * DB ERROR 07 *
     C                     MOVELWWREFN    DBKEY1  8        ***************
     C                     MOVE WWOPNB    DBKEY1
     C                     MOVELDBKEY1    DBKEY
     C                     EXSR *PSSR
      *
      ***  If record found.
      *
     C                     ELSE
     C                     MOVEAMHSECA    SSARR
      *
     C                     MOVELCNSARR,4  DDTXT1           title 1st line = New Security
     C                     MOVELCNSARR,5  DDTXT2           title 2nd line = Stock Allocation
     C                     MOVELCNSARR,6  DDTXT3           title 3rd line = Stock Rounding
     C                     MOVELCNSARR,3  DDTXT4           title 4th line = Cash Rounding
      *
      ***  If first time, initialise array index to 1 to retrieve first New Security and line
      ***  counter to 3.
      *
     C           FIRSTA    IFEQ 'Y'
     C           *INKE     OREQ '1'                                       137884
     C                     Z-ADD1         A       20
     C                     Z-ADD0         B       20
     C                     Z-ADD3         CTR2
      *
      ***  If not, add 1 to array index to retrieve next New Security.
      *
     C                     ELSE
     C                     Z-ADDB         A
     C                     ADD  1         A
     C                     ENDIF
      *
      ***  Do until subfile page is full or the 20 potential New Security processed.
      *
     C           CTR1      DOUEQCTR2                       Subfile page full
     C           A         ORGT 20                         20 Potential New Security processed
      *
     C           SSARR,A   IFNE *BLANKS
     C                     MOVELSSARR,A   WWSECS           New Security
      *
      ***  Retrieve New Currency, New Security decimal Places and New Price Basis.
      *
     C                     MOVE WWSECS    KSECN
     C                     EXSR ACCSEC
      *
     C                     MOVE NMCY      WNMCYN           New Currency
     C                     Z-ADDNMDP      WNMDPN           New Security Decimal Places
     C                     MOVE SPBS      WSPBSN  1        New Price Basis
      *
      ***  Retrieve Number of Decimal Places of New Currency by calling access objet.
      *
     C                     CALL 'AOCURRR0'
     C                     PARM '*MSG   ' WLRTCD
     C                     PARM '*KEY   ' WLOPTN
     C                     PARM NMCY      WLAJCD
     C           SDCURR    PARM SDCURR    DSSDY
      *
      ***  If record not found, handle Database Error.
      *
     C           WLRTCD    IFNE *BLANKS
     C                     Z-ADD008       DBASE            ***************
     C                     MOVEL'SDCURRPD'DBFILE           * DB ERROR 08 *
     C                     MOVELWLAJCD    DBKEY            ***************
     C                     EXSR *PSSR
     C                     ENDIF
     C                     Z-ADDA6NBDP    NMDPCN  10       New Currency Decimal Places
      *
      ***  Retrive CO Depot details for the New Security.
      *
     C           KEY2      CHAINSECODPL3             31
      *
      ***  If record not found, load fields for CO Details file with all amounts = 0 and write a
      ***  record on SECODPL3.
      *
     C           *IN31     IFEQ '1'
     C                     EXSR U002
     C                     WRITESECODPD0
     C                     ENDIF
      *
     C                     MOVE *BLANKS   DDACT1           set actual 1st line to blank
     C                     MOVE *BLANKS   DDBLD1           set Balancing Diff. 1st line to blank
      *
     C                     MOVELSSARR,A   DDNSEC           New Security
      *
      ***  Move file fields to subfile fields.
      *
     C                     EXSR P009
      *
      ***  Write a record to the subfile and increment counter.
      *
     C                     MOVE '0'       *IN45            Display New Security fields
     C                     ADD  1         #1RRN      83
     C                     MOVE 'Y'       WNEWPG
     C                     WRITESE7593S1
     C                     ADD  1         CTR1
     C                     ENDIF
      *
      ***  Increment and save array index.
      *
     C                     Z-ADDA         B       20
     C                     ADD  1         A
      *
     C           A         IFEQ 20
     C                     MOVE '1'       *IN31
     C                     ENDIF
      *
     C                     ENDDO
     C                     ENDIF
      *
      ***  Else, if Processing Type = Dividend Events, Free Allocation, Capital Change, Rights
      ***  Issues, Offers or Exercices and Conversions (= not 'CR').
      ***  And Currency Changes.                                          CEU017
      *
     C                     ELSE
      *
     C                     MOVE '1'       *IN31
      *
      ***  Chain to LFs according to Processing Type.
      *
     C                     SELEC
     C           WWPTYP    WHEQ 'DV'                       Dividend Events
     C           KEY3      CHAINSECODVL1             89
     C                     MOVE 'SECODVL1'DBFILS  8
      *
     C           WWPTYP    WHEQ 'FR'                       Free Allocation
     C           KEY3      CHAINSECOFRL1             89
     C                     MOVE 'SECOFRL0'DBFILS
      *
     C           WWPTYP    WHEQ 'CE'                       Capital Changes
     C           KEY3      CHAINSECOCEL1             89
     C                     MOVE 'SECOCEL1'DBFILS
      *
     C           WWPTYP    WHEQ 'RG'                       Rights Issues
     C           KEY3      CHAINSECORGL1             89
     C                     MOVE 'SECORGL1'DBFILS
      *
     C           WWPTYP    WHEQ 'OF'                       Offers
     C           KEY3      CHAINSECOOFL1             89
     C                     MOVE 'SECOOFL1'DBFILS
      *
     C           WWPTYP    WHEQ 'EX'                       Exercices and Conversions
     C           KEY3      CHAINSECOEXL1             89
     C                     MOVE 'SECOEXL1'DBFILS
      *                                                                   135690
     C           WWPTYP    WHEQ 'NC'                       Name Changes   135690
     C           WWREFN    CHAINSECONCL1             89                   135690
     C                     MOVE 'SECONCL1'DBFILS                          135690
      *                                                                   CEU017
     C           WWPTYP    WHEQ 'CC'                       Currency       CEU017
     C           WWREFN    CHAINSECOCCL2             89    Changes.       CEU017
     C                     MOVE 'SECOCCL2'DBFILS                          CEU017
     C                     ENDSL
      *
      ***  If record not found, handle Database Error.
      *
     C           *IN89     IFEQ '1'
     C                     Z-ADD009       DBASE            ***************
     C                     MOVELDBFILS    DBFILE           * DB ERROR 09 *
     C                     MOVELWWREFN    DBKEY1  8        ***************
     C                     MOVE WWOPNB    DBKEY1
     C                     MOVELDBKEY1    DBKEY
     C                     EXSR *PSSR
      *
      ***  Else, if record is found, load screen fields.
      *
     C                     ELSE
      *
     C                     MOVELCNSARR,4  DDTXT1           title 1st line = New Security
     C                     MOVELCNSARR,5  DDTXT2           title 2nd line = Stock Allocation
     C                     MOVELCNSARR,6  DDTXT3           title 3rd line = Stock Rounding
     C                     MOVELCNSARR,3  DDTXT4           title 4th line = Cash Rounding
      *
      ***  Load New Security Shortname.
      *
     C                     SELEC
     C           WWPTYP    WHEQ 'DV'                       If Dividend Events
     C                     MOVELMDNSEC    WWSECS           = from file
      *
     C           WWPTYP    WHEQ 'FR'                       If Free Allocation
     C                     MOVELMENSEC    WWSECS           = from file
      *
     C           WWPTYP    WHEQ 'CE'                       If Capital Changes
     C                     MOVELWWSECN    WWSECS           = Security received as parameter
      *
     C           WWPTYP    WHEQ 'RG'                       If Rights Issues
     C                     MOVELMFNSEC    WWSECS           = from file
      *
     C           WWPTYP    WHEQ 'OF'                       If Offers
     C                     MOVELMINSEC    WWSECS           = from file
      *
     C           WWPTYP    WHEQ 'EX'                       If Exercices and Conversions
     C                     MOVELMJNSEC    WWSECS           = from file
      *
     C           WWPTYP    WHEQ 'NC'                       If Name Changes
     C           MLSESN    IFEQ *BLANKS                    If Sec.blank   135690
     C                     MOVELWWSECN    WWSECS           = Security received as parameter
     C                     ELSE                            Else           135690
     C                     MOVELMLSESN    WWSECS           = from file    135690
     C                     ENDIF                                          135690
      *
     C           WWPTYP    WHEQ 'NF'                       If Non Financial Events
     C                     MOVELWWSECN    WWSECS           = Security received as parameter
      *                                                                   CEU017
     C           WWPTYP    WHEQ 'CC'                       If Ccy Changes CEU017
     C                     MOVELNANSEC    WWSECS           = from file    CEU017
     C                     ENDSL
      *
      ***  Process New Security.
      *
     C                     MOVELWWSECS    DDNSEC
     C           DDNSEC    IFNE *BLANKS
      *
      ***  Retrieve New Currency, New Security Decimal Places and New Price Basis.
      *
     C                     MOVE WWSECS    KSECN
     C                     EXSR ACCSEC
      *
     C                     MOVE NMCY      WNMCYN           New Currency
     C                     Z-ADDNMDP      WNMDPN           New Security Decimal Places
     C                     MOVE SPBS      WSPBSN  1        New Price Basis
      *
      ***  Retrieve Number of Decimal Places of New Currency by calling access objet.
      *
     C                     CALL 'AOCURRR0'
     C                     PARM '*MSG   ' WLRTCD
     C                     PARM '*KEY   ' WLOPTN
     C                     PARM NMCY      WLAJCD
     C           SDCURR    PARM SDCURR    DSSDY
      *
      ***  If record not found, handle Database Error.
      *
     C           WLRTCD    IFNE *BLANKS
     C                     Z-ADD010       DBASE            ***************
     C                     MOVEL'SDCURRPD'DBFILE           * DB ERROR 10 *
     C                     MOVELWLAJCD    DBKEY            ***************
     C                     EXSR *PSSR
     C                     ENDIF
     C                     Z-ADDA6NBDP    NMDPCN  10       New Currency Decimal Places
      *
      ***  If Processing Type = Dividend Events and Subscription Price is entered as Market,
      ***  the Subscription Price must first be calculated from Current Market Price minus Discount.
      *
     C           WWPTYP    IFEQ 'DV'
     C           MDMRKI    ANDEQ'Y'
     C                     EXSR CSBPR
     C                     ENDIF
      *                                                                   136935
      ***  If Processing Type = Dividend Events with Stock Dividend Type  136935
      ***  = 'R', if Compensation Price entered, use Compensation Price   136935
      ***  instead of Subscription Price, otherwise, calculate Subscrip-  136935
      ***  tion Price = Subscription Price minus Discount Rate.           136935
      *                                                                   136935
     C           WWPTYP    IFEQ 'DV'                                      136935
     C           MDSDIT    ANDEQ'R'                                       136935
     C           MDCOMP    IFNE 0                                         136935
     C                     Z-ADDMDCOMP    MDSBPR                          136935
     C                     ELSE                                           136935
     C           MDMRKI    IFNE 'Y'                                       136935
     C           MDSBPR    MULT MDDSRT    WWSBPR                          136935
     C           WWSBPR    DIV  100       WWSBPR                          136935
     C                     SUB  WWSBPR    MDSBPR                          136935
     C                     ENDIF                                          136935
     C                     ENDIF                                          136935
     C                     ENDIF                                          136935
      *
      ***  Chain to LF/SECODPL3 to retrieve record details.
      *
     C           KEY2      CHAINSECODPL3             33
      *
      ***  If record not found, load fields for CO Details file with all amounts = 0 and write a
      ***  record on SECODPL3.
      *
     C           *IN33     IFEQ '1'
     C                     EXSR U002
     C                     WRITESECODPD0
     C                     ENDIF
      *
      ***  Move file fields to subfile fields for New Security lines.
      *
     C                     EXSR P009
      *
      ***  Write a record to the subfile.
      *
     C                     MOVE '0'       *IN45            Display New Security fields
     C                     ADD  1         #1RRN
     C                     WRITESE7593S1
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  P005   - Validation of the subfile (Amend Mode).             *
      *                                                               *
      *  Called by: P002   - Detail processing.                       *
      *                                                               *
      *  Calls: P006     - Validate and perform Action taken (Amend   *
      *                    Mode).                                     *
      *         P007     - Process Action.                            *
      *         P008     - Update/write LF/SECODRL0 (Amend Mode).     *
      *         V001     - Validate details input (Amend Mode).       *
      *         Y2CLMSC  - Midas ABS Clear specific program message   *
      *                    queue.                                     *
      *                                                               *
      *****************************************************************
      *
     C           P005      BEGSR                           ** P005 SR **
      *
      ***  Clear messages from program message queue.
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
      *
      ***  Read first record amended on subfile.
      *
     C                     READCSE7593S1                 34
      *
     C                     MOVE 'Y'       UPDCAS  1
      *
      ***  Setup subfile record number with the current CPF'S Position within the subfile.
      *
     C           *IN34     IFEQ '1'
     C                     Z-ADDCPFPOS    #1RRN
     C                     ENDIF
      *
      ***  Do while record amended on subfile and not F3: Exit and not F12: Previous Screen pressed.
      *
     C           *IN34     DOWEQ'0'                        Record amended
     C           *INKC     ANDEQ'0'                        Not F3
     C           *INKL     ANDEQ'0'                        Not F12
      *
      ***  Reset error indicators.
      *
     C                     MOVEA'0000'    *IN,58
      *
     C                     MOVE '1'       *IN85            SFLNXTCHG
      *
      ***  Set 'Record valid' flag to 'Y'.
      *
     C                     MOVE 'Y'       VALREC  1
      *
      ***  Validate details input.
      *
     C                     EXSR V001
      *
      *
      ***  If details input not valide, read next subfile record changed.
      *
     C           VALREC    IFEQ 'N'
     C                     GOTO TAG2
     C                     ENDIF
      *
      ***  If action indicator is changed, validate and perform Action taken.
      *
     C           DDACN3    IFNE ' '
     C                     EXSR P006
      *
      ***  If Action Code not valide, read next subfile record changed.
      *
     C           VALREC    IFEQ 'N'
     C                     GOTO TAG2
     C                     ENDIF
      *
     C                     ENDIF
      *
      ***  If record is valid.
      *
     C           VALREC    IFEQ 'Y'
      *
      ***  Update SECODRL0.
      *
     C                     EXSR P008
     C                     ENDIF
      *
      ***  Process action.
      *
     C           DDACN3    IFNE ' '
     C                     EXSR P007
      *
     C                     MOVE '0'       *IN85
     C                     MOVE *BLANKS   DDACN3           Reset Action Code
     C                     UPDATSE7593S1                   Update subfile record
     C                     ENDIF
      *
     C           TAG2      TAG
      *
     C                     WRITE#MSGCTL                    write program message
      *
      ***  Read next subfile record canged.
      *
     C                     READCSE7593S1                 34
     C                     ENDDO
      *
     C                     Z-ADD1         #1RRN
      *
      ***  Redisplay current screen.
      *
     C                     WRITE#MSGCTL                    Program messages
     C                     WRITESE7593C1                   Subfile
     C                     WRITESE7593F0                   Command Keys
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  P006   - Validate and perform Action taken (Amend Mode).     *
      *                                                               *
      *  Called by: P005   - Validation of the subfile (Amend Mode).  *
      *                                                               *
      *  Calls: ZASNMS - Send message to program message queue.       *
      *                                                               *
      *****************************************************************
      *
     C           P006      BEGSR                           ** P006 SR **
      *
      ***  Action must either be 'A', 'E' or 'F'.
      *
     C           DDACN3    IFNE 'A'
     C           DDACN3    ANDNE'E'
     C           DDACN3    ANDNE'F'
     C           DDACN3    ANDNE' '
     C                     MOVE 'N'       VALREC           'Record not valid' flag
     C                     MOVE '1'       *IN58            Reverse Action Code
     C                     MOVEL'CO00204' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
      *
      ***  Action 'A' is not allowed in the enquiry mode.
      *
     C           DDACN3    IFEQ 'A'
     C           WWACTN    ANDEQ'E'
     C                     MOVE 'N'       VALREC           'Record not valid' flag
     C                     MOVE '1'       *IN58            Reverse Action Code
     C                     MOVEL'CO00237' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
      *
      ***  Action 'F' is not allowed for Cash Line.
      *
     C           DDACN3    IFEQ 'F'
     C           DDTXT1    ANDEQCNSARR,1
     C                     MOVE *BLANKS   DDACN3
     C                     MOVE 'N'       VALREC           'Record not valid' flag
     C                     MOVE '1'       *IN58            Reverse Action Code
     C                     MOVEL'CO00205' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
     C/COPY WNCPYSRC,SEH00036
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  P007   - Process Action.                                     *
      *                                                               *
      *  Called by: P005   - Validation of the subfile (Amend Mode).  *
      *                                                               *
      *  Calls: *PSSR  - Error processing.                            *
      *         SE0040   - Midas SE Security detail maintenance.      *
      *         SE7513   - Midas SE CO Customer/Book by Depot         *
      *                    Maintenance.                               *
      *                                                               *
      *****************************************************************
      *
     C           P007      BEGSR                           ** P007 SR **
      *
      ***  If Action 'F' is entered in front of New Security line,
      ***  call RPG/SE0040 to enquire the Security details of the New Security.
      *
     C           DDACN3    IFEQ 'F'
     C           DDTXT1    ANDEQCNSARR,4
      *
     C           CAP137    IFNE 'Y'                                       CAP137
     C                     CALL 'SE0040'               87
     C                     PARM *BLANKS   #RTCD   7
     C                     PARM 'E'       #ACCD   1
     C                     PARM DDNSEC    #NSEC  10
     C                     PARM *BLANKS   #OSEC  10
     C                     PARM *BLANKS   #OTHER156                       138957
     C                     ELSE                                           CAP137
     C                     CALL 'SEC023'               87                 CAP137
     C                     PARM           PPGM                            CAP137
     C                     PARM *BLANKS   #RTCD                           CAP137
     C                     PARM 'E'       #ACCD                           CAP137
     C                     PARM DDNSEC    #NSEC                           CAP137
     C                     PARM *BLANKS   #OSEC                           CAP137
     C                     PARM *BLANKS   #OTHER                          CAP137
     C                     PARM 'Y'       PLINK   1                       CAP137
     C                     ENDIF                                          CAP137
      *
      ***  If there is an error in calling the pgm or return code is not blank and not F3 pressed,
      ***  handle database error.
      *
     C           *IN87     IFEQ '1'
     C           *INU7     OREQ '1'
     C           *INU8     OREQ '1'
     C           #RTCD     ORNE *BLANKS
     C           #RTCD     ANDNE'Y2U9999'
     C                     Z-ADD011       DBASE            ***************
     C                     MOVEL'SE7593 ' DBFILE           * DB ERROR 11 *
     C           CAP137    IFNE 'Y'                                       CAP137
     C                     MOVELNARR,1    DBKEY            ***************
     C                     ELSE                                           CAP137
     C                     MOVELNARR,3    DBKEY                           CAP137
     C                     ENDIF                                          CAP137
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDIF
      *
      ***  If Action 'A' or 'E' is entered.
      *
     C           DDACN3    IFEQ 'A'
     C           DDACN3    OREQ 'E'
     C                     COMIT
      *
      ***  If Action 'A' is entered, New Security is equal to '**CASH**'.
      *
     C           DDTXT1    IFEQ CNSARR,1
     C                     MOVEL'**CASH**'WNSEC  10 P
     C                     ENDIF
      *
      ***  If Action 'E' is entered, New Security is the same as displayed on the screen.
      *
     C           DDTXT1    IFEQ CNSARR,4
     C                     MOVE DDNSEC    WNSEC
     C                     ENDIF
      *
      ***  Call RPG/SE7513 to display the Customer/Books Maintenance Enquiry screen.
      *
     C                     CALL 'SE7513'               87
     C                     PARM           WWREFN  6        ER Reference
     C                     PARM           WWSECN 10        Security Shortname
     C                     PARM           WWCOAT  2        Corporate Action Type
     C                     PARM           WWPTYP  2        Processing Type
     C                     PARM           WWEXDT  5        Ex-Date
     C                     PARM           DDSTS3           Status
     C                     PARM           DDFLA3           Final Allocation Indicator
     C                     PARM           DDBRC3           Branch Code
     C                     PARM           DDDEP3           Depot
     C                     PARM           DDNCC3           Nominal Currency
     C                     PARM           WNSEC            New Security
     C                     PARM           DDOPT3           Option Number
     C                     PARM DDACN3    WWMODE  1        Amend or Enquiry ?
     C                     PARM           WWRTCD  1        Return Code
      *
      ***  If there is an error in calling the pgm or return code is 'E', handle Database Error.
      *
     C           *IN87     IFEQ '1'
     C           *INU7     OREQ '1'
     C           *INU8     OREQ '1'
     C           WWRTCD    OREQ 'E'
     C                     Z-ADD012       DBASE            ***************
     C                     MOVEL'SE7593 ' DBFILE           * DB ERROR 12 *
     C                     MOVELNARR,2    DBKEY            ***************
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ***  If 'Return Code' equal to '3', seton F3: Exit indicator.
      *
     C           WWRTCD    IFEQ '3'
     C                     MOVE '1'       *INKC
     C                     ELSE
     C                     MOVE 'Y'       FIRSTA
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  P008   - Update/Write LF/SECODRL0 (Amend Mode).              *
      *                                                               *
      *  Called by: P005   - Validation of the subfile (Amend Mode).  *
      *                                                               *
      *  Calls: U001   - Load field for file SECODRL0.                *
      *                                                               *
      *****************************************************************
      *
     C           P008      BEGSR                           ** P008 SR **
      *
      ***  Setup key list to LF/SECODRL0.
      *
     C                     MOVE DDBRC3    WWDSBA           Branch from screen
     C                     MOVELDDDEP3    WWDDPT           Depot from screen
      *
      ***  Retrieve CO Depot Reference details for CO Reference, Branch and Depot processed.
      *
     C           KEY1      CHAINSECODRL0             35
      *
      ***  If details not found, load fields and write a record to PF/SECODRL0.
      *
     C           *IN35     IFEQ '1'
     C                     EXSR U001
     C                     WRITESECODRD0
      *
      ***  Else, if found, load fields and update the record in PF/SECODRL0.
      *
     C                     ELSE
     C                     EXSR U001
     C                     UPDATSECODRD0
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  P009   - Move file fields to screen fields for New Security. *
      *                                                               *
      *  Called by: P004   - Build the subfile (Amend Mode).          *
      *             V001   - Validate details input (Amend Mode).     *
      *                                                               *
      *  Calls: ZSEDIT - Standart subroutine to insert a decimal      *
      *                  point into a numeric field and to blank out  *
      *                  leading zeroes.                              *
      *                                                               *
      *****************************************************************
      *
     C           P009      BEGSR                           ** P009 SR **
      *
      ***  Currencies.
      *
     C                     MOVE WNMCYN    DDCCY2           Currency 1
     C                     MOVE WNMCYN    DDCCY3           Currency 2
     C                     MOVE WNMCYN    DDCCY4           Currency 3
      *
      ***  Holding taken as stock.
      *
     C                     MOVE *BLANKS   DDHTS3
     C           MBHOST    IFNE *ZEROS
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVELMBHOST    ZFLD15
     C                     Z-ADDWNMDPO    ZDECS            Event Security Decimal Places
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DDHTS3
     C                     ENDIF
      *
      ***  Original Stock Alocation = Original 2.
      *
     C                     MOVE *BLANKS   DDORG2
     C           MBOALR    IFNE *ZEROS
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVELMBOALR    ZFLD15
     C                     Z-ADDWNMDPN    ZDECS            New Security Decimal Places
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DDORG2
     C                     ENDIF
      *
      ***  Original Stock Rounding = Original 3.
      *
     C                     MOVE *BLANKS   DDORG3
     C           MBOSTR    IFNE *ZEROS
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVELMBOSTR    ZFLD15
     C                     Z-ADD8         ZDECS            8 Decimal Places
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DDORG3
     C                     ENDIF
      *
      ***  Original Cash Rounding = Original 4.
      *
     C                     MOVE *BLANKS   DDORG4
     C           MBOCAR    IFNE *ZEROS
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVELMBOCAR    ZFLD15
     C                     Z-ADDNMDPCN    ZDECS            New Currency Decimal Places
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DDORG4
     C                     ENDIF
      *
      ***  Actual Stock Allocation = Actual 2.
      *
     C                     MOVE *BLANKS   DDACT2
     C                     MOVE *BLANKS   DDAC2H
     C           MBASTA    IFNE *ZEROS
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVELMBASTA    ZFLD15
     C                     Z-ADDWNMDPN    ZDECS            New Security Decimal Places
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DDACT2
     C                     MOVE ZOUT21    DDAC2H
     C                     ENDIF
      *
      ***  Actual Stock Rounding = Actul 3.
      *
     C                     MOVE *BLANKS   DDACT3
     C                     MOVE *BLANKS   DDAC3H
     C           MBASTR    IFNE *ZEROS
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVELMBASTR    ZFLD15
     C                     Z-ADD8         ZDECS            8 Decimal Places
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DDACT3
     C                     MOVE ZOUT21    DDAC3H
     C                     ENDIF
      *
      ***  Actual Cash Rounding = Actual 4.
      *
     C                     MOVE *BLANKS   DDACT4
     C                     MOVE *BLANKS   DDAC4H
     C           MBACAR    IFNE *ZEROS
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVELMBACAR    ZFLD15
     C                     Z-ADDNMDPCN    ZDECS            New Currency Decimal Places
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DDACT4
     C                     MOVE ZOUT21    DDAC4H
     C                     ENDIF
      *
      ***  Balancing Stok Allocation = Balancing Difference 2.
      *
     C                     MOVE *BLANKS   DDBLD2
     C           MBBSTA    IFNE *ZEROS
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVELMBBSTA    ZFLD15
     C                     Z-ADDWNMDPN    ZDECS            New Security Decimal Places
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DDBLD2
     C                     ENDIF
      *
      ***  Balancing Stock Rounding = Balancing Difference 3.
      *
     C                     MOVE *BLANKS   DDBLD3
     C           MBBSTR    IFNE *ZEROS
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVELMBBSTR    ZFLD15
     C                     Z-ADD8         ZDECS            8 Decimal Places
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DDBLD3
     C                     ENDIF
      *
      ***  Balancing Cash Rounding = Balancing Difference 4.
      *
     C                     MOVE *BLANKS   DDBLD4
     C           MBBCAR    IFNE *ZEROS
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVELMBBCAR    ZFLD15
     C                     Z-ADDNMDPCN    ZDECS            New Currency Decimal Places
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DDBLD4
     C                     ENDIF
      *
      ***  Reset screen fields used only for Security '**CASH**' line.
      *
     C                     MOVE *BLANKS   DDACT1           Actual 1
     C                     MOVE *BLANKS   DDBLD1           Balancing Difference 1
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  P010   - Move file fields to screen fields for Security      *
      *           '**CASH**'.                                         *
      *                                                               *
      *  Called by: P004   - Build the subfile (Amend Mode).          *
      *             V001   - Validate details input (Amend Mode).     *
      *                                                               *
      *  Calls: ZSEDIT - Standart subroutine to insert a decimal      *
      *                  point into a numeric field and to blank out  *
      *                  leading zeroes.                              *
      *                                                               *
      *****************************************************************
      *
     C           P010      BEGSR                           ** P010 SR **
      *
      ***  If Processing Type = Corporate Reorganisation, set Number of Decimal Places used in
      ***  standart subroutine ZSEDIT to Event Currency Decimal Places and screen Currencies to
      ***  Event Currency, else set Number of Decimal Places to New Currency Decimal Places and
      ***  Currencies to New Currency.
      *
     C           WWPTYP    IFEQ 'CR'
     C                     Z-ADDNMDPCO    ZDECS            Event Currency Decimal Places
     C                     MOVE WNMCYO    DDCCY2
     C                     MOVE WNMCYO    DDCCY3
     C                     ELSE
     C                     Z-ADDNMDPCN    ZDECS            New Currency Deciam Places
     C                     MOVE WNMCYN    DDCCY2
     C                     MOVE WNMCYN    DDCCY3
     C                     ENDIF
      *
      ***  Original Cash Option = Original 2.
      *
     C                     MOVE *BLANKS   DDORG2
     C           MBCAOP    IFNE *ZEROS
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVELMBCAOP    ZFLD15
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DDORG2
     C                     ENDIF
      *
      ***  Original Cash Rounding = Original 3.
      *
     C                     MOVE *BLANKS   DDORG3
     C           MBOCAR    IFNE *ZEROS
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVELMBOCAR    ZFLD15
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DDORG3
     C                     ENDIF
      *
      ***  Actual 1 = Actual Cash Allocation + Actual Cash Rounding.
      *
     C                     Z-ADD*ZEROS    WWACTC 150
     C           MBACAA    ADD  MBACAR    WWACTC
     C                     MOVE *BLANKS   DDACT1
     C           WWACTC    IFNE *ZEROS
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVELWWACTC    ZFLD15
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DDACT1
     C                     ENDIF
      *
      ***  Actual Cash Allocation = Actual 2.
      *
     C                     MOVE *BLANKS   DDACT2
     C                     MOVE *BLANKS   DDAC2H
     C           MBACAA    IFNE *ZEROS
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVELMBACAA    ZFLD15
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DDACT2
     C                     MOVE ZOUT21    DDAC2H
     C                     ENDIF
      *
      ***  Actual Cash Rounding = Actual 3.
      *
     C                     MOVE *BLANKS   DDACT3
     C                     MOVE *BLANKS   DDAC3H
     C           MBACAR    IFNE *ZEROS
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVELMBACAR    ZFLD15
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DDACT3
     C                     MOVE ZOUT21    DDAC3H
     C                     ENDIF
      *
      ***  Balancing Cash Allocation = Balancing Difference 1.
      *
     C                     MOVE *BLANKS   DDBLD1
     C           MBBCAA    IFNE *ZEROS
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVELMBBCAA    ZFLD15
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DDBLD1
     C                     ENDIF
      *
      ***  Reset screen fields used only for New Security line.
      *
     C                     MOVE *BLANKS   DDCCY4           Currency 4
     C                     MOVE *BLANKS   DDORG4           Original 4
     C                     MOVE *BLANKS   DDACT4           Actual 4
     C                     MOVE *BLANKS   DDAC4H           Actual 4 hidden
     C                     MOVE *BLANKS   DDBLD2           Balancing Difference 2
     C                     MOVE *BLANKS   DDBLD3           Balancing Difference 3
     C                     MOVE *BLANKS   DDBLD4           Balancing Difference 4
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  V001   - Validate details input (Amend Mode).                *
      *                                                               *
      *  Called by: P005   - Validation of the subfile (Amend Mode).  *
      *                                                               *
      *  Calls: REASTR   - Recalculate Actual Stock Rounding for New  *
      *                    Security on Security line.                 *
      *         REACAR   - Recalculate Actual Cash Rounding for New   *
      *                    Security on Security line.                 *
      *         REACAA   - Recalculate Actual Cash Allocation for     *
      *                    Security '**CASH**' on Security line.      *
      *         P009     - Move file fields to screen fields for New  *
      *                    Security.                                  *
      *         P010     - Move file fields to screen fields for      *
      *                    Security '**CASH**'.                       *
      *         ACCSEC   - Access Security details file.              *
      *         *PSSR    - Error processing.                          *
      *         ZASNMS   - Send message to program message queue.     *
      *         SETNDP   - Set amount from num field 30.9 to num      *
      *                    field 15.0 with Number of Decimal Places   *
      *                    from a Currency.                           *
      *         RTVNDP   - Set amount from num field 15.0 to num      *
      *                    field 30.9 with Retrieve Number of Decimal *
      *                    Places from a Ccy.                         *
      *         ROUNDD   - Perform rounding to the Number of Decimal  *
      *                    Places.                                    *
      *         ZASIGN   - Standart subroutine to validate and        *
      *                    right-align signed numeric fields.         *
      *         AOCURRR0 - Midas AO Currency codes access object.     *
      *                                                               *
      *****************************************************************
      *
     C           V001      BEGSR                           ** V001 SR **
      *
      ***  Cash Line (title 1st line = 'Cash').
      *
     C           DDTXT1    IFEQ CNSARR,1
      *
      ***  Retrieve CO Depot details for Security 'CASH'.
      *
     C                     MOVEL'**CASH**'WWSECS    P
     C           KEY2      CHAINSECODPL3             36
      *
      ***  If record no longer on file, handle Database Error.
      *
     C           *IN36     IFEQ '1'
     C                     Z-ADD013       DBASE            ***************
     C                     MOVE 'SECODPL3'DBFILE           * DB ERROR 13 *
     C                     MOVELWWREFN    DBKEY1           ***************
     C                     MOVE WWOPNB    DBKEY1
     C                     MOVELWWBRCA    DBKEY2  9
     C                     MOVE WWDPOT    DBKEY2
     C                     MOVELDBKEY1    DBKEY3 17
     C                     MOVE DBKEY2    DBKEY3
     C                     MOVELDBKEY3    DBKEY4 27
     C                     MOVE WWSECS    DBKEY4
     C                     MOVELDBKEY4    DBKEY
     C                     EXSR *PSSR
     C                     ENDIF
      *                                                                   136935
     C                     Z-ADD0         DUACAA 150                      136935
     C                     Z-ADD0         DUACAR 150                      136935
      *
      ***  If Actual Cash Opt/Allocat. is changed.
      *
     C           DDACT2    IFNE DDAC2H
      *
      ***  Save some fields.
      *
     C                     Z-ADDMBACAA    WWACAA           Old Actual Cash Allocation
      *
      ***  Validate the input.
      *
     C                     MOVE *BLANKS   ZFLD17
     C                     MOVELDDACT2    ZFLD17
     C           WWPTYP    IFEQ 'CR'
     C                     Z-ADDNMDPCO    ZSDEC            Event Currency Decimal Places
     C                     ELSE
     C                     Z-ADDNMDPCN    ZSDEC            New Currency Decimal Places
     C                     ENDIF
     C           15        SUB  ZSDEC     ZSDIG
     C                     EXSR ZASIGN
      *
      ***  Field must be numeric.
      *
     C           *IN99     IFEQ '1'
     C                     MOVE 'N'       VALREC
     C                     MOVE 'N'       UPDCAS
     C                     MOVE '1'       *IN59
     C                     MOVEL'CO00230' ZAMSID
     C                     EXSR ZASNMS
     C                     ELSE
      *
      ***  Move Actual Cash Allocation entered into file field.
      *
     C                     MOVE ZOUT15    MBACAA
     C           ZFSIGN    IFEQ '-'
     C                     MULT -1        MBACAA
     C                     ENDIF
      *
      ***  Recalculate Balancing Cash Allocation on file by adding
      ***  the old value and subtracting the new value of Actual Cash
      ***  Allocation.
      *
     C                     ADD  WWACAA    MBBCAA
     C                     SUB  MBACAA    MBBCAA
      *                                                                   136935
      ***  If Balancing Difference recalculated not 0 and Processing Type 136935
      ***  = Currency Change or not Currency Change with ICD field 'Allo- 136935
      ***  cate Difference to Dummy' = 'Y', save Balancing Difference and 136935
      ***  reset it.                                                      136935
      *                                                                   136935
     C           MBBCAA    IFNE 0                                         136935
     C           WWPTYP    ANDEQ'CC'                                      136935
     C           MBBCAA    ORNE 0                                         136935
     C           WWPTYP    ANDNE'CC'                                      136935
     C           BVALDC    ANDEQ'Y'                                       136935
     C                     Z-SUBMBBCAA    DUACAA                          136935
     C                     Z-ADD0         MBBCAA                          136935
     C                     ENDIF                                          136935
      *
     C                     ENDIF
     C                     ENDIF
      *
      ***  If Actual Cash rounding is changed.
      *
     C           DDACT3    IFNE DDAC3H
      *
      ***  Save some fields.
      *
     C                     Z-ADDMBACAR    WWACAR           Old Actual Cash Rounding
      *
      ***  Validate the input.
      *
     C                     MOVE *BLANKS   ZFLD17
     C                     MOVELDDACT3    ZFLD17
     C           WWPTYP    IFEQ 'CR'
     C                     Z-ADDNMDPCO    ZSDEC            Event Currency Decimal Places
     C                     ELSE
     C                     Z-ADDNMDPCN    ZSDEC            New Currency Decimal Places
     C                     ENDIF
     C           15        SUB  ZSDEC     ZSDIG
     C                     EXSR ZASIGN
      *
      ***  Field must be numeric.
      *
     C           *IN99     IFEQ '1'
     C                     MOVE 'N'       VALREC
     C                     MOVE 'N'       UPDCAS
     C                     MOVE '1'       *IN60
     C                     MOVEL'CO00231' ZAMSID
     C                     EXSR ZASNMS
     C                     ELSE
      *
      ***  Move Actual Cash Rounding entered into file field.
      *
     C                     MOVE ZOUT15    MBACAR
     C           ZFSIGN    IFEQ '-'
     C                     MULT -1        MBACAR
     C                     ENDIF
      *
      ***  Recalculate Balancing Cash Rounding on file by adding
      ***  Old value and subtracting New value of Actual Cash Rounding.
      *
     C           MBBCAA    ADD  WWACAR    MBBCAA
     C           MBBCAA    SUB  MBACAR    MBBCAA
      *                                                                   136935
      ***  If Balancing Difference recalculated not 0 and Processing Type 136935
      ***  = Currency Change or not Currency Change with ICD field 'Allo- 136935
      ***  cate Difference to Dummy' = 'Y', save Balancing Difference and 136935
      ***  reset it.                                                      136935
      *                                                                   136935
     C           MBBCAA    IFNE 0                                         136935
     C           WWPTYP    ANDEQ'CC'                                      136935
     C           MBBCAA    ORNE 0                                         136935
     C           WWPTYP    ANDNE'CC'                                      136935
     C           BVALDC    ANDEQ'Y'                                       136935
     C                     Z-ADDMBBCAA    DUACAR                          136935
     C                     Z-ADD0         MBBCAA                          136935
     C                     ENDIF                                          136935
      *
     C                     ENDIF
     C                     ENDIF
      *
      ***  If Actual Cash Allocation or Actual Cash Rounding changed.
      *
     C           DDACT2    IFNE DDAC2H
     C           DDACT3    ORNE DDAC3H
      *
      ***  If no error, update current record in LF/SECODPL3.
      *
     C           *IN59     IFEQ '0'
     C           *IN60     ANDEQ'0'
     C                     MOVE 'A'       MBCHTP
     C                     Z-ADDBJRDNB    MBLCD
     C                     MOVE USER      MBLUID
     C                     UPDATSECODPD0
     C                     MOVE 'Y'       WWUPDT  1
      *
      ***  Move file fields to subfile fields (**CASH**).
      *
     C                     EXSR P010
     C                     MOVE '0'       *IN85            SFLNXTCHG
     C                     MOVE '1'       *IN45
     C                     UPDATSE7593S1
      *                                                                   136935
      ***  If Balancing Difference has been allocated to Dummy Customer,  136935
      ***  update record from SECOALL7 for Security '**CASH**' and Dummy  136935
      ***  Customer                                                       136935
      *                                                                   136935
     C           DUACAA    IFNE 0                                         136935
     C           DUACAR    ORNE 0                                         136935
     C           KEY7      CHAINSECOALL7             36                   136935
      *                                                                   136935
      ***  If record no longer on file, handle Database Error.            136935
      *                                                                   136935
     C           *IN36     IFEQ '1'                                       136935
     C                     Z-ADD024       DBASE            ***************136935
     C                     MOVE 'SECOALL7'DBFILE           * DB ERROR 24 *136935
     C                     MOVELWWREFN    DBKEY1           ***************136935
     C                     MOVE WWOPNB    DBKEY1                          136935
     C                     MOVELWWBRCD    DBKEY2  9                       136935
     C                     MOVE WWDDPT    DBKEY2                          136935
     C                     MOVELDBKEY1    DBKEY3 17                       136935
     C                     MOVE DBKEY2    DBKEY3                          136935
     C                     MOVELDBKEY3    DBKEY4 27                       136935
     C                     MOVE WWSECS    DBKEY4                          136935
     C                     MOVELWWCOTP    DBKEY5  3                       136935
     C                     MOVE WWBOOK    DBKEY5                          136935
     C                     MOVELWWERCU    DBKEY6 10                       136935
     C                     MOVE WWPTFR    DBKEY6                          136935
     C                     MOVELDBKEY5    DBKEY7 13                       136935
     C                     MOVE DBKEY6    DBKEY7                          136935
     C                     MOVELDBKEY4    DBKEY8 40                       136935
     C                     MOVE DBKEY7    DBKEY8                          136935
     C                     MOVELDBKEY8    DBKEY                           136935
     C                     EXSR *PSSR                                     136935
     C                     ENDIF                                          136935
     C                     ADD  DUACAA    MCACAA                          136935
     C                     ADD  DUACAR    MCACAR                          136935
     C                     MOVE 'A'       MCCHTP                          136935
     C                     Z-ADDBJRDNB    MCLCD                           136935
     C                     MOVE USER      MCLUID                          136935
     C                     MOVE 'M'       MCREST                          136935
     C                     UPDATSECOALD0                                  136935
     C                     ENDIF                                          136935
      *
      ***  If error, update subfile.
      *
     C                     ELSE
     C                     MOVE '1'       *IN85            SFLNXTCHG
     C                     MOVE '1'       *IN45
     C                     UPDATSE7593S1
     C                     ENDIF
      *
     C                     ELSE
     C           DDACN3    IFEQ ' '
     C/COPY WNCPYSRC,SEH00037
     C                     MOVE '0'       *IN85            SFLNXTCHG
     C                     MOVE '1'       *IN45
     C                     UPDATSE7593S1
     C                     ENDIF
     C                     ENDIF
      *
      ***  Security Line.
      *
     C                     ELSE
      *
      ***  Reset indicators fields amended.
      *
     C                     MOVE 'N'       AMACAR  1        Actual Cash Rounding recalculated ind.
     C                     MOVE 'N'       AMACAA  1        Actual Cash Allocation recalculated ind.
     C                     MOVE 'N'       AMASTR  1        Actual Stock Rounding recalculated ind.
      *                                                                   136935
     C                     Z-ADD0         DUACAA                          136935
     C                     Z-ADD0         DUACAR                          136935
     C                     Z-ADD0         DUASTA 150                      136935
     C                     Z-ADD0         DUASTR 150                      136935
      *
      ***  Actual Stock Allocation and Actual Cash Rounding can not
      ***  be changed in same time.
      *
     C           DDACT2    IFNE DDAC2H
     C           DDACT4    ANDNEDDAC4H
     C                     MOVEL'CO00302' ZAMSID
     C                     MOVE 'N'       VALREC
     C                     MOVE '1'       *IN59
     C                     MOVE '1'       *IN61
     C                     EXSR ZASNMS
     C                     ELSE
      *
      ***  Retrieve CO Depot details for the New Security.
      *
     C                     MOVELDDNSEC    WWSECS
     C           KEY2      CHAINSECODPL3             36
      *
      ***  If record no longer on file, handle Database Error.
      *
     C           *IN36     IFEQ '1'
     C                     Z-ADD014       DBASE            ***************
     C                     MOVEL'SECODPL3'DBFILE           * DB ERROR 14 *
     C                     MOVELWWREFN    DBKEY1           ***************
     C                     MOVE WWOPNB    DBKEY1
     C                     MOVELWWBRCA    DBKEY2  9
     C                     MOVE WWDPOT    DBKEY2
     C                     MOVELDBKEY1    DBKEY3 17
     C                     MOVE DBKEY2    DBKEY3
     C                     MOVELDBKEY3    DBKEY4 27
     C                     MOVE WWSECS    DBKEY4
     C                     MOVELDBKEY4    DBKEY
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ***  Retrieve New Currency, New Security decimal places and New Price Basis for the new
      ***  Security.
      *
     C                     MOVE WWSECS    KSECN
     C                     EXSR ACCSEC
      *
     C                     MOVE NMCY      WNMCYN  3        New currency
     C                     Z-ADDNMDP      WNMDPN  10       New Security Decimal Places
     C                     MOVE SPBS      WSPBSN  1        New Price basis
      *
      ***  Retrieve Number of Decimal Places of New Currency by calling access objet.
      *
     C                     CALL 'AOCURRR0'
     C                     PARM '*MSG   ' WLRTCD
     C                     PARM '*KEY   ' WLOPTN
     C                     PARM NMCY      WLAJCD
     C           SDCURR    PARM SDCURR    DSSDY
      *
      ***  If record not found, handle Database Error.
      *
     C           WLRTCD    IFNE *BLANKS
     C                     Z-ADD015       DBASE            ***************
     C                     MOVEL'SDCURRPD'DBFILE           * DB ERROR 15 *
     C                     MOVELWLAJCD    DBKEY            ***************
     C                     EXSR *PSSR
     C                     ENDIF
     C                     Z-ADDA6NBDP    NMDPCN  10       New Currency Decimal Places
      *
      ***  If Actual Stock Allocation is changed.
      *
     C           DDACT2    IFNE DDAC2H
      *
      ***  Save some fields in work fields.
      *
     C                     Z-ADDMBASTA    WWASTA 150       Old Actual Stock Allocation
     C                     Z-ADDMBASTR    WWASTR 158       Old Actual Stock Rounding
     C                     Z-ADDMBACAR    WOACAR           Old Actual Cash  Rounding
     C                     Z-ADDMBHOCA    WWHOCA 150       Holding Taken as Cash
      *
      ***  Validate the input.
      *
     C                     MOVE *BLANKS   ZFLD17
     C                     MOVELDDACT2    ZFLD17
     C                     Z-ADDWNMDPN    ZSDEC            New Security Decimal Places
     C           15        SUB  ZSDEC     ZSDIG
     C                     EXSR ZASIGN
      *
      ***  Field must be numeric.
      *
     C           *IN99     IFEQ '1'
     C                     MOVE 'N'       VALREC
     C                     MOVE '1'       *IN59
     C                     MOVEL'CO00232' ZAMSID
     C                     EXSR ZASNMS
     C                     ELSE
      *
      ***  Move Actual Stock Allocation entered into work field.
      *
     C                     MOVE ZOUT15    WUASTA 150
     C           ZFSIGN    IFEQ '-'
     C                     MULT -1        WUASTA
     C                     ENDIF
      *
      ***  Access File depending on CO Type.
      *
     C                     SELEC
     C           WWPTYP    WHEQ 'DV'                       Dividend Events
     C           KEY3      CHAINSECODVL1             89
     C                     MOVE 'SECODVL1'DBFILS  8
      *
     C           WWPTYP    WHEQ 'CE'                       Capital Change
     C           KEY3      CHAINSECOCEL1             89
     C                     MOVE 'SECOCEL1'DBFILS
      *
     C           WWPTYP    WHEQ 'RG'                       Rights Issues
     C           KEY3      CHAINSECORGL1             89
     C                     MOVE 'SECORGL1'DBFILS
      *
     C           WWPTYP    WHEQ 'FR'                       Free Allocation
     C           KEY3      CHAINSECOFRL1             89
     C                     MOVE 'SECOFRL1'DBFILS
      *
     C           WWPTYP    WHEQ 'OF'                       Offers
     C           KEY3      CHAINSECOOFL1             89
     C                     MOVE 'SECOOFL1'DBFILS
      *
     C           WWPTYP    WHEQ 'CR'                       Corporate Reorganisation
     C           KEY3      CHAINSECOCRL1             89
     C                     MOVE 'SECOCRL1'DBFILS
      *
     C           WWPTYP    WHEQ 'EX'                       Exercices and Conversions
     C           KEY3      CHAINSECOEXL1             89
     C                     MOVE 'SECOEXL1'DBFILS
      *                                                                   CEU017
     C           WWPTYP    WHEQ 'CC'                       Currency       CEU017
     C           WWREFN    CHAINSECOCCL2             89    Changes.       CEU017
     C                     MOVE 'SECOCCL2'DBFILS                          CEU017
     C                     ENDSL
      *
      ***  If record not found in its corresponding LF, handle Database Error.
      *
     C           *IN89     IFEQ '1'
     C                     Z-ADD016       DBASE            ***************
     C                     MOVELDBFILS    DBFILE           * DB ERROR 16 *
     C                     MOVELWWREFN    DBKEY1  8        ***************
     C                     MOVE WWOPNB    DBKEY1
     C                     MOVELDBKEY1    DBKEY
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ***  If Processing Type not Capital Changes, not Non Financial Events and not Name Changes.
      *
     C                     MOVE 'Y'       ASAVAL  1        Actual Stock Allocation valid
     C           WWPTYP    IFNE 'CE'                       Not Capital Changes
     C           WWPTYP    ANDNE'NF'                       Not Non Financial Events
     C           WWPTYP    ANDNE'NC'                       Not Name Changes
      *
      ***  Retrieve Number of New Shares, Rounding, Rounding Settlement in Cash, Denomination
      ***  Multiplier and Compensation Price depending on CO Type.
      *
     C                     Z-ADD0         WWDMLT                          135686
     C           WWPTYP    IFEQ 'CR'
     C                     MOVEAMHSECA    TSEC,1
     C                     Z-ADD1         I       20
     C           WWSECS    LOKUPTSEC,I                   17Search New Security (20 Potential)
     C           *IN17     IFEQ '0'
     C                     Z-ADD0         WWNSHA 157       Number of New Shares
     C                     MOVE ' '       WWRNDN  1        Rounding
     C                     MOVE ' '       WWRNDS  1        Rounding Settlement in Cash
     C                     Z-ADD0         WWCOMP 158       Compensation Price
     C******               Z-ADD0         WWDMLT 158       Denom. Multip. 135686
     C                     Z-ADD1         WWDMLT 309       Denom. Multip. 135686
     C                     ELSE
     C                     MOVEAMHNSHA    TNSH,1
     C                     MOVE TNSH,I    WWNSHA           Number of New Shares
     C                     MOVEAMHRNDN    TNDN,1
     C                     MOVE TNDN,I    WWRNDN           Rounding
     C                     MOVEAMHRNDS    TNDS,1
     C                     MOVE TNDS,I    WWRNDS           Rounding Settlement in Cash
     C                     MOVEAMHCOPA    TOPA,1
     C                     MOVE TOPA,I    WWCOMP           Compensation Price
     C                     MOVEAMHDMLA    TDML,1
     C******               MOVE TDML,I    WWDMLT           Denom. Multip. 135686
     C                     MOVE TDML,I    WUNUM            Denom. Multip. 135686
     C                     ENDIF
     C                     ELSE
     C                     Z-ADDMDNSHA    WWNSHA           Number of New Shares
      *                                                                   135685
      ***  If Processing Type = Currency Change                           135685
      ***  and Compensation Price is entered as Market,                   135685
      ***  the Compensation Price must first be calculated                135685
      ***  from Current Market Price.                                     135685
      *                                                                   135685
     C           WWPTYP    IFEQ 'CC'                                      135685
     C           NAMRKI    ANDEQ'Y'                                       135685
     C                     EXSR CCOMP                                     135685
     C                     ENDIF                                          135685
      *                                                                   135685
     C                     Z-ADDMDCOMP    WWCOMP           Compensation Price
     C******               Z-ADDMDDMLT    WWDMLT           Denom. Multip. 135686
     C                     Z-ADDMDDMLT    WUNUM            Denom. Multip. 135686
     C                     MOVE MDRNDN    WWRNDN           Rounding
     C                     MOVE MDRNDS    WWRNDS           Rounding Settlement in Cash
     C                     ENDIF
     C******               ENDIF                                          135686
      *
      ***  Prepare Denomination Multiplier.
      *
     C                     Z-ADDWNMDPN    WWNBDP           New Security Decimal Places
     C           WWDMLT    IFNE 1                                         135686
     C                     EXSR RTVNDP                                    135686
     C                     Z-ADDWWNUM     WWDMLT                          135686
     C                     ENDIF                                          135686
      *                                                                   135686
     C                     Z-ADDWUASTA    WUNUM
     C                     EXSR RTVNDP
     C******     WWRNDN    IFEQ 'D'                                       135686
     C           WWNUM     DIV  WWDMLT    W1ASTA 300
     C                     MVR            RESTE   90                      135686
     C******               ELSE                                           135686
     C******     WWNUM     DIV  WWDMLT    W1ASTA    H                     135686
     C******               ENDIF                                          135686
     C******     W1ASTA    MULT WWDMLT    W2ASTA 309                      135686
      *
      ***  Actual Stock Allocation entered must be in Denomination Multiplier and Rounding
      ***  precision.
      *
     C******     WWNUM     IFNE W2ASTA                                    135686
     C           RESTE     IFNE 0                                         135686
     C                     MOVE 'N'       ASAVAL           Actual Stock Allocation not valid
     C                     MOVE 'N'       VALREC           Record not valid
     C                     MOVE '1'       *IN59
     C                     MOVEL'CO00362' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF                                          135686
     C                     ENDIF
      *
      ***  If Actual Stock Allocation entered valid.
      *
     C           ASAVAL    IFEQ 'Y'
      *
      ***  Move Actual Stock Allocation saved into file field.
      *
     C                     Z-ADDWUASTA    MBASTA
      *
      ***  If Processing Type not Non Financial Events and not Name Changes.
      *
     C           WWPTYP    IFNE 'NF'                       If not Non Financial Events
     C           WWPTYP    ANDNE'NC'                       and not Name Changes
      *
      ***  Recalculate Actual Stock Rounding (MBASTR).
      *
     C                     EXSR REASTR
      *
      ***  Recalculate Actual Cash Rounding (MBACAR).
      *
     C                     EXSR REACAR
      *
      ***  If Actual Cash Rounding recalculated, recalculate Balancing Cash Rounding (MBBCAR) on
      ***  file by subtracting old vaue and adding new value of Actual Cash Rounding.
      *
     C           AMACAR    IFEQ 'Y'
     C                     SUB  WOACAR    MBBCAR
     C                     ADD  MBACAR    MBBCAR
     C                     Z-ADDMBACAR    WNACAR
      *                                                                   136935
      ***  If Balancing Difference recalculated not 0 and Processing Type 136935
      ***  = Currency Change or not Currency Change with ICD field 'Allo- 136935
      ***  cate Difference to Dummy' = 'Y', save Balancing Difference and 136935
      ***  reset it.                                                      136935
      *                                                                   136935
     C           MBBCAR    IFNE 0                                         136935
     C           WWPTYP    ANDEQ'CC'                                      136935
     C           MBBCAR    ORNE 0                                         136935
     C           WWPTYP    ANDNE'CC'                                      136935
     C           BVALDC    ANDEQ'Y'                                       136935
     C                     Z-ADDMBBCAR    DUACAR                          136935
     C                     Z-ADD0         MBBCAR                          136935
     C                     ENDIF                                          136935
     C                     ENDIF
      *
      ***  If Actual Stock Rounding recalculated, recalculate Balancing Stock Rounding (MBBSTR) on
      ***  file by subtracting old value and adding new value of Actual Stock Rounding.
      *
     C           AMASTR    IFEQ 'Y'
     C                     SUB  WWASTR    MBBSTR
     C                     ADD  MBASTR    MBBSTR
      *                                                                   136935
      ***  If Balancing Difference recalculated not 0 and Processing Type 136935
      ***  = Currency Change or not Currency Change with ICD field 'Allo- 136935
      ***  cate Difference to Dummy' = 'Y', save Balancing Difference and 136935
      ***  reset it.                                                      136935
      *                                                                   136935
     C           MBBSTR    IFNE 0                                         136935
     C           WWPTYP    ANDEQ'CC'                                      136935
     C           MBBSTR    ORNE 0                                         136935
     C           WWPTYP    ANDNE'CC'                                      136935
     C           BVALDC    ANDEQ'Y'                                       136935
     C                     Z-ADDMBBSTR    DUASTR                          136935
     C                     Z-ADD0         MBBSTR                          136935
     C                     ENDIF                                          136935
     C                     ENDIF
      *
     C                     ENDIF
      *
      ***  Recalculate Balancing Stock Allocation on file by subtracting
      ***  Old value and adding New value of Actual Stock Allocation.
      *
     C                     SUB  WWASTA    MBBSTA
     C                     ADD  MBASTA    MBBSTA
      *                                                                   136935
      ***  If Balancing Difference recalculated not 0 and Processing Type 136935
      ***  = Currency Change or not Currency Change with ICD field 'Allo- 136935
      ***  cate Difference to Dummy' = 'Y', save Balancing Difference and 136935
      ***  reset it.                                                      136935
      *                                                                   136935
     C           MBBSTA    IFNE 0                                         136935
     C           WWPTYP    ANDEQ'CC'                                      136935
     C           MBBSTA    ORNE 0                                         136935
     C           WWPTYP    ANDNE'CC'                                      136935
     C           BVALDC    ANDEQ'Y'                                       136935
     C                     Z-ADDMBBSTA    DUASTA                          136935
     C                     Z-ADD0         MBBSTA                          136935
     C                     ENDIF                                          136935
      *
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
      ***  If Actual Cash Rounding is changed.
      *
     C           DDACT4    IFNE DDAC4H
      *
      ***  Save some fields in work fields.
      *
     C                     Z-ADDMBACAR    WOACAR 150       Old Actual Cash Rounding
      *
      ***  Validate the input.
      *
     C                     MOVE *BLANKS   ZFLD17
     C                     MOVELDDACT4    ZFLD17
     C                     Z-ADDNMDPCN    ZSDEC            New Currency Decimal Places
     C           15        SUB  ZSDEC     ZSDIG
     C                     EXSR ZASIGN
      *
      ***  Field must be numeric.
      *
     C           *IN99     IFEQ '1'
     C                     MOVE 'N'       VALREC
     C                     MOVE '1'       *IN61
     C                     MOVEL'CO00231' ZAMSID
     C                     EXSR ZASNMS
     C                     ELSE
      *
      ***  Move Actual Cash Rounding entered into file field and save it.
      *
     C                     MOVE ZOUT15    MBACAR
     C                     MOVE ZOUT15    WNACAR 150
     C           ZFSIGN    IFEQ '-'
     C                     MULT -1        MBACAR
     C                     MULT -1        WNACAR
     C                     ENDIF
      *
      ***  Recalculate Balancing Cash Rounding on file by subtracting
      ***  Old value and adding new value of Actual Cash Rounding.
      *
     C                     SUB  WOACAR    MBBCAR
     C                     ADD  MBACAR    MBBCAR
     C                     MOVE 'Y'       AMACAR           Actual Cash rounding amended ind.
      *                                                                   136935
      ***  If Balancing Difference recalculated not 0 and Processing Type 136935
      ***  = Currency Change or not Currency Change with ICD field 'Allo- 136935
      ***  cate Difference to Dummy' = 'Y', save Balancing Difference and 136935
      ***  reset it.                                                      136935
      *                                                                   136935
     C           MBBCAR    IFNE 0                                         136935
     C           WWPTYP    ANDEQ'CC'                                      136935
     C           MBBCAR    ORNE 0                                         136935
     C           WWPTYP    ANDNE'CC'                                      136935
     C           BVALDC    ANDEQ'Y'                                       136935
     C                     Z-ADDMBBCAR    DUACAR                          136935
     C                     Z-ADD0         MBBCAR                          136935
     C                     ENDIF                                          136935
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
      ***  If Actual Stock Allocation or Actual Cash Rounding changed.
      *
     C           DDACT2    IFNE DDAC2H
     C           DDACT4    ORNE DDAC4H
      *
      ***  If no error, update current record in LF/SECODPL3.
      *
     C           *IN59     IFEQ '0'
     C           *IN61     ANDEQ'0'
     C                     MOVE 'A'       MBCHTP
     C                     MOVE USER      MBLUID
     C                     Z-ADDBJRDNB    MBLCD
     C                     UPDATSECODPD0
     C                     MOVE 'Y'       WWUPDT
      *                                                                   136935
      ***  If at least one Balancing Difference has been allocated to     136935
      ***  Dummy Customer, update record from SECOALL7 for New Security   136935
      ***  and Dummy Customer.                                            136935
      *                                                                   136935
     C           DUASTA    IFNE 0                                         136935
     C           DUASTR    ORNE 0                                         136935
     C           DUACAR    ORNE 0                                         136935
     C           KEY7      CHAINSECOALL7             36                   136935
      *                                                                   136935
      ***  If record no longer on file, handle Database Error.            136935
      *                                                                   136935
     C           *IN36     IFEQ '1'                                       136935
     C                     Z-ADD025       DBASE            ***************136935
     C                     MOVE 'SECOALL7'DBFILE           * DB ERROR 25 *136935
     C                     MOVELWWREFN    DBKEY1           ***************136935
     C                     MOVE WWOPNB    DBKEY1                          136935
     C                     MOVELWWBRCD    DBKEY2  9                       136935
     C                     MOVE WWDDPT    DBKEY2                          136935
     C                     MOVELDBKEY1    DBKEY3 17                       136935
     C                     MOVE DBKEY2    DBKEY3                          136935
     C                     MOVELDBKEY3    DBKEY4 27                       136935
     C                     MOVE WWSECS    DBKEY4                          136935
     C                     MOVELWWCOTP    DBKEY5  3                       136935
     C                     MOVE WWBOOK    DBKEY5                          136935
     C                     MOVELWWERCU    DBKEY6 10                       136935
     C                     MOVE WWPTFR    DBKEY6                          136935
     C                     MOVELDBKEY5    DBKEY7 13                       136935
     C                     MOVE DBKEY6    DBKEY7                          136935
     C                     MOVELDBKEY4    DBKEY8 40                       136935
     C                     MOVE DBKEY7    DBKEY8                          136935
     C                     MOVELDBKEY8    DBKEY                           136935
     C                     EXSR *PSSR                                     136935
     C                     ENDIF                                          136935
     C                     ADD  DUASTA    MCASTA                          136935
     C                     ADD  DUASTR    MCASTR                          136935
     C                     ADD  DUACAR    MCACAR                          136935
     C                     MOVE 'A'       MCCHTP                          136935
     C                     Z-ADDBJRDNB    MCLCD                           136935
     C                     MOVE USER      MCLUID                          136935
     C                     MOVE 'M'       MCREST                          136935
     C                     UPDATSECOALD0                                  136935
     C                     ENDIF                                          136935
      *
      ***  Move file fields to subfile fields (Security line).
      *
     C                     EXSR P009
     C                     MOVE '0'       *IN85            SFLNXTCHG
     C                     MOVE '0'       *IN45
     C                     UPDATSE7593S1
      *
      ***  Access SECODPL3 with Reference, Option Number, Branch, Depot and Security '**CASH**'.
      *
     C                     MOVEL'**CASH**'WWSECS    P
     C           KEY2      CHAINSECODPL3             36
      *
      ***  If record no longer on file, handle Database Error.
      *
     C           *IN36     IFEQ '1'
     C                     Z-ADD017       DBASE            ***************
     C                     MOVEL'SECODPL3'DBFILE           * DB ERROR 17 *
     C                     MOVELWWREFN    DBKEY1           ***************
     C                     MOVE WWOPNB    DBKEY1
     C                     MOVELWWBRCA    DBKEY2  9
     C                     MOVE WWDPOT    DBKEY2
     C                     MOVELDBKEY1    DBKEY3 17
     C                     MOVE DBKEY2    DBKEY3
     C                     MOVELDBKEY3    DBKEY4 27
     C                     MOVE WWSECS    DBKEY4
     C                     MOVELDBKEY4    DBKEY
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ***  Save some fields in work fields.
      *
     C                     Z-ADDMBACAA    WWACAA 150       Old Actual Cash Allocation
     C                     Z-ADDMBACAR    WWACAR 150       Old Actual Cash Rounding
     C                     Z-ADDMBBCAA    SVBCAA 150       Balancing Cash Allocation
     C                     Z-ADDMBHOCA    WWHOCA 150       Holding Taken as Cash
      *
      ***  If Actual stock Allocation changed.
      *
     C           DDACT2    IFNE DDAC2H
      *
      ***  Recalculate Actual Cash Allocation (MBACAA).
      *
     C                     EXSR REACAA
      *
      ***  If Actual Cash Allocation recalculated, recalculate Balancing Cash (MBBCAA) on file
      ***  by subtracting old value and adding new value of Actual Cash Allocation.
      *
     C           AMACAA    IFEQ 'Y'
     C                     SUB  WWACAA    MBBCAA
     C                     ADD  MBACAA    MBBCAA
      *                                                                   136935
      ***  If Balancing Difference recalculated not 0 and Processing Type 136935
      ***  = Currency Change or not Currency Change with ICD field 'Allo- 136935
      ***  cate Difference to Dummy' = 'Y', save Balancing Difference and 136935
      ***  reset it.                                                      136935
      *                                                                   136935
     C           MBBCAA    IFNE 0                                         136935
     C           WWPTYP    ANDEQ'CC'                                      136935
     C           MBBCAA    ORNE 0                                         136935
     C           WWPTYP    ANDNE'CC'                                      136935
     C           BVALDC    ANDEQ'Y'                                       136935
     C                     Z-SUBMBBCAA    DUACAA                          136935
     C                     Z-ADD0         MBBCAA                          136935
     C                     ENDIF                                          136935
     C                     ENDIF
      *
     C                     ENDIF
      *
      ***  If Actual Cash Rounding recalculated or changed, recalculate Actual Cash Rounding on file
      ***  by subtracting old value and adding new value of Actual Cash Rounding.
      *
     C           AMACAR    IFEQ 'Y'
     C           WWPTYP    IFEQ 'CR'
     C                     Z-ADDNMDPCO    WWNBDP           Event Currenbcy Decimal Places
     C                     ELSE
     C                     Z-ADDNMDPCN    WWNBDP           New Currency Decimal Places
     C                     ENDIF
     C                     Z-ADDMBACAR    WUNUM
     C                     EXSR RTVNDP
     C                     Z-ADDWWNUM     WUACAR
     C                     Z-ADDNMDPCN    WWNBDP           New Currency Decimal Places
     C                     Z-ADDWOACAR    WUNUM
     C                     EXSR RTVNDP
     C                     SUB  WWNUM     WUACAR
     C                     Z-ADDWNACAR    WUNUM
     C                     EXSR RTVNDP
     C                     ADD  WWNUM     WUACAR
     C           WWPTYP    IFEQ 'CR'
     C                     Z-ADDNMDPCO    WWNBDP           Event Currency Decimal Places
     C                     ELSE
     C                     Z-ADDNMDPCN    WWNBDP           New Currency Decimal Places
     C                     ENDIF
     C                     MOVE 'R'       WUROOP           Rounding = half adjust
     C                     Z-ADDWUACAR    WWNUM
     C                     EXSR ROUNDD
     C                     EXSR SETNDP
     C                     Z-ADDWUNUM     MBACAR           Actual Cash Rounding
      *
      ***  Recalculate Balancing Cash on file by adding old value and subtracting new value of
      ***  Actual Cash Rounding.
      *
     C                     ADD  WWACAR    MBBCAA
     C                     SUB  MBACAR    MBBCAA
      *                                                                   136935
      ***  If Balancing Difference recalculated not 0 and Processing Type 136935
      ***  = Currency Change or not Currency Change with ICD field 'Allo- 136935
      ***  cate Difference to Dummy' = 'Y', save Balancing Difference and 136935
      ***  reset it.                                                      136935
      *                                                                   136935
     C           MBBCAA    IFNE 0                                         136935
     C           WWPTYP    ANDEQ'CC'                                      136935
     C           MBBCAA    ORNE 0                                         136935
     C           WWPTYP    ANDNE'CC'                                      136935
     C           BVALDC    ANDEQ'Y'                                       136935
     C                     Z-ADDMBBCAA    DUACAR                          136935
     C                     Z-ADD0         MBBCAA                          136935
     C                     ENDIF                                          136935
     C                     ENDIF
      *
      ***  If Actual Cash Allocation, Actual Cash Rounding or Balancing Cash Allocation changed,
      ***  update record on LF/SECODPL3.
      *
     C           WWACAA    IFNE MBACAA
     C           WWACAR    ORNE MBACAR
     C           SVBCAA    ORNE MBBCAA
     C                     MOVE 'A'       MBCHTP           Type of last change
     C                     MOVE USER      MBLUID           User Id.
     C                     Z-ADDBJRDNB    MBLCD            Last change date
     C                     ENDIF
     C                     UPDATSECODPD0
     C                     MOVE 'Y'       WWUPDT           Update done
      *                                                                   136935
      ***  If at least one Balancing Difference has been allocated to     136935
      ***  Dummy Customer, update record from SECOALL7 for New Security   136935
      ***  and Dummy Customer.                                            136935
      *                                                                   136935
     C           DUACAA    IFNE 0                                         136935
     C           DUACAR    ORNE 0                                         136935
     C           KEY7      CHAINSECOALL7             36                   136935
      *                                                                   136935
      ***  If record no longer on file, handle Database Error.            136935
      *                                                                   136935
     C           *IN36     IFEQ '1'                                       136935
     C                     Z-ADD026       DBASE            ***************136935
     C                     MOVE 'SECOALL7'DBFILE           * DB ERROR 26 *136935
     C                     MOVELWWREFN    DBKEY1           ***************136935
     C                     MOVE WWOPNB    DBKEY1                          136935
     C                     MOVELWWBRCD    DBKEY2  9                       136935
     C                     MOVE WWDDPT    DBKEY2                          136935
     C                     MOVELDBKEY1    DBKEY3 17                       136935
     C                     MOVE DBKEY2    DBKEY3                          136935
     C                     MOVELDBKEY3    DBKEY4 27                       136935
     C                     MOVE WWSECS    DBKEY4                          136935
     C                     MOVELWWCOTP    DBKEY5  3                       136935
     C                     MOVE WWBOOK    DBKEY5                          136935
     C                     MOVELWWERCU    DBKEY6 10                       136935
     C                     MOVE WWPTFR    DBKEY6                          136935
     C                     MOVELDBKEY5    DBKEY7 13                       136935
     C                     MOVE DBKEY6    DBKEY7                          136935
     C                     MOVELDBKEY4    DBKEY8 40                       136935
     C                     MOVE DBKEY7    DBKEY8                          136935
     C                     MOVELDBKEY8    DBKEY                           136935
     C                     EXSR *PSSR                                     136935
     C                     ENDIF                                          136935
     C                     ADD  DUACAA    MCACAA                          136935
     C                     ADD  DUACAR    MCACAR                          136935
     C                     MOVE 'A'       MCCHTP                          136935
     C                     Z-ADDBJRDNB    MCLCD                           136935
     C                     MOVE USER      MCLUID                          136935
     C                     MOVE 'M'       MCREST                          136935
     C                     UPDATSECOALD0                                  136935
     C                     ENDIF                                          136935
      *
      ***  If at least one 'CASH' field changed, update 'CASH' line on subfile.
      *
     C           UPDCAS    IFEQ 'Y'
     C/COPY WNCPYSRC,SEH00038
     C                     Z-ADD#1RRN     W3RRN   40
     C                     Z-ADD1         #1RRN
     C           #1RRN     CHAINSE7593S1             89
      *
      ***  Move file fields to subfile fields.
      *
     C                     EXSR P010
     C                     MOVE '0'       *IN85            SFLNXTCHG
     C                     MOVE '1'       *IN45
     C                     UPDATSE7593S1
     C                     Z-ADDW3RRN     #1RRN
     C                     ENDIF
      *
      ***  If error, update subfile.
      *
     C                     ELSE
     C                     MOVE '1'       *IN85            SFLNXTCHG
     C                     MOVE '0'       *IN45
     C                     UPDATSE7593S1
     C                     ENDIF
     C                     ELSE
     C           DDACN3    IFEQ ' '
     C                     MOVE '0'       *IN85            SFLNXTCHG
     C                     MOVE '0'       *IN45
     C                     UPDATSE7593S1
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  REASTR - Recalculate Actual Stock Rounding for New Security  *
      *           on Security line.                                   *
      *                                                               *
      *  Called by: V001   - Validate details input (Amend Mode).     *
      *                                                               *
      *  Calls: RTVNDP - Set amount from num field 15.0 to num field  *
      *                  30.9 with Retrieve Number of Decimal Places  *
      *                  from a Ccy.                                  *
      *         ROUNDD - Perform rounding to the Number of Decimal    *
      *                  Places.                                      *
      *                                                               *
      *****************************************************************
      *
     C           REASTR    BEGSR                           ** REASTR SR **
      *
      ***  Retrieve Holding Taken as Stock.
      *
     C                     Z-ADDWNMDPO    WWNBDP  10       Event Security Decimal Places
     C                     Z-ADDMBHOST    WUNUM
     C                     EXSR RTVNDP
     C                     Z-ADDWWNUM     WTHOST 309
      *
     C           WWPTYP    IFEQ 'DV'                       If Processing Type = Dividends Events
     C           MDSDIT    ANDEQ'S'                           Stock Dividend Type = Stock for Stock
     C           MDOSHA    ANDNE0                             Number of Old Shares not zero
     C           WWNSHA    ANDNE0                             Number of New Shares not zero
      *
     C           WWPTYP    OREQ 'DV'                       Or Processing Type = Dividends Events
     C           MDSDIT    ANDEQ'C'                           Stk Dividend Tp=Combined for Cash&Stk
     C           MDOSHA    ANDNE0                             Number of Old Shares not zero
     C           WWNSHA    ANDNE0                             Number of New Shares not zero
      *
     C           WWPTYP    OREQ 'DV'                       Or Processing Type = Dividends Events
     C           MDSDIT    ANDEQ'O'                           Stk Dividend Tp=Optional for Cash&Stk
     C           MDOSHA    ANDNE0                             Number of Old Shares not zero
     C           WWNSHA    ANDNE0                             Number of New Shares not zero
      *
     C           WWPTYP    OREQ 'CR'                       Or Proces.Type = Corporate Reorganisation
     C           MDOSHA    ANDNE0                             Number of Old Shares not zero
     C           WWNSHA    ANDNE0                             Number of New Shares not zero
      *
     C           WWPTYP    OREQ 'OF'                       Or Processing Type = Offers
     C           MIOFTY    ANDEQ'E'                           Offer Type = Exchange Offers
     C           MDOSHA    ANDNE0                             Number of Old Shares not zero
     C           WWNSHA    ANDNE0                             Number of New Shares not zero
      ******                                                              136935
     C******     MAPTYP    OREQ 'OF'                       Or Subscript.  136935
     C******     MIOFTY    ANDEQ'S'                           Offers      136935
     C******     MDOSHA    ANDNE0                             Old Sh.not 0136935
     C******     WWNSHA    ANDNE0                             New Sh.not 0136935
      *
     C           WWPTYP    OREQ 'EX'                       Or Proces.Type= Exercices and Conversion
     C           MDOSHA    ANDNE0                             Number of Old Shares not zero
     C           WWNSHA    ANDNE0                             Number of New Shares not zero
     C           MDSBPR    ANDNE0                             Subscription Price not zero
      *
     C           WWPTYP    OREQ 'FR'                       Or Processing Type = Free Allocation
     C           MDOSHA    ANDNE0                             Number of Old Shares not zero
     C           WWNSHA    ANDNE0                             Number of New Shares not zero
      *
     C           WWPTYP    OREQ 'RG'                       Or Processing Type = Rights Issues
     C           MDOSHA    ANDNE0                             Number of Old Shares not zero
     C           WWNSHA    ANDNE0                             Number of New Shares not zero
      *                                                                   CEU017
      ***  Or Processing Type = Currency Changes and Old and New Nominal  CEU017
      ***  Size not zero.                                                 CEU017
      *                                                                   CEU017
     C           WWPTYP    OREQ 'CC'                                      CEU017
     C           MDOSHA    ANDNE0                                         CEU017
     C           WWNSHA    ANDNE0                                         CEU017
      *
      ***  Calculate work field A = Total of all Customers and Books Holdings Taken as Stock
      ***  divided by Number of Old Shares.
      *
     C           WTHOST    DIV  MDOSHA    FLDA   309
      *
      ***  Calculate True Stock Allocation (using Total of all Customers and Books Holdings Taken
      ***  as Stock as Holding) = work field A previously calculated multiplied by Number of New
      ***  Shares.
      *
     C           FLDA      MULT WWNSHA    WUOTSA 309
      *
      ***  Calculate Actual Stock Rounding = True Stock Allocation (using Total of all Customers and
      ***  Books Holdings Taken as Stock) - Actual Stock Allocation entered.
      *
     C                     Z-ADDWNMDPN    WWNBDP  10       New Security Decimal Places
     C                     Z-ADDMBASTA    WUNUM
     C                     EXSR RTVNDP                     retrieve Actual Stock Allocation
     C           WUOTSA    SUB  WWNUM     WUASTR 309
      *
      ***  Set Actual Stock Rounding calculated.
      *
     C                     Z-ADD8         WWNBDP           8 Decimal Places
     C                     MOVE WWRNDN    WUROOP  1        Rounding from CO Detail file
     C                     Z-ADDWUASTR    WWNUM
     C                     EXSR ROUNDD
     C                     Z-ADDWWNUM     WUASTR
     C                     Z-ADDWUASTR    MBASTR           Actual Stock Rounding
     C                     MOVE 'Y'       AMASTR           Actual Stock Rounding recalculated ind.
     C                     ENDIF
      *
     C           WWPTYP    IFEQ 'DV'                       If Processing Type = Dividends Events
     C           MDSDIT    ANDEQ'S'                           Stock Dividend Type = Stock for Stock
     C********** MDDIVU    ANDNE0                             Dividend per Unit not zero      190204
     C           MDDIV1    ANDNE0                             Dividend per Unit not zero      190204
     C           MDSBPR    ANDNE0                             Subscription Price not zero
      *
      ***  Calculate Cash Pool = Total of all Customers and Books Holdings Taken as Stock multiplied
      ***  by Dividend Per Unit.
      *
     C******               Z-ADDWNMDPN    WWNBDP           New Sec.Dec.Pl.135690
     C**********           Z-ADDNMDPCN    WWNBDP           New Cur.135690 190204
     C**********           Z-ADDMDDIVU    WUNUM                           190204
     C**********           EXSR RTVNDP                     retrieve Divide190204
     C                     Z-ADDMDDIV1    WWNUM            13,8 => 30,9   190204
     C           WTHOST    MULT WWNUM     CASPOO 309
      *
      ***  Calculate work field BB = Cash Pool divided by Subscription Price.
      *
     C           CASPOO    DIV  MDSBPR    FLDBB  309
      *
      ***  If Price Basis is 'P' for %, result should be divided by 100.
      *
     C           WSPBSN    IFEQ 'P'
     C           FLDBB     DIV  100       FLDBB
     C                     ENDIF
      *
      ***  Set True Stock Allocation (using Total of all Customers and Books Holdings Taken
      ***  as Stock as Holding) = work field BB previously calculated.
      *
     C                     Z-ADDFLDBB     WUOTSA
      *
      ***  Calculate Actual Stock Rounding = True Stock Allocation (using Total of all Customers and
      ***  Books Holdings Taken as Stock) - Actual Stock Allocation entered.
      *
     C                     Z-ADDWNMDPN    WWNBDP  10       New Security Decimal Places
     C                     Z-ADDMBASTA    WUNUM
     C                     EXSR RTVNDP                     Retrieve Actual Stock allocation
     C           WUOTSA    SUB  WWNUM     WUASTR 309
      *
      ***  Set Actual Stock Rounding calculated.
      *
     C                     Z-ADD8         WWNBDP           8 Decimal Places
     C                     MOVE WWRNDN    WUROOP  1        Rounding from CO Detail file
     C                     Z-ADDWUASTR    WWNUM
     C                     EXSR ROUNDD
     C                     Z-ADDWWNUM     WUASTR
     C                     Z-ADDWUASTR    MBASTR           Actual Stock Rounding
     C                     MOVE 'Y'       AMASTR           Actual Stock Rounding recalculated ind.
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  REACAR - Recalculate Actual Cash Rounding for New Security   *
      *           on Security line.                                   *
      *                                                               *
      *  Called by: V001   - Validate details input (Amend Mode).     *
      *                                                               *
      *  Calls: SETNDP - Set amount from num field 30.9 to num field  *
      *                  15.0 with Number of Decimal Places from a    *
      *                  Currency.                                    *
      *         RTVNDP - Set amount from num field 15.0 to num field  *
      *                  30.9 with Retrieve Number of Decimal Places  *
      *                  from a Ccy.                                  *
      *         ROUNDD - Perform rounding to the Number of Decimal    *
      *                  Places.                                      *
      *                                                               *
      *****************************************************************
      *
     C           REACAR    BEGSR                           ** REACAC SR **
      *
     C                     Z-ADD0         WUACAR           Reset Actual Cash Rounding work field
      *
     C           WWPTYP    IFEQ 'DV'                       If Processing Type = Dividends Events
     C           MDSDIT    ANDEQ'S'                           Stock Dividend Type = Stock for Stock
     C           MDOSHA    ANDNE0                             Number of Old Shares not zero
     C           WWNSHA    ANDNE0                             Number of New Shares not zero
      *
     C           WWPTYP    OREQ 'DV'                       Or Processing Type = Dividends Events
     C           MDSDIT    ANDEQ'C'                           Stk Dividend Tp=Combined for Cash&Stk
     C           MDOSHA    ANDNE0                             Number of Old Shares not zero
     C           WWNSHA    ANDNE0                             Number of New Shares not zero
      *
     C           WWPTYP    OREQ 'DV'                       Or Processing Type = Dividends Events
     C           MDSDIT    ANDEQ'O'                           Stk Dividend Tp=Optional for Cash&Stk
     C           MDOSHA    ANDNE0                             Number of Old Shares not zero
     C           WWNSHA    ANDNE0                             Number of New Shares not zero
      *
     C           WWPTYP    OREQ 'FR'                       Or Processing Type = Free Allocation
     C           MDOSHA    ANDNE0                             Number of Old Shares not zero
     C           WWNSHA    ANDNE0                             Number of New Shares not zero
      *
     C           WWPTYP    OREQ 'RG'                       Or Processing Type = Rights Issues
     C           MDOSHA    ANDNE0                             Number of Old Shares not zero
     C           WWNSHA    ANDNE0                             Number of New Shares not zero
      *
     C           WWPTYP    OREQ 'CR'                       Or Proces.Type = Corporate Reorganisation
     C           MDOSHA    ANDNE0                             Number of Old Shares not zero
     C           WWNSHA    ANDNE0                             Number of New Shares not zero
      *
     C           WWPTYP    OREQ 'OF'                       Or Processing Type = Offers
     C           MIOFTY    ANDEQ'E'                           Exchange    136935
     C           MDOSHA    ANDNE0                             Number of Old Shares not zero
     C           WWNSHA    ANDNE0                             Number of New Shares not zero
      *
     C           WWPTYP    OREQ 'EX'                       Or Proces.Type= Exervices and Conversions
     C           MDOSHA    ANDNE0                             Number of Old Shares not 0
     C           WWNSHA    ANDNE0                             Number of New Shares not 0
     C           MDSBPR    ANDNE0                             Subscription Price not 0
      *                                                                   CEU017
      ***  Or Processing Type = Currency Changes and Old and New Nominal  CEU017
      ***  Size not zero.                                                 CEU017
      *                                                                   CEU017
     C           WWPTYP    OREQ 'CC'                                      CEU017
     C           MDOSHA    ANDNE0                                         CEU017
     C           WWNSHA    ANDNE0                                         CEU017
      *
      ***  If Rounding Settlement in Cash = 'Y', Actual Cash Rounding = Actual Stock
      ***  Rounding re-calculated previously multiplied by Compensation Price.
      *
     C           WWRNDS    IFEQ 'Y'
     C           WUASTR    MULT WWCOMP    WUACAR 309
      *
      ***  If Price Basis is 'P' for %, result should be divided by 100.
      *
     C           WSPBSN    IFEQ 'P'
     C           WUACAR    DIV  100       WUACAR
     C                     ENDIF
      *
      ***  Set Actual Cash Rounding calculated.
      *
     C                     Z-ADDNMDPCN    WWNBDP           New Currency Decimal Places
     C                     MOVE 'R'       WUROOP           Rounding = half adjust
     C                     Z-ADDWUACAR    WWNUM  309
     C                     EXSR ROUNDD
     C                     Z-ADDWWNUM     WUACAR
     C                     EXSR SETNDP
     C                     Z-ADDWUNUM     MBACAR           Actual Cash Rounding
     C                     MOVE 'Y'       AMACAR           Actual Cash Rounding recalculated ind.
      *
      ***  Else, If Rounding Settlement in Cash not equal 'Y', set Actual Cash Rounding to zero.
      *
     C                     ELSE
     C                     Z-ADD0         MBACAR
     C                     MOVE 'Y'       AMACAR           Actual Cash Rounding recalculated ind.
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           WWPTYP    IFEQ 'DV'                       If Processing Type = Dividends Events
     C           MDSDIT    ANDEQ'S'                           Stock Dividend Type = Stock for Stock
     C********** MDDIVU    ANDNE0                             Dividend Per Unit not zero      190204
     C           MDDIV1    ANDNE0                             Dividend Per Unit not zero      190204
     C           MDSBPR    ANDNE0                             Subscription Price not zero
      *
      ***  If Rounding Settlement in Cash = 'Y', Actual Cash Rounding = (Holding Taken as Stock
      ***  from New Security multiplied by Dividend Per Unit) - (Actual Stock Allocation entered
      ***  multiplied by Subscription Price).
      *
     C           WWRNDS    IFEQ 'Y'
     C                     Z-ADDWNMDPO    WWNBDP           Event Security Decimal Places
     C                     Z-ADDMBHOST    WUNUM
     C                     EXSR RTVNDP                     retrieve Holding Taken as Stock
     C                     Z-ADDWWNUM     WTHOST
     C******               Z-ADDWNMDPN    WWNBDP           New Sec.Dec.Pl.135690
     C**********           Z-ADDNMDPCN    WWNBDP           New Cur.135690 190204
     C**********           Z-ADDMDDIVU    WUNUM                           190204
     C**********           EXSR RTVNDP                     retrieve Divide190204
     C                     Z-ADDMDDIV1    WWNUM            13,8 => 30,9   190204
     C           WTHOST    MULT WWNUM     W1ACAR 309
     C                     Z-ADDMBASTA    WUNUM
     C                     EXSR RTVNDP                     retrieve Actual Stock Allocation
     C           WWNUM     MULT MDSBPR    W2ACAR 309
      *
      ***  If Price Basis is 'P' for %, result should be divided by 100.
      *
     C           WSPBSN    IFEQ 'P'
     C           W2ACAR    DIV  100       W2ACAR
     C                     ENDIF
     C           W1ACAR    SUB  W2ACAR    WUACAR
      *
      ***  Set Actual Cash Rounding calculated.
      *
     C                     Z-ADDNMDPCN    WWNBDP           New Currency Decimal Places
     C                     MOVE 'R'       WUROOP           Rounding = half adjust
     C                     Z-ADDWUACAR    WWNUM  309
     C                     EXSR ROUNDD
     C                     Z-ADDWWNUM     WUACAR
     C                     EXSR SETNDP
     C                     Z-ADDWUNUM     MBACAR           Actual Cash Rounding
     C                     MOVE 'Y'       AMACAR           Actual Cash Rounding recalculated ind.
      *
      ***  Else, If Rounding Settlement in Cash not equal 'Y', set Actual Cash Rounding to zero.
      *
     C                     ELSE
     C                     Z-ADD0         MBACAR
     C                     MOVE 'Y'       AMACAR           Actual Cash Rounding recalculated ind.
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  REACAA - Recalculate Actual Cash Allocation for Security     *
      *           '**CASH**' on Security line.                        *
      *                                                               *
      *  Called by: V001   - Validate details input (Amend Mode).     *
      *                                                               *
      *  Cals: SETNDP - Set amount from num field 30.9 to num field   *
      *                 15.0 with Number of Decimal Places from a     *
      *                 Currency.                                     *
      *        RTVNDP - Set amount from num field 15.0 to num field   *
      *                 30.9 with Retrieve Number of Decimal Places   *
      *                 from a Ccy.                                   *
      *        ROUNDD - Perform rounding to the Number of Decimal     *
      *                 Places.                                       *
      *                                                               *
      *****************************************************************
      *
     C           REACAA    BEGSR                           ** REACAA SR **
      *
      *****If*Processing*Type*=*Exercices*and*Conversions,*calculate*Actu 161732
      *****Holding*Taken*as*Cash*from*New*Security*multiplied*by*Subscrip 161732
      ** If Processing Type = Exercises and Conversions, calculate        161732
      ** Actual Cash Allocation = Holding Taken as Cash multiplied        161732
      ** with the Receive/Cash Price.                                     161732
      *
     C           WWPTYP    IFEQ 'EX'                       If Exercices and Conversions
     C                     Z-ADDNMDPCN    WWNBDP           New Currency Decimal Places
     C                     Z-ADDWWHOCA    WUNUM
     C                     EXSR RTVNDP                     retrieve Holding Taken as Cash
     C***********WWNUM     MULT MDSBPR    WWACAA                          161732
     C           WWNUM     MULT MDRCPR    WWACAA                          161732
      *
      ***  If Price Basis is 'P' for %, result should be divided by 100.
      *
     C           WSPBSN    IFEQ 'P'
     C           WWACAA    DIV  100       WWACAA
     C                     ENDIF
      *
      ***  Set Actual Cash Allocation calculated.
      *
     C                     MOVE 'R'       WUROOP           Rounding = half adjust
     C                     Z-ADDWWACAA    WWNUM
     C                     EXSR ROUNDD
     C                     Z-ADDWWNUM     WWACAA
     C                     EXSR SETNDP
     C                     Z-ADDWUNUM     MBACAA           Actual Cash Allocation
     C                     MOVE 'Y'       AMACAA           Actual Cash Allocation recalculated ind.
     C                     ELSE
      *
      ***  If Processing Type = Dividend Events and Stock Dividend type not Stock for Stock,
      ***  calculate Actual Cash Allocation = Holding Taken as Cash from New Security
      ***  multiplied by Dividend Per Unit, and half adjust.
      *
     C           WWPTYP    IFEQ 'DV'                       If Dividend Events
     C           MDSDIT    ANDNE'S'                           not Stock for Stock
     C******               Z-ADDWNMDPN    WWNBDP           New Sec.Dec.pl.135690
     C                     Z-ADDNMDPCN    WWNBDP           New Cur.Dec.Pl.135690
     C**********           Z-ADDMDDIVU    WUNUM                           190204
     C                     Z-ADDMDDIV1    WUNUM                           190204
     C                     EXSR RTVNDP                     retrieve Dividend Per Unit
     C                     Z-ADDWWNUM     WWDIVU 309
     C                     Z-ADDNMDPCN    WWNBDP           New Currency Decimal Places
     C                     Z-ADDWWHOCA    WUNUM
     C                     EXSR RTVNDP                     retrieve Holding Taken as Cash
     C           WWNUM     MULT WWDIVU    WWACAA
      *
      ***  Set Actual Cash Allocation calculated.
      *
     C                     MOVE 'R'       WUROOP           Rounding = half adjust
     C                     Z-ADDWWACAA    WWNUM
     C                     EXSR ROUNDD
     C                     Z-ADDWWNUM     WWACAA
     C                     EXSR SETNDP
     C                     Z-ADDWUNUM     MBACAA           Actual Cash Allocation
     C                     MOVE 'Y'       AMACAA           Actual Cash Allocation recalculated ind.
     C                     ENDIF
     C                     ENDIF
      *                                                                   136935
      ***  If Processing Type = Offers with Offer Type = Subscription,    136935
      ***  calculate Actual Cash Allocation = Actual Stock Allocation     136935
      ***  multiplied by Subscription Price.                              136935
      *                                                                   136935
     C           MAPTYP    IFEQ 'OF'                                      136935
     C           MIOFTY    ANDEQ'S'                                       136935
     C                     Z-ADDWNMDPN    ZSDEC                           136935
     C                     Z-ADDWUASTA    WUNUM                           136935
     C                     EXSR RTVNDP                                    136935
     C           WWNUM     MULT MISUPR    WWACAA                          136935
      *                                                                   136935
      ***  If Price Basis is 'P' for %, result should be divided by 100.  136935
      *                                                                   136935
     C           WSPBSN    IFEQ 'P'                                       136935
     C           WWACAA    DIV  100       WWACAA                          136935
     C                     ENDIF                                          136935
      *                                                                   136935
      ***  Set Actual Cash Allocation calculated.                         136935
      *                                                                   136935
     C                     Z-ADDNMDPCN    WWNBDP           New Cur.Dec.Pl.136935
     C                     MOVE 'R'       WUROOP           Half Adjust    136935
     C                     Z-ADDWWACAA    WWNUM                           136935
     C                     EXSR ROUNDD                                    136935
     C                     Z-ADDWWNUM     WWACAA                          136935
     C                     EXSR SETNDP                                    136935
     C                     Z-ADDWUNUM     MBACAA                          136935
     C                     MOVE 'Y'       AMACAA                          136935
     C                     ENDIF                                          136935
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  U001   - Load field for file SECODRL0.                       *
      *                                                               *
      *  Called by: P008   - Update/Write LF/SECODRL0 (Amend Mode).   *
      *                                                               *
      *  Calls: ZDATE1 - Standart subroutine to validate dates        *
      *                  submitted and convert to a number of days.   *
      *                                                               *
      *****************************************************************
      *
     C           U001      BEGSR                           ** U001 SR **
      *
      ***  If record not found in LF/SECODRL0, load "inserted" fields.
      *
     C           *IN35     IFEQ '1'
     C                     MOVE 'D'       MSRECI           Record Id.
     C                     MOVE 'I'       MSCHTP           Type of last Change = Insert
     C                     MOVE DDREF3    MSCORF           Reference Number
     C                     MOVE DDBRC3    MSBRCD           Branch
     C                     MOVELDDDEP3    MSDDPT           Depot
     C                     ELSE
      *
      ***  Else, if record found in LF/SECODRL0, load "changed" fields.
      *
     C                     MOVE 'A'       MSCHTP           Type of last Change = Amend
     C                     ENDIF
      *
      ***  Load file fields.
      *
     C                     Z-ADDBJRDNB    MSLCD            Last Change Date = Run Date
     C                     MOVELUSER      MSLUID           User Id.
     C                     MOVE DDFLA3    MSFAID           Final Allocation Indicator
     C                     MOVE DDSTS3    MSCOST           Corporate Actions Status
      *
     C                     MOVE DDLTA3    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    MSCOAD           Last Allocation Date
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  U002   - Load fields for file SECODPL3 with all amounts = 0. *
      *                                                               *
      *  Called by: P004   - Build the subfile (Amend Mode).          *
      *                                                               *
      *  Calls: ZASIGN - Standart subroutine to validate and          *
      *                  right-align signed numeric fields.           *
      *                                                               *
      *****************************************************************
      *
     C           U002      BEGSR                           ** U002 SR **
      *
      ***  Load file fields.
      *
     C                     MOVE 'D'       MBRECI           Record Id.
     C                     Z-ADDBJRDNB    MBLCD            Last Change Date = Run Date
     C                     MOVE 'I'       MBCHTP           Type of last Change = Insert
     C                     MOVELUSER      MBLUID           User Id.
      *
     C                     MOVE DDREF3    MBCORF           Reference Number
     C                     MOVE DDOPT3    MBOPTN           Option Number
     C                     MOVE DDBRC3    MBBRCD           Branch
     C                     MOVELDDDEP3    MBDDPT           Depot
      *
     C                     MOVE WWSECS    MBSESN           New Security Shortname
      *
     C                     MOVE *BLANKS   ZFLD17
     C                     MOVELWWEXPO    ZFLD17
     C                     Z-ADDWNMDPO    ZSDEC            Event Security Decimal Places
     C           15        SUB  ZSDEC     ZSDIG
     C                     EXSR ZASIGN
     C                     MOVE ZOUT15    MBEXPO           Ex-Date Position
      *
      ***  Set all amounts to 0.
      *
     C                     Z-ADD*ZEROS    MBOALR           Original Stock Allocation
     C                     Z-ADD*ZEROS    MBOTSA           True Stock Allocation
     C                     Z-ADD*ZEROS    MBOSTR           Original Stock Rounding
     C                     Z-ADD*ZEROS    MBOCAR           Original Cash Rounding
     C                     Z-ADD*ZEROS    MBCAOP           Original Cash Option
     C                     Z-ADD*ZEROS    MBASTA           Actual Stock Allocation
     C                     Z-ADD*ZEROS    MBHOST           Holding Taken as Stock
     C                     Z-ADD*ZEROS    MBHOCA           Holding Taken as Cash
     C                     Z-ADD*ZEROS    MBACAA           Actual Cash Allocation
     C                     Z-ADD*ZEROS    MBASTR           Actual Stock Rounding
     C                     Z-ADD*ZEROS    MBACAR           Actual Cash Rounding
     C                     Z-ADD*ZEROS    MBBSTA           Balancing Stock Allocation
     C                     Z-ADD*ZEROS    MBBCAA           Balancing Cash Allocation
     C                     Z-ADD*ZEROS    MBBSTR           Balancing Stock Rounding
     C                     Z-ADD*ZEROS    MBBCAR           Balancing Cash Rounding
      *
     C                     Z-ADD*ZEROS    MBTNL1           Position Settlement TNL1 = 0
     C                     MOVE *BLANKS   MBCOAT           Corporate Action Type = blank
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  ACCSEC - Access Security Details file.                       *
      *                                                               *
      *  Called by: P001   - Initial processing.                      *
      *             P004   - Build the subfile (Amend Mode).          *
      *             V001   - Validate details input (Amend Mode).     *
      *                                                               *
      *  Calls: *PSSR  - Error processing.                            *
      *                                                               *
      *****************************************************************
      *
     C           ACCSEC    BEGSR                           ** ACCSEC SR **
      *
      ***  Retrieve Nominal Currency, Nominal Currency decimal Places and Price Basis.
      *
     C           KSECN     CHAINSECTYZ1              30
      *
      ***  If no record found, handle Database Error.
      *
     C           *IN30     IFEQ '1'
     C                     Z-ADD018       DBASE            ***************
     C                     MOVEL'SECTY   'DBFILE           * DB ERROR 18 *
     C                     MOVELKSECN     DBKEY            ***************
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error processing.                                   *
      *                                                               *
      *  Called by: P001   - Initial processing.                      *
      *             P004   - Build the subfile (Amend Mode).          *
      *             P007   - Process Action.                          *
      *             V001   - Validate details input (Amend Mode).     *
      *             ACCSEC - Access Security Details file.            *
      *                                                               *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
      ***  Update LDA with error information.
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVELDBFILE    WWFILE
     C                     MOVELDBKEY     WWKEY
     C                     MOVELDBPGM     WWPGM
     C                     MOVELDBASE     WWASE
     C                     OUT  LDA
      *
      ***  Rollback changes, print dump and cancel program.
      *
     C                     ROLBK
     C                     MOVE 'E'       WWRTCD
     C                     DUMP
      *
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  ZASNMS - Send message to program message queue.              *
      *                                                               *
      *  Called by: P002   - Detail processing.                       *
      *             P006   - Validate and perform Action taken (Amend *
      *                      Mode).                                   *
      *             V001   - Validate details input (Amend Mode).     *
      *                                                               *
      *  Calls: Y2SNMGC  - Midas ABS Send a message SNDxxxMSG.        *
      *                                                               *
      *****************************************************************
      *
     C           ZASNMS    BEGSR                           ** ZASNMS SR **
      *
      ***  Send message.
      *
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGM            Program queue
     C                     PARM           ZAPGRL           Rel queue
     C                     PARM           ZAMSID           Message Id.
     C                     PARM           ZAMSGF           Message file.
     C                     PARM           ZAMSDA           Message data.
     C                     PARM           ZAMSTP           Message type.
      *
      ***  Clear all fields for default mechanism next time.
      *
     C                     MOVEL*BLANKS   ZAPGM            Message queue
     C                     MOVEL*BLANKS   ZAPGRL           Rel queue
     C                     MOVEL*BLANKS   ZAMSDA           Message data.
     C                     MOVEL*BLANKS   ZAMSTP           Message type.
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SETNDP - Set amount from num field 30.9 to num field 15.0    *
      *           with Number of Decimal Places from a Currency.      *
      *                                                               *
      *  Called by: V001   - Validate details input (Amend Mode).     *
      *             REACAR - Recalculate Actual Cash Rounding for New *
      *                      Security on Security line.               *
      *             REACAA - Recalculate Actual Cash Allocation for   *
      *                      Security '**CASH**' on Security line.    *
      *                                                               *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           SETNDP    BEGSR                           ** SETNDP SR **
      *
     C           WWNBDP    IFEQ 0                          If no Decimal Place
     C                     Z-ADDWWNUM     WUNUM  150
     C                     ELSE
     C           WWNBDP    IFEQ 1                          If 1 Decimal Place
     C           WWNUM     MULT 10        WUNUM
     C                     ELSE
     C           WWNBDP    IFEQ 2                          If 2 Decimal Places
     C           WWNUM     MULT 100       WUNUM
     C                     ELSE
     C           WWNBDP    IFEQ 3                          If 3 Decimal Places
     C           WWNUM     MULT 1000      WUNUM
     C                     ELSE
     C           WWNBDP    IFEQ 4                          If 4 Decimal Places
     C           WWNUM     MULT 10000     WUNUM
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  RTVNDP - Set amount from num field 15.0 to num field 30.9    *
      *           with Retrieve Number of Decimal Places from a Ccy.  *
      *                                                               *
      *  Called by: V001   - Validate details input (Amend Mode).     *
      *             REASTR - Recalculate Actual Stock Rounding for    *
      *                      New Security on Security line.           *
      *             REACAR - Recalculate Actual Cash Rounding for New *
      *                      Security on Security line.               *
      *             REACAA - Recalculate Actual Cash Allocation for   *
      *                      Security '**CASH**' on Security line.    *
      *                                                               *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           RTVNDP    BEGSR                           ** RTVNDP SR **
      *
     C           WWNBDP    IFEQ 0                          If no Decimal Place
     C                     Z-ADDWUNUM     WWNUM
     C                     ELSE
     C           WWNBDP    IFEQ 1                          If 1 Decimal Place
     C           WUNUM     DIV  10        WWNUM
     C                     ELSE
     C           WWNBDP    IFEQ 2                          If 2 Decimal Places
     C           WUNUM     DIV  100       WWNUM
     C                     ELSE
     C           WWNBDP    IFEQ 3                          If 3 Decimal Places
     C           WUNUM     DIV  1000      WWNUM
     C                     ELSE
     C           WWNBDP    IFEQ 4                          If 4 Decimal Places
     C           WUNUM     DIV  10000     WWNUM
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  ROUNDD - Perform rounding to the Number of Decimal Places.   *
      *                                                               *
      *  Called by: V001   - Validate details input (Amend Mode).     *
      *             REASTR - Recalculate Actual Stock Rounding for    *
      *                      New Security on Security line.           *
      *             REACAR - Recalculate Actual Cash Rounding for New *
      *                      Security on Security line.               *
      *             REACAA - Recalculate Actual Cash Allocation for   *
      *                      Security '**CASH**' on Security line.    *
      *                                                               *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           ROUNDD    BEGSR                           ** ROUNDD SR **
      *
     C           WWNBDP    IFEQ 0                          If no Decimal Place
     C           WUROOP    IFEQ 'R'
     C                     Z-ADDWWNUM     WUFLD0 300H
     C                     ELSE
     C                     Z-ADDWWNUM     WUFLD0
     C           WUROOP    IFEQ 'U'
     C           WWNUM     ANDNEWUFLD0
     C                     ADD  1         WUFLD0
     C                     ENDIF
     C                     ENDIF
     C                     Z-ADDWUFLD0    WWNUM
     C                     ELSE
     C           WWNBDP    IFEQ 1                          If 1 Decimal Place
     C           WUROOP    IFEQ 'R'
     C                     Z-ADDWWNUM     WUFLD1 301H
     C                     ELSE
     C                     Z-ADDWWNUM     WUFLD1
     C           WUROOP    IFEQ 'U'
     C           WWNUM     ANDNEWUFLD1
     C                     ADD  0.1       WUFLD1
     C                     ENDIF
     C                     ENDIF
     C                     Z-ADDWUFLD1    WWNUM
     C                     ELSE
     C           WWNBDP    IFEQ 2                          If 2 Decimal Places
     C           WUROOP    IFEQ 'R'
     C                     Z-ADDWWNUM     WUFLD2 302H
     C                     ELSE
     C                     Z-ADDWWNUM     WUFLD2
     C           WUROOP    IFEQ 'U'
     C           WWNUM     ANDNEWUFLD2
     C                     ADD  0.01      WUFLD2
     C                     ENDIF
     C                     ENDIF
     C                     Z-ADDWUFLD2    WWNUM
     C                     ELSE
     C           WWNBDP    IFEQ 3                          If 3 Decimal Places
     C           WUROOP    IFEQ 'R'
     C                     Z-ADDWWNUM     WUFLD3 303H
     C                     ELSE
     C                     Z-ADDWWNUM     WUFLD3
     C           WUROOP    IFEQ 'U'
     C           WWNUM     ANDNEWUFLD3
     C                     ADD  0.001     WUFLD3
     C                     ENDIF
     C                     ENDIF
     C                     Z-ADDWUFLD3    WWNUM
     C                     ELSE
     C           WWNBDP    IFEQ 4                          If 4 Decimal Places
     C           WUROOP    IFEQ 'R'
     C                     Z-ADDWWNUM     WUFLD4 304H
     C                     ELSE
     C                     Z-ADDWWNUM     WUFLD4
     C           WUROOP    IFEQ 'U'
     C           WWNUM     ANDNEWUFLD4
     C                     ADD  0.0001    WUFLD4
     C                     ENDIF
     C                     ENDIF
     C                     Z-ADDWUFLD4    WWNUM
     C                     ELSE
     C           WWNBDP    IFEQ 8                          If 8 Decimal Places
     C           WUROOP    IFEQ 'R'
     C                     Z-ADDWWNUM     WUFLD8 308H
     C                     ELSE
     C                     Z-ADDWWNUM     WUFLD8
     C           WUROOP    IFEQ 'U'
     C           WWNUM     ANDNEWUFLD8
     C                     ADD  0.00000001WUFLD8
     C                     ENDIF
     C                     ENDIF
     C                     Z-ADDWUFLD8    WWNUM
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  CSBPR  - Calculate Subscription Price if Subscription Price  *
      *           entered as Market for Dividend Events.              *
      *                                                               *
      *  Called by: P004   - Build the subfile (Amend Mode).          *
      *                                                               *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           CSBPR     BEGSR                           ** CSBPR SR **
      *
      ***  Find Market Price with correct Date.
      *
     C                     MOVE 'N'       WWCHCK  1
      *
     C                     MOVE WWEXDT    KPVDT
     C           KEY4      SETGTPRICM
     C                     READPPRICM                    44
      *
      ***  If record found use this Price.
      *
     C           *IN44     IFEQ '0'
     C           PSSN      ANDEQMDNSEC
     C                     MOVE 'Y'       WWCHCK
      *
      ***  Otherwise check for Current Price.
      *
     C                     ELSE
     C                     Z-ADD*HIVAL    KPVDT
      *
     C           KEY4      SETGTPRICM
     C                     READPPRICM                    44
      *
      ***  If record found use this price.
      *
     C           *IN44     IFEQ '0'
     C           PSSN      ANDEQMDNSEC
     C                     MOVE 'Y'       WWCHCK
     C                     ENDIF
     C                     ENDIF
      *
     C           WWCHCK    IFEQ 'N'
     C                     Z-ADDMKPR      MPRICE 158       Market Price from Security file.
     C                     ELSE
     C                     Z-ADDPPRC      MPRICE
     C                     ENDIF
      *
      ***  Calculate Subscription Price = Current Market Price minus Discount.
      *
     C           MPRICE    MULT MDDSRT    WWSBPR 158
     C           WWSBPR    DIV  100       WWSBPR
     C           MPRICE    SUB  WWSBPR    MDSBPR
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT                                                              135685
      *****************************************************************   135685
      *                                                               *   135685
      *  CCOMP  - Calculate Compensation Price if Compensation Price  *   135685
      *           entered as Market for Currency Change Events        *   135685
      *                                                               *   135685
      *  Called by: P004   - Build the subfile (Amend Mode).          *   135685
      *                                                               *   135685
      *  Calls: None.                                                 *   135685
      *                                                               *   135685
      *****************************************************************   135685
      *                                                                   135685
     C           CCOMP     BEGSR                           ** CCOMP SR ** 135685
      *                                                                   135685
      ***  Find Market Price with correct Date.                           135685
      *                                                                   135685
     C                     MOVE 'N'       WWCHCK  1                       135685
      *                                                                   135685
     C           WWREFN    CHAINSECORFL1             89                   135685
      *                                                                   135685
     C           *IN89     IFEQ '1'                                       135685
     C                     Z-ADD021       DBASE            ***************135685
     C                     MOVEL'SECORFL1'DBFILE           * DB ERROR 21 *135685
     C                     MOVELWWREFN    DBKEY            ***************135685
     C                     EXSR *PSSR                                     135685
     C                     ENDIF                                          135685
      *                                                                   135685
     C           KEY5      KLIST                                          135685
     C                     KFLD           NANSEC           New Security   135685
     C                     KFLD           KALEF   50       Alloc. Effect. 135685
      *                                                                   135685
     C                     Z-ADDMAALEF    KALEF                           135685
      *                                                                   135685
     C           KEY5      SETGTPRICM                                     135685
     C                     READPPRICM                    44               135685
      *                                                                   135685
      ***  If record found use this Price.                                135685
      *                                                                   135685
     C           *IN44     IFEQ '0'                                       135685
     C           PSSN      ANDEQNANSEC                                    135685
     C                     MOVE 'Y'       WWCHCK                          135685
      *                                                                   135685
      ***  Otherwise check for Current Price.                             135685
      *                                                                   135685
     C                     ELSE                                           135685
     C                     Z-ADD*HIVAL    KALEF                           135685
      *                                                                   135685
     C           KEY5      SETGTPRICM                                     135685
     C                     READPPRICM                    44               135685
      *                                                                   135685
      ***  If record found use this price.                                135685
      *                                                                   135685
     C           *IN44     IFEQ '0'                                       135685
     C           PSSN      ANDEQNANSEC                                    135685
     C                     MOVE 'Y'       WWCHCK                          135685
     C                     ENDIF                                          135685
     C                     ENDIF                                          135685
      *                                                                   135685
     C           WWCHCK    IFEQ 'N'                                       135685
     C                     Z-ADDMKPR      MDCOMP 158       Market Price   135685
     C                     ELSE                                           135685
     C                     Z-ADDPPRC      MDCOMP                          135685
     C                     ENDIF                                          135685
      *                                                                   135685
     C                     ENDSR                                          135685
      *                                                                   135685
      *****************************************************************   135685
      /EJECT
      *****************************************************************
      *                                                               *
      *  ZSEDIT - Standart subroutine to insert a decimal point into  *
      *           a numeric field and to blank out leading zeroes.    *
      *                                                               *
      *  Called by: P009   - Move file fields to screen fields for    *
      *                      New Security.                            *
      *             P010   - Move file fields to screen fields for    *
      *                      Security '**CASH**'.                     *
      *                                                               *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C/COPY ZSRSRC,ZSEDITZ3
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *  ZDATE1 - Standart subroutine to validate dates submitted and *
      *           convert to a number of days.                        *
      *                                                               *
      *  Called by: U001   - Load field for file SECODRL0.            *
      *                                                               *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C/COPY ZSRSRC,ZDATE1Z2
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *  ZDATE2 - Standart subroutine to convert a Day Number to      *
      *           date formats.                                       *
      *                                                               *
      *  Called by: P001   - Initial processing.                      *
      *                                                               *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C/COPY ZSRSRC,ZDATE2Z4
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *  ZASIGN - Standart subroutine to validate and right-align     *
      *           signed numeric fields.                              *
      *                                                               *
      *  Called by: V001   - Validate details input (Amend Mode).     *
      *             U002   - Load fields for file SECODPL3 with all   *
      *                      amounts = 0.                             *
      *                                                               *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C/COPY ZSRSRC,ZASIGNZ2
      *
      /EJECT
**  TXT - Text message for command keys.
F3=Exit  F12=Previous Screen  Rollup/Down
F3=Exit  F12=Previous
F3=Exit F5=Refresh Screen F12=Previous F20=Exit Without Saving   Rollup/Down
F12=Previous
Enter 'N' to Save Changes or 'Y' to Remove Changes
**  CNSARR - Titles for subfile lines.
Cash
Cash Opt/Allocat
Cash Rounding
New Security
Stock Allocation
Stock Rounding
**  NARR - Error narrative when calling PGM.
ERROR CALLING PGM 'SE0040'
ERROR CALLING PGM 'SE7513'
ERROR CALLING PGM 'SEC023'
**  ZYDY - Years in days cumulative, four year span,
0366073110961461
**  ZMDY - Months in days cumulative through year,
000031059090120151181212243273304334365
**  ZMNM - Months short name.
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  CPY@ - Copyright statement.
(c) Finastra International Limited 2001
