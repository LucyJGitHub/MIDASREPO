     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Corporate actions - options maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  SE7599 - SE Corporate Actions - Options Maintenance          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. 247764             Date 12Jun07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 220823             Date 31Oct03               *
      *                 215165             Date 21Mar03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 190204             Date 31May01               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CSE020             Date 21Mar00               *
      *                 161732             Date 21Mar00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 137880             Date 07Jul98               *
      *                 138962             Date 13Aug98               *
      *                 CSE007  *CREATE    Date 01Dec97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  247764 - Database Error on Corporate Action Screen.  Apply   *
      *           UBS-MAD local fix 221720.                           *
      *  221720 - Looping and other numerous problems caused by fix   *
      *           215165. Fix is to revert fix 215165 and create a new*
      *           fix for that error by checking return code from     *
      *           called programs.                                    *
      *  220823 - When action code is incorrect, program is looping.  *
      *           Make sure that error is displayed when action code  *
      *           is incorrect.                                       *
      *  215165 - No subfile if number of options = 1.                *
      *  190204 - Recompiled due to changes in PF/SECODVPD.           *
      *  CSE020 - Corporate Actions Domino Effect Processing          *
      *           (Recompiled due to changes in PF/SECORFPD)          *
      *  161732 - Corporate Actions Exercise and Conversion -         *
      *           Part Stock/Keep Rest Calculation                    *
      *           (Recompiled due to changes in PF/SECOEXPD)          *
      *  137880 - Corporate Actions: Errors with 'multiple options'   *
      *           processing                                          *
      *  138962 - Corporate Actions: various errors:                  *
      *           When Corporate Type Exercises and Conversion linked *
      *           to Dividend Events or Right Issues was created      *
      *           using amendments on Dividend Events or Right Issues *
      *           Option, this new Corporate Exercises and Conversion *
      *           did not appeared, so refresh subfile with using new *
      *           parameter transmited between programs.              *
      *  CSE007 - Corporate Actions.                                  *
      *                                                               *
      *****************************************************************
      /EJECT
     FSECORFL0UF  E           K        DISK         KCOMIT       A
     F                                              KINFSR *PSSR
      **  MA - SE CORPORATE ACTIONS References - Upd Idx
      *
     FSECODVL0UF  E           K        DISK         KCOMIT       A
     F                                              KINFSR *PSSR
      **  MD - SE Corporate Actions - Dividends Events
      *
     FSECOFRL0UF  E           K        DISK         KCOMIT       A
     F                                              KINFSR *PSSR
      **  ME - SE Corporate Actions - Free Allocations
      *
     FSECOCEL0UF  E           K        DISK         KCOMIT       A
     F                                              KINFSR *PSSR
      **  MG - SE Corporate Actions - Capital Events
      *
     FSECOCRL0UF  E           K        DISK         KCOMIT       A
     F                                              KINFSR *PSSR
      **  MH - SE Corporate Actions - Merger
      *
     FSECOOFL0UF  E           K        DISK         KCOMIT       A
     F                                              KINFSR *PSSR
      **  MI - SE Corporate Actions - Exchange Offers
      *
     FSECORGL0UF  E           K        DISK         KCOMIT       A
     F                                              KINFSR *PSSR
      **  MF - SE Corporate Actions - Rights Issues
      *
     FSECOEXL0UF  E           K        DISK         KCOMIT       A
     F                                              KINFSR *PSSR
      **  MJ - SE Corporate Actions - Exercises and Conversions
      *
     FSE7599DFCF  E                    WORKSTN      KINFSR *PSSR
     F                                        #1RRN KSFILE SE7599S1
      **  Display file
      *
      *****************************************************************
      /EJECT
      *****************************************************************
     E                    CPY@    1   1 80
      **  For compilation - copyright insertion
      *
     E                    TXT     1   3 78
      **  Text messages for command keys
      *
     E                    NARR    1   1 24
      **  Narrative
      *
     E/COPY ZSRSRC,ZDATE2Z1
      *
      *****************************************************************
      /EJECT
      *****************************************************************
     ICPYR@#      DS
      **  Data Structure for compilation - Copyright Insertion
      *
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *
     IPGMDS     ESDSY2PGDSP
      **  Program Status Data Structure for WS/User ID's
      *
     I                                      244 253 WSID
     I                                      254 263 USIDXX
      *
     IINFDS1    E DSY2I1DSP
      **   FILE INFORMATION DATA STRUCTURE
      *
     ISCRDS       DS
      *
     I                                      197 205 DSPNUM
      *
     ILDA         DS                            256
      **  DATA STRUCTURE FOR DATABASE ERROR
      *
     I                                      132 183 WWLOT
     I                                      132 141 WWFILE
     I                                      142 170 WWKEY
     I                                      171 180 WWPGM
     I                                      181 1830WWASE
      *
     IRUNDAT    E DS
      **  Standard Data Area Layout for System flags and Run Date
      *
     ISDBANK    E DSSDBANKPD
      **  Data structure for bank details
      *
     IDSSDY     E DSDSSDY
      **  Long data structure for access objects
      *
     I/COPY WNCPYSRC,SE7599I001
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            MAIN   - Main Routine.                             *
      *                                                               *
      * CALLS      P001   - Initial Processing.                       *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
      *
      ** ENTRY PARAMETERS
      *
     C           *ENTRY    PLIST
     C                     PARM           WWREFN  6        ER Reference
     C                     PARM           WWSECN 10        Security Shortname
     C                     PARM           WWCOAT  2        Corporate Action Type
     C                     PARM           WWSTAT  1        Corporate Action Stat
     C                     PARM           WWFINA  1        Final Allocation Indi
     C                     PARM           WWPTYP  2        Processing Type
     C                     PARM           WWEXDT  5        Ex-Date
     C                     PARM           WWNBOP  2        Number of Options
     C                     PARM           WWACTN  1        Action 'G' or 'F'
     C                     PARM           WWEVDP  1        Events or Depots
     C                     PARM           WWTDVD  1        Trade/Value Position
     C                     PARM           WWRTCD  1        Return Code
     C                     PARM           WWWREX  1                       138962
      *
     C                     MOVE WWNBOP    W2NBOP  20
      *
      ** PERFORM INITIAL PROCESSING
     C                     EXSR P001
      *
      ** SET FIRST TIME FLAG TO 'Y'
     C                     MOVE 'Y'       FIRST   1
      *                                                                         215165
     C**********           MOVE ' '       #1SEL                                        215165 247764
     C**********           MOVE *OFF      *INKL                                        215165 247764
      *
      ** DO UNTIL F3 OR F12
     C           *INKC     DOUEQ'1'                         B*1
     C           *INKL     OREQ '1'
      *
      ** IF 'FIRST TIME' = 'Y' OR F9 PRESSED
     C           FIRST     IFEQ 'Y'                         B*2
      *
     C           WWEVDP    IFEQ 'E'                         B*3
      *
     C           WWACTN    IFEQ 'F'                         B*4
     C                     MOVELTXT,1     ##CTX1
     C                     MOVE '0'       *IN60                           137880
     C                     ELSE                             X*4
     C           WWSTAT    IFEQ 'I'                         B*5           137880
     C           WWSTAT    OREQ 'L'                                       137880
     C           WWFINA    ANDNE'Y'                                       137880
     C                     MOVELTXT,2     ##CTX1
     C                     MOVE '1'       *IN60                           137880
     C                     ELSE                             X*5           137880
     C                     MOVELTXT,1     ##CTX1                          137880
     C                     MOVE '0'       *IN60                           137880
     C                     ENDIF                            E*5           137880
     C                     ENDIF                            E*4
      *
     C                     ELSE                             X*3
     C                     MOVELTXT,3     ##CTX1
     C                     MOVE '0'       *IN60                           137880
     C                     ENDIF                            E*3
      *
     C                     MOVE W2NBOP    #1NBOP
      *
     C           *INKI     IFEQ '0'                        B*3
      *
      ** CLEAR MESSAGES FROM PROGRAM MESSAGE QUEUE.
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
     C                     ENDIF                            E*3
      *
     C                     MOVE '0'       *IN50
      *
      ** BUILD THE SUBFILE
     C                     EXSR P002
      *
      ** SET FIRST TIME FLAG TO 'N'
     C                     MOVE 'N'       FIRST
      *
      ** WRITE SE7599DF ON SCREEN
     C           SFLLEN    IFNE *ZEROS
     C                     Z-ADD1         #1RRN
     C********** W2NBOP    IFEQ 1                                                      215165 247764
     C********** WWRTCD    ANDNE'X'                                                    215165 247764
     C**********           MOVE 'A'       #1SEL                                        215165 247764
     C**********           END                                                         215165 247764
     C                     ELSE
     C                     Z-ADD0         #1RRN
     C                     ENDIF
      *
     C                     WRITESE7599C2
     C                     WRITESE7599C1
     C                     WRITESE7599F1
      *
     C                     ENDIF                           E*2
      *
      ** READ SE7599DF
     C           *INKC     IFEQ '0'                        B*2
     C********** W2NBOP    ANDNE1                                                      215165 247764
     C********** *INKC     OREQ '0'                                                    215165 247764
     C********** WWRTCD    ANDEQ'X'                                                    215165 247764
     C                     READ SE7599F1                 89
     C                     ENDIF                           E*2
      *
      ** CLEAR MESSAGES FROM PROGRAM MESSAGE QUEUE.
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
      *
      ** IF F3
     C                     MOVE ' '       WWRTCD
      *
     C           *INKC     IFEQ *ON                        B*2
      *
      ** RETURN TO SE7510 WITH PARAMETER 'RETURN CODE' SET TO '3'
     C                     MOVE '3'       WWRTCD
     C                     ENDIF                           E*2
      *
      ** IF F12
     C           *INKL     IFEQ '1'                        B*2
     C                     MOVE '2'       WWRTCD
     C                     ENDIF                           E*2
      *
      ** IF F9
     C           *INKI     IFEQ '1'                        B*2
      *
     C           W2NBOP    IFEQ 9                          B*3
     C                     MOVEL'CO00128' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE 'Y'       FIRST
      *
     C                     ELSE                            X*3
      *
      ** PERFORM P006 - RETRIEVE A FREE OPTION NUMBER
     C                     EXSR P006
      *
     C                     MOVE W1NBOP    WWOPNB  2        Option Number
      ** PERFORM P004 - CALL RESPECTIVE DETAIL SCREEN DEPENDING OF THE PROCESSIN
      *
     C                     EXSR P004
      *
     C           WWRTCD    IFEQ 'U'                        B*4
     C           WWRTCD    OREQ 'W'
      *
     C                     MOVE 'Y'       FIRST
      *
      **   ACCESS ER REFERENCE FILE TO RETRIEVE NEW NUMBER OF OPTIONS
      *
     C           #1REFN    CHAINSECORFL0             89
      *
     C           *IN89     IFEQ '1'                        B*5
     C                     Z-ADD1         DBASE            ***************
     C                     MOVE 'SECORFL0'DBFILE           * DB ERROR 01 *
     C                     MOVEL#1REFN    DBKEY            ***************
     C                     EXSR *PSSR
     C                     ENDIF                           E*5
      *
     C                     MOVE MANBOP    W2NBOP
     C                     COMIT                                          CSE020
      *
     C                     ENDIF                           E*4
      *
     C                     ENDIF                           E*3
      *
     C                     ENDIF                           E*2
      *
      ** IF ENTER TAKEN
     C           *INKC     IFEQ '0'                        B*2
     C           *INKL     ANDEQ'0'
     C           *INKI     ANDEQ'0'
      *
      ** VALIDATE SUBFILE RECORDS
     C                     EXSR P003
     C                     ENDIF                           E*2
      *
     C                     ENDDO                           E*1
      *
     C                     MOVE W2NBOP    WWNBOP
      *
      ** RETURN TO SE7510
     C           ENDPGM    TAG
     C                     RETRN
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            P001   - INITIAL PROCESSING.                       *
      *                                                               *
      * CALLS      *PSSR  - Error Processing.                         *
      *            ZDATE2 -                                           *
      *                                                               *
      * CALLED BY  MAIN   - Main Routine.                             *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           P001      BEGSR
      *
      ** INITIALISE KEY AND WORKING FIELDS
     C                     MOVE *BLANKS   WWLOT
     C                     MOVEL'SE7599  'DBPGM  10
      *
     C                     MOVE WWREFN    #1REFN           ER Reference
     C                     MOVE WWSECN    #1SECN           Security Shortname
     C                     MOVE WWCOAT    #1COAT           Corporate Action Type
     C                     MOVE WWNBOP    #1NBOP           Number of Options
      *
     C           WWEXDT    IFNE *BLANKS                    B*1
     C           WWEXDT    ANDNE'00000'
     C                     MOVE WWEXDT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     #1EXDT           Ex-Date
     C                     ELSE                            X*1
     C                     MOVE *BLANKS   #1EXDT           Ex-Date
     C                     ENDIF                           E*1
      *
      ** SETUP SCREEN TIME
     C                     TIME           ##TME   60       Screen time.
      *
     C           WWACTN    IFEQ 'F'                        B*1
     C                     MOVE '1'       *IN40            Enquiry Mode
     C                     MOVELTXT,1     ##CTX1
     C                     ELSE                            X*1
     C                     MOVE '0'       *IN40            Amend Mode
     C                     MOVELTXT,2     ##CTX1
     C                     ENDIF                           E*1
      *
      **  Read in DTAARA/RUNDAT to get multibranching indicator
      *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
      *
     C           AGMBIN    IFEQ 'Y'
     C                     MOVE '1'       *IN45
     C                     ENDIF
      *
      ** IF FIRST TIME PROGRAM CALLED
     C           FIRST     IFNE 'N'                        B*1
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   WLRTCD  7
     C                     PARM '*FIRST'  WLOPTN  7
     C           SDBANK    PARM SDBANK    DSSDY
      *
      ** IF RECORD NOT FOUND IN SDBANKL1
     C           WLRTCD    IFNE *BLANKS
     C                     Z-ADD2         DBASE   30       ***************
     C                     MOVE 'SDBANKPD'DBFILE 10        * DB ERROR 02 *
     C                     MOVEL*BLANKS   DBKEY  29        ***************
     C                     EXSR *PSSR
     C                     ENDIF                           E*2
      *
     C                     MOVE BJMRDT    ##MRDT           MIDAS RUN DATE
      *
     C           BJDFIN    IFEQ 'M'                        B*2
     C                     MOVE *ON       *IN98
     C                     ENDIF                           E*2
      *
     C                     ENDIF                           E*1
      *
     C                     MOVEL'SEUSRMSG'PMSGF  10
      *
      ** FILL IN FIELDS FOR SUBROUTINE ZASNMS
     C                     MOVEL'SE7599'  ZAPGM  10        Message queue
     C                     MOVEL'SEUSRMSG'ZAMSGF 10        Message file
     C                     MOVEL*BLANK    ZAPGRL  5        Rel queue
     C                     MOVEL*BLANK    ZAMSID  7        Message Id.
     C                     MOVEL*BLANK    ZAMSDA132        Message data.
     C                     MOVEL*BLANK    ZAMSTP  7        Message type.
      *
      ** CLEAR MESSAGES FROM PROGRAM MESSAGE QUEUE.
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
      *
     C                     WRITESE7599C2
      *
      ** JUST FOR DEFINITION OF THE FILE IN 'F' CARD WITH A 'A' (=ADD)
     C           *IN99     IFEQ '0'                        B*1
     C           *IN99     ANDEQ'1'
     C                     WRITESECORFD0
     C                     WRITESECODVD0
     C                     WRITESECOFRD0
     C                     WRITESECOCED0
     C                     WRITESECOCRD0
     C                     WRITESECOOFD0
     C                     WRITESECORGD0
     C                     WRITESECOEXD0
     C                     ENDIF                           E*1
     C/COPY WNCPYSRC,SE7599C001
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            P002   - BUILD THE SUBFILE                         *
      *                                                               *
      * CALLS      ZASNMS - Send a Message.                           *
      *                                                               *
      * CALLED BY  MAIN   - Main Routine.                             *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           P002      BEGSR
      *
      ** CLEAR SUBFILE
     C                     MOVE *ON       *IN80
     C                     WRITESE7599C1
     C                     MOVE *OFF      *IN80
     C                     MOVE *OFF      *IN81
      *
     C                     Z-ADD0         SFLLEN  40
     C                     Z-ADD1         #1RRN
     C                     Z-ADD#1RRN     SFLRRN
      *
     C                     Z-ADD0         WWCPT   20
      *
     C                     SELEC
      *
     C           WWPTYP    WHEQ 'DV'
     C           WWREFN    SETLLSECODVL0
     C           WWREFN    READESECODVL0                 89
      *
     C           WWPTYP    WHEQ 'FR'
     C           WWREFN    SETLLSECOFRL0
     C           WWREFN    READESECOFRL0                 89
      *
     C           WWPTYP    WHEQ 'CE'
     C           WWREFN    SETLLSECOCEL0
     C           WWREFN    READESECOCEL0                 89
      *
     C           WWPTYP    WHEQ 'CR'
     C           WWREFN    SETLLSECOCRL0
     C           WWREFN    READESECOCRL0                 89
      *
     C           WWPTYP    WHEQ 'OF'
     C           WWREFN    SETLLSECOOFL0
     C           WWREFN    READESECOOFL0                 89
      *
     C           WWPTYP    WHEQ 'RG'
     C           WWREFN    SETLLSECORGL0
     C           WWREFN    READESECORGL0                 89
      *
     C           WWPTYP    WHEQ 'EX'
     C           WWREFN    SETLLSECOEXL0
     C           WWREFN    READESECOEXL0                 89
      *
     C                     ENDSL
      *
      ** DO WHILE COUNTER LOWER OR EQUAL THAN NUMBER OF OPTIONS
     C           *IN89     DOWEQ'0'                        B*1
     C           WWCPT     ANDLT16
      *
     C                     MOVEL*BLANKS   #1SEL            Action Code
      *
     C                     MOVE WWPTYP    #1PTYP           Processing Type
      *
     C                     SELEC
      *
     C           WWPTYP    WHEQ 'DV'
     C                     MOVE MDOPTN    #1OPTN           Option Number
     C                     MOVE MDOPSH    #1OPSH           Option Shortname
      *
     C           WWPTYP    WHEQ 'FR'
     C                     MOVE MEOPTN    #1OPTN           Option Number
     C                     MOVE MEOPSH    #1OPSH           Option Shortname
      *
     C           WWPTYP    WHEQ 'CE'
     C                     MOVE MGOPTN    #1OPTN           Option Number
     C                     MOVE MGOPSH    #1OPSH           Option Shortname
      *
     C           WWPTYP    WHEQ 'CR'
     C                     MOVE MHOPTN    #1OPTN           Option Number
     C                     MOVE MHOPSH    #1OPSH           Option Shortname
      *
     C           WWPTYP    WHEQ 'OF'
     C                     MOVE MIOPTN    #1OPTN           Option Number
     C                     MOVE MIOPSH    #1OPSH           Option Shortname
      *
     C           WWPTYP    WHEQ 'RG'
     C                     MOVE MFOPTN    #1OPTN           Option Number
     C                     MOVE MFOPSH    #1OPSH           Option Shortname
      *
     C           WWPTYP    WHEQ 'EX'
     C                     MOVE MJOPTN    #1OPTN           Option Number
     C                     MOVE MJOPSH    #1OPSH           Option Shortname
      *
     C                     ENDSL
      *
      ** WRITE SUBFILE RECORD
     C                     WRITESE7599S1
     C                     ADD  1         SFLLEN
     C                     ADD  1         SFLRRN     81
     C                     Z-ADDSFLRRN    #1RRN
      *
     C                     ADD  1         WWCPT
      *
     C                     SELEC
      *
     C           WWPTYP    WHEQ 'DV'
     C           WWREFN    READESECODVL0                 89
      *
     C           WWPTYP    WHEQ 'FR'
     C           WWREFN    READESECOFRL0                 89
      *
     C           WWPTYP    WHEQ 'CE'
     C           WWREFN    READESECOCEL0                 89
      *
     C           WWPTYP    WHEQ 'CR'
     C           WWREFN    READESECOCRL0                 89
      *
     C           WWPTYP    WHEQ 'OF'
     C           WWREFN    READESECOOFL0                 89
      *
     C           WWPTYP    WHEQ 'RG'
     C           WWREFN    READESECORGL0                 89
      *
      *
     C           WWPTYP    WHEQ 'EX'
     C           WWREFN    READESECOEXL0                 89
      *
     C                     ENDSL
      *
     C                     ENDDO                           E*1
      *
     C           WWCPT     IFEQ *ZEROS                     B*1
     C                     MOVEL'CO00001' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     ENDIF                           E*1
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            P003   - VALIDATE SUBFILE RECORDS                  *
      *                                                               *
      * CALLS      P004   - Call Program Depending on Corporate Action*
      *                     Type.                                     *
      *            P005   - Process Confirmation Screen.              *
      *            ZASNMS - Send a Message.                           *
      *                                                               *
      * CALLED BY  MAIN   - Main Routine.                             *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           P003      BEGSR
      *
      ** CLEAR MESSAGES FROM PROGRAM MESSAGE QUEUE.
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
      *
      ** READ FIRST SUBFILE RECORD
     C                     Z-ADD1         SFLRRN  40
     C                     Z-ADD0         #3RRN   40
      *
     C           SFLRRN    IFLE SFLLEN                     B*1
     C                     READCSE7599S1                 89
      *                                                                   215165
     C********** W2NBOP    IFEQ 1                                                      215165 247764
     C********** FIRST     ANDNE'Y'                                                    215165 247764
     C********** WWRTCD    ANDNE'X'                                                    215165 247764
     C**********           MOVE '0'       *IN89                                        215165 247764
     C**********           END                                                         215165 247764
      *
      ** DO WHILE RECORDS EXIST IN SUBFILE
     C           *IN89     DOWEQ*OFF                       B*3
      *
      ** RESET INDICATORS 50 (REVERSE IMAGE)
      *
     C                     MOVE '0'       *IN50
      *
      **  IF ACTION CODE ENTERED, MUST BE 'A', 'E', 'D'
      *
     C           #1SEL     IFNE ' '                        B*4
     C           #1SEL     ANDNE'A'
     C           #1SEL     ANDNE'E'
     C           #1SEL     ANDNE'D'
     C           WWACTN    ANDEQ'G'
     C           #1SEL     ORNE ' '
     C           #1SEL     ANDNE'E'
     C           WWACTN    ANDEQ'F'
      *
     C           WWACTN    IFEQ 'G'                        B*5
     C                     MOVEL'CO00065' ZAMSID
     C                     ELSE                            X*5
     C                     MOVEL'CO00068' ZAMSID
     C                     ENDIF                           E*5
      *
     C                     MOVE '1'       *IN50
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN84
     C                     UPDATSE7599S1
      *
     C                     MOVE 'X'       WWRTCD                                              220823
     C                     MOVE '1'       *IN89                                               220823
     C                     ELSE                            X*4
      *
      ** IF ACTION CODE OF 'A' ENTERED
     C           #1SEL     IFEQ 'A'                        B*5
      *
     C                     MOVE #1OPTN    WWOPNB  2        Option Number
      *
      ** PERFORM P004 - CALL RESPECTIVE DETAIL SCREEN DEPENDING OF THE PROCESSIN
      *
     C                     EXSR P004
      *
     C                     MOVE *BLANKS   #1SEL
     C                     MOVE '0'       *IN50
     C                     MOVE '1'       *IN81
     C                     MOVE '0'       *IN84
     C********** W2NBOP    IFNE 1                                                      215165 247764
     C********** WWRTCD    OREQ 'X'                                                    215165 247764
     C                     UPDATSE7599S1
      *
     C                     MOVE 'Y'       FIRST
     C**********           END                                                         215165 247764
     C                     ENDIF                           E*5
      *
      ** IF ACTION CODE OF 'E' ENTERED
     C           #1SEL     IFEQ 'E'                        B*5
      *
     C                     MOVE #1OPTN    WWOPNB           Option Number
      *
      ** PERFORM P004 - CALL RESPECTIVE DETAIL SCREEN DEPENDING OF THE PROCESSIN
      *
     C                     EXSR P004
      *
     C                     MOVE *BLANKS   #1SEL
     C                     MOVE '0'       *IN50
     C                     MOVE '1'       *IN81
     C                     MOVE '0'       *IN84
     C                     UPDATSE7599S1
     C                     ENDIF                           E*5
      *
      *
      ** IF ACTION CODE OF 'D' ENTERED
     C           #1SEL     IFEQ 'D'                        B*5
      *
     C           #1OPTN    IFEQ '01'                       B*6
      *
     C                     MOVEL'CO00306' ZAMSID
     C                     MOVE '1'       *IN50
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN84
     C                     UPDATSE7599S1
      *
     C                     ELSE                            X*6
      *
      ** PERFORM P005
     C                     EXSR P005
     C                     MOVE *BLANKS   #1SEL
     C                     MOVE '0'       *IN50
     C                     MOVE '1'       *IN81
     C                     MOVE '0'       *IN84
     C                     UPDATSE7599S1
      *
     C                     ENDIF                           E*5
      *
     C                     ENDIF                           E*4
      *
     C                     ENDIF
      *
      ** READ NEXT SUBFILE RECORD
     C                     READCSE7599S1                 89
      *
     C                     ENDDO                           E*3
      *
      ** REDISPLAY CURRENT SCREEN
     C                     WRITESE7599C2
     C                     WRITESE7599C1
     C                     WRITESE7599F1
      *
     C                     ENDIF                           E*1
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            P004   - CALL PROGRAM DEPENDING ON CORPORATE       *
      *                     ACTION TYPE.                              *
      *                                                               *
      * CALLS      *PSSR  - Error Processing.                         *
      *                                                               *
      * CALLED BY  P003   - Validate Subfile Records.                 *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           P004      BEGSR
      *
      ** SETUP MODE (INSERT, AMEND OR ENQUIRE)
      *
     C           *INKI     IFEQ '1'                        B*1
     C                     MOVE 'I'       WWMODE  1
     C                     ELSE                            X*1
     C                     MOVE #1SEL     WWMODE
     C                     ENDIF                           E*1
      *                                                                         215165
     C********** W2NBOP    IFEQ 1                                                      215165 247764
     C********** WWMODE    ANDEQ'A'                                                    215165 247764
     C********** WWRTCD    ANDNE'X'                                                    215165 247764
     C**********           MOVE '1'       *INKL                                        215165 247764
     C**********           END                                                         215165 247764
      *
      ** IF DIVIDENDS EVENTS (DIVIDEND IN STOCK, CASH AND STOCK, CASH OR STOCK,
      **                      REINVESTMENT OF DIVIDEND)
      *
     C                     SELEC
      *
     C           WWPTYP    WHEQ 'DV'                       B*1
     C                     MOVE 'SE7503'  WWPGM  10
     C/COPY WNCPYSRC,SE7599C002
      *
      ** IF FREE ALLOCATION (SCRIP ISSUE, BONUS SHARE, STOCK SPLIT, REVERSE SPLI
      *
     C           WWPTYP    WHEQ 'FR'                       B*2
     C                     MOVE 'SE7504'  WWPGM
     C/COPY WNCPYSRC,SE7599C003
      *
      ** IF RIGHTS ISSUES
      *
     C           WWPTYP    WHEQ 'RG'                       B*3
     C                     MOVE 'SE7505'  WWPGM
     C/COPY WNCPYSRC,SE7599C004
      *
      ** IF CAPITAL CHANGE
      *
     C           WWPTYP    WHEQ 'CE'                       B*4
     C                     MOVE 'SE7506'  WWPGM
     C/COPY WNCPYSRC,SE7599C005
      *
      ** IF CORPORATE REORGANISATION
      *
     C           WWPTYP    WHEQ 'CR'                       B*5
     C                     MOVE 'SE7507'  WWPGM
     C/COPY WNCPYSRC,SE7599C006
      *
      ** IF OFFERS
      *
     C           WWPTYP    WHEQ 'OF'                       B*6
     C                     MOVE 'SE7508'  WWPGM
     C/COPY WNCPYSRC,SE7599C007
      *
      ** IF EXERCISES AND CONVERSIONS
      *
     C           WWPTYP    WHEQ 'EX'                       B*7
     C                     MOVE 'SE7509'  WWPGM
     C/COPY WNCPYSRC,SE7599C008
      *
     C                     ENDSL                           E*7
      *
     C/COPY WNCPYSRC,SE7599C009
     C                     CALL WWPGM                  99
     C                     PARM           WWREFN           ER Reference
     C                     PARM           WWSECN           Security Shortname
     C                     PARM           WWCOAT           Corporate Action Type
     C                     PARM           WWSTAT           Corporate Action Stat
     C                     PARM           WWFINA           Final Allocation Indi
     C                     PARM           WWEXDT           Ex-Date
     C                     PARM           WWOPNB           Option Number
     C                     PARM           WWMODE           Input, Amend or Enqui
     C                     PARM           WWRTCD  1        Return Code
     C                     PARM           WWWREX  1                       138962
     C/COPY WNCPYSRC,SE7599C010
      *
      **   IF ERROR INDICATOR OF THE PROGRAM CALLED IS ON, DATABASE ERROR
      *
     C           *IN99     IFEQ '1'                        B*1
     C           WWRTCD    OREQ 'E'
     C                     MOVE WWPGM     NARR,1
     C                     Z-ADD3         DBASE            *****************
     C                     MOVEL*BLANKS   DBFILE           ** DB ERROR 03 **
     C                     MOVELNARR,1    DBKEY            *****************
     C                     EXSR *PSSR
     C                     ENDIF                           E*1
      *
     C           WWRTCD    IFEQ '3'                        B*1
     C                     MOVE '1'       *INKC
     C                     ENDIF                           E*1
      *                                                                                       247764
     C           WWRTCD    IFEQ '2'                                                           247764
     C           W2NBOP    ANDEQ1                                                             247764
     C                     MOVE '1'       *INKL                                               247764
     C                     ENDIF                                                              247764
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            P005   - PROCESS 'CONFIRMATION' SCREEN             *
      *                                                               *
      * CALLS      *PSSR  - Error Processing.                         *
      *            ZASNMS - Send a Message.                           *
      *                                                               *
      * CALLED BY  P003   - Validate Subfile Records.                 *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           P005      BEGSR
      *
     C                     MOVE *BLANKS   #1CONF
      *
     C                     MOVE '0'       *IN44
     C                     MOVELTXT,1     ##CTX1
      *
      **   DO WHILE F3 OR F12 NOT PRESSED AND NO ERROR
      *
     C           *INKC     DOWEQ'0'                        B*1
     C           *INKL     ANDEQ'0'
     C           #1CONF    ANDNE'Y'
     C           #1CONF    ANDNE'N'
      *
      **   SETUP SCREEN TIME
      *
     C                     TIME           ##TME            Screen time.
      *
      **   EXECUTE SCREEN FORMAT
      *
     C                     WRITESE7599C2
     C                     WRITESE7599F1
     C                     EXFMTSE7599F2
      *
      **   CLEAR MESSAGES FROM PROGRAM MESSAGE QUEUE.
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
      *
     C           *INKC     IFEQ '0'                        B*2
     C           *INKL     ANDEQ'0'
      *
      **   IF 'CONFIRMATION INDICATOR' IS NOT EQUAL TO 'Y' OR 'N'
      *
     C           #1CONF    IFNE 'N'                        B*3
     C           #1CONF    ANDNE'Y'
     C                     MOVE '1'       *IN44            Reverse image
     C                     MOVEL'CO00032' ZAMSID           Message id.
     C                     EXSR ZASNMS                     Send message
     C                     ELSE                            X*3
     C                     MOVE '0'       *IN44            Reverse image
     C                     ENDIF                           E*3
      *
     C                     ENDIF                           E*2
      *
     C                     ENDDO                           E*1
      *
     C           *INKC     IFEQ '0'                        B*1
     C           *INKL     ANDEQ'0'
     C           #1CONF    ANDEQ'Y'
      *
     C                     MOVE 'Y'       FIRST
      *
     C                     MOVE #1OPTN    WOPTN
      *
     C           KEY1      KLIST
     C                     KFLD           #1REFN
     C                     KFLD           WOPTN   20
      *
     C                     SELEC
      *
     C           WWPTYP    WHEQ 'DV'                       B*2
     C           KEY1      CHAINSECODVL0             89
     C                     DELETSECODVD0
      *
     C           WWPTYP    WHEQ 'FR'                       B*3
     C           KEY1      CHAINSECOFRL0             89
     C                     DELETSECOFRD0
      *
     C           WWPTYP    WHEQ 'CE'                       B*4
     C           KEY1      CHAINSECOCEL0             89
     C                     DELETSECOCED0
      *
     C           WWPTYP    WHEQ 'CR'                       B*5
     C           KEY1      CHAINSECOCRL0             89
     C                     DELETSECOCRD0
      *
     C           WWPTYP    WHEQ 'OF'                       B*6
     C           KEY1      CHAINSECOOFL0             89
     C                     DELETSECOOFD0
      *
     C           WWPTYP    WHEQ 'RG'                       B*7
     C           KEY1      CHAINSECORGL0             89
     C                     DELETSECORGD0
      *
     C           WWPTYP    WHEQ 'EX'                       B*8
     C           KEY1      CHAINSECOEXL0             89
     C                     DELETSECOEXD0
      *
     C                     ENDSL
      *
      **   ACCESS ER REFERENCE FILE
      *
     C           #1REFN    CHAINSECORFL0             89
      *
     C           *IN89     IFEQ '1'                        B*5
     C                     Z-ADD4         DBASE            ***************
     C                     MOVE 'SECORFL0'DBFILE           * DB ERROR 04 *
     C                     MOVEL#1REFN    DBKEY            ***************
     C                     EXSR *PSSR
     C                     ENDIF                           E*5
      *
     C                     MOVE 'D'       MARECI
     C                     Z-ADDBJRDNB    MALCD
     C                     MOVE 'A'       MACHTP
     C                     MOVE ##USR     MALUID
     C                     SUB  1         MANBOP
     C                     UPDATSECORFD0
     C                     MOVE MANBOP    W2NBOP
      *
     C                     ENDIF                           E*1
      *
     C                     MOVE '0'       *INKL
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            P006   - RETRIEVE A FREE OPTION NUMBER             *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY  MAIN   - Main Routine.                             *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           P006      BEGSR
      *
     C                     Z-ADD1         WWCPT
     C                     Z-ADD*ZEROS    W1NBOP  20
      *
     C                     SELEC
      *
     C           WWPTYP    WHEQ 'DV'                       B*1
     C           #1REFN    SETLLSECODVL0
     C           #1REFN    READESECODVD0                 89
      *
     C           WWPTYP    WHEQ 'FR'                       B*2
     C           #1REFN    SETLLSECOFRL0
     C           #1REFN    READESECOFRD0                 89
      *
     C           WWPTYP    WHEQ 'CE'                       B*3
     C           #1REFN    SETLLSECOCEL0
     C           #1REFN    READESECOCED0                 89
      *
     C           WWPTYP    WHEQ 'CR'                       B*4
     C           #1REFN    SETLLSECOCRL0
     C           #1REFN    READESECOCRD0                 89
      *
     C           WWPTYP    WHEQ 'OF'                       B*5
     C           #1REFN    SETLLSECOOFL0
     C           #1REFN    READESECOOFD0                 89
      *
     C           WWPTYP    WHEQ 'RG'                       B*6
     C           #1REFN    SETLLSECORGL0
     C           #1REFN    READESECORGD0                 89
      *
     C           WWPTYP    WHEQ 'EX'                       B*7
     C           #1REFN    SETLLSECOEXL0
     C           #1REFN    READESECOEXD0                 89
      *
     C                     ENDSL
      *
     C           *IN89     DOWEQ'0'                        B*1
     C           W1NBOP    ANDEQ*ZEROS
      *
     C           WWPTYP    IFEQ 'DV'                       B*2
     C           MDOPTN    ANDNEWWCPT
     C           WWPTYP    OREQ 'FR'
     C           MEOPTN    ANDNEWWCPT
     C           WWPTYP    OREQ 'CE'
     C           MGOPTN    ANDNEWWCPT
     C           WWPTYP    OREQ 'CR'
     C           MHOPTN    ANDNEWWCPT
     C           WWPTYP    OREQ 'OF'
     C           MIOPTN    ANDNEWWCPT
     C           WWPTYP    OREQ 'RG'
     C           MFOPTN    ANDNEWWCPT
     C           WWPTYP    OREQ 'EX'
     C           MJOPTN    ANDNEWWCPT
     C                     Z-ADDWWCPT     W1NBOP
     C                     ELSE                            X*2
     C                     ADD  1         WWCPT
      *
     C                     SELEC
      *
     C           WWPTYP    WHEQ 'DV'
     C           #1REFN    READESECODVD0                 89
      *
     C           WWPTYP    WHEQ 'FR'
     C           #1REFN    READESECOFRD0                 89
      *
     C           WWPTYP    WHEQ 'CE'
     C           #1REFN    READESECOCED0                 89
      *
     C           WWPTYP    WHEQ 'CR'
     C           #1REFN    READESECOCRD0                 89
      *
     C           WWPTYP    WHEQ 'OF'
     C           #1REFN    READESECOOFD0                 89
      *
     C           WWPTYP    WHEQ 'RG'
     C           #1REFN    READESECORGD0                 89
      *
     C           WWPTYP    WHEQ 'EX'
     C           #1REFN    READESECOEXD0                 89
      *
     C                     ENDSL
      *
     C                     ENDIF
      *
     C                     ENDDO                           E*1
      *
     C           W1NBOP    IFEQ *ZEROS                     B*1
     C                     Z-ADDWWCPT     W1NBOP
     C                     ENDIF                           E*1
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            *PSSR  - ERROR PROCESSING                          *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY  MAIN   - Main Routine.                             *
      *            P001   - Initial Processing.                       *
      *            P004   - Call Program Depending on Corporate Action*
      *                     Type.                                     *
      *            P005   - Process Confirmation Screen.              *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR
      *
      ** UPDATE LDA WITH ERROR INFORMATION
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVELDBFILE    WWFILE
     C                     MOVELDBKEY     WWKEY
     C                     MOVELDBPGM     WWPGM
     C                     MOVELDBASE     WWASE
     C                     OUT  LDA
      *
      **   ROLLBACK CHANGES, PRINT DUMP AND CANCEL PROGRAM
     C                     ROLBK
     C                     MOVE 'E'       WWRTCD
     C                     DUMP
      *
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     GOTO ENDPGM
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *          ZASNMS - Send Message to Program Message Queue       *
      *                                                               *
      * CALLS    Y2SNMSGC                                             *
      *                                                               *
      * CALLED BY       -                                             *
      *                                                               *
      * FLDS USED       -                                             *
      *                                                               *
      *****************************************************************
     C           ZASNMS    BEGSR
      *
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGM            Program queue
     C                     PARM           ZAPGRL           Rel queue
     C                     PARM           ZAMSID           Message Id.
     C                     PARM           ZAMSGF           Message file.
     C                     PARM           ZAMSDA           Message data.
     C                     PARM           ZAMSTP           Message type.
      *
      ** Clear all fields for default mechanism next time.
     C                     MOVEL*BLANK    ZAPGM            Message queue
     C                     MOVEL*BLANK    ZAPGRL           Rel queue
     C                     MOVEL*BLANK    ZAMSDA           Message data.
     C                     MOVEL*BLANK    ZAMSTP           Message type.
      *
     C           ZASNM9    ENDSR                           ZASNM9 TAG
      ********************************************************************
     /EJECT
     C/COPY ZSRSRC,ZDATE2Z4
      *
      ********************************************************************
**  CPY@
(c) Finastra International Limited 2001
**
F3=Exit  F12=Previous
F3=Exit  F9=Add Option  F12=Previous Screen
F3=Exit  F12=Previous Screen  Rollup/Down
**  NARR
ERROR CALLING PGM
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
