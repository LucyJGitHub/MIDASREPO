      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SF Menu items maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Security Profile Facility                            *
      *                                                               *
      *  SF000010DF - Midas Menu Items Maintenance                    *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG5820            Date 17Mar05               *
      *                 BUG5748            Date 04Feb05               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CSF002 *RENAME     Date 11Aug03               *
      *                 CSC011             Date 18Sep01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 153359             Date 06Jan99               *
      *                 CAAA01             Date 11Apr95               *
      *                 050065             Date 16Mar93               *
      *                 RT0148             Date 06Jun91               *
      *                 E22128             Date 07Aug90               *
      *                 S01253             Date 17Apr90               *
      *                 E10221             Date 16Mar87               *
      *                 S01199             Date 18Sep89               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG5820- Change screen title to 'Client Menu Items Mainte-   *
      *           nance'/ 'Client Menu Items Enquiry"                 *
      *         - Add validation for atleast one action code must     *
      *           be entered.                                         *
      *  BUG5748- Change 5th field label for API name.                *
      *  CSF002 - Global Processing                                   *
      *           The associated program has been converted to ILE.   *
      *           Commented out Program Codes have been stripped out  *
      *           for easy maintainance.                              *
      *  CSC011 - 24x7 Midas Availability                             *
      *  153359 - HIDE REDUNDANT FIELD,                               *
      *           REMOVE ASSOCIATED HELP TEXT                         *
      *  CAAA01 - GUI/400 DEVELOPMENT,                                *
      *           MAINTAIN NEW FIELDS ON MITEMDD.                     *
      *  050065 - POSITION CURSOR ON FIELD IN ERROR.                  *
      *  RT0148 - INCREASE NUMBER OF ACTION CODES TO 10               *
      *  E22128 - ENSURE THAT CURSOR IS POSITIONED ON FIRST FIELD     *
      *           WHICH IS IN ERROR.                                  *
      *  S01253 - RECOMPILED FOR MULTILANGUAGE SCREENS                *
      *  E10221 - ERROR CODE '037' IS RAISED WHEN IN AMEND/CLIENT     *
      *           MODE EVEN THOUGH THE COMMAND FIELD IS PROTECTED     *
      *  01199  - HELP SYSTEM.                                        *
      *                                                               *
      *****************************************************************
     A                                      CHGINPDFT(HI)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R SF000010F0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      PUTOVR
     A                                      OVERLAY
     A            USRID         10A  O  1  2
     A                                 01 13'SF000010'
     A            SWSID         10A  O 02  2
     A            SZONE         10A  O 02 13
      **Start of change BUG5820
     A**********                       01 25'Midas Menu Item Key Screen'                     BUG5820
     A                                 01 25'Client Menu Item Key Screen'
      **End of change BUG5820
     A                                      DSPATR(UL HI)
     A            SRUNA          7A  O 01 72
     A                                 02 71TIME
     A                                 03 06'Menu Item Short Code'
     A                                      OVRATR
     A            SSNAM         20A  B 03 28
     A  21                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  31                                  DSPATR(RI)
     A N31N21                               DSPATR(UL)
     A                                      OVRDTA
     A            SACTN          1A  H
     A                                 23 02'F3=End,'
     A                                 23 10'or F12=Initial Screen'
      *
      ** Detail Fields
      *
     A          R SF000010F1                TEXT('MIDAS USER MAINT. SCREEN')
     A                                      CF03
     A                                      CF10
     A                                      CF12
     A                                      BLINK
     A                                      PUTOVR
     A                                      OVERLAY
     A            USRID         10A  O  1  2
     A                                 01 13'SF000010'
     A            SWSID         10A  O 02  2
     A            SZONE         10A  O 02 13
      **Start of change BUG5820
     A*N25******                       01 28'Menu Item Maintenance'                          BUG5820
     A N25                             01 28'Client Menu Item Maintenance'
      **End of change BUG5820
     A                                      DSPATR(HI)
      **Start of change BUG5820                                                              BUG5820
     A**25******                       01 26'Menu Item Enquiry'
     A  25                             01 26'Client Menu Item Enquiry'
      **End of change BUG5820
     A                                      DSPATR(HI)
     A            SRUNA          7A  O 01 72
     A                                 02 71TIME
      *
     A                                 03 06'Menu Item Short Code'
     A                                      OVRATR
     A            SSNAM         20A  B 03 28
     A  21                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A N21                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 03 50'Action'
     A                                      OVRATR
     A            SACTN          1A  B 03 58
     A  21                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A N21                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 05 06'Item Narrative'
     A                                      OVRATR
     A            SINAR         45A  B 05 22CHECK(LC)
     A N25 33                               DSPATR(PC)
     A  24
     AO 25                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  33                                  DSPATR(RI)
     A N24N25N33                            DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 07 06'Security Level'
     A                                      OVRATR
     A            SSECL          4A  B 07 22
     A  24
     AO 25                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  34                                  DSPATR(RI PC)
     A N34N25N24                            DSPATR(UL)
     A                                      AUTO(RAZ)
     A                                      OVRDTA
     A                                 07 29'New Menu Item Short Code'
     A                                      OVRATR
     A            SNAMM         20A  B 07 55
     A  24
     AO 25
     AO 26                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  35                                  DSPATR(RI PC)
     A N25N24N26
     AAN35                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 09 06'Module'
     A                                      OVRATR
     A            SMODL          2A  O 09 15
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  9 19'Module Shortname'
     A                                      OVRATR
     A            SMMSN         10A  B  9 37
     A  24
     AO 25                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  32                                  DSPATR(RI PC)
     A N24N25N32                            DSPATR(UL)
     A                                      OVRDTA
     A                                  9 49'Execution Type'
     A                                      OVRATR
     A            SEXTP          1A  B  9 65
     A  24
     AO 25                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A N24N25N41                            DSPATR(UL)
     A                                      OVRDTA
     A                                 11 06'Frame'
     A                                      OVRATR
     A            SFRAM         20A  B 11 13CHGINPDFT(LC)
     A  24
     AO 25                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  42                                  DSPATR(RI PC)
     A N24N25N42                            DSPATR(UL)
     A                                      OVRDTA
     A                                 11 35'Actions permitted during COB'
     A N22                                  DSPATR(ND)
     A                                      OVRATR
     A            #2ACOB         1A  B 11 65DSPATR(HI)
     A  24
     AO 25
     AON22                                  DSPATR(PR)
     A N45N24N25
     AA 22                                  DSPATR(UL)
     A  45                                  DSPATR(RI PC)
     A N22                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 13 06'Path/Command Parameters'
     A                                      OVRATR
     A            SPAT1         50A  B 14 10
     A  24
     AO 25                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  38                                  DSPATR(RI PC)
     A N25N38N24                            DSPATR(UL)
     A                                      OVRDTA
      *
     A            SPAT2         50A  B 15 10
     A  24
     AO 25                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  38                                  DSPATR(RI PC)
     A N25N38N24                            DSPATR(UL)
     A                                      OVRDTA
     A                                 17 06'API Names  (1):'
     A                                      OVRATR
     A            SAPI1          4A  B 17 23
     A  24
     AO 25                                  DSPATR(PR)
     A N24N25N46                            DSPATR(UL)
     A  46                                  DSPATR(RI PC)
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                 17 29'(2):'
     A                                      OVRATR
     A            SAPI2          4A  B 17 35
     A  24
     AO 25                                  DSPATR(PR)
     A N24N25N47                            DSPATR(UL)
     A  47                                  DSPATR(RI PC)
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                 17 41'(3):'
     A                                      OVRATR
     A            SAPI3          4A  B 17 47
     A  24
     AO 25                                  DSPATR(PR)
     A N24N25N48                            DSPATR(UL)
     A  48                                  DSPATR(RI PC)
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                 17 53'(4):'
     A                                      OVRATR
     A            SAPI4          4A  B 17 59
     A  24
     AO 25                                  DSPATR(PR)
     A N24N25N49                            DSPATR(UL)
     A  49                                  DSPATR(RI PC)
     A                                      DSPATR(HI)
     A                                      OVRDTA
      ** Start of change BUG5748
     A**********                       17 65'(4):'                                           BUG5748
     A                                 17 65'(5):'
      ** End of change BUG5748
     A                                      OVRATR
     A            SAPI5          4A  B 17 71
     A  24
     AO 25                                  DSPATR(PR)
     A N24N25N50                            DSPATR(UL)
     A  50                                  DSPATR(RI PC)
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                 19 06'Action codes (1):'
     A                                      OVRATR
     A            SINS           1A  B 19 24
     A  24
     AO 25                                  DSPATR(PR)
     A N24
     AAN25N76N77                            DSPATR(UL)
     A  51 76
     AO 51 77                               DSPATR(RI PC)
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                 19 27'(2):'
     A                                      OVRATR
     A            SAMD           1A  B 19 32
     A  24
     AO 25                                  DSPATR(PR)
     A N24
     AAN25N76N77                            DSPATR(UL)
     A  52 76
     AO 52 77                               DSPATR(RI PC)
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                 19 35'(3):'
     A                                      OVRATR
     A            SENQ           1A  B 19 40
     A  24
     AO 25                                  DSPATR(PR)
     A N24
     AAN25N76N77                            DSPATR(UL)
     A  53 76
     AO 53 77                               DSPATR(RI PC)
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                 19 43'(4):'
     A                                      OVRATR
     A            SDEL           1A  B 19 48
     A  24
     AO 25                                  DSPATR(PR)
     A N24
     AAN25N76N77                            DSPATR(UL)
     A  54 76
     AO 54 77                               DSPATR(RI PC)
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                 19 51'(5):'
     A                                      OVRATR
     A            SREV           1A  B 19 56
     A  24
     AO 25                                  DSPATR(PR)
     A N24
     AAN25N76N77                            DSPATR(UL)
     A  55 76
     AO 55 77                               DSPATR(RI PC)
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                 20 19'(6):'
     A                                      OVRATR
     A            SCLO           1A  B 20 24
     A  24
     AO 25                                  DSPATR(PR)
     A N24
     AAN25N76N77                            DSPATR(UL)
     A  56 76
     AO 56 77                               DSPATR(RI PC)
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                 20 27'(7):'
     A                                      OVRATR
     A            SPRI           1A  B 20 32
     A  24
     AO 25                                  DSPATR(PR)
     A N24
     AAN25N76N77                            DSPATR(UL)
     A  58 76
     AO 58 77                               DSPATR(RI PC)
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                 20 35'(8):'
     A                                      OVRATR
     A            SAUT           1A  B 20 40
     A  24
     AO 25                                  DSPATR(PR)
     A N24
     AAN25N76N77                            DSPATR(UL)
     A  59 76
     AO 59 77                               DSPATR(RI PC)
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                 20 43'(9):'
     A            SAUX1          1A  B 20 48
     A  24
     AO 25                                  DSPATR(PR)
     A N24
     AAN25N76N77                            DSPATR(UL)
     A  60 76
     AO 60 77                               DSPATR(RI PC)
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                 20 50'(10):'
     A                                      OVRATR
     A            SAUX2          1A  B 20 56
     AO 24
     AO 25                                  DSPATR(PR)
     A N24
     AAN25N76N77                            DSPATR(UL)
     A  61 76
     AO 61 77                               DSPATR(RI PC)
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                      CHECK(FE)
     A                                 23 02'F3=End,'
     A                                 23 10'or F12=Initial Screen'
     A  24                             23 32', or F10=Delete'
      *
      ** Message Subfile Record Format.
      *
     A          R SF000010S0                TEXT('MESSAGE SUBFILE FORMAT')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            ##SFKY                    SFLMSGKEY
     A            PGM                       SFLPGMQ
      *
      ** Message Subfile Control Format.
      *
     A          R SF000010C0                TEXT('Program messages')
     A                                      SFLCTL(SF000010S0)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  62
     AON62                                  SFLEND
     A            PGM                       SFLPGMQ
