      *****************************************************************
/*S*D****DSPFBASE******************************************************
      *****************************************************************
      *                                                               *
      *  Midas - Security Profile Facility                            *
      *                                                               *
      *  SF000020DF - Midas Users Maintenance                         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. AR904191*REDUNDANT Date 01Feb12               *
      *  Prev Amend No. AR787620           Date 10Jun11               *
      *                 BUG28159           Date 02Oct10               *
      *                 BUG27503           Date 20May10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 CSD086             Date 17Mar10               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 239297             Date 08May06               *
      *                 239898             Date 29May06               *
      *                 BUG2844            Date 27May04               *
      *                 TDA035             Date 21Mar04               *
      *                 CSF002 *RENAME     Date 11Aug03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CSC011             Date 27May02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CGL008             Date 01Mar99               *
      *                 071482             Date 09May96               *
      *                 CAAA01             Date 24Mar95               *
      *                 S01408             Date 06Mar95               *
      *                 079476             Date 13Dec94               *
      *                 S01486             Date 08Jun94               *
      *                 050065             Date 16Mar93               *
      *                 S01117             Date 14Feb92               *
      *                 S01319             Date 5Sep91                *
      *                 S01269             Date 19Jun91               *
      *                 LN1192             Date 23May91               *
      *                 S01253             Date 17Apr90               *
      *                 S01199             Date 18Sep89               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR904191 - Replaced as per BF requirement (CBF005)           *
      *  AR787620 - Hooks Incorporation                               *
      *             WNCPYSRC,SFH00001                                 *
      *             WNCPYSRC,SFH00002                                 *
      *             WNCPYSRC,SFH00003                                 *
      *             WNCPYSRC,SFH00004                                 *
      *             WNCPYSRC,SFH00005                                 *
      *             WNCPYSRC,SFH00006                                 *
      *             WNCPYSRC,SFH00007                                 *
      *             WNCPYSRC,SFH00008                                 *
      *             WNCPYSRC,SFH00009                                 *
      *             WNCPYSRC,SFH00010                                 *
      *             WNCPYSRC,SFH00011                                 *
      *             WNCPYSRC,SFH00012                                 *
      *             WNCPYSRC,SFH00013                                 *
      *             WNCPYSRC,SFH00014                                 *
      *             WNCPYSRC,SFH00015                                 *
      *             WNCPYSRC,SFH00016                                 *
      *             WNCPYSRC,SFH00017                                 *
      *             WNCPYSRC,SFH00018                                 *
      *             WNCPYSRC,SFH00019                                 *
      *             WNCPYSRC,SFH00020                                 *
      *             WNCPYSRC,SFH00021                                 *
      *             WNCPYSRC,SFH00022                                 *
      *             WNCPYSRC,SFH00023                                 *
      *             WNCPYSRC,SFH00024                                 *
      *             WNCPYSRC,SFH00025                                 *
      *             WNCPYSRC,SFH00026                                 *
      *             WNCPYSRC,SFH00027                                 *
      *             WNCPYSRC,SFH00028                                 *
      *             WNCPYSRC,SFH00029                                 *
      *             WNCPYSRC,SFH00030                                 *
      *             WNCPYSRC,SFH00031                                 *
      *             WNCPYSRC,SFH00032                                 *
      *             WNCPYSRC,SFH00033                                 *
      *             WNCPYSRC,SFH00034                                 *
      *             WNCPYSRC,SFH00035                                 *
      *             WNCPYSRC,SFH00036                                 *
      *             WNCPYSRC,SFH00037                                 *
      *             WNCPYSRC,SFH00038                                 *
      *             WNCPYSRC,SFH00039                                 *
      *             WNCPYSRC,SFH00040                                 *
      *             WNCPYSRC,SFH00041                                 *
      *             WNCPYSRC,SFH00042                                 *
      *             WNCPYSRC,SFH00043                                 *
      *             WNCPYSRC,SFH00044                                 *
      *             WNCPYSRC,SFH00045                                 *
      *             WNCPYSRC,SFH00046                                 *
      *             WNCPYSRC,SFH00047                                 *
      *             WNCPYSRC,SFH00048                                 *
      *             WNCPYSRC,SFH00049                                 *
      *             WNCPYSRC,SFH00050                                 *
      *             WNCPYSRC,SFH00051                                 *
      *             WNCPYSRC,SFH00052                                 *
      *             WNCPYSRC,SFH00053                                 *
      *             WNCPYSRC,SFH00054                                 *
      *             WNCPYSRC,SFH00055                                 *
      *             WNCPYSRC,SFH00056                                 *
      *             WNCPYSRC,SFH00057                                 *
      *             WNCPYSRC,SFH00058                                 *
      *             WNCPYSRC,SFH00059                                 *
      *             WNCPYSRC,SFH00060                                 *
      *             WNCPYSRC,SFH00061                                 *
      *             WNCPYSRC,SFH00062                                 *
      *             WNCPYSRC,SFH00063                                 *
      *             WNCPYSRC,SFH00064                                 *
      *             WNCPYSRC,SFH00065                                 *
      *             WNCPYSRC,SFH00066                                 *
      *             WNCPYSRC,SFH00067                                 *
      *             WNCPYSRC,SFH00068                                 *
      *             WNCPYSRC,SFH00069                                 *
      *             WNCPYSRC,SFH00070                                 *
      *             WNCPYSRC,SFH00071                                 *
      *             WNCPYSRC,SFH00072                                 *
      *             WNCPYSRC,SFH00073                                 *
      *             WNCPYSRC,SFH00074                                 *
      *             WNCPYSRC,SFH00075                                 *
      *             WNCPYSRC,SFH00076                                 *
      *             WNCPYSRC,SFH00077                                 *
      *             WNCPYSRC,SFH00078                                 *
      *             WNCPYSRC,SFH00079                                 *
      *             WNCPYSRC,SFH00080                                 *
      *             WNCPYSRC,SFH00081                                 *
      *             WNCPYSRC,SFH00082                                 *
      *             WNCPYSRC,SFH00083                                 *
      *             WNCPYSRC,SFH00084                                 *
      *             WNCPYSRC,SFH00085                                 *
      *             WNCPYSRC,SFH00086                                 *
      *             WNCPYSRC,SFH00087                                 *
      *             WNCPYSRC,SFH00088                                 *
      *             WNCPYSRC,SFH00089                                 *
      *             WNCPYSRC,SFH00090                                 *
      *             WNCPYSRC,SFH00091                                 *
      *             WNCPYSRC,SFH00092                                 *
      *             WNCPYSRC,SFH00093                                 *
      *             WNCPYSRC,SFH00094                                 *
      *  BUG28159 - Allow mixed case input in overridable profile     *
      *  BUG27503 - Overridable profile is not protected on enquire   *
      *  CSD086 - Overridable Errors                                  *
      *  239297 - Display 'Teller ID' field only when CRT001 feature  *
      *           is switched on.                                     *
      *  239898 - Applied core fix BUG11032.                          *
      *  BUG11032 - Reporting About Users                             *
      *  BUG2844- Account officer field should not be shown when text *
      *           is not displayed.                                   *
      *  TDA035 - Added Teller ID.                                    *
      *  CSF002 - Global Processing                                   *
      *           The associated program has been converted to ILE.   *
      *           Commented out Program Codes have been stripped out  *
      *           for easy maintainance.                              *
      *  CSC011 - 24x7 Availability:                                  *
      *           - Addition of COBAUT field.                         *
      *  CGL008 - Security on Journal Entry Input                     *
      *  071482 - Validation of department codes should not be        *
      *           conditioned on setup complete flag.                 *
      *  CAAA01 - GUI/400 DEVELOPMENT,                                *
      *           ADDED NEW FIELDS TO DEFINITION AND MENU ITEMS       *
      *           SCREEN.                                             *
      *  S01408 - Core hooks added - SF0020DFFH                       *
      *                            - SF0020DFF1                       *
      *  079476 - Account Officer and Department fields not properly  *
      *           conditioned                                         *
      *  S01486 - Add Account Officer for Private Banking             *
      *  050065 - POSITION CURSOR ON FIELD IN ERROR.                  *
      *  S01117 - FT RELEASE 10 MULTIBRANCHING CHANGES                *
      *           (OUTPUT QUEUE FOR CHEQUES ADDED)                    *
      *  S01319 - REMOVE REFERENCE TO FRA/IRS IN REFRESH PERIOD       *
      *  S01269 - SE TRADE AUTHORISATION LIMITS ADDED                 *
      *  LN1192 - ADD HELP TAG FOR MULTILANGUAGE FIELD                *
      *  S01253 - AMENDED FOR MULTILANGUAGE SCREENS                   *
      *  S01199 - HELP SYSTEM.                                        *
      *                                                               *
      *****************************************************************
     A                                      CHGINPDFT(HI)
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
     A          R SF000020F1                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      CF03
     A*/COPY WNCPYSRC,SFH00001
      *
     A            USRID         10A  O 01 02
     A                                 01 13'SF000020'
     A            SWSID         10A  O 02 02
     A            SZONE         10A  O 02 13
     A                                 01 26'MIDAS USERS MAINTENANCE'
     A                                      DSPATR(UL HI)
     A            SRUNA          7A  O 01 72
     A                                 02 71TIME
      *
     A                                 03 06'User Profile'
     A            SUSER         10A  B 03 20
     A                                      DSPATR(HI)
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00002
     A                                 03 33'Action'
     A            SACTN          1A  B 03 41
     A                                      DSPATR(HI)
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00003
      *
     A                                 05 06'Enter Y against those details requ-
     A                                      ired for this user:'
      *
     A            SUDRQ          1A  B 07 16
     A                                      DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00004
     A                                 07 21'User definition + menu items'
     A  24 50                          07 51'Warning:deletes all details'
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
     A  26 51                          07 51'mandatory for insert'
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
      *
     A  70        SBBRQ          1A  B 09 16
     A                                      DSPATR(HI)
     A  43                                  DSPATR(RI PC)
     A N43                                  DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00005
     A  70                             09 21'Booking Branches'
     A  24 53
     A*/COPY WNCPYSRC,SFH00006
     AO 25 53                          09 51'No details exist'
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
      *
     A  70 71     SBORQ          1A  B 10 16
     A                                      DSPATR(HI)
     A  44                                  DSPATR(RI PC)
     A N44                                  DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00007
     A  70 71                          10 21'Originating Branches'
     A  24 54
     A*/COPY WNCPYSRC,SFH00008
     AO 25 54                          10 51'No details exist'
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
      *
     A  70        SCORQ          1A  B 11 16
     A                                      DSPATR(HI)
     A  45                                  DSPATR(RI PC)
     A N45                                  DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00009
     A  70                             11 21'Companies'
     A  24 55
     A*/COPY WNCPYSRC,SFH00010
     AO 25 55                          11 51'No details exist'
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
      *
     A            SACRQ          1A  B 12 16
     A                                      DSPATR(HI)
     A  46                                  DSPATR(RI PC)
     A N46                                  DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00011
     A                                 12 21'Action Codes'
     A  24 56
     A*/COPY WNCPYSRC,SFH00012
     AO 25 56                          12 51'No details exist'
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
      *
     A                                 23 02'I=Insert  A=Amend  E=Enquire +
     A                                       D=Delete  R=Reactivate'
     A*/COPY WNCPYSRC,SFH00013
     A                                 24 02'F3=End'
     A*/COPY WNCPYSRC,SFH00014
      *
     A            ERCD          78   O 21 02
     A  80                                  DSPATR(RI HI)
     A            ERMS          78   O 22 02
     A  80                                  DSPATR(RI HI)
      *
     A          R SF000020F2                TEXT('User Maint.- Mandatory detls')
     A                                      CF03
     A  24                                  CF10
     A                                      CF12
      *
     A            USRID         10A  O 01 02
     A                                 01 13'SF000020'
     A            SWSID         10A  O 02 02
     A            SZONE         10A  O 02 13
     A                                 01 22'USER MAINTENANCE - DEFINITION + ME-
     A                                      NU ITEMS'
     A                                      DSPATR(UL HI)
     A            SRUNA          7A  O 01 72
     A                                 02 71TIME
      *
     A                                 03 06'User Profile'
     A            SUSER         10A  B 03 20
     A                                      DSPATR(PR)
     A                                      DSPATR(HI)
     A                                 03 33'Action'
     A            SACTN          1A  B 03 41
     A                                      DSPATR(PR)
     A                                      DSPATR(HI)
      *
     A  65 25                          03 55'DELETED'
     A  65 25                               DSPATR(RI HI)
     A*/COPY WNCPYSRC,SFH00015
      *
     A                                 05 06'Midas user'
     A            SMUSR         25A  B 05 18
     A  24
     A*/COPY WNCPYSRC,SFH00016
     AO 25                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  32                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,SFH00017
     A N32N25N24                            DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00018
      *
      **Start of change BUG11032
     A                                 05 45'User Employee Number'
     A            SEMPN         12A  B 05 66
     AO 24
     A*/COPY WNCPYSRC,SFH00019
     AO 25                                  DSPATR(PR)
     A  83                                  DSPATR(RI PC)
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,SFH00020
     A N83N25N24                            DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00021
      *
      **End of change BUG11032
      *
     A                                 06 06'Midas security level'
     A            SSECL          4A  B 06 27
     A  24
     A*/COPY WNCPYSRC,SFH00022
     AO 25                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  33                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,SFH00023
     A N33N25N24                            DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00024
     A                                      AUTO(RAZ)
     A                                  7 06'Language code'
     A            MULTA          2A  B  7 21
     A  24
     A*/COPY WNCPYSRC,SFH00025
     AO 25                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  42                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,SFH00026
     A N42N25N24                            DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00027
     A                                  8 06'Department'
     A            SDPPT          3A  B  8 18
     A  24
     A*/COPY WNCPYSRC,SFH00028
     AO 25                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  37                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,SFH00029
     A N37N25N24                            DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00030
     A                                  8 23'Account Officer'
     A N29                                  DSPATR(ND)
     A            SACOF          3A  B  8 40
     A N29                                  DSPATR(ND)
      ** Start of Change BUG2844
     A**24******                                                                             BUG2844
     A*O*25*****                            DSPATR(PR)                                       BUG2844
     A N29
     AO 24
     A*/COPY WNCPYSRC,SFH00031
     AO 25                                  DSPATR(PR)
      ** End of Change BUG2844
     A                                      DSPATR(HI)
     A  22                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,SFH00032
     A N22N25N24                            DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00033
     A  70                              8 45'Default branch'
     A  70        SDBRN          3A  B  8 61
     A N70
     AO 24
     A*/COPY WNCPYSRC,SFH00034
     AO 25                                  DSPATR(PR)
     A  11                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,SFH00035
     A N11N24N25                            DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00036
     A  14                              8 66'mandatory entry'
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
     A  67                              9 06'Perform payment authorisation'
     A  67        SPAUT          1A  B  9 38
     A N67
     A*/COPY WNCPYSRC,SFH00037
     AO 24
     AO 25                                  DSPATR(PR)
     A N57                                  DSPATR(HI)
     A  57                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,SFH00038
     A N57N24N25                            DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00039
     A  68                              9 43'System Routing Officer'
     A  68        SROUF          1A  B  9 67
     A N68
     A*/COPY WNCPYSRC,SFH00040
     AO 24
     AO 25                                  DSPATR(PR)
     A N62                                  DSPATR(HI)
     A  62                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,SFH00041
     A N62N24N25                            DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00042
     A  66                             10 06'Auto-refresh'
     A  66        SAURF          1A  B 10 19
     A N66
     A*/COPY WNCPYSRC,SFH00043
     AO 24
     AO 25                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  63                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,SFH00044
     A N63N24N25                            DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00045
     A  66                             10 22'Refresh period'
     A  66        SREFP          4A  B 10 37
     A N66
     A*/COPY WNCPYSRC,SFH00046
     AO 24
     AO 25                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  64                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,SFH00047
     A N64N24N25                            DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00048
     A                                 10 43'COB User'
     A            SCOBU          1A  B 10 52
     A  24
     A*/COPY WNCPYSRC,SFH00049
     AO 25                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  05                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,SFH00050
     A N05N24N25                            DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00051
      **Start of change TDA035
     A                                 10 55'Teller ID'
     A**Start of change 239297
     A N82                                  DSPATR(ND)
     A**End of change 239297
     A            STLID          3A  B 10 65
     A**Start of change 239297
     A N82                                  DSPATR(ND)
     A**End of change 239297
     A  24
     A*/COPY WNCPYSRC,SFH00052
     A**Start of change 239297
     AON82
     A**End of change 239297
     AO 25                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  77                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,SFH00053
     A N77N24N25                            DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00054
      **End of change TDA035
     A N47                             11  6'Securities trading:'
     A N47                             11 45'Entering limit'
     A            ENLIM         17A  B 11 63TEXT('Entering Limit')
     A  24
     A*/COPY WNCPYSRC,SFH00055
     AO 25
     AO 47                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  72                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,SFH00056
     A N72N25N24                            DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00057
     A  47                                  DSPATR(ND)
     A N48                             12 11'Approving limit'
     A            APLIM         17A  B 12 27TEXT('Approving Limit')
     A  24
     A*/COPY WNCPYSRC,SFH00058
     AO 25
     AO 48                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  73                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,SFH00059
     A N73N25N24                            DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00060
     A  48                                  DSPATR(ND)
     A N47                             12 45'Authorising limit'
     A            AULIM         17A  B 12 63TEXT('Authorising limit')
     A  24
     A*/COPY WNCPYSRC,SFH00061
     AO 25
     AO 47                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  74                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,SFH00062
     A N74N25N24                            DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00063
     A  47                                  DSPATR(ND)
     A                                 13 06'Choice of report output-
     A                                       instructions'
     A            SROUT          1A  B 13 44
     A  24
     A*/COPY WNCPYSRC,SFH00064
     AO 25                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  38                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,SFH00065
     A N38N25N24                            DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00066
     A                                 14 06'AS/400 Directory user id and addre-
     A                                      ss'
     A            SDIRID         8A  B 14 44
     A  24
     A*/COPY WNCPYSRC,SFH00067
     AO 25                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  58
     AO 59
     AO 60
     AO 61                                  DSPATR(RI PC)
     A N58N59N60
     A*/COPY WNCPYSRC,SFH00068
     AAN25N24N61                            DSPATR(UL)
     A            SDIRAD         8A  B 14 54
     A  24
     A*/COPY WNCPYSRC,SFH00069
     AO 25                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  58
     AO 59
     AO 60
     AO 61                                  DSPATR(RI PC)
     A N58N59N60
     A*/COPY WNCPYSRC,SFH00070
     AAN25N24N61                            DSPATR(UL)
     A                                 15 06'Output queue for reports'
     A            SOUTQ         10A  B 15 32
     A  24
     A*/COPY WNCPYSRC,SFH00071
     AO 25                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  13                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,SFH00072
     A N13N25N24                            DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00073
     A  67                             15 44'Output queue for cheques'
     A  67        SCOUTQ        10A  B 15 70
     A N67
     AO 24
     A*/COPY WNCPYSRC,SFH00074
     AO 25                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  16                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,SFH00075
     A N16N25N24                            DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00076
     A                                 16 06'Allow access to mult +
     A                                      users'' rpts'
     A            SMUREP         1A  B 16 39
     A  24
     A*/COPY WNCPYSRC,SFH00077
     AO 25                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  39                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,SFH00078
     A N39N25N24                            DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00079
     A  28                             16 43'Allow access J. entry +
     A                                      acct code'
     A  28        SJEAA          1A  B 16 75
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,SFH00080
     A N08N24N25                            DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00081
     A  08                                  DSPATR(RI PC)
     A N28
     A*/COPY WNCPYSRC,SFH00082
     AO 24
     AO 25                                  DSPATR(PR)
     A  70                             17 06'Allow access to multiple branc-
     A                                      hes'' reports'
     A  70        SMBREP         1A  B 17 50
     A N70
     A*/COPY WNCPYSRC,SFH00083
     AO 24
     AO 25                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  12                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,SFH00084
     A N12N24N25                            DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00085
     A  28                             17 53'Reference User'
     A  28        SREFU         10A  B 17 68
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,SFH00086
     A N09N24N25                            DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00087
     A  09                                  DSPATR(RI PC)
     A N28
     A*/COPY WNCPYSRC,SFH00088
     AO 24
     AO 25                                  DSPATR(PR)
     A  28                             18 53'User Authority Lvl'
     A  28        SALVL          1A  B 18 73
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,SFH00089
     A N10N24N25                            DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00090
     A  10                                  DSPATR(RI PC)
     A N28
     A*/COPY WNCPYSRC,SFH00091
     AO 24
     AO 25                                  DSPATR(PR)
     A  70                             18 06'Allow access to Bank level functio-
     A                                      ns'
     A  70        SBANK          1A  B 18 44
     A N70
     A*/COPY WNCPYSRC,SFH00092
     AO 24
     AO 25                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  36                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,SFH00093
     A N36N24N25                            DSPATR(UL)
      **Start of Change CSD086
     A                                 19 06'Override Profile'
     A            SOVPR         10A  B 19 24
     A                                      DSPATR(HI)
     A  84                                  DSPATR(RI PC)
      **Start of Change BUG27503
     A*N84******                            DSPATR(UL)                                      BUG27503
     A  24
     AO 25                                  DSPATR(PR)
     A N84N24N25                            DSPATR(UL)
     A*/COPY WNCPYSRC,SFH00094
      **End of Change BUG27503
      **End of Change CSD086
      ** Start of Change BUG28159
     A                                      CHECK(LC)
      ** End of Change BUG28159
      *
     A                                 24 02'F3=End,'
     A                                 24 10'or F12=Initial Screen'
     A  24                             24 32', or F10=Delete'
      *
     A            ERCD          78   O 22 02
     A  80                                  DSPATR(RI HI)
     A            ERMS          78   O 23 02
     A  80                                  DSPATR(RI HI)
      *
     A          R SF000020F3                TEXT('Blank screen')
     A                                  1  2' '
     A                                 24 79' '
