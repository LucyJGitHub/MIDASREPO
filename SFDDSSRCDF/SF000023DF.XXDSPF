      *****************************************************************
/*S*D****DSPFBASE******************************************************
      *****************************************************************
      *                                                               *
      *  Midas - Security Profile Facility                            *
      *                                                               *
      *  SF000023DF - Midas Users Maintenance - Companies             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. AR904191*REDUNDANT Date 03Feb12               *
      *  Prev Amend No. AR787620           Date 10Jun11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSF002 *RENAME     Date 11Aug03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01199             Date 18Sep89               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR904191 - Replaced as per BF requirement (CBF005)           *
      *  AR787620 - Hooks Incorporation                               *
      *             WNCPYSRC,SFH00158                                 *
      *             WNCPYSRC,SFH00159                                 *
      *  CSF002 - Global Processing                                   *
      *           The associated program has been converted to ILE.   *
      *           Commented out Program Codes have been stripped out  *
      *           for easy maintainance.                              *
      *  S01199 - HELP SYSTEM.                                        *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(MPRINT)
     A                                      CF03
     A  24                                  CF10
     A                                      CF12
      *
     A          R SF000023F1                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      SFL
     A  12                                  SFLNXTCHG
      *
     A            SSLCT          1   B  8 11TEXT('Select?')
     A                                      DSPATR(HI)
     A  23
     A*/COPY WNCPYSRC,SFH00158
     AO 24                                  DSPATR(PR)
     A  21N31
     AO 22N31                               DSPATR(UL)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
      *
     A            SCMPY          3   O  8 16TEXT('Company Code')
     A                                      DSPATR(HI)
      *
     A            SCMNM         30   O  8 22TEXT('Company Name')
     A                                      DSPATR(HI)
      *
     A          R SF000023F2                TEXT('Subfile controller')
     A                                      SFLCTL(SF000023F1)
     A                                      SFLSIZ(100)
     A                                      SFLPAG(12)
     A                                      SFLDSPCTL
     A  10                                  SFLDSP
     A  11                                  SFLEND
     A                                      OVERLAY
     A            SDREC          3  0H      SFLRCDNBR
      *
     A N10                             20  2'  THERE ARE NO COMPANY CODES DEFIN-
     A                                      ED ON THE SYSTEM                   -
     A                                               '
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
      *
     A  30        SERCD         78   O 21  2DSPATR(HI)
     A                                      DSPATR(RI)
      *
     A                                 23  1'F3=End, or F12=I-
     A                                      nitial screen'
     A  24                             23 31', or F10=Delete'
      *
     A          R SF000023F3                TEXT('Screen Header')
      *
     A            USRID         10A  O 01  2
     A                                 01 13'SF000023'
     A            SWSID         10A  O 02  2
     A            SZONE         10A  O 02 13
     A                                  1 26'USER MAINTENANCE - COMPANIES'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            SRUNA          7A  O 01 72
     A                                 02 71TIME
     A                                  3  6'User Profile'
     A            SUSER         10   O  3 20DSPATR(HI)
     A                                  3 33'Action'
     A            SACTN          1   O  3 41DSPATR(HI)
     A  26                              3 55'DELETED'
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
     A*/COPY WNCPYSRC,SFH00159
     A  21
     AO 22                              5  6'Enter Y against those Companies pe-
     A                                      rmitted for this user:'
     A  23
     AO 24                              5  6'The user has authority to those Co-
     A                                      mpanies with Y entered against them-
     A                                      :'
     A                                  7 15'Code'
     A                                      DSPATR(UL)
     A                                  7 23'Name'
     A                                      DSPATR(UL)
