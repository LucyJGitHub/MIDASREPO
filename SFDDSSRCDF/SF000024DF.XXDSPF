      *****************************************************************
/*S*D****DSPFBASE******************************************************
      *****************************************************************
      *                                                               *
      *  Midas - Security Profile Facility                            *
      *                                                               *
      *  DSPF/SF000024DF - Midas Users Maintenance - Action Codes     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. AR904191*REDUNDANT Date 03Feb12               *
      *  Prev Amend No. AR787620           Date 10Jun11               *
      *                 BUG28076           Date 27Apr10               *
      *                 BUG16742           Date 08Apr08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 BUG4864            Date 25Jan06               *
      *                 CSF002 *RENAME     Date 11Aug03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAAA01             Date 24Apr95               *
      *                 RT0148             Date 06Jun91               *
      *                 S01199             Date 18Sep89               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR904191 - Replaced as per BF requirement (CBF005)           *
      *  AR787620 - Hooks Incorporation                               *
      *             WNCPYSRC,SFH00173                                 *
      *             WNCPYSRC,SFH00174                                 *
      *             WNCPYSRC,SFH00175                                 *
      *             WNCPYSRC,SFH00176                                 *
      *             WNCPYSRC,SFH00177                                 *
      *             WNCPYSRC,SFH00178                                 *
      *  BUG28076 - Reverse fix BUG16742                              *
      *  BUG16742 - Details of the newly created FX Deal are missing  *
      *             in WIP.                                           *
      *  BUG4864 - Enable 'Position to'                               *
      *  CSF002 - Global Processing                                   *
      *           The associated program has been converted to ILE.   *
      *           Commented out Program Codes have been stripped out  *
      *           for easy maintainance.                              *
      *  CAAA01 - GUI/400 DEVELOPMENT:                                *
      *         - INCREMENT LENGTH OF RRN NUMBER BY 2.                *
      *         - ADD 'AUTHORISE TO ALL UNDEFINED ITEMS' KEY.         *
      *  RT0148 - INCREASE NUMBER OF ACTION CODES TO 10               *
      *  S01199 - HELP SYSTEM.                                        *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(MPRINT)
     A                                      CF03
     A                                      CF12
     A          R SF000024F1                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      SFL
     A  09                                  SFLNXTCHG
      *
     A            SSLCT          1   B  8 11TEXT('Select?')
     A                                      DSPATR(HI)
     A N12N13                               DSPATR(UL)
     A  12
     AO 13                                  DSPATR(RI PC)
      *
     A            SBRCA          3   O  8 16TEXT('Branch Code')
     A                                      DSPATR(HI)
      *
     A            SBRNM         30   O  8 22TEXT('Branch Name')
     A                                      DSPATR(HI)
      *
     A            SMSG          23   O  8 56TEXT('Error message')
     A  13                                  DSPATR(HI)
     A  13                                  DSPATR(RI)
     A N13                                  DSPATR(ND)
      *
     A          R SF000024F2                TEXT('Subfile controller for Branch-
     A                                       selection screen')
     A                                      SFLCTL(SF000024F1)
     A                                      SFLSIZ(100)
     A                                      SFLPAG(11)
     A  05                                  SFLCLR
     A  06                                  SFLDSPCTL
     A  07                                  SFLDSP
     A  08                                  SFLEND
     A                                      OVERLAY
     A            SDREC1         4  0H      SFLRCDNBR
      *
     A N07                             20  2'  THE USER IS NOT AUTHORISED TO AN-
     A                                      Y BOOKING BRANCHES                 -
     A                                             '
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
      *
     A  11        SERCD         78   O 21  2DSPATR(HI)
     A                                      DSPATR(RI)
      *
     A                                 23  1'F3=End  F12=Initial screen'
      *
     A          R SF000024F3                TEXT('Branch selection screen heade-
     A                                      r')
      *
     A            USRID         10A  O 01  2
     A                                 01 13'SF000024'
     A            SWSID         10A  O 02  2
     A            SZONE         10A  O 02 13
     A                                  1 26'USER MAINTENANCE - ACTION CODES'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            SRUNA          7A  O 01 72
     A                                 02 71TIME
     A                                  3  6'User Profile'
     A            SUSER         10   O  3 20DSPATR(HI)
     A                                  3 33'Action'
     A            SACTN          1   O  3 41DSPATR(HI)
     A                                  5  5'Enter Y against those Booking Bran-
     A                                      ches requiring Action Code maintena-
     A                                      nce.'
      *
     A                                  7 15'Code'
     A                                      DSPATR(UL)
     A                                  7 23'Name'
     A                                      DSPATR(UL)
      *
     A          R SF000024F4                TEXT('Subfile for Action Codes deta-
     A                                      ils')
     A                                      SFL
     A  19                                  SFLNXTCHG
      *
     A            SINDX         10   H      TEXT('Unique Code')
     A            SSNAM         20   O  9  2TEXT('Menu Item Code')
     A                                      DSPATR(HI)
     A            SINAR         33   O  9 23TEXT('Menu Item Narrative')
     A  72                                  DSPATR(RI)
      *
     A            SPACD         10   O  9 57TEXT('Applicable A/Code')
     A                                      DSPATR(HI)
     A            SACAD         10   B  9 68TEXT('Action Code?')
     A                                      DSPATR(HI)
     A  23
     A*/COPY WNCPYSRC,SFH00173
     AO 24                                  DSPATR(PR)
     A  21N31N32
      ** Start of change BUG28076
     A**Start*of*change*BUG16742***************************************                     BUG28076
     A*N39******                                                                            BUG28076
      ** End of change BUG28076
     A*O*22N31N32                            DSPATR(UL)                                     BUG16742
      ** Start of change BUG28076
     AO 22N31N32
     A**O*22N31N32                                                                          BUG28076
     A*N39******                            DSPATR(UL)                                      BUG28076
     A**End*of*change*BUG16742*****************************************                     BUG28076
      ** End of change BUG28076
     A  31
      ** Start of change BUG28076
     A**Start*of*change*BUG16742***************************************                     BUG28076
     A**O*39****                                                                            BUG28076
     A**End*of*change*BUG16742*****************************************                     BUG28076
      ** End of change BUG28076
     AO 32                                  DSPATR(RI PC)
      *
     A          R SF000024F5                TEXT('Subfile controller for Action-
     A                                       Codes details')
     A  70                                  CF02
     A  24                                  CF10
     A*/COPY WNCPYSRC,SFH00174
     A N23N24N29                            CF13(71)
     A*/COPY WNCPYSRC,SFH00175
     A                                      SFLCTL(SF000024F4)
     A                                      SFLSIZ(11)
     A                                      SFLPAG(11)
     A N17N16                               SFLCLR
     A  16                                  SFLDSPCTL
     A  17                                  SFLDSP
     A  82                                  SFLEND
     A N82N29                               ROLLUP(67)
     A N29N34                               ROLLDOWN(68)
     A                                      OVERLAY
      *
     A  29                             20  2'  THE USER IS NOT AUTHORISED TO AN-
     A                                      Y MENU ITEMS WITH ACTION CODES     -
     A                                             '
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
      *
     A  30        SERCD         78   O 21  2DSPATR(HI)
     A                                      DSPATR(RI)
      *
     A                                 23  1'F3=End, or F12=Init-
     A                                      ial screen'
     A  24                             23 31', or F10=Delete'
     A  70                             24  1'or F2=Branch selection screen'
     A*/COPY WNCPYSRC,SFH00176
     A N23N24N29                       23 31'F13=Authorise to all undefined +
     A                                      items.'
     A*/COPY WNCPYSRC,SFH00177
      *
     A          R SF000024F6                TEXT('Action Codes Details Screen H-
     A                                      eader')
     A            USRID         10A  O 01  2
     A                                 01 13'SF000024'
     A            SWSID         10A  O 02  2
     A            SZONE         10A  O 02 13
     A                                  1 26'USER MAINTENANCE - ACTION CODES'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            SRUNA          7A  O 01 72
     A                                 02 71TIME
     A                                  3  6'User Profile'
     A            SUSER         10   O  3 20DSPATR(HI)
     A                                  3 33'Action'
     A            SACTN          1   O  3 41DSPATR(HI)
     A                                  3 48'Branch'
     A N70                                  DSPATR(ND)
     A            SBRCA          3   O  3 56DSPATR(HI)
     A N70                                  DSPATR(ND)
     A  26                              3 70'DELETED'
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
     A*/COPY WNCPYSRC,SFH00178
     A  21
     AO 22                              5  6'For each menu item select the Acti-
     A                                      on Codes permitted for this user:'
     A  21
     AO 22                              6  6'Items with defaulted action codes +
     A                                      have highlighted text.'
     A  23
     AO 24                              5  5'The user has authority to those Ac-
     A                                      tion Codes in the A/CODES ALLOWED f-
     A                                      ield:'
     A**********                        7  2'M e n u    I t e m s'                           BUG4864
     A            S#NAM         20   B  7  2TEXT('Menu Item Code')                           BUG4864
     A                                      DSPATR(HI)                                       BUG4864
     A                                  7 57'Applicable'
     A                                  7 68'A/Codes'
     A                                  8  2'Short Code'
     A                                      DSPATR(UL)
     A                                  8 23'Narrative'
     A                                      DSPATR(UL)
     A                                  8 57'A/Codes'
     A                                      DSPATR(UL)
     A                                  8 68'Allowed'
     A                                      DSPATR(UL)
