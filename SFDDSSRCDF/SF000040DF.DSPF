      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SF Copy reference user')
      *****************************************************************
      *                                                               *
      *  Midas - Security Profile Facility                            *
      *                                                               *
      *  DSPF/SF000040DF - Screen Format for reference User           *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG2598            Date 12May04               *
      *                 CSF002 *RENAME     Date 11Aug03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01199             Date 18Sep89               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG2598- Webfacing changes.  Conditioned reverse image on    *
      *           message subfile only when messages are shown.       *
      *  CSF002 - Global Processing                                   *
      *           The associated program has been converted to ILE.   *
      *           Commented out Program Codes have been stripped out  *
      *           for easy maintainance.                              *
      *  S01199 - Help system.                                        *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
      *
     A                                      CF03
     A                                      CF12
     A                                      HOME
     A                                      CHGINPDFT
     A          R SF000040D1                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      BLINK
     A            USRID         10A  O 01  2
     A                                 01 13'SF000040'
     A            SWSID         10A  O 02  2
     A            SZONE         10A  O 02 13
     A                                  1 30'COPY REFERENCE USER'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            SRUNA          7A  O 01 72
     A                                 02 71TIME
     A                                  5 18'Reference User'
     A            RFUSR         10A  B  5 36
     A                                      DSPATR(HI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(RI)
     A                                  6 18'New User'
     A            NWUSR         10A  B  6 36
     A                                      DSPATR(HI)
     A N32                                  DSPATR(UL)
     A  32                                  DSPATR(RI)
     A                                  7 18'Description'
     A            NWDES         25A  B  7 36
     A                                      DSPATR(HI)
     A N33                                  DSPATR(UL)
     A  33                                  DSPATR(RI)
     A                                  8 18'AS/400 Directory user id and addre-
     A                                      ss'
     A            NDIRID         8A  B  8 55
     A                                      DSPATR(HI)
     A N38                                  DSPATR(UL)
     A  38                                  DSPATR(RI)
     A            NDIRAD         8A  B  8 65
     A                                      DSPATR(HI)
     A N38                                  DSPATR(UL)
     A  38                                  DSPATR(RI)
     A                                 10  7'Enter the following fields if you -
     A                                      wish to swap branches:'
     A  55                                  DSPATR(ND)
     A                                 12 18'From ref.branch'
     A  55                                  DSPATR(ND)
     A            FMBRC          3A  B 12 36
     A                                      DSPATR(HI)
     A N34                                  DSPATR(UL)
     A  34                                  DSPATR(RI)
     A  55                                  DSPATR(ND PR)
     A                                 13 18'To new branch'
     A  55                                  DSPATR(ND)
     A            TOBRC          3A  B 13 36
     A                                      DSPATR(HI)
     A N35                                  DSPATR(UL)
     A  35                                  DSPATR(RI)
     A  55                                  DSPATR(ND PR)
     A                                 15  7'Enter the following fields if you -
     A                                      wish to swap companies:'
     A  55                                  DSPATR(ND)
     A                                 17 18'From ref.company'
     A  55                                  DSPATR(ND)
     A            FMCMP          3A  B 17 36
     A                                      DSPATR(HI)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(RI)
     A  55                                  DSPATR(ND PR)
     A                                 18 18'To new company'
     A  55                                  DSPATR(ND)
     A            TOCMP          3A  B 18 36
     A                                      DSPATR(HI)
     A N37                                  DSPATR(UL)
     A  37                                  DSPATR(RI)
     A  55                                  DSPATR(ND PR)
     A**Start of change BUG2598
     A**********  ERCD          75A  O 20  4DSPATR(RI)                                       BUG2598
     A N10        ERCD          75A  O 20  4DSPATR(RI)
     A**End of change  BUG2598
     A  10                                  DSPATR(ND)
     A**Start of change BUG2598
     A**********  ERMSG         75A  O 21  4DSPATR(RI)                                       BUG2598
     A N10        ERMSG         75A  O 21  4DSPATR(RI)
     A**End of change  BUG2598
     A  10                                  DSPATR(ND)
     A                                 23  4'F3=Exit'
