      *****************************************************************
/*S*D****DSPFBASE******************************************************
      *****************************************************************
      *                                                               *
      *  Midas - Security Profile Facility                            *
      *                                                               *
      *  SF000205DF - Midas Users Enquiry                             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. AR904191*REDUNDANT Date 03Feb12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSF002 *RENAME     Date 11Aug03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 LN0122             Date 15Jun90               *
      *                 S01199             Date 18Sep89               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR904191 - Replaced as per BF requirement (CBF005)           *
      *  CSF002 - Global Processing                                   *
      *           The associated program has been converted to ILE.   *
      *           Commented out Program Codes have been stripped out  *
      *           for easy maintainance.                              *
      *  LN0122 - IMPROVE PROGRAMME PERFORMANCE                       *
      *  S01199 - HELP SYSTEM.                                        *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(MPRINT)
     A                                      CF03
     A          R SF000205F1                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      SFL
     A  12                                  SFLNXTCHG
      *
     A            SSLCT          1   B  5 20TEXT('Select?')
     A  22                                  DSPATR(RI PC)
      *
     A            SUSR          10   O  5 26TEXT('User Profile')
     A                                      DSPATR(HI)
      *
     A            SUSNM         25   O  5 39TEXT('User Name')
     A                                      DSPATR(HI)
      *
     A          R SF000205F2                TEXT('Subfile controller')
     A                                      SFLCTL(SF000205F1)
     A                                      SFLSIZ(100)
     A                                      SFLPAG(15)
     A                                      SFLDSPCTL
     A  10                                  SFLDSP
     A  11                                  SFLEND
     A                                      OVERLAY
     A            SDREC          3  0H      SFLRCDNBR
      *
     A N10                             20  2'  THERE ARE NO USERS DEFIN-
     A                                      ED ON THE SYSTEM                   -
     A                                               '
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
     A  24                             20  2'Invalid Character Entered'
     A                                      DSPATR(HI RI)
      *
     A  30        SERCD         78   O 21  2DSPATR(HI)
     A                                      DSPATR(RI)
      *
     A                                 23  1'F3=End'
     A                                 23 10'Enter ''Y'' to select'
      *
     A          R SF000205F3                TEXT('Screen Header')
      *
     A            USRID         10A  O 01  2
     A                                 01 13'SF000205'
     A            SWSID         10A  O 02  2
     A            SZONE         10A  O  2 13
     A                                  1 28'USER MAINTENANCE - USERS'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            SRUNA          7A  O 01 72
     A                                 02 71TIME
     A                                  4 20'Sel---User---------User Descripti+
     A                                      on----'
     A                                      DSPATR(HI)
