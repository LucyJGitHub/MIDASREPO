      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SF Account codes authorities maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Security Profile Facility Module                     *
      *                                                               *
      *  SF0032DF - Account Codes Authorities Maintenace              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 181417             Date 13Dec00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CGL008             Date 01Mar99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  181417 - Add Help Tag                                        *
      *  CGL008 - Security on Journal Entry Input                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      CA03(03 'End of job')
     A                                      CA05(05 'Refresh')
     A                                      PRINT
     A*                                      /Begin 181417/
     A*                                      /Begin 181417/
      *
      ** Screen 1 Subfile Record Format.
      *
     A          R SF0032S1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A  31                                  SFLNXTCHG
     A            R1SEL          1A  B  7 13DSPATR(HI)
     A N60N39                               DSPATR(UL)
     A  60N39                               DSPATR(RI PC)
     A  39                                  DSPATR(PR)
     A**Start of change CGL029
     A**********  R1ACOD         4A  O  7 16DSPATR(HI)                                        CGL029
     A            R1ACOD        10A  O  7 16DSPATR(HI)
     A**End of change CGL029
     A            R1ACCN        25A  O  7 29DSPATR(HI)
     A            R1ACTY         1A  O  7 58DSPATR(HI)
      *
      ** Screen 1 Control Record Format
      *
     A          R SF0032C1                  SFLCTL(SF0032S1)
     A                                      BLINK
     A                                      OVERLAY
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A N29                                  SFLDSPCTL
     A N29N28                               SFLDSP
     A  29                                  SFLCLR
     A  37                                  SFLEND
     A  35                                  ROLLUP(27)
     A  52                                  CF21(21 'Print all')
      *
     A            SFLRR1         4S 0H      SFLRCDNBR
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SF0032'
     A                                  1 22'ACCOUNT CODES AUTHORITIES +
     A                                      MAINTENANCE'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                 04 13'Account Code'
     A**Start of change CGL029
     A**********  S1ACOD         4A  B 05 16                                                  CGL029
     A            S1ACOD        10A  B 05 16
     A**End of change CGL029
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 04 29'Account Code Name'
     A            S1ACCN        25A  B 05 29
     A                                      CHECK(LC)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 04 57'Type'
     A            S1ACTY         1A  B 05 58
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 04 66'User profile'
     A            S1USRP        10A  B 05 67
     A                                      DSPATR(HI)
     A N64                                  DSPATR(UL)
     A  64                                  DSPATR(RI PC)
      *
      ** Screen 1 Footings Format.
      *
     A          R SF0032F1                  OVERLAY
     A  50                             22 02'A=Amend'
     A  51                                +2'E=Enquire'
     A  52                                +3'P=Print'
     A                                 23 02'F3=Exit  F5=Refresh'
     A  52                                +2'F21=Print all'
      *
      ***  Screen 2 Subfile Record Format.
      *
     A          R SF0032S2                  SFL
     A  36                                  SFLNXTCHG
     A            R2USRP        10A  B  9 15DSPATR(HI)
     A N32                                  DSPATR(PR)
     A N64 32                               DSPATR(UL)
     A  64                                  DSPATR(RI PC)
     A            R2USR1        25A  O  9 28DSPATR(HI)
     A            R2USR2        10   H
      *
      ** Screen 2 Control Record Format
      *
     A          R SF0032C2                  SFLCTL(SF0032S2)
     A                                      OVERLAY
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      BLINK
     A  38                                  SFLDSP
     A  38                                  SFLDSPCTL
     A  29                                  SFLCLR
     A  37                                  SFLEND
     A  35                                  ROLLUP(27)
     A                                      CF12(12 'Previous Screen')
      *
     A            SFLRR2         4S 0H      SFLRCDNBR
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'SF0032'
     A                                  1 22'ACCOUNT CODES AUTHORITIES +
     A                                      MAINTENANCE'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 32'USER PROFILES'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                 04 03'Account Code'
     A**Start of change CGL029
     A**********  S2ACOD         4A  O 04 21DSPATR(HI)                                        CGL029
     A            S2ACOD        10A  O 04 21DSPATR(HI)
     A**End of change CGL029
     A                                 05 03'Account Code Name'
     A            S2ACCN        25A  O 05 21DSPATR(HI)
     A                                 07 14'User Profile'
     A                                 07 28'Name'
      *
      ** Screen 2 Footings Format.
      *
     A          R SF0032F2                  OVERLAY
     A N65                             23 02'F3=Exit  F5=Refresh  +
     A                                      F12=Previous Screen'
     A  65                             23 02'F3=Exit  F12=Previous +
     A                                      Screen'
      *
      ** Error message Subfile Format
      *
     A          R SF0032S3                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMNAM                    SFLPGMQ
      *
      ** Error message Subfile Controller Format
      *
     A          R SF0032C3                  TEXT('Program messages')
     A                                      SFLCTL(SF0032S3)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  34                                  SFLCLR
     A  33
     AON33                                  SFLEND
     A            SPGM                      SFLPGMQ
