      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SF Password Enquiry DSPF')
     A********************************************************************
     A**                                                                **
     A*  Midas SECURITY PROFILE FACILITY
     A**                                                                **
     A**     DSPF/SF0210DF - MIDAS PASSWORD ENQUIRY                     **
     A**                                                                **
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A**                                                                **
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A**     LAST AMEND NO. S01199           DATE 18/09/89              **S01199
     A**                                                                **
     A********************************************************************
     A*                                                                  *
     A*      S01199 - HELP SYSTEM.                                       *S01199
     A*                                                                  *
     A********************************************************************
     A                                      PRINT(MPRINT)
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A          R SF0210F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A                                      CF03
     A            USRID         10A  O 01  2
     A                                 01 13'SF0210'
     A            SWSID         10A  O 02  2
     A                                 01 26'MIDAS PASSWORD ENQUIRY'
     A                                      DSPATR(UL HI)
     A            SRUNA          7A  O 01 72
     A                                 02 71TIME
     A                               O 10 05'Enter Midas User Code or-
     A                                       leave blank for all users'
     A            SUSRC         10A  B 10 60
     A  20        ERCD          78A  O 21 02
     A                                      DSPATR(RI HI)
     A*
     A                                 23 02'F3=End'
     A*
     A          R SF0210F2
     A                                      SFL
     A            SUSRP         10A  O 08 03
     A            SSOCNT         3  0O 08 25EDTCDE(3)
     A            SLPWD          7A  O 08 49
     A            SLPWT          7  0O 08 65EDTWRD('   :  :  ')
     A          R SF0210F3                  SFLCTL(SF0210F2)
     A  55                                  SFLCLR
     A N55                                  SFLDSPCTL
     A N55                                  SFLDSP
     A                                      SFLSIZ(200)
     A                                      SFLPAG(10)
     A  40                                  SFLEND
     A                                      OVERLAY
     A                                      CF03
     A                                      CF12
     A*
     A N55                             23 02'F3=End, or F12=Initial S+
     A                                      creen'
     A*
     A          R SF0210F4
     A                                      OVERLAY
     A            USRID         10A  O 01  2
     A                                 01 13'SF0210'
     A            SWSID         10A  O 02  2
     A                                 01 26'MIDAS PASSWORD ENQUIRY'
     A                                      DSPATR(UL HI)
     A            SRUNA          7A  O 01 72
     A                                 02 71TIME
     A  30                             03 05'There has been no Univer+
     A                                      sal Password Change'
     A  30                                  DSPATR(RI HI)
     A N30                             03 05'Last Universal Password +
     A                                       Change on '
     A N30        SLUPWD         7A  O 03 40
     A N30                             03 50'at'
     A N30        SLUPWT         6  0O 03 53EDTWRD('  :  :  ')
     A                                 05 01'User Profile'
     A                                 05 16'Number of sign-ons since'
     A                                 05 44'Date of last'
     A                                 05 62'Time of last'
     A                                 06 16'last password change'
     A                                 06 44'password change'
     A                                 06 62'password change'
