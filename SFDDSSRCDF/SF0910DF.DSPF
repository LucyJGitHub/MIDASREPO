      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas¦ABS Maintain Restricted Account Code Ranges')
      *****************************************************************
      *                                                               *
      *  Midas - Security Profile Facility                            *
      *                                                               *
      *  DSPF/SF0910DF - MAINTAIN RESTRICTED ACCOUNT CODE RANGES      *
      *                                                               *
      *  (C) COPYRIGHT MIDAS-KAPITI INTERNATIONAL LTD. 1996           *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CRE024   *CREATE   Date 07Oct05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRE024 - COUNTRY LEVEL SWITCHABLE FEATURE :                  *
      *           SECURITY FEATURES FOR RESTRICTED ACCOUNTS           *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      HELP
     A                                      HLPDOC(H001@000 SFMINS ABSENG/DBAR2)
      *
     A          R SF0910HH                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
     A            SUSER     R   10   O  1  2REFFLD(USER)
     A            SPGM      R   10   O  1 13REFFLD(JOPGM)
     A            SRUNA     R    7   O  1 72REFFLD(RUNA)
     A            SWSID     R   10   O  2  2REFFLD(JOJOB)
     A                                  2 26'Restricted Accounts Maintenance'
     A                                      DSPATR(HI)
     A                                  2 71TIME
      *
     A          R SF0910S0                  SFL
     A  33                                  SFLNXTCHG
     A            S0SEL     R        B  8 10REFFLD(ACTN)
     A                                      VALUES('D' 'E' 'X')
     A                                      CHECK(AB)
     A                                      SFLCSRPRG
     A                                      DSPATR(HI UL)
     A  40                                  DSPATR(PR ND)
     A            S0FRAC        10A  B  8 14
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI PC)
     A N40                                  DSPATR(PR)
     A  40                                  DSPATR(UL)
     A            S0TOAC        10A  B  8 26
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI PC)
     A N40                                  DSPATR(PR)
     A  40                                  DSPATR(UL)
     A            S0RGDS    R        B  8 37REFFLD(ACKN)
     A N37                                  DSPATR(HI)
     A N37 31                               DSPATR(UL)
     A  37                                  DSPATR(RI)
     A N31                                  DSPATR(PR)
     A                                      CHECK(LC)
      *
     A          R SF0910C0                  TEXT('Subfile control')
     A                                      SFLCTL(SF0910S0)
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY PUTOVR
     A  30                                  SFLDSPCTL
     A  31                                  SFLDSP
     A N30N31                               SFLCLR
     A  32                                  SFLEND
     A N32                                  ROLLUP(39)
     A                                      CA05
     A                                      CA09
     A N40                                  CA11
     A N40                                  CA13
     A N40                                  CA24
     A                                      ERASE(SF0910S0)
     A          H                           HLPARA(05 01 22 80)
     A N40                                  HLPDOC(H001@005 SFMINS ABSENG/DBAR2)
     A          H                           HLPARA(05 01 22 80)
     A  40                                  HLPDOC(H001@010 SFMINS ABSENG/DBAR2)
      *
     A            C0SFRC         4  0H      SFLRCDNBR
     A                                  5 14'Account Range'
     A                                  5 37'Description'
     A                                  6 14'From        To'
      *
     A          R SF0910S1                  SFL
     A            S1SEL     R        B  9 10REFFLD(ACTN)
     A                                      VALUES('D')
     A                                      CHECK(AB)
     A  60                                  DSPATR(HI UL)
     A N60
     AO 61                                  DSPATR(PR ND)
     A            S1AUSR    R        O  9 14REFFLD(USRP)
     A                                      DSPATR(HI)
     A            S1USNM    R        O  9 31REFFLD(USER1)
     A                                      DSPATR(HI)
      *
     A          R SF0910C1                  TEXT('Subfile control')
     A                                      SFLCTL(SF0910S1)
     A                                      SFLPAG(13) SFLSIZ(14)
     A                                      BLINK OVERLAY PUTOVR
     A  50                                  SFLDSPCTL
     A  51                                  SFLDSP
     A N50N51                               SFLCLR
     A  52                                  SFLEND
     A N52                                  ROLLUP(59)
     A  60                                  CA09
     A                                      CA12
     A          H                           HLPARA(05 01 22 80)
     A                                      HLPDOC(H001@015 SFMINS ABSENG/DBAR2)
      *
     A            C1SFRC         4  0H      SFLRCDNBR
     A            S0FRAC        10A  O  5 14
     A                                      DSPATR(HI)
     A            S0TOAC        10A  O  5 26
     A                                      DSPATR(HI)
     A            S0RGDS    R        O  5 37REFFLD(ACKN)
     A                                      DSPATR(HI)
     A                                  7 11'Authorised Users'
      *
     A          R #CMDTXT0                  TEXT('Command keys')
     A                                      OVERLAY
     A N38N40                          23  2'D=Delete  E=Enquire  X=Authorise  -
     A                                      F3=Main Menu  F5=Refresh  F9=Add  -
     A                                      F24=More'
     A  38N40                          23  2'F11=Print  F13=Users              -
     A                                                                        -
     A                                      F24=More'
     A  40                             23  2'F3=Main Menu  F5=Refresh  -
     A                                      F9=Change'
      *
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY
     A N60                             23  2'F3=Main Menu  F12=Previous'
     A  60                             23  2'D=Delete  F3=Main Menu  F9=Add  -
     A                                      F12=Previous'
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
      *
