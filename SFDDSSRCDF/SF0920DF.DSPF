      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas¦ABS Maintain Restricted Account Users')
      *****************************************************************
      *                                                               *
      *  Midas - Security Profile Facility                            *
      *                                                               *
      *  DSPF/SF0920DF - MAINTAIN RESTRICTED ACCOUNT USERS            *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CRE024  *CREATE    Date 07Oct05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRE024 - COUNTRY LEVEL SWITCHABLE FEATURE :                  *
      *           SECURITY FEATURES FOR RESTRICTED ACCOUNTS           *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      HELP
     A                                      HLPDOC(H001@000 SFMINS ABSENG/DBAR2)
      *
     A          R SF0920HH                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A                                      KEEP
     A            SUSER     R   10   O  1  2REFFLD(USER)
     A            SPGM      R   10   O  1 13REFFLD(JOPGM)
     A            SRUNA     R    7   O  1 72REFFLD(RUNA)
     A            SWSID     R   10   O  2  2REFFLD(JOJOB)
     A                                  2 28'Authorised Users Maintenance'
     A                                      DSPATR(HI)
     A                                  2 71TIME
      *
     A          R SF0920S0                  SFL
     A            S0SEL     R        B  7 10REFFLD(ACTN)
     A                                      VALUES('D' 'E' 'X')
     A                                      CHECK(AB)
     A                                      DSPATR(HI UL)
     A            S0AUSR    R        O  7 14REFFLD(USRP)
     A                                      DSPATR(HI)
     A            S0USNM    R        O  7 31REFFLD(USER1)
     A                                      DSPATR(HI)
      *
     A          R SF0920C0                  TEXT('Subfile control')
     A                                      SFLCTL(SF0920S0)
     A                                      SFLPAG(15) SFLSIZ(16)
     A                                      BLINK OVERLAY PUTOVR
     A  30                                  SFLDSPCTL
     A  31                                  SFLDSP
     A N30N31                               SFLCLR
     A  32                                  SFLEND
     A N32                                  ROLLUP(39)
     A                                      CA11
     A                                      CA13
     A                                      ERASE(SF0920S0)
     A          H                           HLPARA(05 01 22 80)
     A                                      HLPDOC(H001@020 SFMINS ABSENG/DBAR2)
      *
     A            C0SFRC         4  0H      SFLRCDNBR
     A                                  5 11'Authorised Users'
      *
     A          R SF0920S1                  SFL
     A            S1SEL     R        B 10 10REFFLD(ACTN)
     A                                      VALUES('D')
     A                                      CHECK(AB)
     A  60                                  DSPATR(HI UL)
     A N60                                  DSPATR(PR ND)
     A            S1FRAC        10A  O 10 14
     A                                      DSPATR(HI)
     A            S1TOAC        10A  O 10 26
     A                                      DSPATR(HI)
     A            S1RGDS    R        O 10 37REFFLD(ACKN)
     A                                      DSPATR(HI)
      *
     A          R SF0920C1                  TEXT('Subfile control')
     A                                      SFLCTL(SF0920S1)
     A                                      SFLPAG(12) SFLSIZ(13)
     A                                      BLINK OVERLAY PUTOVR
     A  50                                  SFLDSPCTL
     A  51                                  SFLDSP
     A N50N51                               SFLCLR
     A  52                                  SFLEND
     A N52                                  ROLLUP(59)
     A  60                                  CA09
     A                                      CA12
     A          H                           HLPARA(05 01 22 80)
     A                                      HLPDOC(H001@025 SFMINS ABSENG/DBAR2)
      *
     A            C1SFRC         4  0H      SFLRCDNBR
     A            S0AUSR    R        O  5 14REFFLD(USRP)
     A                                      DSPATR(HI)
     A            S0USNM    R        O  5 31REFFLD(USER1)
     A                                      DSPATR(HI)
     A                                  7 14'Account Range'
     A                                  7 37'Description'
     A                                  8 14'From        To'
      *
     A          R #CMDTXT0                  TEXT('Command keys')
     A                                      OVERLAY
     A                                 23  2'D=Delete  E=Enquire  X=Authorise  -
     A                                      F3=Main Menu  F11=Print  F13=Ranges'
      *
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY
     A N60                             23  2'F3=Main Menu  F12=Previous'
     A  60                             23  2'D=Delete  F3=Main Menu  F9=Add  -
     A                                      F12=Previous'
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
      *
