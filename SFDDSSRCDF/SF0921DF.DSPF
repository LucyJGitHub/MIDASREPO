      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas¦ABS Select Restricted Account Code Ranges')
      *****************************************************************
      *                                                               *
      *  Midas - Security Profile Facility                            *
      *                                                               *
      *  DSPF/SF0921DF - SELECT RESTRICTED ACCOUNT CODE RANGE         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CRE024  *CREATE    Date 07Oct05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRE024 - COUNTRY LEVEL SWITCHABLE FEATURE :                  *
      *           SECURITY FEATURES FOR RESTRICTED ACCOUNTS           *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA12
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      HELP
     A                                      HLPDOC(H001@000 SFMINS ABSENG/DBAR2)
      *
     A          R SF0921S0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
     A                                      SFL
     A            S0SEL     R        I  8 10REFFLD(ACTN)
     A                                      VALUES('1')
     A                                      CHECK(AB)
     A                                      DSPATR(UL HI)
     A            S0FRAC        10A  O  8 14
     A                                      DSPATR(HI)
     A            S0TOAC        10A  O  8 26
     A                                      DSPATR(HI)
     A            S0RGDS    R        O  8 43REFFLD(ACKN)
     A                                      DSPATR(HI)
      *
     A          R SF0921C0                  TEXT('Subfile control')
     A                                      SFLCTL(SF0921S0)
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY PUTOVR
     A  30                                  SFLDSPCTL
     A  31                                  SFLDSP
     A N30N31                               SFLCLR
     A  32                                  SFLEND
     A N32                                  ROLLUP(27)
     A          H                           HLPARA(05 01 22 80)
     A                                      HLPDOC(H001@025 SFMINS ABSENG/DBAR2)
      *
     A            C0SFRC         4  0H      SFLRCDNBR
     A            SUSER     R   10   O  1  2REFFLD(USER)
     A            SPGM      R   10   O  1 13REFFLD(JOPGM)
     A            SRUNA     R    7   O  1 72REFFLD(RUNA)
     A            SWSID     R   10   O  2  2REFFLD(JOJOB)
     A                                  2 32'Select Account Range'
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A                                  5 12'Account Range'
     A                                  5 31'Description'
     A                                  6 14'From   To'
      *
     A          R #CMDTXT0                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  2'1=Select  F12=Previous'
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
      *
