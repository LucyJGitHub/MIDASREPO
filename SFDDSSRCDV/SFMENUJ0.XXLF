      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas SF Zonal menu action codes by unique code')
      *****************************************************************
      *                                                               *
      *  Midas - Security Profile Facility                            *
      *                                                               *
      *  LF/SFMENUJ0 - Zonal Menu Action Codes by Unique Code         *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD056561 *REDUNDANTDate 31Aug20               *
      *  Prev Amend No. MD056948           Date 12Oct20               *
      *                 MD055681           Date 31Jul20               *
      *                 MD046248           Date 27Oct17               *
      *                 BUG16742           Date 08Apr08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 BUG2287            Date 28Apr04               *
      *                 CSF002  *CREATE    Date 11Aug03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD056561 - Deliverable Data Split for SFMENUPD and GPMTXTPD  *
      *  MD056948 - Missing selection when feature is disabled.       *
      *  MD055681 - Deliverable Data Split for SAR                    *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG16742 - Details of the newly created FX Deal are missing  *
      *             in WIP.                                           *
      *  BUG2287- Show consolidated global view (Recompile)           *
      *  CSF002 - Global Processing                                   *
      *           - New file.                                         *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R @MENUJ0                   JFILE(SFMENUPD SCSARIPD +
      * Start of Change MD055681
     A**********                                  SCSARDPD SDMMIDPD)
     A                                            SCSRDXTD SDMMIDPD)
      * End of Change MD055681
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A          J                           JOIN(1 2)
     A                                      JFLD(MIINDX ADINDX)
      *
     A          J                           JOIN(2 3)
     A                                      JFLD(ADSARN SARN)
      *
     A          J                           JOIN(1 4)
     A                                      JFLD(MIMMSN FOMMSN)
      *
     A            MIINDX
     A            MISECL
     A            MIAAC1
     A            MIAAC2
     A            MIAAC3
     A            MIAAC4
     A            MIAAC5
     A            MIAAC6
     A            MIAAC7
     A            MIAAC8
     A            MIAAC9
     A            MIAAC10
     A            MIALLAC                   CONCAT(MIAAC1 MIAAC2 MIAAC3 +
     A                                      MIAAC4 MIAAC5 MIAAC6 MIAAC7 +
     A                                      MIAAC8 MIAAC9 MIAAC10)
     A**Start of change BUG16742
     A            MIEXTP
     A**End of change BUG16742
      *
      * Fields included to allow join to function
      *
     A            ADINDX
     A            ADSARN
     A            ADAVAL
     A            SARN
     A            MIMMSN
     A            FOMMSN
     A            FOMMIN
      ** Start of Change MD055681
     A            ENABLD
      ** End of Change MD055681
      *
      * Key field - Unique Code
      *
     A          K MIINDX
      *
      * Selection
      *
      * Select if no SARI record and module is on and has action codes
     A          S ADSARN                    COMP(EQ ' ')
     A            FOMMIN                    COMP(EQ 'Y')
     A            MIALLAC                   COMP(NE '          ')
      *
      * Select if SARI record and feature present and show if present
      * and module is on and has action codes
     A          S ADSARN                    COMP(NE ' ')
     A            SARN                      COMP(NE ' ')
      ** Start of Change MD055681
     A            ENABLD                    COMP(EQ 'E')
      ** End of Change MD055681
     A            ADAVAL                    COMP(EQ 'Y')
     A            FOMMIN                    COMP(EQ 'Y')
     A            MIALLAC                   COMP(NE '          ')
      *
      * Select if SARI record and feature not present and show if not
      * present and module is on and has action codes
     A          S ADSARN                    COMP(NE ' ')
     A            SARN                      COMP(EQ ' ')
     A            ADAVAL                    COMP(EQ 'N')
     A            FOMMIN                    COMP(EQ 'Y')
     A            MIALLAC                   COMP(NE '          ')
      ** Start of Change MD056948
     A          S ADSARN                    COMP(NE ' ')
     A            SARN                      COMP(NE ' ')
     A            ENABLD                    COMP(EQ 'D')
     A            ADAVAL                    COMP(EQ 'N')
     A            FOMMIN                    COMP(EQ 'Y')
     A            MIALLAC                   COMP(NE '          ')
      ** End of Change MD056948
