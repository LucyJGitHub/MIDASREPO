      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas SF Zonal menu action codes by unique code')
      *****************************************************************
      *                                                               *
      *  Midas - Security Profile Facility                            *
      *                                                               *
      *  LF/SFMENUJ1 - Zonal Menu Action Codes by Unique Code         *
      *                (All Fields)                                   *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG7142            Date 28Jun05               *
      *                 BUG2287            Date 28Apr04               *
      *                 CGP001  *CREATE    Date 23May03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BGU7142 - API names not replicated to global layer           *
      *  BUG2287- Show consolidated global view (Recompile)           *
      *  CGP001 - Global Processing                                   *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R @MENUJ1                   JFILE(SFMENUPD SCSARIPD +
     A                                            SCSARDPD SDMMIDPD)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A          J                           JOIN(1 2)
     A                                      JFLD(MIINDX ADINDX)
      *
     A          J                           JOIN(2 3)
     A                                      JFLD(ADSARN SARN)
      *
     A          J                           JOIN(1 4)
     A                                      JFLD(MIMMSN FOMMSN)
      *
     A            MIZONE
     A            MIINDX
     A            MISECL
     A            MIMODC
     A            MIMMSN
     A            MIEXTP
     A            MIFRAM
     A            MIPATH
     A            MIAAC1
     A            MIAAC2
     A            MIAAC3
     A            MIAAC4
     A            MIAAC5
     A            MIAAC6
     A            MIAAC7
     A            MIAAC8
     A            MIAAC9
     A            MIAAC10
     A            MIACOB
      * Start of change BUG7142
     A            MIAPI1
     A            MIAPI2
     A            MIAPI3
     A            MIAPI4
     A            MIAPI5
      * End of change BUG7142
     A            MIINS
      * Start of change BUG7142
     A            MICON
      * End of change BUG7142
     A            MIALLAC                   CONCAT(MIAAC1 MIAAC2 MIAAC3 +
     A                                      MIAAC4 MIAAC5 MIAAC6 MIAAC7 +
     A                                      MIAAC8 MIAAC9 MIAAC10)
      *
      * Fields included to allow join to function
      *
     A            ADINDX
     A            ADSARN
     A            ADAVAL
     A            SARN
     A            FOMMSN
     A            FOMMIN
      *
      * Key field - Unique Code
      *
     A          K MIINDX
      *
      * Selection
      *
      * Select if no SARI record and module is on and has action codes
     A          S ADSARN                    COMP(EQ ' ')
     A            FOMMIN                    COMP(EQ 'Y')
     A            MIALLAC                   COMP(NE '          ')
      *
      * Select if SARI record and feature present and show if present
      * and module is on and has action codes
     A          S ADSARN                    COMP(NE ' ')
     A            SARN                      COMP(NE ' ')
     A            ADAVAL                    COMP(EQ 'Y')
     A            FOMMIN                    COMP(EQ 'Y')
     A            MIALLAC                   COMP(NE '          ')
      *
      * Select if SARI record and feature not present and show if not
      * present and module is on and has action codes
     A          S ADSARN                    COMP(NE ' ')
     A            SARN                      COMP(EQ ' ')
     A            ADAVAL                    COMP(EQ 'N')
     A            FOMMIN                    COMP(EQ 'Y')
     A            MIALLAC                   COMP(NE '          ')
