      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SF Access rights of Midasplus users audit')
      *****************************************************************
      *                                                               *
      *  Midas - Security Profile Facility                            *
      *                                                               *
      *  PRTF/SF000350AU - MidasPlus Users Access Rights - Audit      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 239898 *CREATE     Date 29May06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  239898 - Applied core fix BUG11032.                          *
      *  BUG11032 - Reporting About Users                             *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R SF000350A0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2006')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SF000350AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    42'MIDAS USERS ACCESS RIGHT+
     A                                       S - AUDIT'
     A                                      SPACEA(1)
      *
     A                                    42'------------------------+
     A                                      ---------'
     A                                      SPACEA(3)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R SF000350A1
     A                                    50'TRAILER'
     A                                    75'CALCULATED TOTAL'
     A                                      SPACEA(1)
     A                                     6'FILE'
     A                                    50'NUMBER'
     A                                    75'NUMBER'
     A                                      SPACEA(1)
     A                                    50'----------'
     A                                    75'----------------'
     A                                      SPACEA(3)
     A                                     6'MIDAS USERS'
     A            TNUMBR         5  0     52EDTCDE(3)
      *
     A            TCALCN         5  0     77EDTCDE(3)
     A                                      SPACEA(1)
      *
     A          R SF000350A2
     A                                    52'*** OUT OF BALANCE ***'
     A                                      SPACEB(3)
      *
      **  No Details
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      ** Database Error
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
