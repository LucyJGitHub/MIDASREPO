      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SF Account codes authorities report')
      *****************************************************************
      *                                                               *
      *  Midas - Security Profile Facility                            *
      *                                                               *
      *  PRTF/SF0031P1 - Account Codes Authorities - Report           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CGL008             Date 01Mar99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL008 - Security on Journal Entry Input                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ***  First heading line
      *
     A          R HEADER1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE SF0031P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
     A          R HEADER2
     A                                    44'ACCOUNT CODES AUTHORITIES+
     A                                       - AUDIT - LIST'
     A                                      SPACEA(001)
     A                                    44'-------------------------+
     A                                      ---------------'
     A                                      SPACEA(001)
     A          R HEADER3
     A                                    44'ACCOUNT CODES AUTHORITIES+
     A                                       - FULL - LIST'
     A                                      SPACEA(001)
     A                                    44'-------------------------+
     A                                      --------------'
     A                                      SPACEA(001)
     A          R HEADER4
     A                                    30'Account Code:'
     A                                      SPACEB(002)
     A**Start of change CGL029
     A**********  RRACOD         4A  O    49                                                  CGL029
     A            RRACOD        10A  O    49
     A**End of change CGL029
     A                                    30'Account Code Name:'
     A                                      SPACEB(001)
     A            RRACCN        25A  O    49
     A                                    40'User Profile'
     A                                      SPACEB(002)
     A                                    60'Name'
     A                                      SPACEA(001)
      *
      *** Print details
      *
     A          R DETAIL1
     A            RRUSRP        10A  O    41
     A                                      SPACEB(001)
     A            RRUSR1        25A  O    60
      *
      ***  Print footer
      *
     A          R FOOTING1
     A                                    57
     A                                      '** END OF REPORT **'
     A                                      SPACEB(004)
