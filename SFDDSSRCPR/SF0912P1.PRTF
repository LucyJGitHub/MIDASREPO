      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas¦ABS Authorised Users by Account Ranges P1')
      *****************************************************************
      *                                                               *
      *  Midas - Security Profile Facility                            *
      *                                                               *
      *  PRTF/SF0912P1 - AUTHORISED USERS BY ACCOUNT RANGES P1        *
      *                                                               *
      *  (C) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CRE024  *CREATE    Date 07Oct05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRE024 - COUNTRY LEVEL SWITCHABLE FEATURE :                  *
      *           SECURITY FEATURES FOR RESTRICTED ACCOUNTS           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      * Heading
     A          R SF0912H1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
      *
     A                                      SKIPB(3)
     A                                     4'REFERENCE SF0912P1'
     A            BJURPT    R             40REFFLD(URPT)
     A                                   109'DATE'
     A            BJMRDT    R            114REFFLD(MRDT)
     A                                   123'PAGE'
     A                                   127PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   109'TIME'
     A                                   114TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    45'AUTHORISED USERS BY RESTRICTED +
     A                                      ACCOUNT RANGES'
     A                                      SPACEA(1)
     A                                    45'-------------------------------+
     A                                      --------------'
     A                                      SPACEA(2)
      * Sub-Heading
     A          R SF0912S1
     A                                     8'ACCOUNT CODE RANGE'
     A                                    43'DESCRIPTION'
     A                                    79'USER'
     A                                    92'NAME'
     A                                      SPACEA(2)
      * Detail
     A          R SF0912D1
      *
     A N30                                  SPACEB(1)
     A N30        R1FRAC        10A       11
     A N30        R1TOAC        10A       24
     A N30        R1RGDS    R             43REFFLD(ACKN)
     A            R1AUSR    R             79REFFLD(USRP)
     A            R1USNM    R             92REFFLD(USER1)
     A                                      SPACEA(1)
      * Trailer
     A          R SF0912T1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
