      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas¦ABS Account Ranges by Authorised Users Audit')
      *****************************************************************
      *                                                               *
      *  Midas - Security Profile Facility                            *
      *                                                               *
      *  PRTF/SF0922AU - ACCOUNT RANGES BY AUTHORISED USERS AUDIT     *
      *                                                               *
      *  (C) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CRE024  *CREATE    Date 07Oct05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRE024 - COUNTRY LEVEL SWITCHABLE FEATURE :                  *
      *           SECURITY FEATURES FOR RESTRICTED ACCOUNTS           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      * Heading
     A          R SF0922HA                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
      *
     A                                      SKIPB(3)
     A                                     4'REFERENCE SF0922AU'
     A            BJURPT    R             40REFFLD(URPT)
     A                                   109'DATE'
     A            BJMRDT    R            114REFFLD(MRDT)
     A                                   123'PAGE'
     A                                   127PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   109'TIME'
     A                                   114TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    41'RESTRICTED ACCOUNT RANGES BY +
     A                                      AUTHORISED USERS - AUDIT'
     A                                      SPACEA(1)
     A                                    41'-----------------------------+
     A                                      ------------------------'
     A                                      SPACEA(2)
      * Control
     A          R SF0922CA
     A                                    50'NUMBER OF RECORDS PRINTED:'
     A            RCOUNT         5  0     80EDTCDE(3)
     A                                      SPACEA(2)
      * Error
     A          R SF0922EA
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      * No Details
     A          R SF0922NA
      *
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
