      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas¦ABS Account Ranges by Authorised Users P1')
      *****************************************************************
      *                                                               *
      *  Midas - Security Profile Facility                            *
      *                                                               *
      *  PRTF/SF0922P1 - ACCOUNT RANGES BY AUTHORISED USERS P1        *
      *                                                               *
      *  (C) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CRE024  *CREATE    Date 07Oct05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRE024 - COUNTRY LEVEL SWITCHABLE FEATURE :                  *
      *           SECURITY FEATURES FOR RESTRICTED ACCOUNTS           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      * Heading
     A          R SF0922H1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
      *
     A                                      SKIPB(3)
     A                                     4'REFERENCE SF0922P1'
     A            BJURPT    R             40REFFLD(URPT)
     A                                   109'DATE'
     A            BJMRDT    R            114REFFLD(MRDT)
     A                                   123'PAGE'
     A                                   127PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   109'TIME'
     A                                   114TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    45'RESTRICTED ACCOUNT RANGES BY +
     A                                      AUTHORISED USERS'
     A                                      SPACEA(1)
     A                                    45'-----------------------------+
     A                                      ----------------'
     A                                      SPACEA(2)
      * Sub-Heading
     A          R SF0922S1
     A                                    11'USER'
     A                                    24'NAME'
     A                                    60'ACCOUNT CODE RANGE'
     A                                    92'DESCRIPTION'
     A                                      SPACEA(2)
      * Detail
     A          R SF0922D1
      *
     A N30                                  SPACEB(1)
     A N30        R1AUSR    R             11REFFLD(USRP)
     A N30        R1USNM    R             24REFFLD(USER1)
     A            R1FRAC        10A       60
     A            R1TOAC        10A       73
     A            R1RGDS    R             92REFFLD(ACKN)
     A                                      SPACEA(1)
      * Error
     A          R SF0922E1
      *
     A                                      SPACEB(1)
     A                                    51'***  DATABASE ERROR  ***'
     A                                      SPACEA(1)
      * Trailer
     A          R SF0922T1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
