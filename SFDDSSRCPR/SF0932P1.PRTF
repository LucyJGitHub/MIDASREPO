      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas¦ABS Restricted Accounts Security Control')
      *****************************************************************
      *                                                               *
      *  Midas - Security Profile Facility                            *
      *                                                               *
      *  PRTF/SF0932P1 - RESTRICTED ACCOUNTS SECURITY CONTROL P1      *
      *                                                               *
      *  (C) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CRE024   *CREATE   Date 07Oct05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRE024 - COUNTRY LEVEL SWITCHABLE FEATURE :                  *
      *           SECURITY FEATURES FOR RESTRICTED ACCOUNTS           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      * Heading
     A          R SF0932H1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
      *
     A                                      SKIPB(3)
     A                                     4'REFERENCE SF0932P1'
     A            BJURPT    R             40REFFLD(URPT)
     A                                   109'DATE'
     A            BJMRDT    R            114REFFLD(MRDT)
     A                                   123'PAGE'
     A                                   127PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A  40                               109'TIME'
     A  40                               114TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    46'RESTRICTED ACCOUNTS SECURITY +
     A                                      CONTROL'
     A                                      SPACEB(1)
     A                                      SPACEA(1)
     A                                    46'-----------------------------+
     A                                      -------'
     A                                      SPACEA(2)
      * Sub-Heading
     A          R SF0932S1
     A                                     2'USER'
     A                                    20'RANGE'
     A                                    41'ACCOUNT'
     A                                    64'MENU OPTION'
     A                                   105'TIME'
     A                                   114'TERMINAL'
     A                                   125'JOB NO'
     A                                      SPACEA(2)
      * Detail
     A          R SF0932D1
      *
     A N30                                  SPACEB(1)
     A N30        R1JUSR    R              2REFFLD(JUSR)
     A N31        R1FRAC        10A       13
     A N31        R1TOAC        10A       24
     A            R1BRCA    R             35REFFLD(BRCA)
     A            R1CNUM    R             39REFFLD(CNUMA)
     A            R1CCY     R             46REFFLD(CCY)
     A            R1ACOD    R             50REFFLD(ACOD)
     A            R1ACSQ    R             61REFFLD(ACSQA)
     A            R1INAR        40A       64
     A            R1TIME    R            105REFFLD(MTIME)
     A                                      EDTWRD('  :  :  ')
     A            R1JNAM    R            114REFFLD(JNAM)
     A            R1JNOM    R            125REFFLD(JNOM)
     A                                      SPACEA(1)
      * Trailer
     A          R SF0932T1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
