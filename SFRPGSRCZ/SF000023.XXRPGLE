     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2003')
      *****************************************************************
/*S*D****RPGBASEBND****************************************************
      *****************************************************************
      *                                                               *
      *  Midas - Security Profile Facility                            *
      *                                                               *
      *  SF000023 - Midas Users Maintenance - Companies               *
      *                                                               *
      *  Function: This program updates the Valid Companies file by   *
      *  I/C       inserting, amending, deleting records.The file can *
      *            also be enquired upon.                             *
      *                                                               *
      *  Called By: SF000020                                          *
      *             SF000200                                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. AR904191*REDUNDANT Date 03Feb12               *
      *  Prev Amend No. AR787620           Date 10Jun11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 BUG10744A (Reopen) Date 23May06               *
      *                 CSF002 *RENAME     Date 11Aug03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 LN1187             Date 07Mar91               *
      *                 S01199             Date 18Sep89               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR904191 - Replaced as per BF requirement (CBF005)           *
      *  AR787620 - Hooks Incorporation                               *
      *             WNCPYSRC,SFH00160                                 *
      *             WNCPYSRC,SFH00161                                 *
      *             WNCPYSRC,SFH00162                                 *
      *             WNCPYSRC,SFH00163                                 *
      *             WNCPYSRC,SFH00164                                 *
      *             WNCPYSRC,SFH00165                                 *
      *             WNCPYSRC,SFH00166                                 *
      *             WNCPYSRC,SFH00167                                 *
      *             WNCPYSRC,SFH00168                                 *
      *             WNCPYSRC,SFH00169                                 *
      *             WNCPYSRC,SFH00170                                 *
      *             WNCPYSRC,SFH00171                                 *
      *             WNCPYSRC,SFH00172                                 *
      *  BUG10744A - Files for update should be for commitment to     *
      *              avoid locking in User Maintenance. (Reopen)      *
      *  CSF002 - Global Processing                                   *
      *           SF0023 has been converted to ILE and renamed.       *
      *           Commented out program codes have been stripped out  *
      *           for clarity.                                        *
      *  LN1187 - WHEN REINSERTING USERS, RESTORE PREVIOUS DETAILS    *
      *  S01199 -  HELP SYSTEM.                                       *
      *                                                               *
      *****************************************************************
     FSF000023DFCF   E             WORKSTN
     F                                     SFILE(SF000023F1:RRN)
     FSDCOMPL1  IF   E           K DISK
     F***MCMPS**   UF A E           K DISK                                                 BUG10744A
     FMCMPS     UF A E           K DISK    COMMIT                                          BUG10744A
     F/COPY WNCPYSRC,SFH00160
      *
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *****************************************************************
      *                                                               *
      *    10         SUBFILE DISPLAY                                 *
      *    11         SUBFILE END                                     *
      *    12         SFLNXTCHG                                       *
      *                                                               *
      *    20         DISPLAY SCREEN                                  *
      *                                                               *
      *    21         INSERT                                          *
      *    22         AMEND                                           *
      *    23         ENQUIRE                                         *
      *    24         DELETE                                          *
      *                                                               *
      *    26         ENQUIRY ON A DELETED RECORD                     *
      *                                                               *
      *    30         GENERAL ERROR INDICATOR                         *
      *    31         SELECT IN ERROR - NOT 'Y' OR BLANK              *
      *                                                               *
      *    40         MCMPS - CHAIN FAIL                              *
      *    41         MCMPS - RECORD NOT LIVE (IE. RECICM NOT 'D')    *
      *                                                               *
      *    50         SDCOMPL1/COMPANY CODES - END OF FILE            *
      *    52         CO. CODE IS ON USER'S CO. CODES ARRAY           *
      *                                                               *
      *    60         NO MORE 'READ CHANGED' RECORDS ON SUBFILE       *
      *                                                               *
      *****************************************************************
     D CMU             S              3    DIM(100)
      *
      ** HOLDS DISPLAY SCREEN ERROR CODES
      *
     D ARRDS           DS
     D  SERCD                  1     78
     D  ARR                    1     76
     D                                     DIM(19)
      ** HOLDS WORKSTATION ID
      *
     D PSDS           SDS
     D  SWSID                244    253
     D  USRID                254    263
     I/SPACE 2
     IMCMPSDDF
      ** USER'S COMPANIES
      *
     I              RECI                        RECICM
     I/COPY WNCPYSRC,SFH00161
      /EJECT
      *
      ** RECEIVE USER PROFILE, ACTION CODE, RETURN CODE, UPDATE
      ** AND  RUN-DATE(2)  PARAMETERS FROM CALLING PROGRAM (SF0020).
      *
     C     *ENTRY        PLIST
     C                   PARM                    USRPRF           10
     C                   PARM                    ACTCDE            1
     C                   PARM                    RTCDE             1 0
     C                   PARM                    UPDATE            1
     C                   PARM                    SRUNA             7
     C                   PARM                    RUNDD             5 0
     C                   PARM                    SZONE            10
      /SPACE 2
      *********************************************************************
      * MAIN PROCEDURE                                                    *
      *********************************************************************
     C/COPY WNCPYSRC,SFH00162
      *
      ** DETERMINE ACTION CODE
      *
     C     ACTCDE        COMP      'I'                                    21
     C     ACTCDE        COMP      'A'                                    22
     C     ACTCDE        COMP      'E'                                    23
     C     ACTCDE        COMP      'D'                                    24
      *
      ** SETUP USER PROFILE'S ARRAY OF VALID COMPANY CODES
      *
     C/COPY WNCPYSRC,SFH00163
     C     USRPRF        CHAIN     MCMPS                              40
     C  N40RECICM        COMP      'D'                                4141
     C/COPY WNCPYSRC,SFH00164
      *
     C     *IN40         IFEQ      '0'
     C     *IN41         ANDEQ     '0'
     C                   MOVEA     VCMP          CMU
     C                   END
      *
      ** ENQUIRY ON A DELETED RECORD
      *
     C     *IN23         IFEQ      '1'
     C     RECICM        ANDEQ     'C'
     C                   SETON                                        26
     C                   MOVEA     VCMP          CMU
     C                   END
      *
      ** SETUP SUBFILE CONTROL AND HEADING FIELDS
      *
     C                   Z-ADD     0             RRN               3 0
     C                   Z-ADD     1             SDREC
     C                   MOVE      USRPRF        SUSER
     C                   MOVE      ACTCDE        SACTN
      *
      ** SET ON SUBFILE AND DISPLAY SCREEN INDICATORS
      *
     C                   SETON                                        1011
     C                   SETON                                        20
     C/COPY WNCPYSRC,SFH00165
      *
      ** LOAD SUBFILE
      *
     C                   EXSR      LODSBF
     C/EJECT
      *
      ** DISPLAY SCREEN :-
      *
     C     *IN20         DOWEQ     '1'
      *
      ** - HEADINGS
      *
     C                   WRITE     SF000023F3
      *
      ** - SUBFILE
      *
     C                   EXFMT     SF000023F2
      *
      ** RESET INDICATORS
      *
     C                   SETOFF                                       2030
      *
      ** CHECK CMD/KEY PRESSED + ACTION CODE ON EXITING DISPLAY
      *
     C     *INKC         CASEQ     '1'           EXIT
     C     *INKL         CASEQ     '1'           INITSC
     C     *IN10         CASEQ     '0'           ENDPGM
      *
     C     ACTCDE        CASEQ     'I'           INSAMD
     C     ACTCDE        CASEQ     'A'           INSAMD
     C     ACTCDE        CASEQ     'D'           DELETE
     C                   END
      *
     C                   END
     C/SPACE 2
      *
      ** END PROGRAM
      *
     C                   EXSR      ENDPGM
      **********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * LODSBF - LOAD SUBFILE WITH COMPANY CODE RECORDS.              *
      *                                                               *
      *****************************************************************
     C     LODSBF        BEGSR
      *
     C                   MOVE      *BLANK        COKEY             3
     C     COKEY         SETLL     SDCOMPL1
      *
     C                   READ      SDCOMPL1                               50
      *
     C     *IN50         DOWEQ     '0'
      *
     C                   ADD       1             RRN
      *
     C                   MOVE      APCMCD        SCMPY
     C                   MOVE      APCMNM        SCMNM
      *
     C     APCMCD        LOOKUP    CMU                                    52
      *
     C     *IN52         IFEQ      '1'
     C                   SETON                                        12
     C                   MOVE      'Y'           SSLCT
     C                   ELSE
     C                   MOVE      ' '           SSLCT
     C                   END
      *
     C                   WRITE     SF000023F1
      *
     C                   SETOFF                                       12
      *
     C                   READ      SDCOMPL1                               50
      *
     C                   END
      *
      ** NO RECORDS IN SUBFILE
      *
     C     RRN           IFEQ      0
     C                   SETOFF                                       10
     C                   END
      *
     C                   ENDSR
      **********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * INSAMD - TO HANDLE INSERTS + AMENDMENTS                       *
      *                                                               *
      *****************************************************************
     C     INSAMD        BEGSR
      *
      ** RESET ARRAYS AND SUBFILE CONTROL FIELDS
      *
     C                   Z-ADD     0             X                 2 0
     C                   MOVE      *BLANK        ARR
     C                   Z-ADD     0             N                 3 0
     C                   MOVE      *BLANK        CMU
      *
     C                   Z-ADD     0             RRN
     C                   Z-ADD     1             SDREC
      *
      ** VALIDATE SELECTION ENTRIES :-
      *
     C     *IN60         DOUEQ     '1'
      *
     C                   SETOFF                                       31
     C                   SETON                                        12
      *
     C                   READC     SF000023F1                             60
      *
     C     *IN60         IFEQ      '0'
      *
      ** - INVALID ENTRY
      *
     C     SSLCT         IFNE      ' '
     C     SSLCT         ANDNE     'Y'
      *
     C                   SETON                                        31
      *
     C     *IN30         IFEQ      '0'
     C     X             ADD       1             X
     C                   MOVE      '050'         ARR(X)
      *
     C                   Z-ADD     RRN           SDREC
     C                   SETON                                        30
     C                   END
      *
     C                   ELSE
      *
      ** - VALID ENTRY
      *
     C     SSLCT         IFEQ      'Y'
     C                   ADD       1             N
     C                   MOVE      SCMPY         CMU(N)
     C                   ELSE
     C                   SETOFF                                       12
     C                   END
      *
     C                   END
      *
     C                   UPDATE    SF000023F1
      *
     C                   END
      *
     C                   END
      *
      ** IF ANY ERRORS, SET ON SCREEN INDICATOR
      *
     C     *IN30         IFEQ      '1'
      *
     C                   SETON                                        20
      *
     C                   ELSE
      *
      ** IF VALID, UPDATE FILES :-
      *
     C                   SORTA     CMU
     C                   MOVEA     CMU           VCMP
     C                   MOVE      N             NOOC
     C                   Z-ADD     RUNDD         LCD
     C                   MOVE      ACTCDE        CHTP
     C                   TIME                    TIMEP             6 0
     C                   Z-ADD     TIMEP         LCT
      *
      ** UPDATE MCMPS (USER'S VALID COMPANIES)
      *
     C     *IN40         IFEQ      '1'
      *
     C     NOOC          IFNE      0
      *
      ** - INSERT (NEW)
      *
     C                   MOVE      'D'           RECICM
     C                   MOVE      USRPRF        USRP
     C/COPY WNCPYSRC,SFH00166
     C                   WRITE     MCMPSDDF
     C                   MOVE      'Y'           UPDATE
     C/COPY WNCPYSRC,SFH00167
     C                   END
      *
     C                   ELSE
      *
     C/COPY WNCPYSRC,SFH00168
     C                   UPDATE    MCMPSDDF
     C                   MOVE      'Y'           UPDATE
     C/COPY WNCPYSRC,SFH00169
     C                   END
      *
     C                   END
      *
     C                   ENDSR
      **********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * DELETE - TO HANDLE DELETIONS                                  *
      *                                                               *
      *****************************************************************
     C     DELETE        BEGSR
      *
      ** CMD/10 PRESSED - UPDATE MCMPS RECORD TO 'NON-LIVE'
      *
     C     *INKJ         IFEQ      '1'
      *
     C     *IN40         IFEQ      '0'
     C     *IN41         ANDEQ     '0'
     C/COPY WNCPYSRC,SFH00170
     C                   DELETE    MCMPSDDF
     C                   MOVE      'Y'           UPDATE
     C/COPY WNCPYSRC,SFH00171
     C                   END
      *
     C                   ELSE
      *
      ** 'ENTER' PRESSED - REDISPLAY SCREEN
      *
     C                   SETON                                        20
      *
     C                   END
      *
     C                   ENDSR
      **********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * EXIT   - CMD/3 TAKEN TO EXIT  "MIDAS USER'S MAINTENACE"       *
      *                                                               *
      *****************************************************************
     C     EXIT          BEGSR
      *
     C                   Z-ADD     1             RTCDE
     C                   SETON                                        LR
     C                   RETURN
      *
     C                   ENDSR
      **********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * INITSC - CMD/12 TAKEN TO DISPLAY INITIAL SCREEN               *
      *                                                               *
      *****************************************************************
     C     INITSC        BEGSR
      *
     C                   Z-ADD     5             RTCDE
     C                   SETON                                        LR
     C                   RETURN
      *
     C                   ENDSR
      **********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * ENDPGM - RETURNS TO CALLING PROGRAM                           *
      *                                                               *
      *****************************************************************
     C     ENDPGM        BEGSR
      *
     C                   Z-ADD     0             RTCDE
     C                   SETON                                        LR
     C                   RETURN
      *
     C                   ENDSR
      *
     C/COPY WNCPYSRC,SFH00172
