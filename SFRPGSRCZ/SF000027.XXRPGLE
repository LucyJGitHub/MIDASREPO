     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2003')
      *****************************************************************
/*S*D****RPGBASEBND****************************************************
      *****************************************************************
      *                                                               *
      *  Midas - Security Profile Facility                            *
      *                                                               *
      *  SF000027 - Midas Users Maintenance                           *
      *             - Activate All User's details                     *
      *                                                               *
      *  Function: This program reactivates all User's details:       *
      *  I/C       User Details, Valid Booking Branches,              *
      *            Valid Companies, Valid Originating Branches and    *
      *            Valid Action Codes files and Midas SF Users        *
      *            Trailer file.                                      *
      *                                                               *
      *  Called By: SF000020                                          *
      *  Calls: None                                                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. AR904191*REDUNDANT Date 01Feb12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. 249659             Date 10Aug07               *
      *                 CRE026             Date 24May06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 239898             Date 29May06               *
      *                 TDA035             Date 02Apr04               *
      *                 CSF002 *CREATE     Date 11Sep03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR904191 - Replaced as per BF requirement (CBF005)           *
      *  249659 - Applied fix 249427.                                 *
      *  249427 - Add 'COMMIT' on f-specs of files MUSERZZ and MUSER  *
      *           to avoid commitment control error.                  *
      *  CRE026 - Consumer Banking (Recompile)                        *
      *  239898 - Applied core fix BUG11032.                          *
      *  BUG11032 - Reporting About Users (Recompile)                 *
      *  TDA035 - RTS Signon changes for MidasPlus. (Recompile)       *
      *  CSF002 - Global Processing                                   *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Use of indicators.                                           *
      *                                                               *
      *    20         MUSER - CHAIN FAIL OR RECORD NOT LIVE           *
      *    21         MBRBS - CHAIN FAIL OR RECORD NOT LIVE           *
      *    22         MCMPS - CHAIN FAIL OR RECORD NOT LIVE           *
      *    23         MBROS - CHAIN FAIL OR RECORD NOT LIVE           *
      *    24         MACBR1 - END OF FILE FOR USER PROFILE           *
      *    25         MACBR1 - RECORD NOT LIVE                        *
      *                                                               *
      *****************************************************************
 
      ** +--------------------------------------+
      ** ¦ F-specs                              ¦
      ** ¦ =======                              ¦
      ** +--------------------------------------+
 
     FMUSER     UF   E           K DISK    INFSR(*PSSR)
     F                                     COMMIT                                             249427
      ** Midas SF Midas users by user
 
     FMBRBS     UF   E           K DISK    INFSR(*PSSR)
      ** Midas SF Valid booking branches by user
 
     FMCMPS     UF   E           K DISK    INFSR(*PSSR)
      ** Midas SF Valid Companies by User
 
     FMBROS     UF   E           K DISK    INFSR(*PSSR)
      ** Midas SF Valid originating branches by user
 
     FMACBR1    UF   E           K DISK    INFSR(*PSSR)
      ** Midas SF Valid action codes by user/brch/Uniq code
 
     FMUSERZZ   UF A E             DISK    INFSR(*PSSR)
     F                                     COMMIT                                             249427
      ** Midas SF Users Trailer
 
      ** +--------------------------------------+
      ** ¦ Declared variables                   ¦
      ** ¦ ==================                   ¦
      ** +--------------------------------------+
 
      ** *Entry Parameters
     D USRPRF          S             10
     D RUNDD           S              5  0
     D MBRIND          S              1
     D OBRIND          S              1
 
      ** Key Fields
     D USRPK           S             10
     D BRCAK           S              3
     D INDXK           S             10
 
      ** Work Variable
     D WRun            S              1
 
      ** +--- Start of Main processing -----------------------------------+
      ** ¦                                                                ¦
      ** ¦ Initial processing is performed automatically: the *INZSR is   ¦
      ** ¦ executed at program activation.                                ¦
      ** ¦                                                                ¦
      ** +----------------------------------------------------------------+
 
      ** Midas User Details
 
     C                   EXSR      SrMuser
 
      ** Process Booking Branches, Originating Branches and Companies if
      ** MULTIBRANCH IND. is on.
 
     C     MBRIND        IFEQ      'Y'
 
      ** USER'S BOOKING BRANCHES
 
     C                   EXSR      SrMbrbs
 
      ** ONLY PROCESS ORIGINATING BRANCHES IF ORIGINATING BR. IND IS ON
 
     C     OBRIND        IFEQ      'Y'
 
      ** USER'S ORIGINATING BRANCHES
 
     C                   EXSR      SrMbros
 
     C                   ENDIF
 
      ** USER'S COMPANIES
 
     C                   EXSR      SrMcmps
 
     C                   ENDIF
 
      ** USER'S ACTION CODES
 
     C                   EXSR      SrMacbr1
 
      ** Midas SF Users Trailer
 
     C                   EXSR      SrMuserzz
 
      ** Should always update trailer file at Reactivation of User.
 
     C                   EVAL      *INLR = *ON
      *****************************************************************
      *                                                               *
      *  SrMuser - Process the MUSERDD files                          *
      *                                                               *
      *****************************************************************
     C     SrMuser       BEGSR
 
     C     USRPRF        CHAIN     MUSER                              20
 
     C                   IF        *IN20 = *OFF
     C                   MOVE      'D'           RECI
     C                   Z-ADD     RUNDD         LCD
     C                   MOVE      'R'           CHTP
     C                   Z-ADD     TIMEP         LCT
     C                   UPDATE    MUSERDDF
     C                   ENDIF
 
     C                   ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SrMbrbs - Process the MBRBSDD files                          *
      *                                                               *
      *****************************************************************
     C     SrMbrbs       BEGSR
 
     C     USRPK         CHAIN     MBRBS                              21
 
     C                   IF        *IN21 = *OFF
     C                   MOVE      'D'           RECI
     C                   Z-ADD     RUNDD         LCD
     C                   MOVE      'R'           CHTP
     C                   Z-ADD     TIMEP         LCT
     C                   UPDATE    MBRBSDDF
     C                   ENDIF
 
     C                   ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SrMbros - Process the MBROSDD files                          *
      *                                                               *
      *****************************************************************
     C     SrMbros       BEGSR
 
     C     USRPK         CHAIN     MBROS                              23
 
     C                   IF        *IN23 = *OFF
     C                   MOVE      'D'           RECI
     C                   Z-ADD     RUNDD         LCD
     C                   MOVE      'R'           CHTP
     C                   Z-ADD     TIMEP         LCT
     C                   UPDATE    MBROSDDF
     C                   ENDIF
 
     C                   ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SrMcmps - Process the MCMPSDD files                          *
      *                                                               *
      *****************************************************************
     C     SrMcmps       BEGSR
 
     C     USRPK         CHAIN     MCMPS                              22
 
     C                   IF        *IN22 = *OFF
     C                   MOVE      'D'           RECI
     C                   Z-ADD     RUNDD         LCD
     C                   MOVE      'R'           CHTP
     C                   Z-ADD     TIMEP         LCT
     C                   UPDATE    MCMPSDDF
     C                   ENDIF
 
     C                   ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SrMacbr1 - Process the MACBRDD file                          *
      *                                                               *
      *****************************************************************
     C     SrMacbr1      BEGSR
 
     C     ACBRKY        SETLL     MACBR1
 
     C     *IN24         DOWEQ     '0'
 
     C     READ          TAG
     C                   SETOFF                                       25
 
     C                   READ      MACBR1                                 24
     C  N24USRP          COMP      USRPK                              2424
     C  N24RECI          COMP      'C'                                2525
 
     C                   IF        *IN24 = *OFF
 
     C                   IF        *IN25 = *OFF
     C                   MOVE      'D'           RECI
     C                   Z-ADD     RUNDD         LCD
     C                   MOVE      'R'           CHTP
     C                   Z-ADD     TIMEP         LCT
     C                   UPDATE    MACBRDDF
     C                   ELSE
     C                   GOTO      READ
     C                   ENDIF
      *
     C                   ENDIF
      *
     C                   ENDDO
 
     C                   ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SrMuserzz - Process the MUSERZZ file                         *
      *                                                               *
      *****************************************************************
     C     SrMuserzz     BEGSR
 
     C     1             CHAIN     MUSERZZF                           26
 
     C                   IF        *IN26 = *ON
     C                   MOVE      'Z'           RECI
     C                   Z-ADD     1             NORE
     C                   WRITE     MUSERZZF
     C                   ELSE
     C                   ADD       1             NORE
     C                   UPDATE    MUSERZZF
     C                   ENDIF
 
     C                   ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *INZSR - Program Initialisation routine                       *
      *                                                               *
      * Called by: Implicitly on program activation                   *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     *INZSR        BEGSR
 
      ** RECEIVE USER PROFILE, RUN-DATE, MULTIBR. AND ORIGBR.
      ** PARAMETERS FROM CALLING PROGRAM. (SF000020)
 
     C     *ENTRY        PLIST
     C                   PARM                    USRPRF
     C                   PARM                    RUNDD
     C                   PARM                    MBRIND
     C                   PARM                    OBRIND
 
      ** KEY TO ACCESS LF/MACBR1 (USER'S VALID ACTION CODES BY BRANCH)
 
     C     ACBRKY        KLIST
     C                   KFLD                    USRPK
     C                   KFLD                    BRCAK
     C                   KFLD                    INDXK
 
      ** Setup the Key fields.
 
     C                   MOVE      USRPRF        USRPK
     C                   MOVE      '   '         BRCAK
     C                   MOVE      *BLANKS       INDXK
 
      ** Setup the time.
 
     C                   TIME                    TIMEP             6 0
 
     C                   ENDSR
      *****************************************************************
      /EJECT
      *********************************************************************
      *                                                                   *
      * *PSSR  - Program exception error routine                          *
      *          Called automatically if a program error occurs,          *
      *          or directly by the program code using EXSR.              *
      *          This subroutine DUMPs the program just once.             *
      *                                                                   *
      *********************************************************************
     C     *PSSR         BEGSR
 
     C                   DUMP
 
     C                   IF        WRUN = *BLANK
     C                   EVAL      WRUN = 'Y'
 
     C                   CALL      'DBERRCTL'
 
     C                   ENDIF
 
     C                   EVAL      *INU7 = *ON
     C                   EVAL      *INU8 = *ON
     C                   EVAL      *INLR = *ON
 
     C                   RETURN
 
     C                   ENDSR
      ********************************************************************
