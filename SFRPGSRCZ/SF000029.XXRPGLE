     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2003')
      *****************************************************************
/*S*D****RPGBASEBND****************************************************
      *****************************************************************
      *                                                               *
      *  Midas - Security Profile Facility                            *
      *                                                               *
      *  SF000029 - Midas Users Maintenance - Updates MUSER Flags     *
      *                                                               *
      *  Function: This program checks to see whether the user is     *
      *  I/C       authorised to any Booking Branches, Originating    *
      *            Branches, Companies or action Codes.               *
      *                                                               *
      *  Called By: SF000020                                          *
      *  Calls: None                                                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. AR904191*REDUNDANT Date 01Feb12               *
      *  Prev Amend No. AR787620           Date 10Jun11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSF002 *RENAME     Date 11Aug03               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 197434             Date 09Oct01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR904191 - Replaced as per BF requirement (CBF005)           *
      *  AR787620 - Hooks Incorporation                               *
      *             WNCPYSRC,SFH00234                                 *
      *             WNCPYSRC,SFH00235                                 *
      *  CSF002 - Global Processing                                   *
      *           SF0029 has been converted to ILE and renamed.       *
      *           Commented out program codes have been stripped out  *
      *           for clarity.                                        *
      *  197434 - Array index error on SF0021. Recompile only.        *
      *                                                               *
      *****************************************************************
     F/COPY WNCPYSRC,SFH00234
     FMBRBS     IF   E           K DISK
     FMCMPS     IF   E           K DISK
     FMBROS     IF   E           K DISK
     FMACBR1    IF   E           K DISK
     F/COPY WNCPYSRC,SFH00235
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   20  - MDAFS - CHAIN FAIL OR RECORD NOT LIVE                 *
      *   21  - MBRBS - CHAIN FAIL OR RECORD NOT LIVE                 *
      *   22  - MCMPS - CHAIN FAIL OR RECORD NOT LIVE                 *
      *   23  - MBROS - CHAIN FAIL OR RECORD NOT LIVE                 *
      *   24  - MACBR1 - END OF FILE FOR USER PROFILE                 *
      *   25  - MACBR1 - RECORD NOT LIVE                              *
      *                                                               *
      *****************************************************************
      /EJECT
      *
      ** RECEIVE USER PROFILE, MULTIBR. , ORIGBR. AND 'FLAG'
      ** PARAMETERS FROM CALLING PROGRAM. (SF000020)
      *
     C     *ENTRY        PLIST
     C                   PARM                    USRPRF           10
     C                   PARM                    MBRIND            1
     C                   PARM                    OBRIND            1
     C                   PARM                    DAF               1
     C                   PARM                    BRB               1
     C                   PARM                    BRO               1
     C                   PARM                    CMP               1
     C                   PARM                    ACD               1
     C/SPACE 2
      *********************************************************************
      * MAIN PROCEDURE                                                    *
      *********************************************************************
     C/SPACE 2
      *
      ** KEY TO ACCESS LF/MACBR1 (USER'S VALID ACTION CODES BY BRANCH)
      *
     C     ACBRKY        KLIST
     C                   KFLD                    USRPK            10
     C                   KFLD                    BRCAK             3
     C                   KFLD                    INDXK            10
      /SPACE 2
     C                   MOVE      USRPRF        USRPK
     C                   MOVE      '   '         BRCAK
     C                   MOVE      *BLANKS       INDXK
     C/SPACE 2
      *
      ** USER'S DAF FILES
      *
      /SPACE 2
      *
      ** ONLY CHECK BOOKING BRANCHES, ORIGINATING BRANCHES AND
      ** COMPANIES IF MULTIBRANCH IND. IS ON
      *
     C     MBRIND        IFEQ      'Y'
      *
      ** USER'S BOOKING BRANCHES
      *
     C     USRPK         CHAIN     MBRBS                              21
     C  N21RECI          COMP      'D'                                2121
      *
     C     *IN21         IFEQ      '0'
      *
     C     NOBR          IFEQ      0
     C                   MOVE      'N'           BRB
     C                   ELSE
     C                   MOVE      'Y'           BRB
     C                   END
      *
     C                   ELSE
     C                   MOVE      'N'           BRB
      *
     C                   END
      *
      ** ONLY CHECK ORIGINATING BRANCHES IF ORIGINATING BR. IND IS ON
      *
     C     OBRIND        IFEQ      'Y'
      *
      ** USER'S ORIGINATING BRANCHES
      *
     C     USRPK         CHAIN     MBROS                              23
     C  N23RECI          COMP      'D'                                2323
      *
     C     *IN23         IFEQ      '0'
      *
     C     NOBR          IFEQ      0
     C                   MOVE      'N'           BRO
     C                   ELSE
     C                   MOVE      'Y'           BRO
     C                   END
      *
     C                   ELSE
     C                   MOVE      'N'           BRO
      *
     C                   END
      *
     C                   END
      *
      ** USER'S COMPANIES
      *
     C     USRPK         CHAIN     MCMPS                              22
     C  N22RECI          COMP      'D'                                2222
      *
     C     *IN22         IFEQ      '0'
      *
     C     NOOC          IFEQ      0
     C                   MOVE      'N'           CMP
     C                   ELSE
     C                   MOVE      'Y'           CMP
     C                   END
      *
     C                   ELSE
     C                   MOVE      'N'           CMP
      *
     C                   END
      *
     C                   END
     C/SPACE 2
      *
      ** USER'S ACTION CODES
      *
     C     ACBRKY        SETLL     MACBR1
      *
     C     READ          TAG
     C                   SETOFF                                       25
      *
     C                   READ      MACBR1                                 24
     C  N24USRP          COMP      USRPK                              2424
     C  N24RECI          COMP      'D'                                2525
      *
     C     *IN24         IFEQ      '0'
      *
     C     *IN25         IFEQ      '0'
     C                   MOVE      'Y'           ACD
     C                   ELSE
     C                   GOTO      READ
     C                   END
      *
     C                   ELSE
     C                   MOVE      'N'           ACD
     C                   END
      *
     C                   SETON                                        LR
      *
