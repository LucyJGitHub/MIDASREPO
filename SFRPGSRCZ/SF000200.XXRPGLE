     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2003')
      *****************************************************************
/*S*D****RPGBASEBND****************************************************
      *****************************************************************
      *                                                               *
      *  Midas - Security Profile Facility                            *
      *                                                               *
      *  SF000200 - SPF Enquiries - Menu                              *
      *                                                               *
      *  Function:  This program displays a list of the branches,     *
      *  I/C        companies and originating branches that the user  *
      *             is authorised to.                                 *
      *                                                               *
      *  Called By: SF000205                                          *
      *  Calls:     SF000021                                          *
      *             SF000022                                          *
      *             SF000023                                          *
      *             SF000024                                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. AR904191*REDUNDANT Date 03Feb12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. CRE026             Date 24May06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 TDA035             Date 02Apr04               *
      *                 CSF002 *RENAME     Date 11Aug03               *
      * Midas Release 4.01 -------------------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAAA01             Date 08Mar95               *
      *                 S01408             Date 06Mar95               *
      *                 S01117             Date 25Mar92               *
      *                 S01269             Date 24Jun91               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR904191 - Replaced as per BF requirement (CBF005)           *
      *  CRE026 - Consumer Banking (Recompile)                        *
      *  TDA035 - RTS Signon changes for MidasPlus. (Recompile)       *
      *  CSF002 - Global Processing                                   *
      *           SF0200 has been converted to ILE and renamed.       *
      *           Commented out program codes have been stripped out  *
      *           for clarity.                                        *
      *  CAAA01 - GUI/400 development                                 *
      *         - Add GUI user indicator to SF0024 call.              *
      *  S01408 - Core hooks added - SF0200CCPG                       *
      *                            - SF0200CVSF                       *
      *                            - SF0200CCP1                       *
      *  S01117 - RECOMPILE FOR FT CHANGE TO MUSERDD                  *
      *  S01269 - RECOMPILE OVER CHANGED LF/MUSER                     *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Use of indicators.                                           *
      *                                                               *
      *    01         END OF PROGRAMME - EXIT                         *
      *    02         PREVIOUS SCREEN                                 *
      *    03         OPTION INVALID                                  *
      *    04         MULTIBRANCHED ENVIRONMENT                       *
      *    05         END OF PROGRAMME - RETURN TO CALLING PROGRAMME  *
      *    99         CHAIN FAIL                                      *
      *                                                               *
      *****************************************************************
     FSF000200DFCF   E             WORKSTN
     FMUSER     IF   E           K DISK
      *
     D PSDS           SDS
      *
      **     Program status data structure.
     D  WSID                 244    246
     D  WSID2                245    246
     D  SWSID                244    253
     D  USRID                254    263
      *
      ** Local data area.
     D LDA             DS           256
     D  DBFILE               134    141
     D  DBKEY                142    170
     D  DBPGM                171    180
     D  DBASE                181    183  0
      *
      ** DATA STRUCTURES FOR USE WITH ACCESS OBJECTS
     D SDBANK        E DS                  EXTNAME(SDBANKPD)
      *
     D SDMMOD        E DS                  EXTNAME(SDMMODPD)
      *
     D DSFDY         E DS                  EXTNAME(DSFDY)
      ** FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE
      *
     C     *ENTRY        PLIST
     C                   PARM                    USER             10
     C                   PARM                    RTCD              1
     C                   PARM                    SZONE            10
      *
      ** OBTAIN USER DETAILS
      *
     C     USER          CHAIN     MUSERDDF                           99
      *
     C     *IN99         IFEQ      '1'
     C                   MOVE      'SF000200'    DBPGM
     C                   MOVE      'MUSER   '    DBFILE
     C                   Z-ADD     01            DBASE
     C                   RETURN
     C                   END
      *
      ** CHECK RUN DATE AND MULTIBRANICHING INDICATOR
      *
     C                   CALL      'AOBANKR0'
     C                   PARM      '*DBERR'      @RTCD             7
     C                   PARM      '*FIRST  '    @OPTN             7
     C     SDBANK        PARM      SDBANK        DSFDY
      *
     C                   CALL      'AOMMODR0'
     C                   PARM      '*DBERR'      @RTCD
     C                   PARM      '*FIRST  '    @OPTN
     C     SDMMOD        PARM      SDMMOD        DSFDY
      *
     C                   MOVE      BJMRDT        RUND              7
     C                   MOVE      BGMBIN        MBIN              1
      *
      ** IF MULTIBRANCHED ENVIRONMENT DISPLAY EXTRA SCREEN ITEMS
      *
     C     MBIN          IFEQ      'Y'
     C                   SETON                                        04
     C                   END
      *
      ** INITIALISE CALLING PARAMETERS FOR CALLING SF00002X PROGRAMMES
      *
     C                   MOVE      'E'           AUX1              1
     C                   Z-ADD     0             AUX2              1 0
     C                   MOVE      'N'           AUX3              1
     C                   Z-ADD     0             AUX4              5 0
      *
      ** DO WHILE END, INITIAL SCREEN OR ENTER KEYS ARE NOT PRESSED
      *
     C     *IN01         DOWEQ     '0'
     C     *IN02         ANDEQ     '0'
     C     *IN05         ANDEQ     '0'
      *
     C                   MOVE      ' '           OPTION
     C                   EXFMT     MENUFMT
      *
      ** IF ENTER IS PRESSED SET ON 05 TO RETURN TO CALLING PROG/SCREEN
      *
     C     OPTION        IFEQ      ' '
     C                   SETON                                        05
     C                   END
      *
      ** IF NOT MULTIBRANCHING, ONLY 0 AND 1 ARE ALLOWED AS OPTIONS
      ** OTHERWISE SET ON ERROR INDICATOR
      *
     C                   SETOFF                                       03
      *
     C     MBIN          IFNE      'Y'
     C     OPTION        IFNE      ' '
     C     OPTION        ANDNE     '1'
     C                   SETON                                        03
     C                   END
      *
     C                   ELSE
      *
      ** IF MULTIBRANCHING, 0, 1, 2, 3, AND 4 ARE ALLOWED AS OPTIONS
      ** OTHERWISE SET ON ERROR INDICATOR
      *
     C     OPTION        IFNE      ' '
     C     OPTION        ANDNE     '1'
     C     OPTION        ANDNE     '2'
     C     OPTION        ANDNE     '3'
     C     OPTION        ANDNE     '4'
     C                   SETON                                        03
     C                   END
     C                   END
      *
      ** IF OPTION 2 IS CHOSEN CALL BOOKING BRANCH PROGRAMME
      *
     C     OPTION        IFEQ      '2'
     C                   CALL      'SF000021'
     C                   PARM                    USER
     C                   PARM                    AUX1
     C                   PARM                    AUX2
     C                   PARM                    AUX3
     C                   PARM                    RUND
     C                   PARM                    AUX4
     C                   PARM                    SZONE
     C                   END
      *
      ** IF OPTION 3 IS CHOSEN CALL ORIGINATING BRANCH PROGRAMME
      *
     C     OPTION        IFEQ      '3'
     C                   CALL      'SF000023'
     C                   PARM                    USER
     C                   PARM                    AUX1
     C                   PARM                    AUX2
     C                   PARM                    AUX3
     C                   PARM                    RUND
     C                   PARM                    AUX4
     C                   PARM                    SZONE
     C                   END
      *
      ** IF OPTION 4 IS CHOSEN CALL COMPANIES PROGRAMME
      *
     C     OPTION        IFEQ      '4'
     C                   CALL      'SF000022'
     C                   PARM                    USER
     C                   PARM                    AUX1
     C                   PARM                    AUX2
     C                   PARM                    AUX3
     C                   PARM                    RUND
     C                   PARM                    AUX4
     C                   PARM                    SZONE
     C                   END
      *
      ** IF OPTION 1 IS CHOSEN CALL ACTION CODES PROGRAMME
      *
     C     OPTION        IFEQ      '1'
     C                   CALL      'SF000024'
     C                   PARM                    USER
     C                   PARM                    AUX1
     C                   PARM                    AUX2
     C                   PARM                    AUX3
     C                   PARM                    RUND
     C                   PARM                    AUX4
     C                   PARM                    MBIN
     C                   PARM                    SECL
     C                   PARM                    SZONE
     C                   END
      *
      ** IF RETURN CODE IS EQUAL TO 1 SET ON 01 TO END PROGRAMME
      *
     C     AUX2          IFEQ      1
     C                   SETON                                        01
     C                   END
      *
     C                   END
      *
      ** IF CMD/12 IS TAKEN SER RETURN CODE TO 1
      *
     C     *IN02         IFEQ      '1'
     C                   MOVE      '1'           RTCD
     C                   END
      *
      ** IF CMD/3 IS TAKEN SET RETURN CODE TO 2
      *
     C     *IN01         IFEQ      '1'
     C                   MOVE      '2'           RTCD
     C                   END
      *
     C                   SETON                                        LR
     C                   RETURN
      *
