     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas¦ABS Account Ranges by Authorised Users')
      *****************************************************************
      *                                                               *
      *  Midas - Security Profile Facility                            *
      *                                                               *
      *  SF0922 - ACCOUNT RANGES BY AUTHORISED USERS                  *
      *                                                               *
      *  Function:  This program reports all authorised users to      *
      *             restricted account code ranges showing the        *
      *             restricted account code ranges for each user.     *
      *                                                               *
      *  Called By: SFC0922 - Account Ranges by Authorised Users      *
      *                                                               *
      *  (C) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CRE024  *CREATE    Date 07Oct05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRE024 - COUNTRY LEVEL SWITCHABLE FEATURE :                  *
      *           SECURITY FEATURES FOR RESTRICTED ACCOUNTS           *
      *                                                               *
      *****************************************************************
      *
     FSFRACRPAIF  E           K        DISK         KINFSR *PSSR
     FSFRAAUPAIF  E           K        DISK         KINFSR *PSSR
     FSF0922P1O   E                    PRINTER      KINFDS SPOOL1     UC
     FSF0922AUO   E                    PRINTER      KINFDS SPOOLU
      *-------------------------------------------------------------------
     E                    CPY@    1   1 80               MKI Copyright array
      *-------------------------------------------------------------------
      *
     ISPOOL1      DS
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
      * Data Structure to pass parameters to Access programs
     IAOFMTS    E DSDSFDY
      *
      * Data Structure to receive SDBANKPD details
     IAOBANK    E DSSDBANKPD
      *
      * Data Structure to receive MUSER details
     IAOUSER    E DSMUSERDD
      *
     ILDA       EUDSLDA                         256
      *
     I              'Authority to All Acc-C         ALLTXT
     I              'ounts'
      *
      *-------------------------------------------------------------------
      *
     C           *ENTRY    PLIST
     C                     PARM           P0RTCD  7        Return code
     C                     PARM           P0MODE  1        Mode
     C                     PARM           P0SEQ   5        Sequence number
     C                     PARM           P0LVL   1        Level
     C                     PARM           P0ENTY  3        Entity
      *
     C                     EXSR #INIT
     C  N90                EXSR #REPT
     C                     EXSR #AUDT
      *
     C                     SETON                     LR
     C                     RETRN
      *
      *******************************************************************
      *                                                                 *
      * #REPT - Report Processing Subroutine                            *
      *                                                                 *
      *******************************************************************
      *
     C           #REPT     BEGSR
      *
     C                     READ SFRAAUP0                 90
      *
     C           *IN90     DOWEQ'0'
     C                     EXSR #PROC
     C                     READ SFRAAUP0                 90
     C                     END
      *
      * Write Trailer
     C           RCOUNT    IFNE 0
     C                     EXSR #TRL1
     C                     END
      *
     C                     ENDSR
      *
      *******************************************************************
      *                                                                 *
      * #PROC - Process Report Details                                  *
      *                                                                 *
      *******************************************************************
      *
     C           #PROC     BEGSR
      *
      * Call access program AOUSERR0 to obtain User Name if changed
     C           MAUSR     IFNE R1AUSR
     C                     CALL 'AOUSERR0'
     C                     PARM '*BLANKS' P2RTCD           Return Code
     C                     PARM '*KEY'    P2OPTN           Option
     C                     PARM MAUSR     P2AUSR           Key
     C           AOUSER    PARM *BLANKS   AOFMTS           Return Data
      *
      * Place user fields in report fields, left justifying user name
     C                     MOVELMAUSR     R1AUSR           User Profile
      *
     C           USER1     IFEQ *BLANKS
     C                     MOVE *BLANKS   R1USNM
     C                     ELSE
     C           ' '       CHECKUSER1     POSN
     C           25        SUB  POSN      LNGTH
     C           LNGTH     SUBSTUSER1:POSNR1USNM    P      User Name
     C                     END
      *
     C                     MOVE '0'       *IN30
     C                     END
      *
      * Output details only if user found
     C           P3RTCD    IFNE '*NRF'
      *
      * If range is *ALL, set text
     C           MFRAC     IFEQ '*ALL'
     C                     MOVE *BLANKS   MTOAC
     C                     MOVE ALLTXT    MRGDS
     C                     ELSE
      *
      * Else, access range to obtain range details
     C           MFRAC     CHAINSFRACRP0             99
     C           *IN99     IFEQ '1'
     C                     MOVELMFRAC     DBKEY
     C                     MOVE 'SFRACRP0'DBFILE           ***************
     C                     Z-ADD002       DBASE            * DB ERROR 02 *
     C                     EXSR #DET1                      ***************
     C                     EXSR *PSSR
     C                     END
     C                     END
      *
      * Place range fields in report fields
     C                     MOVE MFRAC     R1FRAC
     C                     MOVE MTOAC     R1TOAC
     C                     MOVE MRGDS     R1RGDS
      *
     C                     ADD  1         RCOUNT
     C                     EXSR #DET1
     C                     MOVE '1'       *IN30
     C                     END
      *
     C                     ENDSR
      *
      *******************************************************************
      *                                                                 *
      * #DET1 - Output P1 Details                                       *
      *                                                                 *
      *******************************************************************
      *
     C           #DET1     BEGSR
      *
      * Check for line overflow
     C           OFLLN1    SUB  PRTLN1    DIFLN
     C           DIFLN     IFLE 2
     C                     WRITESF0922H1
     C                     WRITESF0922S1
     C                     MOVE '0'       *IN30
     C                     END
      *
      * Write detail/error line
     C  N99                WRITESF0922D1
     C   99                WRITESF0922E1
      *
     C                     ENDSR
      *
      *******************************************************************
      *                                                                 *
      * #TRL1 - Output P1 Tralier details                               *
      *                                                                 *
      *******************************************************************
      *
     C           #TRL1     BEGSR
      *
     C           OFLLN1    SUB  PRTLN1    DIFLN
     C           DIFLN     IFLE 4
     C                     WRITESF0922H1
     C                     END
      *
      * Output totals and trailer
     C                     WRITESF0922T1
      *
     C                     ENDSR
      *
      *******************************************************************
      *                                                                 *
      * #AUDT - Output Audit report details                             *
      *                                                                 *
      *******************************************************************
      *
     C           #AUDT     BEGSR
      *
      * Process RCF
     C                     EXSR #RCFAU
      *
      * Output Audit Header
     C                     WRITESF0922HA
      *
      * If there is a DB Error, Output the DB Error Information
     C           *INU7     IFEQ '1'
     C                     WRITESF0922EA
     C                     ELSE
      *
      * Output "No Details" message or File COntrols
     C           RCOUNT    IFEQ 0
     C                     WRITESF0922NA
     C                     ELSE
     C                     WRITESF0922CA
     C                     END
     C                     END
      *
     C                     ENDSR
      *
      *******************************************************************
      *                                                                 *
      * #RCFP1 - P1 Report Control Facility processing                  *
      *                                                                 *
      *******************************************************************
      *
     C           #RCFP1    BEGSR
      *
      * Ensure Report Spool File recorded by RCF.
     C                     Z-ADDSFNUM1    ZSNUM
      *
     C                     UNLCKLDA
     C                     CALL 'ZSFILE'
     C                     PARM P0SEQ     SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM *BLANKS   ZSERR
     C           *LOCK     IN   LDA
      *
     C           ZSERR     IFEQ 'Y'
     C                     EXSR *PSSR
     C                     END
      *
     C                     ENDSR
      *
      *******************************************************************
      *                                                                 *
      * #RCFAU - AU Report Control Facility processing                  *
      *                                                                 *
      *******************************************************************
      *
     C           #RCFAU    BEGSR
      *
      * Ensure Audit Spool File recorded by RCF.
     C                     Z-ADDSFNUMU    ZSNUM
      *
     C                     UNLCKLDA
     C                     CALL 'ZSFILE'
     C                     PARM P0SEQ     SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUM
     C                     PARM *BLANKS   ZSERR
     C           *LOCK     IN   LDA
      *
     C           ZSERR     IFEQ 'Y'
     C                     EXSR *PSSR
     C                     END
      *
     C                     ENDSR
      *
      *******************************************************************
      *                                                                 *
      * #INIT - Initial Program Subroutine                              *
      *                                                                 *
      *******************************************************************
      *
     C           #INIT     BEGSR
      *
      * Define program work fields
     C           *LIKE     DEFN MAUSR     P2AUSR
      *
     C                     MOVE *BLANKS   P1RTCD  7
     C                     MOVE *BLANKS   P1OPTN  7
     C                     MOVE *BLANKS   P2RTCD  7
     C                     MOVE *BLANKS   P2OPTN  7
     C                     MOVE *BLANKS   P3RTCD  7
     C                     MOVE *BLANKS   SEQ     5
     C                     MOVE *BLANKS   SENTY   3
     C                     MOVE *BLANKS   ZSERR   1
      *
     C                     Z-ADD0         ZSNUM   60
     C                     Z-ADD0         RCOUNT  50
     C                     Z-ADD0         DIFLN   30
     C                     Z-ADD0         POSN    20
     C                     Z-ADD0         LNGTH   20
      *
      * Set up copyright
     C                     MOVEACPY@      MKI@   80
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
      *
      * Access SDBANKPD to obtain Report Title and Run Date
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' P1RTCD
     C                     PARM '*FIRST ' P1OPTN
     C           AOBANK    PARM *BLANKS   AOFMTS
      *
     C           P1RTCD    IFNE *BLANKS
     C                     MOVE 'SDBANKPD'DBFILE           ***************
     C                     Z-ADD001       DBASE            * DB ERROR 01 *
     C                     EXSR *PSSR                      ***************
     C                     END
      *
      * Check if any records on file
     C           *LOVAL    SETLLSFRAAUP0             90
      *
      * If records to report, open P1 report file and process RCF
     C           *IN90     IFEQ '0'
     C                     OPEN SF0922P1
     C                     EXSR #RCFP1
     C                     END
      *
     C                     ENDSR
      *
      *******************************************************************
      *                                                                 *
      * *PSSR  - Program Exception Error Routine                        *
      *                                                                 *
      *******************************************************************
      *
     C           *PSSR     BEGSR
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     MOVEL'SF0922'  DBPGM
     C                     OUT  LDA
     C                     DUMP
     C                     EXSR #AUDT
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *******************************************************************
      *
** CPY@     : Copyright notice for inclusion in all MKI programs
(C) Finastra International Limited 2005
