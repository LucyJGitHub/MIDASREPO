     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SF Validate Company/User')
      *****************************************************************
      *                                                               *
      *  Midas  SECURITY PROFILE FACILITY
      *                                                               *
      *   ZBCOU - TO VALIDATE THAT THE USER IS AUTHORISED TO A COMPANY*
      *                                                               *
      *  Function: This program checks that the user is authorised    *
      *            to a company. It's called in batch mode only.      *
      *            A parameter is passed back to the calling program  *
      *            to indicate validity :                             *
      *            0 - all valid                                      *
      *            1 - no authorised companies found for user         *
      *            2 - user is not authorised to this company         *
      *                                                               *
      *  Where Used: Input Cycle, multibranch environment             *
      *                                                               *
      *  Called By: Any program requiring this validation             *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Any changes made to this program may also need to be         *
      *  incorporated in ZVCOU.                                       *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *   LAST AMEND NO. CLE004 *CREATE  DATE  27MAY97                *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE004 - Syndications & Authorisations                       *
      *                                                               *
      ********************************************************************
      *                                                               *
     FMCMPS   IF  E           K        DISK
      *****************************************************************
      *
      * ID F  C  H  L     FUNCTION OF INDICATORS
      *
      *       01       OPEN FILE
      *       99       NO VALID COMPANIES FOUND
      *
      *****************************************************************
      *       INPUTS TO THIS PROGRAMME
      *
      *       COMPANY CODE 3A  FROM THE CALLING PROGRAMME
      *       USER ID     10A  FROM THE CALLING PROGRAM
      *
      *****************************************************************
      *       OUTPUT FROM THIS PROGRAMME
      *
      *       ERROR CODE  : 0 = NO ERROR
      *                     1 = NO VALID COMPANIES FOUND FOR USER
      *                     2 = THIS COMPANY CODE NOT VALID FOR USER
      *
      *****************************************************************
     E                    ZCO       100  3               STORAGE ARRAY
      **
     C           *ENTRY    PLIST
     C                     PARM           ZCMPYX  3        COMPANY CODE 3A
     C                     PARM           USRID  10        USER ID      3A
     C                     PARM           ERR     10       ERROR CODE 1S
      *
     C                     SETOF                       99
     C                     Z-ADD0         ERR
      *
      *****************************************************************
      **  IF FIRST ROUND MOVE ZCMP INFO INTO ARRAY
      *
     C           *IN01     IFEQ '0'                        B01
      **
     C                     SETON                     01    1ST ROUND IND
      *
     C                     EXSR ZSPFC
      *
     C                     END
      *****************************************************************
      * SUBSEQUENT ROUNDS
      * IF ZCMP IS EMPTY SET ERROR CONDITION TO 1
      *
     C           C         IFEQ 0
     C                     Z-ADD1         ERR
      *
      * ELSE AT LEAST ONE ENTRY IS ON ZCMP
      *
     C                     ELSE
      *
      **  COMPANY MUST BE VALID FOR USER
      *
     C           ZCMPYX    LOKUPZCO                      99
      **
     C           *IN99     IFEQ '0'
     C                     Z-ADD2         ERR
     C                     END
      *
     C                     END
      *
     C                     RETRN
      *****************************************************************
      **  P/ZSPFC STD SR TO MAKE SPF USER/COMPANIES AVAILABLE TO PGM
      **  INPUT FROM FILE MCMPS- ZCO1 & 2,UP TO 100 COMPANIES,TOP DOWN
      **                         ZINO,INDEX OF LOWEST ENTRY FOR ARRAY
      **  OUTPUT - ZCO  ARRAY, TOP DOWN
      **             C, EQ ZINO
      *****************************************************************
     C           ZSPFC     BEGSR                           **  ZSPFC **
      **
     C           USRID     CHAINMCMPSDDF             22
     C  N22      RECI      COMP 'D'                  2222
     C  N22      VCMP      COMP *BLANK                   22
      **
     C           *IN22     IFEQ '1'
     C                     MOVE '000'     NOOC
     C                     MOVE *BLANK    VCMP
     C                     END
      **
     C                     MOVEAVCMP      ZCO,1
     C                     MOVE NOOC      C       30
     C                     ENDSR
      *****************************************************************
