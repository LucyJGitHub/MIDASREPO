     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SF Validate Company/User')
     F*****************************************************************
     F*                                                               *
     F*  Midas  SECURITY PROFILE FACILITY
     F*                                                               *
     F*   ZVCOU - TO VALIDATE THAT THE USER IS AUTHORISED TO A COMPANY*
     F*                                                               *
     F*  Function: This program checks that the user is authorised    *
     F*            to a company.                                      *
     F*            A parameter is passed back to the calling program  *
     F*            to indicate validity :                             *
     F*            0 - all valid                                      *
     F*            1 - no authorised companies found for user         *
     F*            2 - user is not authorised to this company         *
     F*                                                               *
     F*  Where Used: Input Cycle, multibranch environment             *
     F*                                                               *
     F*  Called By: Any program requiring this validation             *
     F*                                                               *
     F*  Calls: None                                                  *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
     F*  Any changes made to this program may also need to be         *
     F*  incorporated in ZBCOU.                                       *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*   LAST AMEND NO. E26748          DATE  01AUG91                *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*   E26748 - SOURCE MEMBER INCORRECTLY PLACED IN ZARPGSRC       *
     F*            SO MOVE TO SFRPGSRC.                               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F*
     F* ID F  C  H  L     FUNCTION OF INDICATORS
     F*
     F*       01       OPEN FILE
     F*       99       NO VALID COMPANIES FOUND
     F*
     F*****************************************************************
     F*       INPUTS TO THIS PROGRAMME
     F*
     F*       COMPANY CODE 3A  FROM THE CALLING PROGRAMME
     F*       DTAARA/ZCMP      LIST OF VALID COMPANIES FOR THIS USER
     F*
     F*****************************************************************
     F*       OUTPUT FROM THIS PROGRAMME
     F*
     F*       ERROR CODE  : 0 = NO ERROR
     F*                     1 = NO VALID COMPANIES FOUND FOR USER
     F*                     2 = THIS COMPANY CODE NOT VALID FOR USER
     F*
     F*****************************************************************
     E                    ZCO       100  3               STORAGE ARRAY
     I*
     I* DATA STRUCTURE TO SET UP VALID COMPANIES
     I*
     IZCMP        DS
     I                                        1   1 ZCHO
     I                                        2   4 ZINO
     I                                        5 154 ZCO1
     I                                      155 304 ZCO2
     C**
     C           *ENTRY    PLIST
     C                     PARM           ZCMPYX  3        COMPANY CODE 3A
     C                     PARM           ERR     10       ERROR CODE 1S
     C*
     C                     SETOF                       99
     C                     Z-ADD0         ERR
     C*
     C*****************************************************************
     C**  IF FIRST ROUND MOVE ZCMP INFO INTO ARRAY
     C*
     C           *IN01     IFEQ '0'                        B01
     C**
     C                     SETON                     01    1ST ROUND IND
     C*
     C                     EXSR ZSPFC
     C*
     C                     END
     C*****************************************************************
     C* SUBSEQUENT ROUNDS
     C* IF ZCMP IS EMPTY SET ERROR CONDITION TO 1
     C*
     C           C         IFEQ 0
     C                     Z-ADD1         ERR
     C*
     C* ELSE AT LEAST ONE ENTRY IS ON ZCMP
     C*
     C                     ELSE
     C*
     C* SAVE 'C' SO THAT SEARCHING ALWAYS STARTS AT LOWEST ENTRY
     C*
     C                     Z-ADDC         V       30
     C*
     C**  COMPANY MUST BE VALID FOR USER
     C*
     C           ZCMPYX    LOKUPZCO,V                    99
     C**
     C           *IN99     IFEQ '0'
     C                     Z-ADD2         ERR
     C                     END
     C*
     C                     END
     C*
     C                     RETRN
     C*****************************************************************
     C**  P/ZSPFC STD SR TO MAKE SPF USER/COMPANIES AVAILABLE TO PGM
     C**  INPUT FROM EXT.DTAARA- ZCO1 & 2,UP TO 100 COMPANIES,TOP DOWN
     C**                         ZINO,INDEX OF LOWEST ENTRY FOR ARRAY
     C**  OUTPUT - ZCO  ARRAY, TOP DOWN
     C**             C, EQ ZINO
     C**
     C           ZSPFC     BEGSR                           **  ZSPFC **
     C           *NAMVAR   DEFN           ZCMP
     C                     IN   ZCMP
     C                     MOVEAZCO1      ZCO,1
     C                     MOVEAZCO2      ZCO,51
     C                     MOVE ZINO      C       30
     C                     UNLCKZCMP
     C                     ENDSR
     C*****************************************************************
