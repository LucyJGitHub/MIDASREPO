      *****************************************************************
/*STD *  DSPFBASE                                                     *
      *****************************************************************
      *                                                               *
      *  Midas - (Module Name) Module                                 *
      *                                                               *
      *  DSPF/mmnnnnDF - (display file name)                          *
      *                                                               *
      *  (c) Finastra International Limited 2007                      *
      *                                                               *
      *  Last Amend No. xxxxxx  *CREATE    Date ddMmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  xxxxxx - (change description)                                *
      *                                                               *
      *****************************************************************
      *
------*-------------------------------------------------------------------------
      *  THE FOLLOWING ARE SOME OF THE KEYWORDS THAT MIGHT BE SPECIFIED
      *  FOR A DISPLAY FILE.
------*-------------------------------------------------------------------------
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
------*-------------------------------------------------------------------------
      *  THE FIRST FORMAT MUST CONTAIN THE OBJECT COPYRIGHT STATEMENT.
------*-------------------------------------------------------------------------
     A          R MMNNNNF0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2007')
      *
     A                                      PUTOVR
      *
------*-------------------------------------------------------------------------
      *
      ***  Example /COPY.
     A*/COPY WNCPYSRC,MMnnnnDF01
      *
------*-------------------------------------------------------------------------
      *  THE FOLLOWING IS THE STANDARD SCREEN HEADING. REPLACE MMNNNN WITH
      *  THE PROGRAM NAME. THE POSTION OF THE SCREEN TITLE MAY NEED TO BE
      *  ADJUSTED DEPENDING ON ITS LENGTH.
------*-------------------------------------------------------------------------
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'MMnnnnF1'
      * The MMnnnnFx hard coded reference is displayed in the screen
      * header as a unique screen identifier. The number 'x' should
      * be incremented by 1 for each subsequent screen - i.e. the
      * first screen will be MMnnnnF1, the second screen MMnnnnF2 etc.
     A                                  1 25'(Screen Title)'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
------*-------------------------------------------------------------------------
      *  HERE ARE SOME EXAMPLE INPUT/OUTPUT FIELDS. THE INDICATORS MEAN THE
      *  FOLLOWING:
      *     09  - Enquiry Mode
      *     20  - Error on Action Code
      *     21  - Error on Security
      *     22  - Error on Branch
      *     32  - Error on Interest
      *     37  - Multibranching Switched On
      *  IN THIS EXAMPLE, BRANCH IS ONLY DISPLAYED FOR INPUT IF THE SYSTEM
      *  HAS MULTIBRANCHING SWITCHED ON.
------*-------------------------------------------------------------------------
     A                                  4  2'Action Code . :'
     A            SACTN     R        B  4 18REFFLD(ACTN)
     A                                      DSPATR(HI)
     A  20                                  DSPATR(RI PC)
     A  09                                  DSPATR(PR)
     A N20N09                               DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  4 40'Security  . . :'
     A            SSECT         10A  B  4 56DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A  09                                  DSPATR(PR)
     A N21N09                               DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  5 40'Branch  . . . :'
     A N37                                  DSPATR(ND)
     A            SBRCA     R        B  5 56REFFLD(BRCA)
     A N37                                  DSPATR(ND)
     A  37                                  DSPATR(HI)
     A  22                                  DSPATR(RI PC)
     A  09N37                               DSPATR(PR)
     A N22N09 37                            DSPATR(UL)
     A                                      OVRDTA
      *
------*-------------------------------------------------------------------------
      *  THE LAST INPUT FIELD ON THE SCREEN MUST BE CHECKED FOR FIELD EXIT.
------*-------------------------------------------------------------------------
     A                                 17  2'Interest  . . :'
     A            SPINT         14A  B 17 18DSPATR(HI)
     A  32                                  DSPATR(RI PC)
     A  09                                  DSPATR(PR)
     A N32N09                               DSPATR(UL)
     A                                      CHECK(FE)
     A                                      OVRDTA
      *
------*-------------------------------------------------------------------------
      *  COMMAND KEYS AVAILABLE DISPLAYED ON LINE 23. THIS EXAMPLE SHOWS
      *  HOW A CHANGE (EG. ERROR NUMBER 056789) IS MADE IN DDS SOURCE.
------*-------------------------------------------------------------------------
      *                                              Begin /056789/
     A***********                      23  3'F3=End'
     A                                 23  3'F3=End, +
     A                                      F5=Initial Screen'
      *                                              End   /056789/
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
