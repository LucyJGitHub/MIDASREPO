/*STD *  DSPFBASE                                                     *
      *****************************************************************
      *                                                               *
      *  Midas - xxxxxxxxxxxxx ILE Module                             *
      *                                                               *
      *  SKFfffRDF - Invalid xxxxxxxxxxxxxxxxx details browse         *
      *              ("REPAIR FUNCTION")                              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2007            *
      *                                                               *
      *  Last Amend No. CAP0nn  *CREATE    Date ddMmmyy               *
      *  Prev Amend No. xxxxxx             Date ddMmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP0nn - Conversion of MN inputs into modular structure to   *
      *           use as APIs.                                        *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A          R SKFFFFS1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2007')
     A                                      SFL
     A            DDOPT          1A  B  7  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDACTN         1   O  7  7DSPATR(HI)
     A            DDFOTRANID    20   O  7 11DSPATR(HI)
     A            DDTRNN         6A  O  7 32
     A            DDTREF        10A  O  7 40
     A            DDFOASOCIDR        H      REFFLD(FOTRNID)
     A            D@TMESTMP R   26A  H      REFFLD(TIMESTMP)
     A            DDBRSN    R        H      REFFLD(BRSN)
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN CONTROL                                            *
      * -------------------------------------------------------------------*
      *
     A          R SKFFFFS0                  SFLCTL(SKFFFFS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SKFFFFRPB'
     A                                  1 24'Invalid xxxxxxxxxx +
     A                                      xxxxxxx Maintenance'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 36'Browse'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Sel'
     A                                  4  7'Act'
     A                                  4 11'Front Office Id.'
     A                                  4 31'Transac.'
     A                                  4 40'Transaction'
     A                                  5 32'Number'
     A                                  5 40'Reference'
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN FOOTER                                             *
      * -------------------------------------------------------------------*
      *
     A          R SKFFFFF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
      *
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
      * -------------------------------------------------------------------*
      * * END MESSAGE                                                      *
      * -------------------------------------------------------------------*
      *
     A          R SKFFFFEND                 TEXT('END MESSAGE')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'SKFFFFRPB'
     A                                  1 24'Invalid xxxxxxxxxx +
     A                                      xxxxxxx Maintenance'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this Function'
