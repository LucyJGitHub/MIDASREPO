/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas SM Shell for rebuild of multi-member LFs')      */
/*********************************************************************/
/*                                                                   */
/*       Midas - Implementation module                               */
/*                                                                   */
/*       SMC000002 - Shell for rebuild of multi-member LFs           */
/*                                                                   */
/*       (c) Finastra International Limited 2006                     */
/*                                                                   */
/*       Last Amend No. MD054605           Date 17Oct19              */
/*       Prev Amend No. MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*                      CPK025  *CREATE    Date 03Sep06              */
/*                      XXXXXX             Date DDMmmYY              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD054605 - Deliverable Data Split for SDSVALPD              */
/*       MD046248 - Finastra Rebranding                              */
/*       CPK025 - MidasPlus 1.3 packaging.  New for global layer.    */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&PFXSVAL &BRGSVAL)

             DCL        VAR(&PFXSVAL) TYPE(*CHAR) LEN(20)
             DCL        VAR(&BRGSVAL) TYPE(*CHAR) LEN(20)

             DCL        VAR(&SBSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&PTFUPG) TYPE(*CHAR) LEN(10)
             DCL        VAR(&BLANKSVAL) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)
             DCL        VAR(&RETURN) TYPE(*CHAR) LEN(1)
             DCL        VAR(&WRAP) TYPE(*CHAR) LEN(10)

/**********  DCLF       FILE(UPGSVALJ0) */                                              /*MD054605*/
             DCLF       FILE(UPGSVALJ0) ALWNULL(*YES)                                   /*MD054605*/

             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2006')

/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ERROR))

             CHGJOB     LOG(4 0 *SECLVL) LOGCLPGM(*YES)
             CHGJOB     SWS(XXXXXX00)

/* Change 'message queue full' handling so that job doesn't crash */
             RTVJOBA    JOBMSGQFL(&WRAP)
             CHGJOB     JOBMSGQFL(*PRTWRAP)

/* Create data area /MIDASMSG in QTEMP */
             DLTDTAARA  DTAARA(QTEMP/MIDASMSG)
             MONMSG     MSGID(CPF0000)
             CRTDTAARA  DTAARA(QTEMP/MIDASMSG) TYPE(*CHAR) LEN(800) +
                          VALUE(' ')

VALIDATE:
             RCVF
             MONMSG     MSGID(CPF0864) EXEC(DO)
                GOTO       CMDLBL(CHECK)
             ENDDO

             IF         COND(&GISVAL *EQ &PFXSVAL) THEN(DO)
                IF         COND(&GIVAL *EQ ' ') THEN(DO)
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')
                ENDDO
                ELSE       CMD(DO)
                   CHGVAR     VAR(&SBSID) VALUE(&GIVAL)
                ENDDO
             ENDDO

             IF         COND(&GISVAL *EQ &BRGSVAL) THEN(DO)
                IF         COND(&GIVAL *EQ ' ') THEN(DO)
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')
                ENDDO
                ELSE       CMD(DO)
                   CHGVAR     VAR(&PTFUPG) VALUE(&GIVAL)
                ENDDO
             ENDDO

             GOTO       CMDLBL(VALIDATE)

 CHECK:
             IF         COND(&BLANKSVAL *EQ 'Y') THEN(DO)
/* Set up messages for Midas Information Screen  */
                RTVMSG     MSGID(UPM0003) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
                RTVMSG     MSGID(UPM0004) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (151 50)) VALUE(&MESSAGE)
                CALL       PGM(SCC0010) PARM('SMC000002' 'ENTER' ' ')
                GOTO       CMDLBL(ENDPGM)
             ENDDO

             CALL       PGM(GPC000016) PARM(&SBSID)

             CHGVAR     VAR(&RETURN) VALUE(' ')

/*******************************************************************/
/*                                                                 */
/*  SM0264 - Change library names for FILEMULMBR                   */
/*                                                                 */
/*******************************************************************/

             CALL       PGM(SM0264)

/*******************************************************************/
/*                                                                 */
/*  UPC0265 - Rebuild multi-member logical files                   */
/*                                                                 */
/*******************************************************************/

             CALL       PGM(UPC0265) PARM(&PTFUPG &RETURN)
/*If the program ended with error then take appropriate action */
             IF         COND(&RETURN *EQ 'C') THEN(DO)
                GOTO       CMDLBL(ENDPGM)
             ENDDO
/*******************************************************************/
/*                                                                 */
/*  SMC0266 - Rebuild logical file members                         */
/*                                                                 */
/*******************************************************************/

             CALL       PGM(SMC0266) PARM(&RETURN)
/*If the program ended with error then take appropriate action */
             IF      COND(&RETURN *EQ 'C') THEN(DO)
                GOTO       CMDLBL(ENDPGM)
             ENDDO

             GOTO       CMDLBL(ENDPGM)
ERROR:
/* Set up messages for Midas Information Screen  */

             RTVMSG     MSGID(UPM0001) MSGF(UTMSGF) +
                          MSGDTA('SMC000002') MSG(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
             MONMSG     MSGID(CPF0000)

             CHGDTAARA  DTAARA(MIDASMSG (201 50)) VALUE('Check +
                          joblog for details')
             MONMSG     MSGID(CPF0000)
             CALL       PGM(SCC0010) PARM('SMC000002' 'JOBLOG' 'Y')
             MONMSG     MSGID(CPF0000)

ENDPGM:
             CHGJOB     JOBMSGQFL(&WRAP)

             ENDPGM
