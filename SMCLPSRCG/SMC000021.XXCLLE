/*********************************************************************/
/*S*D****CLPBASEBND***************************************************/
/*********************************************************************/
/*                                                                   */
/*       Midas - Implementation module                               */
/*                                                                   */
/*       SMC000021 - Restore libraries for CREATEZONE                */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2005           */
/*                                                                   */
/*       Last Amend No. CUP041 *REDUNDANT  Date 07Oct14              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. CSM006  *CREATE    Date 22Mar05              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CUP041 - New Bridge methodology to combine menu options     */
/*       CSM006 - Create new zone based on existing zone.            */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&NEWZONE &GLOBLYR)
 
             DCL        VAR(&NEWZONE) TYPE(*CHAR) LEN(2)
             DCL        VAR(&GLOBLYR) TYPE(*CHAR) LEN(2)
 
             DCL        VAR(&DMLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DPLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DTALIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DVLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DZLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&GXLIB) TYPE(*CHAR) LEN(10)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2005')
 
/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ERROR))
 
/* Set up variables for library names. */
             CHGVAR     VAR(&DMLIB) VALUE(&NEWZONE *TCAT 'DMLIB')
             CHGVAR     VAR(&DPLIB) VALUE(&NEWZONE *TCAT 'DPLIB')
             CHGVAR     VAR(&DTALIB) VALUE(&NEWZONE *TCAT 'DTALIB')
             CHGVAR     VAR(&DVLIB) VALUE(&NEWZONE *TCAT 'DVLIB')
             CHGVAR     VAR(&DZLIB) VALUE(&NEWZONE *TCAT 'DZLIB')
             CHGVAR     VAR(&GXLIB) VALUE(&GLOBLYR *TCAT 'GXLIB')
 
/* Attempt to delete any occurrences of libraries already existing. */
             DLTLIB     LIB(MP_DVLIB)
             MONMSG     MSGID(CPF2110)
             DLTLIB     LIB(MP_DMLIB)
             MONMSG     MSGID(CPF2110)
             DLTLIB     LIB(MP_DPLIB)
             MONMSG     MSGID(CPF2110)
             DLTLIB     LIB(MP_DTALIB)
             MONMSG     MSGID(CPF2110)
             DLTLIB     LIB(MP_DZLIB)
             MONMSG     MSGID(CPF2110)
             DLTLIB     LIB(&DVLIB)
             MONMSG     MSGID(CPF2110)
             DLTLIB     LIB(&DMLIB)
             MONMSG     MSGID(CPF2110)
             DLTLIB     LIB(&DPLIB)
             MONMSG     MSGID(CPF2110)
             DLTLIB     LIB(&DTALIB)
             MONMSG     MSGID(CPF2110)
             DLTLIB     LIB(&DZLIB)
             MONMSG     MSGID(CPF2110)
 
             DLTLIB     LIB(MP_SQLSRCQ)
             MONMSG     MSGID(CPF2110)
 
/* Restore physical file libraries as they have been saved. */
             RSTLIB     SAVLIB(MP_DMLIB) DEV(*SAVF) +
                          SAVF(&GXLIB/MP_DMLIB) OUTPUT(*PRINT)
             RSTLIB     SAVLIB(MP_DPLIB) DEV(*SAVF) +
                          SAVF(&GXLIB/MP_DPLIB) OUTPUT(*PRINT)
             RSTLIB     SAVLIB(MP_DTALIB) DEV(*SAVF) +
                          SAVF(&GXLIB/MP_DTALIB) OUTPUT(*PRINT)
 
/* Restore logical file library to new name. */
             RSTLIB     SAVLIB(MP_DVLIB) DEV(*SAVF) +
                          SAVF(&GXLIB/MP_DVLIB) RSTLIB(&DVLIB) +
                          OUTPUT(*PRINT)
/* Restore 24x7 file library to new name. */
             RSTLIB     SAVLIB(MP_DZLIB) DEV(*SAVF) +
                          SAVF(&GXLIB/MP_DZLIB) RSTLIB(&DZLIB) +
                          OUTPUT(*PRINT)
 
/* Rename pysical file libraries. */
             RNMOBJ     OBJ(MP_DMLIB) OBJTYPE(*LIB) NEWOBJ(&DMLIB)
             RNMOBJ     OBJ(MP_DPLIB) OBJTYPE(*LIB) NEWOBJ(&DPLIB)
             RNMOBJ     OBJ(MP_DTALIB) OBJTYPE(*LIB) NEWOBJ(&DTALIB)
 
/* Restore library containing source for SQL views and stored procedures. */
             RSTLIB     SAVLIB(MP_SQLSRCQ) DEV(*SAVF) +
                          SAVF(&GXLIB/MP_SQLSRCQ) OUTPUT(*PRINT)
 
             GOTO       CMDLBL(END)
 
ERROR:
             CHGJOB     SWS(XXXXXX11)
END:
             ENDPGM
