/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas SM Create non-database objects - global')       */
/*********************************************************************/
/*                                                                   */
/*       Midas - Implementation module                               */
/*                                                                   */
/*       SMC000024 - Create non-database objects                     */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2006           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/*       Last Amend No. CUT011             Date 12Nov07              */
/*       Prev Amend No. CUP033  *CREATE    Date 12Sep06              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CUT011 - New CRTALLOBJ command.                             */
/*       CUP033 - New utility for CRTALLOBJ.                         */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&GPFXSVAL &BRGSVAL)
 
             DCL        VAR(&GPFXSVAL) TYPE(*CHAR) LEN(20)
             DCL        VAR(&BRGSVAL) TYPE(*CHAR) LEN(20)
 
             DCL        VAR(&GSBSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&BRGLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&BLANKSVAL) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&LIBNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)
             DCL        VAR(&GTALIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OWNER) TYPE(*CHAR) LEN(10)
 
             DCLF       FILE(UPGSVALJ0)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2006')
 
/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ERROR))
 
             CHGJOB     LOG(4 0 *SECLVL) LOGCLPGM(*YES)
             CHGJOB     SWS(XXXXXX00)
 
/* Create data area /MIDASMSG in QTEMP */
             DLTDTAARA  DTAARA(QTEMP/MIDASMSG)
             MONMSG     MSGID(CPF0000)
             CRTDTAARA  DTAARA(QTEMP/MIDASMSG) TYPE(*CHAR) LEN(800) +
                          VALUE(' ')
 
VALIDATE:
             RCVF
             MONMSG     MSGID(CPF0864) EXEC(DO)
                GOTO       CMDLBL(CHECK)
             ENDDO
 
             IF         COND(&GISVAL *EQ &GPFXSVAL) THEN(DO)
                IF         COND(&GIVAL *EQ ' ') THEN(DO)
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')
                ENDDO
                ELSE       CMD(DO)
                   CHGVAR     VAR(&GSBSID) VALUE(&GIVAL)
                ENDDO
             ENDDO
 
             IF         COND(&GISVAL *EQ &BRGSVAL) THEN(DO)
                IF         COND(&GIVAL *EQ ' ') THEN(DO)
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')
                ENDDO
                ELSE       CMD(DO)
                   CHGVAR     VAR(&BRGLIB) VALUE(&GIVAL)
                ENDDO
             ENDDO
 
             GOTO       CMDLBL(VALIDATE)
 
CHECK:
             IF         COND(&BLANKSVAL *EQ 'Y') THEN(DO)
                RTVMSG     MSGID(UPM0003) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
                RTVMSG     MSGID(UPM0004) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (151 50)) VALUE(&MESSAGE)
                CALL       PGM(SCC0010) PARM('SMC000024' 'ENTER' ' ')
                GOTO       CMDLBL(ENDPGM)
             ENDDO
 
/* Set up library list over which objects are to be created. */
             CALL       PGM(GPC000016) PARM(&GSBSID)
 
/* Get library name that is to be created. */
LIBNAME:
             ?          LIBNAME
             MONMSG     MSGID(CPF6801) EXEC(DO)
                GOTO       CMDLBL(ENDPGM)
             ENDDO
             RTVDTAARA  DTAARA(QTEMP/LIBNAME) RTNVAR(&LIBNAME)
 
/* Check that library exists. */
             CHKOBJ     OBJ(&LIBNAME) OBJTYPE(*LIB)
             MONMSG     MSGID(CPF9801) EXEC(DO)
                CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE('Library' +
                             *BCAT &LIBNAME *BCAT 'does not exist.')
                CALL       PGM(SCC0010) PARM('SMC000024' 'ENTER' ' ')
                GOTO       CMDLBL(LIBNAME)
             ENDDO
 
             CHGVAR     VAR(&GTALIB) VALUE(&GSBSID *TCAT 'GTALIB')
             CHGVAR     VAR(&OWNER) VALUE(&GSBSID *TCAT 'OWNER')
 
/**********  ?          CRTALLOBJ SRCLIB(&LIBNAME) ??JOBQLIB(SMJOBQ) +                 */ /*CUT011*/
/**********               ?*CTLFILE(*DFT) MSGFLIB(&GTALIB) +                           */ /*CUT011*/
/**********               ?*RECREATE(*NO) ??OWNER(&OWNER) +                            */ /*CUT011*/
/**********               ?*AUTL(*NONE) EDTLIBL(*YES) +                                */ /*CUT011*/
/**********               GLOBAL(&GSBSID *NO *PFX *NO *PFX *NO *PFX +                  */ /*CUT011*/
/**********               *NO *PFX *YES &LIBNAME) ?-ZONE(*N)                           */ /*CUT011*/
             ?          CRTALLOBJ SRCLIB(&LIBNAME) +
                          ??JOBQLIB(&BRGLIB/SMJOBQ) +
                          MSGFLIB(&GTALIB) GLOBAL((*N ()) ((*DEV +
                          *CMD *OPMMOD *SRVPGM *PGM) &LIBNAME +
                          &OWNER)) ?-ZONE((*N ()) (()))                                   /*CUT011*/
             MONMSG     MSGID(CPF6801) EXEC(DO)
                GOTO       CMDLBL(ENDPGM)
             ENDDO
 
             GOTO       CMDLBL(ENDPGM)
ERROR:
/* Set up messages for Midas Information Screen  */
             RTVMSG     MSGID(UPM0001) MSGF(UTMSGF) +
                          MSGDTA('SMC000024') MSG(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (201 50)) VALUE('Check +
                          joblog for details')
             MONMSG     MSGID(CPF0000)
             CALL       PGM(SCC0010) PARM('SMC000024' 'JOBLOG' 'Y')
             MONMSG     MSGID(CPF0000)
 
ENDPGM:
             ENDPGM
