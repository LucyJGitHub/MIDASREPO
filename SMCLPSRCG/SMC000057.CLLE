/*********************************************************************/
/*XBI    OVRDBF FILE(SMC200057F) TOFILE(UPDBRLTPD)                   */
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas SM Save logical files for CHGCCSIDS')           */
/*********************************************************************/
/*                                                                   */
/*       Midas - Implementation                                      */
/*                                                                   */
/*       SMC000057 - Save logical files for changing CCSIDs          */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2008           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/*       Last Amend No. 256038  *CREATE    Date 11Jul08              */
/*       Prev Amend No. xxxxxx             Date ddMmmyy              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       256038 - Handle logical file problems.                      */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&FILE &LIB &SAVLIB)
 
             DCL        VAR(&FILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SAVLIB) TYPE(*CHAR) LEN(10)
 
             DCL        VAR(&ERRMSG) TYPE(*CHAR) LEN(100)
             DCL        VAR(&SAVNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&NOTDELETE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&RECURSIVE) TYPE(*CHAR) LEN(1) VALUE(N)
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MSGF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGFLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGDTA) TYPE(*CHAR) LEN(100)
 
             DCLF       FILE(SMC200057F)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2008')
 
/* Global monitor message. */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ERROR))
 
/* Build list of files built over file. */
             DLTF       FILE(QTEMP/SMC200057F)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(QTEMP/DBR)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(UPDBRLTPD) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(SMC200057F)
             DSPDBR     FILE(&LIB/&FILE) OUTPUT(*OUTFILE) +
                          OUTFILE(QTEMP/DBR)
             CPYF       FROMFILE(QTEMP/DBR) TOFILE(QTEMP/SMC200057F) +
                          MBROPT(*REPLACE) FMTOPT(*MAP *DROP)
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
 
READNEXT:
             RCVF
             MONMSG     MSGID(CPF0864) EXEC(DO)
                GOTO       CMDLBL(DELETE_LF)
             ENDDO
 
             IF         COND(&WHREFI *NE &SAVNAME) THEN(DO)
                CHGVAR     VAR(&SAVNAME) VALUE(&WHREFI)
 
                DLTF       FILE(&SAVLIB/&WHREFI)
                MONMSG     MSGID(CPF0000)
                CRTSAVF    FILE(&SAVLIB/&WHREFI)
                SAVOBJ     OBJ(&WHREFI) LIB(&WHRELI) DEV(*SAVF) +
                             OBJTYPE(*FILE) SAVF(&SAVLIB/&WHREFI) +
                             OUTPUT(*NONE)
             ENDDO
 
             GOTO       CMDLBL(READNEXT)
 
/* Call program to delete logicals. */
DELETE_LF:
             CALL       PGM(SMC000059) PARM(&NOTDELETE)
             IF         COND(&NOTDELETE *EQ 'Y') THEN(DO)
                GOTO       CMDLBL(DELETE_LF)
             ENDDO
             IF         COND(&NOTDELETE *EQ 'E') THEN(DO)
                GOTO       CMDLBL(ERROR)
             ENDDO
 
/* Copy records to master list of database relations. */
             CPYF       FROMFILE(QTEMP/SMC200057F) +
                          TOFILE(QTEMP/SMC100057F) MBROPT(*ADD)
 
             GOTO       CMDLBL(ENDPGM)
ERROR:
             CHGVAR     VAR(&ERRMSG) VALUE('Error in saving / +
                          deleting logical files.  Check job log.')
             CALL       PGM(UP008010) PARM('*WRITE ' 'SMC000057 ' +
                          &ERRMSG)
             MONMSG     MSGID(CPF0000 RPG0000)
 
ENDPGM:
             ENDPGM
