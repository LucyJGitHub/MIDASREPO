/*********************************************************************/
/*XBI    OVRDBF FILE(SMC000062F) TOFILE(UPFDBATPD)                   */
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas SM Delete all save files from PLIB')            */
/*********************************************************************/
/*                                                                   */
/*       Midas - Implementation                                      */
/*                                                                   */
/*       SMC000062 - Delete all save files from PLIB                 */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2008           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/*       Last Amend No. 256038  *CREATE    Date 18Aug08              */
/*       Prev Amend No.                    Date ddMmmyy              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       256038 - Rewrite of utility to change CCSIDs for database.  */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&PLIB)
 
             DCL        VAR(&PLIB) TYPE(*CHAR) LEN(10)
 
             DCLF       FILE(SMC000062F)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2008')
 
/* Global monitor message. */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ERROR))
 
/* Build up list of save files in PLIB. */
             DLTF       FILE(QTEMP/SMC000062F)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(QTEMP/SAVF)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(UPFDBATPD) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(SMC000062F)
             DSPFD      FILE(&PLIB/*ALL) TYPE(*BASATR) +
                          OUTPUT(*OUTFILE) FILEATR(*SAVF) +
                          OUTFILE(QTEMP/SAVF) OUTMBR(*FIRST *ADD)
             MONMSG     MSGID(CPF3020) EXEC(DO)
                GOTO       CMDLBL(ENDPGM)
             ENDDO
             CPYF       FROMFILE(QTEMP/SAVF) +
                          TOFILE(QTEMP/SMC000062F) MBROPT(*REPLACE) +
                          FMTOPT(*MAP *DROP)
 
READNEXT:
             RCVF
             MONMSG     MSGID(CPF0864) EXEC(DO)
                GOTO       CMDLBL(ENDPGM)
             ENDDO
 
             DLTF       FILE(&PLIB/&ATFILE)
             MONMSG     MSGID(CPF2105)
 
             GOTO       CMDLBL(READNEXT)
 
             GOTO       CMDLBL(ENDPGM)
ERROR:
             CHGJOB     SWS(XXXXXX11)
 
ENDPGM:
             ENDPGM
