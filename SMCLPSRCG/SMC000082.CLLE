/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas SM Configure IASP System Value set up')         */
/*********************************************************************/
/*                                                                   */
/*       Midas - Implementation module                               */
/*                                                                   */
/*       SMC000082 - CPP for CFGIASP                                 */
/*                                                                   */
/*       (c) Finastra International Limited 2010                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. BUG28037 *CREATE   Date 18Aug10              */
/*                      XXXXXX             Date DDMmmYY              */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       BUG28037 - New command to set up IASP System Values.        */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&ASPGRP &ASPRDB)
 
             DCL        VAR(&ASPGRP) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ASPRDB) TYPE(*CHAR) LEN(18)
 
             DCL        VAR(&SYSVAL) TYPE(*CHAR) LEN(20)
             DCL        VAR(&SYSVALVAL) TYPE(*CHAR) LEN(200)
 
             DCL        VAR(&RECURSIVE) TYPE(*CHAR) LEN(1) VALUE(N)
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MSGF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGFLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGDTA) TYPE(*CHAR) LEN(100)
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2010')
 
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000) EXEC(GOTO +
                          CMDLBL(ERROR))
 
             CHGVAR     VAR(&SYSVAL) VALUE('IASPinstallation')
             CHGVAR     VAR(&SYSVALVAL) VALUE('Y')
             CALL       PGM(GP009510) PARM(&SYSVAL &SYSVALVAL ' ')
 
             CHGVAR     VAR(&SYSVAL) VALUE('IASPgroup')
             CHGVAR     VAR(&SYSVALVAL) VALUE(&ASPGRP)
             CALL       PGM(GP009510) PARM(&SYSVAL &SYSVALVAL ' ')
 
             CHGVAR     VAR(&SYSVAL) VALUE('IASPdatabase')
             IF         COND(&ASPRDB *EQ '*ASPGRP') THEN(DO)
                CHGVAR     VAR(&SYSVALVAL) VALUE(&ASPGRP)
             ENDDO
             ELSE       CMD(DO)
                CHGVAR     VAR(&SYSVALVAL) VALUE(&ASPRDB)
             ENDDO
             CALL       PGM(GP009510) PARM(&SYSVAL &SYSVALVAL ' ')
 
             GOTO       CMDLBL(ENDPGM)
 
ERROR:
             IF         COND(&RECURSIVE *EQ 'N') THEN(DO)
                CHGVAR     VAR(&RECURSIVE) VALUE('Y')
RCVMSG:
                RCVMSG     MSGTYPE(*DIAG) MSGDTA(&MSGDTA) MSGID(&MSGID) +
                             MSGF(&MSGF) SNDMSGFLIB(&MSGFLIB)
                IF         COND(&MSGID *NE ' ') THEN(DO)
                   SNDPGMMSG  MSGID(&MSGID) MSGF(&MSGFLIB/&MSGF) +
                                MSGDTA(&MSGDTA) MSGTYPE(*DIAG)
                   GOTO       CMDLBL(RCVMSG)
                ENDDO
                RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSGDTA) MSGID(&MSGID) +
                             MSGF(&MSGF) SNDMSGFLIB(&MSGFLIB)
                SNDPGMMSG  MSGID(&MSGID) MSGF(&MSGFLIB/&MSGF) +
                             MSGDTA(&MSGDTA) MSGTYPE(*ESCAPE)
             ENDDO
 
ENDPGM:
             ENDPGM
