/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas SM Shell to compare all users rights')          */
/*********************************************************************/
/*                                                                   */
/*       Midas - Implementation module                               */
/*                                                                   */
/*       SMC000125 - Compare all user's access rights                */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2011           */
/*                                                                   */
/*       Last Amend No. CBF005   *CREATE   Date 04Jul11              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CBF005 - BF Infrastructure: Single Security User Maintenance*/
/*                Profile                                            */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&GPFXSVAL &MODE)
 
             DCL        VAR(&GPFXSVAL) TYPE(*CHAR) LEN(20)
             DCL        VAR(&MODE) TYPE(*CHAR) LEN(4)
 
             DCL        VAR(&GSBSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&USER01) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USER02) TYPE(*CHAR) LEN(10)
             DCL        VAR(&BLANKSVAL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)
             DCL        VAR(&STMSTR) TYPE(*CHAR) LEN(80)
             DCL        VAR(&GENPGM) TYPE(*CHAR) LEN(10) +
                          VALUE('SMC000125')
 
             DCLF       FILE(UPGSVALJ0)
 
             COPYRIGHT  TEXT('(c) Misys International Banking +
                          Systems Ltd. 2011')
 
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000) EXEC(GOTO +
                          CMDLBL(ERROR))
 
             CHGJOB     SWS(00000000)
 
/** Create Midas Info Screen data area in QTEMP. */
             DLTDTAARA  DTAARA(QTEMP/MIDASMSG)
             MONMSG MSGID(CPF2105)
             CRTDTAARA  DTAARA(QTEMP/MIDASMSG) TYPE(*CHAR) LEN(800) +
                          TEXT('Midas Info Screen Data Area')
 
VALIDATE:
             RCVF
             MONMSG     MSGID(CPF0864) EXEC(DO)
                GOTO       CMDLBL(CHECK)
             ENDDO
 
             IF         COND(&GISVAL *EQ &GPFXSVAL) THEN(DO)
                IF         COND(&GIVAL *EQ ' ') THEN(DO)
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')
                ENDDO
                ELSE       CMD(DO)
                   CHGVAR     VAR(&GSBSID) VALUE(&GIVAL)
                ENDDO
             ENDDO
 
             GOTO       CMDLBL(VALIDATE)
 
CHECK:
             IF         COND(&BLANKSVAL *EQ 'Y') THEN(DO)
                RTVMSG     MSGID(UPM0003) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
                RTVMSG     MSGID(UPM0004) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (151 50)) VALUE(&MESSAGE)
                CALL       PGM(SCC0010) PARM(&GENPGM 'ENTER' ' ')
                GOTO       CMDLBL(ENDPGM)
             ENDDO
 
/** Save library list and set up global library list. */
             SAVLIBL    SAVDTL(SMC000125L)
             CALL       PGM(GPC000016) PARM(&GSBSID)
 
/** Call program that will build list of users and compare them; */
/** the mode parameter dictates whether the summary or detail    */
/** report is produced.                                          */
             CALL       PGM(SM000037) PARM(&MODE)
             IF         COND(%SWITCH(XXXXXXX1)) THEN(DO)
                GOTO       CMDLBL(ERROR)
             ENDDO
 
/** Report depending upon mode. */
             IF         COND(&MODE *EQ '*SUM') THEN(DO)
                CALL       PGM(SM000035)
             ENDDO
             IF         COND(&MODE *EQ '*DTL') THEN(DO)
                CALL       PGM(SM000045)
             ENDDO
             IF         COND(%SWITCH(XXXXXXX1)) THEN(DO)
                GOTO       CMDLBL(ERROR)
             ENDDO
 
/** Restore library list. */
             RSTLIBL    SAVEDLIBL(SMC000125L)
 
             GOTO       CMDLBL(ENDPGM)
ERROR:
             RTVMSG     MSGID(UPM0001) MSGF(UTMSGF) MSGDTA(&GENPGM) +
                          MSG(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (201 50)) VALUE('Check +
                          joblog / dump for details')
             MONMSG     MSGID(CPF0000)
             CALL       PGM(SCC0010) PARM(&GENPGM 'JOBLOG' 'Y')
             MONMSG     MSGID(CPF0000)
 
ENDPGM:
             ENDPGM
