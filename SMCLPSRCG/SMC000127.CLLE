/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas SM Populate role / permissions table')          */
/*********************************************************************/
/*                                                                   */
/*       Midas - Implementation module                               */
/*                                                                   */
/*       SMC000127 - Populate role / permissions table               */
/*                                                                   */
/*       (c) Finastra International Limited 2011                     */
/*                                                                   */
/*       Last Amend No. MD054605           Date 17Oct19              */
/*       Prev Amend No. MD046248           Date 27Oct17              */
/*                      CBF005  *CREATE    Date 04Jul11              */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*                                                                   */
/*       MD054605 - Deliverable Data Split for SDSVALPD              */
/*       MD046248 - Finastra Rebranding                              */
/*       CBF005 - BF Infrastructure: Single Security User Maintenance*/
/*                                               Profile             */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&GPFXSVAL)

             DCL        VAR(&GPFXSVAL) TYPE(*CHAR) LEN(20)

             DCL        VAR(&GSBSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&STMSTR) TYPE(*CHAR) LEN(80)
             DCL        VAR(&BLANKSVAL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)
             DCL        VAR(&GENPGM) TYPE(*CHAR) LEN(10) +
                          VALUE('SMC000127')

/**********  DCLF       FILE(UPGSVALJ0) */                                              /*MD054605*/
             DCLF       FILE(UPGSVALJ0) ALWNULL(*YES)                                   /*MD054605*/

             COPYRIGHT  TEXT('(c) Finastra International Limited +
                          2011')

             MONMSG     MSGID(CPF0000 RPG0000 MCH0000) EXEC(GOTO +
                          CMDLBL(ERROR))

             CHGJOB     SWS(00000000)

/* Create and rename temporary source file for RUNSQLSTM. */
             DLTF       FILE(QTEMP/RUNSQLSTM)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(QTEMP/RUNSQL)
             MONMSG     MSGID(CPF0000)
             CRTSRCPF   FILE(QTEMP/RUNSQLSTM) RCDLEN(112) +
                          MBR(RUNSQLSTM) TEXT('Temporary source +
                          file for SMC000127')
             RNMOBJ     OBJ(QTEMP/RUNSQLSTM) OBJTYPE(*FILE) +
                          NEWOBJ(RUNSQL)

/* Create Midas Info Screen data area in QTEMP. */
             DLTDTAARA  DTAARA(QTEMP/MIDASMSG)
             MONMSG MSGID(CPF2105)
             CRTDTAARA  DTAARA(QTEMP/MIDASMSG) TYPE(*CHAR) LEN(800) +
                          TEXT('Midas Info Screen Data Area')

VALIDATE:
             RCVF
             MONMSG     MSGID(CPF0864) EXEC(DO)
                GOTO       CMDLBL(CHECK)
             ENDDO

             IF         COND(&GISVAL *EQ &GPFXSVAL) THEN(DO)
                IF         COND(&GIVAL *EQ ' ') THEN(DO)
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')
                ENDDO
                ELSE       CMD(DO)
                   CHGVAR     VAR(&GSBSID) VALUE(&GIVAL)
                ENDDO
             ENDDO

             GOTO       CMDLBL(VALIDATE)

CHECK:
             IF         COND(&BLANKSVAL *EQ 'Y') THEN(DO)
                RTVMSG     MSGID(UPM0003) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
                RTVMSG     MSGID(UPM0004) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (151 50)) VALUE(&MESSAGE)
                CALL       PGM(SCC0010) PARM(&GENPGM 'ENTER' ' ')
                GOTO       CMDLBL(ENDPGM)
             ENDDO

/* Save library list and set up global library list. */
             SAVLIBL    SAVDTL(SMC000127L)
             CALL       PGM(GPC000016) PARM(&GSBSID)

             RTVJOBA    JOB(&JOB) USER(&USER)

             DLTF       FILE(QTEMP/SMBFRPQT)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(SMBFRPPD) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(SMBFRPQT)

/* Call program to build populate role / permissions file. */
             CALL       PGM(SM000046) PARM(&USER &JOB)
             IF         COND(%SWITCH(XXXXXXX1)) THEN(DO)
                GOTO       CMDLBL(ERROR)
             ENDDO

/* Update roles / permission file from temporary version while  */
/* removing duplicate records.                                  */
             CHGVAR     VAR(&STMSTR) VALUE('insert into SMBFRPPD +
                          (RPROLE, RPARTF) select distinct')
             CALL       PGM(UTWRTSQL) PARM(&STMSTR '*CLEAR')
             CHGVAR     VAR(&STMSTR) VALUE('RPROLE, RPARTF from +
                          SMBFRPQT')
             CALL       PGM(UTWRTSQL) PARM(&STMSTR '*WRITE')
             RUNSQLSTM  SRCFILE(QTEMP/RUNSQL) SRCMBR(RUNSQLSTM) +
                          COMMIT(*NONE)

/* Restore library list. */
             RSTLIBL    SAVEDLIBL(SMC000127L)

             GOTO       CMDLBL(ENDPGM)
ERROR:
             RTVMSG     MSGID(UPM0001) MSGF(UTMSGF) MSGDTA(&GENPGM) +
                          MSG(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (201 50)) VALUE('Check +
                          joblog / dump for details')
             MONMSG     MSGID(CPF0000)
             CALL       PGM(SCC0010) PARM(&GENPGM 'JOBLOG' 'Y')
             MONMSG     MSGID(CPF0000)

ENDPGM:
             ENDPGM
