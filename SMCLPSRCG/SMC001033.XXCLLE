/*********************************************************************/
/*S*D****CLPBASEBND***************************************************/
/*********************************************************************/
/*                                                                   */
/*       Midas - Implementation module                               */
/*                                                                   */
/*       SMC001033 - Change job descriptions' library list           */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2005           */
/*                                                                   */
/*       Last Amend No. CUP041 *REDUNDANT  Date 07Oct14              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. CSM006  *CREATE    Date 31Mar05              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CUP041 - New Bridge methodology to combine menu options     */
/*       CSM006 - Create new zone based on existing zone.            */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&NEWZONE &BASEZONE)
 
             DCL        VAR(&NEWZONE) TYPE(*CHAR) LEN(2)
             DCL        VAR(&BASEZONE) TYPE(*CHAR) LEN(2)
 
             DCL        VAR(&XLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&BXLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBDLBL) TYPE(*CHAR) LEN(2750)
             DCL        VAR(&STRING) TYPE(*CHAR) LEN(999)
             DCL        VAR(&CMD) TYPE(*CHAR) LEN(2850)
             DCL        VAR(&CMDLEN) TYPE(*DEC) LEN(15 5) VALUE(2850)
             DCL        VAR(&CHECK) TYPE(*DEC) LEN(4 0)
/* Parameters for QCLSCAN. */
             DCL        VAR(&STRLEN) TYPE(*DEC) LEN(3 0) VALUE(999)
             DCL        VAR(&STRPOS) TYPE(*DEC) LEN(3 0) VALUE(1)
             DCL        VAR(&PATTERN) TYPE(*CHAR) LEN(2)
             DCL        VAR(&PATLEN) TYPE(*DEC) LEN(2) VALUE(2)
             DCL        VAR(&TRANSLATE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&TRIM) TYPE(*CHAR) LEN(1)
             DCL        VAR(&WILD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&RESULT) TYPE(*DEC) LEN(3 0)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2005')
 
/* Declare driving file keyed on XLIB library. */
             DCLF       FILE(SMSETUL0)
 
/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CHGVAR     VAR(&XLIB) VALUE(&NEWZONE *TCAT 'XLIB')
             CHGVAR     VAR(&CMD) VALUE('CHGJOBD JOBD(' *TCAT &XLIB +
                          *TCAT '/')
             CHGVAR     VAR(&BXLIB) VALUE(&BASEZONE *TCAT 'XLIB')
             CHGVAR     VAR(&PATTERN) VALUE(&BASEZONE)
 
/* Retrieve job description library list from "based on" zone's MIDASIC. */
             RTVJOBD    JOBD(&BXLIB/MIDASIC) INLLIBL(&JOBDLBL)
             CHGVAR     VAR(&STRING) VALUE(%SST(&JOBDLBL 1 999))
 
/* Loop through job description library list changing "Based on" reference */
/* to the new zone. */
             CHGVAR     VAR(&CHECK) VALUE(1)
NEXTLIB:
             CALL       PGM(QCLSCAN) PARM(&STRING &STRLEN &STRPOS +
                          &PATTERN &PATLEN &TRANSLATE &TRIM &WILD +
                          &RESULT)
             IF         COND(&RESULT *EQ 0) THEN(DO)
                GOTO       CMDLBL(CHGJOBDLBL)
             ENDDO
             IF         COND(&RESULT *NE &CHECK) THEN(DO)
                CHGVAR     VAR(&STRPOS) VALUE(&RESULT + 1)
                GOTO       CMDLBL(NEXTLIB)
             ENDDO
             CHGVAR     VAR(%SST(&JOBDLBL &RESULT 2)) VALUE(&NEWZONE)
             CHGVAR     VAR(&CHECK) VALUE(&RESULT + 10)
 
/* Change library lists for all job descriptions. */
CHGJOBDLBL:
             RCVF
             MONMSG     MSGID(CPF0864) EXEC(DO)
                GOTO       CMDLBL(END)
             ENDDO
/* Only process job descriptions. */
             IF         COND(&DFTYPE *EQ '*JOBD') THEN(DO)
                CHGVAR     VAR(&CMD) VALUE(&CMD *TCAT &DFNAME *TCAT ') +
                             INLLIBL(' *TCAT &JOBDLBL *TCAT ')')
                CALL       PGM(QCMDEXC) PARM(&CMD &CMDLEN)
             ENDDO
 
             GOTO       CMDLBL(CHGJOBDLBL)
 
             GOTO       CMDLBL(END)
ABNOR:
             CHGJOB     SWS(XXXXXX11)
END:
             ENDPGM
