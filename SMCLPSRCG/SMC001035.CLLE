/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas SM Global Processing initial system setup')     */
/*********************************************************************/
/*                                                                   */
/*       Midas - Implementation module                               */
/*                                                                   */
/*       SMC001035 - Initial set up of Midas Global Processing layer */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2003           */
/*                                                                   */
/*       Last Amend No. CBF012             Date 04Jul11              */
/*       Prev Amend No. CSC043             Date 18Jun10              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/*                      BUG27240           Date 12Mar10              */
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/*                      247277             Date 24May07              */
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*                      CUP033             Date 08Jun06              */
/*                      CPK023             Date 13Dec05              */
/*                      CSM005             Date 21Sep05              */
/*                      CUP099             Date 12May05              */
/*                      CUP025  *CREATE    Date 28Nov03              */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*                                                                   */
/*       CBF012 - BF Infrastructure: Upgradability                   */
/*       CSC043 - MidasPlus IASP Enablement                          */
/*       BUG27240- Applied CPK029 file locking fix                   */
/*       CPK029 - Stop lock on file.                                 */
/*       247277 - Cater for native JDBC driver as well as toolbox    */
/*       CUP033 - Removal of System Value /COPYs.                    */
/*       CPK023 - Further change to pre-start job entries.           */
/*       CSM005 - Changes to /COPYs.  Recompile.                     */
/*       CUP099 - Add changes to pre-start job entries.              */
/*       CUP025 - MidasPlus Bridge changes.                          */
/*                                                                   */
/*********************************************************************/
/**********  PGM                                                                       */ /*CUP033*/
             PGM        PARM(&GPFXSVAL)                                                   /*CUP033*/
 
             DCL        VAR(&GPFXSVAL) TYPE(*CHAR) LEN(20)                                /*CUP033*/
/**COPY*UPCPYSRCG,UPGSVALDCL*                                        */ /*             */ /*CUP033*/
             DCL        VAR(&GPSBSID) TYPE(*CHAR) LEN(2)                                  /*CUP033*/
             DCL        VAR(&GPSBSID2) TYPE(*CHAR) LEN(2)                                 /*CBF012*/
             DCL        VAR(&BLANKSVAL) TYPE(*CHAR) LEN(1) VALUE('N')                     /*CUP033*/
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)
 
             DCLF       FILE(UPGSVALJ0)                                                   /*CUP033*/
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2003')
 
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CHGJOB     SWS(00000000)
 
/* Create Midas Info Screen data area in QTEMP */
             DLTDTAARA  DTAARA(QTEMP/MIDASMSG)
             MONMSG MSGID(CPF2105)
             CRTDTAARA  DTAARA(QTEMP/MIDASMSG) TYPE(*CHAR) LEN(800) +
                          TEXT('Midas Info Screen Data Area')
 
/* Retrieve Global layer system prefix. */
VALIDATE:                                                                                 /*CUP033*/
             RCVF                                                                         /*CUP033*/
             MONMSG     MSGID(CPF0864) EXEC(DO)                                           /*CUP033*/
                GOTO       CMDLBL(CHECK)                                                  /*CUP033*/
             ENDDO                                                                        /*CUP033*/
 
             IF         COND(&GISVAL *EQ &GPFXSVAL) THEN(DO)                              /*CUP033*/
                IF         COND(&GIVAL *EQ ' ') THEN(DO)                                  /*CUP033*/
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')                                  /*CUP033*/
                ENDDO                                                                     /*CUP033*/
                ELSE       CMD(DO)                                                        /*CUP033*/
                   CHGVAR     VAR(&GPSBSID) VALUE(&GIVAL)                                 /*CUP033*/
                   CHGVAR     VAR(&GPSBSID2) VALUE(&GIVAL)                                /*CBF012*/
                ENDDO                                                                     /*CUP033*/
/**********     GOTO       CMDLBL(CHECK)                                    */ /*CUP033*/ /*CPK029*/
             ENDDO                                                                        /*CUP033*/
 
             GOTO       CMDLBL(VALIDATE)                                                  /*CUP033*/
 
/**COPY*UPCPYSRCG,UPGSVALCL1*                                        */ /*             */ /*CUP033*/
/**********  IF         COND(&GPSBSID *EQ '  ') THEN(DO)                               */ /*CUP033*/
 CHECK:                                                                                   /*CUP033*/
             IF         COND(&BLANKSVAL *EQ 'Y') THEN(DO)                                 /*CUP033*/
/* Set up messages for Midas Information Screen  */
                RTVMSG     MSGID(UPM0003) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
                RTVMSG     MSGID(UPM0004) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (151 50)) VALUE(&MESSAGE)
                CALL       PGM(SCC0010) PARM('SMC001035' 'ENTER' ' ')
                GOTO       CMDLBL(ENDPGM)
             ENDDO
 
/* Add changes to pre-start job entries. */                                               /*CUP099*/
             CHGPJE     SBSD(QSYSWRK) PGM(QSQSRVR) MAXUSE(1)                              /*CUP099*/
/**********  CHGPJE     SBSD(QUSRWRK) PGM(QZDASOINIT) MAXUSE(1)             */ /*CUP099*/ /*CPK023*/
             CHGPJE     SBSD(QUSRWRK) PGM(QZDASOINIT) INLJOBS(50) +
                          MAXUSE(1)                                                       /*CPK023*/
             CHGPJE     SBSD(QSYSWRK) PGM(QSQSRVR) INLJOBS(50) +
                          MAXUSE(1)                                                       /*247277*/
 
/* Call program to create libraries for standard Midas library list  */
/*  and owning user profile, etc.                                    */
             OVRDBF     FILE(SMSETUL1) TOFILE(SMSETULI) OVRSCOPE(*JOB)
/**********  CALL       PGM(SMC1023) PARM(&GPSBSID)   */                                  /*CBF012*/
             CALL       PGM(SMC1023) PARM(&GPSBSID &GPSBSID2)                             /*CBF012*/
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                GOTO       CMDLBL(ABNOR)
             ENDDO
/*                                                                                        /*CSC043*/
/** Call program to create objects required in the GJLIB. */                              /*CSC043*/
/*                                                                                        /*CSC043*/
             CALL       PGM(SMUC00221) PARM(&GPSBSID)                                     /*CSC043*/
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)                                  /*CSC043*/
                GOTO       CMDLBL(ABNOR)                                                  /*CSC043*/
             ENDDO                                                                        /*CSC043*/
 
             GOTO       CMDLBL(ENDPGM)
ABNOR:
/* Set up messages for Midas Information Screen  */
             RTVMSG     MSGID(UPM0001) MSGF(UTMSGF) +
                          MSGDTA('SMC001035') MSG(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (201 50)) VALUE('Check +
                          joblog for details')
             MONMSG     MSGID(CPF0000)
             CALL       PGM(SCC0010) PARM('SMC001035' 'JOBLOG' 'Y')
             MONMSG     MSGID(CPF0000)
ENDPGM:
             DLTOVR     FILE(SMSETUL1) LVL(*JOB)
             MONMSG     MSGID(CPF0000)
 
             ENDPGM
