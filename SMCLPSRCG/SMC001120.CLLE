/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas SM Shell for Global layer authority setup')     */
/*********************************************************************/
/*                                                                   */
/*       Midas - Implementation module                               */
/*                                                                   */
/*       SMC001120 - Global layer authority setup                    */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2003           */
/*                                                                   */
/*       Last Amend No. AR886414           Date 05Jan12              */
/*       Prev Amend No. CSC043             Date 18Jun10              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*                      CUP033             Date 08Jun06              */
/*                      CSM005             Date 21Sep05              */
/*                      CPK020             Date 18Aug04              */
/*                      CUP025  *CREATE    Date 28Nov03              */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*                                                                   */
/*       AR886414 - Creation of CBS, CBA and BF libraries in SMSETUPD*/
/*                  not catered                                      */
/*       CSC043 - MidasPlus IASP Enablement                          */
/*       CUP033 - Removal of System Value /COPYs.                    */
/*       CSM005 - Changes to /COPYs.  Recompile.                     */
/*       CPK020 - Set up library list.                               */
/*       CUP025 - MidasPlus Bridge changes.                          */
/*                                                                   */
/*********************************************************************/
/**********  PGM                                                                       */ /*CUP033*/
/**********  PGM        PARM(&GPFXSVAL)                                     */ /*CUP033*/ /*CSC043*/
             PGM        PARM(&GPFXSVAL &IASPYNSVAL &IASPSVAL)                             /*CSC043*/
 
             DCL        VAR(&GPFXSVAL) TYPE(*CHAR) LEN(20)                                /*CUP033*/
             DCL        VAR(&IASPYNSVAL) TYPE(*CHAR) LEN(20)                              /*CSC043*/
             DCL        VAR(&IASPSVAL) TYPE(*CHAR) LEN(20)                                /*CCS043*/
/**COPY*UPCPYSRCG,UPGSVALDCL*                                        */ /*             */ /*CUP033*/
             DCL        VAR(&GPSBSID) TYPE(*CHAR) LEN(2)                                  /*CUP033*/
             DCL        VAR(&GPSBSID2) TYPE(*CHAR) LEN(2)                               /*AR886414*/
             DCL        VAR(&IASP_YN) TYPE(*CHAR) LEN(1)                                  /*CSC043*/
             DCL        VAR(&IASP) TYPE(*CHAR) LEN(10)                                    /*CSC043*/
             DCL        VAR(&BLANKSVAL) TYPE(*CHAR) LEN(1) VALUE('N')                     /*CUP033*/
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)
 
             DCLF       FILE(UPGSVALJ0)                                                   /*CUP033*/
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2003')
 
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CHGJOB     SWS(00000000)
 
/* Create Midas Info Screen data area in QTEMP */
             DLTDTAARA  DTAARA(QTEMP/MIDASMSG)
             MONMSG MSGID(CPF2105)
             CRTDTAARA  DTAARA(QTEMP/MIDASMSG) TYPE(*CHAR) LEN(800) +
                          TEXT('Midas Info Screen Data Area')
 
VALIDATE:                                                                                 /*CUP033*/
             RCVF                                                                         /*CUP033*/
             MONMSG     MSGID(CPF0864) EXEC(DO)                                           /*CUP033*/
                GOTO       CMDLBL(CHECK)                                                  /*CUP033*/
             ENDDO                                                                        /*CUP033*/
 
             IF         COND(&GISVAL *EQ &GPFXSVAL) THEN(DO)                              /*CUP033*/
                IF         COND(&GIVAL *EQ ' ') THEN(DO)                                  /*CUP033*/
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')                                  /*CUP033*/
                ENDDO                                                                     /*CUP033*/
                ELSE       CMD(DO)                                                        /*CUP033*/
                   CHGVAR     VAR(&GPSBSID) VALUE(&GIVAL)                                 /*CUP033*/
                   CHGVAR     VAR(&GPSBSID2) VALUE(&GIVAL)                              /*AR886414*/
                ENDDO                                                                     /*CUP033*/
             ENDDO                                                                        /*CUP033*/
 
             IF         COND(&GISVAL *EQ &IASPYNSVAL) THEN(DO)                            /*CSC043*/
                IF         COND(&GIVAL *EQ ' ') THEN(DO)                                  /*CSC043*/
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')                                  /*CSC043*/
                ENDDO                                                                     /*CSC043*/
                ELSE       CMD(DO)                                                        /*CSC043*/
                   CHGVAR     VAR(&IASP_YN) VALUE(&GIVAL)                                 /*CSC043*/
                ENDDO                                                                     /*CSC043*/
             ENDDO                                                                        /*CSC043*/
                                                                                          /*CSC043*/
             IF         COND(&GISVAL *EQ &IASPSVAL) THEN(DO)                              /*CSC043*/
                IF         COND((&GIVAL *EQ ' ') *AND (&IASP_YN *EQ +
                          'Y')) THEN(DO)                                                  /*CSC043*/
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')                                  /*CSC043*/
                ENDDO                                                                     /*CSC043*/
                ELSE       CMD(DO)                                                        /*CSC043*/
                   CHGVAR     VAR(&IASP) VALUE(&GIVAL)                                    /*CSC043*/
                ENDDO                                                                     /*CSC043*/
             ENDDO                                                                        /*CSC043*/
 
             GOTO       CMDLBL(VALIDATE)                                                  /*CUP033*/
 
/**COPY*UPCPYSRCG,UPGSVALCL1*                                        */ /*             */ /*CUP033*/
/**********  IF         COND(&GPSBSID *EQ '  ') THEN(DO)                               */ /*CUP033*/
 CHECK:                                                                                   /*CUP033*/
             IF         COND(&BLANKSVAL *EQ 'Y') THEN(DO)                                 /*CUP033*/
/* Set up messages for Midas Information Screen  */
                RTVMSG     MSGID(UPM0003) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
                RTVMSG     MSGID(UPM0004) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (151 50)) VALUE(&MESSAGE)
                CALL       PGM(SCC0010) PARM('SMC001120' 'ENTER' ' ')
                GOTO       CMDLBL(ENDPGM)
             ENDDO
 
/* Set up library list. */                                                                /*CPK020*/
             CALL       PGM(GPC000016) PARM(&GPSBSID)                                     /*CPK020*/
 
             IF         COND(&IASP_YN *NE 'Y') THEN(DO)                                   /*CSC043*/
                CHGVAR     VAR(&IASP) VALUE(' ')                                          /*CSC043*/
             ENDDO                                                                        /*CSC043*/
 
/* Call program to create libraries for standard Midas library list  */
/*  and owning user profile, etc.                                    */
             OVRDBF     FILE(SMSETUL6) TOFILE(SMSETULJ) OVRSCOPE(*JOB)
/**********  CALL       PGM(SMC1121) PARM(&GPSBSID)                                    */ /*CSC043*/
/**********  CALL       PGM(SMC1121) PARM(&GPSBSID &IASP) */                 /*CSC043*/ /*AR886414*/
             CALL       PGM(SMC1121) PARM(&GPSBSID &GPSBSID2 &IASP)                     /*AR886414*/
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
             GOTO       CMDLBL(ENDPGM)
ABNOR:
/* Set up messages for Midas Information Screen  */
             RTVMSG     MSGID(UPM0001) MSGF(UTMSGF) +
                          MSGDTA('SMC001120') MSG(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (201 50)) VALUE('Check +
                          joblog for details')
             MONMSG     MSGID(CPF0000)
             CALL       PGM(SCC0010) PARM('SMC001120' 'JOBLOG' 'Y')
             MONMSG     MSGID(CPF0000)
ENDPGM:
             DLTOVR     FILE(SMSETUL6) LVL(*JOB)
             MONMSG     MSGID(CPF0000)
 
             ENDPGM
