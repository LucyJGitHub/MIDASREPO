/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas SM BankFusion Pre-Tape Set-up 1')               */
/*********************************************************************/
/*                                                                   */
/*       Midas - Implementation module                               */
/*                                                                   */
/*       SMC002101 - Midas SM BankFusion Pre-Tape Set-up 1           */
/*                                                                   */
/*       (c) Finastra International Limited 2011                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. AR886414 *CREATE   Date 05Jan12              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       AR886414 - Creation of CBS, CBA and BF libraries in SMSETUPD*/
/*                  not catered                                      */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&SBSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&ZBFLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&GBFLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&GCBSLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&GCBALIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OWNR) TYPE(*CHAR) LEN(10) +
                          VALUE('MIDASOWNER')
 
             DCL        VAR(&JOBTYPE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
 
             COPYRIGHT  TEXT('(c) Finastra International Limited +
                          2011')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
             RTVJOBA    TYPE(&JOBTYPE)
             CHGJOB     SWS(XXXXXX00)
 
/* Request new system ID. */
             ?          SYSTEMID
             MONMSG     MSGID(CPF6801) EXEC(DO)
                GOTO       CMDLBL(END)
             ENDDO
             RTVDTAARA  DTAARA(QTEMP/SYSID) RTNVAR(&SBSID)
 
/* Attach prefix */
 
             CHGVAR     VAR(&ZBFLIB) VALUE(&SBSID *CAT &SBSID *CAT 'BFLIB')
             CHGVAR     VAR(&GBFLIB) VALUE(&SBSID *CAT 'BFLIB')
             CHGVAR     VAR(&GCBSLIB) VALUE(&SBSID *CAT 'CBSLIB')
             CHGVAR     VAR(&GCBALIB) VALUE(&SBSID *CAT 'CBALIB')
 
/* Create BankFusion Libraries */
 
             CHKOBJ     OBJ(&ZBFLIB) OBJTYPE(*LIB)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             CRTLIB     LIB(&ZBFLIB) TEXT('BankFusion Zone Master +
                          Schema') CRTAUT(*EXCLUDE)
             ENDDO
 
             CHKOBJ     OBJ(&GBFLIB) OBJTYPE(*LIB)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             CRTLIB     LIB(&GBFLIB) TEXT('BankFusion Global Master +
                          Schema') CRTAUT(*EXCLUDE)
             ENDDO
 
             CHKOBJ     OBJ(&GCBSLIB) OBJTYPE(*LIB)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             CRTLIB     LIB(&GCBSLIB) TEXT('BankFusion CBS Global +
                          Master schema') CRTAUT(*EXCLUDE)
             ENDDO
 
             CHKOBJ     OBJ(&GCBALIB) OBJTYPE(*LIB)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             CRTLIB     LIB(&GCBALIB) TEXT('BankFusion CBA Global +
                          Master schema') CRTAUT(*EXCLUDE)
             ENDDO
 
/* Change object ownership & auts of BankFusion Libraries */
 
             CHGOWNAUT  OBJ(QSYS/&ZBFLIB) OBJTYPE(*LIB) +
                          NEWOWN(&OWNR) PUBAUT(*USE) +
                          AUTL(*REVOKE) CHGLIB(N)
             CHGOWNAUT  OBJ(QSYS/&GBFLIB) OBJTYPE(*LIB) +
                          NEWOWN(&OWNR) PUBAUT(*USE) +
                          AUTL(*REVOKE) CHGLIB(N)
             CHGOWNAUT  OBJ(QSYS/&GCBSLIB) OBJTYPE(*LIB) +
                          NEWOWN(&OWNR) PUBAUT(*USE) +
                          AUTL(*REVOKE) CHGLIB(N)
             CHGOWNAUT  OBJ(QSYS/&GCBALIB) OBJTYPE(*LIB) +
                          NEWOWN(&OWNR) PUBAUT(*USE) +
                          AUTL(*REVOKE) CHGLIB(N)
 
             GOTO       CMDLBL(END)
 
ABNOR:
 
             CHGJOB     SWS(XXXXXX11)
 
/* Abnormal termination - batch job */
 
             IF         COND(&JOBTYPE = '0') THEN(DO)
               SNDPGMMSG MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                            SMC002101 ended abnormally - see job log') +
                            TOMSGQ(MOPERQ)
               MONMSG     MSGID(CPF0000 MCH0000)
             ENDDO
 
/* Abnormal termination - interactive job */
 
             IF         COND(&JOBTYPE = '1') THEN(DO)
 
             SNDUSRMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          SMC002101 ended abnormally - see job +
                          log')
 
             ENDDO
 
END:
 
             ENDPGM
