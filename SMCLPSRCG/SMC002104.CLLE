/*********************************************************************/
/*XBI    OVRDBF FILE(BFAOBJLST) TOFILE(UPOBJDTPD)                    */
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas SM Delete JRNRCV & LF/SYS* w/in BF Libs')       */
/*********************************************************************/
/*                                                                   */
/*       Midas - Implementation module                               */
/*                                                                   */
/*       SMC002104 - Midas SM Delete Journal receivers & SYS* views  */
/*                                                                   */
/*       (c) Finastra International Limited 2011                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. AR886414 *CREATE   Date 05Jan12              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       AR886414 - Creation of CBS, CBA and BF libraries in SMSETUPD*/
/*                  not catered                                      */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&JOBTYPE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
             COPYRIGHT  TEXT('(c) Finastra International Limited +
                          2011')
 
             DCLF       FILE(BFAOBJLST)
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
             RTVJOBA    TYPE(&JOBTYPE)
             CHGJOB     SWS(XXXXXX00)
 
/* Read through objects list */
 
READNEXT:
 
             RCVF
             MONMSG     MSGID(CPF0864) EXEC(DO)
                GOTO       CMDLBL(END)
             ENDDO
 
             IF         COND(&ODOBTP = '*JRNRCV') THEN(DO)
 
/* Delete Journal receiver */
 
             DLTJRNRCV  JRNRCV(&ODLBNM/&ODOBNM) DLTOPT(*IGNINQMSG)
 
             ENDDO
 
             IF         COND((&ODOBTP = '*FILE') *AND (&ODOBAT = +
                          'LF') *AND (%SUBSTRING(&ODOBNM 1 3) = +
                          'SYS')) THEN(DO)
 
/* Delete SYS* views */
 
             DLTF       FILE(&ODLBNM/&ODOBNM)
 
             ENDDO
 
             GOTO       CMDLBL(READNEXT)
 
             GOTO       CMDLBL(END)
 
ABNOR:
 
             CHGJOB     SWS(XXXXXX11)
 
/* Abnormal termination - batch job */
 
             IF         COND(&JOBTYPE = '0') THEN(DO)
               SNDPGMMSG MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                            SMC002101 ended abnormally - see job log') +
                            TOMSGQ(MOPERQ)
               MONMSG     MSGID(CPF0000 MCH0000)
             ENDDO
 
/* Abnormal termination - interactive job */
 
             IF         COND(&JOBTYPE = '1') THEN(DO)
 
             SNDUSRMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          SMC002101 ended abnormally - see job +
                          log')
 
             ENDDO
 
END:
 
             ENDPGM
