/*********************************************************************/
/*S*D****CLPBASE******************************************************/
/*E*I****TEXT('Midas*SM*Set*up*system*authority')*********************/
/*********************************************************************/
/*                                                                   */
/*       Midas - Implementation module                               */
/*                                                                   */
/*       SMC1121 - Set up authorities                                */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2003           */
/*                                                                   */
/*       Last Amend No. CUP042 *REDUNDANT  Date 11Nov14              */
/*       Prev Amend No. AR886414           Date 05Jan12              */
/*                      AR808720           Date 20Jul11              */
/*                      CSC043             Date 18Jun10              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*                      CUP016 *CREATE     Date 01Apr03              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CUP042 - Changes for Bridge deliverable data split          */
/*       AR886414 - Creation of CBS, CBA and BF libraries in SMSETUPD*/
/*                  not catered                                      */
/*     AR808720 - Remove prompt for authority change.                */
/*       CSC043 - MidasPlus IASP Enablement                          */
/*       CUP016 - Add implementation software to core.               */
/*                                                                   */
/*********************************************************************/
/**********  PGM        PARM(&LIBGRP)                                                  */ /*CSC043*/
/**********  PGM        PARM(&LIBGRP &IASP) */                               /*CSC043*/ /*AR886414*/
             PGM        PARM(&LIBGRP &GPSBSID &IASP)                                    /*AR886414*/
 
             DCL        VAR(&LIBGRP) TYPE(*CHAR) LEN(2)
             DCL        VAR(&GPSBSID) TYPE(*CHAR) LEN(2)                                /*AR886414*/
             DCL        VAR(&IASP) TYPE(*CHAR) LEN(10)                                    /*CSC043*/
 
             DCL        VAR(&SUBMITJOB) TYPE(*CHAR) LEN(4)                              /*AR808720*/
             DCL        VAR(&JOBNAME) TYPE(*CHAR) LEN(10)                               /*AR808720*/
             DCL        VAR(&SBMCMD) TYPE(*CHAR) LEN(300)                               /*AR808720*/
             DCL        VAR(&CMDLEN) TYPE(*DEC) LEN(15 5) VALUE(300)                    /*AR808720*/
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Misys International Banking Systems Ltd. +
                          2003')
/* Parameters for QCLSCAN. */
             DCL        VAR(&STRLEN) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&STRPOS) TYPE(*DEC) LEN(3 0) VALUE(1)
             DCL        VAR(&PATTERN) TYPE(*CHAR) LEN(2) VALUE('**')
             DCL        VAR(&PATTERN2) TYPE(*CHAR) LEN(2) VALUE('##')                   /*AR886414*/
             DCL        VAR(&PATLEN) TYPE(*DEC) LEN(2) VALUE(2)
             DCL        VAR(&TRANSLATE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&TRIM) TYPE(*CHAR) LEN(1)
             DCL        VAR(&WILD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&RESULT) TYPE(*DEC) LEN(3)
 
/* Declare driving file keyed on SETUPAUT. */
             DCLF       FILE(SMSETUL6)
 
/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* Prompt for whether jobs should be submitted. */                                      /*AR808720*/
             SUBMITJOB  ??SUBMITJOB(*N)                                                 /*AR808720*/
             MONMSG     MSGID(CPF6801) EXEC(DO)                                         /*AR808720*/
                GOTO       CMDLBL(END)                                                  /*AR808720*/
             ENDDO                                                                      /*AR808720*/
             RTVDTAARA  DTAARA(QTEMP/SUBMITJOB) RTNVAR(&SUBMITJOB)                      /*AR808720*/
 
READNEXT:
             RCVF
             MONMSG     MSGID(CPF0864) EXEC(GOTO CMDLBL(END))
 
/* Scan command to replace '**' with system ID. */
SCAN:
             CHGVAR     VAR(&RESULT) VALUE('0')
             CHGVAR     VAR(&STRLEN) VALUE(300)
             CALL       PGM(QCLSCAN) PARM(&DFCMD &STRLEN &STRPOS +
                          &PATTERN &PATLEN &TRANSLATE &TRIM &WILD +
                          &RESULT)
             IF         COND(&RESULT *GT 0) THEN(DO)
                CHGVAR     VAR(%SST(&DFCMD &RESULT 2 )) VALUE(&LIBGRP)
                GOTO       CMDLBL(SCAN)
             ENDDO
 
/* Scan command to replace '##' with system ID. */                                      /*AR886414*/
SCAN2:                                                                                  /*AR886414*/
             CHGVAR     VAR(&RESULT) VALUE('0')                                         /*AR886414*/
             CHGVAR     VAR(&STRLEN) VALUE(300)                                         /*AR886414*/
             CALL       PGM(QCLSCAN) PARM(&DFCMD &STRLEN &STRPOS +
                          &PATTERN2 &PATLEN &TRANSLATE &TRIM &WILD +
                          &RESULT)                                                      /*AR886414*/
             IF         COND(&RESULT *GT 0) THEN(DO)                                    /*AR886414*/
                CHGVAR     VAR(%SST(&DFCMD &RESULT 2 )) VALUE(&GPSBSID)                 /*AR886414*/
                GOTO       CMDLBL(SCAN2)                                                /*AR886414*/
             ENDDO                                                                      /*AR886414*/
                                                                                        /*AR886414*/
             IF         COND(&DFNAME *NE '**GXLIB' *AND &DFNAME *NE +
                          '**XLIB') THEN(DO)                                              /*CSC043*/
/* Check if the set up is using IASP. */                                                  /*CSC043*/
                IF         COND(&IASP *NE ' ') THEN(DO)                                   /*CSC043*/
                   CHGVAR     VAR(&DFCMD) VALUE(&DFCMD *BCAT 'ASP(*ASPDEV) +
                                ASPDEV(' *TCAT &IASP *TCAT ')')                           /*CSC043*/
                ENDDO                                                                     /*CSC043*/
             ENDDO                                                                        /*CSC043*/
 
             IF         COND(&SUBMITJOB *EQ '*YES') THEN(DO)                            /*AR808720*/
/* Get job name from Description field. */                                              /*AR808720*/
                CHGVAR     VAR(&JOBNAME) VALUE(%SST(&DFDESC 1 10))                      /*AR808720*/
                IF         COND(&JOBNAME *EQ ' ') THEN(DO)                              /*AR808720*/
                   CHGVAR     VAR(&JOBNAME) VALUE('SETUPAUT')                           /*AR808720*/
                ENDDO                                                                   /*AR808720*/
/* Note that the program uses QCMDEXC to submit the job rather than */                  /*AR808720*/
/*  submitting the CALL QCMDEXC; this is because the SBMJOB seems   */                  /*AR808720*/
/*  to misinterpret the command length.                             */                  /*AR808720*/
                CHGVAR     VAR(&SBMCMD) VALUE('SBMJOB CMD(' *TCAT +
                             &DFCMD *TCAT ') JOB(' *TCAT &JOBNAME +
                             *TCAT ') JOBD(SETUP) JOBQ(SMJOBQNM)')                      /*AR808720*/
                CALL       PGM(QCMDEXC) PARM(&SBMCMD 300)                               /*AR808720*/
             ENDDO                                                                      /*AR808720*/
             ELSE       CMD(DO)                                                         /*AR808720*/
                CALL       PGM(QCMDEXC) PARM(&DFCMD 300)                                /*AR808720*/
             ENDDO                                                                      /*AR808720*/
/**F3*taken*from*command*prompt.**/ /*                                               */ /*AR808720*/
/**********  MONMSG     MSGID(CPF6801)                                               */ /*AR808720*/
 
             GOTO       CMDLBL(READNEXT)
ABNOR:
             CHGJOB     SWS(XXXXXX11)
END:
             CHGVAR     VAR(&CPYFLD) VALUE('(c) +
                          Misys International Banking Systems Ltd.')
             ENDPGM
