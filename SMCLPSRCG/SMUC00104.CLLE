/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas SM Transfer and correct records of files')      */
/*********************************************************************/
/*                                                                   */
/*       Midas - Implementation module                               */
/*                                                                   */
/*       SMUC00104 - Transfer files to have correct data and correct */
/*                   records of files in alphanumeric                */
/*                                                                   */
/*       (c) Finastra International Limited 2020                     */
/*                                                                   */
/*       Last Amend No. MD059958           Date 17May22              */
/*       Prev Amend No. MD057448  *CREATE  Date 07Jan21              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD059958 - GPST and ZPST successfully run but produced dump.*/
/*                  Error in SQL insert in GZDEALSDG due to added    */
/*                  fields by CIR020.                                */
/*                - Applied for MD-60057                             */
/*       MD057448 - Component REC74 00001 failed during COB.         */
/*                  Introduce a global counter part of SMUC00105.    */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&LAYER)

             COPYRIGHT  TEXT('(c) Finastra International Limited 2020')

             DCL        VAR(&LAYER)    TYPE(*CHAR) LEN(7)

/*System prefixes declaration*/
             DCL        VAR(&GPREFIX) TYPE(*CHAR) LEN(2)
             DCL        VAR(&ZPREFIX) TYPE(*CHAR) LEN(2)

/*for AOSVALR0 params declaration*/
             DCL        VAR(&SVAL) TYPE(*CHAR) LEN(200)
             DCL        VAR(&ZVAL) TYPE(*CHAR) LEN(200)
             DCL        VAR(&AORTN) TYPE(*CHAR) LEN(7)
             DCL        VAR(&LIBR) TYPE(*CHAR) LEN(10)                                  /*MD059958*/

/* Global monitor message. */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO CMDLBL(ABNOR))


             IF         COND(&LAYER *EQ 'Global') THEN(DO)
/*Get Global system prefix*/
                CALL       PGM(GPAOSVALR0) PARM(&AORTN 'BrgMidasGlobalPrefix' +
                             &SVAL ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' +
                             ' ' ' ' ' ' ' ' ' ' ' ')
/*Get global prefix*/
             CHGVAR     VAR(&GPREFIX) VALUE(%SST(&SVAL 1 2))
             ENDDO


             IF         COND(&LAYER *EQ 'Zonal ') THEN(DO)
/*Get Zone system prefix*/
             CALL       PGM(AOSVALR0) PARM(&AORTN 'BrgMidasSystemPrefix' &ZVAL ' ' +
                          ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' +
                          ' ' ' ' ' ')
/*Get Zone prefix*/
             CHGVAR     VAR(&ZPREFIX) VALUE(%SST(&ZVAL 1 2))
             ENDDO

             CHGVAR     VAR(&LIBR) VALUE('#.' *CAT &GPREFIX *CAT 'GMLIB')               /*MD059958*/
             DLTF       FILE(QTEMP/GZDEALSDG)                                           /*MD059958*/
             MONMSG     MSGID(CPF2105)                                                  /*MD059958*/
             CPYF       FROMFILE(GZDEALSDG) TOFILE(QTEMP/GZDEALSDG) +
                          CRTFILE(*YES)                                                 /*MD059958*/
             CPYF       FROMFILE(&LIBR/GZDEALSDG) +
                          TOFILE(QTEMP/GZDEALSDG) MBROPT(*REPLACE) +
                          FMTOPT(*NOCHK)                                                /*MD059958*/
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)                                  /*MD059958*/
                                                                                        /*MD059958*/
             DLTF       FILE(QTEMP/GZDLGHISPD)                                          /*MD059958*/
             MONMSG     MSGID(CPF2105)                                                  /*MD059958*/
             CPYF       FROMFILE(GZDLGHISPD) +
                          TOFILE(QTEMP/GZDLGHISPD) CRTFILE(*YES)                        /*MD059958*/
             CPYF       FROMFILE(&LIBR/GZDLGHISPD) +
                          TOFILE(QTEMP/GZDLGHISPD) MBROPT(*REPLACE) +
                          FMTOPT(*NOCHK)                                                /*MD059958*/
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)                                  /*MD059958*/

/* Call SMU01050 to fix corrupted data */

             CALL       PGM(SMU01050) PARM(&LAYER &ZPREFIX &GPREFIX)

/* Call SMU01051 to correct data */

             CALL       PGM(SMU01051) PARM(&LAYER)

             GOTO       CMDLBL(END)

 ABNOR:
             CHGJOB     SWS(XXXXXX11)

 END:
             ENDPGM




