/*********************************************************************/
/*XBI    OVRDBF FILE(UPMBRLT) TOFILE(UPMBRLTPD)                      */
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas SM Set customers to blanks for multi-members')  */
/*********************************************************************/
/*                                                                   */
/*       Midas - Implementation module                               */
/*                                                                   */
/*       SMC000034 - Set customer to blanks for multi-members        */
/*                                                                   */
/*       (c) Finastra International Limited 2007                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/*       Prev Amend No. CSM010             Date 22Jun07              */
/*                      CSD027  *CREATE    Date 16Apr07              */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       CSM010 - Applied by CSD027.                                 */
/*       CSD027 - MidasPlus 1.3 packaging.  Lux features, changes    */
/*                for customer numbers.                              */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&TOLIB &TOFILE &RETURNCODE)
 
             DCL        VAR(&TOLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TOFILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&RETURNCODE) TYPE(*CHAR) LEN(7)
 
             DCL        VAR(&DTAQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DTAQCPY) TYPE(*CHAR) LEN(10)
 
             DCLF       FILE(UPMBRLT)
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2007')
 
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000) EXEC(GOTO +
                          CMDLBL(ERROR))
 
/* Display list of members in the new file. */
             DLTF       FILE(QTEMP/UPMBRLT)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(QTEMP/SMC000034F)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(UPMBRLTPD) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(UPMBRLT)
             DSPFD      FILE(&TOLIB/&TOFILE) TYPE(*MBRLIST) +
                          OUTPUT(*OUTFILE) OUTFILE(QTEMP/SMC000034F)
             CPYF       FROMFILE(QTEMP/SMC000034F) +
                          TOFILE(QTEMP/UPMBRLT) MBROPT(*REPLACE) +
                          FMTOPT(*MAP *DROP)
 
/* Take copy of data queue as it will be cleared each time. */
             CHGVAR     VAR(&DTAQ) VALUE('SM000044')
             CHGVAR     VAR(&DTAQCPY) VALUE('SM000044X')
             DLTDTAQ    DTAQ(QTEMP/SM000044X)
             MONMSG     MSGID(CPF0000)
             CPYDTAQ    FMDTAQ(QTEMP/&DTAQ) TODTAQ(QTEMP/SM000044X) +
                          CRTDTAQ(*YES)
READFILE:
             RCVF
             MONMSG     MSGID(CPF0864) EXEC(DO)
                GOTO       CMDLBL(ENDPGM)
             ENDDO
 
             OVRDBF     FILE(&TOFILE) TOFILE(&TOLIB/&TOFILE) +
                          MBR(&MLNAME)
             CALL       PGM(SM000041) PARM(&TOLIB &TOFILE &RETURNCODE)
             DLTOVR     FILE(&TOFILE)
             CPYDTAQ    FMDTAQ(QTEMP/&DTAQCPY) TODTAQ(QTEMP/&DTAQ)
 
             GOTO       CMDLBL(READFILE)
 
             GOTO       CMDLBL(ENDPGM)
ERROR:
             CHGVAR     VAR(&RETURNCODE) VALUE('*ERROR')
ENDPGM:
             ENDPGM
