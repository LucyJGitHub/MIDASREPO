/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas SM Handle multi-language files - zone')         */
/*********************************************************************/
/*                                                                   */
/*       Midas - Implementation module                               */
/*                                                                   */
/*       SMC000068 - Handle multi-language files                     */
/*                                                                   */
/*       (c) Finastra International Limited 2010                     */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. CUP003  *CREATE    Date 29Jul10              */
/*                      XXXXXX             Date DDMmmYY              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       CUP003 - New program to handle multi-language files         */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&PFXSVAL &OPFXSVAL)
 
             DCL        VAR(&PFXSVAL) TYPE(*CHAR) LEN(20)
             DCL        VAR(&OPFXSVAL) TYPE(*CHAR) LEN(20)
 
             DCL        VAR(&SBSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&OLDSBSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&ORLVL) TYPE(*CHAR) LEN(22)
             DCL        VAR(&FDTLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&GEN) TYPE(*CHAR) LEN(10)
             DCL        VAR(&BLANKSVAL) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)
 
             DCLF       FILE(UPSVALJ0) OPNID(A)
             DCLF       FILE(SDLANGPD) OPNID(B)
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2010')
 
/* Global monitor message. */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ERROR))
 
/* Create data area MIDASMSG in QTEMP. */
             DLTDTAARA  DTAARA(QTEMP/MIDASMSG)
             MONMSG     MSGID(CPF0000)
             CRTDTAARA  DTAARA(QTEMP/MIDASMSG) TYPE(*CHAR) LEN(800) +
                          VALUE(' ')
 
VALIDATE:
             RCVF       OPNID(A)
             MONMSG     MSGID(CPF0864) EXEC(DO)
                GOTO       CMDLBL(CHECK)
             ENDDO
 
             IF         COND(&A_GISVAL *EQ &PFXSVAL) THEN(DO)
                IF         COND(&A_GIVAL *EQ ' ') THEN(DO)
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')
                ENDDO
                ELSE       CMD(DO)
                   CHGVAR     VAR(&SBSID) VALUE(&A_GIVAL)
                ENDDO
             ENDDO
 
             IF         COND(&A_GISVAL *EQ &OPFXSVAL) THEN(DO)
                IF         COND(&A_GIVAL *EQ ' ') THEN(DO)
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')
                ENDDO
                ELSE       CMD(DO)
                   CHGVAR     VAR(&OLDSBSID) VALUE(&A_GIVAL)
                ENDDO
             ENDDO
 
             GOTO       CMDLBL(VALIDATE)
 
 CHECK:
             IF         COND(&BLANKSVAL *EQ 'Y') THEN(DO)
/* Set up messages for Midas Information Screen  */
                RTVMSG     MSGID(UPM0003) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
                RTVMSG     MSGID(UPM0004) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (151 50)) VALUE(&MESSAGE)
                CALL       PGM(SCC0010) PARM('SMC000068' 'ENTER' ' ')
                GOTO       CMDLBL(ENDPGM)
             ENDDO
 
/* Set up library list. */
             CALL       PGM(UPC0014) PARM(&SBSID)
 
/* Set up library names. */
             CHGVAR     VAR(&FDTLIB) VALUE(&OLDSBSID *TCAT 'DTALIB')
 
/* Prompt for 'GB' message files. */
             WRKOBJPDM  LIB(&FDTLIB) OBJ('*GB*') OBJTYPE(*MSGF)
 
             OVRDBF     FILE(SDLANGPD) TOFILE(&FDTLIB/SDLANGPD)
READNEXT:
             RCVF       OPNID(B)
             MONMSG     MSGID(CPF0864) EXEC(DO)
                GOTO       CMDLBL(ENDPGM)
             ENDDO
 
             IF         COND(&B_SCUNT *EQ 'GB') THEN(DO)
                GOTO       CMDLBL(READNEXT)
             ENDDO
 
             CHGVAR     VAR(&GEN) VALUE('*' *TCAT &B_SCUNT *TCAT '*')
 
             WRKOBJPDM  LIB(&FDTLIB) OBJ(&GEN) OBJTYPE(*MSGF)
 
             GOTO       CMDLBL(READNEXT)
 
             GOTO       CMDLBL(ENDPGM)
 
ERROR:
             RTVMSG     MSGID(UPM0001) MSGF(UTMSGF) +
                          MSGDTA('SMC000068') MSG(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (201 50)) VALUE('Check +
                          joblog for details.')
             MONMSG     MSGID(CPF0000)
             CALL       PGM(SCC0010) PARM('SMC000068' 'JOBLOG' 'Y')
             MONMSG     MSGID(CPF0000 MCH0000)
 
ENDPGM:
             DLTOVR     FILE(SDLANGPD)
 
             ENDPGM
