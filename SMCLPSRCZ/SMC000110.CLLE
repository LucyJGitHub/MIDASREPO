/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas SM Set on / off system set up flags')           */
/*********************************************************************/
/*                                                                   */
/*       Midas - Implementation module                               */
/*                                                                   */
/*       SMC000110 - Set on / off system set up flags                */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2010           */
/*                                                                   */
/*       Last Amend No. CUP004  *CREATE    Date 15Oct10              */
/*       Prev Amend No. xxxxxx             Date ddMmmyy              */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*                                                                   */
/*       CUP004 - New utility to replace SCBEGIN.                    */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&PFXSVAL &MODE)
 
             DCL        VAR(&PFXSVAL) TYPE(*CHAR) LEN(20)
             DCL        VAR(&MODE) TYPE(*CHAR) LEN(4)
 
             DCL        VAR(&PFX) TYPE(*CHAR) LEN(2)
             DCL        VAR(&ERRPGM) TYPE(*CHAR) LEN(10) VALUE(' ')
             DCL        VAR(&BLANKSVAL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&RECURSIVE) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)
 
             DCLF       FILE(UPSVALJ0)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2010')
 
/* Global monitor message. */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ERROR))
 
             CHGJOB     SWS(XXXXX000)
 
/* Create data area MIDASMSG in QTEMP for screen error handling. */
             DLTDTAARA  DTAARA(QTEMP/MIDASMSG)
             MONMSG     MSGID(CPF0000)
             CRTDTAARA  DTAARA(QTEMP/MIDASMSG) TYPE(*CHAR) LEN(800) +
                          VALUE(' ')
 
VALIDATE:
             RCVF
             MONMSG     MSGID(CPF0864) EXEC(DO)
                GOTO       CMDLBL(CHECK)
             ENDDO
 
             IF         COND(&GISVAL *EQ &PFXSVAL) THEN(DO)
                IF         COND(&GIVAL *EQ ' ') THEN(DO)
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')
                ENDDO
                ELSE       CMD(DO)
                   CHGVAR     VAR(&PFX) VALUE(&GIVAL)
                ENDDO
             ENDDO
 
             GOTO       CMDLBL(VALIDATE)
 
 CHECK:
             IF         COND(&BLANKSVAL *EQ 'Y') THEN(DO)
                RTVMSG     MSGID(UPM0003) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
                RTVMSG     MSGID(UPM0004) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (151 50)) VALUE(&MESSAGE)
                CALL       PGM(SCC0010) PARM('SMC000110' 'ENTER' ' ')
                GOTO       CMDLBL(ENDPGM)
             ENDDO
 
             CALL       PGM(UPC0014) PARM(&PFX)
 
             IF         COND(&MODE *EQ '*OFF') THEN(DO)
                CALL       PGM(SD9999)
                IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                   CHGVAR     VAR(&ERRPGM) VALUE('SD9999')
                   GOTO       CMDLBL(ERROR)
                ENDDO
             ENDDO
 
             IF         COND(&MODE *EQ '*ON ') THEN(DO)
                CALL       PGM(SD9998)
                IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                   CHGVAR     VAR(&ERRPGM) VALUE('SD9998')
                   GOTO       CMDLBL(ERROR)
                ENDDO
             ENDDO
 
             GOTO       CMDLBL(ENDPGM)
 
ERROR:
             IF         COND(&RECURSIVE *NE 'Y') THEN(DO)
                CHGVAR     VAR(&RECURSIVE) VALUE('Y')
                ROLLBACK
                MONMSG     MSGID(CPF0000)
                IF         COND(&ERRPGM *NE ' ') THEN(DO)
                   RTVMSG     MSGID(UPM0001) MSGF(UTMSGF) MSGDTA(&ERRPGM) +
                                MSG(&MESSAGE)
                   MONMSG     MSGID(CPF0000)
                ENDDO
                ELSE       CMD(DO)
                   RTVMSG     MSGID(UPM0001) MSGF(UTMSGF) +
                                MSGDTA('SMC000110') MSG(&MESSAGE)
                   MONMSG     MSGID(CPF0000)
                ENDDO
                CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
                MONMSG     MSGID(CPF0000)
                CHGDTAARA  DTAARA(MIDASMSG (201 50)) VALUE('Check +
                             joblog for details')
                MONMSG     MSGID(CPF0000)
                CALL       PGM(SCC0010) PARM('SMC000110' 'ENTER' ' ')
                MONMSG     MSGID(CPF0000)
             ENDDO
 
ENDPGM:
             RCLRSC
             MONMSG     MSGID(CPF0000)
 
             ENDPGM
