/*********************************************************************/
/*S*D****CLPBASE******************************************************/
/*E*I****TEXT(Midas*SM*Enter*new*Midas*system*ID')********************/
/*********************************************************************/
/*                                                                   */
/*       Midas - Implementation module                               */
/*                                                                   */
/*       SMC0002 - Enter new Midas system ID                         */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2003           */
/*                                                                   */
/*       Last Amend No. CUP042 *REDUNDANT  Date 06Nov14              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. CUP033             Date 08Jun06              */
/*                      CSM005             Date 21Sep05              */
/*                      CSM003             Date 30May05              */
/*                      CUP016 *CREATE     Date 01Apr03              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CUP042 - Changes for Bridge deliverable data split          */
/*       CUP033 - Removal of System Value /COPYs.                    */
/*       CSM005 - Changes to /COPYs.  Recompile.                     */
/*       CSM003 - Use system value rather than UPSTAT.               */
/*       CUP016 - Add implementation software to core.               */
/*                                                                   */
/*********************************************************************/
             PGM
 
/**COPY*UPCPYSRC,UPSVALDCLCL*                                        */ /*             */ /*CUP033*/
             DCL        VAR(&NEWSBSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&SYSTEMVAL) TYPE(*CHAR) LEN(200)
             DCL        VAR(&EXIST) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&REPLY1) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Misys International Banking Systems Ltd. +
                          2003')
 
/* Parameters for QCLSCAN. */
             DCL        VAR(&STRLEN) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&STRPOS) TYPE(*DEC) LEN(3 0) VALUE(1)
             DCL        VAR(&PATTERN) TYPE(*CHAR) LEN(2) VALUE('**')
             DCL        VAR(&PATLEN) TYPE(*DEC) LEN(2) VALUE(2)
             DCL        VAR(&TRANSLATE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&TRIM) TYPE(*CHAR) LEN(1)
             DCL        VAR(&WILD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&RESULT) TYPE(*DEC) LEN(3)
 
             DCLF       FILE(SMSETUL1)
 
/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* Create data area /MIDASMSG in QTEMP */
             DLTDTAARA  DTAARA(QTEMP/MIDASMSG)
             MONMSG     MSGID(CPF0000)
             CRTDTAARA  DTAARA(QTEMP/MIDASMSG) TYPE(*CHAR) LEN(800) +
                          VALUE(' ')
 
/* Request new system ID. */
             ?          SYSTEMID
             MONMSG     MSGID(CPF6801) EXEC(DO)
                GOTO       CMDLBL(END)
             ENDDO
             RTVDTAARA  DTAARA(QTEMP/SYSID) RTNVAR(&NEWSBSID)
 
/* Check if any libraries with this prefix already exist. */
CHECK:
             RCVF
             MONMSG     MSGID(CPF0864) EXEC(GOTO CMDLBL(CHGDTAARA))
 
             IF         COND(&DFTYPE *NE '*LIB') THEN(DO)
                GOTO       CMDLBL(CHECK)
             ENDDO
/* Scan object name to replace '**' with system ID. */
             CHGVAR     VAR(&RESULT) VALUE('0')
             CHGVAR     VAR(&STRLEN) VALUE(10)
             CALL       PGM(QCLSCAN) PARM(&DFNAME &STRLEN &STRPOS +
                          &PATTERN &PATLEN &TRANSLATE &TRIM &WILD +
                          &RESULT)
             IF         COND(&RESULT *GT 0) THEN(DO)
                CHGVAR     VAR(%SST(&DFNAME &RESULT 2 )) VALUE(&NEWSBSID)
/* If library does not exist then continue ... */
                CHKOBJ     OBJ(&DFNAME) OBJTYPE(*LIB)
                MONMSG     MSGID(CPF9801) EXEC(DO)
                   GOTO       CMDLBL(CHECK)
                ENDDO
/* ... otherwise flag that it does exist. */
                CHGVAR     VAR(&EXIST) VALUE('Y')
                GOTO       CMDLBL(CHECK)
             ENDDO
             ELSE       CMD(DO)
                GOTO       CMDLBL(CHECK)
             ENDDO
 
CHGDTAARA:
/* If any of the libraries exist then send warning message. */
             IF         COND(&EXIST *EQ 'Y') THEN(DO)
                CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE('One or more +
                             library with prefix' *BCAT &NEWSBSID *BCAT +
                             'already exists.')
                CHGDTAARA  DTAARA(MIDASMSG (201 50)) VALUE('Do you wish +
                          to continue?')
                CALL       PGM(SCC0010) PARM('SMC0002' 'OPTION' &REPLY1)
                IF         COND(&REPLY1 *EQ 'N') THEN(DO)
                   GOTO       CMDLBL(END)
                ENDDO
             ENDDO
 
/**Change*data*area*UPSTAT*to*store*system*ID name.**/ /*                              */ /*CSM003*/
/**********  CHGDTAARA  DTAARA(UPSTAT (62 2)) VALUE(&NEWSBSID)                         */ /*CSM003*/
 
/**Check*system*values*to*see*if*a*Midas*system*prefix*has*been entered*...**/ /*      */ /*CSM003*/
/**COPY*UPCPYSRC,UPSVALCAL1                                          */ /*             */ /*CSM003*/
/**********  IF         COND(&SBSID *EQ ' ') THEN(DO)                                  */ /*CSM003*/
/**...*and*if*it*has*not*then*enter*the*new*system*Id.**/ /*                           */ /*CSM003*/
/* Enter the new system Id into the System Value. */                                      /*CSM003*/
             CHGVAR     VAR(%SST(&SYSTEMVAL 1 2)) VALUE(&NEWSBSID)
             CALL       PGM(SD009510) PARM('BrgMidasSystemPrefix' +
                             &SYSTEMVAL ' ')
/**********  ENDDO                                                                     */ /*CSM003*/
 
             GOTO       CMDLBL(END)
 
ABNOR:
/* Set up messages for Midas Information Screen  */
             RTVMSG     MSGID(UPM0001) MSGF(UTMSGF) +
                          MSGDTA('SMC0002') MSG(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (201 50)) VALUE('Check +
                          joblog for details')
             MONMSG     MSGID(CPF0000)
             CALL       PGM(SCC0010) PARM('SMC0002' 'ENTER' ' ')
             MONMSG     MSGID(CPF0000)
 
END:
             CHGVAR     VAR(&CPYFLD) VALUE('(c) +
                          Misys International Banking Systems Ltd.')
 
             ENDPGM
