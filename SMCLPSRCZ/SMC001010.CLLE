/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas SM Shell to call Account Code convrsn. tool')   */
/*********************************************************************/
/*                                                                   */
/*       Midas - Implementation module                               */
/*                                                                   */
/*       SMC001010 - Shell to call Account Code Conversion tool      */
/*                                                                   */
/*       (c) Finastra International Limited 2006                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. CUP003             Date 15Sep10              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/*                      CUP034             Date 24Jan07              */
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*                      CSM008  *CREATE    Date 27Feb06              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       CUP003 - Keep copies in QTEMP to improve re-runnability.    */
/*       CUP034 - Reduce size of implementation library.             */
/*       CSM008 - New program to call SMC000010 from UTMENU.         */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&PFXSVAL &BRGSVAL &JOBDSVAL)
 
             DCL        VAR(&PFXSVAL) TYPE(*CHAR) LEN(20)
             DCL        VAR(&BRGSVAL) TYPE(*CHAR) LEN(20)
             DCL        VAR(&JOBDSVAL) TYPE(*CHAR) LEN(20)
             DCL        VAR(&BLANKSVAL) TYPE(*CHAR) LEN(1)
 
             DCL        VAR(&PFX) TYPE(*CHAR) LEN(2)
             DCL        VAR(&BRGLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SUBMITJOB) TYPE(*CHAR) LEN(4)
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2006')
 
             DCLF       FILE(UPSVALJ0)
 
/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CHGJOB     LOG(4 0 *SECLVL) LOGCLPGM(*YES)
             CHGJOB     SWS(XXXXXX00)
 
/* Create data area /MIDASMSG in QTEMP */
             DLTDTAARA  DTAARA(QTEMP/MIDASMSG)
             MONMSG     MSGID(CPF0000)
             CRTDTAARA  DTAARA(QTEMP/MIDASMSG) TYPE(*CHAR) LEN(800) +
                          VALUE(' ')
 
/* Prompt for whether job should be submitted. */
             SUBMITJOB  ??SUBMITJOB(*NO)
             MONMSG     MSGID(CPF6801) EXEC(DO)
                GOTO       CMDLBL(END)
             ENDDO
             RTVDTAARA  DTAARA(QTEMP/SUBMITJOB) RTNVAR(&SUBMITJOB)
 
/* Check that System Values have an entry. */
VALIDATE:
             RCVF
             MONMSG     MSGID(CPF0864) EXEC(DO)
                GOTO       CMDLBL(CHECK)
             ENDDO
 
             IF         COND(&GISVAL *EQ &PFXSVAL) THEN(DO)
                IF         COND(&GIVAL *EQ ' ') THEN(DO)
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')
                ENDDO
                ELSE       CMD(DO)
                   CHGVAR     VAR(&PFX) VALUE(&GIVAL)
                ENDDO
             ENDDO
 
             IF         COND(&GISVAL *EQ &BRGSVAL) THEN(DO)
                IF         COND(&GIVAL *EQ ' ') THEN(DO)
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')
                ENDDO
                ELSE       CMD(DO)
                   CHGVAR     VAR(&BRGLIB) VALUE(&GIVAL)
                ENDDO
             ENDDO
 
             IF         COND(&GISVAL *EQ &JOBDSVAL *AND &SUBMITJOB *EQ +
                          '*YES') THEN(DO)
                IF         COND(&GIVAL *EQ ' ') THEN(DO)
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')
                ENDDO
                ELSE       CMD(DO)
                   CHGVAR     VAR(&JOBD) VALUE(&GIVAL)
                ENDDO
             ENDDO
 
             GOTO       CMDLBL(VALIDATE)
 
 CHECK:
             IF         COND(&BLANKSVAL *EQ 'Y') THEN(DO)
                RTVMSG     MSGID(UPM0003) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
                RTVMSG     MSGID(UPM0004) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (151 50)) VALUE(&MESSAGE)
                CALL       PGM(SCC0010) PARM('SMC001010' 'ENTER' ' ')
                GOTO       CMDLBL(END)
             ENDDO
 
/* Set up library list. */                                                                /*CUP034*/
             CALL       PGM(UPC0014) PARM(&PFX)                                           /*CUP034*/
 
             IF         COND(&SUBMITJOB *EQ '*YES') THEN(DO)
/**********     SBMJOB     CMD(CALL PGM(SMC000010) PARM(&PFX &BRGLIB)) +               */ /*CUP003*/
/**********                  JOBD(&JOBD) CURLIB(&BRGLIB)                               */ /*CUP003*/
                SBMJOB     CMD(CALL PGM(SMC000010) PARM(&PFX)) +
                             JOBD(&JOBD) CURLIB(&BRGLIB)                                  /*CUP003*/
             ENDDO
 
             ELSE       CMD(DO)
/**********     CALL       PGM(SMC000010) PARM(&PFX &BRGLIB)                           */ /*CUP003*/
                CALL       PGM(SMC000010) PARM(&PFX)                                      /*CUP003*/
             ENDDO
 
             GOTO       CMDLBL(END)
ABNOR:
/* Set up messages for Midas Information Screen  */
             RTVMSG     MSGID(UPM0001) MSGF(UTMSGF) +
                          MSGDTA('SMC001010') MSG(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (201 50)) VALUE('Check +
                          joblog for details')
             MONMSG     MSGID(CPF0000)
             CALL       PGM(SCC0010) PARM('SMC001010' 'ENTER' ' ')
             MONMSG     MSGID(CPF0000)
 
END:
             ENDPGM
