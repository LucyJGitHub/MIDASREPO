/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas SM OTM Shell drop and create stored procs.')    */
/*********************************************************************/
/*                                                                   */
/*       Midas - Bridge                                              */
/*                                                                   */
/*       SMC008102 - OTM generic migration component                 */
/*                                                                   */
/*       Function: This program creates stored procedures for the    */
/*                 zone. It is similar to UPC002002.                 */
/*                                                                   */
/*       (c) Finastra International Limited 2022                     */
/*                                                                   */
/*       Last Amend No. CUP046   *CREATE   Date 18Jul22              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CUP046 - One Touch Bridge Automation Changes - Version Upg  */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&MODE &PFXSVAL &SRCSVAL &BRGSVAL +
                          &GPPFXSVAL)                                                    /*CPK025*/

             DCL        VAR(&MODE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&PFXSVAL) TYPE(*CHAR) LEN(20)                                 /*CPK025*/
             DCL        VAR(&SRCSVAL) TYPE(*CHAR) LEN(20)                                 /*CPK025*/
             DCL        VAR(&BRGSVAL) TYPE(*CHAR) LEN(20)                                 /*CPK025*/
             DCL        VAR(&GPPFXSVAL) TYPE(*CHAR) LEN(20)                               /*CPK025*/
             DCL        VAR(&COMPARE) TYPE(*CHAR) LEN(8)                                  /*CUP094*/

/**COPY*UPCPYSRC,UPSVALDCL*                                          */ /*             */ /*CPK025*/
             DCL        VAR(&SBSID) TYPE(*CHAR) LEN(2)                                    /*CPK025*/
             DCL        VAR(&PTFSRC) TYPE(*CHAR) LEN(10)                                  /*CPK025*/
             DCL        VAR(&PTFUPG) TYPE(*CHAR) LEN(10)                                  /*CPK025*/
             DCL        VAR(&GPPFX) TYPE(*CHAR) LEN(2)                                    /*CPK025*/
             DCL        VAR(&BLANKSVAL) TYPE(*CHAR) LEN(1)                                /*CPK025*/
             DCL        VAR(&CURSYSVAL) TYPE(*CHAR) LEN(200)                              /*CPK025*/
/**********  DCL        VAR(&XLIB) TYPE(*CHAR) LEN(10)  */                                /*CSC043*/
             DCL        VAR(&XPLIB) TYPE(*CHAR) LEN(10)                                   /*CSC043*/
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)

             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2004')

/**********  DCLF       FILE(UPSVALJ0) */                                      /*CPK025* *MD054605*/
             DCLF       FILE(UPSVALJ0) ALWNULL(*YES)                                    /*MD054605*/

/* Global monitor message. */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ERROR))

             CHGJOB     LOG(4 0 *SECLVL) LOGCLPGM(*YES)
             CHGJOB     SWS(XXXXXX00)

/* Create data area /MIDASMSG in QTEMP. */
             DLTDTAARA  DTAARA(QTEMP/MIDASMSG)
             MONMSG     MSGID(CPF0000)
             CRTDTAARA  DTAARA(QTEMP/MIDASMSG) TYPE(*CHAR) LEN(800) +
                          VALUE(' ')


/* Check that System Values have an entry. */                                             /*CPK025*/
VALIDATE:                                                                                 /*CPK025*/
             RCVF                                                                         /*CPK025*/
             MONMSG     MSGID(CPF0864) EXEC(DO)                                           /*CPK025*/
                GOTO       CMDLBL(CHECK)                                                  /*CPK025*/
             ENDDO                                                                        /*CPK025*/

             IF         COND(&GISVAL *EQ &PFXSVAL) THEN(DO)                               /*CPK025*/
                IF         COND(&GIVAL *EQ ' ') THEN(DO)                                  /*CPK025*/
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')                                  /*CPK025*/
                ENDDO                                                                     /*CPK025*/
                ELSE       CMD(DO)                                                        /*CPK025*/
                   CHGVAR     VAR(&SBSID) VALUE(&GIVAL)                                   /*CPK025*/
                ENDDO                                                                     /*CPK025*/
             ENDDO                                                                        /*CPK025*/

             IF         COND(&GISVAL *EQ &SRCSVAL) THEN(DO)                               /*CPK025*/
                IF         COND(&GIVAL *EQ ' ') THEN(DO)                                  /*CPK025*/
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')                                  /*CPK025*/
                ENDDO                                                                     /*CPK025*/
                ELSE       CMD(DO)                                                        /*CPK025*/
                   CHGVAR     VAR(&PTFSRC) VALUE(&GIVAL)                                  /*CPK025*/
                ENDDO                                                                     /*CPK025*/
             ENDDO                                                                        /*CPK025*/

             IF         COND(&GISVAL *EQ &BRGSVAL) THEN(DO)                               /*CPK025*/
                IF         COND(&GIVAL *EQ ' ') THEN(DO)                                  /*CPK025*/
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')                                  /*CPK025*/
                ENDDO                                                                     /*CPK025*/
                ELSE       CMD(DO)                                                        /*CPK025*/
                   CHGVAR     VAR(&PTFUPG) VALUE(&GIVAL)                                  /*CPK025*/
                ENDDO                                                                     /*CPK025*/
             ENDDO                                                                        /*CPK025*/

             IF         COND(&MODE *EQ '*IMP') THEN(DO)                                   /*CPK025*/
                IF         COND(&GISVAL *EQ &GPPFXSVAL) THEN(DO)                          /*CPK025*/
                   IF         COND(&GIVAL *EQ ' ') THEN(DO)                               /*CPK025*/
                      CHGVAR     VAR(&BLANKSVAL) VALUE('Y')                               /*CPK025*/
                   ENDDO                                                                  /*CPK025*/
                   ELSE       CMD(DO)                                                     /*CPK025*/
                      CHGVAR     VAR(&GPPFX) VALUE(&GIVAL)                                /*CPK025*/
                   ENDDO                                                                  /*CPK025*/
                ENDDO                                                                     /*CPK025*/
             ENDDO                                                                        /*CPK025*/

             GOTO       CMDLBL(VALIDATE)                                                  /*CPK025*/

 CHECK:                                                                                   /*CPK025*/
             IF         COND(&BLANKSVAL *EQ 'Y') THEN(DO)                                 /*CPK025*/
                GOTO       CMDLBL(ERROR)
             ENDDO

/* Set up zone library list. */
             CALL       PGM(UPC0014) PARM(&SBSID)

/* Delete the driving file in QTEMP if it already exists. */                              /*CUP033*/
             DLTF       FILE(QTEMP/UPDLVC*)                                               /*CUP033*/
             MONMSG     MSGID(CPF0000)                                                    /*CUP033*/

             RMVLIBLE   LIB(&PTFUPG)
             MONMSG     MSGID(CPF0000)
             ADDLIBLE   LIB(&PTFUPG) POSITION(*FIRST)

             CHGVAR     VAR(&XPLIB) VALUE(&SBSID *TCAT 'XPLIB')                           /*CSC043*/
             CHGVAR     VAR(&COMPARE) VALUE('SQLSRCZ')                                    /*CUP094*/


             IF         COND(&MODE *EQ '*IMP') THEN(DO)
/* Call GP009510 to ensure that the global System Value contains the */                   /*CPK025*/
/*  global sytem prefix so that the driving file (UPDLVCPD) can be   */                   /*CPK025*/
/*  populated.                                                       */                   /*CPK025*/
                CHGVAR     VAR(%SST(&CURSYSVAL 1 2)) VALUE(&GPPFX)                        /*CPK025*/
                CALL       PGM(GP009510) PARM('BrgMidasGlobalPrefix' +
                             &CURSYSVAL ' ')                                              /*CPK025*/
                CALL       PGM(UPC000002) PARM(&PTFSRC &COMPARE)                          /*CUP094*/
                IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                   GOTO       CMDLBL(ERROR)
                ENDDO
                OVRDBF     FILE(UPDLVCPD) TOFILE(QTEMP/UPDLVCPD) +
                             OVRSCOPE(*JOB)
             ENDDO

/* Call program to drop and create procedures. */
             CALL       PGM(UPC000001) PARM(&XPLIB &PTFSRC &COMPARE)                      /*CSC043*/
             DLTOVR     FILE(UPDLVCPD) LVL(*JOB)
             MONMSG     MSGID(CPF0000)
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                GOTO       CMDLBL(ERROR)
             ENDDO

             GOTO       CMDLBL(ENDPGM)
ERROR:
             CHGJOB     SWS(XXXXXX11)
             DMPCLPGM

ENDPGM:
             ENDPGM
