/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas SM OTM generic migration program')              */
/*********************************************************************/
/*                                                                   */
/*       Midas - Bridge                                              */
/*                                                                   */
/*       SMC008106 - OTM generic migration component                 */
/*                                                                   */
/*       Function: This program copies the content of HAF library    */
/*                 HFLIB.                                            */
/*                                                                   */
/*       (c) Finastra International Limited 2022                     */
/*                                                                   */
/*       Last Amend No. MD061322           Date 27Apr23              */
/*       Prev Amend No. CUP046   *CREATE   Date 18Jul22              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD061322 - Migration issues.                                */
/*                - Applied for MD-61330                             */
/*       CUP046 - One Touch Bridge Automation Changes - Version Upg  */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&LAYER &RETURN &PARM &GLOB &ZONE)

             DCL        VAR(&LAYER) TYPE(*CHAR) LEN(7)
             DCL        VAR(&RETURN) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FRLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TOLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PARM) TYPE(*CHAR) LEN(100)
             DCL        VAR(&GLOB) TYPE(*CHAR) LEN(2)
             DCL        VAR(&ZONE) TYPE(*CHAR) LEN(2)
             DCL        VAR(&ERRPGM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SVAL1) TYPE(*CHAR) LEN(200)
             DCL        VAR(&SVAL2) TYPE(*CHAR) LEN(200)
             DCL        VAR(&RTNCDE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&ORLVL) TYPE(*CHAR) LEN(22)
             DCL        VAR(&OLDSBSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&IASP_YN) TYPE(*CHAR) LEN(1)
             DCL        VAR(&IASP) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SBSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&LIBF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&LIBT) TYPE(*CHAR) LEN(10)

             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2022')

/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ERROR))

/* set lib variables for migration */
             CALL       PGM(AOSVALR0) PARM(&RTNCDE +
                             'BrgOldZonePrefix' &SVAL1  +
                             ' ' ' ' ' ' ' ' +
                          ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' +
                          ' ' ' ' ' ' ')
                IF         COND(%SST(&SVAL1 1 4) *EQ '*NRF' *OR &RTNCDE +
                             *NE ' ') THEN(DO)
                   GOTO       CMDLBL(ERROR)
                ENDDO

             CHGVAR     VAR(&OLDSBSID) VALUE(%SST(&SVAL1 1 2))
             CHGVAR     VAR(&SBSID) VALUE(&ZONE *TCAT 'DMLIB')

 /* Check whether system is in IASP environment. */
             CALL       PGM(GPAOSVALR0) PARM(&RTNCDE +
                          'IASPinstallation' &SVAL1 'IASPgroup' +
                          &SVAL2 ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' +
                          ' ' ' ' ' ' ' ' ' ' ' ' ' ' ')
             CHGVAR     VAR(&IASP_YN) VALUE(%SST(&SVAL1 1 1))
             IF         COND(&IASP_YN *EQ 'Y') THEN(DO)
/** Get name of IASP. */
                CHGVAR     VAR(&IASP) VALUE(%SST(&SVAL2 1 10))
             ENDDO

UPSTAT:

/* Build library names. */
             CHGVAR     VAR(&LIBF) VALUE(&OLDSBSID *TCAT 'HFLIB')
             CHGVAR     VAR(&LIBT) VALUE(&SBSID *TCAT 'HFLIB')

/* Create save file. */
             DLTF       FILE(QTEMP/HAFSAVE)
             MONMSG     MSGID(CPF0000)
             CRTSAVF    FILE(QTEMP/HAFSAVE) TEXT('Temporary save +
                          file for HFLIB')

/* Save contents of "from" HFLIB. */
             SAVOBJ     OBJ(*ALL) LIB(&LIBF) DEV(*SAVF) +
                          SAVF(QTEMP/HAFSAVE)

             MONMSG     MSGID(CPF3770) EXEC(GOTO CMDLBL(END))                           /*MD061322*/
                                                                                        /*MD061322*/
/* Restore object to "to" library. */

/*  If IASP environment */

             IF         COND(&IASP_YN *EQ 'Y') THEN(DO)
             RSTOBJ     OBJ(*ALL) SAVLIB(&LIBF) DEV(*SAVF) +
                          SAVF(QTEMP/HAFSAVE) RSTLIB(&LIBT) +
                          RSTASPDEV(&IASP)
             ENDDO
             ELSE       CMD(DO)
             RSTOBJ     OBJ(*ALL) SAVLIB(&LIBF) DEV(*SAVF) +
                          SAVF(QTEMP/HAFSAVE) RSTLIB(&LIBT)
             ENDDO


             GOTO       CMDLBL(END)

ERROR:      CHGJOB     SWS(XXXXXX11)
            DMPCLPGM

/* End program */

END:        ENDPGM

