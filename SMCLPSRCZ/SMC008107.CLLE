/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas SM OTM generic migration program')              */
/*********************************************************************/
/*                                                                   */
/*       Midas - Bridge                                              */
/*                                                                   */
/*       SMC008107 - OTM generic migration component                 */
/*                                                                   */
/*       Function: This program restores SFPRECHECK library          */
/*                                                                   */
/*       (c) Finastra International Limited 2022                     */
/*                                                                   */
/*       Last Amend No. CUP046   *CREATE   Date 18Jul22              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CUP046 - One Touch Bridge Automation Changes - Version Upg  */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&LAYER &RETURN &PARM &GLOB &ZONE)

             DCL        VAR(&LAYER) TYPE(*CHAR) LEN(7)
             DCL        VAR(&RETURN) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FRLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TOLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PARM) TYPE(*CHAR) LEN(100)
             DCL        VAR(&GLOB) TYPE(*CHAR) LEN(2)
             DCL        VAR(&ZONE) TYPE(*CHAR) LEN(2)
             DCL        VAR(&ERRPGM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PTFUPG) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OLDSBSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&LIBLIST) TYPE(*CHAR) LEN(500)
             DCL        VAR(&LIBPOS) TYPE(*DEC) LEN(3 0) VALUE(1)
             DCL        VAR(&SVAL1) TYPE(*CHAR) LEN(200)
             DCL        VAR(&SVAL2) TYPE(*CHAR) LEN(200)
             DCL        VAR(&SVAL3) TYPE(*CHAR) LEN(200)
             DCL        VAR(&RTNCDE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&ORLVL) TYPE(*CHAR) LEN(22)
             DCL        VAR(&IASP_YN) TYPE(*CHAR) LEN(1)
             DCL        VAR(&IASP) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CORLIB) TYPE(*CHAR) LEN(10)

             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2022')

/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ERROR))

/* set lib variables for migration */
             CALL       PGM(AOSVALR0) PARM(&RTNCDE +
                             'BrgDeliveredBrgLib' &SVAL1  +
                             'BrgOldZonePrefix' &SVAL2 'BrgCoreProgramLib' &SVAL3 +
                          ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' +
                          ' ' ' ' ' ' ')
                IF         COND(%SST(&SVAL1 1 4) *EQ '*NRF' *OR &RTNCDE +
                             *NE ' ') THEN(DO)
                   GOTO       CMDLBL(ERROR)
                ENDDO
                IF         COND(%SST(&SVAL2 1 4) *EQ '*NRF' *OR &RTNCDE +
                             *NE ' ') THEN(DO)
                   GOTO       CMDLBL(ERROR)
                ENDDO
                IF         COND(%SST(&SVAL3 1 4) *EQ '*NRF' *OR &RTNCDE +
                             *NE ' ') THEN(DO)
                   GOTO       CMDLBL(ERROR)
                ENDDO

             CHGVAR     VAR(&PTFUPG) VALUE(%SST(&SVAL1 1 10))
             CHGVAR     VAR(&OLDSBSID) VALUE(%SST(&SVAL2 1 10))
             CHGVAR     VAR(&LIBLIST) VALUE(&SVAL3)

 /* Check whether system is in IASP environment. */
             CALL       PGM(GPAOSVALR0) PARM(&RTNCDE +
                          'IASPinstallation' &SVAL1 'IASPgroup' +
                          &SVAL2 ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' ' +
                          ' ' ' ' ' ' ' ' ' ' ' ' ' ' ')                                  /*CSC043*/
 /* Check whether system is in IASP environment. */                                       /*CSC043*/
             CHGVAR     VAR(&IASP_YN) VALUE(%SST(&SVAL1 1 1))                             /*CSC043*/
             IF         COND(&IASP_YN *EQ 'Y') THEN(DO)                                   /*CSC043*/
/** Get name of IASP. */                                                                  /*CSC043*/
                CHGVAR     VAR(&IASP) VALUE(%SST(&SVAL2 1 10))                            /*CSC043*/
             ENDDO                                                                        /*CSC043*/
                                                                                          /*CSC043*/
/*                                                                                        /*CSC043*/
/** If IASP environment */                                                                /*CSC043*/
/*                                                                                        /*CSC043*/
             IF         COND(&IASP_YN *EQ 'Y') THEN(DO)                                   /*CSC043*/
             DLTLIB     LIB(SFPRECHECK) ASPDEV(&IASP)                                     /*CSC043*/
             MONMSG     MSGID(CPF2110)                                                    /*CSC043*/
             RSTLIB     SAVLIB(SFPRECHECK) DEV(*SAVF) +
                          SAVF(&PTFUPG/SFPRECHECK) RSTASPDEV(&IASP)                       /*CSC043*/
             ENDDO                                                                        /*CSC043*/
             ELSE       CMD(DO)                                                           /*CSC043*/
             DLTLIB     LIB(SFPRECHECK)
             MONMSG     MSGID(CPF2110)
             RSTLIB     SAVLIB(SFPRECHECK) DEV(*SAVF) +
                          SAVF(&PTFUPG/SFPRECHECK)
             ENDDO                                                                        /*CSC043*/

/* Loop through the system value to find all program libraries. */                      /*AR790665*/
RSTLIB:                                                                                 /*AR790665*/
             IF         COND(%SST(&LIBLIST &LIBPOS 10) *NE ' ') +
                          THEN(DO)                                                      /*AR790665*/
             CHGVAR     VAR(&CORLIB) VALUE(%SST(&LIBLIST &LIBPOS 10))                   /*AR790665*/
             RMVLIBLE   LIB(&CORLIB)                                                      /*CSM003*/
             MONMSG     MSGID(CPF0000)
             ADDLIBLE   LIB(&CORLIB)                                                      /*CSM003*/
             CHGVAR     VAR(&LIBPOS) VALUE(&LIBPOS + 10)                                /*AR790665*/
             GOTO       CMDLBL(RSTLIB)                                                  /*AR790665*/
             ENDDO                                                                      /*AR790665*/
             RMVLIBLE   LIB(SFPRECHECK)
             MONMSG     MSGID(CPF0000)
             ADDLIBLE   LIB(SFPRECHECK)

             CALL       PGM(SFPRECHECK)
             GOTO       CMDLBL(END)

ERROR:      CHGJOB     SWS(XXXXXX11)
            DMPCLPGM

/* End program */

END:        ENDPGM

