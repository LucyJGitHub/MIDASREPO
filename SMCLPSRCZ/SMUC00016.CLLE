/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas SM Private banking upgrade - SDACODPD')         */
/*********************************************************************/
/*                                                                   */
/*       Midas - Implementation module                               */
/*                                                                   */
/*       SMUC00016 - Private Banking upgrade for SDACODPD            */
/*                                                                   */
/*       (c) Finastra International Limited 2009                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/*       Prev Amend No. BG26993            Date 26Jan10              */
/*       Last Amend No. CSM001 *CREATE     Date 14Oct02              */
/*                      xxxxxx             Date ddMmmyy              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       BG26993 - Delivery of upgrade programs to core.             */
/*               - Renamed CPBMIGC24.                                */
/*       CSM001  - Upgrade of Private Banking layer                  */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&FLIB)
 
             DCL        VAR(&FLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&STATUS) TYPE(*CHAR) LEN(5)
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                            2009')
 
 /* Global monitor message. */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                            CMDLBL(ERROR))
 
/* Check new field exists in 'from' and 'to' files. */
             DSPFFD     FILE(&FLIB/SDACODPD) OUTPUT(*OUTFILE) +
                          OUTFILE(QTEMP/OLD_FFD)
             DSPFFD     FILE(SDACODPD) OUTPUT(*OUTFILE) +
                          OUTFILE(QTEMP/NEW_FFD)
             CALL       PGM(UP0891) PARM('A5TRAN' &STATUS)
/* Only attempt to migrate if field found in both files. */
             IF         COND(&STATUS *NE '*BOTH') THEN(DO)
                GOTO       CMDLBL(ENDPGM)
             ENDDO
 
/* Attempt to copy file to template. */
             CPYF       FROMFILE(&FLIB/SDACODPD) TOFILE(SMPBACPD) +
                          MBROPT(*REPLACE) FMTOPT(*MAP *DROP)
/* If the 'from' file is not found. */
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2802) EXEC(DO)
                GOTO       CMDLBL(ENDPGM)
             ENDDO
/* If the 'from' file is empty. */
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) EXEC(DO)
                GOTO       CMDLBL(ENDPGM)
             ENDDO
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2870) EXEC(DO)
                GOTO       CMDLBL(ENDPGM)
             ENDDO
 
/* Copy 'to' version of file to template. */
             CPYF       FROMFILE(SDACODPD) TOFILE(SMPBAC2PD) +
                          MBROPT(*REPLACE) FMTOPT(*MAP *DROP)
/* If SDACODPD file is empty. */
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) EXEC(DO)
                GOTO       CMDLBL(ENDPGM)
             ENDDO
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2870) EXEC(DO)
                GOTO       CMDLBL(ENDPGM)
             ENDDO
 
             CALL       PGM(SMU00038)
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                GOTO       CMDLBL(ERROR)
             ENDDO
 
/* Copy from template to 'to' version of file. */
             CPYF       FROMFILE(SMPBAC2PD) TOFILE(SDACODPD) +
                          MBROPT(*REPLACE) FMTOPT(*MAP *DROP)
 
             GOTO       CMDLBL(ENDPGM)
 
 ERROR:
             CHGJOB     SWS(XXXXXX11)
 
 ENDPGM:
             ENDPGM
