/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas SM Change ST record to MC in FACHISA')          */
/*********************************************************************/
/*                                                                   */
/*       Midas - Implementation module                               */
/*                                                                   */
/*       SMUC00025 - Updates multi-ccy rollover records on FACHISA.  */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2002           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/*       Last Amend No. BG26993            Date 26Jan10              */
/*       Prev Amend No. 194912 *CREATE     Date 25Feb02              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       BG26993 - Delivery of upgrade programs to core.             */
/*               - Renamed S194912C01.                               */
/*       194912  - Calls RPG program SC19491201 in update mode, which*/
/*                 then changes the action type of records on the    */
/*                 facility history file FACHISA from 'ST' to 'MC' if*/
/*                 they relate to a multi-ccy rollover.              */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&MODE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&DMLIB) TYPE(*CHAR) LEN(8)
             DCL        VAR(&DPLIB) TYPE(*CHAR) LEN(8)
             DCL        VAR(&PREF) TYPE(*CHAR) LEN(2)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                            2009')
 
/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR1))
 
             CHGVAR     VAR(&MODE) VALUE('U')
 
/**/
     SNDPGMMSG  MSG('Facility History Utility') +
                                       TOMSGQ(MRUNQ)
 
/** Get system prefix **/
             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&PREF)
 
             CHGVAR     VAR(&DMLIB) VALUE(&PREF *TCAT 'DMLIB')
             CHGVAR     VAR(&DPLIB) VALUE(&PREF *TCAT 'DPLIB')
 
/** Create backup of PF/FACHISA to PF/ZFACHISA before changing   **/
/** FAACTN from 'ST' to 'MC' for multi-currency rollovers.      **/
             DO
 
/** Create backup of PF/FACHISA to PF/ZFACHISA for update of files.**/
                CPYF       FROMFILE(&DMLIB/FACHISA) +
                             TOFILE(&DPLIB/ZFACHISA) MBROPT(*REPLACE) +
                             CRTFILE(*YES) FMTOPT(*MAP *DROP)
                MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) EXEC(CLRPFM +
                             FILE(ZFACHISA))
                MONMSG     MSGID(CPF2817 CPF3130 CPF2909) EXEC(DO)
                   SNDUSRMSG  MSG('Error occurred in copying PF/FACHISA to +
                                PF/ZFACHISA. Process terminated.') +
                                MSGTYPE(*INFO)
                   CHGJOB     SWS(XXXXXX11)
                   MONMSG     MSGID(CPF0000)
                   GOTO       CMDLBL(END)
                ENDDO
             ENDDO
 
 CONTIN1:
             CHGJOB     SWS(XXXXXX00)
             CALL       PGM(SMU00062) PARM(&MODE)
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
/** Successful update of FACHISA. Delete backup **/
             DLTF       FILE(&DPLIB/ZFACHISA)
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)
 
             GOTO       CMDLBL(END)
 
ABNOR:
             CHGJOB     SWS(XXXXXX11)
             MONMSG     MSGID(CPF0000)
 
 /** Restore backup of files **/
             DO
 
                CPYF       FROMFILE(&DPLIB/ZFACHISA) +
                             TOFILE(&DMLIB/FACHISA) MBROPT(*REPLACE) +
                             FMTOPT(*MAP *DROP)
                MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) EXEC(DO)
                   GOTO       CMDLBL(DSPERR2)
                ENDDO
                MONMSG     MSGID(CPF2817 CPF3130 CPF2909) EXEC(GOTO +
                             CMDLBL(DSPERR2))
             ENDDO
 
             GOTO       CMDLBL(END)
 
DSPERR2:
             SNDUSRMSG  MSG('Error occurred in restoring files.  +
                          Please restore the files manually:  +
                          PF/ZFACHISA to PF/FACHISA') +
                          MSGTYPE(*INFO)
 ABNOR1:
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          SMUC00025 ended abnormally - see job +
                          log') TOMSGQ(MOPERQ)
             CHGJOB     SWS(XXXXXX11)
             MONMSG     MSGID(CPF0000)
 
 END:
             ENDPGM
