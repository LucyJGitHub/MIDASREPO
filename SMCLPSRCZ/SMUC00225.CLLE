/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Utility to populate front office ID in GLCOLAPD')     */
/*********************************************************************/
/*                                                                   */
/*       Midas - FC Module                                           */
/*                                                                   */
/*       SMUC000225 - Utility to populate blank front office         */
/*                    transaction ID in file GLCOLAPD                */
/*                                                                   */
/*       (c) Finastra International Limited 2010                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. BUG28101 *CREATE   Date 06Sep10              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*  BUG28101 - Error in component GOC01000 00066                     */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&INVLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DMLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PRE) TYPE(*CHAR) LEN(2)
 
             DCL        VAR(&PRTCD) TYPE(*CHAR) LEN(7)
             DCL        VAR(&POP01) TYPE(*CHAR) LEN(20)
             DCL        VAR(&POP02) TYPE(*CHAR) LEN(20)
             DCL        VAR(&POP03) TYPE(*CHAR) LEN(20)
             DCL        VAR(&POP04) TYPE(*CHAR) LEN(20)
             DCL        VAR(&POP05) TYPE(*CHAR) LEN(20)
             DCL        VAR(&POP06) TYPE(*CHAR) LEN(20)
             DCL        VAR(&POP07) TYPE(*CHAR) LEN(20)
             DCL        VAR(&POP08) TYPE(*CHAR) LEN(20)
             DCL        VAR(&POP09) TYPE(*CHAR) LEN(20)
             DCL        VAR(&POP10) TYPE(*CHAR) LEN(20)
             DCL        VAR(&PVL01) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVL02) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVL03) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVL04) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVL05) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVL06) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVL07) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVL08) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVL09) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVL10) TYPE(*CHAR) LEN(200)
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2010')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CHGJOB     SWS(00000000)
 
/** Invalid parameters **/
 
/* Create data area LDA */
 
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
             MONMSG     MSGID(CPF1023)
 
/* Save a copy of the update files in INVLIB */
 
             CHGVAR     VAR(&PRTCD) VALUE(' ')
             CHGVAR     VAR(&POP01) VALUE('BrgMidasSystemPrefix')
             CALL       PGM(AOSVALR0) PARM(&PRTCD &POP01 &PVL01 +
                          &POP02 &PVL02 &POP03 &PVL03 &POP04 &PVL04 +
                          &POP05 &PVL05 &POP06 &PVL06 &POP07 &PVL07 +
                          &POP08 &PVL08 &POP09 &PVL09 &POP10 &PVL10)
 
             IF         COND(&PRTCD *NE '       ') THEN(GOTO CMDLBL(ABNOR))
 
             CHGVAR     VAR(&PRE) VALUE(%SST(&PVL01 1 2))
             CHGVAR     VAR(&INVLIB) VALUE(&PRE *TCAT 'INVLIB')
             CHGVAR     VAR(&DMLIB) VALUE(&PRE *TCAT 'DMLIB')
 
             CRTDUPOBJ  OBJ(GLCOLAPD) FROMLIB(&DMLIB) OBJTYPE(*FILE) +
                          TOLIB(&INVLIB) NEWOBJ(BKGLCOLAPD) DATA(*YES)
 
             MONMSG     MSGID(CPF4128 CPF2130 CPD2104) EXEC(DO)
             SNDPGMMSG  MSG('Failed to create backup file +
                          BKGLCOLAPD, exiting program') +
                          TOMSGQ(MOPERQ MRUNQ)
             GOTO       CMDLBL(END)
             ENDDO
 
/* Call update program */
 
             CALL       PGM(SMU00225) PARM('U')
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                           CMDLBL(ABNOR))
 
             GOTO       CMDLBL(END)
 
/* Abnormal termination */
 
 ABNOR:
 
/* Restore data to update files */
 
             CPYF       FROMFILE(&INVLIB/BKGLCOLAPD) +
                          TOFILE(&DMLIB/GLCOLAPD) MBROPT(*REPLACE) +
                          FMTOPT(*MAP *DROP)
             MONMSG     MSGID(CPF2802)
 
             CHGJOB     SWS(XXXXXX11)
             DMPCLPGM
             MONMSG     MSGID(CPF0000 MCH0000)
 
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          SMUC00225 ended abnormally - see joblog +
                          ') TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
/* End program */
 
 END:
 
/* Delete backup files */
 
             DLTF       FILE(&INVLIB/BKGLCOLAPD)
             MONMSG     MSGID(CPF0000)
 
             ENDPGM
