/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas SM Utility to move mkt cnt dtaara DP->DM')      */
/*********************************************************************/
/*                                                                   */
/*       Midas - FF Module                                           */
/*                                                                   */
/*       SMUC00226 - Utility to move Market Centre data area from    */
/*                    xxDPLIB to XXDMLIB                             */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2010           */
/*                                                                   */
/*       Last Amend No. BUG28116 *CREATE   Date 08Sep10              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*  BUG28116 - The market centre entered is not currently available  */
/*             for transaction maintenance                           */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&FRLIB) TYPE(*CHAR) LEN(7)
             DCL        VAR(&TOLIB) TYPE(*CHAR) LEN(7)
             DCL        VAR(&SDSTAT) TYPE(*CHAR) LEN(256)
 
             DCLF       FILE(MKCTL)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2010')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CHGJOB     SWS(00000000)
 
/* Create data area LDA */
 
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
             MONMSG     MSGID(CPF1023)
 
             MONMSG     MSGID(CPF8351)
 
             RTVDTAARA  DTAARA(SDSTAT *ALL) RTNVAR(&SDSTAT)
             CHGVAR     VAR(&FRLIB) VALUE(%SST(&SDSTAT 6 2) *CAT 'DPLIB')
             CHGVAR     VAR(&TOLIB) VALUE(%SST(&SDSTAT 6 2) *CAT 'DMLIB')
 
/* Read records from MKCTLD, for each record read delete data area from xxDPLIB and         */
/* create a new data area in xxDMLIB where MKCI is not equal to 'S' or is not equal t0 '*'  */
 
 READNXT:       RCVF
 
                MONMSG     MSGID(CPF0864) EXEC(DO)
                           GOTO       CMDLBL(END)
                ENDDO
 
/* Do not process delete records or market centre whose status is 'setup' */
                IF         COND(&MKCI *EQ 'S' *OR &RECI *EQ '*') THEN(DO)
                           GOTO       CMDLBL(READNXT)
                ENDDO
 
                DLTDTAARA  DTAARA(&FRLIB/&MRKT)
                MONMSG     MSGID(CPF2105)
 
                CRTDTAARA  DTAARA(&TOLIB/&MRKT) TYPE(*CHAR) LEN(2) AUT(*LIBCRTAUT)
                MONMSG     MSGID(CPF1023)
 
 
                GOTO       CMDLBL(READNXT)
 
/* Abnormal termination */
 
 ABNOR:
             CHGJOB     SWS(XXXXXX11)
             DMPCLPGM
             MONMSG     MSGID(CPF0000 MCH0000)
 
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          SMUC000216 ended abnormally - see joblog +
                          ') TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
/* End program */
 
 END:
             MONMSG     MSGID(CPF0000 MCH0000)
 
             ENDPGM
