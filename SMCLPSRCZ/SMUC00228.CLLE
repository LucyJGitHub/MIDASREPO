/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Utility to populate CLGLAC in RECMHLPD')              */
/*********************************************************************/
/*                                                                   */
/*       Midas - FC Module                                           */
/*                                                                   */
/*       SMUC00228 - Utility to populate CLGLAC in RECMHLPD,         */
/*                     RECMHLTPD and RELCMHLPD                       */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2010           */
/*                                                                   */
/*       Last Amend No. AR676213 *CREATE   Date 19Nov10              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       AR676213 - Incorrect definition of CLGLAI field             */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&JOBTYPE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
             DCL        VAR(&INVLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DMLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DPLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PRE) TYPE(*CHAR) LEN(2)
             DCL        VAR(&PRTCD) TYPE(*CHAR) LEN(7)
             DCL        VAR(&POP01) TYPE(*CHAR) LEN(20)
             DCL        VAR(&POP02) TYPE(*CHAR) LEN(20)
             DCL        VAR(&POP03) TYPE(*CHAR) LEN(20)
             DCL        VAR(&POP04) TYPE(*CHAR) LEN(20)
             DCL        VAR(&POP05) TYPE(*CHAR) LEN(20)
             DCL        VAR(&POP06) TYPE(*CHAR) LEN(20)
             DCL        VAR(&POP07) TYPE(*CHAR) LEN(20)
             DCL        VAR(&POP08) TYPE(*CHAR) LEN(20)
             DCL        VAR(&POP09) TYPE(*CHAR) LEN(20)
             DCL        VAR(&POP10) TYPE(*CHAR) LEN(20)
             DCL        VAR(&PVL01) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVL02) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVL03) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVL04) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVL05) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVL06) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVL07) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVL08) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVL09) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PVL10) TYPE(*CHAR) LEN(200)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2010')
 
/* Global monitor message. */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CHGJOB     SWS(XXXXXX00)
 
/* Save a copy of the update files in INVLIB */
 
             CHGVAR     VAR(&PRTCD) VALUE(' ')
             CHGVAR     VAR(&POP01) VALUE('BrgMidasSystemPrefix')
             CALL       PGM(AOSVALR0) PARM(&PRTCD &POP01 &PVL01 +
                          &POP02 &PVL02 &POP03 &PVL03 &POP04 &PVL04 +
                          &POP05 &PVL05 &POP06 &PVL06 &POP07 &PVL07 +
                          &POP08 &PVL08 &POP09 &PVL09 &POP10 &PVL10)
 
             IF         COND(&PRTCD *NE '       ') THEN(GOTO CMDLBL(ABNOR))
 
             CHGVAR     VAR(&PRE) VALUE(%SST(&PVL01 1 2))
             CHGVAR     VAR(&INVLIB) VALUE(&PRE *TCAT 'INVLIB')
             CHGVAR     VAR(&DMLIB) VALUE(&PRE *TCAT 'DMLIB')
             CHGVAR     VAR(&DPLIB) VALUE(&PRE *TCAT 'DPLIB')
 
             CRTDUPOBJ  OBJ(RECMHLPD) FROMLIB(&DMLIB) OBJTYPE(*FILE) +
                          TOLIB(&INVLIB) NEWOBJ(BKRECMHLPD) DATA(*YES)
 
             MONMSG     MSGID(CPF4128 CPF2130 CPD2104) EXEC(DO)
             SNDPGMMSG  MSG('Failed to create backup file +
                          BKRECMHLPD, exiting program') +
                          TOMSGQ(MOPERQ MRUNQ)
             GOTO       CMDLBL(END)
             ENDDO
 
             CRTDUPOBJ  OBJ(RECMHLTPD) FROMLIB(&DPLIB) OBJTYPE(*FILE) +
                          TOLIB(&INVLIB) NEWOBJ(BKRECMHLT) DATA(*YES)
 
             MONMSG     MSGID(CPF4128 CPF2130 CPD2104) EXEC(DO)
             SNDPGMMSG  MSG('Failed to create backup file +
                          BKRECMHLT, exiting program') +
                          TOMSGQ(MOPERQ MRUNQ)
             GOTO       CMDLBL(END)
             ENDDO
 
             CRTDUPOBJ  OBJ(RELCMHLPD) FROMLIB(&DPLIB) OBJTYPE(*FILE) +
                          TOLIB(&INVLIB) NEWOBJ(BKRELCMHL) DATA(*YES)
 
             MONMSG     MSGID(CPF4128 CPF2130 CPD2104) EXEC(DO)
             SNDPGMMSG  MSG('Failed to create backup file +
                          BKRELCMHL, exiting program') +
                          TOMSGQ(MOPERQ MRUNQ)
             GOTO       CMDLBL(END)
             ENDDO
 
             CALL       PGM(SMU00228)
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                           CMDLBL(ABNOR))
 
             GOTO       CMDLBL(END)
 
ABNOR:
 
/* Restore data to update files */
 
             CPYF       FROMFILE(&INVLIB/BKRECMHLPD) +
                          TOFILE(&DMLIB/RECMHLPD) MBROPT(*REPLACE) +
                          FMTOPT(*MAP *DROP)
             MONMSG     MSGID(CPF2802 CPF2869 CPF2817 CPF3130 CPF2909)
 
             CPYF       FROMFILE(&INVLIB/BKRECMHLT) +
                          TOFILE(&DMLIB/RECMHLTPD) MBROPT(*REPLACE) +
                          FMTOPT(*MAP *DROP)
             MONMSG     MSGID(CPF2802 CPF2869 CPF2817 CPF3130 CPF2909)
 
             CPYF       FROMFILE(&INVLIB/BKRELCMHL) +
                          TOFILE(&DMLIB/RELCMHLPD) MBROPT(*REPLACE) +
                          FMTOPT(*MAP *DROP)
             MONMSG     MSGID(CPF2802 CPF2869 CPF2817 CPF3130 CPF2909)
 
             CHGJOB     SWS(XXXXXX11)
             DMPCLPGM
             MONMSG     MSGID(CPF0000 MCH0000)
 
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          SMUC00228 ended abnormally - see joblog +
                          ') TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
END:
 
/* Delete backup files */
 
             DLTF       FILE(&INVLIB/BKRECMHLPD)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(&INVLIB/BKRECMHLT)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(&INVLIB/BKRELCMHL)
             MONMSG     MSGID(CPF0000)
 
             ENDPGM
