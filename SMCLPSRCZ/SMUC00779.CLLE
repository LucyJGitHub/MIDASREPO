/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Utility to update DRCD and OLDR in ACCNTAB')          */
/*********************************************************************/
/*                                                                   */
/*       Midas - Implementation module                               */
/*                                                                   */
/*       SMUC00779 - Utility to update DRCD and OLDR in ACCNTAB      */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2016           */
/*                                                                   */
/*       Last Amend No. MD038563 *CREATE   Date 29Jun16              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD038563 - Update DRCD and OLDR in ACCNTAB                  */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&MODE)

             DCL        VAR(&MODE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&INVLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DMLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PREF) TYPE(*CHAR) LEN(2)

             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2016')

/* Global monitor message. */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))

             CHGJOB     SWS(XXXXXX00)
/**/
/* Create LDA if it doesn't exist */
/**/
             CHKOBJ     OBJ(QTEMP/LDA) OBJTYPE(*DTAARA)
             MONMSG     MSGID(CPF9815 CPF9801) EXEC(DO)
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
             ENDDO
/**/
/* Passed parameter is invalid */
/**/
             IF         COND((&MODE *NE 'U') *AND (&MODE *NE 'u') +
                          *AND (&MODE *NE 'A') *AND (&MODE *NE +
                          'a')) THEN(DO)
             SNDPGMMSG  MSG('SMUC00779 - Invalid parameter passed') +
                          TOMSGQ(MOPERQ MRUNQ)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Invalid +
                          parameter. It should either be ''U'' or +
                          ''u'' for Update and ''A'' or ''a'' for +
                          Audit.') MSGTYPE(*ESCAPE)
             GOTO       CMDLBL(END)
             ENDDO
/**/
/* Back-up the files that will be updated */
/**/
             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&PREF)
             CHGVAR     VAR(&DMLIB) VALUE(&PREF *TCAT 'DMLIB')
             CHGVAR     VAR(&INVLIB) VALUE(&PREF *TCAT 'INVLIB')

             CRTDUPOBJ  OBJ(ACCNTAB) FROMLIB(&DMLIB) OBJTYPE(*FILE) +
                          TOLIB(&INVLIB) NEWOBJ(ACCNTABBAK) DATA(*YES)

             MONMSG     MSGID(CPF4128 CPF2130 CPD2104) EXEC(DO)
             SNDPGMMSG  MSG('Failed to create backup file +
                          REHISPS, exiting program') +
                          TOMSGQ(MOPERQ MRUNQ)
             GOTO       CMDLBL(END)
             ENDDO

/* Excecute program to update PDID and PCID in REHISPS */

             CHGJOB     SWS(XXXXXX00)
             CALL       PGM(SMU00779) PARM(&MODE)

             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                           CMDLBL(ABNOR))

             GOTO       CMDLBL(END)

ABNOR:

/* Restore data to update files */

             CPYF       FROMFILE(&INVLIB/ACCNTABBAK) +
                          TOFILE(&DMLIB/ACCNTAB) MBROPT(*REPLACE) +
                          FMTOPT(*MAP *DROP)
             MONMSG     MSGID(CPF2802 CPF2869 CPF2817 CPF3130 CPF2909)

             CHGJOB     SWS(XXXXXX11)
             DMPCLPGM
             MONMSG     MSGID(CPF0000 MCH0000)

             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          SMUC00779 ended abnormally - see joblog +
                          ') TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)

END:

/* Delete backup files */

             DLTF       FILE(&INVLIB/ACCNTABBAK)
             MONMSG     MSGID(CPF0000)

             ENDPGM
