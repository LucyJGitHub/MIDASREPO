/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('CL Program to to populate SCRMVDPD')                  */
/*********************************************************************/
/*                                                                   */
/*       Midas - General Ledger Module                               */
/*                                                                   */
/*       SMUC90014 - CL Program to to populate SCRMVDPD              */
/*                                                                   */
/*       (c) Finastra International Limited 2023                     */
/*                                                                   */
/*       Last Amend No. MD060752 *CREATE   Date 23Jan23              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD060752 - System Values overwritten in PEA system.         */
/*                  Run Utility to populate SCRMVDPD  with BTD/XTD   */
/*                  DDS files                                        */
/*                                                                   */
/*********************************************************************/
             PGM

             DCL        VAR(&DILIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DTALIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PRE) TYPE(*CHAR) LEN(2)
             DCL        VAR(&SDSTAT) TYPE(*CHAR) LEN(256)
             DCL        VAR(&SQL) TYPE(*CHAR) LEN(80)

             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) Finastra +
                          International Limited 2023')


/**/
/** Global monitor message */
/**/

             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))

             SNDPGMMSG  MSG('SMUC90014 - Datapatch to populate +
                          SCRMVDPD') TOMSGQ(MRUNQ MOPERQ)

             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(LDA) VALUE(*BLANK)
             CHGJOB     SWS(XXXXXX00)


/**/
/** Save copies of SCRMVDPD */
/**/

                RTVDTAARA  DTAARA(SDSTAT) RTNVAR(&SDSTAT)
                CHGVAR     VAR(&PRE) VALUE(%SST(&SDSTAT 6 2))
                CHGVAR     VAR(&DILIB) VALUE(&PRE *TCAT 'DILIB')
                CHGVAR     VAR(&DTALIB) VALUE(&PRE *TCAT 'DTALIB')

                CRTDUPOBJ  OBJ(SCRMVDPD) FROMLIB(&DTALIB) +
                             OBJTYPE(*FILE) TOLIB(&DILIB) +
                             NEWOBJ(SCRMVDPDBK) DATA(*YES)
                MONMSG     MSGID(CPF2130 CPD2104) EXEC(DO)
                   SNDPGMMSG  MSG('FILE SCRMVDPDBK already exists.  +
                             Backup not created.') TOMSGQ(MOPERQ MRUNQ)
                   GOTO       CMDLBL(END)
                ENDDO

 /* First set up the file used for the RUNSQLSTM command.  */
             DLTF       FILE(QTEMP/RUNSQLSTM)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(QTEMP/RUNSQL)
             MONMSG     MSGID(CPF0000)
             CRTSRCPF   FILE(QTEMP/RUNSQLSTM) RCDLEN(112) +
                          MBR(RUNSQLSTM)
             RNMOBJ     OBJ(QTEMP/RUNSQLSTM) OBJTYPE(*FILE) +
                          NEWOBJ(RUNSQL)

             DSPOBJD    OBJ(&DTALIB/*ALL) OBJTYPE(*FILE) +
                          OUTPUT(*OUTFILE) OUTFILE(QTEMP/DDSFILEZ)

 /* Insert Zonal  BTD files */
             CHGVAR     VAR(&SQL) VALUE('INSERT INTO SCRMVDPD +
                          (PEOBJ) SELECT ODOBNM FROM DDSFILEZ WHERE')
             CALL       PGM(UTWRTSQL) PARM(&SQL '*CLEAR')
             CHGVAR     VAR(&SQL) VALUE('ODOBNM LIKE ''%CTD%''')
             CALL       PGM(UTWRTSQL) PARM(&SQL '*WRITE')

             RUNSQLSTM  SRCFILE(QTEMP/RUNSQL) SRCMBR(RUNSQLSTM) +
                          COMMIT(*CHG) NAMING(*SYS)
             MONMSG     MSGID(SQL9010) EXEC(GOTO ABNOR)

             CHGVAR     VAR(&SQL) VALUE('UPDATE SCRMVDPD  SET +
                          PEOBJ = REPLACE(PEOBJ,''CTD'', ''BTD'') +
                          WHERE')
             CALL       PGM(UTWRTSQL) PARM(&SQL '*CLEAR')
             CHGVAR     VAR(&SQL) VALUE('PEOBJ LIKE ''%CTD%''')
             CALL       PGM(UTWRTSQL) PARM(&SQL '*WRITE')

             RUNSQLSTM  SRCFILE(QTEMP/RUNSQL) SRCMBR(RUNSQLSTM) +
                          COMMIT(*CHG) NAMING(*SYS)
             MONMSG     MSGID(SQL9010) EXEC(GOTO ABNOR)

  /* Insert Zonal XTD files */

             CHGVAR     VAR(&SQL) VALUE('INSERT INTO SCRMVDPD +
                          (PEOBJ) SELECT ODOBNM FROM DDSFILEZ WHERE')
             CALL       PGM(UTWRTSQL) PARM(&SQL '*CLEAR')
             CHGVAR     VAR(&SQL) VALUE('ODOBNM LIKE ''%CTD%''')
             CALL       PGM(UTWRTSQL) PARM(&SQL '*WRITE')

             RUNSQLSTM  SRCFILE(QTEMP/RUNSQL) SRCMBR(RUNSQLSTM) +
                          COMMIT(*CHG) NAMING(*SYS)
             MONMSG     MSGID(SQL9010) EXEC(GOTO ABNOR)

             CHGVAR     VAR(&SQL) VALUE('UPDATE SCRMVDPD  SET +
                          PEOBJ = REPLACE(PEOBJ,''CTD'', ''XTD'') +
                          WHERE')
             CALL       PGM(UTWRTSQL) PARM(&SQL '*CLEAR')
             CHGVAR     VAR(&SQL) VALUE('PEOBJ LIKE ''%CTD%''')
             CALL       PGM(UTWRTSQL) PARM(&SQL '*WRITE')

             RUNSQLSTM  SRCFILE(QTEMP/RUNSQL) SRCMBR(RUNSQLSTM) +
                          COMMIT(*CHG) NAMING(*SYS)
             MONMSG     MSGID(SQL9010) EXEC(GOTO ABNOR)

  /* Update type column */
             CHGVAR     VAR(&SQL) VALUE('UPDATE SCRMVDPD SET PETYPE +
                          = ''*FILE'' WHERE PETYPE = ''    ''')
             CALL       PGM(UTWRTSQL) PARM(&SQL '*CLEAR')

             RUNSQLSTM  SRCFILE(QTEMP/RUNSQL) SRCMBR(RUNSQLSTM) +
                          COMMIT(*CHG) NAMING(*SYS)
             MONMSG     MSGID(SQL9010) EXEC(GOTO ABNOR)

             DLTF       FILE(&DILIB/SCRMVDPDBK)

             GOTO       CMDLBL(END)

 ABNOR:      SNDPGMMSG  MSG('Program ended abnormally, please +
                          contact support desk')
             CPYF       FROMFILE(&DILIB/SCRMVDPDBK) +
                             TOFILE(&DTALIB/SCRMVDPD) MBROPT(*REPLACE) +
                             FMTOPT(*MAP *DROP)

             DLTF       FILE(&DILIB/SCRMVDPDBK)

 END:        CHGVAR     VAR(&CPYFLD) VALUE('(c) Finastra International +
                          Limited 2023')
             ENDPGM

