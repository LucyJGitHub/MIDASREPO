/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Utility to Correct HKRCFR and HKRCDM of MMDELDPP')    */
/*********************************************************************/
/*                                                                   */
/*       Midas - Money Market                                        */
/*                                                                   */
/*       SMUC90015 - Midas DL MD-45018 Utility to Correct HKRCFR     */
/*                   and HKRCDM of MMDELDPP                          */
/*                                                                   */
/*       This program lists and updates all fixed-term deals on      */
/*       PF/MMDELDPP that have different values of Rate Change       */
/*       Frequency and  Rate Change Days with those on PF/DEALSDC.   */
/*                                                                   */
/*       (c) Finastra International Limited 2018                     */
/*                                                                   */
/*       Last Amend No. MD045018  *CREATE   Date 03Feb23             */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*                                                                   */
/*       MD045018 - MM deal, with a Base Rate Code, could not be     */
/*                  amended due to blank Rate Change Date.           */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&MODE)

             COPYRIGHT  TEXT('(c) Finastra International Limited 2023')

             DCL        VAR(&MODE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&PRFX)  TYPE(*CHAR) LEN(2)
             DCL        VAR(&DPLIB) TYPE(*CHAR) LEN(7)
             DCL        VAR(&DMLIB) TYPE(*CHAR) LEN(7)

/* Global monitor message */

             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))

/** Retrieve System Prefix */

             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&PRFX)

             CHGVAR     VAR(&DMLIB) VALUE(&PRFX *TCAT 'DMLIB')
             CHGVAR     VAR(&DPLIB) VALUE(&PRFX *TCAT 'DPLIB')

/* Main Processing */
             CHGJOB     SWS(XXXXXX00)

/* Create data area LDA */

             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
             MONMSG     MSGID(CPF1023)

             SNDPGMMSG MSG('Utility to Correct HKRCFR and HKRCDM of MMDELDPP - Report') +
                       TOMSGQ(MOPERQ MRUNQ)

/* Check if parameter passed is valid */
             IF         COND((&MODE *NE 'U') *AND (&MODE *NE 'u') +
                        *AND (&MODE *NE 'A') *AND (&MODE *NE 'a')) +
                        THEN(DO)

                SNDPGMMSG  MSG('Invalid parameter passed - UTCMM0001')  +
                           TOMSGQ(MOPERQ MRUNQ)
                GOTO       CMDLBL(END)
                        ENDDO

             ELSE       CMD(DO)

/** Update mode */
                IF         COND((&MODE *EQ 'U') *OR (&MODE *EQ 'u')) +
                           THEN(DO)
/* Backup MMDELDPP */
                   DLTF       FILE(&DPLIB/MMDELDPPBU)
                   MONMSG     MSGID(CPF0000)

                   CPYF       FROMFILE(&DMLIB/MMDELDPP) +
                          TOFILE(&DPLIB/MMDELDPPBU) CRTFILE(*YES)
                   MONMSG     MSGID(CPF0000 ) EXEC(DO)
                   SNDPGMMSG  MSG('Error in BACKUP of file MMDELDPP - +
                                   Please contact your local Misys Branch') +
                              TOMSGQ(MOPERQ MRUNQ)
                   GOTO       CMDLBL(END)
                           ENDDO
                           ENDDO

                        ENDDO

/* Call program to produce audit and/or update report */
             CALL       PGM(SMU90015) PARM(&MODE)

/* If error, send message */
             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO CMDLBL(ABNOR))

             GOTO       CMDLBL(END)

 ABNOR:
             CHGJOB     SWS(XXXXXX11)

             IF         COND((&MODE *EQ 'U') *OR (&MODE *EQ 'u')) +
                        THEN(DO)
/* Restore from PF/MMDELDPBU if Update Mode and error is encountered*/
                CPYF       FROMFILE(&DPLIB/MMDELDPPBU) +
                           TOFILE(&DMLIB/MMDELDPP) MBROPT(*REPLACE)
                MONMSG     MSGID(CPF0000 MCH0000) EXEC(DO)
                SNDPGMMSG  MSG('Error in RESTORE of file MMDELDPP - +
                               Please contact your local Misys Branch') +
                          TOMSGQ(MOPERQ MRUNQ)
                GOTO       CMDLBL(END)
                        ENDDO
                        ENDDO

             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program UTMM0001 +
                          ended abnormally - see joblog ') TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 MCH0000)

 END:
             ENDPGM
