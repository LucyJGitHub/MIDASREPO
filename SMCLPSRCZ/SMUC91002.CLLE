/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('CL Program to call SMU91002')                         */
/*********************************************************************/
/*                                                                   */
/*       Midas - Dealing Module                                      */
/*                                                                   */
/*       SMUC91002 - CL program to call datapatch PGM/SMU91002       */
/*                                                                   */
/*       (c) Finastra International Limited 2023                     */
/*                                                                   */
/*       Last Amend No. MD060920A *CREATE  Date 29Apr23              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD060920A - FRA/IRS: incorrect reflection of interest in    */
/*                   assets and liabilities for CAPS/Coll/Floor.     */
/*                 - Update old RF deals for details in Our/Their    */
/*                   side.                                           */
/*                                                                   */
/*********************************************************************/
             PGM
/**/
             DCL        VAR(&LSTD) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&DMLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DPLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PREF) TYPE(*CHAR) LEN(2)
             DCL        VAR(&RUNP) TYPE(*CHAR) LEN(3) VALUE('NO')
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) Finastra +
                          International Limited 2023')
/**/
/* Global monitor message */
/**/
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR1))
/**/
/* Start of program */
/**/
             SNDPGMMSG  MSG('SMUC91002 - MD-60920A Data Patch Program') +
                          TOMSGQ(MRUNQ MOPERQ)
/**/
/* Create LDA if it doesn't exist */
/**/
             CHKOBJ     OBJ(QTEMP/LDA) OBJTYPE(*DTAARA)
             MONMSG     MSGID(CPF9815 CPF9801) EXEC(DO)
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
             ENDDO

/**/
/* Get system prefix */
/**/

             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&PREF)
             CHGVAR     VAR(&DPLIB) VALUE(&PREF *TCAT 'DPLIB')
             CHGVAR     VAR(&DMLIB) VALUE(&PREF *TCAT 'DMLIB')
/**/

FLECHK:
/* Check if YYXXXXXXXX exists */
/**/
             CHKOBJ     OBJ(&DPLIB/YYDEALSDG) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(GOTO CMDLBL(FLECHK2))
             DLTF       FILE(YYDEALSDG)
/**/
FLECHK2:
             CHKOBJ     OBJ(&DPLIB/YYDLGHISPD) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(GOTO CMDLBL(BACKUP))
             DLTF       FILE(YYDLGHISPD)

/**/
/* Create backup */
/**/
BACKUP:
             CPYF       FROMFILE(&DMLIB/DEALSDG) TOFILE(&DPLIB/YYDEALSDG) +
                        MBROPT(*REPLACE) CRTFILE(*YES)
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) EXEC(DO)
             CLRPFM     FILE(YYDEALSDG)
             ENDDO
             MONMSG     MSGID(CPF2817 CPF3130 CPF2909) EXEC(DO)
             SNDUSRMSG  MSG('Error occurred in copying PF/DEALSDG to +
                PF/YYDEALSDG. Process terminated.') MSGTYPE(*INFO)
             GOTO       CMDLBL(END)
             ENDDO

             CPYF       FROMFILE(&DMLIB/DLGHISPD) TOFILE(&DPLIB/YYDLGHISPD) +
                        MBROPT(*REPLACE) CRTFILE(*YES)
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) EXEC(DO)
             CLRPFM     FILE(YYDLGHISPD)
             ENDDO
             MONMSG     MSGID(CPF2817 CPF3130 CPF2909) EXEC(DO)
             SNDUSRMSG  MSG('Error occurred in copying PF/DLGHISPD to +
                PF/YYDLHGISPD. Process terminated.') MSGTYPE(*INFO)
             GOTO       CMDLBL(END)
             ENDDO
/**/
/* Call ILE/SMU91002 */
/**/
             CHGJOB     SWS(XXXXXX00)
             CHGVAR     VAR(&RUNP) VALUE('YES')

             CALL       PGM(SMU91002)
/**/
/* Error occurred in datapatch program */
/**/
             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                          CMDLBL(ABNOR))

             SNDPGMMSG  MSG('SMUC91002 completed normally.') TOPGMQ(*EXT) +
                          TOMSGQ(MOPERQ MRUNQ)

             GOTO       CMDLBL(END)

/**/
/* Abnormal Termination of the program or */
/* Restore backup if an error occurred in update mode */
/**/
ABNOR:
             IF         COND(&RUNP *EQ 'YES') THEN(DO)

                CPYF FROMFILE(&DPLIB/YYDEALSDG) TOFILE(&DMLIB/DEALSDG) +
                     MBROPT(*REPLACE) FMTOPT(*NONE)
                MONMSG     MSGID(CPF2817 CPF3130 CPF2909) EXEC(DO)
                SNDUSRMSG  MSG('Error occurred in restoring PF/DEALSDG +
                        from PF/YYDEALSDG. Process terminated.  Please restore +
                        PF/DEALSDG manually from PF/YYDEALSDG.') +
                        MSGTYPE(*INFO)
                ENDDO

                CPYF FROMFILE(&DPLIB/YYDLGHISPD) TOFILE(&DMLIB/DLGHISPD) +
                     MBROPT(*REPLACE) FMTOPT(*NONE)
                MONMSG     MSGID(CPF2817 CPF3130 CPF2909) EXEC(DO)
                SNDUSRMSG  MSG('Error occurred in restoring PF/DLGHISPD +
                        from PF/YYDLGHISPD. Process terminated.  Please restore +
                        PF/DLGHISPD manually from PF/YYDLGHISPD.') +
                        MSGTYPE(*INFO)
                ENDDO

             ENDDO

 ABNOR1:     SNDPGMMSG  MSG('Error occurred in SMUC91002 program') TOPGMQ(*EXT) +
                          TOMSGQ(MOPERQ MRUNQ)

 END:        CHGVAR     VAR(&CPYFLD) VALUE('(c) Finastra International Limited +
                          2023')
             SNDPGMMSG  MSG('End of SMUC91002') TOMSGQ(MRUNQ MOPERQ)
             ENDPGM
