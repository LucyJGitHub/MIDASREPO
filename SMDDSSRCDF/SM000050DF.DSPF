      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SM Maintain Account Code Conversions')
      *****************************************************************
      *                                                               *
      *  Midas - Implementation module                                *
      *                                                               *
      *  DSPF/SM000050DF - Maintain Account Code Conversions          *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSM007  *CREATE    Date 27Oct05               *
      *                                    Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSM007 - Conversion of Account Codes                         *
      *           Created to display all the Account Codes and allow  *
      *           them to be changed                                  *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *================================================================
     A          R #SFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A  84                                  SFLNXTCHG
      *
     A            #1OLDAC        4  0   5 11TEXT('Old Account Code')
     A                                      DSPATR(HI)
     A  88                                  DSPATR(ND)
     A            #1NWACS       10   B  5 31TEXT('New Account Code')
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL HI)
     A  88                                  DSPATR(ND)
      *=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(17) SFLSIZ(18)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N80
     AA 81                                  SFLDSP
     A  82                                  SFLEND
      * Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CA05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
      * SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
      *.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A                                  1 13'SM000050'
     A                                  1 38'Bridge'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
     A                                  2 23'Conversion of A/C Code +
     A                                      to 10 Long'
     A                                      DSPATR(HI)
     A            ##ZOMSYS       2      2 58TEXT('Zone System Id')
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A  81                              4 10'Old Account Code'
     A  81                              4 28'New Account Code'
      *=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Exit  F5=Refresh'
     A                                      OVRATR
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *=========================================================================
