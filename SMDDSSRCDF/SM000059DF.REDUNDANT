      *****************************************************************
/*S*D****DSPFBASE******************************************************                     AR690511
      *****************************************************************
      *                                                               *
      *  Midas - Implementation Module                                *
      *                                                               *
      *  SM000059DF - Midas SM Update MX files for Bundesbank Screen  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2009            *
      *                                                               *
      *  Last Amend No. AR690511*REDUNDANT Date 31Jan11               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER045  *CREATE    Date 20Jul09               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  AR690511 - Rename from SM000059DF to ER000059DF              *
      *             (Child: AR690512)                                 *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER045 - German Features - Reporting Bundesbank              *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *
      * --------------------------------------------------------------*
      *   Detail Record Format                                        *
      * --------------------------------------------------------------*
      *
     A          R SM59DETL
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2009')
     A                                      CF03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            PSUSER        10A  O  1  2TEXT('User Profile')
     A            ZADATE         7A  O  1 73TEXT('Rundate')
     A            PSJOBNAME     10A  O  2  2TEXT('Workstation ID')
     A                                  2 73TIME
     A                                  1 13'SM000059'
     A                                  1 37'Bridge'
     A                                  2 25'Update of MX Files for Bundesbank'
     A                                      DSPATR(HI)
     A                                  6  3'Message Queue . . . . :'
     A                                  7  3'Next Export Date  . . :'
     A            SMSGQ         48   B  6 27
     A                                      DSPATR(HI)
     A N23N90                               DSPATR(UL)
     A  23                                  DSPATR(RI PC)
     A  90                                  DSPATR(PR)
     A                                      OVRATR
     A                                      OVRDTA
     A            SEXND          6   B  7 27
     A                                      DSPATR(HI)
     A N24N90                               DSPATR(UL)
     A  24                                  DSPATR(RI PC)
     A  90                                  DSPATR(PR)
     A                                      OVRATR
     A                                      OVRDTA
      *
      * --------------------------------------------------------------*
      * * Footer record format                                        *
      * --------------------------------------------------------------*
      *
     A          R SM59FOOT                  TEXT('Footer')
     A                                      OVERLAY
     A N28                             23  2'F3=Exit   F5=Refresh'
     A  28                             23  2'ENTER=Confirm   F12=Cancel'
     A  28                                   DSPATR(HI)
      *
      * --------------------------------------------------------------*
      * * Message subfile record format                               *
      * --------------------------------------------------------------*
      *
     A          R SM59MSGR                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMNAM                    SFLPGMQ
      *
      * --------------------------------------------------------------*
      *   Message subfile control record format                       *
      * --------------------------------------------------------------*
      *
     A          R SM59MSGCTL                TEXT('Program messages')
     A                                      SFLCTL(SM59MSGR)
     A                                      OVERLAY SFLINZ SFLPAG(1) SFLSIZ(3)
     A                                      SFLDSPCTL SFLDSP
     A  90
     AON90                                  SFLEND
     A            PGMNAM                    SFLPGMQ
      *
