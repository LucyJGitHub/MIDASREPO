      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SM Users selection for data migration')
      *****************************************************************
      *                                                               *
      *  Midas - Implementation module                                *
      *                                                               *
      *  DSPF/SM1041DF - Display existing Midas users for select/copy *
      *                  to the new system.                           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD013 *CREATE     Date 09Jul02               *
      *  Prev Amend No. xxxxxx             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *  CSD013 - Midas users data maintenance.                       *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A          R FMT1                      SFL
      *
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A  84                                  SFLNXTCHG
      *
     A            XXOPT          1A  B  8  3VALUES('X' ' ' 'R')
     A N57                                  DSPATR(UL HI)
     A  57                                  DSPATR(RI PC)
     A            USRP          10A  O  8  7
     A            USER1         25A  O  8 22
     A            EXIST          3A  O  8 55
     A            EXIND          1A  H
      *
     A          R FMT2                      SFLCTL(FMT1)
     A                                      SETOF(98 'Force input format')
 
     A                                      CHANGE(01 'On if any field changes')
 
     A                                      SFLSIZ(0030)
     A                                      SFLPAG(0014)
     A                                      TEXT('Subfile control format')
     A                                      CA03(03 'Exit')
     A                                      CA05(05 'Refresh')
     A                                      OVERLAY
     A                                      SFLDSP
     A N30                                  SFLDSPCTL
     A  30                                  SFLCLR
     A  50                                  SFLEND(*MORE)
     A                                      SETOF(99 'Global error flag')
     A            SUSER         10A  O  1  2
     A                                  1 13'SM1070'
     A                                  1 38'Bridge'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A            SWSID         10A  O  2  2
     A                                  2 21'Migrate Midas Users From'
     A            FRMSYS         2A  O  2 46
     A                                  2 49'To'
     A            TOSYS          2A  O  2 52
     A                                  2 55'Systems'
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  4  2'Go to'
     A                                  5  2'User'
     A            XXUSRP        10A  B  5  7DSPATR(PC)
     A                                  6 50'Already exist in'
     A                                      DSPATR(HI)
     A                                  7  7'User Profile'
     A                                      DSPATR(HI)
     A                                  7 22'Description'
     A                                      DSPATR(HI)
     A                                  7 50'the'
     A                                      DSPATR(HI)
     A            NEWSYS         2A  O  7 54
     A                                      DSPATR(HI)
     A                                  7 57'System?'
     A                                      DSPATR(HI)
      *
     A          R FMT0
      *
     A                                 23  3'X = Select/copy to the new'
     A                                      DSPATR(HI)
     A                                 23 30'system'
     A                                      DSPATR(HI)
     A                                 23 41'R = Rename/copy to the new'
     A                                      DSPATR(HI)
     A                                 23 68'system'
     A                                      DSPATR(HI)
     A N57                             24  2'F3=Exit'
     A N57                             24 13'F5=Refresh'
     A  57                             24  2'The new user name should not be'
     A  57                             24 34'blank.'
      *
     A          R NORCD
     A                                      TEXT('Blank format - no records')
     A                                      CHANGE(01 'On if any field changes')
     A                                      CA03(03 'Exit')
     A                                      CA05(05 'Refresh')
     A                                      OVERLAY
     A            SUSER         10A  O  1  2
     A                                  1 13'SM1070'
     A                                  1 38'Bridge'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A            SWSID         10A  O  2  2
     A                                  2 21'Migrate Midas Users From'
     A            FRMSYS         2A  O  2 46
     A                                  2 49'To'
     A            TOSYS          2A  O  2 52
     A                                  2 55'Systems'
     A                                  2 27'Select Existing Midas Users'
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  4  2'Go to'
     A                                  5  2'User'
     A            XXUSRP        10A  B  5  7DSPATR(PC)
     A                                  6 50'Already exist in'
     A                                      DSPATR(HI)
     A                                  7  7'User Profile'
     A                                      DSPATR(HI)
     A                                  7 22'Description'
     A                                      DSPATR(HI)
     A                                  7 50'the'
     A                                      DSPATR(HI)
     A            NEWSYS         2A  O  7 54
     A                                      DSPATR(HI)
     A                                  7 57'System?'
     A                                      DSPATR(HI)
     A                                  9 22'No record to display'
     A                                      DSPATR(HI)
      *
     A                                 24  2'F3=Exit'
     A                                 24 13'F5=Refresh'
