      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_NMMXM32                                                   *
/*EXI *  TEXT('Midas SM Template for migration of SDCSINPD')          *
      *****************************************************************
      *                                                               *
      *  Midas - Implementation module                                *
      *                                                               *
      *  PF/SMUGF07PD - Template for SDCSINPD migration               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2009            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CSM011  *CREATE    Date 21May09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CSM011 - New utility to process CAR migration                *
      *                                                               *
      *****************************************************************
      *
     A          R SMUGF07D0                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2009')
      *
     A            FBRCA          3A         TEXT('Branch code')
     A                                      COLHDG('Brc' 'Cde')
     A            FCUST          6S 0       TEXT('Customer number')
     A                                      COLHDG('Cust.' 'Number')
     A            FCCY           3A         TEXT('Currency code')
     A                                      COLHDG('Ccy')
     A            FDLAC         12S 0       TEXT('Deal / account number')
     A                                      COLHDG('Deal / a/c' 'Number')
     A            FBITM          6A         TEXT('Batch item')
     A                                      COLHDG('Batch' 'Item')
     A            FRVIN          1A         TEXT('Reversal')
     A                                      COLHDG('Rvs')
     A            FTRTP          4A         TEXT('Transaction type')
     A                                      COLHDG('Tran' 'Type')
     A            FSLID          5P 0       TEXT('Start date or last int. date')
     A                                      COLHDG('Str Dat' 'or Last' 'Int. +
     A                                      Dat')
     A            FVALD          5P 0       TEXT('Value date')
     A                                      COLHDG('Val' 'Date')
     A            FGINTC        15P 0       TEXT('Gross interest in currency')
     A                                      COLHDG('Gross' 'Interest' 'in Ccy')
     A            FGINTD        15P 0       TEXT('Gross interest in tax +
     A                                      currency')
     A                                      COLHDG('Gross' 'Int in' 'Tax Ccy')
     A            FWHTC         15P 0       TEXT('Witholding tax in currency')
     A                                      COLHDG('Withold' 'Tax in Ccy')
     A            FWHTB         15P 0       TEXT('Witholding tax in tax +
     A                                      currency')
     A                                      COLHDG('Withold Tax' 'in Tax Ccy')
     A            FSCNC         15P 0       TEXT('Solidarity contribution in +
     A                                      currency')
     A                                      COLHDG('Sldrty Cont' 'in Ccy')
     A            FSCNB         15P 0       TEXT('Solidarity contribution in +
     A                                      tax currency')
     A                                      COLHDG('Sldrty Cont' 'in Tax Ccy')
     A            FNINTC        15P 0       TEXT('Net interest in currency')
     A                                      COLHDG('Net Int' 'in Ccy')
     A            FNINTD        15P 0       TEXT('Net interest in tax currency')
     A                                      COLHDG('Net Int' 'in Tax Ccy')
     A            FCRTDT         5P 0       TEXT('Creation date')
     A                                      COLHDG('Crt' 'Date')
     A            FTADJC        13P 0       TEXT('Manual tax adjustment in +
     A                                      currency')
     A                                      COLHDG('Mnl Tax' 'Adj in Ccy')
     A            FTADJB        13P 0       TEXT('Manual tax adjustment in tax +
     A                                      currency')
     A                                      COLHDG('Mnl Tax Adj' 'in Tax Ccy')
     A            FSPRT         13P 8       TEXT('Spot rate')
     A                                      COLHDG('Spot' 'Rate')
     A            FDTYP          2A         TEXT('Deal type')
     A                                      COLHDG('Deal' 'Type')
     A            FDLST          2A         TEXT('Deal sub-type')
     A                                      COLHDG('Deal' 'Sub-' 'Type')
     A            FETARA        13P 8       TEXT('Effective tax rate')
     A                                      COLHDG('Effect' 'Tax Rate')
     A            FESORA        13P 8       TEXT('Effective solidarity rate')
     A                                      COLHDG('Effect' 'Sldrty Rate')
     A            FPSTD          5P 0       TEXT('Posting date')
     A                                      COLHDG('Post' 'Date')
     A            FSADJC        13P 0       TEXT('Manual solidarity adjustment +
     A                                      in currency')
     A                                      COLHDG('Mnl Sol Adj' 'in Ccy')
     A            FSADJB        13P 0       TEXT('Manual solidarity adjustment +
     A                                      in tax currency')
     A                                      COLHDG('Mnl Sol Adj' 'in Tax Ccy')
     A            FTHUSC        15P 0       TEXT('Portion of threshold used in +
     A                                      currency')
     A                                      COLHDG('Prtn of Thold' 'Used in +
     A                                      CCY')
     A            FTHUSD        15P 0       TEXT('Portion of threshold used in +
     A                                      tax currency')
     A                                      COLHDG('Prtn of Thold' 'Used in' +
     A                                      'Tax Ccy')
     A            FTAXI          1          TEXT('Tax Indicator')
     A                                      COLHDG('Tax' 'Indicator')
     A            FTAXT          2          TEXT('Tax Type')
     A                                      COLHDG('Tax' 'Type')
      *
     A          K FBRCA
     A          K FCCY
     A          K FDLAC
     A          K FBITM
     A          K FRVIN
     A          K FTRTP
     A          K FSLID
