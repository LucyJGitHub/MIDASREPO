      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SM Display objects that were not found')
      *****************************************************************
      *                                                               *
      *  Midas - Implemenatation module                               *
      *                                                               *
      *  DSPF/SM000022DF - Dsplay objects not found                   *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. BUG27793 *CREATE   Date 11Jun10               *
      *                 XXXXXX             Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG27793 - Make migration muli-streamed.                     *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A          R FMT1                      SFL
      *
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2010')
     A            XXOPT          1A  B  7  2VALUES('D' 'O' 'X' 'V' ' ')
     A            ODOBNM        10A  O  7  4
     A            ODLBNM        10A  O  7 15
     A            XXTYPE         6A  O  7 26
     A            XXTEXT        47A  O  7 33
     A            ODOBTP         8A  H
     A            ODSRCF        10A  H
     A            ODSRCL        10A  H
     A            ODSRCM        10A  H
      *
     A          R FMT2                      SFLCTL(FMT1)
      *
     A                                      CHANGE(01 'On if any field changes')
      *
     A                                      SFLSIZ(0030)
     A                                      SFLPAG(0014)
     A                                      TEXT('Subfile control format')
     A                                      CA03(03 'Exit')
     A                                      CA05(05 'Refresh')
     A                                      OVERLAY
     A                                      SFLDSP
     A N30                                  SFLDSPCTL
     A  30                                  SFLCLR
     A  50                                  SFLEND(*MORE)
     A            SUSER         10A  O  1  2
     A                                  1 13'SM000022DF'
     A                                  1 67'Date '
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A            SWSID         10A  O  2  2
     A                                  2 28'Display objects not found'
     A                                      DSPATR(UL)
     A                                  2 67'Time'
     A                                  2 73TIME
     A                                  4  1'Go to'
     A            XXPOS         10A  B  4  7DSPATR(PC)
     A                                  4 52'Subset by type'
     A            XXSUB          6A  B  4 67
     A                                  6  4'Object'
     A                                      DSPATR(UL)
     A                                  6 15'Library'
     A                                      DSPATR(UL)
     A                                  6 26'Type'
     A                                      DSPATR(UL)
     A                                  6 33'Text'
     A                                      DSPATR(UL)
      *
     A          R FMT0
     A                                 23  2'D=Delete'
     A                                      DSPATR(HI)
     A                                 23 12'O=Display object description'
     A                                      DSPATR(HI)
     A                                 23 42'V=View object'
     A                                      DSPATR(HI)
     A                                 23 57'X=Process request'
     A                                      DSPATR(HI)
     A                                 24  2'F3=Exit'
     A                                 24 13'F5=Refresh'
     A          R NORCD
     A                                      TEXT('Blank format - no records')
     A                                      CA03(03 'Exit')
     A                                      CA05(05 'Refresh')
     A                                      OVERLAY
     A            SUSER         10A  O  1  2
     A                                  1 13'SM000022DF'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'Date '
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A            SWSID         10A  O  2  2
     A                                  2 28'Display objects not moved'
     A                                  2 67'Time'
     A                                  2 73TIME
     A                                  4  1'Go to'
     A            XXPOS         10A  B  4  7DSPATR(PC)
     A                                  4 52'Subset by type'
     A            XXSUB          6A  B  4 67
     A                                  8 26'No records found'
     A                                      DSPATR(HI)
     A                                  6  4'Object'
     A                                      DSPATR(UL)
     A                                  6 15'Library'
     A                                      DSPATR(UL)
     A                                  6 26'Type'
     A                                      DSPATR(UL)
     A                                  6 33'Text'
     A                                      DSPATR(UL)
      *
     A                                 23  2'D=Delete'
     A                                      DSPATR(HI)
     A                                 23 12'O=Display object description'
     A                                      DSPATR(HI)
     A                                 23 42'V=View object'
     A                                      DSPATR(HI)
     A                                 23 57'X=Process request'
     A                                      DSPATR(HI)
     A                                 24  2'F3=Exit'
     A                                 24 13'F5=Refresh'
