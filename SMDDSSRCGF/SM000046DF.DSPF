      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SM Set user up as role')                         *
      *****************************************************************
      *                                                               *
      *  Midas - Implementation module                                *
      *                                                               *
      *  DSPF/SM000046DF - Set up user as role                        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2011            *
      *                                                               *
      *  Last Amend No. AR1085933          Date 06Feb13               *
      *  Prev Amend No. CBF005   *CREATE   Date 04Jul11               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR1085933 - Position-to function not working properly        *
      *  CBF005 - BF Infrastructure: Single Security User Maintenance *
      *           Profile                                             *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
      *
     A          R SM000046S0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2011')
     A                                      SFL
      *
     A            DUSER         10      7  3TEXT('User')
     A            DBRCH          3      7 14TEXT('Branch')
     A            DROLE         40   B  7 21TEXT('Role')
     A                                      CHANGE(89 'On if any field changes')
     A                                      CHGINPDFT(LC UL)
      *
     A          R SM000046C0                TEXT('Subfile control')
     A                                      SFLCTL(SM000046S0)
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      OVERLAY
     A                                      SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A  82                                  SFLEND
     A                                      CA03(03 'Exit')
     A                                      CA05(05 'Refresh')
      *
     A            MATCH          4S 0H      SFLRCDNBR(CURSOR)                              AR1085933
      *                                                                                    AR1085933
     A            DCUSR         10      1  2TEXT('Current user')
     A                                  1 13'SM000046'
     A                                  1 38'Set User as Role'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            DJOB          10      2  2TEXT('Current job')
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  1'Go to'
     A**********  DPOS          10A  B  4  7DSPATR(PC)                                     AR1085933
     A            DPOS          10A  B  4  7                                               AR1085933
     A                                  6  3'User'
     A                                      DSPATR(HI)
     A                                  6 14'Branch'
     A                                      DSPATR(HI)
     A                                  6 21'Role'
     A                                      DSPATR(HI)
      *
     A          R SM000046F0                TEXT('Command keys')
     A                                 23  2'F3=Exit  F5=Refresh'
     A                                      DSPATR(HI)
      *
