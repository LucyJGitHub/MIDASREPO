      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SM Implementation options maintenance')          *
      *****************************************************************
      *                                                               *
      *  Midas - Implementation                                       *
      *                                                               *
      *  SM000066DF - Implementation steps maintenance                *
      *                                                               *
      *  (c) Finastra International Limited 2014                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CUP042  *CREATE    Date 14Nov14               *
      *                 XXXXXXXX           Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CUP042 - Restructure of deliverable data files.              *
      *                                                               *
      *****************************************************************
      *
     A N44                                  CA09
     A                                      CA05
     A N40                                  CA10
     A N41                                  CA03
     A N43                                  CA12
     A                                      INDARA
     A          R SM000066F0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2014')
     A            GFUSER        10A     1  2
     A                                  1 13'SM000066'
     A            GFDATE         7A     1 72
     A            GFJOBN        10A     2  2
     A                                  2 31'Implementation Steps'
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A          R SM000066F1                OVERLAY
     A                                 23  2'F9=Insert'
     A                                      COLOR(BLU)
     A                                 23 14'F3=Exit'
     A                                      COLOR(BLU)
     A                                 23 24'F5=Refresh'
     A                                      COLOR(BLU)
      *
     A          R SM000066F2                OVERLAY
      * Key fields; can only be amended if insert.
     A                                  4  2'Menu sub-option'
     A                                  4 22':'
     A            GFSMNU        20A  B  4 24CHECK(LC)
     A N70                                  DSPATR(HI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  70                                  DSPATR(RI PC)
      *
     A                                  5  2'Layer'
     A                                  5 22':'
     A            GFLAYR         7A  B  5 24
     A N71                                  DSPATR(HI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  71                                  DSPATR(RI PC)
      *
     A                                  6  2'Item'
     A                                  6 22':'
     A            GFNAME        10A  B  6 24CHECK(LC)
     A N72                                  DSPATR(HI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  72                                  DSPATR(RI PC)
     A                                  7  2'Type'
     A                                  7 22':'
     A            GFTYPE        10A  B  7 24CHECK(LC)
     A N73                                  DSPATR(HI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  73                                  DSPATR(RI PC)
      *
      * Mode field; can never be amended.
     A                                  8  2'Core or Bespoke'
     A                                  8 22':'
     A            GFDMOD         7A  O  8 24
      *
      * Fields that can be only be amended if bespoke or insert.
     A                                  9  2'Sequence'
     A                                  9 22':'
     A            GFSEQN         5D 0B  9 24
     A N74                                  DSPATR(HI)
     A  36                                  DSPATR(UL)
     A N36                                  DSPATR(PR)
     A  74                                  DSPATR(RI PC)
      *
     A                                 10  2'Command'
     A                                 10 22':'
     A            GFCMD1        50   B 10 24CHECK(LC)
     A N75                                  DSPATR(HI)
     A  36                                  DSPATR(UL)
     A N36                                  DSPATR(PR)
     A  75                                  DSPATR(RI PC)
     A            GFCMD2        50   B 11 24CHECK(LC)
     A N75                                  DSPATR(HI)
     A  36                                  DSPATR(UL)
     A N36                                  DSPATR(PR)
     A  75                                  DSPATR(RI PC)
     A            GFCMD3        50   B 12 24CHECK(LC)
     A N75                                  DSPATR(HI)
     A  36                                  DSPATR(UL)
     A N36                                  DSPATR(PR)
     A  75                                  DSPATR(RI PC)
     A            GFCMD4        50   B 13 24CHECK(LC)
     A N75                                  DSPATR(HI)
     A  36                                  DSPATR(UL)
     A N36                                  DSPATR(PR)
     A  75                                  DSPATR(RI PC)
     A            GFCMD5        50   B 14 24CHECK(LC)
     A N75                                  DSPATR(HI)
     A  36                                  DSPATR(UL)
     A N36                                  DSPATR(PR)
     A  75                                  DSPATR(RI PC)
     A            GFCMD6        50   B 15 24CHECK(LC)
     A N75                                  DSPATR(HI)
     A  36                                  DSPATR(UL)
     A N36                                  DSPATR(PR)
     A  75                                  DSPATR(RI PC)
      *
      * Fields that can be amended for bespoke or core or if insert.
     A                                 16  2'Status'
     A                                 16 22':'
     A            GFSTS         40A  B 16 24CHECK(LC)
     A N76                                  DSPATR(HI)
     A  37                                  DSPATR(UL)
     A N37                                  DSPATR(PR)
     A  76                                  DSPATR(RI PC)
      *
     A                                 17  2'Logically deleted ?'
     A                                 17 22':'
     A            GFDDEL         3A  O 17 24
      *
     A          R SM000066F3                OVERLAY
     A                                 12 30'No details to display.'
     A                                      DSPATR(HI)
     A          R SM000066F4                OVERLAY
     A                                 23  2'Enter to confirm'
     A                                      COLOR(BLU)
     A                                 23 21'F12=Cancel'
     A                                      COLOR(BLU)
     A                                 23 34'F5=Refresh'
     A                                      COLOR(BLU)
     A          R SM000066F5                OVERLAY
     A                                 23  2'F10=Confirm delete / reset'
     A                                      COLOR(BLU)
     A                                 23 31'F12=Cancel'
     A                                      COLOR(BLU)
      *
     A          R SM000066S0                SFL
     A  27                                  SFLNXTCHG
     A            GFAORD         1A  B  8  4
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A            GFSMNU        20A     8 10DSPATR(HI)
     A                                      TEXT('Menu sub-task')
     A            GFLAYR         7A     8 32DSPATR(HI)
     A                                      TEXT('Layer')
     A            GFNAME        10A     8 40DSPATR(HI)
     A                                      TEXT('Name')
     A            GFTYPE        10A     8 52DSPATR(HI)
     A                                      TEXT('Type')
     A            GFDMOD         7A     8 64DSPATR(HI)
     A                                      TEXT('Mode')
     A            GFMODE         1A  H      TEXT('Mode')
     A            GFSEQN         5D 0H      TEXT('Sequence')
     A            GFCMD1        50A  H      TEXT('Command Line 1')
     A            GFCMD2        50A  H      TEXT('Command Line 2')
     A            GFCMD3        50A  H      TEXT('Command Line 3')
     A            GFCMD4        50A  H      TEXT('Command Line 4')
     A            GFCMD5        50A  H      TEXT('Command Line 5')
     A            GFCMD6        50A  H      TEXT('Command Line 6')
     A            GFSTS         40A  H      TEXT('Status')
     A            GFDDEL         3A     8 73DSPATR(HI)
     A                                      TEXT('Logically deleted')
     A            GFDEL          1A  H      TEXT('Logically deleted')
      *
     A          R SM000066C0                SFLCTL(SM000066S0)
     A  26                                  SFLCLR
     A  22                                  SFLDSPCTL
     A  23                                  SFLDSP
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(13)
     A N24
     AO 24                                  SFLEND(*MORE)
     A                                      OVERLAY
     A  30                              3  2'Go to:'
     A N30                                  DSPATR(ND)
     A  30        GFSRCH        20A  B  3  9CHECK(LC)
     A  30                                  DSPATR(HI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(ND)
     A N30                                  DSPATR(PR)
     A  30                              5  2'(A/D/E)'
     A N30                                  DSPATR(ND)
     A  30                              5 10'Menu sub-task'
     A N30                                  DSPATR(ND)
     A  30                              5 32'Layer'
     A N30                                  DSPATR(ND)
     A  30                              5 40'Name'
     A N30                                  DSPATR(ND)
     A  30                              5 52'Type'
     A N30                                  DSPATR(ND)
     A  30                              5 64'Mode'
     A N30                                  DSPATR(ND)
     A  30                              5 73'Del?'
     A N30                                  DSPATR(ND)
      *
     A          R SM000066S1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            GFMSGK                    SFLMSGKEY
     A            GFPGMQ                    SFLPGMQ
      *
     A          R SM000066C1                TEXT('Program messages')
     A                                      SFLCTL(SM000066S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  28                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            GFPGMQ                    SFLPGMQ
      *****************************************************************
