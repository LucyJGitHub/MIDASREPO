      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SM Object initialisation maintenance')           *
      *****************************************************************
      *                                                               *
      *  Midas - Implementation                                       *
      *                                                               *
      *  SM000067DF - Implementation steps maintenance                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2014            *
      *                                                               *
      *  Last Amend No. CUP042   *CREATE   Date 14Nov14               *
      *  Prev Amend No. xxxxxxxx           Date ddMmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUP042 - Restructure of deliverable data files.              *
      *                                                               *
      *****************************************************************
      *
     A N44                                  CA09
     A                                      CA05
     A N40                                  CA10
     A N41                                  CA03
     A N43                                  CA12
     A                                      INDARA
     A          R SM000067F0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2014')
     A            DFUSER        10A     1  2
     A                                  1 13'SM000067'
     A            DFDATE         7A     1 72
     A            DFJOBN        10A     2  2
     A                                  2 31'Object Initialisation'
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A          R SM000067F1                OVERLAY
     A                                 23  2'F9=Insert'
     A                                      COLOR(BLU)
     A                                 23 14'F3=Exit'
     A                                      COLOR(BLU)
     A                                 23 24'F5=Refresh'
     A                                      COLOR(BLU)
      *
     A          R SM000067F2                OVERLAY
      * Key fields; can only be amended if insert.
     A                                  4  2'Object'
     A                                  4 22':'
     A            DFOBJ         10A  B  4 24
     A N70                                  DSPATR(HI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  70                                  DSPATR(RI PC)
      *
     A                                  5  2'Type'
     A                                  5 22':'
     A            DFTYPE         7A  B  5 24
     A N71                                  DSPATR(HI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  71                                  DSPATR(RI PC)
      *
     A                                  6  2'Sequence'
     A                                  6 22':'
     A            DFSEQ          2D 0B  6 24
     A N72                                  DSPATR(HI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  72                                  DSPATR(RI PC)
      *
      * Mode field; can never be amended.
     A                                  7  2'Core or Bespoke'
     A                                  7 22':'
     A            DFDMOD         7A  O  7 24
      *
      * Fields that can be only be amended if bespoke or insert.
      *
      * Fields that can be amended for bespoke or core or if insert.
     A                                  8  2'Command'
     A                                  8 22':'
     A            DFCMD1        40   B  8 24CHECK(LC)
     A N73                                  DSPATR(HI)
     A  37                                  DSPATR(UL)
     A N37                                  DSPATR(PR)
     A  73                                  DSPATR(RI PC)
     A            DFCMD2        40   B  9 24CHECK(LC)
     A N73                                  DSPATR(HI)
     A  37                                  DSPATR(UL)
     A N37                                  DSPATR(PR)
     A  73                                  DSPATR(RI PC)
      *
      *
     A                                 10  2'Logically deleted ?'
     A                                 10 22':'
     A            DFDDEL         3A  O 10 24
      *
     A          R SM000067F3                OVERLAY
     A                                 12 30'No details to display.'
     A                                      DSPATR(HI)
     A          R SM000067F4                OVERLAY
     A                                 23  2'Enter to confirm'
     A                                      COLOR(BLU)
     A                                 23 21'F12=Cancel'
     A                                      COLOR(BLU)
     A                                 23 34'F5=Refresh'
     A                                      COLOR(BLU)
     A          R SM000067F5                OVERLAY
     A                                 23  2'F10=Confirm delete / reset'
     A                                      COLOR(BLU)
     A                                 23 31'F12=Cancel'
     A                                      COLOR(BLU)
      *
     A          R SM000067S0                SFL
     A  27                                  SFLNXTCHG
     A            DFAORD         1A  B  8  4
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A            DFOBJ         10A     8 10DSPATR(HI)
     A                                      TEXT('Object')
     A            DFTYPE         7A     8 22DSPATR(HI)
     A                                      TEXT('Type')
     A            DFSEQ          2D 0   8 31DSPATR(HI)
     A                                      TEXT('Name')
     A            DFDMOD         7A     8 64DSPATR(HI)
     A                                      TEXT('Mode')
     A            DFMODE         1A  H      TEXT('Mode')
     A            DFCMD1        40A  H      TEXT('Command Line 1')
     A            DFCMD2        40A  H      TEXT('Command Line 2')
     A            DFDDEL         3A     8 73DSPATR(HI)
     A                                      TEXT('Logically deleted')
     A            DFDEL          1A  H      TEXT('Logically deleted')
      *
     A          R SM000067C0                SFLCTL(SM000067S0)
     A  26                                  SFLCLR
     A  22                                  SFLDSPCTL
     A  23                                  SFLDSP
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(13)
     A N24
     AO 24                                  SFLEND(*MORE)
     A                                      OVERLAY
     A  30                              3  2'Go to:'
     A N30                                  DSPATR(ND)
     A  30        DFSRCH        10A  B  3  9
     A  30                                  DSPATR(HI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(ND)
     A N30                                  DSPATR(PR)
     A  30                              5  2'(A/D/E)'
     A N30                                  DSPATR(ND)
     A  30                              5 10'Object'
     A N30                                  DSPATR(ND)
     A  30                              5 22'Type'
     A N30                                  DSPATR(ND)
     A  30                              5 31'Seq.'
     A N30                                  DSPATR(ND)
     A  30                              5 64'Mode'
     A N30                                  DSPATR(ND)
     A  30                              5 73'Del?'
     A N30                                  DSPATR(ND)
      *
     A          R SM000067S1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            DFMSGK                    SFLMSGKEY
     A            DFPGMQ                    SFLPGMQ
      *
     A          R SM000067C1                TEXT('Program messages')
     A                                      SFLCTL(SM000067S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  28                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            DFPGMQ                    SFLPGMQ
      *****************************************************************
