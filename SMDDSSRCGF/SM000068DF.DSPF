      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SM Upgrade programs maintenance')                *
      *****************************************************************
      *                                                               *
      *  Midas - Implementation                                       *
      *                                                               *
      *  SM000068DF - Implementation steps maintenance                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2014            *
      *                                                               *
      *  Last Amend No. CUP042   *CREATE   Date 14Nov14               *
      *  Prev Amend No. xxxxxxxx           Date ddMmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUP042 - Restructure of deliverable data files.              *
      *                                                               *
      *****************************************************************
      *
     A N44                                  CA09
     A                                      CA05
     A N40                                  CA10
     A N41                                  CA03
     A N43                                  CA12
     A                                      INDARA
     A          R SM000068F0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2014')
     A            DFUSER        10A     1  2
     A                                  1 13'SM000068'
     A            DFDATE         7A     1 72
     A            DFJOBN        10A     2  2
     A                                  2 33'Upgrade Programs'
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A          R SM000068F1                OVERLAY
     A                                 23  2'F9=Insert'
     A                                      COLOR(BLU)
     A                                 23 14'F3=Exit'
     A                                      COLOR(BLU)
     A                                 23 24'F5=Refresh'
     A                                      COLOR(BLU)
      *
     A          R SM000068F2                OVERLAY
      * Key fields; can only be amended if insert.
     A                                  4  2'Object'
     A                                  4 22':'
     A            DFMBNM        10A  B  4 24
     A N70                                  DSPATR(HI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  70                                  DSPATR(RI PC)
      *
     A                                  5  2'Release'
     A                                  5 22':'
     A            DFPROJ        10A  B  5 24
     A N71                                  DSPATR(HI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  71                                  DSPATR(RI PC)
      *
      *
      * Mode field; can never be amended.
     A                                  6  2'Core or Bespoke'
     A                                  6 22':'
     A            DFDMOD         7A  O  6 24
      *
      * Fields that can be only be amended if bespoke or insert.
     A                                  7  2'Description'
     A                                  7 22':'
     A            DFMBTX        50A  B  7 24CHECK(LC)
     A N72                                  DSPATR(HI)
     A  36                                  DSPATR(UL)
     A N36                                  DSPATR(PR)
     A  72                                  DSPATR(RI PC)
      *
     A                                  8  2'Priority'
     A                                  8 22':'
     A            DFIPTY         1D 0B  8 24
     A N73                                  DSPATR(HI)
     A  36                                  DSPATR(UL)
     A N36                                  DSPATR(PR)
     A  73                                  DSPATR(RI PC)
      *
     A                                  9  2'To run or not (O/I)'
     A                                  9 22':'
     A            DFDTYP         1A  B  9 24
     A N74                                  DSPATR(HI)
     A  36                                  DSPATR(UL)
     A N36                                  DSPATR(PR)
     A  74                                  DSPATR(RI PC)
      *
     A                                 10  2'Before or After'
     A                                 10 22':'
     A            DFFMTO        10A  B 10 24
     A N75                                  DSPATR(HI)
     A  36                                  DSPATR(UL)
     A N36                                  DSPATR(PR)
     A  75                                  DSPATR(RI PC)
      *
     A                                 11  2'Layer'
     A                                 11 22':'
     A            DFLAYR         7A  B 11 24
     A N76                                  DSPATR(HI)
     A  36                                  DSPATR(UL)
     A N36                                  DSPATR(PR)
     A  76                                  DSPATR(RI PC)
      *
     A                                 12  2'Parameters'
     A                                 12 22':'
     A            DFEXIN        50A  B 12 24
     A N77                                  DSPATR(HI)
     A  36                                  DSPATR(UL)
     A N36                                  DSPATR(PR)
     A  77                                  DSPATR(RI PC)
      *
      * Fields that can be amended for bespoke or core or if insert.
     A                                 13  2'Status'
     A                                 13 22':'
     A            DFUSTS         1A  B 13 24
     A N78                                  DSPATR(HI)
     A  37                                  DSPATR(UL)
     A N37                                  DSPATR(PR)
     A  78                                  DSPATR(RI PC)
      *
     A                                 14  2'Logically deleted ?'
     A                                 14 22':'
     A            DFDDEL         3A  O 14 24
      *
     A          R SM000068F3                OVERLAY
     A                                 12 30'No details to display.'
     A                                      DSPATR(HI)
     A          R SM000068F4                OVERLAY
     A                                 23  2'Enter to confirm'
     A                                      COLOR(BLU)
     A                                 23 21'F12=Cancel'
     A                                      COLOR(BLU)
     A                                 23 34'F5=Refresh'
     A                                      COLOR(BLU)
     A          R SM000068F5                OVERLAY
     A                                 23  2'F10=Confirm delete / reset'
     A                                      COLOR(BLU)
     A                                 23 31'F12=Cancel'
     A                                      COLOR(BLU)
      *
     A          R SM000068S0                SFL
     A  27                                  SFLNXTCHG
     A            DFAORD         1A  B  8  4
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A            DFMBNM        10A     8 10DSPATR(HI)
     A                                      TEXT('Object')
     A            DFPROJ        10A     8 22DSPATR(HI)
     A                                      TEXT('Release')
     A            DFDMOD         7A     8 64DSPATR(HI)
     A                                      TEXT('Mode')
     A            DFMODE         1A  H      TEXT('Mode')
     A            DFMBTX        50A  H      TEXT('Description')
     A            DFIPTY         1D 0H      TEXT('Priority')
     A            DFDTYP         1A  H      TEXT('To run or not')
     A            DFFMTO        10A  H      TEXT('Before or after')
     A            DFLAYR         7A  H      TEXT('Layer')
     A            DFEXIN        50A  H      TEXT('Parameters')
     A            DFUSTS         1A  H      TEXT('Status')
     A            DFDDEL         3A     8 73DSPATR(HI)
     A                                      TEXT('Logically deleted')
     A            DFDEL          1A  H      TEXT('Logically deleted')
      *
     A          R SM000068C0                SFLCTL(SM000068S0)
     A  26                                  SFLCLR
     A  22                                  SFLDSPCTL
     A  23                                  SFLDSP
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(13)
     A N24
     AO 24                                  SFLEND(*MORE)
     A                                      OVERLAY
     A  30                              3  2'Go to:'
     A N30                                  DSPATR(ND)
     A  30        DFSRCH        10A  B  3  9
     A  30                                  DSPATR(HI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(ND)
     A N30                                  DSPATR(PR)
     A  30                              5  2'(A/D/E)'
     A N30                                  DSPATR(ND)
     A  30                              5 10'Object'
     A N30                                  DSPATR(ND)
     A  30                              5 22'Release'
     A N30                                  DSPATR(ND)
     A  30                              5 64'Mode'
     A N30                                  DSPATR(ND)
     A  30                              5 73'Del?'
     A N30                                  DSPATR(ND)
      *
     A          R SM000068S1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            DFMSGK                    SFLMSGKEY
     A            DFPGMQ                    SFLPGMQ
      *
     A          R SM000068C1                TEXT('Program messages')
     A                                      SFLCTL(SM000068S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  28                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            DFPGMQ                    SFLPGMQ
      *****************************************************************
