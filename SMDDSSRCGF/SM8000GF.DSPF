      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SM OTB Migr components maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Implemenatation module                               *
      *                                                               *
      *  DSPF/SM8000DF - Migration components maintenance             *
      *                                                               *
      *  (c) Finastra International Limited 2022                      *
      *                                                               *
      *  Last Amend No. CUP046   *CREATE   Date 18Jul22               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUP046 - One Touch Bridge Automation Changes - Version Upg   *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(27 132)
     A N44                                  CA09
     A                                      CA05
     A N40                                  CA10
     A N41                                  CA03
     A N43                                  CA12
     A                                      INDARA
     A          R SM8000F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2022')
     A            DFDUSR        10A     1  2
     A                                  1 13'SM8000'
     A            DFDATE         7A     1125
     A            DFJOBN        10A     2  2
     A                                  2 40'One Touch Bridge - Migration Compo-
     A                                      nents Maintenance'
     A                                      DSPATR(HI)
     A                                  2124TIME
     A          R SM8000F1                  OVERLAY
     A                                 26  2'F9=Insert'
     A                                      COLOR(BLU)
     A                                 26 13'F3=Exit'
     A                                      COLOR(BLU)
     A                                 26 22'F5=Refresh'
     A                                      COLOR(BLU)
      *
     A          R SM8000F2                  OVERLAY
      * Key fields; can only be amended if insert.
     A                                  4  2'Component.......:'
     A            DDCMP         10A  B  4 20
     A N70                                  DSPATR(HI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  70                                  DSPATR(RI PC)
      *
      * Mode field; can never be amended.
     A                                 12  2'Core or Bespoke.:'
     A            DDCRBS        12A  O 12 20
      *
      * Fields that can be only be amended if bespoke or insert.
     A                                  6  2'Description.....:'
     A            DDSEQ          5N 0B  5 20
     A N71                                  DSPATR(HI)
     A  36                                  DSPATR(UL)
     A N36                                  DSPATR(PR)
     A  71                                  DSPATR(RI PC)
      *
     A                                  7  2'Program.........:'
     A            DDPGM         10A  B  7 20
     A                                      CHECK(LC)
     A N77                                  DSPATR(HI)
     A  36                                  DSPATR(UL)
     A N36                                  DSPATR(PR)
     A  77                                  DSPATR(RI PC)
      *
     A                                  8  2'Parameters......:'
     A            DDPAR        100A  B  8 20
     A                                      CHECK(LC)
     A N72                                  DSPATR(HI)
     A  36                                  DSPATR(UL)
     A N36                                  DSPATR(PR)
     A  72                                  DSPATR(RI PC)
      *
      *
      * Fields that can be amended for bespoke or core or if insert.
     A                                  9  2'Run Y/N.........:'
     A            DDRUN          1A  B  9 20
     A N75                                  DSPATR(HI)
     A  37                                  DSPATR(UL)
     A N37                                  DSPATR(PR)
     A  75                                  DSPATR(RI PC)
      *
     A                                 10  2'Bypass Y/N......:'
     A            DDBYP          1A  B 10 20
     A N74                                  DSPATR(HI)
     A  37                                  DSPATR(UL)
     A N37                                  DSPATR(PR)
     A  74                                  DSPATR(RI PC)
     A*
     A* Key fields; can only be amended if insert.
     A                                  5  2'Sequence........:'
     A            DDDES         70A  B  6 20
     A                                      CHECK(LC)
     A N76                                  DSPATR(HI)
     A  36                                  DSPATR(UL)
     A N36                                  DSPATR(PR)
     A  76                                  DSPATR(RI PC)
     A                                 11  2'Layer...........:'
     A            DDLYR          7A  B 11 20
     A N73                                  DSPATR(HI)
     A  36                                  DSPATR(UL)
     A N36                                  DSPATR(PR)
     A  73                                  DSPATR(RI PC)
      *
      *
     A          R SM8000F3                  OVERLAY
     A                                 12 58'No details to display.'
     A                                      DSPATR(HI)
     A          R SM8000F4                  OVERLAY
     A                                 26  2'Enter to confirm'
     A                                      COLOR(BLU)
     A                                 26 20'F12=Cancel'
     A                                      COLOR(BLU)
     A                                 26 32'F5=Refresh'
     A                                      COLOR(BLU)
     A          R SM8000F5                  OVERLAY
     A                                 26  2'F10=Confirm delete / reset'
     A                                      COLOR(BLU)
     A                                 26 30'F12=Cancel'
     A                                      COLOR(BLU)
      *
     A          R SM8000S0                  SFL
     A  27                                  SFLNXTCHG
     A            DFAORD         1A  B  8  7
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A            DFCMP         10A     8 13DSPATR(HI)
     A            DFSEQ          5A     8 26DSPATR(HI)
     A            DFDES         70A     8 36DSPATR(HI)
     A            DFLYR          7A     8109DSPATR(HI)
     A            DFMODE         7A     8119DSPATR(HI)
     A            DDCMP         10A  H
     A            DDCRBS        12A  H
     A            DDSEQ          5N 0H
     A            DDPGM         10A  H
     A            DDPAR        100A  H
     A            DDRUN          1A  H
     A            DDBYP          1A  H
     A            DDDES         70A  H
     A            DDLYR          7A  H
      *
     A          R SM8000C0                  SFLCTL(SM8000S0)
     A  26                                  SFLCLR
     A  22                                  SFLDSPCTL
     A  23                                  SFLDSP
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(17)
     A N24
     AO 24                                  SFLEND(*MORE)
     A                                      OVERLAY
     A  30                              4  2'Position to sequence:'
     A N30                                  DSPATR(ND)
     A  30        X_SEQ          5N 0B  4 24
     A  30                                  DSPATR(HI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(ND)
     A N30                                  DSPATR(PR)
     A  30                              6  4'(A/D/E)'
     A N30                                  DSPATR(ND)
     A  30                              6 13'Component'
     A N30                                  DSPATR(ND)
     A  30                              6 26'Sequence'
     A N30                                  DSPATR(ND)
     A  30                              6 36'Description'
     A N30                                  DSPATR(ND)
     A  30                              6109'Layer'
     A N30                                  DSPATR(ND)
     A  30                              6119'Mode'
     A N30                                  DSPATR(ND)
      * Criteria
     A            X_CMP         10A  B  7 13
     A            X_DES         70A  B  7 36
     A                                      CHECK(LC)
     A            X_LAY          7A  B  7109
     A            X_MOD          7A  B  7119
     A                                      CHECK(LC)
     A

     A          R SM8000S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(27)
     A            DFMSGK                    SFLMSGKEY
     A            DFPGMQ                    SFLPGMQ
      *
     A          R SM8000C1                  TEXT('Program messages')
     A                                      SFLCTL(SM8000S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  28                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            DFPGMQ                    SFLPGMQ
      *****************************************************************
