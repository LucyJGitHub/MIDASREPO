      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UP OTM attention key screen')                    *
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  SM8005GF - OTR attention key screen                          *
      *                                                               *
      *  (c) Finastra International Limited 2022                      *
      *                                                               *
      *  Last Amend No. MD060980           Date 30Jan23               *
      *  Prev Amend No. CUP046 *CREATE     Date 01Jun22               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD060980 - Improvement to CUP046                             *
      *             1. make sure subsystem is started                 *
      *             2. send different msg depending on scenarios      *
      *             3. add option to cancel process                   *
      *  CUP046 - One Touch Bridge Automation Changes - Version Upg   *
      *                                                               *
      *****************************************************************

     A                                      CA03
     A                                      CA12
      * Start of MD060980
     A                                      CA10
      * End of MD060980
     A N56                                  CA06
     A                                      CA07
     A  24                                  CA04
     A                                      INDARA
     A          R SM8005F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2022')
     A            DFDUSR        10A     1  2
     A                                  1 13'SM8005'
     A            DFDATE         7A     1 72
     A            DFJOBN        10A     2  2
     A                                  3 18'One Touch Bridge - Migration Atten-  -
     A                                      tion Key Menu'
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A                                  6  3'Press F3 to signoff and leave the -
     A                                      One Touch Bridge Migration running.'
     A                                  7  3'Or press F12 to get back to the mo-
     A                                      nitor screen.'
     A N56                              8  3'Or press F6 to halt the migration-
     A                                       immediately.'
     A                                  9  3'Or press F7 to halt the migration-
     A                                       before a component.'
     A  24                             10  3'Or press F4 to check status of mig-
     A                                      ration.'
     A  22                             12  7'Halt before migration component:'
     A  22        ##CMP         10   B 12 40
     A  52N21 22                            DSPATR(RI PC)
     A N52N21 22                            DSPATR(HI UL)
     A  21 22                               DSPATR(PR)
     A  22        ##SEQ          5N 0B 12 51
     A  53N21 22                            DSPATR(RI PC)
     A N53N21 22                            DSPATR(HI UL)
     A  21 22                               DSPATR(PR)
      *
     A  57        ##NARR        48A  O 17 17
      * Start of MD060980
     A  25                             19  3'Or press F10 to cancel the migrati-
     A                                      on process.'
     A  24                             20  7'Confirm (Y/N):'
     A  24        ##CNF          1   B 20 22
     A  58N23 24                            DSPATR(RI PC)
     A N58N23 24                            DSPATR(HI UL)
     A  23 24                               DSPATR(PR)
      * End of MD060980
     A          R SM8005S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            DFMSGK                    SFLMSGKEY
     A            DFPGMQ                    SFLPGMQ
      *
     A          R SM8005C1                  TEXT('Program messages')
     A                                      SFLCTL(SM8005S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  28                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            DFPGMQ                    SFLPGMQ
      *****************************************************************
