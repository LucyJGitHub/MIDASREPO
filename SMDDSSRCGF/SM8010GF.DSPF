      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UP OTB Migration initialize')                    *
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  SM8010GF - OTB Migration intiailize screen                   *
      *                                                               *
      *  (c) Finastra International Limited 2022                      *
      *                                                               *
      *  Last Amend No. CUP046 *CREATE     Date 18Jul22               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUP046 - One Touch Bridge Automation Changes - Version Upg   *
      *                                                               *
      *****************************************************************

     A                                      CA03
     A                                      INDARA
     A          R SM8010F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2022')
     A                                      OVERLAY
     A            DFDUSR        10A     1  2
     A                                  1 13'SM8010'
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            DFJOBN        10A     2  2
     A                                  3 23'One Touch Bridge - Migration Initi- -
     A                                      alize'
     A                                      DSPATR(HI)
     A                                  2 71TIME

     A          R SM8010F10                 OVERLAY
     A                                  6  3'Enter release level of the old sys-
     A                                      tem:'
     A            #RELEASE      22   B  6 42
     A                                      CHECK(LC)
     A  56N21                               DSPATR(RI PC)
     A N56N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A  21                             23 11'Enter=Confirm and update system va-
     A                                      lue'
     A                                      COLOR(BLU)

     A          R SM8010F1                  OVERLAY
     A                                  6  3'Enter the following system values:'
     A                                  8 54'System Value'
     A                                  8  3'Description'
     A            #BRGLIB       10   B  9 54
     A  51N21                               DSPATR(RI PC)
     A N51N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A                                  9  3'BrgDeliveredBrgLib - Implementatio-
     A                                      n Library (*I).:'
     A            #STRLIB       10   B 10 54
     A  56N21                               DSPATR(RI PC)
     A N56N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A                                 10  3'BrgDeliveredStrPrLib - Stored proc--
     A                                      edures lib (*Q):'
     A            #GLBPFX        2   B 13 54
     A  52N21                               DSPATR(RI PC)
     A N52N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A                                 13  3'BrgMidasGlobalPrefix - Global Laye-
     A                                      r prefix.......:'
     A            #ZONPFX        2   B 14 54
     A  53N21                               DSPATR(RI PC)
     A N53N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A                                 14  3'BrgMidasSystemPrefix - Zone Layer -
     A                                      prefix.........:'
     A            #GLOPFX        2   B 17 54
     A  54N21N22                            DSPATR(RI PC)
     A N54N21N22                            DSPATR(HI UL)
     A  21
     AO 22                                  DSPATR(PR)
     A                                 17  3'BrgOldGlobalPrefix - Old Global La-
     A                                      yer prefix.....:'
     A            #ZOOPFX        2   B 18 54
     A  55N21                               DSPATR(RI PC)
     A N55N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A                                 18  3'BrgOldZonePrefix - Old Midas Zone -
     A                                       prefix........:'
     A                                 19  3'BrgOldSystemRlsLvl - Release level--
     A                                       of old system:.'
     A            #ORLSLV       22   O 19 54
     A* 56N21                               DSPATR(RI PC)
     A*N56N21                               DSPATR(HI)

     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A  21                             23 11'Enter=Confirm and update system va-
     A                                      lues'
     A                                      COLOR(BLU)
     A                                 12  3'New system'
     A                                 16  3'Old system '

     A          R SM8020F1                  OVERLAY
     A                                  6  3'Confirm layer(s) to be migrated:'
     A N22        #MIGNRG       31   O  9  3
     A N22        #MIGGYN        1   B  9 36
     A  61N21                               DSPATR(RI PC)
     A N61N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A N22                              9 39'(Y/N)'
     A            #MIGNRZ       31   O 10  3
     A            #MIGZYN        1   B 10 36
     A  62N21                               DSPATR(RI PC)
     A N62N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A  21                             23 11'Enter=Confirm'
     A                                      COLOR(BLU)
     A                                 10 39'(Y/N)'
     A                                 12  3'End copy at system storage %'
     A                                 12 39'(01-99)'
     A            #SYSSTG        2  0B 12 35
     A  63N21                               DSPATR(RI PC)
     A N63N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A                                 13  3'End copy if storage jumps %'
     A            #JUMP          2  0B 13 35
     A  64N21                               DSPATR(RI PC)
     A N64N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A                                 13 39'(1-10)'
     A N22        #CURASP       50     14  3

     A          R SM8031F1                  OVERLAY
     A                                  6  3'Migrate global layer database: ent-
     A                                      er up to 20 files, data queues or d-
     A                                      ata '
     A                                  7  3'areas to not be migrated.'
     A                                  9  3'Library to copy data from  . . . >'
     A                                 10  3'Library to copy data to  . . . . >'
     A            #MIGFRM       10   O  9 38
     A                                      DSPATR(HI)
     A            #MIGTO        10   O 10 38
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A  21                             23 11'Enter=Confirm'
     A                                      COLOR(BLU)
     A            ##GOBJ1       10   B 13  3
     A  41N21                               DSPATR(RI PC)
     A N41N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GTYP1        9   B 13 14
     A  61N21                               DSPATR(RI PC)
     A N61N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GOBJ2       10   B 13 25
     A  42N21                               DSPATR(RI PC)
     A N42N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GTYP2        9   B 13 36
     A  62N21                               DSPATR(RI PC)
     A N62N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GOBJ3       10   B 13 47
     A  43N21                               DSPATR(RI PC)
     A N43N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GTYP3        9   B 13 58
     A  63N21                               DSPATR(RI PC)
     A N63N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GOBJ4       10   B 14  3
     A  44N21                               DSPATR(RI PC)
     A N44N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GTYP4        9   B 14 14
     A  64N21                               DSPATR(RI PC)
     A N64N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GOBJ5       10   B 14 25
     A  45N21                               DSPATR(RI PC)
     A N45N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GTYP5        9   B 14 36
     A  65N21                               DSPATR(RI PC)
     A N65N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GOBJ6       10   B 14 47
     A  46N21                               DSPATR(RI PC)
     A N46N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GTYP6        9   B 14 58
     A  66N21                               DSPATR(RI PC)
     A N66N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GOBJ7       10   B 15  3
     A  47N21                               DSPATR(RI PC)
     A N47N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GTYP7        9   B 15 14
     A  67N21                               DSPATR(RI PC)
     A N67N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GOBJ8       10   B 15 25
     A  48N21                               DSPATR(RI PC)
     A N48N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GTYP8        9   B 15 36
     A  68N21                               DSPATR(RI PC)
     A N68N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GOBJ9       10   B 15 47
     A  49N21                               DSPATR(RI PC)
     A N49N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GTYP9        9   B 15 58
     A  69N21                               DSPATR(RI PC)
     A N69N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GOBJ10      10   B 16  3
     A  50N21                               DSPATR(RI PC)
     A N50N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GTYP10       9   B 16 14
     A  70N21                               DSPATR(RI PC)
     A N70N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GOBJ11      10   B 16 25
     A  51N21                               DSPATR(RI PC)
     A N51N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GTYP11       9   B 16 36
     A  71N21                               DSPATR(RI PC)
     A N71N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GOBJ12      10   B 16 47
     A  52N21                               DSPATR(RI PC)
     A N52N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GTYP12       9   B 16 58
     A  72N21                               DSPATR(RI PC)
     A N72N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GOBJ13      10   B 17  3
     A  53N21                               DSPATR(RI PC)
     A N53N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GTYP13       9   B 17 14
     A  73N21                               DSPATR(RI PC)
     A N73N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GOBJ14      10   B 17 25
     A  54N21                               DSPATR(RI PC)
     A N54N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GTYP14       9   B 17 36
     A  74N21                               DSPATR(RI PC)
     A N74N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GOBJ15      10   B 17 47
     A  55N21                               DSPATR(RI PC)
     A N55N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GTYP15       9   B 17 58
     A  75N21                               DSPATR(RI PC)
     A N75N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GOBJ16      10   B 18  3
     A  56N21                               DSPATR(RI PC)
     A N56N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GTYP16       9   B 18 14
     A  76N21                               DSPATR(RI PC)
     A N76N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GOBJ17      10   B 18 25
     A  57N21                               DSPATR(RI PC)
     A N57N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GTYP17       9   B 18 36
     A  77N21                               DSPATR(RI PC)
     A N77N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GOBJ18      10   B 18 47
     A  58N21                               DSPATR(RI PC)
     A N58N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GTYP18       9   B 18 58
     A  78N21                               DSPATR(RI PC)
     A N78N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GOBJ19      10   B 19  3
     A  59N21                               DSPATR(RI PC)
     A N59N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GTYP19       9   B 19 14
     A  79N21                               DSPATR(RI PC)
     A N79N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GOBJ20      10   B 19 25
     A  60N21                               DSPATR(RI PC)
     A N60N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##GTYP20       9   B 19 36
     A  80N21                               DSPATR(RI PC)
     A N80N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A                                 12  3'Object'
     A                                 12 25'Object'
     A                                 12 14'Obj type'
     A                                 12 47'Object'
     A                                 12 36'Obj type'
     A                                 12 58'Obj type'

     A          R SM8032F1                  OVERLAY
     A                                  6  3'Migrate zone layer database: enter-
     A                                       up to 20 files, data queues or dat-
     A                                      a'
     A                                  7  3'areas to not be migrated:'
     A                                  9  3'Library to copy data from  . . . >'
     A                                 10  3'Library to copy data to  . . . . >'
     A            #MIZFRM       10   O  9 38
     A                                      DSPATR(HI)
     A            #MIZTO        10   O 10 38
     A                                      DSPATR(HI)
     A                                 12  3'Object'
     A                                 12 25'Object'
     A                                 12 14'Obj type'
     A                                 12 47'Object'
     A                                 12 36'Obj type'
     A                                 12 58'Obj type'
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A  21                             23 11'Enter=Confirm'
     A                                      COLOR(BLU)
     A            ##ZOBJ1       10   B 13  3
     A  41N21                               DSPATR(RI PC)
     A N41N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZTYP1        9   B 13 14
     A  61N21                               DSPATR(RI PC)
     A N61N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZOBJ2       10   B 13 25
     A  42N21                               DSPATR(RI PC)
     A N42N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZTYP2        9   B 13 36
     A  62N21                               DSPATR(RI PC)
     A N62N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZOBJ3       10   B 13 47
     A  43N21                               DSPATR(RI PC)
     A N43N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZTYP3        9   B 13 58
     A  63N21                               DSPATR(RI PC)
     A N63N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZOBJ4       10   B 14  3
     A  44N21                               DSPATR(RI PC)
     A N44N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZTYP4        9   B 14 14
     A  64N21                               DSPATR(RI PC)
     A N64N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZOBJ5       10   B 14 25
     A  45N21                               DSPATR(RI PC)
     A N45N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZTYP5        9   B 14 36
     A  65N21                               DSPATR(RI PC)
     A N65N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZOBJ6       10   B 14 47
     A  46N21                               DSPATR(RI PC)
     A N46N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZTYP6        9   B 14 58
     A  66N21                               DSPATR(RI PC)
     A N66N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZOBJ7       10   B 15  3
     A  47N21                               DSPATR(RI PC)
     A N47N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZTYP7        9   B 15 14
     A  67N21                               DSPATR(RI PC)
     A N67N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZOBJ8       10   B 15 25
     A  48N21                               DSPATR(RI PC)
     A N48N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZTYP8        9   B 15 36
     A  68N21                               DSPATR(RI PC)
     A N68N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZOBJ9       10   B 15 47
     A  49N21                               DSPATR(RI PC)
     A N49N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZTYP9        9   B 15 58
     A  69N21                               DSPATR(RI PC)
     A N69N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZOBJ10      10   B 16  3
     A  50N21                               DSPATR(RI PC)
     A N50N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZTYP10       9   B 16 14
     A  70N21                               DSPATR(RI PC)
     A N70N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZOBJ11      10   B 16 25
     A  51N21                               DSPATR(RI PC)
     A N51N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZTYP11       9   B 16 36
     A  71N21                               DSPATR(RI PC)
     A N71N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZOBJ12      10   B 16 47
     A  52N21                               DSPATR(RI PC)
     A N52N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZTYP12       9   B 16 58
     A  72N21                               DSPATR(RI PC)
     A N72N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZOBJ13      10   B 17  3
     A  53N21                               DSPATR(RI PC)
     A N53N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZTYP13       9   B 17 14
     A  73N21                               DSPATR(RI PC)
     A N73N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZOBJ14      10   B 17 25
     A  54N21                               DSPATR(RI PC)
     A N54N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZTYP14       9   B 17 36
     A  74N21                               DSPATR(RI PC)
     A N74N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZOBJ15      10   B 17 47
     A  55N21                               DSPATR(RI PC)
     A N55N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZTYP15       9   B 17 58
     A  75N21                               DSPATR(RI PC)
     A N75N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZOBJ16      10   B 18  3
     A  56N21                               DSPATR(RI PC)
     A N56N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZTYP16       9   B 18 14
     A  76N21                               DSPATR(RI PC)
     A N76N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZOBJ17      10   B 18 25
     A  57N21                               DSPATR(RI PC)
     A N57N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZTYP17       9   B 18 36
     A  77N21                               DSPATR(RI PC)
     A N77N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZOBJ18      10   B 18 47
     A  58N21                               DSPATR(RI PC)
     A N58N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZTYP18       9   B 18 58
     A  78N21                               DSPATR(RI PC)
     A N78N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZOBJ19      10   B 19  3
     A  59N21                               DSPATR(RI PC)
     A N59N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZTYP19       9   B 19 14
     A  79N21                               DSPATR(RI PC)
     A N79N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZOBJ20      10   B 19 25
     A  60N21                               DSPATR(RI PC)
     A N60N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)
     A            ##ZTYP20       9   B 19 36
     A  80N21                               DSPATR(RI PC)
     A N80N21                               DSPATR(HI UL)
     A  21                                  DSPATR(PR)

     A          R SM8010S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            DFMSGK                    SFLMSGKEY
     A            DFPGMQ                    SFLPGMQ
      *
     A          R SM8010C1                  TEXT('Program messages')
     A                                      SFLCTL(SM8010S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  28                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            DFPGMQ                    SFLPGMQ
      *****************************************************************
