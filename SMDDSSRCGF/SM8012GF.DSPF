      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SM OTB Migr components maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Implemenatation module                               *
      *                                                               *
      *  DSPF/SM8012DF - Migration components logs enquiry            *
      *                                                               *
      *  (c) Finastra International Limited 2022                      *
      *                                                               *
      *  Last Amend No. CUP046 *CREATE     Date 18Jul22               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUP046 - One Touch Bridge Automation Changes - Version Upg   *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(27 132)
     A N44                                  CA09
     A                                      CA05
     A N40                                  CA10
     A N41                                  CA03
     A N43                                  CA12
     A                                      INDARA
     A          R SM8012F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2022')
     A            PSUSER        10A     1  2
     A                                  1 13'SM8012'
     A            DFDATE         7A     1125
     A            PSJOBNAME     10A     2  2
     A                                  2 40'One Touch Bridge - Migration Compo-
     A                                      nent Logs Enquiry'
     A                                      DSPATR(HI)
     A                                  2124TIME
     A          R SM8012F1                  OVERLAY
     A                                 26  4'F3=Exit'
     A                                      COLOR(BLU)

     A          R SM8012F3                  OVERLAY
     A                                 12 58'No details to display.'
     A                                      DSPATR(HI)
     A          R SM8012F4                  OVERLAY
     A                                 26  2'Enter to confirm'
     A                                      COLOR(BLU)
     A                                 26 20'F12=Cancel'
     A                                      COLOR(BLU)
     A                                 26 32'F5=Refresh'
     A                                      COLOR(BLU)
      *
     A          R SM8012S0                  SFL
     A  27                                  SFLNXTCHG
     A            DFAORD         1A  B  8  3
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A            DFSTRD         5A     8  6DSPATR(HI)
     A            DFSTRT         8A     8 12DSPATR(HI)
     A            DFENDD         5A     8 23DSPATR(HI)
     A            DFENDT         8A     8 29DSPATR(HI)
     A            DFRUNT          T  O  8 39DSPATR(HI)
     A                                      TIMFMT(*HMS)
     A                                      TIMSEP(':')
     A            DFBYP          1A     8 48
     A                                      COLOR(RED)
     A            DFDES         70A     8 50DSPATR(HI)
     A            DFLYR          7A     8124DSPATR(HI)
     A            DDCMP         10A  H
     A            DDCRBS        12A  H
     A            DDSEQ          5A  H
     A            DDJNM         10A  H
     A            DDJUS         10A  H
     A            DDJNB          6A  H
     A            DDBYP          1A  H
     A            DDDES         70A  H
     A            DDLYR          7A  H
     A            ##HST           T  H
     A            ##HET           T  H
      *
     A          R SM8012C0                  SFLCTL(SM8012S0)
     A  26                                  SFLCLR
     A  22                                  SFLDSPCTL
     A  23                                  SFLDSP
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(17)
     A N24
     AO 24                                  SFLEND(*MORE)
     A                                      OVERLAY
     A  30                              6  2'(V)'
     A*N30                                  DSPATR(ND)
     A  30                              6  6'Start date/time'
     A*N30                                  DSPATR(ND)
     A  30                              6 23'End date/time'
     A*N30                                  DSPATR(ND)
     A  30                              6 50'Description'
     A*N30                                  DSPATR(ND)
     A  30                              6124'Layer'
     A*N30                                  DSPATR(ND)
     A            X_DES         70A  B  7 50
     A                                      CHECK(LC)
     A            X_LAY          7A  B  7124
     A*N30                                  DSPATR(ND)
     A  30                              6 39'Runtime'
     A

     A          R SM8012S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(27)
     A            DFMSGK                    SFLMSGKEY
     A            DFPGMQ                    SFLPGMQ
      *
     A          R SM8012C1                  TEXT('Program messages')
     A                                      SFLCTL(SM8012S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  28                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            DFPGMQ                    SFLPGMQ
      *****************************************************************
