      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SM Report of mis / matches - summary')           *
      *****************************************************************
      *                                                               *
      *  Midas - Implementation module                                *
      *                                                               *
      *  SM000035P1 - Report of mis / matches - summary               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2011            *
      *                                                               *
      *  Last Amend No. CBF005   *CREATE   Date 04Jul11               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CBF005 - BF Infrastructure: Single Security User Maintenance *
      *           Profile                                             *
      *                                                               *
      *****************************************************************
      *
     A          R FHDR                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2011')
     A                                      SKIPB(2)
      *
     A                                     5'SM000035P1'
     A                                    29'Report of Matches and Mismatches +
     A                                      - summary'
     A                                    80'Date:'
     A                                    86DATE
     A                                      EDTWRD('  /  /  ')
     A                                    95'Time:'
     A                                   101TIME
     A                                      EDTWRD('  :  :  ')
     A                                   121'Page:'
     A                                   126PAGNBR
     A                                      EDTCDE(3)
     A                                      SPACEA(2)
      *
     A                                    27'User 01'
     A                                    97'User 02'
     A                                      SPACEA(1)
     A                                     2'-----------------------------------
     A                                      -----------------------'
     A                                    72'-----------------------------------
     A                                      -----------------------'
     A                                      SPACEA(1)
     A                                     2'Profile'
     A                                    14'Rec Nos.'
     A                                    23'% Matches'
     A                                    34'Mismatch Nos.'
     A                                    49'% Mismatch'
     A                                    62'Matches'
     A                                    72'Profile'
     A                                    84'Rec Nos.'
     A                                    93'% Matches'
     A                                   104'Mismatch Nos.'
     A                                   119'% Mismatch'
     A                                   132'Matches'
     A                                      SPACEA(1)
      *
     A          R FDTL
     A            PUSR1         10         2
     A            PRCD1          7  0     14EDTCDE(3)
     A            PMPC1          3  0     26EDTCDE(3)
     A            PNM01          7  0     37EDTCDE(3)
     A            PNPC1          3  0     52EDTCDE(3)
     A            PMTCH          7  0     62EDTCDE(3)
     A            PUSR2         10        72
     A            PRCD2          7  0     84EDTCDE(3)
     A            PMPC2          3  0     96EDTCDE(3)
     A            PNM02          7  0    107EDTCDE(3)
     A            PNPC2          3  0    122EDTCDE(3)
     A                                      SPACEA(1)
      *
     A          R FTLR
     A                                      SPACEB(2)
     A                                    51'*** End of report ***'
