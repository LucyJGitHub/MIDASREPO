      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SM Update Change User on GPCURRPD')              *
      *****************************************************************
      *                                                               *
      *  Midas - Implementation module                                *
      *                                                               *
      *  SMU00219P1 - Midas SM Update Change User on GPCURRPD Report  *
      *                                                               *
      *  (c) Finastra International Limited 2014                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD031342           Date 24Nov14               *
      *                 CFB001  *CREATE    Date 12Nov14               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD031342 - Error encountered when running from Bridge.       *
      *  CFB001 - FusionBanking Midas Rebranding                      *
      *                                                               *
      *****************************************************************
 
      ** Report Header Record Format
 
     A          R HEADER
     A                                      TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2014')
     A                                      SKIPB(003)
     A                                     6'Reference SMU00219P1'
      ** Start of Change MD031342
     A**********  BJURPT        53A       37                                                MD031342
     A**********                         101'Date'                                          MD031342
     A**********  BJMRDT         7A      106                                                MD031342
     A                                   101'Time'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      ** End of Change MD031342
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
      ** Start of Change MD031342
     A**********                            SPACEA(1)                                       MD031342
     A                                      SPACEA(2)
 
     A**********                         101'Time'                                          MD031342
     A**********                         106TIME                                            MD031342
     A**********                            EDTWRD('  :  :  ')                              MD031342
     A**********                            SPACEA(1)                                       MD031342
      ** End of Change MD031342
 
     A N14                                 6'AUDIT MODE'
     A                                      SPACEB(001)
     A  14                                 6'UPDATE MODE'
     A                                      SPACEB(001)
     A N14                                50'TO UPDATE CHANGE USER ON +
     A                                       GPCURRPD'
     A                                      SPACEB(001)
     A  14                                50'UPDATED CHANGE USER ON +
     A                                       GPCURRPD'
     A                                      SPACEB(001)
     A N14                                50'-------------------------+
     A                                      ---------'
     A                                      SPACEB(001)
     A  14                                50'----------------------+
     A                                      ---------'
     A                                      SPACEB(001)
 
     A                                    10'CURRENCY CODE'
     A                                      SPACEB(002)
     A                                    25'DESCRIPTION'
     A                                    65'CHANGE USER'
     A                                    80'NEW CHANGE USER'
     A                                   100'STATUS'
     A                                      SPACEA(001)
 
      ** Report Detail
 
     A          R DETAIL
     A                                      SPACEA(001)
     A            PCCYCD         3A       10
     A            PDESC         30A       25
     A            POCGUS        10A       65
     A            PNCGUS        10A       80
     A            PSTAT         10A      100
 
      ** Report Summary
 
     A          R TRAILER                   SPACEB(02)
     A N14                                51'Number of Records to +
     A                                      Update:'
     A            PNORE          5  0     87EDTCDE(4)
     A  14                                51'Number of Records +
     A                                      Updated: '
     A            RNUPDT         5  0     87EDTCDE(4)
 
     A N11                                51'   ***  END OF REPORT  ***'
     A                                      SPACEB(003)
     A  11                                51'***  NO DETAILS TO REPORT +
     A                                         ***'
     A                                      SPACEB(003)
      *****************************************************************
