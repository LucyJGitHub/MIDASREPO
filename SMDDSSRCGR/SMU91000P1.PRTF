      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas SM Utility Printer File for GPGLFCTD')           *
      *****************************************************************
      *                                                               *
      *  Midas - Implementation Module                                *
      *                                                               *
      *  PRTF/SMU91000P1 - Midas SM Utility Printer File for GPGLFCTD *
      *                                                               *
      *  (c) Finastra International Limited 2021                      *
      *                                                               *
      *   Last Amend No. MD058864  *CREATE  Date 18Nov21              *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD058864 - GOC0100 00130 fails during COB due to match       *
      *             field error in file GZCDEPPL0.                    *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      *** Report Header Record Format  *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Finastra International Li-
     A                                      mited 2021')
      *
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A                                     6
     A                                      'REFERENCE SMU91000P1'
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     6
     A                                      'UPDATE'
     A                                      SPACEB(001)
     A                                    37
     A                                      'Records in GPGLFCTD that were upda-
     A                                      ted by the Utility'
     A                                      SPACEB(002)
     A                                    37
     A                                      '-----------------------------------
     A                                      ------------------'
     A                                      SPACEB(001)
     A                                    36
     A                                      'Module'
     A                                      SPACEB(003)
     A                                    45
     A                                      'Global File'
     A                                    59
     A                                      'Global Key - 6'
     A                                    76
     A                                      'Global Key - 7'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAILP1
      *
     A                                      SPACEB(001)
     A            GMODE          2A  O    36
     A            GFILE         10A  O    45
     A            GKEY6         10A  O    59
     A            GKEY7         10A  O    76
      *
      *-------------------*
      *** No Details      *
      *-------------------*
      *
     A          R NODETP1
     A                                    50
     A                                      '*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(003)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TOTALP1
     A                                      SPACEB(003)
      *
     A                                    29
     A                                      'No. of Records Updated in GPGLFCTD-
     A                                      :'
     A            TOTAL1        10  0     65
     A                                      EDTWRD('        0 ')
     A                                    55
     A                                      '*** END OF REPORT ***'
     A                                      SPACEB(001)
      *
      *-------------------*
      *** Database Error  *
      *-------------------*
      *
     A          R DBERROR
     A                                      SPACEB(001)
     A                                    47
     A                                      'DATABASE ERROR'
     A            DBASE          3  0     62
     A                                    66
     A                                      'IN PROGRAM'
     A            DBPGM         10        77
     A                                    47
     A                                      'FILE'
     A                                      SPACEB(001)
     A            DBFILE         8        52
     A                                    62
     A                                      'KEY'
     A            DBKEY         29        66
      *
