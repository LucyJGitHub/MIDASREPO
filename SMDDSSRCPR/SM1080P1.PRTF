      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SM Discrepancy of PCMD/PARA by MGRP ICDE')
      *****************************************************************
      *                                                               *
      *  Midas - Implementation module                                *
      *                                                               *
      *  PRTF/SM1080P1 - Discrepancy of PCMD/PARA by MGRP ICDE        *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSF002 *CREATE     Date 29Sep03               *
      *                 xxxxxx             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSF002 - Global Processing changes for SPF.                  *
      *                                                               *
      *****************************************************************
      *
      *  Formats for Program SM1080
      *
     A          R HEAD1                     TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      SKIPB(2)
      *
     A            PSUSER        10         3
     A                                    21'Differences of Menu Program and +
     A                                      Parameters by Group and Item +
     A                                      between MITEMDD and SMSFCVPD'
     A                                   119'Date'
     A                                   124DATE
     A                                      EDTWRD('  /  /  ')
     A                                      SPACEA(1)
     A            PSJOBNAME     10         3
     A                                   119'Time'
     A                                   124TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                     3'SM1080P1'
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(3)
     A                                      SPACEA(2)
     A                                     3'Menu'
     A                                    10'Menu'
     A                                      SPACEA(1)
     A                                     3'Group'
     A                                    10'Item'
     A                                    38'Menu Program'
     A                                    59'Menu Program Parameters'
     A                                      SPACEA(2)
      *
     A          R NORECS1
                                          40'*** No details to report ***'
     A                                      SPACEA(2)
      *
     A          R DTL1
     A            MGRP           4  0      3
     A            ICDE           4        10
     A                                    24'In MITEMDD :'
     A            PCMD          20        38
     A            PARA          70        59
     A                                      SPACEA(1)
     A                                    24'In SMSFCVPD:'
     A            XXPCMD        20        38
     A            XXPARA        70        59
     A                                      SPACEA(2)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                      SPACEB(2)
     A                                    51'***  END OF REPORT  ***'
