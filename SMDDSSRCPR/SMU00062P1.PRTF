      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SM Rpt. for SC194912 - MC records on FACHISA')
      *****************************************************************
      *                                                               *
      *  Midas - Implementation module                                *
      *                                                               *
      *  SMU00062P1 - Datapatch Printer File Program                  *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. BG26993            Date 26Jan10               *
      *                 194912 *C *CREATE  Date 25Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BG26993 - Delivery of upgrade programs to core.              *
      *          - Renamed S194912P.                                  *
      *  194912 - This reports facility history with ST records that  *
      *           should be change to 'MC'.                           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
     A          R HEADER1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                      SKIPB(003)
     A                                     6'Reference SMU00062P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'Date'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'Time'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A N14                                 6'AUDIT MODE'
     A                                      SPACEB(001)
     A  14                                 6'UPDATE MODE'
     A                                      SPACEB(001)
     A N14                                44'FACILITY HISTORY WITH ST +
     A                                       ACTION FOR MULTI-CCY'
     A                                      SPACEB(001)
     A  14                                50'UPDATED FACILITY HISTORY +
     A                                       RECORDS'
     A                                      SPACEB(001)
     A N14                                44'-------------------------+
     A                                      ---------------------'
     A                                      SPACEB(001)
     A  14                                50'-------------------------+
     A                                      --------'
     A                                      SPACEB(001)
      *
     A                                     4'BRANCH'
     A                                      SPACEB(001)
     A                                    12'CUSTOMER'
     A                                    22'FACILITY'
     A                                    32'SEQ.'
     A                                    37'VALUE DATE'
     A                                    50'LOAN'
     A                                      SPACEA(001)
      *
      ** Report Detail
      *
     A          R DETAIL1
     A                                      SPACEA(001)
     A            RBRCA          3A        5
     A            RCNUM          6A       13
     A            RFACT          3A       23
     A                                    26'/'
     A            RFCNO          2A       27
     A            RFSEQ          2A       33
     A            RVDAT          7A       38
     A            RLNRF          6A       49
      *
      ** Report Summary
      *
     A          R TRAILER1
     A                                    35'Number of Multi-currency +
     A                                      Rollover Action for Loans'
     A                                      SPACEB(003)
     A            RNORE          5  0     87EDTCDE(3)
     A N14                                35'Number of ST Records +
     A                                      in FACHISA to be changed to MC'
     A                                      SPACEB(001)
     A  14                                35'Number of Updated Facility +
     A                                       History'
     A                                      SPACEB(001)
     A            RCHNG          5  0     87EDTCDE(3)
      *
     A N11                                51'   ***  END OF REPORT  ***'
     A                                      SPACEB(003)
     A  11                                51'***  NO DETAILS TO REPORT +
     A                                         ***'
     A                                      SPACEB(003)
      *
      ** Database Error
      *
     A          R DBERROR1
     A                                      SKIPB(17)
     A                                    34'**** DATABASE ERROR ****'
     A                                      SPACEA(1)
     A                                    20'*************************'
     A                                    45'*************************'
     A                                    70'*****'
     A                                      SPACEA(1)
     A                                    20'*    FILE NAME'
     A            DBFILE         8        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    KEY'
     A            DBKEY         29        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    AT'
     A            DBASE          3  0     39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*************************'
     A                                    45'*************************'
     A                                    70'*****'
     A                                      SKIPA(43)
     A                                    35'*** END OF REPORT ***'
