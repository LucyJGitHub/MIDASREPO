      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SM Datapatch utility program - CLOANCL')         *
      *****************************************************************
      *                                                               *
      *  Midas - Implementation module                                *
      *                                                               *
      *  SMU00063P1 - Printer file for SC201762 for Audit and Update  *
      *              modes.  The report displays loans with           *
      *              corrupted NOPS field within PF/CLOANCL.          *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 BG26993            Date 26Jan10               *
      *                 MDFIX              Date 23Apr02               *
      *                 201762 *CREATE     Date 04Mar02               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  BG26993 - Delivery of upgrade programs to core.              *
      *          - Renamed S201762P.                                  *
      *  MDFIX  - Show processing type of Parts. Sold loan.           *
      *  201762 - NOPS field of the original loan is not properly     *
      *           updated upon maturity of the attached parts sold.   *
      *                                                               *
      *****************************************************************
      *
     A          R PAGHDG                    TEXT('(c) FINASTRA INTERNATIONAL +
     A                                      LIMITED +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     1'REFERENCE:'
     A                                    12'SMU00063P1'
     A            REPT          53A       35
     A                                    94'TIME:'
     A                                   100TIME
     A                                      EDTWRD('  :  :  ')
     A                                   111'DATE:'
     A            RUND           7A      117
     A                                      SPACEA(1)
      *
     A  01                                 1'UPDATE MODE'
     A N01                                 1'AUDIT MODE'
     A                                   111'PAGE:'
     A                                   119PAGNBR EDTCDE(1)
     A                                      SPACEA(2)
      *
     A                                    36'*** LOANS THAT HAVE +
     A                                       CORRUPTED NO. OF +
     A                                       PARTS. SOLD FIELD ***'
     A                                      UNDERLINE
     A                                      HIGHLIGHT
     A                                      SPACEA(3)
      *
     A          R DETHDG
      *
     A                                      SPACEA(2)
     A                                     9'Loan No.'
     A                                      HIGHLIGHT
     A                                    22'Record ID'
     A                                      HIGHLIGHT
     A                                    35'Loan Type'
     A                                      HIGHLIGHT
     A                                    48'Sub Type'
     A                                      HIGHLIGHT
     A                                    60'Proc. Type'
     A                                      HIGHLIGHT
     A N09                                74'No. Of Parts. Sold'
     A N09                                  HIGHLIGHT
     A  09                                74'No Of Parts(Updated)'
     A  09                                  HIGHLIGHT
     A                                    97'Parts Sold Loan No.'
     A                                      HIGHLIGHT
      *                                              Start /MDFIX/
     A**********                         120'Record ID'
     A                                   123'Record ID'
      *                                               End  /MDFIX/
     A                                      HIGHLIGHT
     A                                      SPACEA(1)
      *                                              Start /MDFIX/
     A                                    97'/Processing Type   '
     A                                      HIGHLIGHT
     A                                      SPACEA(1)
      *                                               End  /MDFIX/
      *
     A          R DETAIL
      *
     A                                      SPACEA(1)
      **Start of change CLE148
     A**********  WLNRF1         6S 0     10                                                  CLE148
     A            WLNRF1         6A       10
      **End of change CLE148
     A            WRECI1         1A       26
     A            WLTYP1         2A       38
     A            WSUTP1         2A       51
     A            WPTYP1         2S 0     64
     A            WNOPS1         3S 0     82
      **Start of change CLE148
     A*N07******  WLNRF2         6S 0    103                                                  CLE148
     A N07        WLNRF2         6A      103
      **End of change CLE148
     A  07                               103'NONE'
      *                                              Start /MDFIX/
     A N07        WPTYP2         2S 0    111
     A  07                               111'  '
     A**********  WRECI2         1A      124
     A            WRECI2         1A      128
      *                                               End  /MDFIX/
      *
     A          R DETAIL2
     A                                      SPACEA(1)
      **Start of change CLE148
     A**********  WLNRF2         6S 0    103                                                  CLE148
     A            WLNRF2         6A      103
      **End of change CLE148
      *                                              Start /MDFIX/
     A            WPTYP2         2S 0    111
     A**********  WRECI2         1A      124
     A            WRECI2         1A      128
      *                                               End  /MDFIX/
      *
     A          R TOTALS
     A                                      SPACEB(1)
     A                                    51'Number of loans +
     A                                       read  ='
     A                                      HIGHLIGHT
     A            COUNT1         6  0     84EDTWRD('    0 ')
     A                                      HIGHLIGHT
     A                                      SPACEA(1)
     A  01                                51'Number of loans +
     A                                       updated  ='
     A                                      HIGHLIGHT
     A N01                                51'Number of loans +
     A                                       to be updated  ='
     A                                      HIGHLIGHT
     A            COUNT          6  0     84EDTWRD('    0 ')
     A                                      HIGHLIGHT
     A                                      SPACEA(2)
      *
     A          R END
      *
     A                                      SPACEB(1)
     A  02                                52'*** NO DETAILS TO REPORT ***'
     A N02                                55'*** END OF REPORT ***'
      *
     A          R DBERR
      *
     A                                      SPACEB(1)
     A                                    54'*** DATABASE ERROR ***'
