      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas SM Correct SPIN and INTR in CLOANCL')            *
      *****************************************************************
      *                                                               *
      *  Midas - Implementation module                                *
      *                                                               *
      *  SMU00099P1 - Datapatch Program to Update PF/CLOANCL          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. BG26993            Date 26Jan10               *
      *                 206257  *Create    Date 21Jun02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  BG26993 - Delivery of upgrade programs to core.              *
      *          - Renamed S20625P1.                                  *
      *  206257 - Utility to update interest rate INTR and spread     *
      *           indicator (SPIN) fields of risk loans in PF/CLOANCL.*
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER                    TEXT('(C) MISYS INTERNATIONAL +
     A                                      BANKING SYSTEMS LTD. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'Reference SMU00099P1'
     A            BJURPT    R             40REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A N14                                 6'AUDIT MODE'
     A  14                                 6'UPDATE MODE'
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'DATAPATCH PROGRAM TO +
     A                                      UPDATE PF/CLOANCL'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    46'---------------------+
     A                                      -----------------'
     A                                      SPACEA(2)
      *
      *----------------------------------*
      ***  Detail Record Format - Header *
      *----------------------------------*
      *
     A          R DTLSHDR
      *
     A                                    70'--------- FILE ----------'
     A                                   100'------- EXPECTED --------'
      *
     A                                       SPACEA(1)
      *
     A                                    10'LOAN NUMBER'
     A                                    26'BASE CODE'
     A                                    40'BASE RATE'
     A                                    54'RATE/SPREAD'
     A                                    70'SPR IND'
     A                                    82'INTEREST RATE'
     A                                   100'SPR IND'
     A                                   112'INTEREST RATE'
      *
     A                                      SPACEA(2)
      *
      *--------------------------------*
      ***  Detail Record Format - Body *
      *--------------------------------*
      *
     A          R DTLSBDY
     A                                      SPACEB(1)
      *
      **Start of change CLE148
     A**********  LNRF           6S  O    12                                                  CLE148
     A            LNRF           6A  O    12
      **End of change CLE148
     A            BRTT           2S  O    29
     A            BRTE          11S 7O    39EDTWRD('  0 .       ')
     A            RTSP          11S 7O    54EDTWRD('  0 .       ')
     A            SPIN           1A  O    73
     A            INTR          11S 7O    83EDTWRD('  0 .       ')
     A            NSPIN          1A  O   103
     A            NINTR         11S 7O   113EDTWRD('  0 .       ')
      *
     A                                      SPACEA(1)
      *
      *--------------------*
      ***  Report Trailers *
      *--------------------*
      *
     A          R NOREC
     A                                      SPACEB(2)
     A                                    20'Number of records read :'
     A            NOREAD         5S 0     55
     A                                      EDTCDE(1)
     A                                      SPACEA(1)
     A N14                                20'Number of records to be +
     A                                      updated : '
     A  14                                20'Number of records update+
     A                                      d :'
     A            NOUPDT         5S 0     55
     A                                      EDTCDE(1)
     A                                      SPACEA(1)
      *
      *
     A          R NODTLS
     A                                    54'*** NO RECORD TO UPDATE +
     A                                      ***'
     A                                      SPACEB(3)
      *
     A          R TRAILER
     A                                    57'*** END OF REPORT ***'
     A                                      SPACEB(3)
      *
      ** Database Error
      *
     A          R DBERR                     SPACEB(3)
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29A       30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3S 0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
     A                                      SPACEA(2)
     A                                    57
     A                                      '*** END OF REPORT ***'
