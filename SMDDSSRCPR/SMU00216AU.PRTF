      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Utility to delete records from FCPREQL0 - Audit')
      *****************************************************************
      *                                                               *
      *  Midas - FC Module                                            *
      *                                                               *
      *  PRTF/SMU00216AU - Utility to delete selected report name     *
      *                    from FCPREQL0 and related files - Audit    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Last Amend No. BUG27092 *CREATE   Date 16Mar10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG27092 - Utility program to delete a specific report name  *
      *             from FCPREQL0 as well as all related files        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      **-------------------------------*
      **  Report Header Record Format  *
      **-------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2010')
      *
     A                                      SKIPB(3)
      *
     A                                     6'REFERENCE SMU00216AU'
     A            RURPT     R             37REFFLD(URPT)
     A                                   101'DATE'
     A            RMRDT     R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                    +1PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A  80                                40'Deleted Records from FCPREQL0 - +
     A                                       DELETE MODE'
     A                                      SPACEA(1)
     A N80                                40'Deleted Records from FCPREQL0 - +
     A                                       AUDIT MODE'
     A                                      SPACEA(1)
      *
     A                                    40'--------------------------------+
     A                                       -----------'
     A                                      SPACEA(3)
     A                                    10'REPORT NAME : '
     A            RPRT          10A       24
     A                                      SPACEA(003)
      *
      **---------------------*
      **  File Controls      *
      **---------------------*
      *
     A          R CONTROL
      *
     A                                    20'File Name'
     A  80                                50'Total Number of Records Deleted'
     A N80                                50'Total Number of Records for +
     A                                       Deletion'
     A                                    20'----------'
     A                                      SPACEB(001)
     A  80                                50'-------------------------------'
     A N80                                50'----------------------------+
     A                                       --------'
     A                                    20'FCPREQL0'
     A                                      SPACEB(002)
     A            R1CNT         10S 0     61EDTCDE(3)
     A                                    20'FCPARPL0'
     A                                      SPACEB(002)
     A            R2CNT         10S 0     61EDTCDE(3)
     A                                    20'MAARCPL2'
     A                                      SPACEB(002)
     A            R3CNT         10S 0     61EDTCDE(3)
     A                                    20'FCRREQL9'
     A                                      SPACEB(002)
     A            R4CNT         10S 0     61EDTCDE(3)
     A                                    20'FCPARDL0'
     A                                      SPACEB(002)
     A            R5CNT         10S 0     61EDTCDE(3)
     A                                    20'MAARCDL2'
     A                                      SPACEB(002)
     A            R6CNT         10S 0     61EDTCDE(3)
      *
      *
      **--------------*
      **  No Details  *
      **--------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      **---------------------*
      **  Database Error     *
      **---------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
      *------------------*
      ***  End of Report *
      *------------------*
      *
     A          R ENDRPT
     A                                    56'*** END OF REPORT ***'
     A                                      SPACEB(2)
      *
