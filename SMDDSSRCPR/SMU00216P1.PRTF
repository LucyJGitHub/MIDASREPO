      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Utility to delete records from FCPREQL0 - Detail')
      *****************************************************************
      *                                                               *
      *  Midas - FC Module                                            *
      *                                                               *
      *  PRTF/SMU00216P1 - Utility to delete selected report name     *
      *                    from FCPREQL0 and related files - Detail   *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. BUG27092 *CREATE   Date 17Mar10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG27092 - Utility program to delete a specific report name  *
      *             from FCPREQL0 as well as all related files        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      **------------------------------*
      ** Report Header Record Format  *
      **------------------------------*
      *
     A          R HEADPY
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2010')
     A                                      SKIPB(001)
     A                                     6
     A                                      'REFERENCE SMU00216P1'
     A                                      SKIPB(002)
     A            RURPT     R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            RMRDT     R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                   101
     A                                      'TIME'
     A                                      SPACEB(001)
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A  80                                49
     A                                      'Records Deleted from FCPREQL0'
     A                                      SPACEB(002)
     A N80                                44
     A                                      'Records Read for Deletion from FCP-
     A                                      REQL0'
     A                                      SPACEB(002)
     A                                    44
     A                                      '-----------------------------------
     A                                      -----'
     A                                      SPACEB(001)
     A                                     5
     A                                      'REPORT NAME : '
     A                                      SPACEB(002)
     A            R1RPRT        10A       19
      *
      *---------------------------*
      ***  Detail Record Format 1 *
      *---------------------------*
      *
     A          R OUT1
     A                                    10
     A                                      '============================='
     A                                      SPACEB(002)
     A                                    10
     A                                      'PERIODIC SEQUENCE NUMBER : '
     A                                      SPACEB(001)
     A            RD9PSQN        2A       37
     A                                    10
     A                                      '============================='
     A                                      SPACEB(001)
     A                                    10
     A                                      '*********** FCPREQL0 ***********'
     A                                      SPACEB(002)
     A                                    10
     A                                      'Report Description'
     A                                      SPACEB(001)
     A                                    65
     A                                      'Freq'
     A                                    75
     A                                      'Compnt Name'
     A                                    90
     A                                      'Compnt Seq. No.'
     A            RD9PDSC       50A       10
     A                                      SPACEB(001)
     A            RD9FREQ        1A       65
     A            RD9COTT       10A       75
     A            RD9CSEQ        5A       90
      *
      *---------------------------*
      ***  Detail Record Format 2 *
      *---------------------------*
      *
     A          R OUT2
     A                                    10
     A                                      '*********** FCPARPL0 ***********'
     A                                      SPACEB(002)
     A                                    10
     A                                      'Parameters'
     A                                      SPACEB(001)
     A            RD8PARM      100A       10
     A                                      SPACEB(001)
     A
      *
      *---------------------------*
      ***  Detail Record Format 3 *
      *---------------------------*
      *
     A          R OUT3A
     A                                    10
     A                                      '*********** MAARCPL2 ***********'
     A                                      SPACEB(002)
     A                                    10
     A                                      'Archive ID'
     A                                      SPACEB(001)
     A                                    30
     A                                      'Print File Name'
      *
     A          R OUT3B
     A            RECARID       10A       10
     A                                      SPACEB(001)
     A            RECRSFX       10A       30
      *
      *---------------------------*
      ***  Detail Record Format 4 *
      *---------------------------*
      *
     A          R OUT4
     A                                    15
     A                                      '-----------------------------+
     A                                       ------------------'
     A                                      SPACEB(002)
     A                                    15
     A                                      'REPORT REQUESTS PERIODIC SEQU+
     A                                      ENCE NUMBER : '
     A                                      SPACEB(001)
     A            RD5RQSQ        5A       58
     A                                    15
     A                                      '-----------------------------+
     A                                       ------------------'
     A                                      SPACEB(001)
     A                                    15
     A                                      '*********** FCRREQL9 ***********'
     A                                      SPACEB(002)
     A                                    15
     A                                      'Level'
     A                                      SPACEB(001)
     A                                    25
     A                                      'Component Name'
     A                                    50
     A                                      'Component Sequence Number'
     A            RD5LVL         1A       15
     A                                      SPACEB(001)
     A            RD5COTT       10A       25
     A            RD5CSEQ        5A       50
      *
      *---------------------------*
      ***  Detail Record Format 5 *
      *---------------------------*
      *
     A          R OUT5
     A                                    15
     A                                      '*********** FCPARDL0 ***********'
     A                                      SPACEB(002)
     A                                    15
     A                                      'Parameters'
     A                                      SPACEB(001)
     A            RD7PARM      100A       15
     A                                      SPACEB(001)
     A
      *
      *---------------------------*
      ***  Detail Record Format 6 *
      *---------------------------*
      *
     A          R OUT6A
     A                                    15
     A                                      '*********** MAARCDL2 ***********'
     A                                      SPACEB(002)
     A                                    15
     A                                      'Archive ID'
     A                                      SPACEB(001)
     A                                    35
     A                                      'Print File Name'
      *
     A          R OUT6B
     A            REBARID       10A       15
     A                                      SPACEB(001)
     A            REBRSFX       10A       35
      *
      **-----------------*
      ** Report Trailer  *
      **-----------------*
      *
     A          R TRAILPY
     A                                    51
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(002)
