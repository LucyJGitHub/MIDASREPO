      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Utility to populate front office ID in GLCOLAPD-AU')   *
      *****************************************************************
      *                                                               *
      *  Midas - FC Module                                            *
      *                                                               *
      *  PRTF/SMU00225AU - Utility to populate blank front office     *
      *                    transaction ID in file GLCOLAPD - Audit    *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. BUG28101 *CREATE   Date 06Sep10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG28101 - Error in component GOC01000 00066                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      **-------------------------------*
      **  Report Header Record Format  *
      **-------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2010')
      *
     A                                      SKIPB(3)
      *
     A                                     6'REFERENCE SMU00225AU'
     A            RURPT     R             37REFFLD(URPT)
     A                                   101'DATE'
     A            RMRDT     R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                    +1PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A  80                                40'Updated Records from +
     A                                      GLCOLAPD - UPDATE MODE'
     A                                      SPACEA(1)
     A N80                                37'Records to be updated +
     A                                      from GLCOLAPD - AUDIT MODE'
     A                                      SPACEA(1)
      *
     A                                    37'----------------------+
     A                                      --------------------------'
     A                                      SPACEA(3)
      *
      **---------------------*
      **  File Controls      *
      **---------------------*
      *
     A          R CONTROL
      *
     A  80                                35'Total Number of Records Updated:'
     A N80                                35'Total Number of Records for +
     A                                      Update:'
     A            R1CNT         10S 0     75EDTCDE(3)
      *
      **--------------*
      **  No Details  *
      **--------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      **---------------------*
      **  Database Error     *
      **---------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
      *------------------*
      ***  End of Report *
      *------------------*
      *
     A          R ENDRPT
     A                                    56'*** END OF REPORT ***'
     A                                      SPACEB(2)
      *
