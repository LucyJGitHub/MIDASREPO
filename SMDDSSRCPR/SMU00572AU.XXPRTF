      *****************************************************************
/*S*D ***PRTFBASE******************************************************                     MD021155
/*E*S ***PR_AU60*******************************************************                     MD021155
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/SMU00572AU - Deals to be updated (Audit)                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2012            *
      *                                                               *
      *  Last Amend No. MD021155 *REDUNDANTDate 23Apr13               *
      *  Prev Amend No. AR665113A *CREATE  Date 20Dec12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD021155 - COB Restructure Phase 1 remnants                  *
      *           - Remove unnecessary logicals.                      *
      *  AR665113A - Deals to be updated (Audit)                      *
      *              (Child: AR665114A)                               *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2012')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SMU00572AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    37'Deals where RODN is to  +
     A                                       be updated'
     A                                      SPACEA(1)
      *
     A                                    37'------------------------+
     A                                       ----------'
     A                                      SPACEA(3)
      *-----------------*
      ***  File Control *
      *-----------------*
     A          R CONTROL
     A                                    58'CALCULATED'
     A                                      SPACEA(1)
     A                                    58'----------'
     A                                      SPACEA(3)
      *
     A                                     6'Deals to be updated:'
      *
     A            RCOUNT         6  0     60EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                     6'--------------------'
     A                                      SPACEA(2)
      *
     A                                     6'Deals updated:      '
      *
     A            RCOUNTA        6  0     60EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                     6'--------------------'
     A                                      SPACEA(2)
      *---------------*
      ***  No Details *
      *---------------*
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-----------------*
      ** Database Error *
      *-----------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
