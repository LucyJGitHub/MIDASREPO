      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Updates Invalid Data in FDDTSTPD File for AR796905')   *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/SMU00612P1 - Updates Invalid Data in FDDTSTPD File      *
      *                                                               *
      *  (C) Copyright Misys International Banking Systems Ltd. 2012  *
      *                                                               *
      *  Last Amend No. AR796904 *CREATE     Date 20Dec12             *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR796904 - Printer file of SMU00612 program that will        *
      *             correct invalid data in FDDTSTPD File.            *
      *             (Child: AR796905)                                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER1                   TEXT('(C) Misys Internati+
     A                                      onal Banking Systems +
     A                                      Ltd. 2012')
      *
     A                                      SKIPB(3)
     A                                     1'REFERENCE SMU00612P1'
     A            BJURPT        53   O    40
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A  50                                 1
     A                                      'UPDATE MODE'
     A                                      SPACEB(1)
     A N50                                 1
     A                                      'AUDIT MODE'
     A                                      SPACEB(1)
     A                                    55
     A                                      'DEAL TYPE/SUBTYPE RECORDS'
     A                                      SPACEB(1)
     A                                    55
     A                                      '-------------------------'
     A                                      SPACEB(1)
     A                                     2
     A                                      'Deal Type'
     A                                      SPACEB(2)
     A                                    13
     A                                      'SubType'
     A                                    23
     A                                      'Narrative'
     A                                    60
     A                                      'Last Change Date'
     A                                    80
     A                                      'Effective Date'
     A                                      SPACEA(1)
      *
      *--------------------------------*
      ***  Report Detail Record Format *
      *--------------------------------*
      *
     A          R DETAIL1
     A            R0DTPE         2   O     3
     A            R0DLST         2   O    13
     A            R0NARR        35   O    23
     A            R0LCD          7   O    60
     A            R0EACC         7   O    80
     A                                      SPACEA(1)
      *
      *--------------------------------*
      ***  Report Footer Record Format *
      *--------------------------------*
      *
     A          R TRAILER1
     A                                      SPACEB(2)
     A  50N60                              3'** FDDTSTPD File UPDATED **'
     A  50N60                             42'TOTAL NO. OF RECORDS UPDATED:'
     A N50N60                             42'TOTAL NO. OF RECORDS FOR UPDATE:'
     A            TOTALU         6  0     73EDTCDE(2)
     A                                      SPACEA(3)
     A N60                                56'*** END OF REPORT ***'
     A  60                                54'*** NO DETAILS TO REPORT ***'
      *
