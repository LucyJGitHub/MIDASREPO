      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Utility to update DRCD and OLDR in ACCNTAB')           *
      *****************************************************************
      *                                                               *
      *  Midas - FC Module                                            *
      *                                                               *
      *  PRTF/SMU00779P1 - Utility to update DRCD and OLDR in ACCNTAB *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2016            *
      *                                                               *
      *  Last Amend No. MD038563 *CREATE   Date 29Jun16               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD038563 - Update DRCD and OLDR in ACCNTAB                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      **------------------------------*
      ** Report Header Record Format  *
      **------------------------------*
      *
     A          R HEADPY
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2016')
     A                                      SKIPB(001)
     A                                     6
     A                                      'REFERENCE SMU00779P1'
     A                                      SKIPB(002)
     A            RURPT     R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            RMRDT     R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                   101
     A                                      'TIME'
     A                                      SPACEB(001)
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A  80                                49
     A                                      'Records Updated from ACCNTAB'
     A                                      SPACEB(002)
     A N80                                46
     A                                      'Records to be Updated from +
     A                                      ACCNTAB'
     A                                      SPACEB(002)
     A                                    46
     A                                      '---------------------------+
     A                                      --------'
     A                                      SPACEB(001)
     A                                    10'Account'
     A                                      SPACEB(002)
     A                                    53'DRCD (Old)'
     A                                    65'DRCD (New)'
     A                                    77'OLDR (Old)'
     A                                    90'OLDR (New)'
     A
     A                                    10'============================'
     A                                      SPACEB(001)
     A                                    53'=========='
     A                                    65'=========='
     A                                    77'=========='
     A                                    90'=========='
      *
      *---------------------------*
      ***  Detail Record Format 1 *
      *---------------------------*
      *
     A          R DETAIL1

     A            RBRCA          3A       10
     A                                      SPACEB(001)
     A            RCNUM          6A       14
     A            RCCY           3A       21
     A            RACOD         10A       25
     A            RACSQ          2A       36
     A            ODRCD          5S       53
     A            NDRCD          5S       65
     A            OOLDR          5S       77
     A            NOLDR          5S       90
      *
      **-----------------*
      ** Report Trailer  *
      **-----------------*
      *
     A          R TRAILPY
     A                                    10'============================'
     A                                      SPACEB(001)
     A                                    10'TOTAL:'
     A                                      SPACEB(001)
     A            RTOTAL         5S       16
     A                                    51
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(002)
