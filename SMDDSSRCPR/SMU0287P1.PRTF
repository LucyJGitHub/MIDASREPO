      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Printer File - Facilities for recalculation')          *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/SMU0287P1 - Datapatch Printer File Program              *
      *                   Used by SMU00286 utility for AR1036835      *
      *                                                               *
      *  (c) Misys Banking Systems 2013                               *
      *                                                               *
      *  Last Amend No. AR1036834 *CREATE  Date 25Jun13               *
      *  Prev Amend No. Xxxxxxxxx          Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR1036834 - Report file which shows all facilities that are  *
      *              tagged for recalculation. (Child: AR1036835)     *
      *                                                               *
      *****************************************************************
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A          R HEADER2
     A                                      TEXT('(c) Misys Banking -
     A                                      Systems 2013')
     A                                      SPACEA(001)
     A                                      SKIPB(001)
     A                                     6
     A                                      'REFERENCE SMU0287P1'
     A            BJURPT        53A  O    37
     A                                   101
     A                                      'DATE'
     A            BJMRDT         7A  O   106
     A                                   122
     A                                      'PAGE'
     A                                    +2
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A N30                                 6
     A                                      'AUDIT MODE'
     A                                      SPACEB(001)
     A  30                                 6
     A                                      'UPDATE MODE'
     A                                      SPACEB(001)
     A                                    43
     A                                      'FACILITIES THAT ARE TAGGED +
     A                                      FOR RECALCULATION'
     A                                      SPACEB(001)
     A                                    35
     A                                      '-------------------------+
     A                                      --------------------------+
     A                                      ---------'
     A                                      SPACEB(001)
     A                                    25
     A                                      'BRANCH'
     A                                      SPACEB(001)
     A                                    36
     A                                      'CUSTOMER'
     A                                    49
     A                                      'FACILITY'
     A                                    61
     A                                      'CCY'
     A                                    68
     A                                      'MCCY'
     A                                    80
     A                                      'PROCESSED?'
     A                                      SPACEA(001)
      *
      *----------------*
      ** Report Detail *
      *----------------*
      *
     A          R DETAIL2
     A                                      SPACEA(001)
     A            BRCA           3A  O    26
     A            CNUM           6A  O    37
     A            FACT           3S 0O    50
     A                                    53
     A                                      '/'
     A            FCNO           2S 0O    54
     A            FCCY           3A  O    61
     A            MCCY           1A  O    69
      *
     A N30                                84
     A                                      'N'
     A  30                                84
     A                                      'Y'
      *
      *-----------------*
      ** Report Summary *
      *-----------------*
      *
     A          R TRAILER2
     A N30                                35
     A                                      'Number of Facilities in +
     A                                      Error'
     A                                      SPACEB(001)
     A  30                                35
     A                                      'Number of Triggered Facilities'
     A                                      SPACEB(001)
     A                                    81'='
     A            FACERR         6S 0O    84
     A                                      EDTCDE(3)
     A N12                                51
     A                                      '   ***  END OF REPORT  ***'
     A                                      SPACEB(003)
     A  12                                51
     A                                      '***  NO DETAILS TO REPORT   +
     A                                      ***'
     A                                      SPACEB(003)
      *
      *-----------------*
      ** Database Error *
      *-----------------*
      *
     A          R DBERROR2
     A                                      SKIPB(17)
     A                                    34'**** DATABASE ERROR ****'
     A                                      SPACEA(1)
     A                                    20'*************************'
     A                                    45'*************************'
     A                                    70'*****'
     A                                      SPACEA(1)
     A                                    20'*    FILE NAME'
     A            DBFILE         8        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    KEY'
     A            DBKEY         29        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    AT'
     A            DBASE          3  0     39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*************************'
     A                                    45'*************************'
     A                                    70'*****'
     A                                      SKIPA(43)
     A                                    35'*** END OF REPORT ***'
