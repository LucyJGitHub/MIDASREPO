      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas SM Utility Printer File for SDCUAHPD')           *
      *****************************************************************
      *                                                               *
      *  Midas - Implementation Module                                *
      *                                                               *
      *  PRTF/SMU03000P1 - Midas SM Utility Printer File for SDCUAHPD *
      *                                                               *
      *  (c) Finastra International Limited 2021                      *
      *                                                               *
      *  Last Amend No. MD057554  *CREATE  Date 08Jun21               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD057554 - Unable to Release customer on SP23 that has been  *
      *             closed on earlier SP version.                     *
      *             Prevent the deletion of a Non-account Holder      *
      *             if it still attached to a Customer.               *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      *** Report Header Record Format  *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Finastra International Li-
     A                                      mited 2021')
      *
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A                                     6
     A                                      'REFERENCE SMU03000P1'
     A            BJURPT        53A  O    37
     A                                   101
     A                                      'DATE'
     A            RUNA           7A      106
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     6
     A                                      'UPDATE'
     A                                      SPACEB(001)
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    37
     A                                      'Records in SDCUAHPD that were dele-
     A                                      ted by the Utility'
     A                                      SPACEB(002)
     A                                    37
     A                                      '-----------------------------------
     A                                      ------------------'
     A                                      SPACEB(001)
     A                                    44
     A                                      'Customer Number'
     A                                      SPACEB(003)
     A                                    64
     A                                      'Authority Holder'
     A                                    64
     A                                      'Number'
     A                                      SPACEB(001)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAILP1
      *
     A                                      SPACEB(001)
     A            PRCUST         6A  O    44
     A            PRHOLD        10A  O    64
      *
      *-------------------*
      *** No Details      *
      *-------------------*
      *
     A          R NODETP1
     A                                    50
     A                                      '*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(003)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TOTALP1
     A                                      SPACEB(003)
      *
     A                                    29
     A                                      'No. of Records Updated in SDCUAHPD-
     A                                      :'
     A            TOTAL1        10  0     65
     A                                      EDTWRD('        0 ')
     A                                    55
     A                                      '*** END OF REPORT ***'
     A                                      SPACEB(001)
      *
      *-------------------*
      *** Database Error  *
      *-------------------*
      *
     A          R DBERROR
     A                                      SPACEB(001)
     A                                    47
     A                                      'DATABASE ERROR'
     A            DBASE          3  0     62
     A                                    66
     A                                      'IN PROGRAM'
     A            DBPGM         10        77
     A                                    47
     A                                      'FILE'
     A                                      SPACEB(001)
     A            DBFILE         8        52
     A                                    62
     A                                      'KEY'
     A            DBKEY         29        66
      *
