      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Report Incorrect NAAD and NASN')                       *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  SMU600P2 - Printer file to report deal amendments & correct  *
      *             values of NAAD and NASN fields.                   *
      *                                                               *
      *  (c) Finastra International Limited 2022                      *
      *                                                               *
      *  Last Amend No. 255626 *CREATE     Date 31Jul08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  255626 - Applied fix 242527                                  *
      *  242527 - DL deal amendment is not shown on projected account *
      *           movements. Applied fix 213349.                      *
      *           Report correct values of deal amendments NAAD and   *
      *           NASN fields.                                        *
      *                                                               *
      *****************************************************************
     A          R HEADER
     A                                  1  5'REF: SMU600P2'
     A        30                        1 20'(AUDIT MODE)'
     A        31                        1 20'(UPDATE MODE)'
     A            TITLE         50A  O  1 38
     A                                  1100'DATE'
     A            AGMRDT         7A  O    +2
     A                                  1120'PAGE'
     A            PAGE           4S  O    +2EDTCDE(Z)
      *
     A          R HEAD2
     A                                  5 30'DEAL AMENDMENTS WITH'
     A                                  5 51'INCORRECT NEXT AMENDMENT'
     A                                  5 76'ACTION DATE AND SEQ. NO.'
     A                                  6 30'---------------------'
     A                                  6 51'-------------------------'
     A                                  6 76'------------------------'
      *
     A          R DETHED
     A                                  9 63'   NEXT AMENDMENT   '
     A                                  9 90'   NEXT AMENDMENT   '
     A                                 10 21'DEAL NO.'
     A                                 10 36'VALUE DATE    SEQ. #'
     A                                 10 63'VALUE DATE    SEQ. #'
     A                                 10 90'VALUE DATE    SEQ. #'
     A                                 11 63'   (B E F O R E)    '
     A                                 11 90'    (A F T E R)     '
     A                                 12 21'--------'
     A                                 12 36'--------------------'
     A                                 12 63'--------------------'
     A                                 12 90'--------------------'
      *
     A          R DETINF
     A                                      SPACEB(001)
     A            DLNO           6S  O    22
     A            VDATF          7   O    37
     A            DASN           3  0O    51
     A            NAADF          7   O    64
     A            NASN           3  0O    78
     A            VDATPF         7   O    91
     A            NASNC          3  0O   105
      *
     A          R TRAIL
     A                                      SPACEB(003)
     A        30                          45'NO. OF RECORDS TO BE'
     A        31                          51'NO. OF RECORDS'
     A                                    66'CORRECTED :'
     A            CNTR           5  0O    81EDTCDE(3)
     A                                      SPACEA(001)
     A                                    45'TOTAL NO. OF RECORDS'
     A                                    66'PROCESSED :'
     A            TOTCTR         5  0O    81EDTCDE(3)
     A                                      SPACEA(002)
     A                                    56'*** END OF REPORT ***'
