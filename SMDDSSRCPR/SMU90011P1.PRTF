      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas SM Printer file for MD-60037 Utility')           *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/SMU90011P1 - Deleted SDSTMDPD Records Report            *
      *                                                               *
      *  (c) Finastra International Limited 2023                      *
      *                                                               *
      *  Last Amend No. MD060037 *CREATE   Date 25May23               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD060037 - Delete record in SDSTMDPD if the account no longer*
      *             exist in ACCNTAB.                                 *
      *                                                               *
      *****************************************************************
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A          R HEADER1
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2023')
     A                                      SPACEA(002)
     A                                      SKIPB(001)
     A                                     6
     A                                      'REFERENCE SMU90011P1'
     A            BJURPT        53A  O    37
     A                                   101
     A                                      'DATE'
     A            BJMRDT         7A  O   106
     A                                   119
     A                                      'PAGE'
     A                                    +2
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A N16                                 6
     A                                      'AUDIT MODE'
     A                                      SPACEB(001)
     A  16                                 6
     A                                      'UPDATE MODE'
     A                                      SPACEB(001)
     A                                    35
     A                                      'DELETED STAMP TAX HISTORY '
     A                                      SPACEB(001)
     A                                    61
     A                                      'RECORDS FOR NON-EXISTING '
     A                                    86
     A                                      'ACCOUNTS'
     A                                    30
     A                                      '-----------------------------------
     A                                      --------------------------------'
     A                                      SPACEB(001)
     A                                     6
     A                                      'KEY FIELD ORIGIN'
     A                                      SPACEB(001)
     A                                    32
     A                                      'ACCOUNT'
     A                                    53
     A                                      'TAX POSTED DATE'
     A                                    70
     A                                      'TAX VALUE DATE'
     A                                    88
     A                                      'TAX IN STAMP TAX CCY'
     A                                   114
     A                                      'TAX RATE'
      *
      *----------------*
      ** Report Detail *
      *----------------*
      *
     A          R DETAIL1
     A                                      SPACEA(001)
     A            PKEY          10A  O     9
     A            PBRCA          3A  O    22
     A            PCNUM          6A  O    26
     A            PCCY           3A  O    33
     A            PACOD         10S 0O    37
     A            PACSQ          2S 0O    48
     A            PPSDT          7A  O    57
     A            PVALD          7A  O    73
     A  50        PTASC0        13  0     88EDTWRD(' ,   ,   ,   , 0 ')
     A  51        PTASC1        13  0     88EDTWRD('   ,   ,   , 0 . ')
     A  52        PTASC2        13  0     88EDTWRD('  ,   ,   , 0 .  ')
     A  53        PTASC3        13  0     88EDTWRD(' ,   ,   , 0 .   ')
     A  54                               105'CR'
     A            PTXRT         13  8    114EDTCDE(3)
      *
      *-----------------*
      ** Report Summary *
      *-----------------*
      *
     A          R TRAILER1
     A N16                                38
     A                                      'Number of Records to be Deleted'
     A                                      SPACEB(003)
     A  16                                38
     A                                      'Number of Records Deleted'
     A                                      SPACEB(003)
     A                                    82
     A                                      '='
     A            WRECER         6S 0O    85
     A                                      EDTCDE(3)
     A N12                                55
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(003)
     A  12                                52
     A                                      '***  NO DETAILS TO REPORT  ***'
     A                                      SPACEB(003)
      *
      *-----------------*
      ** Database Error *
      *-----------------*
      *
     A          R DBERROR1
     A                                      SKIPB(017)
     A                                    34
     A                                      '**** DATABASE ERROR ****'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*************************'
     A                                    45
     A                                      '*************************'
     A                                    70
     A                                      '*****'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*    FILE NAME'
     A            DBFILE         8        39
     A                                    73
     A                                      '*'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*    KEY'
     A            DBKEY         29        39
     A                                    73
     A                                      '*'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73
     A                                      '*'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*    AT'
     A            DBASE          3  0     39
     A                                    73
     A                                      '*'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*************************'
     A                                    45
     A                                      '*************************'
     A                                    70
     A                                      '*****'
     A                                      SKIPA(043)
     A                                    35
     A                                      '*** END OF REPORT ***'
