      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas Retail Discrepancy Report')                      *
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/SMU90012P2 - Account Balance and History Balance        *
      *                    Discrepancy Report                         *
      *                                                               *
      *  (c) Finastra International Banking Sytems Ltd. 2022          *
      *                                                               *
      *  Last Amend No. MD060124 *CREATE   Date 02Aug22               *
      *  Prev Amend No. MD0nnnnn           Date ddMmmyy               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD060124 - Utility to correct discrepancies between history  *
      *             balance and account balance files.                *
      *           - Patterned fix to MD-53040                         *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *-----------------------------*
      * Report Header Record Format *
      *-----------------------------*
      *
     A          R HEADP2                    TEXT('(C) Finastra +
     A                                      International Banking +
     A                                      Systems Ltd. 2022')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE'
     A                                    17'SMU90012P2'
     A            TITL      R             37
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE1        004S 0    124EDTCDE(3) SPACEA(2)
      *
     A                                    38'ACCOUNT BALANCE AND '
     A                                    58'HISTORY BALANCE '
     A                                    74'DISCREPANCY REPORT'
     A                                    95'BRANCH'
     A            BRCA#     R            102REFFLD(BRCA)
     A                                   106'-'
     A            BRNM      R            108
     A                                      SPACEA(1)
     A                                    38'--------------------'
     A                                    58'----------------'
     A                                    74'------------------'
     A                                      SPACEA(1)
     A                                    57'FOR LINKED ACCOUNTS'
     A                                      SPACEA(1)
     A                                    57'-------------------'
     A                                      SPACEA(3)
      *
     A                                     1'ACCOUNT NUMBER'
     A                                    25'RETAIL'
     A                                    50'HISTORY'
     A                                    69'TOTAL GENERATED'
     A N69                                98'CLEARED'
     A                                      SPACEA(1)
     A  69                                98'LEDGER '
     A                                      SPACEA(1)
      *
     A                                    25'NUMBER'
     A                                    50'BALANCE'
     A                                    69'ADJUSTMENT'
     A                                    98'BALANCE'
     A                                   113'DIFFERENCE'
     A                                      SPACEA(2)
      *
      *-----------------------------*
      * Report Detail Record Format *
      *-----------------------------*
      *
     A          R DETP2                     SPACEB(1)
      *
     A            ZBRCA     R              1REFFLD(BRCA)
     A                                     4'-'
     A            ZCUST     R              5REFFLD(CNUMA)
     A                                    11'-'
     A            ZACCY     R             12REFFLD(CCY)
     A                                    15'-'
     A            ZACOD     R             16REFFLD(ACOD)
     A                                    26'-'
     A            ZASEQ     R             27REFFLD(ACSQ)
     A  05                                29'*'
     A            TACNO         10S 0     30
     A  70        OHISB         15S 0     39EDTWRD('   ,   ,   ,   ,   &CR')
     A  70        GEAD          15S 0     64EDTWRD('   ,   ,   ,   ,   &CR')
     A  70        ZCLBL         15S 0     89EDTWRD('   ,   ,   ,   ,   &CR')
     A  70        ZDIFF         15S 0    112EDTWRD('               &CR')
     A  71        OHISB1        15S 1     40EDTWRD('  ,   ,   ,   ,   . &CR')
     A  71        GEAD1         15S 1     65EDTWRD('  ,   ,   ,   ,   . &CR')
     A  71        ZCLBL1        15S 1     90EDTWRD('  ,   ,   ,   ,   . &CR')
     A  71        ZDIFF1        15S 1    113EDTWRD('              . &CR')
     A  72        OHISB2        15S 2     41EDTWRD(' ,   ,   ,   ,   .  &CR')
     A  72        GEAD2         15S 2     66EDTWRD(' ,   ,   ,   ,   .  &CR')
     A  72        ZCLBL2        15S 2     91EDTWRD(' ,   ,   ,   ,   .  &CR')
     A  72        ZDIFF2        15S 2    114EDTWRD('             .  &CR')
     A  73        OHISB3        15S 3     43EDTWRD('   ,   ,   ,   .   &CR')
     A  73        GEAD3         15S 3     68EDTWRD('   ,   ,   ,   .   &CR')
     A  73        ZCLBL3        15S 3     93EDTWRD('   ,   ,   ,   .   &CR')
     A  73        ZDIFF3        15S 3    115EDTWRD('            .   &CR')
      *
      *---------------------------------*
      * Report End Branch Record Format *
      *---------------------------------*
      *
     A          R ENDP2
     A                                      SPACEB(3)
     A                                    46'*** END OF REPORT FOR'
     A                                    67' BRANCH '
     A            BRCA#     R             75REFFLD(BRCA)
     A                                    78' ***'
      *
      *------------------------------*
      * Report Database Error Format *
      *------------------------------*
      *
     A          R ERRP2
     A                                      SPACEB(02)
     A                                     9'REFERENCE SMU90012'
     A                                    45'PRINT TERMINATED, '
     A                                    64'DATABASE IN ERROR'
