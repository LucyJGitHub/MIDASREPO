      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas DL MD-60586 Data Patch Printer File')            *
      *****************************************************************
      *                                                               *
      *  Midas - Money Market (Dealing)                               *
      *                                                               *
      *  PRTF/SMU90014P1 - DL MD-60586 Data Patch Printer File        *
      *                                                               *
      *  (c) Finastra International Limited 2022                      *
      *                                                               *
      *  Last Amend No. MD060586  *CREATE  Date 13Oct22               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD060586 - A term deposit can´t be ended prematurely.        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R SMU90014H1                TEXT('(c) Finastra Internatio +
     A                                      nal Limited 2022')
     A                                      SKIPB(1)
     A                                     6'REFERENCE SMU90014P1'
     A                                    27'- UPDATE'
     A            BJURPT    R   53        38REFFLD(URPT)
     A                                   101'DATE:'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                    28'Data patch for DEALSDC'
     A                                      SPACEB(1)
     A  10                                 2'CDL033 IS ON'
     A N10                                 2'CDL033 IS OFF'
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                    28'----------------------'
     A                                      SPACEA(2)
      *
     A          R SMU90014H2
     A                                    02'DEAL'
     A                                    12'CURRENCY'
     A                                    23'BASE'
     A                                    30'SHORT TERM'
     A                                      SPACEA(1)
     A                                    02'NUMBER'
     A                                    12'CODE'
     A                                    23'RATE'
     A                                    30'RATE'
     A                                      SPACEA(1)
      *
     A          R SMU90014D1
     A                                      SPACEB(1)
     A            DLNO           6S 0     02
     A            CCY            3A       12
     A            BRTT           2A       23
     A            XKSTRT        11S 7     30EDTWRD('   0.       ')
      *
     A          R SMU90014T
     A                                      SPACEB(2)
     A                                     2'NUMBER OF RECORD(S) WERE SHORT TERM
     A                                       RATE WAS ZEROISED'
     A                                    56
     A                                      '='
     A            PCTR           9S 0O    59
     A                                      EDTCDE(3)
     A                                      SPACEA(1)
     A          R SMU90014E
     A                                      SPACEB(1)
     A                                    54'*** END OF REPORT ***'

