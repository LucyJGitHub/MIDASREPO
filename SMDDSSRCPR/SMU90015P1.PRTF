      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Utility to Correct HKRCFR and HKRCDM of MMDELDPP')     *
      *****************************************************************
      *                                                               *
      *  Midas - Money Market Module                                  *
      *                                                               *
      *  PRTF/SMU90015P1 - Midas DL MD-45018 tility to Correct HKRCFR *
      *                    and HKRCDM of MMDELDPP                     *
      *                                                               *
      *  (c) Finastra International Limited 2023                      *
      *                                                               *
      *  Last Amend No. MD045018  *CREATE    Date 03Feb23             *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD045018 - MM deal, with a Base Rate Code, could not be      *
      *              amended due to blank Rate Change Date.           *
      *                                                               *
      *****************************************************************
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER                    TEXT('(c) Finastra International +
     A                                      Limited 2023')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE SMU90015P1'
     A            BJURPT        53        37
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A  80N81                             31'DEALS W/ INCORRECT RATE CHANGE +
     A                                       FREQUENCY/DAY IN MMDELDPP REPORT'
     A                                      SPACEA(2)
     A  81N80                             31'DEALS WITH UPDATED RATE CHANGE +
     A                                       FREQUENCY/DAY IN MMDELDPP REPORT'
     A                                      SPACEA(2)
      *
     A                                    31'--------------------------------+
     A                                       -------------------------------'
     A                                      SPACEA(2)
      *
     A                                    42'TYPE/'
     A                                    56'MMDELDPP'
     A                                    75'DEALSDC'
     A                                    92'MMDELDPP'
     A                                   110'DEALSDC'
     A                                      SPACEA(1)
     A                                    14'BRANCH'
     A                                    25'DEAL NUMBER'
     A                                    41'SUBTYPE'
     A                                    53'RATE CHG FREQ'
     A                                    72'RATE CHG FREQ'
     A                                    90'RATE CHG DAY'
     A                                   107'RATE CHG DAY'
     A                                      SPACEA(1)
     A                                    14'------'
     A                                    25'-----------'
     A                                    41'-------'
     A                                    53'-------------'
     A                                    72'-------------'
     A                                    90'------------'
     A                                   107'------------'
     A                                      SPACEA(1)
      *
      *--------------------------*
      ***  Detail Record Format  *
      *--------------------------*
      *
     A          R DETAIL
      *
     A            RBRCA          3A       15TEXT('Branch')
     A            RDLNO          6  0     28TEXT('Deal Number')
     A            RDTYP          2A       42TEXT('Type')
     A                                    44'/'
     A            RSTYP          2A       45TEXT('Subtype')
     A            RHKRCFR        1A       60TEXT('MMDELDPP Rate Change Freq')
     A  82                                62'***'
     A  84                                56'Blank'
     A            RXKRCFR        1A       79TEXT('DEALSDC Rate Change Freq')
     A  85                                76'Blank'
     A            RHKRCDM        2  0     96TEXT('MMDELDPP Rate Change Day')
     A  83                                99'***'
     A            RXKRCDM        2  0    113TEXT('DEALSDC Rate Change Day')
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  No records processed *
      *-------------------------*
      *
     A          R NOREC                     SPACEB(2)
     A                                    54'*** NO DETAILS TO REPORT ***'
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILER
     A  80N81                             14'Total Number of Deals to Update:'
     A  81N80                             14'Total Number of Deals Updated:'
     A            RCOUNT         9  0     50EDTCDE(3)
     A                                      SPACEA(1)
     A                                    51'***  END OF REPORT  ***'
