      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Housekeeping for GLJENPX0 Report')                     *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger                                       *
      *                                                               *
      *  PRTF/SMU90016P1 - Report for GLJENPX0 Housekeeping           *
      *                                                               *
      *  (c) Finastra International Limited 2023                      *
      *                                                               *
      *  Last Amend No. MD060987 *Create   Date 20Apr23               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD060987 - Utility to move GLJENPX0 records to GLJEPHX0      *
      *             for GLACPHPD equivalent records.                  *
      *                                                               *
      *****************************************************************
      *
      ** REPORT HEADER
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2023')
     A                                      SKIPB(3)
     A                                     7'REFERENCE'
     A                                    17'SMU90016P1'
     A            TITL          30        37
     A                                   101'DATE'
     A            RUNA           7       106
     A                                   119'PAGE'
     A            PAGE           5  0    124EDTCDE(Z) SPACEA(2)
     A                                    56'GLJENPX0 HOUSEKEEPING'
     A                                      SPACEA(1)
     A                                    55'---------------------------------'
     A                                      SPACEA(1)
      *
      *****************************************************************
     A          R DTAIL1
      ** REPORT HEADING DETAILS
      *
     A                                      SPACEA(1)
     A                                    31'ACCT -'
     A            P1BRCA         3        38
     A                                    41'-'
     A            P1CNUM         6        42
     A                                    48'-'
     A            P1CCY          3        49
     A                                    52'-'
     A            P1ACOD        10        53
     A                                    63'-'
     A            P1ACSQ         6        64
      *
     A                                    70'P.DATE -'
     A            P1PSTD         7        80
     A                                    88'BATCH -'
     A            P1BTNO         7        97
     A                                      SPACEA(1)
     A*
      *****************************************************************
     A          R NARRP1
      *
     A            P1NAR1        35A       33
     A                                   132' '
     A                                      SPACEA(1)
      *****************************************************************
     A          R ENDP1
      *
      ** REPORT END
     A                                    56'*** END OF REPORT ***'
     A                                      SPACEB(3)
      *****************************************************************
     A          R ERRP1
      *
      ** REPORT DATABASE ERROR
     A                                      SPACEB(02)
     A                                     9'REFERENCE SMU90016'
     A                                    45'PRINT TERMINATED, '
     A                                    64'DATABASE IN ERROR'
