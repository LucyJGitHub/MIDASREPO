      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas SM Utility Printer File for DEALSDB ')           *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/SMU91003P1 - Midas SM Utility Printer File for Deal's   *
      *                    Replication Indicator field in DEALSDB     *
      *                    file.                                      *
      *                                                               *
      *  (c) Finastra International Limited 2023                      *
      *                                                               *
      *   Last Amend No. MD061168  *CREATE  Date 14Dec23              *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD061168 - Matured Deals not showing in FX Deals Screens     *
      *           - Update old FX deals Replication Indicator (field  *
      *             REPI in DEALSDB) to blank in order for the sys-   *
      *             tem to move them in historic files.               *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      *** Report Header Record Format  *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Finastra International Li-
     A                                      mited 2023')
      *
     A                                      SKIPB(002)
     A                                     3
     A                                      'REFERENCE SMU91003P1'
     A                                   104
     A                                      'TIME'
     A                                   109
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                   122
     A                                      'PAGE'
     A                                   127
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     3
     A                                      'UPDATE'
     A                                      SPACEB(001)
     A                                    36
     A                                      'FX Deal record(s) in DEALSDB that -
     A                                      were updated by the Utility'
     A                                      SPACEB(002)
     A                                    36
     A                                      '-----------------------------------
     A                                      ---------------------------'
     A                                      SPACEB(001)
     A                                    90
     A                                      'REPLICATION INDICATOR'
     A                                      SPACEB(002)
     A                                    16
     A                                      'REC ID'
     A                                      SPACEB(001)
     A                                    27
     A                                      'DEAL NO.'
     A                                    40
     A                                      'DEAL TYPE'
     A                                    54
     A                                      'DEAL SUB TYPE'
     A                                    84
     A                                      'OLD VALUE'
     A                                   +15
     A                                      'NEW VALUE'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DTL01
      *
     A                                      SPACEB(001)
     A            PRECI          1A  O    18
     A            PDLNO          6S 0O    27
     A                                      EDTCDE(3)
     A            PDTYP          2A  O   +10
     A            PDLST          2A  O   +14
     A            PREPI          1A  O   +27
     A            PNREPI         1A  O   +23
      *
      *-------------------*
      *** No Details      *
      *-------------------*
      *
     A          R NODETP1
     A                                      SPACEB(002)
     A                                    53
     A                                      '*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(002)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TOTALP1
     A                                      SPACEB(002)
      *
     A                                    44
     A                                      'No. of Records Updated in DEALSDB -
     A                                      :'
     A            TOTAL1         9  0     80
     A                                      EDTWRD('       0 ')
     A                                    56
     A                                      '*** END OF REPORT ***'
     A                                      SPACEB(001)
      *
      *-------------------*
      *** Database Error  *
      *-------------------*
      *
     A          R DBERROR
     A                                      SPACEB(001)
     A                                    44
     A                                      'DATABASE ERROR'
     A            DBASE          3        59
     A                                    63
     A                                      'IN PROGRAM'
     A            DBPGM         10        74
     A                                    44
     A                                      'FILE'
     A                                      SPACEB(001)
     A            DBFILE         8        49
     A                                    59
     A                                      'KEY'
     A            DBKEY         29        63
      *
