     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2006')
      *****************************************************************
/*S*D****RPGBASEBND****************************************************                       CBF005
      *****************************************************************
      *                                                               *
      *  Midas - Implementation Module                                *
      *                                                               *
      *  SM000021 - Update xxUSER profile                             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      *  Last Amend No. CBF005  *REDUNDANT Date 04Jul11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG9823 *CREATE    Date 09Jan06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CBF005 - BF Infrastructure: Single Security User Maintenance *
      *           Profile                                             *
      *  BUG9823 - Rename duplicate program                           *
      *                                                               *
      *****************************************************************
      ** +--------------------------------------+
      ** ¦ F-specs                              ¦
      ** ¦ =======                              ¦
      ** +--------------------------------------+
     FMUSER     UF   E           K DISK    INFSR(*PSSR)
     FMBROS     UF A E           K DISK    INFSR(*PSSR)
     FMBRBS     UF A E           K DISK    INFSR(*PSSR)
     FMACBR     UF   E           K DISK    INFSR(*PSSR)
 
      *****************************************************************
      /EJECT
      *****************************************************************
      ** +--------------------------------------+
      ** ¦ Automatically included D-specs       ¦
      ** ¦ ==============================       ¦
      ** +--------------------------------------+
 
     D/COPY ZACPYSRC,STD_D_SPEC
      ** Data Area Giving Installation Control Details
 
     D/COPY ZACPYSRC,PSDS
      ** Program Status Data Structure
 
      ** +--------------------------------------+
      ** ¦ End of automatically included D-specs¦
      ** ¦ =====================================¦
      ** +--------------------------------------+
 
      *****************************************************************
      /EJECT
      *****************************************************************
 
      ** +--------------------------------------+
      ** ¦ Manually included D-specs            ¦
      ** ¦ =========================            ¦
      ** +--------------------------------------+
 
      ** +--------------------------------------+
      ** ¦ Named constants                      ¦
      ** ¦ ===============                      ¦
      ** +--------------------------------------+
 
      ** +--------------------------------------+
      ** ¦ Arrays and Data Structures           ¦
      ** ¦ ==========================           ¦
      ** +--------------------------------------+
 
     D SDBRCH        E DS                  EXTNAME(SDBRCHPD)
      ** External DS for branch Details
 
     D DSFDY         E DS                  EXTNAME(DSFDY)
      * Second DS for access programs, long data strucure
 
     D BrchArray       S              3    Dim(900)
 
      ** +--------------------------------------+
      ** ¦ Declared variables                   ¦
      ** ¦ ==================                   ¦
      ** +--------------------------------------+
 
     D Prefix          S              2
     D Idx             S              3  0
     D KeyUser         S                   Like(USRP)
     D WorkVBRX        S                   Like(VBRX)
     D StartPos        S              4  0
     D Len             S              4  0
 
      *****************************************************************
      /EJECT
      *****************************************************************
 
      ** +--- Start of Main processing -----------------------------------+
      ** ¦                                                                ¦
      ** ¦ Initial processing is performed automatically: the *INZSR is   ¦
      ** ¦ executed at program activation.                                ¦
      ** ¦                                                                ¦
      ** +----------------------------------------------------------------+
 
      *********************************************************************
      * MAIN PROCEDURE                                                    *
      *********************************************************************
 
      ** Check that the user profile hasn't already been updated
     C                   Eval      KeyUser = Prefix + 'USER'
     C     KeyUser       Chain(e)  MUSER
     C                   If        %Found
     C                   Eval      *INLR = *On
     C                   Return
     C                   EndIf
 
      ** Get all branches
     C                   Call      'AOBRCHR0'
     C                   Parm                    @RtCd
     C                   Parm      '*FIRST '     @Optn
     C                   Parm                    @DSNB             3
     C     SDBRCH        Parm      SDBRCH        DSFDY
      *
      ** Database error
     C                   If        @RtCd <> *BLANK
     C                   MoveL     '*FIRST'      DBKEY
     C                   MoveL     'SDBRCHPD'    DBFILE                         ************
     C                   MoveL     '001'         DBASE                          * DBERR 001*
     C                   ExSr      *Pssr                                        ************
     C                   EndIf
     C                   Eval      Idx = 0
 
     C                   DoU       @RtCd = '*EOF' or
     C                             Idx = 900
     C                   Eval      Idx += 1
     C                   Eval      BrchArray(Idx) = A8BRCD
 
     C                   Call      'AOBRCHR0'
     C                   Parm                    @RtCd
     C                   Parm      '*NEXT  '     @Optn
     C                   Parm                    @DSNB
     C     SDBRCH        Parm      SDBRCH        DSFDY
      *
      ** Database error
     C                   If        @RtCd <> *BLANK and
     C                             @RtCd <> '*EOF'
     C                   MoveL     '*NEXT '      DBKEY
     C                   MoveL     'SDBRCHPD'    DBFILE                         ************
     C                   MoveL     '002'         DBASE                          * DBERR 002*
     C                   ExSr      *Pssr                                        ************
     C                   EndIf
 
     C                   EndDo
 
      ** Update xxUSER profile on MUSERDD
     C                   Eval      KeyUser = 'xxUSER'
     C     KeyUser       Chain(e)  MUSER
      *
      ** Database error
     C                   If        %Error or not %Found
     C                   MoveL     KeyUser       DBKEY
     C                   MoveL     'MUSERDD '    DBFILE                         ************
     C                   MoveL     '003'         DBASE                          * DBERR 003*
     C                   ExSr      *Pssr                                        ************
     C                   EndIf
 
     C                   Eval      %Subst(USRP:1:2) = Prefix
     C                   Eval      DBRN = BrchArray(1)
     C                   Update    MUSERDDF
 
      ** Update xxUSER profile on MACBRDD
     C     KeyUser       Chain(e)  MACBR
      *
      ** Database error
     C                   If        %Error or not %Found
     C                   MoveL     KeyUser       DBKEY
     C                   MoveL     'MACBRDD '    DBFILE                         ************
     C                   MoveL     '004'         DBASE                          * DBERR 004*
     C                   ExSr      *Pssr                                        ************
     C                   EndIf
 
     C                   Eval      %Subst(USRP:1:2) = Prefix
     C                   Eval      DBRN = BrchArray(1)
     C                   Update    MACBRDDF
 
      ** Set up xxUSER profile on MBROSDD, MBRBSDD
     C     KeyUser       Chain(e)  MBROS
     C                   Eval      RECI = 'D'
     C                   Eval      NOBR = Idx
     C                   Eval      StartPos = %Len(VBRX) - (Idx * 3) +  1
     C                   Eval      Len = Idx * 3
     C                   MoveA     BrchArray     WorkVBRX
     C                   Eval      %Subst(VBRX:StartPos:Len) =
     C                             %Subst(WorkVBRX:1:Len)
     C                   If        %Found
     C                   Update    MBROSDDF
     C                   Else
     C                   Write     MBROSDDF
     C                   EndIf
 
     C     KeyUser       Chain(e)  MBRBS
     C                   If        %Found
     C                   Eval      RECI = 'D'
     C                   Eval      NOBR = Idx
     C                   Eval      %Subst(VBRX:StartPos:Len) =
     C                             %Subst(WorkVBRX:1:Len)
     C                   Update    MBRBSDDF
     C                   Else
     C                   Write     MBRBSDDF
     C                   EndIf
 
     C                   Eval      *INLR = *On
     C                   Return
 
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  *INZSR - Program Initialisation Subroutine                   *
      *                                                               *
      *****************************************************************
 
     C     *INZSR        BegSr
 
     C     *Entry        PList
     C                   Parm                    Prefix
 
     C                   EndSr
 
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  *PSSR  - Program Exception Error Routine                     *
      *                                                               *
      *****************************************************************
 
     C     *PSSR         BegSr
 
     C                   If        RunBefore = *BLANKS
     C                   Eval      RunBefore = 'Y'
     C                   Dump
 
     C                   Call      'DBERRCTL'
 
     C                   EndIf
 
     C                   Eval      *INU7 = *ON
     C                   Eval      *INU8 = *ON
     C                   Eval      *INLR = *ON
     C                   Return
 
     C                   EndSr
 
