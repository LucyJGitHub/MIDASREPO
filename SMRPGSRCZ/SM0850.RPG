     H        1
      *****************************************************************
/*XBIA*  OVRDBF FILE(SARI1) TOFILE(SMSARITPD)                         *
/*XBIB*  OVRDBF FILE(SARI2) TOFILE(SMSARITPD)                         *
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SM Switchable features-menu item file *BUILD')
      *****************************************************************
      *                                                               *
      *  Midas - Implementation module                                *
      *                                                               *
      *  SM0850 - Compare two Switchable Features Menu Item Files     *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSF002             Date 23Jun04               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSF002 - Change of key for SCSARIPD.                         *
      *           Created to compare files in R5 and Midas Plus       *
      *           versions.                                           *
      *                                                               *
      *****************************************************************
      * ID F  C  H  L    FUNCTION OF INDICATORS
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write or chain fail
      *       70 - 81 Indicators for changed fields
      *
     FSARI1   IF  E                    DISK         KINFSR *PSSR
     FSARI2   IF  E           K        DISK         KINFSR *PSSR
     F            SCSARID0                          KRENAMESARIF
     FUPSARIPDO   E                    DISK         KINFSR *PSSR
      *
      /EJECT
     E                    CPY@    1   1 80
      *
      * Fields with the same name will need to be renamed.
      *
     ISARIF
     I              ADSARN                          SARN
     I              ADMGRP                          MGRP
     I              ADICDE                          ICDE
     I              ADRECI                          RECI
     I              ADAVAL                          AVAL
     I              ADINAR                          INAR
      *
      ** Array containing Copyright statement
      *
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *ENTRY    PLIST
     C                     PARM           MODE    1
      *
     C           COMP      KLIST
     C                     KFLD           ADSARN
     C                     KFLD           ADMGRP
     C                     KFLD           ADICDE
      *
     C                     READ SARI1                  6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           *IN20     DOWEQ'0'
     C           COMP      CHAINSARIF                3069
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * If record not found then record has been deleted or added
      *  depending on mode
     C           *IN30     IFEQ *ON
     C                     MOVE MODE      ADACTC
     C                     WRITEUPSARID0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
      * Need to find differences but this should only be run once;
      *  run only if mode is 'Delete'
     C           MODE      IFEQ 'D'
      *
      * If record found then look for any differences; only look in
      *  fields that have data all of the time.
     C           *IN30     IFEQ *OFF
     C                     MOVE 'A'       ADACTC
      * Record ID
     C           ADRECI    IFNE RECI
     C                     SETON                     70
     C                     ENDIF
      * Available if feature present
     C           ADAVAL    IFNE AVAL
     C                     SETON                     71
     C                     ENDIF
      * Menu item narrative
     C           ADINAR    IFNE INAR
     C                     SETON                     72
     C                     ENDIF
      *
     C                     MOVEA*IN,70    INTEST  3
     C                     MOVE INTEST    INTNUM  30
      * If any indicators are set on then write record
     C           INTNUM    IFNE 0
     C                     WRITEUPSARID0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * Set values to write second record
     C                     MOVELRECI      ADRECI
      *
     C                     MOVELAVAL      ADAVAL
      *
     C                     MOVELINAR      ADINAR
      *
     C                     WRITEUPSARID0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
      * Read next record
     C                     MOVEA'000'     *IN,70
     C                     READ SARI1                  6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           ENDPGM    TAG
     C                     SETON                     LR
     C                     RETRN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Finastra International Limited 2004
