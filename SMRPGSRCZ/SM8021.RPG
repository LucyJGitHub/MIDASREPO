     H        1
      *****************************************************************
/*XBI *  OVRDBF FILE(MITEM) TOFILE(SMMITMPD)                          *
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SM Check for MITEMDD duplicate records')
      *****************************************************************
      *                                                               *
      *  Midas - Implementation module                                *
      *                                                               *
      *  SM8021 - Check for duplicate records in MITEMDD              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CPK015  *CREATE    Date 03Jan03               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CPK015 - Check for duplicate records in CBDPRLL5.            *
      *                                                               *
      *****************************************************************
     FMITEM   IF  E           K        DISK
     FSM8020P1O   E             37     PRINTER                        UC
      /EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
     C                     EXSR SRINIT
      *
     C                     EXSR SRMAIN
      *
     C           RECS      IFNE 'Y'
     C                     WRITENORECS
     C                     ENDIF
      *
     C                     CLOSESM8020P1
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT - Initialisation routine.                             *
      *                                                               *
      *  Called by: Main routine                                      *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C           SRINIT    BEGSR
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C                     MOVEL'MITEMDD' #FILE
      *
      ** Initialise main DBerror fields
      *
     C                     Z-ADD0         DBASE
     C                     MOVEL*BLANKS   DBFILE
     C                     MOVEL*BLANKS   DBKEY
      *
     C                     Z-ADD0         #MGRP   40
     C                     MOVEL*BLANKS   #ICDE   4
      *
     C                     MOVEL'Group'   #HDR1
     C                     MOVEL'Code'    #HDR2
     C                     MOVEL*BLANKS   #HDR3
     C                     MOVEL*BLANKS   #HDR4
     C                     MOVEL*BLANKS   #HDR5
      *
     C                     OPEN SM8020P1
     C                     WRITEHEADH                  69
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRMAIN - Main processing                                     *
      *                                                               *
      *  Called by: Main routine                                      *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
     C           SRMAIN    BEGSR
      *
     C                     READ MITEM                    01
      *
     C           *IN01     DOWEQ*OFF
      *
      * If the read record has the same key as the last record, then
      *  delete the recently read record. Otherwise, save the current
      *  value of keys.
     C           MGRP      IFEQ #MGRP
     C           ICDE      ANDEQ#ICDE
      *
     C           *IN37     IFEQ *ON
     C                     WRITEHEADH
     C                     SETOF                     37
     C                     ENDIF
      *
     C                     MOVEL#MGRP     #FLD1
     C                     MOVEL#ICDE     #FLD2
     C                     MOVEL*BLANKS   #FLD3
     C                     MOVEL*BLANKS   #FLD4
     C                     MOVEL*BLANKS   #FLD5
     C                     WRITEDTL1
     C                     MOVEL'Y'       RECS    1
      *
     C                     ELSE
      *
      * Save the value of the current record's keys
     C                     Z-ADDMGRP      #MGRP
     C                     MOVELICDE      #ICDE
      *
     C                     ENDIF
      *
      * Read the next record
     C                     READ MITEM                    01
      *
     C                     ENDDO
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *  Called by: (**calling routines**)                            *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     ENDIF
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2003
