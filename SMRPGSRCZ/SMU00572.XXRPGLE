     H DEBUG
      *****************************************************************
/*S*D ***RPGBASEBND****************************************************                     MD021155
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  SMU00572 - Utility to zeroise RODN when reused               *
      *                                                               *
      *  Function:  This program scans DEALS to check which RODN was  *
      *             reused.                                           *
      *  (Input Cycle)                                                *
      *                                                               *
      *  Called By: SMUC00572 - Utility to zeroise RODN when reused   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2012            *
      *                                                               *
      *  Last Amend No. MD021155 *REDUNDANTDate 23Apr13               *
      *  Prev Amend No. AR665113A *CREATE  Date 20Dec12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD021155 - COB Restructure Phase 1 remnants                  *
      *           - Remove unnecessary logicals.                      *
      *  AR665113A - Utility to zeroise RODN when reused              *
      *              (Child: AR665114A)                               *
      *                                                               *
      *****************************************************************
     FSMU00572L0UF   E           K DISK
      ** Live deals where RODN not equal to 0
     F                                     RENAME(DEALSDCF:DEALSDCU)
      *
     FSMU00572L2UF   E           K DISK
      ** Live deals where RODN not equal to 0
     F                                     RENAME(MMDELDP0:MMDELDPU)
      *
     FMMDEALLL  IF   E           K DISK    PREFIX(M2)
      ** All MM deals
      *
     FSMU00572L1IF   E           K DISK    PREFIX(D2)
      ** All deals
      *
     FSMU00572AUO    E             PRINTER
     F                                     INFDS(SPOOLU)
      *
     FSMU00572P1O    E             PRINTER USROPN
     F                                     INFDS(SPOOL1)
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   37  - Multibranching ON                                     *
      *                                                               *
      *                                                               *
      *   89  - End of File                                           *
      *                                                               *
      *   98  - Date Format: DDMMYY (off); MMDDYY (on)                *
      * 90-99 - Used by Standard Subroutines                          *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  *INZSR - Program initialization                              *
      *  SRPROC - Detail processing                                   *
      *  SRREPT - Process report lines                                *
      *  WP1END - Write End of report                                 *
      *  SRAUDT - Produce audit report and end program                *
      *  *PSSR  - Program Error Processing Subroutine                 *
      *                                                               *
      *****************************************************************
     D/EJECT
     D CPY@            S             80    DIM(1) CTDATA PERRCD(1)
      ** Array containing Copyright statement
      *
      /SPACE 3
     D LDA           E DS           256    EXTNAME(LDA)
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     D RUNDAT        E DS                  EXTNAME(RUNDAT)
      ** Data Area giving Installation Control Details
      *
     D PSDS           SDS
      ** Program Status Data Structure
     D  PGM              *PROC
     D  WSID                 244    253
     D  USER                 254    263
     D SPOOL1          DS
      *
      ** File Information Data Structure for SMU00572P1
     D  SFILE1                83     92
     D  SFNUM1               123    124B 0
     D  OFLLN1               188    189B 0
     D  PRTLN1               367    368B 0
      *
     D SPOOLU          DS
      ** File Information Data Structure for SMU00572AU
     D  SFILEU                83     92
     D  SFNUMU               123    124B 0
      *
      ** Dummy Data Structures used by Access Programs.
      *
     D DSFDY         E DS                  EXTNAME(DSFDY)
      ** First DS for access programs
      *
     D SDBANK        E DS                  EXTNAME(SDBANKPD)
      ** External data structures for Bank Details
      *
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ** Perform detail processing.
      *
     C                   EXSR      SRPROC
      *
      ** Output audit report and end program.
      *
     C                   EXSR      SRAUDT
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
     C/EJECT
      /TITLE SR/SRPROC
      *****************************************************************
      *                                                               *
      *  SRPROC - Subroutine to do the detail processing.             *
      *                                                               *
      *****************************************************************
     C     SRPROC        BEGSR
      *
      ** Read first record from File.
      *
     C                   READ      SMU00572L0                             89
      *
      ** If end of file, perform audit reporting (no
      ** records).
      *
     C     *IN89         IFEQ      *ON
     C                   EXSR      SRAUDT
     C                   ENDIF
      *
      ** Do until end of file.
      *
     C     *IN89         DOWEQ     *OFF
      *
     C                   EXSR      SRREPT
      *
      ** Read next record.
      *
     C                   READ      SMU00572L0                             89
      *
      ** End of do until end of valid records.
      *
     C                   ENDDO
      *
      ** Write final records and totals to report.
      *
     C                   EXSR      WP1END
      *
     C                   ENDSR
      *****************************************************************
      /TITLE SR/SRREPT
      *****************************************************************
      *                                                               *
      *  SRREPT - Process report lines.                               *
      *                                                               *
      *****************************************************************
     C     SRREPT        BEGSR
      *
      ** If RODN is not re-used as DLNO by another deal, OR
      ** If RODN is re-used is a true rolled over deal by DLNO,
      **    THEN exit
      *
     C     RODN          CHAIN     SMU00572L1
     C                   IF        (%FOUND(SMU00572L1) AND DLNO=D2RBDN)
     C                   LEAVESR
     C                   ENDIF
      *
     C                   IF        NOT %FOUND(SMU00572L1)
     C                   MOVE      DLNO          R1DLNO
     C     RODN          CHAIN     MMDEALLL
     C                   IF        (%FOUND(MMDEALLL) AND M2HKDADN=R1DLNO)
     C                             OR NOT %FOUND(MMDEALLL)
     C                   LEAVESR
     C                   ENDIF
     C                   ENDIF
      *
     C     WOPNP1        IFEQ      'N'
     C                   MOVE      'Y'           WOPNP1
     C                   OPEN      SMU00572P1
     C                   ENDIF
      *
      ** Count records read.
      *
     C                   ADD       1             RCOUNT
     C                   MOVE      DLNO          R1DLNO
     C                   MOVE      RODN          R1RODN
     C                   MOVE      ' '           IND
      *
      ** Write out the record.
      *
     C                   Z-ADD     2             RQDLN1
     C     OFLLN1        SUB       PRTLN1        DIFLN1
     C     DIFLN1        IFLE      RQDLN1
     C                   WRITE     HEADP1
     C                   ENDIF
      *
      ** If Update mode, then update file
      *
     C     P0MODE        IFEQ      'U'
     C                   Z-ADD     0             RODN
     C                   Z-ADD     0             RODA
     C                   UPDATE    DEALSDCU
      *
     C     DLNO          CHAIN     SMU00572L2
     C                   IF        %FOUND(SMU00572L2)
     C                   MOVE      ' '           HKDADN
     C                   Z-ADD     0             HKRBAT
     C                   Update    MMDELDPU
     C                   ENDIF
     C                   ADD       1             RCOUNTA
     C                   MOVE      '*'           IND
     C                   ENDIF
      *
      ** Write out Detail Record.
      *
     C                   WRITE     DETAILP1
      *
     C                   ENDSR
      *****************************************************************
      /TITLE SR/WP1END
      *****************************************************************
      *                                                               *
      *  WP1END - Subroutine to write end of report.                  *
      *                                                               *
      *****************************************************************
     C     WP1END        BEGSR
      *
     C     WOPNP1        IFEQ      'N'
     C                   MOVE      'Y'           WOPNP1
     C                   OPEN      SMU00572P1
     C                   ENDIF
      *
      ** Check that sufficient lines remain for the format. If not,
      ** write out the Main Headings on a new page.
      *
     C                   Z-ADD     4             RQDLN1
     C     OFLLN1        SUB       PRTLN1        DIFLN1
     C     DIFLN1        IFLE      RQDLN1
     C                   WRITE     HEADP1
     C                   ENDIF
      *
     C                   WRITE     TRAILP1
      *
     C                   ENDSR
      *****************************************************************
      /TITLE SR/SRAUDT
      *****************************************************************
      *                                                               *
      *  SRAUDT - Subroutine to output audit report and end program.  *
      *                                                               *
      *****************************************************************
     C     SRAUDT        BEGSR
      *
      ** Output report header and file controls.
      *
     C                   WRITE     HEADAU
     C                   WRITE     CONTROL
      *
      ** If there is a DB error, output the DB error information.
      *
     C     *INU7         IFEQ      *ON
     C                   WRITE     DBERROR
     C                   ELSE
      *
      ** Or, if no records read, output "No Details" message.
      *
     C     RCOUNT        IFEQ      0
     C                   WRITE     NODTLS
     C                   ENDIF
      *
     C                   ENDIF
      *
      ** End program and return.
      *
     C                   SETON                                        LR
     C                   RETURN
      *
     C                   ENDSR
      *****************************************************************
      /TITLE SR/*INZSR
      *****************************************************************
      *                                                               *
      *  *INZSR - Subroutine to do program initialization.            *
      *                                                               *
      *****************************************************************
     C     *INZSR        BEGSR
      *
      ** Set up copyright parameter
      *
     C                   MOVEA     CPY@          CPY2@            80
      *
     C     *ENTRY        PLIST
     C                   PARM                    P0MODE            1
      *
      ** Initialise LDA field.
      *
     C                   MOVEL     PGM           DBPGM
      *
      ** Bank ICD
      *
     C                   CALL      'AOBANKR0'
     C                   PARM      '*MSG   '     WRTCD             7
     C                   PARM      '*FIRST '     WOPTN             7
     C     SDBANK        PARM      SDBANK        DSFDY
      *
     C     WRTCD         IFNE      *BLANKS
     C                   MOVE      'SDBANKPD'    DBFILE
     C                   Z-ADD     001           DBASE
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      ** Check System Date Format, DDMMYY (*IN98 off)
      **                           MMDDYY (*IN98 on).
      *
     C     BJDFIN        IFEQ      'M'
     C                   SETON                                        98
     C                   ENDIF
      *
      ** Access RUNDAT for multibranching indicator
      *
     C     *DTAARA       DEFINE                  RUNDAT
     C     *DTAARA       DEFINE                  LDA
     C                   IN        RUNDAT
      *
     C     AGMBIN        IFEQ      'M'
     C                   SETON                                        37
     C                   ENDIF
      *
      ** Report Work fields.
      *
     C                   Z-ADD     0             RQDLN1            3 0
     C                   Z-ADD     0             DIFLN1            3 0
      *
     C                   MOVE      'N'           WOPNP1            1
      *
     C                   ENDSR
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
     C     *PSSR         BEGSR
      *
      ** Check to see that *PSSR has not already been called.
      *
     C     WRUN          IFEQ      *BLANK
     C                   MOVE      'Y'           WRUN              1
     C                   DUMP
     C                   EXSR      SRAUDT
     C                   ENDIF
      *
      ** If *PSSR already run, then just end the program here.
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
      *****************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2012
