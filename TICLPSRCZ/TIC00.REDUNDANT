/*********************************************************************/
/*S*D****CLPBASE******************************************************/
/*********************************************************************/
/*                                                                   */
/*       Midas - Trade Innovation Module                             */
/*                                                                   */
/*       TIC00   - Midas TI Module Setup                             */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2001           */
/*                                                                   */
/*       Last Amend No. CUP004  *REDUNDANT Date 13Oct10              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. BUG4050            Date 27Aug04              */
/* Midas Release 4 --------------- Base -----------------------------*/
/* Midas DBA 3.00 ---------------- Base -----------------------------*/
/*                      CTI001  *CREATE    Date 19SEP97              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CUP004 - Replacement for SCBEGIN.                          */
/*       BUG4050 - Reuse positions 4-10 of TIDTA, 'N's not required. */
/*       CTI001 - Midas / Trade Innovation Interface Phase 1         */
/*                                                                   */
/*********************************************************************/
             PGM
 
/*/COPY WNCPYSRC,TIC00INT                                          */
 
             DCL        VAR(&JOBTYPE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Misys International Banking Systems Ltd. +
                          2001')
             DCL        VAR(&TILIB1) TYPE(*CHAR) LEN(10) +
                          VALUE('XXTILIB   ')
             DCL        VAR(&TILIB2) TYPE(*CHAR) LEN(10) +
                          VALUE('MBASETI   ')
             DCL        VAR(&TIDTA) TYPE(*CHAR) LEN(32)
             DCL        VAR(&NOREPLY) TYPE(*CHAR) LEN(1)
             DCL        VAR(&RTCD) TYPE(*CHAR) LEN(7) VALUE(' ')
             DCL        VAR(&FMT) TYPE(*CHAR) LEN(200)
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)
             DCL        VAR(&PREFIX) TYPE(*CHAR) LEN(2)
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             RTVJOBA    TYPE(&JOBTYPE)
             OVRMSGF    MSGF(TIUSRMSG) TOMSGF(GBTIUSRMSG)
 
/*/COPY WNCPYSRC,TIC00MPS                                          */
 
/*  Access SDMMODPD for whether TI module is on             */
 
             CALL       PGM(AOMMODR0) PARM(&RTCD '*FIRST' &FMT)
 
/*  Database error handling  */
 
             IF         COND(&RTCD *NE '       ') THEN(DO)
                SNDPGMMSG  MSG('Database error on Midas modules +
                             file') TOMSGQ(MOPERQ MRUNQ)
 
/* Call SCC0010 to inform user that an error has occurred */
 
                RTVMSG     MSGID(TIM0007) MSGF(TIUSRMSG) +
                             MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (251 50)) VALUE(&MESSAGE)
                RTVMSG     MSGID(TIM0008) MSGF(TIUSRMSG) +
                             MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (301 50)) VALUE(&MESSAGE)
                CALL       PGM(SCC0010) PARM('TIC00' 'ENTER' ' ')
                GOTO       CMDLBL(ABNOR)
 
             ENDDO
 
/*  TI must be switched on before TI File Setup can be run  */
 
             IF         COND(%SST(&FMT 103 1) *NE 'Y') THEN(DO)
 
                SNDPGMMSG  MSG('Switch on TI via Mode 1 Modules +
                          Maintainance option before running +
                          Dealing File Setup program') +
                          TOMSGQ(MOPERQ MRUNQ)
 
/*  Call SCC0010 to inform user that program cannot be run now */
 
                RTVMSG     MSGID(TIM0009) MSGF(TIUSRMSG) +
                             MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (251 50)) VALUE(&MESSAGE)
                RTVMSG     MSGID(TIM0010) MSGF(TIUSRMSG) +
                             MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (351 50)) VALUE(&MESSAGE)
                CALL       PGM(SCC0010) PARM('TIC00' 'ENTER' +
                             &NOREPLY)
                GOTO       CMDLBL(END)
 
             ENDDO
 
             CHGJOB     SWS(XXXXXX00)
             SNDPGMMSG  MSG('Initial TI file setup')  TOMSGQ(MOPERQ)
 
/* Call TI00 */
 
             CALL       PGM(TI00)
 
/* Database error processing */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                             TOPGMQ(*EXT) TOMSGQ(MOPERQ)
                RTVMSG     MSGID(TIM0014) MSGF(TIUSRMSG) +
                             MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (251 50)) VALUE(&MESSAGE)
                CALL       PGM(SCC0010) PARM('TIC00' 'ENTER' +
                               &NOREPLY)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
/* Initialise data areas */
 
             RTVDTAARA DTAARA(SDSTAT (6 2)) RTNVAR(&PREFIX)
             CHGVAR     VAR(%SST(&TILIB1 1 2)) VALUE(&PREFIX)
/**********  CHGVAR     VAR(&TIDTA) VALUE('NNNNNNNNNN' *CAT &TILIB1 +
                          *CAT &TILIB2 *CAT 'YY')                            **********/ /*BUG4050*/
             CHGVAR     VAR(&TIDTA) VALUE('   0000000' *CAT &TILIB1 +
                          *CAT &TILIB2 *CAT 'YY')                                        /*BUG4050*/
             CHGDTAARA  DTAARA(TIDTA) VALUE(&TIDTA)
             CHGDTAARA  DTAARA(TI0340) VALUE('START')
             CHGDTAARA  DTAARA(TI0350) VALUE('START')
 
/*/COPY WNCPYSRC,TIC00MPE                                          */
 
             SNDPGMMSG  MSG('TI files set up') TOMSGQ(MRUNQ)
             RTVMSG     MSGID(TIM0015) MSGF(TIUSRMSG) MSG(&MESSAGE)
             CHGDTAARA  DTAARA(MIDASMSG (251 50)) VALUE(&MESSAGE)
             CALL       PGM(SCC0010) PARM('TIC00' 'ENTER' &NOREPLY)
             GOTO       CMDLBL(END)
 
 ABNOR:
/*/COPY WNCPYSRC,TIC00ERR                                          */
 
             CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          TIC00 ended abnormally - see job log') +
                          TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
             RTVMSG     MSGID(TIM0016) MSGF(TIUSRMSG) MSG(&MESSAGE)
             CHGDTAARA  DTAARA(MIDASMSG (251 50)) VALUE(&MESSAGE)
             CALL       PGM(SCC0010) PARM('TIC00' 'ENTER' &NOREPLY)
 
END:         CHGVAR     VAR(&CPYFLD) VALUE('(c) +
                          Misys International Banking Systems Ltd.')
 
/*/COPY WNCPYSRC,TIC00END                                          */
 
             ENDPGM
