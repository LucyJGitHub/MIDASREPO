/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas TI Database take-on')                           */
/*********************************************************************/
/*                                                                   */
/*       Midas - Trade Innovation Module                             */
/*                                                                   */
/*       TIC002001 - Database take-on                                */
/*                                                                   */
/*       (c) Finastra International Limited 2006                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. BUG10642 *CREATE   Date 17Jan06              */
/*                      XNNNNN             Date DDMmmYY              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       BUG10642 - TI Take on script no longer works                */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&MODE) TYPE(*CHAR) LEN(1) VALUE('S')
             DCL        VAR(&RTNCODE) TYPE(*CHAR) LEN(10)
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2006')
 
/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* Start commitment control */
             CALL       PGM(SCCMTCTL) PARM(&MODE &RTNCODE)
 
/* All files apart from SDHOLPD can be updated by an SQL script */
             CALL       PGM(P_SETUPTI)
 
/* Remove triggers from the holidays file  */
             TRIGZONE   FILE(SDHOLPD) ACTION(*RMVTRIG)
 
/* Take a copy  */
             DLTF       FILE(QTEMP/SDHOLPD)
             MONMSG     MSGID(CPF0000)
             CPYF       FROMFILE(SDHOLPD) TOFILE(QTEMP/SDHOLPD) +
                          CRTFILE(*YES)
/* Clear the file */
             CLRPFM     FILE(SDHOLPD)
 
/* Replace the triggers */
             TRIGZONE   FILE(SDHOLPD) ACTION(*ADDTRIG)
 
/* Restore the data (the trigger will now push it to CVPF) */
             CPYF       FROMFILE(QTEMP/SDHOLPD) TOFILE(SDHOLPD) +
                          MBROPT(*ADD)
 
/* Commit the changes */
             COMMIT
 
             GOTO       CMDLBL(END)
 
ABNOR:
 
 /* Roll back and send error message */
             ROLLBACK
             MONMSG     MSGID(CPF0000)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Error +
                          occurred in TIC002001 - see low level +
                          messages') MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000)
 
END:
             ENDCMTCTL
             MONMSG     MSGID(CPF0000)
 
             ENDPGM
