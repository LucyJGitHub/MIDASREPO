/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas TI Re-set statuses for Midas/TI I/face CoB')    */
/*********************************************************************/
/*                                                                   */
/*       Midas/TI Interface                                          */
/*                                                                   */
/*       TIC0510 - Re-set Midas/TI Interface CoB statuses            */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2001           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/* Midas Release 4 --------------- Base -----------------------------*/
/* Midas DBA 3.05 ---------------------------------------------------*/
/*       Last Amend No. CTI003  *CREATE    Date 30Aug00              */
/*       Prev Amend No. Xnnnnn             Date ddmmmyy              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CTI003 - Midas/TI Interface CoB                             */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&STRCOB &ENDCOB)
 
             DCL        VAR(&STRCOB) TYPE(*CHAR) LEN(4)
             DCL        VAR(&ENDCOB) TYPE(*CHAR) LEN(4)
 
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)
             DCL        VAR(&RECURSIVE) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&KEYWD) TYPE(*CHAR) LEN(10) VALUE('*MODE')
             DCL        VAR(&MESGS) TYPE(*CHAR) LEN(10) +
                          VALUE('RELEASE')
             DCL        VAR(&SEND) TYPE(*CHAR) LEN(80)
             DCL        VAR(&LENGTH) TYPE(*DEC) LEN(5 0) VALUE(20)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2001')
 
/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* Create data area /MIDASMSG in QTEMP */
             DLTDTAARA  DTAARA(QTEMP/MIDASMSG)
             MONMSG     MSGID(CPF0000)
             CRTDTAARA  DTAARA(QTEMP/MIDASMSG) TYPE(*CHAR) LEN(800) +
                          VALUE(' ')
 
/* If re-setting to the beginning of CoB */
             IF         COND(&STRCOB *EQ '*YES') THEN(DO)
/* Re-set data area */
                CHGDTAARA  DTAARA(TIDTA (51 20)) +
                          VALUE('PPPPPPPPPPPPPPPPPPPP')
                CHGDTAARA  DTAARA(TIDTA (101 26)) VALUE(' ')
/* Call TI CSCS program to send RELEASE message */
                CHGVAR     VAR(&SEND) VALUE(&KEYWD *CAT &MESGS)
                CALL       PGM(KAPEQEN) PARM(&SEND &LENGTH)
/* Attempt to re-start background jobs */
                CALL       PGM(TIC0050) PARM('START')
             ENDDO
 
/* If re-setting to the end of CoB */
             IF         COND(&ENDCOB *EQ '*YES') THEN(DO)
                CHGDTAARA  DTAARA(TIDTA (51 20)) +
                          VALUE('CCCCCCCCCCCCCCCCCCCC')
             ENDDO
 
             GOTO       CMDLBL(END)
 
 ABNOR:
             IF         COND(&RECURSIVE *EQ 'N') THEN(DO)
                CHGVAR     VAR(&RECURSIVE) VALUE('Y')
                RTVMSG     MSGID(TIM0035) MSGF(TIUSRMSG) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
                CALL       PGM(SCC0010) PARM('TIC0510' 'ENTER' ' ')
             ENDDO
 
 END:
             ENDPGM
