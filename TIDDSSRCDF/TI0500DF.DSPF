      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas TI Cob Monitor Display File')
      *****************************************************************
      *                                                               *
      *  Midas - Trade Innovation Interface Module                    *
      *                                                               *
      *  TI0500DF - TI COB Monitor Screen                             *
      *                                                               *
      *  Called by TI0500                                             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CTI003  *CREATE    Date 07Aug00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CTI003 - Created for Midas/TI Interface to run the early     *
      *           stages of the TI CoB early.  In this way, all       *
      *           postings are available for review before COB and    *
      *           the processing time of Midas COB is reduced.        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
     A          R TI0500B                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      CF03(03)
     A                                      CF12(12)
     A            USERID        10A  O  1  2TEXT('User ID')
     A                                  1 13'TI0500'
     A                                  1 72DATE EDTCDE(Y)
     A            JOBNM         10A  O  2  2TEXT('System Name')
     A
     A                                  2 29'Midas/TI Interface +
     A                                      CoB'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 72TIME
      *
     A                                 10 10'Start the Midas/TI CoB...:'
     A                                 10 40'(Y/N)'
     A            ANSWER         1   B 10 37
     A                                      DSPATR(UL)
     A                                      DSPATR(RI)
     A            MESSAGE       30   O 13 10
     A                                      DSPATR(HI)
      *
     A                                 22  5'F3=Exit    '
     A                                 22 30'F12=Cancel'
     A          R TI0500D                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      CF03(03)
     A                                      CF05(05)
     A  15                                  CF07(07)
     A  15                                  CF08(08)
     A N15                                  CF10(10)
     A  18                                  CF12(12)
     A            USERID        10A  O  1  2TEXT('User ID')
     A                                  1 13'TI0500'
     A                                  1 72DATE EDTCDE(Y)
     A            JOBNM         10A  O  2  2TEXT('System Name')
     A
     A                                  2 29'Midas/TI Interface +
     A                                      CoB'
     A                                      DSPATR(HI)
     A                                  2 72TIME
      *
     A                                  4  5'Sub-component'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 35'Status'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
      ** Display Status of each Cob sub-component
      *
     A                                  5  5'End of Midas background jobs'
     A            STATUS1       10A  O  5 35
      *
     A                                  6  5'End of jobs successful'
     A            STATUS2       10A  O  6 35
      *
     A                                  7  5'Send CLOSING message'
     A            STATUS3       10A  O  7 35
      *
     A                                  8  5'Receive CLOSINGOK message'
     A            STATUS4       10A  O  8 35
      *
     A                                  9  5'Send STREONL message'
     A            STATUS5       10A  O  9 35
      *
     A                                 10  5'Receive STREONLOK message'
     A            STATUS6       10A  O 10 35
      *
     A                                 11  5'Send SUSPEND message'
     A            STATUS7       10A  O 11 35
      *
     A                                 12  5'Receive SUSPENDOK message'
     A            STATUS8       10A  O 12 35
      *
     A                                 13  5'End *PROP job'
     A            STATUS9       10A  O 13 35
      *
     A                                 14  5'Postings Processing'
     A            STATUS10      10A  O 14 35
      *
     A                                 15  5'Send STREOD message'
     A            STATUS11      10A  O 15 35
      *
      ** Error Reporting fields
      *
     A            FAILPHS       74A  O 16  5TEXT('Failed Sub-component')
     A                                      DSPATR(HI)
     A            FAILMSG       74A  O 17  5TEXT('Failed Message')
     A                                      DSPATR(HI)
      *
      ** Job Status Fields
      *
     A                                 18  5'Job Name: '
     A            JOBNAME       32A  O 18 18TEXT('Job Name')
     A                                 19  5'Job Status: '
     A            JOBSTS        60A  O 19 18TEXT('Job Status')
      *
      ** Function keys for this format
      *
     A                                 21  3'F3=Exit'
     A  18                             21 20'F12=Cancel'
     A                                 21 37'F5=Refresh'
     A  15                             21 54'F7=Re-run Failed Sub-Cmp'
     A  15                             22  3'F8=Bypass Current Sub-Cmp'
     A N15                             22 49'F10=End Background Job'
