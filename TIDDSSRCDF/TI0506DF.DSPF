/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas TI Display Swift Messages')
      *****************************************************************
      *                                                               *
      *  Midas - TI Module                                            *
      *                                                               *
      *  TI0506DF - TI Display Swift Messages                         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CTI003  *CREATE    Date 12Oct00               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CTI003 - Midas/TI Interface CoB changes                      *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      INDARA
      *
     A          R TI0506B1                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            D#SEL          1A  B  6  5DSPATR(HI)
     A                                      VALUES('R')
     A  33                                  DSPATR(ND PR)
     A            DLSTS          1A  O  6 12
     A            DTRNO         16A  O  6 16
     A            DMIR          28A  O  6 35
      *
      * -------------------------------------------------------------------*
      * * Subfile control - used to define but not control subfile.        *
      * -------------------------------------------------------------------*
      *
     A          R TI0506C1                  SFLCTL(TI0506B1)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      TEXT('Subfile definition')
     A                                      CF03
     A                                      CF05
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND(*MORE)
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      BLINK
      *
     A            D#RRN          4S 0H      SFLRCDNBR
      *
      * -------------------------------------------------------------------*
      * * Actual Subfile control  - used to enable full control of subfile *
      * * by program.                                                      *
      * -------------------------------------------------------------------*
      *
     A          R TI0506C2
     A                                      TEXT('Actual Subfile control')
     A                                      VLDCMDKEY(80)
     A                                      CF03
     A                                      CF05
     A                                      ROLLUP(81)
     A                                      ROLLDOWN(82)
     A                                      OVERLAY
     A                                      BLINK
      *
     A            DDUSER        10A  O  1  2
     A                                  1 13'TI0506'
     A                                  1 23'Incoming TI Message Status Enquiry'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73
     A            DDWID         10A  O  2  2TEXT('Workstation ID')
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Select'
     A                                  4  9'Status'
     A                                  4 16'Transaction No.'
     A                                  4 35'SWIFT II Message Input Reference'
     A            D#TRNO        16A  B  5 16
     A                                      DSPATR(HI PC)
      *
      * -------------------------------------------------------------------*
      * * No records screen                                                *
      * -------------------------------------------------------------------*
      *
     A          R TI0506N1
     A                                      TEXT('No records screen')
     A                                      CF03
     A                                      CF05
     A                                      OVERLAY
     A                                      BLINK
      *
     A            DDUSER        10A  O  1  2
     A                                  1 13'TI0506'
     A                                  1 23'Incoming TI Message Status Enquiry'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73
     A            DDWID         10A  O  2  2TEXT('Workstation ID')
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Select'
     A                                  4  9'Status'
     A                                  4 16'Transaction No.'
     A                                  4 35'SWIFT II Message Input Reference'
      *
     A                                 10 18'**There are no records to display*-
     A                                      *'
     A                                      DSPATR(HI)
     A                                 24  2'F3=Exit   F5=Refresh'
      *
      * -------------------------------------------------------------------*
      * * Browse screen footer                                             *
      * -------------------------------------------------------------------*
      *
     A          R TI0506F1                  TEXT('Browse screen footer')
     A                                      BLINK
     A                                      OVERLAY
     A                                      CF03
     A                                      CF05
     A  95                             22  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                      DSPATR(HI)
     A  34                             23  2'R=Reset Status to Unprocessed'
     A                                      DSPATR(HI)
     A                                 24  2'F3=Exit   F5=Refresh'
