      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas TI Update MX files for Trade Finance')           *
      *****************************************************************
      *                                                               *
      *  Midas - Trade Innovation Module                              *
      *                                                               *
      *  TI300000DF - Midas TI Update MX files for Trade Finance      *
      *                                                               *
      *  (c) Finastra International Limited 2014                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CTI006  *CREATE    Date 31Mar14               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CTI006 - Trade Finance BF Midas 2.2 Interface                *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A          R TI30DETL
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2014')
     A                                      CF03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            PSUSER        10A  O  1  2TEXT('User Profile')
     A            ZADATE         7A  O  1 73TEXT('Rundate')
     A            PSJOBNAME     10A  O  2  2TEXT('Workstation ID')
     A                                  2 73TIME
     A                                  1 13'TI300000'
     A                                  1 36'Trade Finance'
     A                                  2 34'Update of MX Files'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  3'Message Queue . . . . :'
     A            SMSGQ         48   B 10 27
     A                                      DSPATR(HI)
     A N23N90                               DSPATR(UL)
     A  23                                  DSPATR(RI PC)
     A  90                                  DSPATR(PR)
     A                                      OVRATR
     A                                      OVRDTA
      *
      ** Footer record format
      *
     A          R TI30FOOT                  TEXT('Footer')
     A                                      OVERLAY
     A N28                             23  2'F3=Exit   F5=Refresh'
     A  28                             23  2'ENTER=Confirm   F12=Cancel'
     A  28                                   DSPATR(HI)
      *
      ** Message subfile record format
      *
     A          R TI30MSGR                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMNAM                    SFLPGMQ
      *
      ** Message subfile control record format
      *
     A          R TI30MSGCTL                TEXT('Program messages')
     A                                      SFLCTL(TI30MSGR)
     A                                      OVERLAY SFLINZ SFLPAG(1) SFLSIZ(3)
     A                                      SFLDSPCTL SFLDSP
     A  90
     AON90                                  SFLEND
     A            PGMNAM                    SFLPGMQ
      *
