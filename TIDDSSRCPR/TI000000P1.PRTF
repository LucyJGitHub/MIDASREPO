      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas TI Report Duplicate TI accounts')
      *****************************************************************
      *                                                               *
      *  Midas - TI Module                                            *
      *                                                               *
      *  PRTF/TI000000P1 - Report Duplicate TI accounts               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      *  Last Amend No. BUG27963*RENAMED   Date 30Nov05               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 30Nov05               *
      *                 BUG5340 *CREATE    Date 06Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG27963 - renamed from CN5340P1                             *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  BUG5340 - Report Duplicate TI accounts                       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ** Report Header Record Format   *
      *--------------------------------*
      *
     A          R HEADPY                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
      *
     A                                      SKIPB(3)
      * Start BUG27963
     A***********                          6'REFERENCE BUG5340P1'                           BUG27963
     A                                     6'Reference TI000000P1'
      * End   BUG27963
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    52'Duplicate TI Accounts Report'
     A                                      SPACEA(1)
     A                                    52'----------------------------'
     A                                      SPACEA(2)
      *
      ** Sub-Heading:
      ** ------------
      *
     A                                     6'Branch'
     A                                    17'Customer'
     A                                    29'CCY'
     A                                    40'Account'
     A                                    51'Account'
     A                                    64'Retail Account'
     A                                    89'TI Account'
     A                                   106'TI Account'
     A                                      SPACEA(1)
     A                                    40'Code'
     A                                    51'Sequence'
     A                                    64'Number'
     A                                    89'Indicator'
     A                                   106'Number / Suffix'
      *
      ** Detail Sub-Headings:
      ** --------------------
      *
     A                                      SPACEA(2)
      *
      *-------------------------*
      ** Detail Record Format   *
      *-------------------------*
      *
     A          R DETAIL
      *
     A  50        PRBRCA    R              8REFFLD(BRCA)
     A                                      SPACEB(1)
      * Begin /CSD027/
     A**50******  PRCNUM    R             17REFFLD(CNUM)                                      CSD027
     A  50        PRCNUM         6A       17TEXT('Customer number')
      * End /CSD027/
     A  50        PRCCY     R             29REFFLD(CCY)
     A  50        PRACOD        10A       39
     A  50        PRACSQ    R             54REFFLD(ACSQ)
     A  50        PRACNO    R             64REFFLD(ACNO)
     A  50        PRTIAC         1A       93
     A  50        PRTIAN         6A      106
     A  50                               113'/'
     A  50        PRTIAS         3A      115
     A                                      SPACEA(1)
      *
     A            ZABRCA    R              8REFFLD(BRCA)
      * Begin /CSD027/
     A**********  ZACNUM    R             17REFFLD(CNUM)                                      CSD027
     A            ZACNUM         6A       17TEXT('Customer number')
      * End /CSD027/
     A            ZACCY     R             29REFFLD(CCY)
     A            ZAACOD        10A       39
     A            ZAACSQ    R             54REFFLD(ACSQ)
     A            ZAACNO    R             64REFFLD(ACNO)
     A            ZATIAC         1A       93
     A            ZATIAN         6A      106
     A                                   113'/'
     A            ZATIAS         3A      115
     A                                      SPACEA(1)
      *
      *-------------------*
      ** Report Trailer   *
      *-------------------*
      *
     A          R TRAILPY
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
      *---------------*
      ** No Details   *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ** Database Error   *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
