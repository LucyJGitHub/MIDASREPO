      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas TI Journal entry held batches report')
      *****************************************************************
      *                                                               *
      *  Midas - Trade Inovation (TI) Module                          *
      *                                                               *
      *  PRTF/TI0320P1 - TI Journal Entry Held Batches Report         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU029             Date 04Sep98               *
      *                 CTI001   *CREATE   Date 28JUL97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CEU029 - EMU Midas/Trade Innovation Interface Enhancements:  *
      *           print Original Currency and Amount for TI Postings. *
      *  CTI001 - Midas / Trade Innovation Interface Phase 1          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE TI0320P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'TI JOURNAL ENTRY HELD BAT+
     A                                      CHES REPORT'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    46'-------------------------+
     A                                      -----------'
     A                                      SPACEA(3)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                     4'Branch..................:'
     A            A8BRCD    R             30REFFLD(ZZ003)
     A            A8BRNM    R             35REFFLD(ZZ030)
     A                                    90'Description.........: Trade Inn+
     A                                      ovation'
     A                                      SPACEA(001)
      *
     A                                     4'Batch...................:'
     A            BRBTNB    R             30REFFLD(ZZ003)
     A                                    90'Total Input.........:'
     A            BRHICC    R             +1REFFLD(AMT)
     A                                      EDTCDE(B)
     A                                      SPACEA(001)
      *
     A                                     4'Department..............:'
     A            BRDPCD    R             30REFFLD(ZZ003)
     A            AEDPNM    R             35REFFLD(ZZ030)
     A                                      SPACEA(001)
      *
     A                                     4'Multiple Branch Batch...:'
     A            BRMBRB    R             +1REFFLD(ZZ001)
     A                                      SPACEA(001)
     A                                     4'Terminal................:'
     A            WSID      R             +1REFFLD(ZZ010)
     A                                      SPACEA(003)
      *
      *                                              Begin /CEU029/
     A**********                         111'Value'
     A**********                         120'Profit'
     A**********                            SPACEA(1)
     A**********
     A**********                           3'Item'
     A**********                          +8'Account Number'
     A**********                          +7'Branch'
     A**********                          55'Amount'
     A**********                          87'Narrative'
     A**********                         112'Date'
     A**********                         120'Centre'
     A**********                            SPACEA(1)
     A                                    85'Value'
     A                                    93'Profit'
     A  01                               100'Original'
     A  01                               113'Original'
     A
     A                                     3'Item'
     A                                      SPACEB(1)
     A                                    +2'Account Number'
     A                                    +2'Branch'
     A                                    38'Amount'
     A                                    63'Narrative'
     A                                    85'Date'
     A                                    93'Centre'
     A  01                               100'Currency'
     A  01                               114'Amount'
      *                                              End   /CEU029/
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *                                              Begin /CEU029/
     A**********  BTBINB    R              3REFFLD(AMDD)
     A**********  BTCUST    R             +8REFFLD(ZZ006)
     A**********  BTCYCD    R             +0REFFLD(ZZ003)
     A**********  BTACCD    R             +0REFFLD(ZZ004)
     A**********  BTACSN    R             +0REFFLD(ZZ002)
     A**********  BTIBCA    R             +8REFFLD(ZZ003)
     A**********  BTPTAM    R             +8REFFLD(NOAC)
     A**********                            EDTCDE(2)
     A**********  BTDCIN    R             +1REFFLD(ZZ001)
     A**********  BTNRDC    R             +8REFFLD(ZZ030)
     A**********  VALDAT    R             +4REFFLD(ZZ007)
     A**********  BTPRCN    R             +4REFFLD(ZZ004)
     A**********                            SPACEA(1)
     A                                      SPACEB(1)
     A            BTBINB    R              3REFFLD(AMDD)
     A            BTCUST    R             +2REFFLD(ZZ006)
     A            BTCYCD    R             +0REFFLD(ZZ003)
     A**Start of change CGL029
     A**********  BTACCD    R             +0REFFLD(ZZ004)                                     CGL029
     A            BTACCD        10A       +0
     A**End of change CGL029
     A            BTACSN    R             +0REFFLD(ZZ002)
     A            BTIBCA    R             +2REFFLD(ZZ003)
     A            BTPTAM    R             +2REFFLD(NOAC)
     A                                      EDTCDE(2)
     A            BTDCIN    R             +1REFFLD(ZZ001)
     A            BTNRDC    R             +2REFFLD(ZZ030)
     A            VALDAT    R             +2REFFLD(ZZ007)
     A            BTPRCN    R             +3REFFLD(ZZ004)
     A  01        BTOCCY    R             +4REFFLD(ZZ003)
     A  01        BTOAMT    R             +3REFFLD(NOOS)
     A                                      EDTCDE(2)
      *                                              End   /CEU029/
      *
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
      *
