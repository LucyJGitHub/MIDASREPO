      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas TI Journal entry gen. of TI postings report')
      *****************************************************************
      *                                                               *
      *  Midas - Trade Innovation Interface Module                    *
      *                                                               *
      *  PRTF/TI0341P1 TI I/C Journal Entry Generation of TI Postings *
      *                which cause the batch to be held.              *
      *                                                               *
      *  Function:  This printer file is created for any transaction  *
      *             posting to an account which should be rejected    *
      *             by Journal Entry (blocked, referred, closed etc.) *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG4860  *CREATE   Date 03Dec04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG4860 - Report on any invalid postings during the Journal  *
      *            Entry Validation process.                          *
      *                                                               *
      *****************************************************************
     A                                      REF(GLJENPPD)
      *
      ** Report Header
     A          R HEADP1
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2004')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE TI0341P1'
     A            BJURPT        53A       37
     A                                   101'DATE'
     A            BJMRDT         7A      106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      SPACEA(1)
      *
     A                                    31'Midas / TI Interface'
     A                                    51' Invalid Posting and'
     A                                    71' Held Batch Detail R'
     A                                    91'eport'
     A                                      SPACEA(1)
     A                                    31'--------------------'
     A                                    51'--------------------'
     A                                    71'--------------------'
     A                                    91'-----'
     A                                      SPACEA(3)
      *
      ** Posting Error
      *
     A          R PSTERROR
     A                                    11'TI Batch number:'
     A                                      SPACEB(2)
     A                                    33'Item number:'
     A            BTBTNB    R             28
     A            BTBINB    R             46
      *
     A                                    21'Account Details'
     A                                      SPACEB(2)
      *
     A                                    11'Branch  '
     A                                      SPACEB(1)
     A                                    21'Customer'
     A                                    31'Currency'
     A                                    41'Account Code  '
     A                                    56'Sequence'
     A                                    67'Retail Account'
     A                                    86'Timestamp'
      *
     A            BTIBCA    R             12
     A                                      SPACEB(1)
     A            BTCUST    R             22
     A            BTCYCD    R             32
     A            BTACCD    R             42
     A            BTACSN    R             57
     A            BTRACN    R             68
     A            BTTMST    R             87
      *
     A                                    11'Reason for posting holding batch'
     A                                      SPACEB(2)
      *
     A            DESC          60        11
     A                                      SPACEB(1)
      *
     A                                    11'*** CHECK FOR OTHER REPORTS FOR TH'
     A                                      SPACEB(2)
     A                                    45'IS BATCH - ONE REPORT IS PRODUCED '
     A                                    79'PER POSTING OR TOTALS IN ERROR ***'
      *
      ** Batch Error
      *
     A          R BCHERROR
     A                                    11'TI Batch number:'
     A                                      SPACEB(2)
     A            BRBTNB         3        28
      *
     A                                    11'Reason for holding Batch'
     A                                      SPACEB(2)
      *
     A                                    11'Total Debits do not equal Credits'
     A                                      SPACEB(2)
     A                                    44' within Currency                 '
      *
     A                                    11'CAUSE - A posting for one of the '
     A                                      SPACEB(1)
     A                                    44'transactions in this batch may ha'
     A                                    77've been rejected by the interface'
      *
     A                                    11'*** CHECK FOR OTHER REPORTS FOR TH'
     A                                      SPACEB(2)
     A                                    45'IS BATCH - ONE REPORT IS PRODUCED '
     A                                    79'PER POSTING OR TOTALS IN ERROR ***'
      *
      ** Report Trailer
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
