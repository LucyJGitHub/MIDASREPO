      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas TI Accrual Suspense A/c entries generated')
      *****************************************************************
      *                                                               *
      *  Midas - Trade Innovation Module                              *
      *                                                               *
      *  TI0343P1 - TI Unbalanced Accruals suspense accounts          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CTI003  *Create    Date 17Apr01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CTI003 - TI accrual suspense accounts                        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEAD                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     2'REFERENCE TI0343P1'
     A            TITL          53        37
     A                                    99'DATE'
     A            AGMRDT    R            105REFFLD(MRDT)
     A                                   115'PAGE'
     A                                   120PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                    99'TIME'
     A                                   105TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                    23'TI Accrual Suspense Acts.'
     A                                      SPACEA(1)
     A                                    23'-------------------------'
     A                                      SPACEA(2)
      *-------------------------*
      ***  Sub header           *
      *-------------------------*
      *
     A          R SUBHED
     A                                     1'Batch'
     A                                    12'Customer'
     A                                    23'Currency'
     A                                    34'Account'
     A                                    45'Account'
     A                                    56'Branch'
     A                                    67'Debit/'
     A                                    79'Total Amount'
     A                                      SPACEA(1)
     A                                     1'Number'
     A                                    12'Number'
     A                                    34'Code'
     A                                    45'Sequence'
     A                                    56'Code'
     A                                    67'Credit'
     A                                      SPACEA(1)
     A                                     1'------'
     A                                    12'--------'
     A                                    23'--------'
     A                                    34'-------'
     A                                    45'--------'
     A                                    56'------'
     A                                    67'------'
     A                                    79'------------'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Display files        *
      *-------------------------*
     A          R DETAIL
     A            BTCYCD         3        23
                  BTBTNB         3         1
                  BTCUST         6        12
     A**Start of change CGL029
      **********  BTACCD         4        34                                                  CGL029
                  BTACCD        10        34
     A**End of change CGL029
                  BTACSN         2        45
                  BTIBCA         3        56
                  BTDCIN         1        67
                  BTPTAM        13  0     78EDTCDE(Z)
     A                                      SPACEA(1)
      *
      *----------------*
      *** Blank Line   *
      *----------------*
      *
     A          R BLANK
     A                                     1' '
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAIL
     A                                      SPACEA(3)
     A                                    51'***  END OF REPORT  ***'
