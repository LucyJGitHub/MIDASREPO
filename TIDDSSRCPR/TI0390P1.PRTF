      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas TI Accounts Opened Automatically')
      *****************************************************************
      *                                                               *
      *  Midas - Trade Innovation Module                              *
      *                                                               *
      *  PRTF/TI0390P1 - TI Accounts Opened Automatically             *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 30Nov05               *
      *                 CTI005  *CREATE    Date 12Apr04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CTI005 - Midas Plus-TI Integration Enhancements              *
      *           Automatic Account Opening                           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
      *
     A                                      SKIPB(3)
     A                                     2'REFERENCE TI0390P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   114'PAGE'
     A                                   122PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A  37                                 2'BRANCH -'
     A  37        A8BRCD    R             11REFFLD(BRCA)
     A  37        A8BRNM    R             15REFFLD(BRNM)
     A  37                                  SPACEA(1)
      *
     A  15                                38'TI ACCOUNTS OPENED AUTOMATICALLY +
     A                                       TODAY - AUDIT '
     A  15                                85TIME
     A                                      EDTWRD('  :  :  ')
     A  15                                  SPACEA(1)
     A  15                                38'---------------------------------+
     A                                       ----------------------'
     A                                      SPACEA(2)
     A N15                                41'TI ACCOUNTS OPENED AUTOMATICALLY +
     A                                       TODAY - FULL LIST'
     A N15                                  SPACEA(1)
     A N15                                41'---------------------------------+
     A                                       -----------------'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A                                     2'Account ..................:'
     A            BRCA           3        30
     A                                    33'-'
      * Begin /CSD027/
      **********  CNUM           6  0     34                                                  CSD027
                  CNUM           6A       34
      * End /CSD027/
     A                                    40'-'
     A            CCY            3        41
     A                                    44'-'
                  ACOD          10  0     45
     A                                    55'-'
                  ACSQ           2  0     56
     A                                    62'TI Account Number/Suffix .:'
                  A5TIAN         6        90
     A                                    96'-'
                  A5TIAS         3        97
     A                                      SPACEA(1)
     A                                     2'Account Name .............:'
     A            ANAM          20        30
     A                                    62'Account Type/Sub Type ....:'
     A            ATYP           1        90
     A            STYP           1        92
     A                                      SPACEA(1)
     A                                     2'Profit Centre ............:'
     A            PRFC           4        30
     A                                    62'TI Account Key ...........:'
     A            A5ACKY        34        90
     A                                      SPACEA(1)
     A                                     2'Internal Flag ............:'
     A            A5INTL         1        30
     A                                    62'TI Branch Number .........:'
     A            A5TIAB         4        90
     A                                      SPACEA(1)
     A                                     2'Timestamp ................:'
     A            TMST            Z       30
     A                                    62'Last Change Type .........:'
     A            CHTP           1        90
     A                                      SPACEA(2)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
