      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Printer File (230076)')
      *****************************************************************
      *                                                               *
      *  Midas - Trade Innovation Module                              *
      *                                                               *
      *  PRTF/TI23007P1 - Accounts with duplicate TI number and suffix*
      *                  for customer in a particular branch.         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027A            Date 16May06               *
      *  Prev Amend No. BUG6597 *Create    Date 10May05               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSD027A - Change Customer number to alpha; post-103 build.   *
      *  BUG6597 - Duplicate TI number and suffix for branch customer.*
      *            Applied fix 230076.                                *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R HEADP
      *
     A                                      SKIPB(1)
     A                                     1'REFERENCE TI23007P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            RUNA           7A      106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A N10                                 6'AUDIT'
     A  10                                 6'UPDATE'
     A                                   101'TIME'
     A                                   105TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
     A                                    40'ACCOUNTS WITH DUPLICATE +
     A                                       TI NUMBER-SUFFIX FOR +
     A                                       BRANCH'
     A                                      SPACEA(1)
     A                                    40'-----------------------+
     A                                       --------------------+
     A                                       ------'
     A                                      SPACEA(3)
      *
     A                                    40'Counter'
     A                                       HIGHLIGHT
     A                                    55'Account number'
     A                                       HIGHLIGHT
     A                                    81'TI Account'
     A                                       HIGHLIGHT
     A                                    94'New TI Suffix'
     A                                       HIGHLIGHT
     A                                      SPACEA(001)
      *
     A          R DETAIL
     A                                      SPACEA(1)
     A  30        SCTR           6S 0O    40EDTCDE(3)
     A            SBRCA          3A  O    50
     A                                    53'-'
      * Start /CSD027A/
     A**********  SCNUM          6S 0O    54                                                 CSD027A
     A            SCNUM          6A  O    54
      * End /CSD027A/
     A                                    60'-'
     A            SCCY           3A  O    61
     A                                    64'-'
     A            SACOD         10S 0O    65
     A                                    75'-'
     A            SACSQ          2S 0O    76
     A            STIAN          6A  O    81
     A                                    87'-'
     A            STIAS          3A  O    88
     A            SNTIAS         3A  O    94
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(003)
      *
     A          R SPACE
     A                                      SPACEB(001)
      *
     A          R TOTAL
     A                                      SPACEB(2)
     A                                    30'Number of Accounts with +
     A                                      duplicate TI number-+
     A                                      suffix found:'
     A                                      SPACEA(1)
     A            SCTR           6  0     30SPACEA(2)
     A                                      EDTCDE(3)
     A                                    54'*** END OF REPORT ***'
      *
     A          R BLANK
     A                                    10' '
     A                                      SPACEA(005)
