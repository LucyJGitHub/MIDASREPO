      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas TM DDS for FX Mnthly Summary (was DL3110DD)')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Treasury Management Module
     A*                                                               *
     A*  DSPF/TM3110DD - FOREIGN EXCHANGE BOOK - MONTHLY SUMMARY      *
     A*                  (FORMERLY DL3110DD)
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. E39162             DATE 18JUN92               *
     A*                 S01117             DATE 06NOV90               *
     A*                 S01253             DATE 17APR90               *
     A*                 S01199             DATE 27FEB90               *
     A*                 S01187             DATE 17JAN89               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  MD046248 - Finastra Rebranding                               *
     A*  E39162  -  CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                *
     A*  S01117  -  ALL PROGRAMS/FILES SPECIFIC TO TREASURY MGMT.     *  S01117
     A*             RENAMED TO HAVE 'TM' PREFIX                       *  S01117
     A*             COPYRIGHT STATEMENT CHANGED                       *  S01117
     A*                                                               *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*  S01199  -  HELP SYSTEM.                                  *
     A*  S01187  -  RELEASE 1 TEST FIX                                *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A* INDICATORS TO BE RECEIVED FROM THE SCREEN - COMMANDS          *
     A*                                                               *
     A*     01   -  EXIT PROGRAM                                      *
     A*     05   -  REFRESH SCREEN                                    *
     A*     10   -  HELP KEY HAS BEEN PRESSED                         *
     A*     15   -  PROTECT SELECT FIELD - NO INPUT ALLOWED           *
     A*     25   -  ROLLUP REQUESTED                                  *
     A*     26   -  ROLLDOWN REQUESTED                                *
     A*     27   -  INHIBIT ROLL/REFRESH/SELECT KEYS & TEXT/ENQ TITLES*
     A*                                                               *
     A* INDICATORS TO CONTROL THE ENQUIRY SUBFILE                     *
     A*                                                               *
     A*     30   -  ACTIVATE SUBFILE END                              *
     A*     35   -  ACTIVATE SUBFILE DISPLAY                          *
     A*     36   -  ACTIVATE SUBFILE DISPLAY CONTROL                  *
     A*     (N35N36   -  ACTIVATE SUBFILE CLEAR)                      *
     A*                                                               *
     A* INDICATORS TO CONTROL THE MESSAGE SUBFILE                     *
     A*                                                               *
     A*     40   -  ACTIVATE SUBFILE END                              *
     A*     46   -  ACTIVATE SUBFILE DISPLAY CONTROL                  *
     A*                                                               *
     A* INDICATORS TO REPORT ERRORS ON VALIDATION OF PROMPT SCREEN    *
     A*                                                               *
     A*     60   -  ERROR ON CURRENCY SELECTED                        *
     A*     61   -  ERROR ON MONTH SELECTED                           *
     A*     69   -  GENERAL INDICATOR ON VALIDATION OF PROMPT SCREEN  *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A  36                                  INVITE
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A                                      CA03(01 'F3 1 to Exit')
     A                                      CA05(05 'F5 to Refresh')
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A*                                                             /* S01117 */
     A*                                                             /* S01117 */
     A*                                                    /*S01199*/
     A*                                                             /* S01117 */
     A**********R DL3110F0                  TEXT('Title')
     A          R TM3110F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*                                                             /* S01117 */
     A*
     A*                                                             /* S01117 */
     A**********                        1  2'Ref:DL3110'
     A                                  1  2'Ref:TM3110'
     A*                                                             /* S01117 */
     A                                  1 61TIME
     A*
     A                                  1 70'WSID'
     A            @WSID          3   O  1 75TEXT('Workstation Id')
     A*
     A*
     A                                  2  2'FX BOOK - MONTHLY SUMMARY'
     A                                      DSPATR(UL HI)
     A            @SCCY          3A     2 62TEXT('Currency selected')
     A                                  2 66'(000s)'
     A            @STEND         5A     3 67TEXT('start/end/blank')
     A*
     A* Subfile Titles
     A*
     A                                  4  2'SELECT'
     A                                      DSPATR(UL)
     A                                  4 11'MONTH'
     A                                      DSPATR(UL)
     A                                  4 25'IN'
     A                                      DSPATR(UL)
     A                                  4 40'OUT'
     A                                      DSPATR(UL)
     A                                  4 53'FX NET'
     A                                      DSPATR(UL)
     A*
     A*                                                             /* S01117 */
     A**********R DL3110F1                  TEXT('Command Keys Text')
     A          R TM3110F1                  TEXT('Command Keys Text')
     A*                                                             /* S01117 */
     A                                      OVERLAY
     A*
     A                                 23  2'F3 to end, F5 to'
     A**********                       23 23'refresh,'                /*S01187*/
     A                                 23 19'refresh,'
     A**********                       23 32'Rollup/Rolldown for'     /*S01187*/
     A                                 23 28'Rollup/Rolldown for'
     A**********                       23 52'further details.'        /*S01187*/
     A                                 23 48'further details.'
     A                                 24  2'Enter ''1'' for'
     A                                 24 16'detail enquiry'
     A*
     A*
     A*                                                             /* S01117 */
     A**********R DL3110S0                  TEXT('Message Subfile')
     A          R TM3110S0                  TEXT('Message Subfile')
     A*                                                             /* S01117 */
     A*
     A* Message Subfile
     A*
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDMGKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A*
     A*                                                             /* S01117 */
     A**********R DL3110C0                  TEXT('Message Subfile Control')
     A          R TM3110C0                  TEXT('Message Subfile Control')
     A*                                                             /* S01117 */
     A*
     A* Message Subfile Control
     A*
     A*                                                             /* S01117 */
     A**********                            SFLCTL(DL3110S0)
     A                                      SFLCTL(TM3110S0)
     A*                                                             /* S01117 */
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A  46                                  SFLDSPCTL
     A                                      SFLINZ
     A  40                                  SFLEND
     A                                      OVERLAY
     A            DDPGMQ                    SFLPGMQ
     A*
     A*
     A*                                                             /* S01117 */
     A**********R DL3110S1                  TEXT('Enquiry Subfile')
     A          R TM3110S1                  TEXT('Enquiry Subfile')
     A*                                                             /* S01117 */
     A*
     A* Subfile Record Format and a select field
     A*
     A                                      SFL
     A N15        SSSLCT         1A  I  6  4TEXT('Select Field')
     A  15                                  DSPATR(PR)
     A            DDDAT          7A     6 11TEXT('Date')
     A                                      DSPATR(HI)
     A            DDIN          10A     6 21TEXT('Amount In')
     A                                      DSPATR(HI)
     A            DDOUT         10A     6 36TEXT('Amount Out')
     A                                      DSPATR(HI)
     A            DDNET         10A     6 51TEXT('FX Net')
     A                                      DSPATR(HI)
     A            DDDAT2         5A     6 66TEXT('Date')
     A                                      DSPATR(ND)
     A*
     A*
     A*                                                             /* S01117 */
     A**********R DL3110C1                  TEXT('Enquiry Subfile Control')
     A          R TM3110C1                  TEXT('Enquiry Subfile Control')
     A*                                                             /* S01117 */
     A*
     A* Enquiry Subfile Control and prompts
     A*
     A*                                                             /* S01117 */
     A**********                            SFLCTL(DL3110S1)
     A                                      SFLCTL(TM3110S1)
     A*                                                             /* S01117 */
     A  35                                  SFLDSP
     A  36                                  SFLDSPCTL
     A N35N36                               SFLCLR
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0016)
     A  30                                  SFLEND
     A                                      OVERLAY
     A N27                                  ROLLUP(25)
     A N27                                  ROLLDOWN(26)
     A*
     A* Prompts
     A*
     A                                 22 30'CURRENCY'
     A            @SRCCY         3A  B 22 39TEXT('Currency Selected')
     A N60                                  DSPATR(HI)
     A  60                                  DSPATR(RI PC)
     A*
     A                                 22 44'DATE'
     A            @SRDAT         5A  B 22 49TEXT('Date Selected')
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(RI PC)
