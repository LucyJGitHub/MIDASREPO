      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas TM Daily deals (was DL3130DD)')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Treasury Management Module                           *
     A*                                                               *
     A*  DSPF/TM3130DD - FX BOOK - DAILY DEALS                        *   S01117
     A*                  (FORMERLY DL3130DD)                          *   S01117
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 Patch D ---------------------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *  Prev Amend No. 167048             DATE 24SEP99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 E39162             DATE 18JUN92               *
     A*                 S01280             DATE 08FEB91               *
     A*                 S01117             DATE 08NOV90               *
     A*                 S01253             DATE 17APR90               *
     A*                 S01199             DATE 27FEB90               *
     A*                 S01187             DATE 16JAN89               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  MD046248 - Finastra Rebranding                               *
     A*  167048  -  Move the INVITE to apply only to TM3130C1         *
     A*  E39162  -  CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                *
     A*  S01280  -  ADD PUTOVR TO PREVENT SCREEN FLASHING             *
     A*          -  CHANGE TM3130F2 SO THAT 'DAILY TOTAL' NOT         *
     A*             DISPLAYED IF NO RECORDS TO DISPLAY                *
     A*  S01117  -  ALL PROGRAMS/FILES SPECIFIC TO TREASURY MGMT.     *  S01117
     A*             RENAMED TO HAVE 'TM' PREFIX                       *  S01117
     A*             COPYRIGHT STATEMENT CHANGED                       *  S01117
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*  S01199  -  HELP SYSTEM.                                  *
     A*  S01187  -  RELEASE 1 TEST FIX                                *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A* INDICATORS TO BE RECEIVED FROM THE SCREEN - COMMANDS          *
     A*                                                               *
     A*     11   -  EXIT PROGRAM                                      *
     A*     12   -  SUMMARY                                           *
     A*     05   -  REFRESH SCREEN                                    *
     A*     10   -  HELP KEY HAS BEEN PRESSED                         *
     A*     25   -  ROLLUP REQUESTED                                  *
     A*     26   -  ROLLDOWN REQUESTED                                *
     A*                                                               *
     A*     27   -  INHIBIT ROLL/REFRESH/SELECT KEYS & TEXT/ENQ.TITLES*
     A*     28   -  ACTIVATE THE OVRDTA KEYWORD FOR KEYS/TEXT & TITLES*
     A*                                                               *
     A* INDICATORS TO CONTROL THE ENQUIRY SUBFILE                     *
     A*                                                               *
     A*     30   -  ACTIVATE SUBFILE END                              *
     A*     35   -  ACTIVATE SUBFILE DISPLAY                          *
     A*     (N35 -  ACTIVATE SUBFILE CLEAR)                           *
     A*                                                               *
     A* INDICATORS TO CONTROL THE MESSAGE SUBFILE                     *
     A*                                                               *
     A*     40   -  ACTIVATE SUBFILE END                              *
     A*     46   -  ACTIVATE SUBFILE DISPLAY CONTROL                  *
     A*                                                               *
     A* INDICATORS TO REPORT ERRORS ON VALIDATION OF PROMPT SCREEN    *
     A*                                                               *
     A*     69   -  GENERAL ERROR ON VALIDATION OF PROMPT SCREEN      *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A                                      CA03(11 'F3 to Exit')
     A**27******                            CA02(12 'F2 Summary')     /*S01187*/
     A  27                                  CA12(12 'F12 Summary')
     A  27                                  CA05(05 'F5 to Refresh')
     A  27                                  ROLLUP(25)
     A  27                                  ROLLDOWN(26)
     A*                                      Begin /167048/
     A**36*******                           INVITE
     A*                                      End   /167048/
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A*                                                    /*S01199*/
     A*                                                             /* S01117 */
     A*                                                             /* S01117 */
     A*                                                    /*S01199*/
     A*                                                             /* S01117 */
     A**********R DL3130F0                  TEXT('Title')
     A          R TM3130F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*                                                               /*S01280*/
     A                                      PUTOVR
     A*                                                               /*S01280*/
     A*                                                             /* S01117 */
     A                                      OVERLAY
     A*
     A*                                                             /* S01117 */
     A**********                        1  2'Ref: DL3130'
     A                                  1  2'Ref: TM3130'
     A*                                                             /* S01117 */
     A*                                                               /*S01280*/
     A*************                     1 61TIME                      /*S01280*/
     A            DDTIME         6  0O  1 61EDTWRD('  :  :  ')
     A                                      OVRDTA
     A*                                                               /*S01280*/
     A                                  1 71'WSID'
     A            DDWSID         3A     1 76
     A                                  2  2'FX BOOK - DAILY DEALS'
     A                                      DSPATR(UL HI)
     A*                                                               /*S01280*/
     A************DDDATE         7A     2 51
     A            DDDATE         7A     2 51OVRDTA
     A************DDCURR         3A     2 61
     A            DDCURR         3A     2 61OVRDTA
     A*                                                               /*S01280*/
     A                                  2 66'(000''s)'
     A*                                                               /*S01280*/
     A************DDSTAR         5A     3 67
     A            DDSTAR         5A     3 67OVRDTA
     A*                                                               /*S01280*/
     A*
     A* Subfile Titles
     A*
     A                                  4  2'SELECT'
     A                                      DSPATR(UL)
     A                                  4 11'DEAL NO.'
     A                                      DSPATR(UL)
     A                                  4 25'CUSTOMER'
     A                                      DSPATR(UL)
     A*
     A*                                                             /* S01117 */
     A**********R DL3130F1                  TEXT('Footer')
     A          R TM3130F1                  TEXT('Footer')
     A*                                                             /* S01117 */
     A                                      OVERLAY
     A*
     A                                 23  2'Press F3 to End,'
     A**********                       23 21'F2 for summary,'         /*S01187*/
     A                                 23 21'F12 for summary,'
     A                                 23 41'F5 to Refresh,'
     A                                 24  2'Rollup/Rolldown for'
     A                                 24 22'further details,'
     A                                 24 39'enter ''1'''
     A                                 24 49'to select'
     A                                 24 59'detail enquiry'
     A*
     A*                                                             /* S01117 */
     A**********R DL3130F2                  TEXT('Footer')
     A          R TM3130F2                  TEXT('Footer')
     A*                                                             /* S01117 */
     A                                      OVERLAY
     A                                      PUTOVR
     A*
     A                                 20 26'DAILY TOTAL'
     A N35                                  DSPATR(ND)
     A*                                                             /* S01280 */
     A                                      OVRATR
     A*                                                             /* S01280 */
     A            DDTOT         11A    20 39DSPATR(HI)
     A N35                                  DSPATR(ND)
     A                                      OVRDTA
     A                                      OVRATR
     A            DDCUR          3A    20 51DSPATR(HI)
     A N35                                  DSPATR(ND)
     A                                      OVRDTA
     A                                      OVRATR
     A*
     A*
     A*                                                             /* S01117 */
     A**********R DL3130S0                  TEXT('Message Subfile')
     A          R TM3130S0                  TEXT('Message Subfile')
     A*                                                             /* S01117 */
     A*
     A* Message Subfile
     A*
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDMGKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A*
     A*                                                             /* S01117 */
     A**********R DL3130C0                  TEXT('Message Subfile Control')
     A          R TM3130C0                  TEXT('Message Subfile Control')
     A*                                                             /* S01117 */
     A*
     A* Message Subfile Control
     A*
     A*                                                             /* S01117 */
     A**********                            SFLCTL(DL3130S0)
     A                                      SFLCTL(TM3130S0)
     A*                                                             /* S01117 */
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A  46                                  SFLDSPCTL
     A                                      SFLINZ
     A  40                                  SFLEND
     A                                      OVERLAY
     A            DDPGMQ                    SFLPGMQ
     A*
     A*
     A*                                                             /* S01117 */
     A**********R DL3130S1                  TEXT('Enquiry Subfile')
     A          R TM3130S1                  TEXT('Enquiry Subfile')
     A*                                                             /* S01117 */
     A*
     A* Subfile Record Format and a select field
     A*
     A                                      SFL
     A*
     A            SSSLCT         1A  I  5  5TEXT('Select Field')
     A                                      DSPATR(HI)
     A            DDDEAL         6A     5 11TEXT('Deal no.')
     A                                      DSPATR(HI)
     A            DDCUST        10A     5 23TEXT('Customer')
     A                                      DSPATR(HI)
     A            DDAMT1        11A     5 39TEXT('Amount 1')
     A                                      DSPATR(HI)
     A            DDCUR1         3A     5 51TEXT('Currency 1')
     A                                      DSPATR(HI)
     A            DDCUR2         3A     5 55TEXT('Currency 2')
     A                                      DSPATR(HI)
     A            DDAMT2        11A     5 59TEXT('Amount 2')
     A                                      DSPATR(HI)
     A*
     A*
     A*                                                             /* S01117 */
     A**********R DL3130C1                  TEXT('Enquiry Subfile Control')
     A          R TM3130C1                  TEXT('Enquiry Subfile Control')
     A*                                                             /* S01117 */
     A*
     A* Enquiry Subfile Control
     A*
     A*                                                             /* S01117 */
     A**********                            SFLCTL(DL3130S1)
     A                                      SFLCTL(TM3130S1)
     A*                                                             /* S01117 */
     A  35                                  SFLDSP
     A  36                                  SFLDSPCTL
     A N35N36                               SFLCLR
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0015)
     A  30                                  SFLEND
     A*                                      Begin /167048/
     A  36                                  INVITE
     A*                                      End   /167048/
     A                                      OVERLAY
     A                                      PUTOVR
     A*
     A                                 22 21'CURRENCY'
     A            DDCCY          3A  B 22 30OVRATR
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A  65                                  DSPATR(RI PC)
     A N65                                  DSPATR(UL)
     A*
     A                                 22 35'DATE'
     A            DDDMY          7A  B 22 40OVRATR
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A  66                                  DSPATR(RI PC)
     A N66                                  DSPATR(UL)
