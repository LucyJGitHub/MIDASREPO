      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas TM FX CCY exposure against limits enquiry')
     A*
     A*****************************************************************
     A*  Midas - Treasury Management Module
     A*                                                               *
     A*     TM3170DD - DISPLAY FILE FOR PGM TM3170                    *
     A*                (CURRENCY EXPOSURE AGAINST LIMITS ENQUIRY)     *
     A*                  (FORMERLY DL3170DD)
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A***                                                         *** *
     A***   CREATION PARAMETERS       - RSTDSP(*YES)              *** *
     A***                             - SHARE(*YES)               *** *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. E39162             DATE 18JUN92               *
     A*  PREV AMEND NO. S01280             DATE 21NOV90               *
     A*                 S01117             DATE 19NOV90               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  E39162  -  CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                *
     A*  S01280  -  TIME NOT BEING UPDATED WHEN SCREEN REFRESHED      *
     A*                                                               *
     A*  S01117  -  ALL PROGRAMS/FILES SPECIFIC TO TREASURY MGMT.     *
     A*             RENAMED TO HAVE 'TM' PREFIX                       *
     A*             COPYRIGHT STATEMENT CHANGED                       *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N   P A R A M E T E R S                        *
     A*                                                               *
     A***************************************************************
     A*                                                               *
     A**  Indicator Function Summary                                  *
     A*                                                               *
     A* ID F  C  H  L    FUNCTION OF INDICATORS                       *
     A*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~*
     A*                                                               *
     A* INDICATORS TO BE RECEIVED FROM THE SCREEN - COMMANDS          *
     A*                                                               *
     A*     01   -  EXIT PROGRAM                                      *
     A*     05   -  REFRESH SCREEN                                    *
     A*     10   -  HELP KEY HAS BEEN PRESSED                         *
     A*     25   -  ROLLUP REQUESTED                                  *
     A*     26   -  ROLLDOWN REQUESTED                                *
     A*                                                               *
     A*     27   -  INHIBIT ROLL/REFRESH/SELECT KEYS & TEXT/ENQ.TITLES*
     A*                                                               *
     A* INDICATORS TO CONTROL THE ENQUIRY SUBFILE                     *
     A*                                                               *
     A*     30   -  ACTIVATE SUBFILE END                              *
     A*     35   -  ACTIVATE SUBFILE DISPLAY                          *
     A*     (N35 -  ACTIVATE SUBFILE CLEAR)                           *
     A*     36   -  ACTIVATE SUBFILE DISPLAY CONTROL                  *
     A*                                                               *
     A* INDICATORS TO CONTROL THE MESSAGE SUBFILE                     *
     A*                                                               *
     A*     40   -  ACTIVATE SUBFILE END                              *
     A*     46   -  ACTIVATE SUBFILE DISPLAY CONTROL                  *
     A*                                                               *
     A* INDICATOR TO SHOW THAT LIMIT AMOUNTS HAVE BEEN EXCEEDED       *
     A*                                                               *
     A*     85   -  DAYLIGHT LIMIT EXCEEDED                           *
     A*     86   -  OVERNIGHT LIMIT EXCEEDED                          *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A  36                                  INVITE
     A                                      CA03(01 'F3 to Exit')
     A                                      CA05(05 'F5 to Refresh')
     A*                                                        /* S01117 */
     A*                                                        /* S01117 */
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A*                                                        /* S01117 */
     A*                                                        /* S01117 */
     A*
     A*                                                        /* S01117 */
     A*****     R DL3170F0                  TEXT('TITLE')
     A          R TM3170F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*                                                        /* S01117 */
     A                                      OVERLAY
     A*                                                        /* S01280 */
     A*****                                 PUTOVR
     A*                                                        /* S01280 */
     A*
     A*                                                        /* S01117 */
     A*****                             1  2'Ref: DL3170'
     A                                  1  2'Ref: TM3170'
     A*                                                        /* S01117 */
     A                                  1 61TIME
     A                                  1 71'WSID'
     A            DDWSID         3A     1 76
     A*
     A                                  2  2'CURRENCY EXPOSURE AGAINST LIMITS'
     A                                      DSPATR(UL HI)
     A                                  2 66'(000''S)'
     A*
     A*                                                        /* S01280 */
     A*****       DDSTEN         5A  O  3 68OVRDTA
     A            DDSTEN         5A  O  3 68
     A*                                                        /* S01280 */
     A*
     A* SUBFILE TITLES
     A*
     A                                  4  3'FX'
     A                                      DSPATR(UL)
     A                                  5  2'CCY'
     A                                      DSPATR(UL)
     A                                  5 12'OPEN POSN'
     A                                      DSPATR(UL)
     A                                  5 33'DAYLIGHT'
     A                                      DSPATR(UL)
     A                                  5 52'OVERNIGHT'
     A                                      DSPATR(UL)
     A*
     A*                                                        /* S01117 */
     A*****     R DL3170F1                  TEXT('Footer')
     A          R TM3170F1                  TEXT('Footer')
     A*                                                        /* S01117 */
     A                                      OVERLAY
     A*
     A                                 24  2'F3 to Exit,'
     A                                 24 18'F5 to Refresh,'
     A                                 24 38'Rollup/Rolldown for'
     A                                 24 58'further details'
     A*
     A*                                                        /* S01117 */
     A*****     R DL3170S0                  TEXT('Message Subfile')
     A          R TM3170S0                  TEXT('Message Subfile')
     A*                                                        /* S01117 */
     A*
     A* Message Subfile
     A*
     A                                      SFL
     A                                      SFLMSGRCD(23)
     A            DDMGKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A*
     A*                                                        /* S01117 */
     A*****     R DL3170C0                  TEXT('Message Subfile Control')
     A          R TM3170C0                  TEXT('Message Subfile Control')
     A*                                                        /* S01117 */
     A*
     A* Message Subfile Control
     A*
     A*                                                        /* S01117 */
     A*****                                 SFLCTL(DL3170S0)
     A                                      SFLCTL(TM3170S0)
     A*                                                        /* S01117 */
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A  46                                  SFLDSPCTL
     A                                      SFLINZ
     A  40                                  SFLEND
     A                                      OVERLAY
     A            DDPGMQ                    SFLPGMQ
     A*
     A*
     A*                                                        /* S01117 */
     A*****     R DL3170S1                  TEXT('Enquiry Subfile')
     A          R TM3170S1                  TEXT('Enquiry Subfile')
     A*                                                        /* S01117 */
     A*
     A* Subfile Record Format and a select field
     A*
     A                                      SFL
     A*
     A            DDCCY          3A     7  2TEXT('CCY')
     A                                      DSPATR(HI)
     A            DDOPP         11A     7 11TEXT('OPEN POSN')
     A                                      DSPATR(HI)
     A            DDDAY         10A     7 31TEXT('DAYLIGHT')
     A  85                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A            DDOVN         10A     7 51TEXT('OVERNIGHT')
     A  86                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A*
     A*                                                        /* S01117 */
     A*****     R DL3170C1                  TEXT('Enquiry Subfile Control')
     A          R TM3170C1                  TEXT('Enquiry Subfile Control')
     A*                                                        /* S01117 */
     A*
     A* Enquiry Subfile Control
     A*
     A*                                                        /* S01117 */
     A*****                                 SFLCTL(DL3170S1)
     A                                      SFLCTL(TM3170S1)
     A*                                                        /* S01117 */
     A  35                                  SFLDSP
     A  36                                  SFLDSPCTL
     A N35N36                               SFLCLR
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0015)
     A  30                                  SFLEND
     A                                      OVERLAY
