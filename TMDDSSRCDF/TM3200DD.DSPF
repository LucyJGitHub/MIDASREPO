      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas TM MM Daily Deals enquiry (was DL3200DD)')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Treasury Management Module
     A*                                                               *
     A*  TM3200DD - DISPLAY FILE FOR PGM TM3200                       *
     A*             (MONEY MARKET DAILY DEALS ENQUIRY)                *
     A*             (FORMERLY DL3200DD)                               *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. E39162             DATE 18JUN92               *
     A*                 E30849             DATE 30OCT91               *
     A*                 S01280             DATE 22OCT90               *
     A*                 S01117             DATE 30OCT90               *
     A*                 S01253             DATE 17APR90               *
     A*                 S01199             DATE 27FEB90               *
     A*                 S01187             DATE 16JAN89               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  MD046248 - Finastra Rebranding                               *
     A*  E39162  -  CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                *
     A*  E30849  -  At Release 10.3 packaging, the file still         *
     A*             referred to the Release 2 Help folder.            *
     A*                                                               *
     A*  S01280  -  Include FRA,IRS,FUTURES and CUSTOMER LENDING      *
     A*             modules.                                          *
     A*                - ENSURE 'NET AT TAKEON' TITLES ALWAYS         *
     A*                  DISPLAYED, IN SPITE OF THE PUTOVR            *
     A*          -  TIME NOT BEING UPDATED IF SCREEN REFRESHED OR     *
     A*             NEW DATA INPUT                                    *
     A*                                                               *
     A*  S01117  -  ALL PROGRAMS/FILES SPECIFIC TO TREASURY MGMT.     *   S01117
     A*             RENAMED TO HAVE 'TM' PREFIX                       *   S01117
     A*             COPYRIGHT STATEMENT CHANGED                       *   S01117
     A*                                                               *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*                                                               *
     A*  S01199  -  HELP SYSTEM                                   *
     A*                                                               *
     A*  S01187  -  RELEASE 1 TEST FIX                                *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A**  Indicator Function Summary                                  *
     A*                                                               *
     A* ID F  C  H  L    FUNCTION OF INDICATORS                       *
     A*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~*
     A*                                                               *
     A* INDICATORS TO BE RECEIVED FROM THE SCREEN - COMMANDS          *
     A*                                                               *
     A*     01   -  EXIT PROGRAM                                      *
     A*     12   -  SUMMARY                                           *
     A*     05   -  REFRESH SCREEN                                    *
     A*     10   -  HELP KEY HAS BEEN PRESSED                         *
     A*     25   -  ROLLUP REQUESTED                                  *
     A*     26   -  ROLLDOWN REQUESTED                                *
     A*                                                               *
     A*     27   -  INHIBIT ROLL/REFRESH/SELECT KEYS & TEXT/ENQ.TITLES*
     A*     28   -  ACTIVATE OVRDTA KEYWORD                           *
     A*                                                               *
     A* INDICATORS TO CONTROL THE ENQUIRY SUBFILE                     *
     A*                                                               *
     A*     30   -  ACTIVATE SUBFILE END                              *
     A*     35   -  ACTIVATE SUBFILE DISPLAY                          *
     A*     (N35 -  ACTIVATE SUBFILE CLEAR)                           *
     A*     36   -  ACTIVATE SUBFILE DISPLAY CONTROL                  *
     A*                                                               *
     A* INDICATORS TO CONTROL THE MESSAGE SUBFILE                     *
     A*                                                               *
     A*     40   -  ACTIVATE SUBFILE END                              *
     A*     46   -  ACTIVATE SUBFILE DISPLAY CONTROL                  *
     A*                                                               *
     A*     69   -  GENERAL ERROR ON VALIDATION OF PROMPT SCREEN      *
     A*                                                               *
     A* INDICATOR TO SHOW THAT LIMIT AMOUNTS HAVE BEEN EXCEEDED       *
     A*                                                               *
     A*     85   -  LIMIT EXCEEDED                                    *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A  36                                  INVITE
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A                                      CA03(01 'F3 to Exit')
     A**27******                            CA02(02 'F2 Summary')     /*S01187*/
     A  27                                  CA12(02 'F12 Summary')
     A  27                                  CA05(05 'F5 to Refresh')
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A  27                                  ROLLUP(25)
     A  27                                  ROLLDOWN(26)
     A*                                                    /*S01199*/
     A*                                                             /* S01117 */
     A*                                                             /* S01117 */
     A*                                                    /*S01199*/
     A*                                                             /* S01117 */
     A**********R DL3200F0
     A          R TM3200F0
     A**********                            TEXT('TITLE')
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*                                                             /* S01117 */
     A                                      OVERLAY
     A                                      PUTOVR
     A*                                                             /* S01117 */
     A**********                        1  2'Ref: DL3200'
     A                                  1  2'Ref: TM3200'
     A*                                                             /* S01117 */
     A*                                                             /* S01280 */
     A**********                        1 61TIME
     A            DDTIME         6  0O  1 61EDTWRD('  :  :  ')
     A                                      OVRDTA
     A*                                                             /* S01280 */
     A                                  1 71'WSID'
     A            DDWSID         3A  O  1 76
     A                                  2  2'MM BOOK - DAILY DEALS'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            DDDATE         7A  O  2 51OVRDTA
     A            DDCURR         3A  O  2 61OVRDTA
     A                                  2 66'(000''S)'
     A            DDSTEN         5A  O  3 68OVRDTA
      *                                     START OF S01280               S01280
     A**********  DDTYPE         3A  O  3  2OVRDTA                        S01280
     A            DDTYPE        24A  O  3  2OVRDTA
     A            DDSTS         23A  O  3 33
      *                                     END OF S01280                 S01280
     A                                  4  2'SELECT'
     A                                      DSPATR(UL)
     A                                  4 11'DEAL NO.'
     A                                      DSPATR(UL)
      *                                     START OF S01280               S01280
     A**********                        4 23'CUSTOMER'                    S01280
     A                                  4 22'CUSTOMER'
     A                                      DSPATR(UL)
     A**********                        4 36'DATE'                        S01280
     A                                  4 35'DATE'
     A                                      DSPATR(UL)
     A**********                        4 44'RATE'
     A                                  4 43'RATE'
     A                                      DSPATR(UL)
     A                                  4 58'TYPE'
     A                                      DSPATR(UL)
     A**********                        4 61'AMOUNT'
     A                                  4 67'AMOUNT'
      *                                     END OF S01280                 S01280
     A                                      DSPATR(UL)
     A*                                                             /* S01117 */
     A**********R DL3200F1
     A          R TM3200F1
     A*                                                             /* S01117 */
     A                                      TEXT('Footer')
     A                                      OVERLAY
     A*********                        23  2'Press F3 to end, F2 for summ-
     A*********                             ary, F5 to refresh'       /*S01187*/
     A                                 23  2'Press F3 to end, F12 for summ-
     A                                      ary, F5 to refresh'
     A                                 24  2'Rollup/Rolldown for'
     A                                 24 22'further details'
     A                                 24 39'enter ''1'''
     A                                 24 49'to select'
     A                                 24 59'detail enquiry'
     A*                                                             /* S01117 */
     A**********R DL3200F2                  TEXT('TOTAL LINE')
     A          R TM3200F2                  TEXT('TOTAL LINE')
     A*                                                             /* S01117 */
     A                                      OVERLAY
     A                                      PUTOVR
     A*
      *                                     START OF S01280               S01280
     A**********                       20 56'NET'
     A                                 19 41'NET AT LAST TAKE ON'
     A*N35*********************             DSPATR(ND)                    S01280
     A**********  DDNET         11A    20 61DSPATR(HI)
     A            DDNET         11A    19 61DSPATR(HI)
      *                                     END OF S01280                 S01280
     A N35                                  DSPATR(ND)
     A                                      OVRDTA
     A                                      OVRATR
     A*
     A*                                                             /* S01117 */
     A**********R DL3200S0                  SFL
     A          R TM3200S0                  SFL
     A*                                                             /* S01117 */
     A*
     A* Message Subfile
     A*
     A                                      SFLMSGRCD(22)
     A                                      TEXT('Message Subfile')
     A            DDMGKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*                                                             /* S01117 */
     A**********R DL3200C0                  SFLCTL(DL3200S0)
     A          R TM3200C0                  SFLCTL(TM3200S0)
     A*                                                             /* S01117 */
     A*
     A* Message Subfile Control
     A*
     A                                      TEXT('Message Subfile Control')
     A                                      OVERLAY
     A                                      SFLDSP
     A  46                                  SFLDSPCTL
     A                                      SFLINZ
     A  40                                  SFLEND
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0001)
     A            DDPGMQ                    SFLPGMQ
     A*                                                             /* S01117 */
     A**********R DL3200S1                  SFL
     A          R TM3200S1                  SFL
     A*                                                             /* S01117 */
     A*
     A* Subfile Record Format and a select field
     A*
     A                                      TEXT('Enquiry Subfile')
     A            DDSLCT         1   I  5  4DSPATR(HI)
     A            DDDEAL         6   O  5 11DSPATR(HI)
      *                                     START OF S01280               S01280
     A**********  DDCUST        10   O  5 23DSPATR(HI)
     A            DDCUST        10   O  5 22DSPATR(HI)
     A**********  DDDAT          7   O  5 36DSPATR(HI)
     A            DDDAT          7   O  5 35DSPATR(HI)
     A**********  DDRATE        12   O  5 44DSPATR(HI)
     A            DDRATE        12   O  5 43DSPATR(HI)
     A            DDDTYP         5   O  5 58DSPATR(HI)
     A            DDBOS          1   O  5 64DSPATR(HI)
     A**********  DDAMNT        11   O  5 61DSPATR(HI)
     A            DDAMNT        11   O  5 67DSPATR(HI)
     A            DDHID          3A  H
      *                                     END OF S01280                 S01280
     A*                                                             /* S01117 */
     A**********R DL3200C1                  SFLCTL(DL3200S1)
     A          R TM3200C1                  SFLCTL(TM3200S1)
     A*                                                             /* S01117 */
     A*
     A* Enquiry Subfile Control
     A*
     A                                      TEXT('Enquiry Subfile Control')
     A                                      OVERLAY
     A  35                                  SFLDSP
     A  36                                  SFLDSPCTL
     A N35N36                               SFLCLR
     A  30                                  SFLEND
      *                                     START OF S01280               S01280
     A**********                            SFLSIZ(0015)
     A**********                            SFLPAG(0015)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0014)
      *                                     END OF S01280                 S01280
     A                                      PUTOVR
     A*
     A* Input Prompts
     A*
     A*                                                    /*S01199*/
     A** Start of change                                   /E30849/
     A** End of change                                     /E30849/
     A*                                                    /*S01199*/
      *                                     START OF S01280               S01280
     A**********                       21  9'CURRENCY'
     A**********  DDCCY          3A  B 21 18OVRATR
     A                                 20  2'CURRENCY'
     A            DDCCY          3A  B 20 11OVRATR
      *                                     END OF S01280                 S01280
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A  65                                  DSPATR(RI PC)
     A N65                                  DSPATR(UL)
     A*
      *                                     START OF S01280               S01280
     A**********                       21 23'DATE'
     A**********  DDDMY          7A  B 21 28OVRATR
     A                                 20 15'DATE'
     A            DDDMY          7A  B 20 21OVRATR
      *                                     END OF S01280                 S01280
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A  66                                  DSPATR(RI PC)
     A N66                                  DSPATR(UL)
     A*
      *                                     START OF S01280               S01280
     A**********                       21 37'INCL. DEAL TYPES'
     A**********  DDTYP          3A  B 21 54OVRATR
     A                                 20 30'INCLUDE DEAL TYPES'
     A                                 20 49'ALL'
     A            DDALL          1A  B 20 54OVRATR
      *                                     END OF S01280                 S01280
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A  67                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
      *                                     START OF S01280               S01280
     A                                 20 57'N/A'
     A            DDNA           1A  B 20 62OVRATR
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A  67                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
     A                                 20 65'L/D'
     A            DDLD           1A  B 20 70OVRATR
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A  67                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
     A                                 20 73'LEN'
     A N41                                  DSPATR(ND)
     A            DDLEN          1A  B 20 78OVRATR
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A  67                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
     A N41                                  DSPATR(ND)
     A N41                                  DSPATR(PR)
      *
     A                                 21 49'FRA'
     A N42                                  DSPATR(ND)
     A            DDFRA          1A  B 21 54OVRATR
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A  67                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
     A N42                                  DSPATR(ND)
     A N42                                  DSPATR(PR)
     A                                 21 57'IRS'
     A N42                                  DSPATR(ND)
     A            DDIRS          1A  B 21 62OVRATR
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A  67                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
     A N42                                  DSPATR(ND)
     A N42                                  DSPATR(PR)
      *
     A                                 21 65'FUT'
     A N43                                  DSPATR(ND)
     A            DDFUT          1A  B 21 70OVRATR
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A  67                                  DSPATR(RI PC)
     A N67                                  DSPATR(UL)
     A N43                                  DSPATR(ND)
     A N43                                  DSPATR(PR)
      *                                     END OF S01280                 S01280
