      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas TM MM N/A daily deals enqry (was DL3210DD)')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Treasury Management Module
     A*                                                               *
     A*  TM3210DD - DISPLAY FILE FOR PGM TM3210                       *
     A*             (MONEY MARKET N/A DAILY DEALS ENQUIRY)            *
     A*             (FORMERLY DL3210DD)                               *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. E39162             DATE 18JUN92               *
     A*                 S01280             DATE 15FEB91               *
     A*                 S01117             DATE 26NOV90               *
     A*                 S01253             DATE 17APR90               *
     A*                 S01199             DATE 28FEB90               *
     A*                 E90092             DATE 02FEB89               *
     A*                                                               *
     A*****************************************************************
     A*  MD046248 - Finastra Rebranding                               *
     A*  E39162  -  CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                *
     A*  S01280  -  Clear subfile from screen if no records for       *
     A*             prompts (for the display file this only involves  *
     A*             adding an extra conditioning indicator for the    *
     A*             totals fields, since they were previously only    *
     A*             conditioned on *IN35-SFLDSP)                      *
     A*          -  ADD PUTOVR TO PREVENT SCREEN FLASH                *
     A*                                                               *
     A*  S01117  -  ALL PROGRAMS/FILES SPECIFIC TO TREASURY MGMT.     *  S01117
     A*             RENAMED TO HAVE 'TM' PREFIX                       *  S01117
     A*             COPYRIGHT STATEMENT CHANGED                       *  S01117
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*  S01199  -  HELP SYSTEM.                                  *
     A*  E90092  -  BOOK TOTAL NOT APPEARING ON SCREEN; ENQUIRY       *
     A*             SHOULD BE SAME AS DRS EQUIVALENT.                 *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A**  Indicator Function Summary                                  *
     A*                                                               *
     A* ID F  C  H  L    FUNCTION OF INDICATORS                       *
     A*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~*
     A*                                                               *
     A* INDICATORS TO BE RECEIVED FROM THE SCREEN - COMMANDS          *
     A*                                                               *
     A*     01   -  EXIT PROGRAM                                      *
     A*     05   -  REFRESH SCREEN                                    *
     A*     10   -  HELP KEY HAS BEEN PRESSED                         *
     A*     25   -  ROLLUP REQUESTED                                  *
     A*     26   -  ROLLDOWN REQUESTED                                *
     A*                                                               *
     A*     27   -  INHIBIT ROLL/REFRESH/SELECT KEYS & TEXT/ENQ.TITLES*
     A*     28   -  ACTIVATE OVRDTA KEYWORD                           *
     A*                                                               *
     A* INDICATORS TO CONTROL THE ENQUIRY SUBFILE                     *
     A*                                                               *
     A*     30   -  ACTIVATE SUBFILE END                              *
     A*     35   -  ACTIVATE SUBFILE DISPLAY                          *
     A*     (N35 -  ACTIVATE SUBFILE CLEAR)                           *
     A*     36   -  ACTIVATE SUBFILE DISPLAY CONTROL                  *
     A*     37   -  SUBFILE TOTALS NOT DISPLAYED                      *   S01280
     A*                                                               *
     A* INDICATORS TO CONTROL THE MESSAGE SUBFILE                     *
     A*                                                               *
     A*     40   -  ACTIVATE SUBFILE END                              *
     A*     46   -  ACTIVATE SUBFILE DISPLAY CONTROL                  *
     A*                                                               *
     A*     69   -  GENERAL ERROR ON VALIDATION OF PROMPT SCREEN      *
     A*                                                               *
     A*     80   -  SHOW DEAL TYPE IN REVERSE                         *
     A*                                                               *
     A* INDICATOR TO SHOW THAT LIMIT AMOUNTS HAVE BEEN EXCEEDED       *
     A*                                                               *
     A*     85   -  LIMIT EXCEEDED                                    *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A  36                                  INVITE
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A                                      CA03(01 'F3 to Exit')
     A  27                                  CA05(05 'F5 to Refresh')
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A  27                                  ROLLUP(25)
     A  27                                  ROLLDOWN(26)
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A*                                                             /* S01117 */
     A**********R DL3210F0
     A          R TM3210F0
     A**********                            TEXT('TITLE')
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*                                                               /*S01280*/
     A                                      PUTOVR
     A*                                                               /*S01280*/
     A**********                        1  2'Ref: DL3210'
     A                                  1  2'Ref: TM3210'
     A*                                                             /* S01117 */
     A*                                                               /*S01280*/
     A***************                   1 61TIME
     A            DDTIME         6  0O  1 61EDTWRD('  :  :  ')
     A                                      OVRDTA
     A*                                                               /*S01280*/
     A                                  1 71'WSID'
     A            DDWSID         3A  O  1 76
     A*                                                               /*S01280*/
     A************DDCURR         3A  O  2 58
     A            DDCURR         3A  O  2 58OVRDTA
     A*                                                               /*S01280*/
     A                                  2 66'(000''S)'
     A*                                                               /*S01280*/
     A************DDSTEN         5A  O  3 68
     A            DDSTEN         5A  O  3 68OVRDTA
     A*                                                               /*S01280*/
     A                                  2  2'NEGOTIABLE ASSETS - DAILY DEALS'
     A                                      DSPATR(UL)
     A                                  5  2'MAT. DATE'
     A                                      DSPATR(UL)
     A                                  5 14'TYPE'
     A                                      DSPATR(UL)
     A                                  5 21'CUSTOMER'
     A                                      DSPATR(UL)
     A                                  5 34'FACE VALUE'
     A                                      DSPATR(UL)
     A                                  5 51'NET VALUE'
     A                                      DSPATR(UL)
     A                                  5 66'YIELD'
     A                                      DSPATR(UL)
     A*                                                             /* S01117 */
     A**********R DL3210F1
     A          R TM3210F1
     A*                                                             /* S01117 */
     A                                      TEXT('Footer')
     A*                                                               /*S01280*/
     A                                      PUTOVR
     A*                                                               /*S01280*/
     A                                      OVERLAY
     A                                 23  2'Press F3 to end, F5 to refre-
     A                                      sh, Rollup/Rolldown for further det-
     A                                      ails'
     A*                                                             /* S01117 */
     A**********R DL3210S0                  TEXT('MESSAGE SUBFILE')
     A          R TM3210S0                  TEXT('MESSAGE SUBFILE')
     A*                                                             /* S01117 */
     A*
     A* Message Subfile
     A*
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDMGKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*                                                             /* S01117 */
     A**********R DL3210C0                  TEXT('Message Subfile Control')
     A          R TM3210C0                  TEXT('Message Subfile Control')
     A*                                                             /* S01117 */
     A*
     A* Message Subfile Control
     A*
     A*                                                             /* S01117 */
     A**********                            SFLCTL(DL3210S0)
     A                                      SFLCTL(TM3210S0)
     A*                                                             /* S01117 */
     A                                      OVERLAY
     A                                      SFLDSP
     A  46                                  SFLDSPCTL
     A                                      SFLINZ
     A  40                                  SFLEND
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0001)
     A            DDPGMQ                    SFLPGMQ
     A*                                                             /* S01117 */
     A**********R DL3210S1                  TEXT('ENQUIRY SUBFILE')
     A          R TM3210S1                  TEXT('ENQUIRY SUBFILE')
     A*                                                             /* S01117 */
     A*
     A* Subfile Record Format and a select field
     A*
     A                                      SFL
     A            DDMDAT         7A  O  7  2TEXT('DATE')
     A                                      DSPATR(HI)
     A            DDTYPE         2A  O  7 15TEXT('TYPE')
     A N80                                  DSPATR(HI)
     A  80                                  DSPATR(RI)
     A            DDCUST        10A  O  7 21TEXT('CUSTOMER')
     A                                      DSPATR(HI)
     A            DDFVAL        10A  O  7 34TEXT('FACE VALUE')
     A                                      DSPATR(HI)
     A            DDNVAL        10A  O  7 51TEXT('NET VALUE')
     A                                      DSPATR(HI)
     A            DDYLD         12A  O  7 66TEXT('YIELD')
     A                                      DSPATR(HI)
     A*                                                             /* S01117 */
     A**********R DL3210C1                  TEXT('Enquiry Subfile Control')
     A          R TM3210C1                  TEXT('Enquiry Subfile Control')
     A*                                                             /* S01117 */
     A*
     A* Enquiry Subfile Control
     A*
     A*                                                             /* S01117 */
     A**********                            SFLCTL(DL3210S1)
     A                                      SFLCTL(TM3210S1)
     A*                                                             /* S01117 */
     A                                      OVERLAY
     A  35                                  SFLDSP
     A  36                                  SFLDSPCTL
     A N35N36                               SFLCLR
     A  30                                  SFLEND
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0012)
     A*******                                                  /E90092/
     A* Book Total Line
     A*
     A*                                                      /*S01280*/
     A**35******                       19 21'BOOK TOTAL'
     A**35******  DDBKT         12A  O 19 32DSPATR(HI)
     A**35******  DDNET         12A  O 19 49DSPATR(HI)
     A  35N37                          19 21'BOOK TOTAL'
     A  35N37     DDBKT         12A  O 19 32DSPATR(HI)
     A  35N37     DDNET         12A  O 19 49DSPATR(HI)
     A*                                                      /*S01280*/
     A*******                                                  /E90092/
     A*
     A* Input Prompts
     A*
     A                                 21 11'CURRENCY'
     A            DDCCY          3A  B 21 21TEXT('CURRENCY SELECTED')
     A N65                                  DSPATR(HI)
     A  65                                  DSPATR(RI PC)
     A*
     A                                 21 26'START DATE'
     A            DDDMY          7A  B 21 38TEXT('START DATE')
     A N66                                  DSPATR(HI)
     A  66                                  DSPATR(RI PC)
     A*
