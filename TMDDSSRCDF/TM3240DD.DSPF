      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas TM MM Bank's position - all currencies')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Treasury Management Module
     A*                                                               *
     A*  DSPF/TM3240DD - MM BANK'S POSITION ENQUIRY - ALL CURRENCIES  *
     A*                  (FORMERLY DL3240DD)                          *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. E39162             DATE 18JUN92               *
     A*                 S01117             DATE 26NOV90               *
     A*                 S01253             DATE 17APR90               *
     A*                 S01199             DATE 28FEB90               *
     A*                 S01187             DATE 17JAN89               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  MD046248 - Finastra Rebranding                               *
     A*  E39162  -  CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                *
     A*  S01117  -  ALL PROGRAMS/FILES SPECIFIC TO TREASURY MGMT.     *
     A*             RENAMED TO HAVE 'TM' PREFIX                       *
     A*             COPYRIGHT STATEMENT CHANGED                       *
     A*                                                               *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*                                                               *
     A*  S01199  -  HELP SYSTEM.                                  *
     A*                                                               *
     A*  S01187 -   RELEASE 1 TEST FIX                                *
     A*                                                               *
     A*****************************************************************
     A*                                                              *
     A** Indicator Function Summary                                  *
     A*                                                              *
     A* ID F  C  H  L    FUNCTION OF INDICATORS                      *
     A*--------------------------------------------------------------*
     A*                                                              *
     A*  INDICATORS TO BE REIEVED FROM THE SCREEN - COMMANDS         *
     A*                                                              *
     A*      01   -  EXIT PROGRAMME                                  *
     A*      05   -  REFRESH SCREEN                                  *
     A*      10   -  HELP KEY                                        *
     A*      25   -  ROLLUP REQUESTED                                *
     A*      26   -  ROLLDOWN REQUESTED                              *
     A*                                                              *
     A* INDICATORS TO CONTROL THE ENQUIRY SUBFILE                    *
     A*                                                              *
     A*      30   -  ACTIVATE SUBFILE END                            *
     A*      35   -  ACTIVATE SUBFILE DISPLAY                        *
     A*      (N35 -  ACTIVATE SUBFILE CLEAR)                         *
     A*      36   -  ACTIVATE SUBFILE DISPLAY CONTROL                *
     A*                                                              *
     A* INDICATORS TO CONTROL THE MESSAGE SUBFILE                    *
     A*                                                              *
     A*      40   -  ACTIVATE SUBFILE END                            *
     A*      46   -  ACTIVATE SUBFILE DISPLAY CONTROL                *
     A*                                                              *
     A****************************************************************
     A*
     A** File description
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CA03(01 'F3 to Exit')
     A                                      CA05(05 'F5 to Refresh')
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A  36                                  INVITE
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A*                                                    /*S01199*/
     A*                                                        /* S01117 */
     A*                                                        /* S01117 */
     A*                                                    /*S01199*/
     A*                                                        /* S01117 */
     A*****     R DL3240F0
     A          R TM3240F0
     A*                                                        /* S01117 */
     A                                      PUTOVR
     A*                                                        /* S01117 */
     A*****                                 TEXT('Headings')
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*                                                        /* S01117 */
     A                                      OVERLAY
     A*                                                        /* S01117 */
     A*****                             1  2'REF: DL3240'
     A                                  1  2'REF: TM3240'
     A*                                                        /* S01117 */
     A            DDTIME         8A     1 61OVRDTA
     A                                  1 71'WSID'
     A            DDWSID         3A     1 76
     A                                  2  2'MM BANK''S POSITION - ALL CURRENCI-
     A                                      ES'
     A                                      DSPATR(UL)
     A                                  2 66'(000''S)'
     A                                  4  3'CCY'
     A                                      DSPATR(UL)
     A                                  4 11'NET BALANCE/INTEREST'
     A                                      DSPATR(UL)
     A                                  4 34'AVE RATE'
     A                                      DSPATR(UL)
     A                                  4 51'MKT RATE'
     A                                      DSPATR(UL)
     A                                  4 66'P/L (UNITS)'
     A                                      DSPATR(UL)
     A            DDSTEN         5A     3 68OVRDTA
     A*
     A** Footing record
     A*                                                        /* S01117 */
     A*****     R DL3240F1
     A          R TM3240F1
     A*                                                        /* S01117 */
     A                                      TEXT('Footer')
     A                                      OVERLAY
     A*/*S01187*/                      23  2'Press F3 to end, F5 to re fre-
     A                                 23  2'Press F3 to end, F5 to refre-
     A                                      sh, Rollup/Rolldown for further det-
     A                                      ails'
     A*
     A** Message subfile record
     A*                                                        /* S01117 */
     A*****     R DL3240S0                  TEXT('Message subfile')
     A          R TM3240S0                  TEXT('Message subfile')
     A*                                                        /* S01117 */
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDMGKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A** Message subfile control record
     A*                                                        /* S01117 */
     A*****     R DL3240C0                  TEXT('Message subfile control')
     A*****                                 SFLCTL(DL3240S0)
     A          R TM3240C0                  TEXT('Message subfile control')
     A                                      SFLCTL(TM3240S0)
     A*                                                        /* S01117 */
     A                                      SFLSIZ(15)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A  46                                  SFLDSPCTL
     A                                      SFLINZ
     A  40                                  SFLEND
     A                                      OVERLAY
     A            DDPGMQ                    SFLPGMQ
     A*
     A** Enquiry subfile record
     A*                                                        /* S01117 */
     A*****     R DL3240S1                  TEXT('Enquiry subfile')
     A          R TM3240S1                  TEXT('Enquiry subfile')
     A*                                                        /* S01117 */
     A                                      SFL
     A            DDCCY          3A  O  5  3TEXT('Currency')
     A                                      DSPATR(HI)
     A            DDNBI         21A  O  5 11TEXT('Net balance/Interest')
     A                                      DSPATR(HI)
     A            DDAVE         12A  O  5 34TEXT('Ave rate')
     A                                      DSPATR(HI)
     A            DDMKT         12A  O  5 51TEXT('Mkt rate')
     A                                      DSPATR(HI)
     A            DDPL          13A  O  5 66TEXT('P/L units')
     A                                      DSPATR(HI)
     A*
     A** Enquiry subfile control record
     A*                                                        /* S01117 */
     A*****     R DL3240C1                  TEXT('Enquiry subfile control')
     A*****                                 SFLCTL(DL3240S1)
     A          R TM3240C1                  TEXT('Enquiry subfile control')
     A                                      SFLCTL(TM3240S1)
     A*                                                        /* S01117 */
     A  35                                  SFLDSP
     A  36                                  SFLDSPCTL
     A N35N36                               SFLCLR
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0015)
     A  30                                  SFLEND
     A                                      OVERLAY
     A                                      PUTOVR
