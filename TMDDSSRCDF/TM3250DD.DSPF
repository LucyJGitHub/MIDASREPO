      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas TM MM Cost to close enq - all currencies')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Treasury Management Module
     A*                                                               *
     A*  DSPF/TM3250DD - MM COST TO CLOSE ENQUIRY - ALL CURRENCIES    *
     A*                  (FORMERLY DL3250DD)                          *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. E39162             DATE 18JUN92               *
     A*  PREV AMEND NO. S01117             DATE 26NOV90               *
     A*                 S01253             DATE 17APR90               *
     A*                 S01199             DATE 28FEB90               *
     A*                 E90072             DATE 31DEC89               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  E39162  -  CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                *
     A*  S01117  -  ALL PROGRAMS/FILES SPECIFIC TO TREASURY MGMT.     *
     A*             RENAMED TO HAVE 'TM' PREFIX                       *
     A*             COPYRIGHT STATEMENT CHANGED                       *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*  S01199  -  HELP SYSTEM.                                  *
     A*  E90072  -  *IN90 is already used in the program, so it       *
     A*             has been replaced with *IN11 to avoid confusion.  *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A* INDICATORS TO BE RECEIVED FROM THE SCREEN - COMMANDS          *
     A*                                                               *
     A*     01   -  EXIT PROGRAM                                      *
     A*     05   -  REFRESH SCREEN                                    *
     A*     10   -  HELP KEY HAS BEEN PRESSED                         *
     A*     25   -  ROLLUP REQUESTED                                  *
     A*     26   -  ROLLDOWN REQUESTED                                *
     A*                                                               *
     A* INDICATORS TO CONTROL THE ENQUIRY SUBFILE                     *
     A*                                                               *
     A*     30   -  ACTIVATE SUBFILE END                              *
     A*     35   -  ACTIVATE SUBFILE DISPLAY                          *
     A*     36   -  ACTIVATE SUBFILE DISPLAY CONTROL                  *
     A*     (N35N36   -  ACTIVATE SUBFILE CLEAR)                      *
     A*                                                               *
     A* INDICATORS TO CONTROL THE MESSAGE SUBFILE                     *
     A*                                                               *
     A*     40   -  ACTIVATE SUBFILE END                              *
     A*     46   -  ACTIVATE SUBFILE DISPLAY CONTROL                  *
     A*                                                               *
     A* INDICATORS TO REPORT ERRORS ON VALIDATION OF PROMPT SCREEN    *
     A*                                                               *
     A******90***-**ERROR*ON*CURRENCY*SELECTED******************E90072*
     A*     11   -  ERROR ON CURRENCY SELECTED                  E90072*
     A*     70   -  DISPLAY REVALUATION IN PROGRESS NARRATIVE         *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      INVITE
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A                                      CA03(01 'F3 to Exit')
     A                                      CA05(05 'F5 to Refresh')
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A*                                                        /* S01117 */
     A*                                                        /* S01117 */
     A*                                                    /*S01199*/
     A*                                                        /* S01117 */
     A*****     R DL3250F0                  TEXT('Title')
     A          R TM3250F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A                                  1  2'Ref:TM3250'
     A*                                                        /* S01117 */
     A*
     A                                  1 61TIME
     A                                  1 71'WSID'
     A            DDWSID         3A  O  1 76
     A*
     A                                  2  2'COST TO CLOSE - ALL CURRENCIES'
     A                                      DSPATR(UL HI)
     A                                  2 66'(000''s)'
     A*
     A            DDPOSN         5A  O  3 70TEXT('Position narr: start/end')
     A*
     A            DDTYPE         3A  O  4  2DSPATR(RI)
     A N70                              4 26'LAST REVALUATION'
     A N70        DDLREV         8A  O  4 45TEXT('Last revaluation')
     A*
     A  70                              4 26'** REVALUATION IN PROGRESS **'
     A                                      DSPATR(RI)
     A*
     A*
     A                                  6  3'CCY'
     A                                      DSPATR(UL)
     A                                  6 18'MM NET'
     A                                      DSPATR(UL)
     A                                  6 36'INTEREST'
     A                                      DSPATR(UL)
     A                                  6 56'P/L - UNITS'
     A                                      DSPATR(UL)
     A*
     A*
     A*                                                        /* S01117 */
     A*****     R DL3250F1                  TEXT('Command Keys Text')
     A          R TM3250F1                  TEXT('Command Keys Text')
     A*                                                        /* S01117 */
     A                                      OVERLAY
     A*
     A                                 23  2'Press F3 to end,'
     A                                 23 22'F5 to refresh,'
     A                                 23 40'Rollup/Rolldown for'
     A                                 23 60'further details.'
     A*
     A*
     A*                                                        /* S01117 */
     A*****     R DL3250S0                  TEXT('Message Subfile')
     A          R TM3250S0                  TEXT('Message Subfile')
     A*                                                        /* S01117 */
     A*
     A* Message Subfile
     A*
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDMGKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A*
     A*                                                        /* S01117 */
     A*****     R DL3250C0                  TEXT('Message Subfile Control')
     A          R TM3250C0                  TEXT('Message Subfile Control')
     A*                                                        /* S01117 */
     A*
     A* Message Subfile Control
     A*
     A*                                                        /* S01117 */
     A*****                                 SFLCTL(DL3250S0)
     A                                      SFLCTL(TM3250S0)
     A*                                                        /* S01117 */
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A  46                                  SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A  40                                  SFLEND
     A                                      OVERLAY
     A            DDPGMQ                    SFLPGMQ
     A*
     A*
     A*                                                        /* S01117 */
     A*****     R DL3250S1                  TEXT('Enquiry Subfile')
     A          R TM3250S1                  TEXT('Enquiry Subfile')
     A*                                                        /* S01117 */
     A*
     A* Subfile Record Format and a select field
     A*
     A                                      SFL
     A            DDCCY          3A     8  3TEXT('Currency')
     A                                      DSPATR(HI)
     A            DDNET         11A     8 18TEXT('MM Net')
     A                                      DSPATR(HI)
     A            DDINTR        11A     8 36TEXT('Interest')
     A                                      DSPATR(HI)
     A            DDPL          13A     8 56TEXT('P/L units')
     A                                      DSPATR(HI)
     A*
     A*
     A*                                                        /* S01117 */
     A*****     R DL3250C1                  TEXT('Enquiry Subfile Control')
     A          R TM3250C1                  TEXT('Enquiry Subfile Control')
     A*                                                        /* S01117 */
     A*
     A* Enquiry Subfile Control and prompts
     A*
     A*                                                        /* S01117 */
     A*****                                 SFLCTL(DL3250S1)
     A                                      SFLCTL(TM3250S1)
     A*                                                        /* S01117 */
     A  35                                  SFLDSP
     A  36                                  SFLDSPCTL
     A N35N36                               SFLCLR
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0012)
     A  30                                  SFLEND
     A                                      OVERLAY
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A*
     A* Prompt
     A*
     A                                 21 11'INCLUDED DEAL TYPES'
     A            DDDLTP         3A  B 21 32TEXT('Deal type selected')
     A******                                                   E90072
     A*N90**                                DSPATR(HI)
     A**90**                                DSPATR(RI PC)
     A N11                                  DSPATR(HI)
     A  11                                  DSPATR(RI PC)
     A******                                                   E90072
