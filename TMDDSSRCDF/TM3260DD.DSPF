      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas TM MM CCY exposure against limits enquiry')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Treasury Management Module
     A*                                                               *
     A*  TM3260DD - DISPLAY FILE FOR PGM TM3260                       *
     A*             (MM CURRENCY EXPOSURE AGAINST LIMITS ENQUIRY)     *
     A*                  (FORMERLY DL3260DD)
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. E39162             DATE 18JUN92               *
     A*                 S01280             DATE 08FEB91               *
     A*                 S01117             DATE 22NOV90               *
     A*                 S01253             DATE 17APR90               *
     A*                 S01199             DATE 28FEB90               *
     A*                 E19848             DATE 08NOV89               *
     A*                                                               *
     A*****************************************************************
     A*  MD046248 - Finastra Rebranding                               *
     A*  E39162  -  CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                *
     A*  S01280  -  REPLACE PUTOVR TO PREVENT SCREEN FLASH            *
     A*          -  TIME NOT BEING UPDATED WHEN SCREEN REFRESHED      *
     A*                                                               *
     A*  S01117  -  ALL PROGRAMS/FILES SPECIFIC TO TREASURY MGMT.     *
     A*             RENAMED TO HAVE 'TM' PREFIX                       *
     A*             COPYRIGHT STATEMENT CHANGED                       *
     A*                                                               *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*                                                               *
     A*  S01199  -  HELP SYSTEM.                                  *
     A*                                                               *
     A*  E19848  -  CHANGE TO HIGHLIGHT THE NET POSITION WHEN EXCESS  *
     A*             POSITION OVER LIMIT                               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A**  Indicator Function Summary                                  *
     A*                                                               *
     A* ID F  C  H  L    FUNCTION OF INDICATORS                       *
     A*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~*
     A*                                                               *
     A* INDICATORS TO BE RECEIVED FROM THE SCREEN - COMMANDS          *
     A*                                                               *
     A*     01   -  EXIT PROGRAM                                      *
     A*     05   -  REFRESH SCREEN                                    *
     A*     10   -  HELP KEY HAS BEEN PRESSED                         *
     A*     25   -  ROLLUP REQUESTED                                  *
     A*     26   -  ROLLDOWN REQUESTED                                *
     A*                                                               *
     A*     27   -  INHIBIT ROLL/REFRESH/SELECT KEYS & TEXT/ENQ.TITLES*
     A*                                                               *
     A* INDICATORS TO CONTROL THE ENQUIRY SUBFILE                     *
     A*                                                               *
     A*     30   -  ACTIVATE SUBFILE END                              *
     A*     35   -  ACTIVATE SUBFILE DISPLAY                          *
     A*     (N35 -  ACTIVATE SUBFILE CLEAR)                           *
     A*     36   -  ACTIVATE SUBFILE DISPLAY CONTROL                  *
     A*                                                               *
     A* INDICATORS TO CONTROL THE MESSAGE SUBFILE                     *
     A*                                                               *
     A*     40   -  ACTIVATE SUBFILE END                              *
     A*     46   -  ACTIVATE SUBFILE DISPLAY CONTROL                  *
     A*                                                               *
     A* INDICATOR TO SHOW THAT LIMIT AMOUNTS HAVE BEEN EXCEEDED       *
     A*                                                               *
     A*     85   -  LIMIT EXCEEDED                                    *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A  36                                  INVITE
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A                                      CA03(01 'F3 to Exit')
     A                                      CA05(05 'F5 to Refresh')
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A*                                                    /*S01199*/
     A*                                                        /* S01117 */
     A*                                                        /* S01117 */
     A*                                                    /*S01199*/
     A*                                                        /* S01117 */
     A*****     R DL3260F0
     A*****                                 TEXT('TITLE')
     A          R TM3260F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*                                                        /* S01117 */
     A                                      OVERLAY
     A*                                                        /* S01280 */
     A*****                                 PUTOVR
     A*                                                        /* S01280 */
     A*                                                               /*S01280*/
     A                                      PUTOVR
     A*                                                               /*S01280*/
     A*                                                        /* S01117 */
     A*****                             1  2'Ref: DL3260'
     A                                  1  2'Ref: TM3260'
     A*                                                        /* S01117 */
     A*                                                               /*S01280*/
     A***************                   1 61TIME
     A            DDTIME         6  0O  1 61EDTWRD('  :  :  ')
     A                                      OVRDTA
     A*                                                               /*S01280*/
     A                                  1 71'WSID'
     A            DDWSID         3A  O  1 76
     A                                  2 66'(000''S)'
     A*                                                        /* S01280 */
     A*****       DDSTEN         5A  O  3 68OVRDTA
     A*                                                               /*S01280*/
     A************DDSTEN         5A  O  3 68
     A            DDSTEN         5A  O  3 68OVRDTA
     A*                                                               /*S01280*/
     A*                                                        /* S01280 */
     A                                  5  6'CCY'
     A                                      DSPATR(UL)
     A                                  2  2'MM CURRENCY EXPOSURE AGAINST LIMIT-
     A                                      S'
     A                                      DSPATR(UL HI)
     A                                  5 21'NET POSITION'
     A                                      DSPATR(UL)
     A                                  5 41'LIMIT'
     A                                      DSPATR(UL)
     A*                                                        /* S01117 */
     A*****     R DL3260F1
     A          R TM3260F1
     A*                                                        /* S01117 */
     A                                      TEXT('Footer')
     A                                      OVERLAY
     A                                 24  2'F3 to Exit,'
     A                                 24 18'F5 to Refresh,'
     A                                 24 38'Rollup/Rolldown for'
     A                                 24 58'further details'
     A*                                                        /* S01117 */
     A*****     R DL3260S0                  SFL
     A          R TM3260S0                  SFL
     A*                                                        /* S01117 */
     A*
     A* Message Subfile
     A*
     A                                      SFLMSGRCD(23)
     A                                      TEXT('Message Subfile')
     A            DDMGKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*                                                        /* S01117 */
     A*****     R DL3260C0                  SFLCTL(DL3260S0)
     A          R TM3260C0                  SFLCTL(TM3260S0)
     A*                                                        /* S01117 */
     A*
     A* Message Subfile Control
     A*
     A                                      TEXT('Message Subfile Control')
     A                                      OVERLAY
     A                                      SFLDSP
     A  46                                  SFLDSPCTL
     A                                      SFLINZ
     A  40                                  SFLEND
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0001)
     A            DDPGMQ                    SFLPGMQ
     A*                                                        /* S01117 */
     A*****     R DL3260S1                  SFL
     A          R TM3260S1                  SFL
     A*                                                        /* S01117 */
     A*
     A* Subfile Record Format and a select field
     A*
     A                                      TEXT('Enquiry Subfile')
     A            DDCCY          3A  O  7  6TEXT('CCY')
     A                                      DSPATR(HI)
     A            DDOPP         11A  O  7 21TEXT('NET POSITION')
     A*                                                                   E19848
     A* E19848 - HIGHLIGHT THE NET POSITION WHEN EXCESS LIMIT
     A*
     A  85                                  DSPATR(RI)
     A*                                                                   E19848
     A                                      DSPATR(HI)
     A            DDLIM         10A  O  7 41TEXT('LIMIT')
     A*                                                                   E19848
     A**85                                  DSPATR(RI)
     A*                                                                   E19848
     A                                      DSPATR(HI)
     A*                                                        /* S01117 */
     A*****     R DL3260C1                  SFLCTL(DL3260S1)
     A          R TM3260C1                  SFLCTL(TM3260S1)
     A*                                                        /* S01117 */
     A*
     A* Enquiry Subfile Control
     A*
     A                                      TEXT('Enquiry Subfile Control')
     A                                      OVERLAY
     A  35                                  SFLDSP
     A  36                                  SFLDSPCTL
     A N35N36                               SFLCLR
     A  30                                  SFLEND
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0015)
