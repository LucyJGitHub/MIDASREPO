      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas TM Profit and Loss - All currencies')
     A*            11:00:36    F7680FD     REL-R08M00  5714-UT1
     A*****************************************************************
     A*                                                               *
     A*  Midas - TREASURY MANAGEMENT MODULE
     A*                                                               *
     A*  DSPF/TM3270DD - PROFIT AND LOSS - ALL CURRENCIES             *
     A*                  (FORMERLY DL3270DD)                          *   S01117
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. E39162             DATE 18JUN92               *
     A*  PREV AMEND NO. S01280             DATE 13NOV90               *
     A*                 S01117             DATE 13NOV90               *
     A*                 S01253             DATE 17APR90               *
     A*                 S01199             DATE 28FEB90               *
     A*                 S01187             DATE 06JAN89               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  E39162  -  CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                *
     A*  S01280  -  TIME NOT BEING UPDATED IF SCREEN REFRESHED OR     *
     A*             NEW DATA INPUT                                    *
     A*  S01117  -  ALL PROGRAMS/FILES SPECIFIC TO TREASURY MGMT.     *
     A*             RENAMED TO HAVE 'TM' PREFIX                       *
     A*             COPYRIGHT STATEMENT CHANGED                       *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*  S01199  -  HELP SYSTEM.                                  *       S01199
     A*  S01187  -  RELEASE 1 TEST FIX                                *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A* INDICATORS TO BE RECEIVED FROM THE SCREEN - COMMANDS          *
     A*                                                               *
     A*     01   -  EXIT PROGRAM                                      *
     A*     05   -  REFRESH SCREEN                                    *
     A*     10   -  HELP KEY HAS BEEN PRESSED                         *
     A*     25   -  ROLLUP REQUESTED                                  *
     A*     26   -  ROLLDOWN REQUESTED                                *
     A*                                                               *
     A*     27   -  INHIBIT ROLL/REFRESH/SELECT KEYS & TEXT/ENQ.TITLES*
     A*     28   -  ACTIVATE THE OVRDTA KEYWORD FOR KEYS/TEXT & TITLES*
     A*                                                               *
     A* INDICATORS TO CONTROL THE ENQUIRY SUBFILE                     *
     A*                                                               *
     A*     30   -  ACTIVATE SUBFILE END                              *
     A*     35   -  ACTIVATE SUBFILE DISPLAY                          *
     A*     (N35 -  ACTIVATE SUBFILE CLEAR)                           *
     A*                                                               *
     A* INDICATORS TO CONTROL THE MESSAGE SUBFILE                     *
     A*                                                               *
     A*     40   -  ACTIVATE SUBFILE END                              *
     A*     46   -  ACTIVATE SUBFILE DISPLAY CONTROL                  *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A  36                                  INVITE
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A                                      CA03(01 'F3 to Exit')
     A**********                            CA12(05 'F12 to Refresh') /*S01187*/
     A                                      CA05(05 'F5 to Refresh')
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A*                                                    /*S01199*/
     A*                                                             /* S01117 */
     A*                                                             /* S01117 */
     A*                                                    /*S01199*/
     A*                                                             /* S01117 */
     A*****     R DL3270F0                  TEXT('Title')
     A          R TM3270F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*                                                             /* S01117 */
     A                                      OVERLAY
     A*                                                             /* S01280 */
     A**********                            PUTOVR
     A*                                                             /* S01280 */
     A*
     A*                                                             /* S01117 */
     A*****                             1  2'Ref: DL3270'
     A                                  1  2'Ref: TM3270'
     A*                                                             /* S01117 */
     A                                  1 61TIME
     A                                  1 71'WSID'
     A            DDWSID         3A     1 76
     A                                  2  2'PROFIT AND LOSS - ALL CURRENCIES'
     A                                      DSPATR(UL)
     A                                  2 39'LAST REVAL.'
     A                                      DSPATR(HI)
     A                                  2 66'(UNITS)'
     A            DDTIM          8A  O  2 51
     A                                      DSPATR(HI)
     A*
     A*                                                        /* S01280 */
     A*****       DDSTEN         5   O  3 68OVRDTA
     A            DDSTEN         5   O  3 68
     A*                                                        /* S01280 */
     A*
     A* Subfile Titles
     A*
     A                                  4  3'FX'
     A                                      DSPATR(UL)
     A                                  4  9'OPEN POSN'
     A                                      DSPATR(UL)
     A                                  5  2'CCY'
     A                                      DSPATR(UL)
     A                                  5  9'IN 000''S'
     A                                      DSPATR(UL)
     A                                  5 27'COR P/L'
     A                                      DSPATR(UL)
     A                                  5 44'SHORT-----------FWD---------TOTAL'
     A                                      DSPATR(UL)
     A*
     A*                                                             /* S01117 */
     A*****     R DL3270F1                  TEXT('Footer')
     A          R TM3270F1                  TEXT('Footer')
     A*                                                             /* S01117 */
     A                                      OVERLAY
     A*
     A            DDRVL         23   O 23 23
     A  98                                  DSPATR(RI)
     A*
     A                                 24  2'F3 to Exit,'
     A***********                      24 18'F12 to Refresh,'         /*S01187*/
     A                                 24 18'F5 to Refresh,'
     A                                 24 38'Rollup/Rolldown for'
     A                                 24 58'further details'
     A*
     A*                                                             /* S01117 */
     A*****     R DL3270F2                  TEXT('Totals')
     A          R TM3270F2                  TEXT('Totals')
     A*                                                             /* S01117 */
     A                                      OVERLAY
     A                                      PUTOVR
     A*
     A                                 20 13'TOTALS'
     A            DDCTOT        13A  O 20 22OVRDTA
     A                                      DSPATR(HI)
     A            DDSTOT        13A  O 20 37OVRDTA
     A                                      DSPATR(HI)
     A            DDBFTT        13A  O 20 51OVRDTA
     A                                      DSPATR(HI)
     A            DDBTTT        13A  O 20 65OVRDTA
     A                                      DSPATR(HI)
     A*
     A*                                                             /* S01117 */
     A*****     R DL3270S0                  TEXT('Message Subfile')
     A          R TM3270S0                  TEXT('Message Subfile')
     A*                                                             /* S01117 */
     A*
     A* Message Subfile
     A*
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDMGKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A*
     A*                                                             /* S01117 */
     A*****     R DL3270C0                  TEXT('Message Subfile Control')
     A          R TM3270C0                  TEXT('Message Subfile Control')
     A*                                                             /* S01117 */
     A*
     A* Message Subfile Control
     A*
     A*                                                             /* S01117 */
     A*****                                 SFLCTL(DL3270S0)
     A                                      SFLCTL(TM3270S0)
     A*                                                             /* S01117 */
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A  46                                  SFLDSPCTL
     A                                      SFLINZ
     A  40                                  SFLEND
     A                                      OVERLAY
     A            DDPGMQ                    SFLPGMQ
     A*
     A*
     A*                                                             /* S01117 */
     A*****     R DL3270S1                  TEXT('Enquiry Subfile')
     A          R TM3270S1                  TEXT('Enquiry Subfile')
     A*                                                             /* S01117 */
     A*
     A* Subfile Record Format and a select field
     A*
     A                                      SFL
     A*
     A            DDCCY          3A     7  2TEXT('FX CCY')
     A                                      DSPATR(HI)
     A            DDOPP         11A     7  8TEXT('OPEN POSN IN 000`S')
     A                                      DSPATR(HI)
     A            DDCOR         13A     7 22TEXT('COR P/L')
     A                                      DSPATR(HI)
     A            DDSHT         13A     7 37TEXT('SHORT')
     A                                      DSPATR(HI)
     A            DDFWD         13A     7 51TEXT('FWD')
     A                                      DSPATR(HI)
     A            DDTOT         13A     7 65TEXT('TOTAL')
     A                                      DSPATR(HI)
     A*
     A*
     A*                                                             /* S01117 */
     A*****     R DL3270C1                  TEXT('Enquiry Subfile Control')
     A          R TM3270C1                  TEXT('Enquiry Subfile Control')
     A*                                                             /* S01117 */
     A*
     A* Enquiry Subfile Control
     A*
     A*                                                             /* S01117 */
     A*****                                 SFLCTL(DL3270S1)
     A                                      SFLCTL(TM3270S1)
     A*                                                             /* S01117 */
     A  35                                  SFLDSP
     A  36                                  SFLDSPCTL
     A N35N36                               SFLCLR
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0012)
     A  30                                  SFLEND
     A                                      OVERLAY
     A                                      PUTOVR
