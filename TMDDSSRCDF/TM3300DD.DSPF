      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas TM Maintain Cross currency posit. kept file')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Treasury Management
     A*                                                               *
     A*   TM3300DD - Cross currency positions kept file maintenance   *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*   LAST AMEND NO. XXXXXX            DATE XX/XXX/XX             *
     A*
     A*****************************************************************
     A*                                                              *
     A*   NEW PROGRAM FOR S01277 - MAINTAIN CROSS CCY POSITIONS      *
     A*                                                              *
     A****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A  30                                  CF10
     A*
     A          R TM3300S1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDMGKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A*
     A          R TM3300S0
     A                                      TEXT('MAIN  SCREEN')
     A                                      SFLCTL(TM3300S1)
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A N42                                  SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A  10                                  SFLEND
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      CA15(15)
     A*****                                 PUTOVR
     A                                      OVERLAY
     A            DDPGMQ                    SFLPGMQ
     A                                  1 28'Cross Currencies File Maintenance'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT         7A  O  1 71
     A                                  2 71TIME
     A                                      EDTWRD('0 :  :  ')
     A                                  1 13'REF TM3300 '
     A            USER          10A  O  2  2
     A                                  1  2'WID'
     A            WSID           3A  O  1  6
     A                                  9  4'Primary Currency:'
     A                                  6  4'Action:'
     A            DDACTN         1   B  6 14TEXT('ACTION CODE')
     A  20                                  DSPATR(PC RI)
     A                                  6 22'(Insert, Enquire or Delete)'
     A            DDPCCY         3   B  9 28TEXT('Primary Ccy Code')
     A  21                                  DSPATR(RI PC)
     A            DDPTXT        21   O  9 38
     A                                 12  4'Secondary Currency:'
     A            DDSCCY         3   B 12 28TEXT('Secondary Ccy Code')
     A  22                                  DSPATR(RI PC)
     A            DDSTXT        21   O 12 38
      *
     A          R TM3300D0                  TEXT('SCREEN FOOTER')
     A                                      OVERLAY
     A*****                                 PUTOVR
     A                                      BLINK
     A                                 23  9'F3 to exit'
     A                                 23 28'F12 initial screen'
     A N30                             23 55'F15 file enquiry'
     A  30                             23 55'F10 confirm delete'
     A*
     A          R TM3300S3                  TEXT('Select Subfile')
     A                                      SFL
     A            DDOPT          1   B  8  9TEXT('Subfile Option')
     A                                      DSPATR(UL)
     A            DDPCCY         3   O  8 26TEXT('Primary Ccy Code')
     A                                      DSPATR(HI)
     A*
     A*****       DDSCCY        10  0O  8 35TEXT('CCURENCY ')
     A            DDSCCY         3   O  8 47TEXT('Secondary Ccy Code')
     A                                      DSPATR(HI)
     A*
     A            DDDLUP         2  0H      TEXT('Day of last update')
     A*
     A            DDMLUP         3   H      TEXT('Month of last update')
     A*
     A            DDYLUP         2  0H      TEXT('Year of last update')
     A*
     A            DDTLUP         6  0H      TEXT('Time of last update')
     A*
     A          R TM3300S2                  TEXT('Select Screen')
     A                                      SFLCTL(TM3300S3)
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N41                                  SFLDSP
     A N41                                  SFLDSPCTL
     A  41                                  SFLCLR
     A  70                                  SFLEND
     A N70                                  ROLLUP(50)
     A                                      PUTOVR
     A                                      BLINK
     A                                      CA03(03)
     A                                      CA12(12)
     A*
     A                                  1 28'Cross Currencies File Maintenance'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                  2 71TIME
     A                                      EDTWRD('0 :  :  ')
     A                                  1 13'REF TM3300 '
     A            USER          10A  O  2  2
     A                                  1  2'WID'
     A            WSID           3A  O  1  6
     A                                  1  2'Ref: TM3300'
     A*
     A                                  6  7'Select'
     A                                  6 19'Primary Currency'
     A                                  6 39'Secondary Currency'
     A            DDSFRN         4  0H      SFLRCDNBR
     A*
     A          R TM3300D1                  TEXT('SELECT SCREEN FOOTER')
     A                                      OVERLAY
     A                                 23  2'Rollup/Rolldown for further'
     A                                    +1'records'
     A                                 24  1'F3-End F12-Previous Screen'
