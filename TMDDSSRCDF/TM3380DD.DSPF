      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas TM FF transaction details enquiry')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Treasury Management Module                *
     A*                                                               *
     A*     TM3380DD - Interest Rate Futures Transaction Enquiry      *   S01117
     A*                (FORMERLY DL3380DD)                            *   S01117
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CFF004             Date 07Oct96               *
     A*                                                               *
     A*  This was created for S01280 - Include Interest Rate Futures  *
     A*  into MM forward book. It is a close copy of PM2090DD. 4DEC90 *
     A*****************************************************************
     A*                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  CFF004  -  Increase in size of Price Fields                  *
     A*                                                               *
     A*****************************************************************
     A*
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CA03
     A                                      CA12
     A          R TM3380F1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                  1  2'DL3380'
     A                                  1 21'INTEREST RATE FUTURES DETAILS ENQU-
     A                                      IRY'
     A                                      DSPATR(HI UL)
     A                                  1 66'WID'
     A            WSID          10A  O  1 70TEXT('WORKSTATION ID')
     A*
     A            DDSTAT         7A  O  3 71TEXT('F&O INSTRUM STATUS')
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A*
     A                                  3  2'Transaction reference'
     A            DDTNBR         6A  O  3 24TEXT('TRANSACTION NUMBER')
     A                                      DSPATR(HI)
     A*
     A                                  5  2'Purchase or sale'
     A            DDTRTY         1A  O  5 21TEXT('TRANSACTION TYPE')
     A                                      DSPATR(HI)
     A                                  5 27'Number of contracts'
     A            DDNUCO         5A  O  5 47TEXT('NUMBER OF CONTRACTS')
     A                                      DSPATR(HI)
     A*
     A                                  6  2'Market'
     A            DDMARK         2A  O  6 12TEXT('MARKET')
     A                                      DSPATR(HI)
     A                                  6 17'Instrument'
     A            DDINST         3A  O  6 31TEXT('INSTRUMENT')
     A                                      DSPATR(HI)
     A                                  6 43'Month'
     A            DDMTHN         3A  O  6 50TEXT('MONTH')
     A                                      DSPATR(HI)
     A                                  6 56'Year'
     A            DDYRNO         2A  O  6 62TEXT('YEAR')
     A                                      DSPATR(HI)
     A*
     A                                  7  2'Contract price'
     A*                                                      Start of /*CFF004*/
     A*********** DDCOPR        12A  O  7 19TEXT('CONTRACT PRICE')
     A***********                           DSPATR(HI)
     A***********                       7 33'Transaction date'
     A*********** DDTRSD         7A  O  7 50TEXT('TRANSACTION DATE')
     A            DDCOPR        16A  O  7 19TEXT('CONTRACT PRICE')
     A                                      DSPATR(HI)
     A                                  7 37'Transaction date'
     A            DDTRSD         7A  O  7 54TEXT('TRANSACTION DATE')
     A*                                                      End of   /*CFF004*/
     A                                      DSPATR(HI)
     A*
     A                                  8  2'Broker'
     A            DDBOCO         6A  O  8 19TEXT('BROKER CODE')
     A                                      DSPATR(HI)
     A*                                                      Start of /*CFF004*/
     A***********                       8 33'Book'
     A*********** DDBOKC         2A  O  8 39TEXT('BOOK CODE')
     A***********                           DSPATR(HI)
     A***********                       8 44'Branch'
     A*********** DDBRCD         3A  O  8 52TEXT('BRANCH CODE OF TRADE')
     A                                  8 37'Book'
     A            DDBOKC         2A  O  8 43TEXT('BOOK CODE')
     A                                      DSPATR(HI)
     A                                  8 48'Branch'
     A            DDBRCD         3A  O  8 56TEXT('BRANCH CODE OF TRADE')
     A*                                                      End of   /*CFF004*/
     A                                      DSPATR(HI)
     A                                  9  2'Narrative'
     A            DDTNAR        25A  O  9 19TEXT('TRANSACTION NARRATIVE')
     A                                      DSPATR(HI)
     A*
     A                                  9 56'To be left open'
     A            DDTBLO         1A  O  9 74TEXT('TO BE LEFT OPEN IND')
     A                                      DSPATR(HI)
     A*
     A                                 10  2'Linked reference'
     A            DDLNKR         6A  O 10 19TEXT('LINKED REFERENCE')
     A                                      DSPATR(HI)
     A*
     A                                 11  2'Customer no.'
     A            DDCNUM         6A  O 11 15TEXT('CUSTOMER NUMBER')
     A                                      DSPATR(HI)
     A                                 11 23'Name'
     A            DDCRNM        10A  O 11 28TEXT('CUSTOMER REPORT NAME')
     A                                      DSPATR(HI)
     A*
     A                                 14 30'BROKER                CUSTOMER'
     A*
     A                                 15  2'Commission code: Std/Same'
     A            DDBCMC         2A  O 15 30TEXT('BROKER COMMISSION CODE')
     A                                      DSPATR(HI)
     A            DDBCMS         2A  O 15 34TEXT('BROKER COMMISSN CODE-SAME DY')
     A                                      DSPATR(HI)
     A            DDCCMC         2A  O 15 52TEXT('CUSTOMER COMMISSION CODE')
     A                                      DSPATR(HI)
     A            DDCCMS         2A  O 15 56TEXT('CUSTOMER COMMN CODE-SAME DY')
     A                                      DSPATR(HI)
     A*
     A                                 16  2'Commission amount'
     A            DDBCMA        14A  O 16 30TEXT('BROKER COMMISSION AMOUNT')
     A                                      DSPATR(HI)
     A            DDCCMA        14A  O 16 52TEXT('CUSTOMER COMMISSION AMOUNT')
     A                                      DSPATR(HI)
     A*
     A                                 17  2'Charge code/amount'
     A            DDBCHC         2A  O 17 30TEXT('BROKER CHARGE CODE')
     A                                      DSPATR(HI)
     A            DDBCHA        14A  O 17 34TEXT('BROKER CHARGE AMOUNT')
     A                                      DSPATR(HI)
     A            DDCCHC         2A  O 17 52TEXT('CUSTOMER CHRG CODE')
     A                                      DSPATR(HI)
     A            DDCCHA        14A  O 17 56TEXT('CUSTOMER CHARGE AMOUNT')
     A                                      DSPATR(HI)
     A*
     A                                 18  2'Initial margin'
     A            DDBMAR        14A  O 18 30TEXT('BROKER MARGIN')
     A                                      DSPATR(HI)
     A            DDCMAR        14A  O 18 52TEXT('CUSTOMER MARGIN')
     A                                      DSPATR(HI)
     A*
     A                                 19  2'Settlement type'
     A            DDBSLT         2A  O 19 30TEXT('BROKER SETTLEMENT TYPE')
     A                                      DSPATR(HI)
     A            DDCSLT         2A  O 19 52TEXT('CUSTOMER SETTLEMENT TYPE')
     A                                      DSPATR(HI)
     A*
     A                                 20  2'Settlement account'
     A**Start of change CGL029
     A**********  DDBSLA        12A  O 20 30TEXT('BROKER SETTLEMENT ACCOUNT')                 CGL029
     A            DDBSLA        18A  O 20 30TEXT('BROKER SETTLEMENT ACCOUNT')
     A**End of change CGL029
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  DDCSLA        12A  O 20 52TEXT('CUSTOMER SETTLEMENT ACCOUNT')               CGL029
     A            DDCSLA        18A  O 20 52TEXT('CUSTOMER SETTLEMENT ACCOUNT')
     A**End of change CGL029
     A                                      DSPATR(HI)
     A*
     A                                 21  2'Counterparty nostro'
     A            DDBCPN         8A  O 21 30TEXT('BROKER COUNTERPARTY NOSTRO')
     A                                      DSPATR(HI)
     A            DDCCPN         8A  O 21 52TEXT('CUSTOMER COUNTERPARTY NOSTRO')
     A                                      DSPATR(HI)
     A*
     A                                 22  2'For account of'
     A            DDBFAO        10A  O 22 30TEXT('BROKER FOR ACCOUNT OF')
     A                                      DSPATR(HI)
     A            DDCFAO        10A  O 22 52TEXT('CUSTOMER FOR ACCOUNT OF')
     A                                      DSPATR(HI)
     A*
     A                                 24  2'F3=Exit  F12=Previous Screen'
     A*
     A          R TM3380F2
     A*
     A                                  1  2TIME
     A                                  1 21'INTEREST RATE FUTURES DETAILS ENQU-
     A                                      IRY'
     A                                      DSPATR(HI UL)
     A                                  1 66'WID'
     A            WSID          10A  O  1 70TEXT('WORKSTATION ID')
     A*
     A                                  3  2'Transaction reference'
     A            DDTNBR         6A  O  3 24TEXT('TRANSACTION NUMBER')
     A                                      DSPATR(HI)
     A            DDSTAT         7A  O  3 71TEXT('F&O INSTRUM STATUS')
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A*
     A                                  5  2'Purchase or sale'
     A            DDTRTY         1A  O  5 21TEXT('TRANSACTION TYPE')
     A                                      DSPATR(HI)
     A                                  5 27'Number of contracts'
     A            DDNUCO         5A  O  5 47TEXT('NUMBER OF CONTRACTS')
     A                                      DSPATR(HI)
     A*
     A                                  6  2'OTC Type'
     A            DDOTCT         5A  O  6 12TEXT('OTC TYPE')
     A                                      DSPATR(HI)
     A*
     A                                  7  2'Contract price'
     A*                                                      Start of /*CFF004*/
     A*********** DDCOPR        12A  O  7 19TEXT('CONTRACT PRICE')
     A***********                           DSPATR(HI)
     A***********                       7 33'Transaction date'
     A*********** DDTRSD         7A  O  7 50TEXT('TRANSACTION DATE')
     A            DDCOPR        16A  O  7 19TEXT('CONTRACT PRICE')
     A                                      DSPATR(HI)
     A                                  7 37'Transaction date'
     A            DDTRSD         7A  O  7 54TEXT('TRANSACTION DATE')
     A*                                                      End of   /*CFF004*/
     A                                      DSPATR(HI)
     A*
     A                                  8  2'Book'
     A            DDBOKC         2A  O  8  8TEXT('BOOK CODE')
     A                                      DSPATR(HI)
     A                                  8 19'Branch'
     A            DDBRCD         3A  O  8 28TEXT('BRANCH CODE OF TRADE')
     A                                      DSPATR(HI)
     A*
     A                                  9  2'Narrative'
     A            DDTNAR        25A  O  9 19TEXT('TRANSACTION NARRATIVE')
     A                                      DSPATR(HI)
     A                                  9 56'To be left open'
     A            DDTBLO         1A  O  9 74TEXT('TO BE LEFT OPEN IND')
     A                                      DSPATR(HI)
     A*
     A                                 10  2'Linked reference'
     A            DDLNKR         6A  O 10 19TEXT('LINKED REFERENCE')
     A                                      DSPATR(HI)
     A*
     A                                 11  2'Customer no.'
     A            DDCNUM         6A  O 11 15TEXT('CUSTOMER NUMBER')
     A                                      DSPATR(HI)
     A                                 11 23'Name'
     A            DDCRNM        10A  O 11 28TEXT('CUSTOMER REPORT NAME')
     A                                      DSPATR(HI)
     A*
     A                                 14 52'CUSTOMER'
     A*
     A                                 15  2'Commission code: Std/Same'
     A            DDCCMC         2A  O 15 52TEXT('CUSTOMER COMMISSION CODE')
     A                                      DSPATR(HI)
     A            DDCCMS         2A  O 15 56TEXT('CUSTOMER COMMN CODE-SAME DY')
     A                                      DSPATR(HI)
     A*
     A                                 16  2'Commission amount'
     A            DDCCMA        14A  O 16 52TEXT('CUSTOMER COMMISSION AMOUNT')
     A                                      DSPATR(HI)
     A*
     A                                 17  2'Charge code/amount'
     A            DDCCHC         2A  O 17 52TEXT('CUSTOMER CHRG CODE')
     A                                      DSPATR(HI)
     A            DDCCHA        14A  O 17 56TEXT('CUSTOMER CHARGE AMOUNT')
     A                                      DSPATR(HI)
     A*
     A                                 18  2'Initial margin'
     A            DDCMAR        14A  O 18 52TEXT('CUSTOMER MARGIN')
     A                                      DSPATR(HI)
     A*
     A                                 19  2'Settlement type'
     A            DDCSLT         2A  O 19 52TEXT('CUSTOMER SETTLEMENT TYPE')
     A                                      DSPATR(HI)
     A*
     A                                 20  2'Settlement account'
     A**Start of change CGL029
     A**********  DDCSLA        12A  O 20 52TEXT('CUSTOMER SETTLEMENT ACCOUNT')               CGL029
     A            DDCSLA        18A  O 20 52TEXT('CUSTOMER SETTLEMENT ACCOUNT')
     A**End of change CGL029
     A                                      DSPATR(HI)
     A*
     A                                 21  2'Counterparty nostro'
     A            DDCCPN         8A  O 21 52TEXT('CUSTOMER COUNTERPARTY NOSTRO')
     A                                      DSPATR(HI)
     A*
     A                                 22  2'For account of'
     A            DDCFAO        10A  O 22 52TEXT('CUSTOMER FOR ACCOUNT OF')
     A                                      DSPATR(HI)
     A*
     A                                 24  2'F3=Exit  F12=+
     A                                       Previous Screen'
