      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas TM File control report totals')
     A****************************************************************
     A*                                                              *
     A*  Midas - TREASURY MANAGEMENT (FX AND MM)                     *
     A*                                                              *
     A*     PRTF/TM3310AU - AUDIT REPORT FOR CROSS CURRENCIES FILE   *
     A*     LIST.                                                    *
     A*                                                              *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                              *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*     LAST AMEND NO. S01277 *CREATE     DATE   22APR91         *
     A*                                                              *
     A*--------------------------------------------------------------*
     A*                                                              *
     A*     NEW PROGRAM FOR S01277. MAINTAIN CROSS CCY POSITIONS     *
     A*                                                              *
     A****************************************************************
     A*
     A**  Indicator Function Summary
     A*
     A* ID F  C  H  L    FUNCTION OF INDICATORS
     A* ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
     A*       63         End of day processing.                        UT
     A*       64         Input cycle processing.                       UT
     A*
     A*
     A**  FILE LEVEL KEYWORDS
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A*
     A**  FORMAT FOR HEADERS.
     A          R TM3310A0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A*
     A                                     7'REFERENCE TM3310AU'
     A            RRTITL    R             37REFFLD(TITL)
     A                                   101'DATE'
     A            RRRUNA    R            106REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A*
     A            RRPRNM    R             31REFFLD(TTIT)
     A                                      SPACEA(1)
     A            RRPRUL    R             31REFFLD(TTIT)
     A                                      SPACEA(2)
     A*
     A**  FORMAT FOR DETAILS.
     A          R TM3310A1                  TEXT('DETAIL FORMAT')
     A*
     A N63                                 7'INPUT CYCLE'
     A N63        RRTIME    R             19REFFLD(TIME)
     A N63                                  SPACEA(3)
     A                                      EDTWRD('0 :  :  ')
     A  63                                 7'END OF DAY'
     A  63                                  SPACEA(3)
     A*
     A                                    63'CALCULATED'
     A                                      SPACEA(1)
     A*
     A                                    63'----------'
     A                                      SPACEA(2)
     A*
     A                                    21'NUMBER OF RECORDS ON FILE'
     A*
     A            RRCALC    R             65REFFLD(CALR)
     A                                      EDTWRD('  0 ')
     A*
     A                                    51'*** END OF REPORT ***'
     A                                      SPACEB(3)
     A*
     A**  DATABASE ERROR MESSAGE.
     A          R TM3310A3                  TEXT('DATABASE ERROR MSG')
     A*
     A                                    33'DATABASE ERROR'
     A                                      SKIPB(35)
     A            RRBASE    R             48REFFLD(DBASE)
     A                                    52'IN PROGRAM'
     A            RRPGM     R             63REFFLD(DBPGM)
     A                                      SPACEA(1)
     A*
     A                                    33'FILE'
     A            RRFILE    R             38REFFLD(DBFILE)
     A                                    46', KEY'
     A            RRKEY     R             52REFFLD(DBKEY)
     A*
