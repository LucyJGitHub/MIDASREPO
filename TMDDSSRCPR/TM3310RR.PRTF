      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Cross currencies - File list')
     A****************************************************************
     A*                                                              *
     A*  Midas - TREASURY MANAGEMENT (FX AND MM)          *
     A*                                                              *
     A*     TM3310 - LIST CROSS CURRENCIES FILE                      *
     A*                                                              *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                              *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*     LAST AMEND NO. XXXXXX             DATE   XX/XXX/XX       *
     A*                                                              *
     A****************************************************************
     A*                                                              *
     A*     NEW PROGRAM FOR S01277, MAINTAIN CROSS CCY POSITIONS     *
     A*                                                              *
     A****************************************************************
     A*
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A**  FORMAT FOR HEADINGS
     A          R TM3310R0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     7'REFERENCE TM3310RR'
     A            RRTITL    R             37REFFLD(TITL)
     A                                   100'DATE'
     A            RRRUNA    R            105REFFLD(RUNA)
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A*
     A                                    24'MIDAS DEALING ROOM SYSTEM '
     A                                    50'- CROSS CURRENCIES - '
     A                                    71'FILE LIST'
     A                                      SPACEA(1)
     A
     A                                    24'--------------------------'
     A                                    50'---------------------'
     A                                    71'---------'
     A                                      SPACEA(2)
     A
     A                                    30'Primary Currency'
     A                                    60'Secondary Currency'
     A                                      SPACEA(1)
     A
     A                                    30'----------------'
     A                                    60'------------------'
     A                                      SPACEA(2)
     A*
     A** FORMAT FOR DETAIL LINES.
     A          R TM3310R1                  TEXT('DETAIL OUTPUT FMT')
     A            RRPCCY    R             30REFFLD(PMCY)
     A            RRPCNM        14        34
     A            RRSCCY    R             60REFFLD(SDCY)
     A            RRSCNM        14        64
     A                                      SPACEA(2)
     A*
     A** FORMAT FOR FINAL LINE.                  *IN62 ON.
     A          R TM3310R2                  TEXT('END OF DATA FMT')
     A  62                                47'*** END OF REPORT ***'
     A*
     A** NO VALID DETAIL RECORDS FOUND TO PRINT. *IN62 NOT ON.
     A N62                                47'*** NO DETAILS TO'
     A N62                                  SKIPB(35)
     A N62                                65'REPORT ***'
     A*
     A**  DATABASE ERROR MESSAGE.
     A          R TM3310R3                  TEXT('DATABASE ERROR MSG')
     A                                    33'DATABASE ERROR'
     A                                      SKIPB(35)
     A            RRBASE    R             48REFFLD(DBASE)
     A                                    52'IN PROGRAM'
     A            RRPGM     R             63REFFLD(DBPGM)
     A                                      SPACEA(1)
     A*
     A                                    33'FILE'
     A            RRFILE    R             38REFFLD(DBFILE)
     A                                    46', KEY'
     A            RRKEY     R             52REFFLD(DBKEY)
     A*
