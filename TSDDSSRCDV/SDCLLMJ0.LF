      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas TS Customer limits joined limit types')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Standing Data Module
     A*                                                               *
     A*   LF/SDCLLMJ0 - CUSTOMER LIMITS BY CUSTOMER/ENTITY            *
     A*                 LIMITS JOINED TO LIMIT TYPES                  *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Last Amend No. CGL011             Date 20Sep99               *
      * Midas DBA 3.01 -----------------------------------------------*
      *  Prev Amend No. CDL006             Date 28Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 C00562             Date 02Aug93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CGL011 - Collateral Processing for Midas. Recompile.         *
      *  CDL006 - Fiduciary Dealing (Recompile).                      *
     A*   C00562 - File created for Trader Support because join       *
     A*            logical file LIMITJ has been amended with a        *
     A*            dubious looking 'fix' (BHF805). This file is a     *
     A*            copy of how LIMITJ used to look and so could be    *
     A*            removed if the fix is ever removed from LIMITJ     *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                            *
     A**************************************************************
     A*
     A                                      JDFTVAL
     A          R SDCLLMD0                  JFILE(SDCLLMPD +
     A                                      SDLIMTPD SDLIMTPD)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A**  JOIN FX LIMIT TYPE TO LIMIT TYPES FILE
     A*
     A          J                           JOIN(1 2)
     A                                      JFLD(DZFXLT A2LMTY)
     A*
     A**  JOIN LENDING LIMIT TYPE TO LIMIT TYPES FILE
     A*
     A          J                           JOIN(1 3)
     A                                      JFLD(DZLLTY A2LMTY)
     A*
     A**  CUSTOMER LIMIT FIELDS
     A*
     A            DZCUST
     A            DZLEVL
     A            DZBCCD
     A*
     A**  CUSTOMER LIMIT FIELDS
     A*
     A            DZFLAM
     A            DZFXLT
     A            DZFLED
     A            DZLLAM
     A            DZLLTY
     A            DZLLED
     A*
     A** FRA/IRA LIMIT FIELDS
     A*
     A            DZFRLA
     A            DZIRLM
     A            DZFRED
     A            DZIRED
     A*
     A**  FX LIMIT  TYPES FIELDS
     A*
     A            FLEP1                     RENAME(A2E1LP) JREF(2)
     A            FLEP2                     RENAME(A2E2LP) JREF(2)
     A            FLEP3                     RENAME(A2E3LP) JREF(2)
     A            FLEP4                     RENAME(A2E4LP) JREF(2)
     A            FLEP5                     RENAME(A2E5LP) JREF(2)
     A            FLEP6                     RENAME(A2E6LP) JREF(2)
     A            FLPC1                     RENAME(A2PL1P) JREF(2)
     A            FLPC2                     RENAME(A2PL2P) JREF(2)
     A            FLPC3                     RENAME(A2PL3P) JREF(2)
     A            FLPC4                     RENAME(A2PL4P) JREF(2)
     A            FLPC5                     RENAME(A2PL5P) JREF(2)
     A            FLPC6                     RENAME(A2PL6P) JREF(2)
     A*
     A**  LENDING LIMIT  TYPES FIELDS
     A*
     A            LLEP1                     RENAME(A2E1LP) JREF(3)
     A            LLEP2                     RENAME(A2E2LP) JREF(3)
     A            LLEP3                     RENAME(A2E3LP) JREF(3)
     A            LLEP4                     RENAME(A2E4LP) JREF(3)
     A            LLEP5                     RENAME(A2E5LP) JREF(3)
     A            LLEP6                     RENAME(A2E6LP) JREF(3)
     A            LLPC1                     RENAME(A2PL1P) JREF(3)
     A            LLPC2                     RENAME(A2PL2P) JREF(3)
     A            LLPC3                     RENAME(A2PL3P) JREF(3)
     A            LLPC4                     RENAME(A2PL4P) JREF(3)
     A            LLPC5                     RENAME(A2PL5P) JREF(3)
     A            LLPC6                     RENAME(A2PL6P) JREF(3)
     A*
     A          K DZCUST
     A          K DZLEVL
     A          K DZBCCD
