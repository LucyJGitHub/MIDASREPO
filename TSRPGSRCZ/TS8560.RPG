     H        1
      *****************************************************************
/*XBIA*  OVRDBF FILE(TSMCLMPD) TOFILE(SDCLLMJ0)                       *
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas TS Real time MDI non-master file update')        *
      *****************************************************************
      *                                                               *
      *     Midas - TRADER SUPPORT                                    *
      *                                                               *
      *     TS8560  -  Real Time MDI NON Master File Update           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CSD095             Date 08Apr14               *
      *  Prev Amend No. CRE073             Date 06Dec10               *
      *                 CAP208             Date 06Dec10               *
      *                 CER059             Date 19Jul10               *
      *                 CER045             Date 20Jul09               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSE075             Date 17Nov05               *
      *                 CSE071             Date 19Jul05               *
      *                 CDL031             Date 24Nov04               *
      *                 CGL013             Date 22Aug03               *
      *                 CSE037             Date 29Apr02               *
      *                 212177             Date 31Mar03               *
      *                 CAS006             Date 21Jan03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CPK015             Date 01Jul02               *
      *                 CSE034             Date 14Jun02               *
      *                 CSE031             Date 13Dec01               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 196833             Date 16Aug01               *
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CSD006             Date 16May01               *
      *                 190946             Date 03Apr01               *
      *                 191531             Date 28Mar01               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CSE023             Date 12Jul00               *
      *                 CSE024             DATE 21Nov00               *
      *                 CRT004             DATE 04MAY00               *
      *                 CDL008             Date 02May00               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CAP137             Date 07Feb00               *
      *                 CAP039             Date 29Mar00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CGL011             Date 20Sep99               *
      *                 CSE006             Date 19Apr99               *
      *                 CFT004             Date 12Nov99               *
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CDL006             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 120534             Date 14Jul97               *
      *                 090949             Date 04Jul96               *
      *                    092168             DATE  21OCT94           *
      *                    C1791              DATE  31AUG93           *
      *                    C1790              DATE  25AUG93           *
      *                    EFIX01             DATE  18AUG93           *
      *                    C00562             DATE  03AUG93           *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSD095 - Allow Deal Sub-Type and Branch for MM and FX SSIs   *
      *           (Recompile)                                         *
      *  CRE073 - Interest Rate Rounding (Recompile)                  *
      *  CAP208 - F&O Market Instrument API (Recompile)               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER045 - German Features - Reporting Bundesbank              *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSE075 - US Enhanced Treasury Upgrade to MidasPlus           *
      *           (Recompile)                                         *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  CDL031 - Effective Date on Extended SSI (Recompile)          *
      *  CGL013 - MT94x Messages Generation (recompile).              *
      *  CSE037 - Bond Pricing - 3 Decimal Places in Trades Input     *
      *           (Recompile)                                         *
      *  212177 - Prevent data mapping error. Increase file size due  *
      *           to introduction of new timestamp field.             *
      *  CAS006 - Hedge Accounting Phase B (Recompile)                *
      *  CPK015 - Release 4.01 Packaging:                             *
      *           - A default timestamp string is required for WRITE. *
      *  CSE034 - Standing Settlement Instructions improvements       *
      *           - Recompiled                                        *
      *  CSE031 - Coupon Fixing for Floating Rate Notes.              *
      *           Recompiled due to changes in SECTYD.                *
      *  196833 - Additional SWIFT Address for CLS Nostros (Recompile)*
      *  CSD006 - Market Data Feed (Recompile)                        *
      *  190946 - Data mapping error for files TSMSE1PD and TSMSECPD  *
      *           due to changes in files SECTYD and SDSECSPD.        *
      *  191531 - Change TS8560 so that 'LIM' is 226 to 274 of JRNENT *
      *           (was 226 to 265). Also change the output of LIM to  *
      *           be end position 149 instead of 148. There is change *
      *           in length of files SDCLLMPD and TSMLIMPD to be 149  *
      *           in DBA3.                                            *
      *  CSE023 - Treaty Withholding Tax. (Recompiled)                *
      *  CSE024 - Withholding Tax - Sub account processing (Recompile)*
      *  CRT004 - Cashier Midas TCP/IP interface. (Recompile)         *
      *  CDL008 - Continous Linked Settlement (Recompiled)            *
      *  CAP137 - Conversion of SE Security inputs into modular       *
      *           structure to use as APIs (SECTYD)                   *
      *  CAP039 - Security customer details API. Fields added to      *
      *           SDSECSPD (recompile)                                *
      *  CGL011 - Collateral Processing for Midas                     *
      *  CSE006 - Recompiled over changed TSMBOKPD and TSMCTYPD.      *
      *  CFT004 - Recompiled over changed TSMBRCPD and TSMCTYPD.      *
      *  CDL006 - Dealing Fiduciary                                   *
      *  120534 - TSMSE1PD array entry should read 25SA not 25PA.     *
      *  090949 - Addition of Securities to the MDI - add files       *
      *           SEMKCD, TRADSD, SDSECSPD, SDCNLMPD and STDSED.      *
      *         - Switch positions of SDSECSPD and INVTPD in MIDASF.  *
      *         - Set up corresponding files for SDSECSPD and INVTPD  *
      *           as TSMSECPD and TSMSE1PD in array TSMFL.            *
      *         - Also write to TSMLIMPD if SDCLLMPD is changed       *
      *           (already writes to TSMCLMPD for customer limits)    *
      *  092168 - Add files SDEXSTPD and SDCTRYPD to realtime MDI     *
      *  C1791  - Rollbacks should be handled as normal amendments    *
      *           UNLESS there is a posibility that the information   *
      *           was transmitted through the gateway (at the moment  *
      *           only deals are sent through the gateway so only     *
      *           rollbacks of deals should be treated specially).    *
      *           The compile time array for trader support file      *
      *           information has been changed. A single character    *
      *           flag has been added that shows wether a rollback    *
      *           on the file should be handled as an ordanary amend  *
      *           "A" or sent to the PC as A database error "D".      *
      *           At the moment only rollbacks of deals should be     *
      *           sent to the PC as Database Errors.                  *
      *           NOTE !!!!                                           *
      *             At the moment deals are the only thing sent to    *
      *             MIDAS through the gateway. If in the future,      *
      *             rates or something else is put through the        *
      *             gateway then the entry in the compile time array  *
      *             should  be changed to "D" so the letter "Q" will  *
      *             be put into the first character of the record     *
      *             sent to the PC so the PC can recognise a database *
      *             error. The PC program will need fix C1459.        *
      *             NOTE : "Q" may need to be changed if there is a   *
      *               posibility that "Q" may be valid in the first   *
      *               character of the rolled back record.            *
      *  C1790  - File SDFOCSPD added to the MDI so Futures &         *
      *           Options customer information can be                 *
      *           downloaded to Trader Support. Changes were made to  *
      *           compile-time arrays MIDASF and TSMFL (changes to    *
      *           compile-time arrays cannot be annotated in the      *
      *           usual way.                                          *
      *  EFIX01 - Field CCY defined incorrectly on journal entry      *
      *           record. It should be 55 characters, NOT 53.         *
      *  C00562 - Files SDCLLMPD and SDLIMTPD added to the MDI so     *
      *           that customer limits information can be             *
      *           downloaded to Trader Support. Changes were made to  *
      *           compile-time arrays MIDASF and TSMFL (changes to    *
      *           compile-time arrays cannot be annotated in the      *
      *           usual way.                                          *
      *           NOTE: Customer Limits are a special case because    *
      *           one limit type change could affect all customer     *
      *           limits.                                             *
      *                                                               *
      *****************************************************************
      *  OVERVIEW : This program is called from TS8550 and extracts   *
      *  all records for files which are designated as secondary      *
      *  files. This program is based on TS8550 and was designed to   *
      *  to increase speed of extraction and also to allow for        *
      *  expansion of more files to be extracted.                     *
      *****************************************************************
     FSDCLLMJ0IF  E           K        DISK                           UC  C00562
     F            SDCLLMD0                          KRENAMESDCLLMDF       C00562
     F**MBOKPDO   F      44            DISK                      A    UC  CSE006
     FTSMBOKPDO   F      49            DISK                      A    UC  CSE006
     FTSMBRCPDO   F      83            DISK                      A    UC
     FTSMBRKPDO   F      54            DISK                      A    UC
     FTSMCCYPDO   F      55            DISK                      A    UC
     FTSMDLSPDO   F      38            DISK                      A    UC
     F***TSMFDIPDO***F      69            DISK                   A    UC                      CPK015
     F***TSMFDIPDO   F     258            DISK                      A    UC            CPK015 212177
     FTSMFDIPDO   F     291            DISK                      A    UC                      212177
     FTSMHOLPDO   F     386            DISK                      A    UC
     FTSMINVPDO   F     100            DISK                      A    UC
     FTSMINYPDO   F     173            DISK                      A    UC
     FTSMMARPDO   F      62            DISK                      A    UC
     FTSMMKCPDO   F      20            DISK                      A    UC
     FTSMNOSPDO   F      74            DISK                      A    UC
     FTSMCLMPDO   E                    DISK                      A    UC  C00562
     FTSMFFCPDO   F     115            DISK                      A    UC  C1790
     FTSMSSIPDO   F     650            DISK                      A    UC  092168
     F***TSMCTYPDO   F      50            DISK                      A    UC            092168 CER045
     FTSMCTYPDO   F      86            DISK                      A    UC                      CER045
     F***TSMSECPDO***F     158            DISK            A    UC  090949 190946
     F*TSMSECPDO**F**** 232            DISK                      A    UC               190946 212177
     FTSMSECPDO   F     432            DISK                      A    UC                      212177
     FTSMSEMPDO   F      33            DISK                      A    UC  090949
     F***TSMSE1PDO***F     533            DISK            A    UC  090949 190946
     FTSMSE1PDO   F     669            DISK                      A    UC  190946
     FTSMCNLPDO   F      72            DISK                      A    UC  090949
     F***TSMLIMPDO***F     140            DISK            A    UC  090949 CDL006
     F***TSMLIMPDO***F     148            DISK            A    UC  CDL006 CGL011
     FTSMLIMPDO   F     149            DISK                      A    UC  CGL011
     FTSMSEDPDO   F     161            DISK                      A    UC  090949
      *
      *
     E                    OPN       100  1               OPEN FILES
     E                    WKF       100  1               TSSTAT WORK COPY
     E                    ZERO    1   2 50               INITIALISE TABLE
     E                    MIDASF  7  36 10               MIDAS FILE NAME  090949
     E                    TSMFL   1  36 14               TSM FILES + FLAGS090949
     E                    STAMP   1   1 26                                                    CPK015
      ** Array containing dummy timestamp                                                     CPK015
     E********************MIDASF  7 100 10               MIDAS FILE NAME  090949
     E********************TSMFL   5 100 13               TSM FILES + FLAG SC1791
     E********************TSMFL   1 100 13               TSM FILES  SC1791090949
      *
      * Journal Entry record received from TS8550.
     IJRNENT      DS
     I                                       67  76 JRNFIL
     I                                      126 225 BLK100                092168
     I                                      226 325 BLK200                092168
     I                                      326 425 BLK300                092168
     I                                      426 525 BLK400                092168
     I                                      526 625 BLK500                092168
     I                                      626 725 BLK600                092168
     I                                      126 169 BOK
     I                                      126 208 BRC
     I                                      126 179 BRK
     I**************************************126 178 CCY                   EFIX01
     I                                      126 180 CCY                   EFIX01
     I                                      126 163 DLS
     I                                      126 194 FDI
     I                                      126 381 HOL1
     I                                      382 511 HOL2
     I                                      126 225 INV
     I                                      126 298 INY
     I                                      126 187 MAR
     I                                      126 145 MKC
     I                                      126 199 NOS
     I                                      126 131 CLMCNO                C00562
     I                                      132 132 CLMLVL                C00562
     I                                      133 135 CLMENT                C00562
     I                                      126 240 FFC                   C1790
     I                                      726 775 SSI                   092168
     I                                      126 175 CTY                   092168
     I**********                            226 283 SEC            090949 190946
     I                                      326 357 SEC                   190946
     I                                      126 158 SEM                   090949
     I**********                            626 658 SE1            090949 190946
     I                                      726 794 SE1                   190946
     I                                      126 197 CNL                   090949
     I**********                            226 265 LIM            090949 191531
     I                                      226 274 LIM                   191531
     I                                      226 286 SED                   090949
      *
      *  Data Structure to reformat Array for Trader Support file
      *  information.                                                     090949
      ***information.*File*name/File*sequence/Priority.***                090949
      *                                                                   090949
      *  TSMFIL - TSM equivalent of Midas file from array MIDASF          090949
      *  SQ     - Sequence no of file, and the indicator used for file    090949
      *  TSFPRI - P = Primary file, processed in TS8550                   090949
      *           S = Secondary file, processed in TS8560                 090949
      *  TSFROL - A = Process Rollback as an amend                        090949
      *           D = Process Rollback as an database error               090949
      *                                                                   090949
     I            DS
     I****************************************1  13 TSDATA                090949
     I                                        1  14 TSDATA                090949
     I                                        1  10 TSMFIL
     I                                       11  120SQ
     I                                       13  13 TSFPRI
     I                                       14  14 TSFROL                090949
      *
      *
     C           *ENTRY    PLIST
     C                     PARM           JRNENT
     C                     PARM           RETCDE  1
     C                     PARM           WFLAGS100
      *                                                                   C00562
      ** Key of Customer Limits file                                      C00562
      *                                                                   C00562
     C           CLMKEY    KLIST                                          C00562
     C                     KFLD           CLMCNO                          C00562
     C                     KFLD           CLMLVL                          C00562
     C                     KFLD           CLMENT                          C00562
      *
      *  If program is requested to terminate by TS8550, then set LR
      *  on to close all files and clear PAG.
     C           RETCDE    IFEQ 'E'
     C                     SETON                     LR
     C                     RETRN
     C                     END
      *
      *  Perform first cycle calcs.
     C           FIRST     IFEQ *BLANK
     C                     MOVE 'N'       FIRST   1
     C                     MOVE *BLANKS   OPN
     C                     END
      *
      *  Determine the TSM equiv. file name from the Midas file name.
     C                     Z-ADD1         X       20
     C           JRNFIL    LOKUPMIDASF,X                 99
      *
     C                     MOVE TSMFL,X   TSDATA
      *  Only extract records for Secondary files.
     C           TSFPRI    IFNE 'P'
     C                     MOVE WFLAGS    WKF
      *  Check if the file is already open, if not then set the work
      *  copy of TSSTAT file flags to Y and open file.
     C           WKF,SQ    IFNE 'Y'
     C                     MOVE 'Y'       WKF,SQ
     C           OPN,SQ    IFNE 'Y'
     C                     MOVE 'Y'       OPN,SQ
     C                     MOVEAZERO      *IN,01
     C                     MOVE '1'       *IN,SQ
     C   02                OPEN TSMBOKPD
     C   03                OPEN TSMBRCPD
     C   04                OPEN TSMBRKPD
     C   06                OPEN TSMCCYPD
     C   14                OPEN TSMDLSPD
     C   17                OPEN TSMFDIPD
     C   18                OPEN TSMHOLPD
     C***20****************OPEN TSMINVPD                                  090949
     C   20                OPEN TSMSECPD                                  090949
     C   21                OPEN TSMINYPD
     C   22                OPEN TSMMARPD
     C   23                OPEN TSMMKCPD
     C   24                OPEN TSMNOSPD
     C   25                OPEN TSMSE1PD                                  090949
     C   28                OPEN TSMCLMPD                                  C00562
     C   28                OPEN TSMLIMPD                                  090949
     C   29                OPEN TSMFFCPD                                  C1790
     C   30                OPEN TSMSSIPD                                  092168
     C   31                OPEN TSMCTYPD                                  092168
     C   32                OPEN TSMSEMPD                                  090949
     C   34                OPEN TSMINVPD                                  090949
     C   35                OPEN TSMCNLPD                                  090949
     C   36                OPEN TSMSEDPD                                  090949
     C                     END
     C                     END
      *  Write the record to the extract files.
     C                     MOVEAZERO      *IN,01
     C                     MOVE '1'       *IN,SQ
     C                     MOVEASTAMP,1   STAMPA 26
     C   02                EXCPTBOKREC
     C   03                EXCPTBRCREC
     C   04                EXCPTBRKREC
     C   06                EXCPTCCYREC
     C   14                EXCPTDLSREC
     C   17                EXCPTFDIREC
     C   18                EXCPTHOLREC
     C***20****************EXCPTINVREC                                    090949
     C   20                EXCPTSECREC                                    090949
     C   21                EXCPTINYREC
     C   22                EXCPTMARREC
     C   23                EXCPTMKCREC
     C   24                EXCPTNOSREC
     C   25                EXCPTSE1REC                                    090949
     C   29                EXCPTFFCREC                                    C1790
     C   30                EXCPTSSIREC                                    092168
     C   31                EXCPTCTYREC                                    092168
     C   32                EXCPTSEMREC                                    090949
     C   34                EXCPTINVREC                                    090949
     C   35                EXCPTCNLREC                                    090949
     C   36                EXCPTSEDREC                                    090949
      *                                                                   C00562
      ** Processing for Customer Limits                                   C00562
      *                                                                   C00562
     C           *IN28     IFEQ '1'                                       C00562
     C                     OPEN SDCLLMJ0                                  C00562
     C           JRNFIL    IFEQ 'SDCLLMPD'                                C00562
     C                     EXCPTLIMREC                                    090949
     C           CLMKEY    CHAINSDCLLMJ0             80                   C00562
     C           *IN80     IFEQ '0'                                       C00562
     C                     WRITESDCLLMD0                                  C00562
     C                     ENDIF                                          C00562
     C                     ENDIF                                          C00562
     C           JRNFIL    IFEQ 'SDLIMTPD'                                C00562
     C           *BLANKS   SETLLSDCLLMJ0                                  C00562
     C                     READ SDCLLMJ0                 80               C00562
     C           *IN80     DOWEQ'0'                                       C00562
     C                     WRITESDCLLMD0                                  C00562
     C                     READ SDCLLMJ0                 80               C00562
     C                     ENDDO                                          C00562
     C                     ENDIF                                          C00562
     C                     CLOSESDCLLMJ0                                  C00562
     C                     ENDIF                                          C00562
      *
     C                     END
      *
     C                     RETRN
      **************************************************************
      *
     OTSMBOKPDEADD             BOKREC
     O************             BOK       44
     O                         BOK       49
     OTSMBRCPDEADD             BRCREC
     O                         BRC       83
     OTSMBRKPDEADD             BRKREC
     O                         BRK       54
     OTSMCCYPDEADD             CCYREC
     O                         CCY       55
     OTSMDLSPDEADD             DLSREC
     O                         DLS       38
     OTSMFDIPDEADD             FDIREC
     O                         FDI       69
     O                         STAMPA    96                                                   CPK015
     O                         STAMPA   177                                                   CPK015
     O                         STAMPA   258                                                   CPK015
     OTSMHOLPDEADD             HOLREC
     O                         HOL1     256
     O                         HOL2     386
     OTSMINVPDEADD             INVREC
     O                         INV      100
     OTSMINYPDEADD             INYREC
     O                         INY      173
     OTSMMARPDEADD             MARREC
     O                         MAR       62
     OTSMMKCPDEADD             MKCREC
     O                         MKC       20
     OTSMNOSPDEADD             NOSREC
     O                         NOS       74
     OTSMFFCPDEADD             FFCREC                                     C1790
     O                         FFC      115                               C1790
     OTSMSSIPDEADD             SSIREC                                     092168
     O                         BLK100   100                               092168
     O                         BLK200   200                               092168
     O                         BLK300   300                               092168
     O                         BLK400   400                               092168
     O                         BLK500   500                               092168
     O                         BLK600   600                               092168
     O                         SSI      650                               092168
     OTSMCTYPDEADD             CTYREC                                     092168
     O                         CTY       50                               092168
     OTSMSECPDEADD             SECREC                                     090949
     O                         BLK100   100                               090949
     O                         BLK200   200                               190946
     O**********               SEC      158                        090949 190946
     O                         SEC      232                               190946
     O                         STAMPA   232                                                   CPK015
     OTSMSEMPDEADD             SEMREC                                     090949
     O                         SEM       33                               090949
     OTSMSE1PDEADD             SE1REC                                     090949
     O                         BLK100   100                               090949
     O                         BLK200   200                               090949
     O                         BLK300   300                               090949
     O                         BLK400   400                               090949
     O                         BLK500   500                               090949
     O                         BLK600   600                               190946
     O**********               SE1      533                        090949 190946
     O                         SE1      669                               190946
     O                         STAMPA   661                                                   CPK015
     OTSMCNLPDEADD             CNLREC                                     090949
     O                         CNL       72                               090949
     OTSMLIMPDEADD             LIMREC                                     090949
     O                         BLK100   100                               090949
     O************             LIM      140                        090949 CGL011
     O************             LIM      148                        CGL011 191531
     O                         LIM      149                               191531
     OTSMSEDPDEADD             SEDREC                                     090949
     O                         BLK100   100                               090949
     O                         SED      161                               090949
** ZERO  TO INITIALISE INDIACTOR ARRAY TO ZERO.
00000000000000000000000000000000000000000000000000
00000000000000000000000000000000000000000000000000
** MIDAS FILE NAMES - MIDASF
ABDEALPP  SDBOOKPD  SDBRCHPD  SDBROKPD  SDBSRTPD  FDCCYPPP  SDCURRPD
SDCUSTPD  DEALSDG   MMDEAMPP  SEDEVD    FXDEALPP  MMDELDPP  SDDLSTPD
MMDENBPP  MMDENSPP  FDINTRPP  SDHOLPD   SDSECSPD  INTYPD    MARKTD        090949
MKCTLD    SDNOSTPD  SECTYD    TRSETD    TRANSD    SDCLLMPD  SDLIMTPD
SDFOCSPD  SDEXSTPD  SDCTRYPD  SEMKCD    TRADSD    INVTPD    SDCNLMPD      090949
STDSED                                                                    090949
** TRADER SUPPORT FILE NAME/SEQUENCE/PRIORITY/ROLBACK IND - TSMFL
TSMABDPD  01PD
TSMBOKPD  02SA
TSMBRCPD  03SA
TSMBRKPD  04SA
TSMBRTPD  05PA
TSMCCYPD  06SA
TSMCURPD  07PA
TSMCUSPD  08PA
TSMDDGPD  09PD
TSMDEMPD  10PD
TSMDEVPD  11PD
TSMDFXPD  12PD
TSMDLDPD  13PD
TSMDLSPD  14SA
TSMDNBPD  15PD
TSMDNSPD  16PD
TSMFDIPD  17SA
TSMHOLPD  18SA  /* No 19 as TSMHOLPD processed twice on PC side */
TSMSECPD  20SA                                                            090949
TSMINYPD  21SA
TSMMARPD  22SA
TSMMKCPD  23SA
TSMNOSPD  24SA
TSMSE1PD  25SA                                                      090949120534
TSMTR1PD  26PD
TSMTR2PD  27PD
TSMCLMPD  28SA
TSMCLMPD  28SA  /* Two 28s as two Midas files feed into one TS file */
TSMFFCPD  29SA
TSMSSIPD  30SA
TSMCTYPD  31SA
TSMSEMPD  32SA                                                            090949
TSMDSEPD  33PD                                                            090949
TSMINVPD  34SA                                                            090949
TSMCNLPD  35SA                                                            090949
TSMSEDPD  36SA                                                            090949
** STAMP - Dummy timestamp                                                                    CPK015
0001-01-01-00.00.00.000000                                                                    CPK015
