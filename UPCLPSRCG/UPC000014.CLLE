/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas UP Change zone records in global files')        */
/*********************************************************************/
/*                                                                   */
/*       Midas - Bridge                                              */
/*                                                                   */
/*       UPC000014 - Update zone records in global files             */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2010           */
/*                                                                   */
/*       Last Amend No. CUP002  *CREATE    Date 13Aug10              */
/*       Prev Amend No. xxxxxx             Date ddMmmyy              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CUP002 - New utility.                                       */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&OZONE &NZONE)
 
             DCL        VAR(&OZONE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&NZONE) TYPE(*CHAR) LEN(10)
 
             DCL        VAR(&STM_STR) TYPE(*CHAR) LEN(80)
             DCL        VAR(&QUOTE) TYPE(*CHAR) LEN(1) VALUE('''')
 
             DCLF       FILE(GPGLOBFPD) OPNID(A)
             DCLF       FILE(SMGNMGPD) OPNID(B)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2010')
 
/* Global monitor message. */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ERROR))
 
GPGLOBFPD:
             RCVF       OPNID(A)
             MONMSG     MSGID(CPF0864) EXEC(DO)
                GOTO       CMDLBL(SMGNMGPD)
             ENDDO
 
/* If the zone field is blank then ignore. */
             IF         COND(&A_GFZONE *EQ ' ') THEN(DO)
                GOTO       CMDLBL(GPGLOBFPD)
             ENDDO
 
/* Else build SQL statement to update all records relating to renamed zone. */
             CHGVAR     VAR(&STM_STR) VALUE(' ')
             CHGVAR     VAR(&STM_STR) VALUE('update' *BCAT &A_GFGPF +
                          *BCAT 'set' *BCAT &A_GFZONE *BCAT '=' *BCAT +
                          &QUOTE *TCAT &NZONE *TCAT &QUOTE *BCAT +
                          'where' *BCAT &A_GFZONE *BCAT '=' *BCAT +
                          &QUOTE *TCAT &OZONE *TCAT &QUOTE)
             CALL       PGM(UTWRTSQL) PARM(&STM_STR '*CLEAR')
             RUNSQLSTM  SRCFILE(QTEMP/RUNSQL) SRCMBR(RUNSQLSTM) +
                          COMMIT(*NONE)
             MONMSG     MSGID(SQL9010)
 
             GOTO       CMDLBL(GPGLOBFPD)
 
SMGNMGPD:
             RCVF       OPNID(B)
             MONMSG     MSGID(CPF0864) EXEC(DO)
                GOTO       CMDLBL(OTHER)
             ENDDO
 
/* If the extension table flag is not 'Y' then ignore. */
             IF         COND(&B_NCETBL *NE 'Y') THEN(DO)
                GOTO       CMDLBL(SMGNMGPD)
             ENDDO
 
/* Only update if zone field has a value. */
             IF         COND(&B_NCZFLD *NE ' ') THEN(DO)
                CHGVAR     VAR(&STM_STR) VALUE(' ')
                CHGVAR     VAR(&STM_STR) VALUE('update' *BCAT &B_NCOBJ +
                             *BCAT 'set' *BCAT &B_NCZFLD *BCAT '=' +
                             *BCAT &QUOTE *TCAT &NZONE *TCAT &QUOTE +
                             *BCAT 'where' *BCAT &B_NCZFLD *BCAT '=' +
                             *BCAT &QUOTE *TCAT &OZONE *TCAT &QUOTE)
                CALL       PGM(UTWRTSQL) PARM(&STM_STR '*CLEAR')
                RUNSQLSTM  SRCFILE(QTEMP/RUNSQL) SRCMBR(RUNSQLSTM) +
                             COMMIT(*NONE)
                MONMSG     MSGID(SQL9010)
             ENDDO
 
             GOTO       CMDLBL(SMGNMGPD)
 
OTHER:
/* Update GPGLCUSL. */
             CHGVAR     VAR(&STM_STR) VALUE(' ')
             CHGVAR     VAR(&STM_STR) VALUE('update GPGLCUSL set +
                          CGZONE =' *BCAT &QUOTE *TCAT &NZONE *TCAT +
                          &QUOTE *BCAT 'where CGZONE =' *BCAT +
                          &QUOTE *TCAT &OZONE *TCAT &QUOTE)
             CALL       PGM(UTWRTSQL) PARM(&STM_STR '*CLEAR')
             RUNSQLSTM  SRCFILE(QTEMP/RUNSQL) SRCMBR(RUNSQLSTM) +
                          COMMIT(*NONE)
             MONMSG     MSGID(SQL9010)
 
/* Update GZFCLTLVPD. */
             CHGVAR     VAR(&STM_STR) VALUE(' ')
             CHGVAR     VAR(&STM_STR) VALUE('update GZFCLTLVPD set +
                          CGZONE =' *BCAT &QUOTE *TCAT &NZONE *TCAT +
                          &QUOTE *BCAT 'where CGZONE =' *BCAT +
                          &QUOTE *TCAT &OZONE *TCAT &QUOTE)
             CALL       PGM(UTWRTSQL) PARM(&STM_STR '*CLEAR')
             RUNSQLSTM  SRCFILE(QTEMP/RUNSQL) SRCMBR(RUNSQLSTM) +
                          COMMIT(*NONE)
             MONMSG     MSGID(SQL9010)
 
/* Update GPMLCSPD. */
             CHGVAR     VAR(&STM_STR) VALUE(' ')
             CHGVAR     VAR(&STM_STR) VALUE('update GPMLCSPD set +
                          CCYZONE =' *BCAT &QUOTE *TCAT &NZONE +
                          *TCAT &QUOTE *BCAT 'where CCYZONE =' +
                          *BCAT &QUOTE *TCAT &OZONE *TCAT &QUOTE)
             CALL       PGM(UTWRTSQL) PARM(&STM_STR '*CLEAR')
             RUNSQLSTM  SRCFILE(QTEMP/RUNSQL) SRCMBR(RUNSQLSTM) +
                          COMMIT(*NONE)
             MONMSG     MSGID(SQL9010)
 
/* Update T_WIPCFG. */
             CHGVAR     VAR(&STM_STR) VALUE(' ')
             CHGVAR     VAR(&STM_STR) VALUE('update T_WIPCFG set +
                          WCFG_ZONE =' *BCAT &QUOTE *TCAT &NZONE +
                          *TCAT &QUOTE *BCAT 'where WCFG_ZONE =' +
                          *BCAT &QUOTE *TCAT &OZONE *TCAT &QUOTE)
             CALL       PGM(UTWRTSQL) PARM(&STM_STR '*CLEAR')
             RUNSQLSTM  SRCFILE(QTEMP/RUNSQL) SRCMBR(RUNSQLSTM) +
                          COMMIT(*NONE)
             MONMSG     MSGID(SQL9010)
 
/* Update T_MSWIP. */
             CHGVAR     VAR(&STM_STR) VALUE(' ')
             CHGVAR     VAR(&STM_STR) VALUE('update T_MSWIP set +
                          MSWZONE =' *BCAT &QUOTE *TCAT &NZONE +
                          *TCAT &QUOTE *BCAT 'where MSWZONE =' +
                          *BCAT &QUOTE *TCAT &OZONE *TCAT &QUOTE)
             CALL       PGM(UTWRTSQL) PARM(&STM_STR '*CLEAR')
             RUNSQLSTM  SRCFILE(QTEMP/RUNSQL) SRCMBR(RUNSQLSTM) +
                          COMMIT(*NONE)
             MONMSG     MSGID(SQL9010)
 
/* Update T_MSWIP_H. */
             CHGVAR     VAR(&STM_STR) VALUE(' ')
             CHGVAR     VAR(&STM_STR) VALUE('update T_MSWIP_H set +
                          WHZONE =' *BCAT &QUOTE *TCAT &NZONE *TCAT +
                          &QUOTE *BCAT 'where WHZONE =' *BCAT +
                          &QUOTE *TCAT &OZONE *TCAT &QUOTE)
             CALL       PGM(UTWRTSQL) PARM(&STM_STR '*CLEAR')
             RUNSQLSTM  SRCFILE(QTEMP/RUNSQL) SRCMBR(RUNSQLSTM) +
                          COMMIT(*NONE)
             MONMSG     MSGID(SQL9010)
 
/* Update T_WIPCF. */
             CHGVAR     VAR(&STM_STR) VALUE(' ')
             CHGVAR     VAR(&STM_STR) VALUE('update T_WIPCF set +
                          WC_ZONE =' *BCAT &QUOTE *TCAT &NZONE +
                          *TCAT &QUOTE *BCAT 'where WC_ZONE =' +
                          *BCAT &QUOTE *TCAT &OZONE *TCAT &QUOTE)
             CALL       PGM(UTWRTSQL) PARM(&STM_STR '*CLEAR')
             RUNSQLSTM  SRCFILE(QTEMP/RUNSQL) SRCMBR(RUNSQLSTM) +
                          COMMIT(*NONE)
             MONMSG     MSGID(SQL9010)
 
/* Update T_GZCUSTEX. */
             CHGVAR     VAR(&STM_STR) VALUE(' ')
             CHGVAR     VAR(&STM_STR) VALUE('update T_GZCUSTEX set +
                          ZONE =' *BCAT &QUOTE *TCAT &NZONE *TCAT +
                          &QUOTE *BCAT 'where ZONE =' *BCAT &QUOTE +
                          *TCAT &OZONE *TCAT &QUOTE)
             CALL       PGM(UTWRTSQL) PARM(&STM_STR '*CLEAR')
             RUNSQLSTM  SRCFILE(QTEMP/RUNSQL) SRCMBR(RUNSQLSTM) +
                          COMMIT(*NONE)
             MONMSG     MSGID(SQL9010)
 
/* Update T_GZBRCHEX. */
             CHGVAR     VAR(&STM_STR) VALUE(' ')
             CHGVAR     VAR(&STM_STR) VALUE('update T_GZBRCHEX set +
                          ZONE =' *BCAT &QUOTE *TCAT &NZONE *TCAT +
                          &QUOTE *BCAT 'where ZONE =' *BCAT &QUOTE +
                          *TCAT &OZONE *TCAT &QUOTE)
             CALL       PGM(UTWRTSQL) PARM(&STM_STR '*CLEAR')
             RUNSQLSTM  SRCFILE(QTEMP/RUNSQL) SRCMBR(RUNSQLSTM) +
                          COMMIT(*NONE)
             MONMSG     MSGID(SQL9010)
 
/* Update T_GZCNTYEX. */
             CHGVAR     VAR(&STM_STR) VALUE(' ')
             CHGVAR     VAR(&STM_STR) VALUE('update T_GZCNTYEX set +
                          ZONE =' *BCAT &QUOTE *TCAT &NZONE *TCAT +
                          &QUOTE *BCAT 'where ZONE =' *BCAT &QUOTE +
                          *TCAT &OZONE *TCAT &QUOTE *TCAT ';')
             CALL       PGM(UTWRTSQL) PARM(&STM_STR '*CLEAR')
             CHGVAR     VAR(&STM_STR) VALUE(' ')
             CHGVAR     VAR(&STM_STR) VALUE('update T_GZCNTYEX set +
                          A4ZONE =' *BCAT &QUOTE *TCAT &NZONE *TCAT +
                          &QUOTE *BCAT 'where A4ZONE =' *BCAT +
                          &QUOTE *TCAT &OZONE *TCAT &QUOTE *TCAT ';')
             CALL       PGM(UTWRTSQL) PARM(&STM_STR '*WRITE')
             RUNSQLSTM  SRCFILE(QTEMP/RUNSQL) SRCMBR(RUNSQLSTM) +
                          COMMIT(*NONE)
             MONMSG     MSGID(SQL9010)
 
/* Update T_GZMIBEX. */
             CHGVAR     VAR(&STM_STR) VALUE(' ')
             CHGVAR     VAR(&STM_STR) VALUE('update T_GZMIBEX set +
                          ZONE =' *BCAT &QUOTE *TCAT &NZONE *TCAT +
                          &QUOTE *BCAT 'where ZONE =' *BCAT &QUOTE +
                          *TCAT &OZONE *TCAT &QUOTE)
             CALL       PGM(UTWRTSQL) PARM(&STM_STR '*CLEAR')
             RUNSQLSTM  SRCFILE(QTEMP/RUNSQL) SRCMBR(RUNSQLSTM) +
                          COMMIT(*NONE)
             MONMSG     MSGID(SQL9010)
 
             GOTO       CMDLBL(ENDPGM)
ERROR:
             CHGJOB     SWS(XXXXXX11)
ENDPGM:
             ENDPGM
