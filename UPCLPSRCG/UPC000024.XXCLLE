/*********************************************************************/
/*S*D****CLPBASEBND***************************************************/
/*********************************************************************/
/*                                                                   */
/*       Midas - Bridge                                              */
/*                                                                   */
/*       UPC000024 - End journaling - Global layer                   */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2010           */
/*                                                                   */
/*       Last Amend No. CUP041  *REDUNDANT Date 01Aug14              */
/*       Prev Amend No. CUP002  *CREATE    Date 27Jul10              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CUP041 - New Bridge methodology to combine menu options     */
/*       CUP002 - Change to stop people signing in.                  */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&GPFXSVAL)
 
             DCL        VAR(&GPFXSVAL) TYPE(*CHAR) LEN(20)
             DCL        VAR(&GPSBSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&BLANKSVAL) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)
 
             DCLF       FILE(UPGSVALJ0)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2010')
 
/* Global monitor message. */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CHGJOB     LOG(4 0 *SECLVL) LOGCLPGM(*YES)
 
/* Create data area /MIDASMSG in QTEMP. */
             DLTDTAARA  DTAARA(QTEMP/MIDASMSG)
             MONMSG     MSGID(CPF0000)
             CRTDTAARA  DTAARA(QTEMP/MIDASMSG) TYPE(*CHAR) LEN(800) +
                          VALUE(' ')
 
VALIDATE:
             RCVF
             MONMSG     MSGID(CPF0864) EXEC(DO)
                GOTO       CMDLBL(CHECK)
             ENDDO
 
             IF         COND(&GISVAL *EQ &GPFXSVAL) THEN(DO)
                IF         COND(&GIVAL *EQ ' ') THEN(DO)
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')
                ENDDO
                ELSE       CMD(DO)
                   CHGVAR     VAR(&GPSBSID) VALUE(&GIVAL)
                ENDDO
                GOTO       CMDLBL(CHECK)
             ENDDO
 
             GOTO       CMDLBL(VALIDATE)
 
 CHECK:
             IF         COND(&BLANKSVAL *EQ 'Y') THEN(DO)
                RTVMSG     MSGID(UPM0003) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
                RTVMSG     MSGID(UPM0004) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (151 50)) VALUE(&MESSAGE)
                CALL       PGM(SCC0010) PARM('UPC000024' 'ENTER' ' ')
                GOTO       CMDLBL(ENDPGM)
             ENDDO
 
             SAVLIBL    SAVDTL(BRIDGE)
 
             CALL       PGM(GPC000016) PARM(&GPSBSID)
 
             ENDJRNPF   FILE(*ALL) JRN(GPJRN)
             ENDJRNOBJ  OBJ(*ALL) OBJTYPE(*ALL) JRN(GPJRN)
 
             RSTLIBL    SAVEDLIBL(BRIDGE)
 
             GOTO       CMDLBL(ENDPGM)
ABNOR:
/* Set up messages for Midas Information Screen  */
             RTVMSG     MSGID(UPM0001) MSGF(UTMSGF) +
                          MSGDTA('UPC000024') MSG(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (201 50)) VALUE('Check +
                          joblog for details')
             MONMSG     MSGID(CPF0000)
             CALL       PGM(SCC0010) PARM('UPC000024' 'ENTER' ' ')
             MONMSG     MSGID(CPF0000)
 
ENDPGM:
             ENDPGM
