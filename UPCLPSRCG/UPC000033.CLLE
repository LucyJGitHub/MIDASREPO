/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas UP Save / restore non-core logical files')      */
/*********************************************************************/
/*                                                                   */
/*       Midas - Bridge                                              */
/*                                                                   */
/*       UPC000033 - Save / restore non-core logical files           */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2013           */
/*                                                                   */
/*       Last Amend No. CUP041 *CREATE   Date 10Sep13                */
/*       Prev Amend No. xxxxxxxx           Date ddMmmyy              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CUP041 - New Bridge methodology to combine menu options     */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&MODE &FILE &LIBRARY &BRGBRGLIB &SAVF +
                          &RTNFLAG)
 
             DCL        VAR(&MODE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&FILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&LIBRARY) TYPE(*CHAR) LEN(10)
             DCL        VAR(&BRGBRGLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SAVF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&RTNFLAG) TYPE(*CHAR) LEN(1)
 
             DCL        VAR(&NBRRSTA) TYPE(*CHAR) LEN(4)
             DCL        VAR(&MSGDTA) TYPE(*CHAR) LEN(8)
             DCL        VAR(&SAVFTEXT) TYPE(*CHAR) LEN(50)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2013')
 
/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ERROR))
 
             IF         COND(&MODE *EQ '*SAV') THEN(DO)
/* Check if the file exists; if it does not then it is assumed that it has */
/*  already been deleted.                                                  */
                CHKOBJ     OBJ(&LIBRARY/&FILE) OBJTYPE(*FILE)
                MONMSG     MSGID(CPF9801) EXEC(DO)
                   CHGVAR     VAR(&RTNFLAG) VALUE('N')
                   GOTO       CMDLBL(ENDPGM)
                ENDDO
 
/* If the file does exist then create save file, save the logical and  */
/*  then delete it.                                                    */
                CHGVAR     VAR(&SAVFTEXT) VALUE('Save of' *BCAT +
                             &LIBRARY *TCAT '/' *TCAT &FILE)
                CRTSAVF    FILE(&BRGBRGLIB/&SAVF) TEXT(&SAVFTEXT)
                SAVOBJ     OBJ(&FILE) LIB(&LIBRARY) DEV(*SAVF) +
                             OBJTYPE(*FILE) SAVF(&BRGBRGLIB/&SAVF)
                DLTF       FILE(&LIBRARY/&FILE)
             ENDDO
 
             IF         COND(&MODE *EQ '*RST') THEN(DO)
                RSTOBJ     OBJ(&FILE) SAVLIB(&LIBRARY) DEV(*SAVF) +
                             OBJTYPE(*FILE) SAVF(&BRGBRGLIB/&SAVF)
                MONMSG     MSGID(CPF3773) EXEC(DO)
                   RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSGDTA)
                   CHGVAR     VAR(&NBRRSTA) VALUE(%BIN(&MSGDTA 5 4))
                   IF         COND(&NBRRSTA *NE '0000') THEN(DO)
                      GOTO       CMDLBL(ERROR)
                   ENDDO
                ENDDO
                DLTF       FILE(&BRGBRGLIB/&SAVF)
             ENDDO
 
             GOTO       CMDLBL(ENDPGM)
 
ERROR:
             CHGVAR     VAR(&RTNFLAG) VALUE('E')
 
ENDPGM:
             ENDPGM
