/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas UP Upgrade data areas and data queues')         */
/*********************************************************************/
/*                                                                   */
/*       Midas - Bridge                                              */
/*                                                                   */
/*       UPC000035 - Upgrade data areas and data queues              */
/*                                                                   */
/*       (c) Finastra International Limited 2013                     */
/*                                                                   */
/*       Last Amend No. MD061906           Date 23Sep23              */
/*       Prev Amend No. CSD101             Date 07Dec18              */
/*                      CUP041   *CREATE   Date 11Sep13              */
/*                      xxxxxxxx           Date ddMmmyy              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD061906 - Bridge journalling improvement                   */
/*       CSD101 - Password Encryption                                */
/*       CUP041 - New Bridge methodology to combine menu options     */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&OBJTYPE &MEMBER &SRCF &BRGSRCLIB +
                          &OBJLIB &LIBMOV &OWNER &AUTL &RTNFLAG)

             DCL        VAR(&OBJTYPE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MEMBER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SRCF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&BRGSRCLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OBJLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&LIBMOV) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OWNER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&AUTL) TYPE(*CHAR) LEN(10)
             DCL        VAR(&RTNFLAG) TYPE(*CHAR) LEN(1)

             DCL        VAR(&RTNLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&NEWNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&EXIST) TYPE(*CHAR) LEN(1) VALUE('N')

             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2013')

/* Global monitor message. */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ERROR))

/* If object is not found then just create. */
             RTVOBJD    OBJ(&MEMBER) OBJTYPE(&OBJTYPE) RTNLIB(&RTNLIB)
             MONMSG     MSGID(CPF9801) EXEC(DO)
                GOTO       CMDLBL(CRTOBJ)
             ENDDO

/* Otherwise move the file to the temporary hodling library. */
             CHGVAR     VAR(&EXIST) VALUE('Y')

             IF         COND(&OBJTYPE *NE '*VLDL') THEN(DO)                               /*CSD101*/
             ENDJRNOBJ  OBJ(&RTNLIB/&MEMBER) OBJTYPE(&OBJTYPE)
             MONMSG     MSGID(CPF7032)
             ENDDO                                                                        /*CSD101*/

             MOVOBJ     OBJ(&RTNLIB/&MEMBER) OBJTYPE(&OBJTYPE) +
                          TOLIB(&LIBMOV)
/* If the object already exists then try to rename it and move again. */
             MONMSG     MSGID(CPF2112) EXEC(DO)
                CHGVAR     VAR(&NEWNAME) VALUE('#.' *TCAT &MEMBER)
                RNMOBJ     OBJ(&LIBMOV/&MEMBER) OBJTYPE(&OBJTYPE) +
                             NEWOBJ(&NEWNAME)
/* If the renamed object already exists then flag as failure. */
                MONMSG     MSGID(CPF2112) EXEC(DO)
                   GOTO       CMDLBL(ERROR)
                ENDDO
                MOVOBJ     OBJ(&RTNLIB/&MEMBER) OBJTYPE(&OBJTYPE) +
                             TOLIB(&LIBMOV)
             ENDDO

CRTOBJ:
             CRTOBJ     OBJ(&OBJLIB/&MEMBER) +
                          SRCFILE(&BRGSRCLIB/&SRCF) SRCMBR(*OBJ) +
                          OWNER(&OWNER) AUTL(&AUTL)
             MONMSG     MSGID(SYS9898 SYS0011 SYS0017) EXEC(DO)
                GOTO       CMDLBL(ERROR)
             ENDDO

/* If the object did not originally exist then there is no copy needed. */
             IF         COND(&EXIST *EQ 'N') THEN(DO)
                GOTO       CMDLBL(ENDPGM)
             ENDDO

             IF         COND(&OBJTYPE *EQ '*DTAQ') THEN(DO)
                CPYDTAQ    FMDTAQ(&LIBMOV/&MEMBER) TODTAQ(&OBJLIB/&MEMBER)
                MONMSG     MSGID(CPF0000 RPG0000 MCH0000) EXEC(DO)
                   GOTO       CMDLBL(ERROR)
                ENDDO
             ENDDO

             IF         COND(&OBJTYPE *EQ '*DTAARA') THEN(DO)
                CPYDTAARA  FMDTAARA(&LIBMOV/&MEMBER) +
                             TODTAARA(&OBJLIB/&MEMBER)
                MONMSG     MSGID(CPF0000) EXEC(DO)
                   GOTO       CMDLBL(ERROR)
                ENDDO
             ENDDO

             GOTO       CMDLBL(ENDPGM)
/***ERROR: */                                                                           /*MD061906*/
/**********  CHGVAR     VAR(&RTNFLAG) VALUE('E') */                                     /*MD061906*/

ENDPGM:
/* Start journalling for DTAARA and DTAQ */                                             /*MD061906*/
             CHKOBJ     OBJ(&OBJLIB/&MEMBER) OBJTYPE(&OBJTYPE)                          /*MD061906*/
             MONMSG     MSGID(CPF9801) EXEC(GOTO CMDLBL(ERROR))                         /*MD061906*/
/* DTAARA */                                                                            /*MD061906*/
             IF         COND(&OBJTYPE *EQ '*DTAARA') THEN(DO)                           /*MD061906*/
             IF         COND(%SUBSTRING(&OBJLIB 3 1) *EQ 'D') THEN(DO)                  /*MD061906*/
             STRJRNOBJ  OBJ(&OBJLIB/&MEMBER) OBJTYPE(&OBJTYPE) +
                          JRN(ICJRN) IMAGES(*BOTH)                                      /*MD061096*/
             ENDDO                                                                      /*MD061906*/
             IF         COND(%SUBSTRING(&OBJLIB 3 1) *EQ 'G') THEN(DO)                  /*MD061906*/
             STRJRNOBJ  OBJ(&OBJLIB/&MEMBER) OBJTYPE(&OBJTYPE) +
                          JRN(GPJRN) IMAGES(*BOTH)                                      /*MD061096*/
             ENDDO                                                                      /*MD061906*/
             ENDDO                                                                      /*MD061906*/
                                                                                        /*MD061906*/
/* DTAQ */                                                                              /*MD061906*/
             IF         COND(&OBJTYPE *EQ '*DTAQ') THEN(DO)                             /*MD061906*/
             IF         COND(%SUBSTRING(&OBJLIB 3 1) *EQ 'D') THEN(DO)                  /*MD061906*/
             STRJRNOBJ  OBJ(&OBJLIB/&MEMBER) OBJTYPE(&OBJTYPE) +
                          JRN(ICJRN)                                                    /*MD061096*/
             ENDDO                                                                      /*MD061906*/
             IF         COND(%SUBSTRING(&OBJLIB 3 1) *EQ 'G') THEN(DO)                  /*MD061906*/
             STRJRNOBJ  OBJ(&OBJLIB/&MEMBER) OBJTYPE(&OBJTYPE) +
                          JRN(GPJRN)                                                    /*MD061096*/
             ENDDO                                                                      /*MD061906*/
             ENDDO                                                                      /*MD061906*/
                                                                                        /*MD061906*/
             GOTO       CMDLBL(ENDPGM2)                                                 /*MD061906*/
                                                                                        /*MD061906*/
ERROR:                                                                                  /*MD061906*/
             CHGVAR     VAR(&RTNFLAG) VALUE('E')                                        /*MD061906*/
ENDPGM2:                                                                                /*MD061906*/
             ENDPGM
