/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas UP Database cross reference - Global Layer')    */
/*********************************************************************/
/*                                                                   */
/*       Midas - Upgrade Module                                      */
/*                                                                   */
/*       UPC000108 - Database cross reference - Global Layer         */
/*                                                                   */
/*       (c) Finastra International Limited 2004                     */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/*       Last Amend No. BUG27240           Date 12Mar10              */
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/*       Prev Amend No. CSM010             Date 22Jun07              */
/*                      CPK027             Date 21May07              */
/*                      CUP033             Date 18Sep06              */
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*                      CSM005             Date 21Sep05              */
/*                      CUP028             Date 13Sep04              */
/*                      CUP027  *CREATE    Date 25Mar04              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       BUG27240- Applied CPK029 file locking fix                   */
/*       CPK029 - Stop lock on file.                                 */
/*       CSM010 - Applied by CPK027.                                 */
/*       CPK027 - Message file spelt incorrectly.                    */
/*       CUP033 - Removal of System Value /COPYs.                    */
/*       CSM005 - Changes to /COPYs.  Recompile.                     */
/*       CUP028 - Add extra parameter to call UP000108 so that       */
/*                UP000108 can be used for global and zone files.    */
/*       CUP027 - Global layer migration of Bridge ICD to the System */
/*                Values file.                                       */
/*                                                                   */
/*********************************************************************/
/**********  PGM                                                                       */ /*CUP033*/
             PGM        PARM(&GPFXSVAL)                                                   /*CUP033*/
 
             DCL        VAR(&GPFXSVAL) TYPE(*CHAR) LEN(20)                                /*CUP033*/
/**COPY*UPCPYSRCG,UPGSVALDCL*                                        */ /*             */ /*CUP033*/
             DCL        VAR(&GPSBSID) TYPE(*CHAR) LEN(2)                                  /*CUP033*/
             DCL        VAR(&BLANKSVAL) TYPE(*CHAR) LEN(1) VALUE('N')                     /*CUP033*/
             DCL        VAR(&GVLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&RETURN) TYPE(*CHAR) LEN(1)
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)
 
             DCLF       FILE(UPGSVALJ0)                                                   /*CUP033*/
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2004')
 
/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CHGJOB     LOG(4 0 *SECLVL) LOGCLPGM(*YES)
             CHGJOB     SWS(XXXXXX00)
 
/* Create data area /MIDASMSG in QTEMP */
             DLTDTAARA  DTAARA(QTEMP/MIDASMSG)
             MONMSG     MSGID(CPF0000)
             CRTDTAARA  DTAARA(QTEMP/MIDASMSG) TYPE(*CHAR) LEN(800) +
                          VALUE(' ')
 
/**COPY*UPCPYSRCG,UPGSVALCL1*                                        */ /*             */ /*CUP033*/
/**********  IF         COND(&GPSBSID *EQ ' ') THEN(DO)                                */ /*CUP033*/
 
VALIDATE:                                                                                 /*CUP033*/
             RCVF                                                                         /*CUP033*/
             MONMSG     MSGID(CPF0864) EXEC(DO)                                           /*CUP033*/
                GOTO       CMDLBL(CHECK)                                                  /*CUP033*/
             ENDDO                                                                        /*CUP033*/
 
             IF         COND(&GISVAL *EQ &GPFXSVAL) THEN(DO)                              /*CUP033*/
                IF         COND(&GIVAL *EQ ' ') THEN(DO)                                  /*CUP033*/
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')                                  /*CUP033*/
                ENDDO                                                                     /*CUP033*/
                ELSE       CMD(DO)                                                        /*CUP033*/
                   CHGVAR     VAR(&GPSBSID) VALUE(&GIVAL)                                 /*CUP033*/
                ENDDO                                                                     /*CUP033*/
/**********     GOTO       CMDLBL(CHECK)                                    */ /*CUP033*/ /*CPK029*/
             ENDDO                                                                        /*CUP033*/
 
             GOTO       CMDLBL(VALIDATE)                                                  /*CUP033*/
 
 CHECK:                                                                                   /*CUP033*/
             IF         COND(&BLANKSVAL *EQ 'Y') THEN(DO)                                 /*CUP033*/
                RTVMSG     MSGID(UPM0003) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
                RTVMSG     MSGID(UPM0004) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (151 50)) VALUE(&MESSAGE)
                CALL       PGM(SCC0010) PARM('UPC000108' 'ENTER' ' ')
                GOTO       CMDLBL(END)
             ENDDO
 
             CHGVAR     VAR(&GVLIB) VALUE(&GPSBSID *TCAT 'GVLIB')
 
/* Create user spaces */
             CRTUSRSPC  USRSPC(QTEMP/DBR) DELETE(*YES)
 
/* Display file description for logical files in DV and DTA libs.    */
             DSPFD      FILE(&GVLIB/*ALL) TYPE(*ACCPTH) +
                          OUTPUT(*OUTFILE) OUTFILE(QTEMP/UPFDAPOF)
             MONMSG     MSGID(CPF3012) EXEC(DO)
                DSPFD      FILE(QAFDACCP) TYPE(*MBR) OUTPUT(*OUTFILE) +
                             OUTFILE(QTEMP/UPFDAPOF)
                CLRPFM     FILE(QTEMP/UPFDAPOF)
             ENDDO
 
/* Call program to retrieve data from user space and produce report. */
/******      CALL       PGM(UP000108) PARM(&GPSBSID &RETURN)         */                   /*CUP028*/
             CALL       PGM(UP000108) PARM(&GPSBSID '*GLOBAL' &RETURN)                    /*CUP028*/
             IF         COND(&RETURN *NE ' ') THEN(DO)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
             GOTO       CMDLBL(END)
 ABNOR:
/* Set up messages for Midas Information Screen  */
/**********  RTVMSG     MSGID(UPM0001) MSGF(UTGMSGF) +                                 */ /*CPK027*/
/**********               MSGDTA('UPC000108') MSG(&MESSAGE)                            */ /*CPK027*/
             RTVMSG     MSGID(UPM0001) MSGF(UTMSGF) +
                          MSGDTA('UPC000108') MSG(&MESSAGE)                               /*CPK027*/
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (201 50)) VALUE('Check +
                          joblog for details')
             MONMSG     MSGID(CPF0000)
             CALL       PGM(SCC0010) PARM('UPC000108' 'JOBLOG' 'Y')
             MONMSG     MSGID(CPF0000 MCH0000)
 
END:
             ENDPGM
