/*********************************************************************/
/*XBI    OVRDBF FILE(UPC000110F) TOFILE(QAFDMBR)                     */
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas UP On-screen compare libraries')                */
/*********************************************************************/
/*                                                                   */
/*       Midas - Upgrade Module                                      */
/*                                                                   */
/*       UPC000110 - Compare libraries                               */
/*                                                                   */
/*       (c) Finastra International Limited 2005                     */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Last Amend No. CSM005  *CREATE    Date 17Aug05              */
/*       Prev Amend No. xxxxxx             Date ddMmmyy              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CSM005 - New program to control Bridge compares.            */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&PREFIX &BASLIB &CMPLIB &MRGLIB &WRKLIB +
                          &JOBD)
 
             DCL        VAR(&PREFIX) TYPE(*CHAR) LEN(2)
             DCL        VAR(&BASLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CMPLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MRGLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&WRKLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBD) TYPE(*CHAR) LEN(20)
 
             DCL        VAR(&JOBDNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBDLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DPLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DVLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SOURCEFND) TYPE(*CHAR) LEN(1) VALUE(N)
             DCL        VAR(&ERRORMSG) TYPE(*CHAR) LEN(100)
             DCL        VAR(&GENPGM) TYPE(*CHAR) LEN(10) +
                          VALUE('UPC000110')
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2005')
 
             DCLF       FILE(UPC000110F)
 
/* Global monitor message. */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ERROR))
 
             CHGJOB     SWS(00000000)
 
/* Set up job description. */
             CHGVAR     VAR(&JOBDNAME) VALUE(%SST(&JOBD 1 10))
             CHGVAR     VAR(&JOBDLIB) VALUE(%SST(&JOBD 11 10))
 
/* Set library names to retrieve UPJOBE* files. */
             CHGVAR     VAR(&DVLIB) VALUE(&PREFIX *TCAT 'GVLIB')
             CHGVAR     VAR(&DPLIB) VALUE(&PREFIX *TCAT 'GPLIB')
 
             CHKOBJ     OBJ(&WRKLIB/UPJOBEPD) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
                MOVOBJ     OBJ(&DPLIB/UPJOBEPD) OBJTYPE(*FILE) +
                          TOLIB(&DVLIB)
                CRTDUPOBJ  OBJ(UPJOBE*) FROMLIB(&DVLIB) OBJTYPE(*FILE) +
                             TOLIB(&WRKLIB)
                MOVOBJ     OBJ(&DVLIB/UPJOBEPD) OBJTYPE(*FILE) +
                          TOLIB(&DPLIB)
             ENDDO
             OVRDBF     FILE(UPJOBEPD) TOFILE(&WRKLIB/UPJOBEPD) +
                          OVRSCOPE(*JOB)
 
/* Create temporary file to hold error messages. */
             DLTF       FILE(QTEMP/UPERRMQT)
             MONMSG     MSGID(CPF2105)
             CRTPF      FILE(QTEMP/UPERRMQT) RCDLEN(100) +
                          TEXT('Temporary file for holding error +
                          messages')
 
/* Create outfile of source members. */
             DSPFD      FILE(&BASLIB/*ALL) TYPE(*MBR) +
                          OUTPUT(*OUTFILE) FILEATR(*PF) +
                          OUTFILE(&WRKLIB/UPC000110F)
             MONMSG     MSGID(CPF3012 CPF3020) EXEC(DO)
/* If there are no source files to compare then send message to report. */
                CHGVAR     VAR(&ERRORMSG) VALUE('There are no source +
                             files to compare.')
                CALL       PGM(UP008010) PARM(*WRITE &GENPGM &ERRORMSG)
                IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                   GOTO       CMDLBL(ERROR)
                ENDDO
                GOTO       CMDLBL(ENDPGM)
             ENDDO
 
             SAVLIBL    SAVDTL(UPC000110)
             RMVLIBLE   LIB(&WRKLIB)
             MONMSG     MSGID(CPF0000)
             ADDLIBLE   LIB(&WRKLIB)
 
READNEXT:
             RCVF
             MONMSG     MSGID(CPF0864) EXEC(DO)
                GOTO       CMDLBL(ENDPGM)
             ENDDO
 
/* Check member belongs to source file and that the source has member. */
             IF         COND(&MBDTAT *NE 'S' *OR &MBNAME *EQ ' ') +
                          THEN(DO)
                GOTO       CMDLBL(READNEXT)
             ENDDO
 
/* Check that member exists in "Compare library". */
             CHKOBJ     OBJ(&CMPLIB/&MBFILE) OBJTYPE(*FILE) +
                          MBR(&MBNAME)
             MONMSG     MSGID(CPF9801 CPF9815) EXEC(DO)
                GOTO       CMDLBL(READNEXT)
             ENDDO
 
/* Submit job for compare. */
             CHGVAR     VAR(&SOURCEFND) VALUE(Y)
             SBMJOB     CMD(CALL PGM(UPC0502) PARM(&MBFILE &CMPLIB +
                          &MBNAME &MBFILE &BASLIB &MBNAME 'Y' +
                          &WRKLIB &MRGLIB)) JOB(&MBNAME) +
                          JOBD(&JOBDLIB/&JOBDNAME)
             MONMSG     MSGID(CPF0000) EXEC(DO)
                CHGVAR     VAR(&ERRORMSG) VALUE('Error on compare for +
                             member' *BCAT &MBNAME *BCAT 'in' *BCAT +
                             &MBFILE *TCAT '.')
                CALL       PGM(UP008010) PARM(*WRITE &GENPGM &ERRORMSG)
                IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                   GOTO       CMDLBL(ERROR)
                ENDDO
                GOTO       CMDLBL(READNEXT)
             ENDDO
 
             GOTO       CMDLBL(READNEXT)
 
 ERROR:
             CHGJOB     SWS(XXXXXX11)
 ENDPGM:
             DLTOVR     FILE(UPJOBEPD) LVL(*JOB)
             MONMSG     MSGID(CPF0000)
 
             RSTLIBL    SAVEDLIBL(UPC000110)
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
 
             IF         COND(&SOURCEFND *EQ 'N') THEN(DO)
/* Send out message that no source members were compared. */
                CHGVAR     VAR(&ERRORMSG) VALUE('No source members were +
                             found to compare.')
                CALL       PGM(UP008010) PARM(*WRITE &GENPGM &ERRORMSG)
                MONMSG     MSGID(CPF0000 MCH0000 RPG0000)
             ENDDO
/* Produce report of messages (if any). */
             CALL       PGM(UP008010) PARM(*REPORT &GENPGM ' ')
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)
 
             ENDPGM
