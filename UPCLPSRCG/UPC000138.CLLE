/*********************************************************************/
/*OVR    OVRDBF FILE(&MLSTFILE) TOFILE(QAFDMBRL)                     */
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas UP Load UPPGMSPD or UPSVPSPD file')             */
/*********************************************************************/
/*                                                                   */
/*       Midas - Bridge                                              */
/*                                                                   */
/*       UPC000138 - Load UPPGMSPD or UPSVPSPD file                  */
/*                                                                   */
/*       Called by: UPC000135                                        */
/*                  UPC000139                                        */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2004           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Last Amend No. CUP029 *CREATE     Date 22SeP04              */
/*       Prev Amend No.                    Date                      */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CUP029 - Cross check module and program sources in a library*/
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&SRCLIB  &MLSTFILE &OUTFILE)
 
             DCL        VAR(&SRCLIB)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&MLSTFILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OUTFILE)  TYPE(*CHAR) LEN(10)
 
             DCL        VAR(&DSPFDYET) TYPE(*CHAR) LEN(1)
             DCL        VAR(&MESSAGE)  TYPE(*CHAR) LEN(50)
 
             DCLF       FILE(UPSRCFLP)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2004')
 
/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CHGJOB     LOG(4 0 *SECLVL) LOGCLPGM(*YES)
 
             CLRPFM     FILE(&OUTFILE)
 
             DLTF       FILE(QTEMP/&MLSTFILE)
             MONMSG     MSGID(CPF2105)
 
READNEXT:
/* Get program source file record from UPSRCFLP */
             RCVF
/* Monitor end of file */
             MONMSG     MSGID(CPF0864) EXEC(GOTO CMDLBL(UPDATE))
 
             DSPFD      FILE(&SRCLIB/&SRFILE) TYPE(*MBRLIST) +
                          OUTPUT(*OUTFILE) OUTFILE(QTEMP/&MLSTFILE) +
                          OUTMBR(*FIRST *ADD)
 
             CHGVAR     VAR(&DSPFDYET) VALUE('Y')
 
             GOTO       CMDLBL(READNEXT)
 
UPDATE:
             IF         COND(&DSPFDYET *EQ 'Y') THEN(DO)
                CPYF       FROMFILE(QTEMP/&MLSTFILE) TOFILE(&OUTFILE) +
                             MBROPT(*ADD) INCREL((*IF MLNRCD *GT 0)) +
                             FMTOPT(*MAP *DROP)
             ENDDO
 
             GOTO       CMDLBL(END)
 
 ABNOR:
             RTVMSG     MSGID(UPM0001) MSGF(UTMSGF) +
                          MSGDTA('UPC000138') MSG(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (201 50)) VALUE('Check +
                          joblog and dump for details')
             MONMSG     MSGID(CPF0000)
             CALL       PGM(SCC0010) PARM('UPC000138' 'JOBLOG' 'Y')
             MONMSG     MSGID(CPF0000 MCH0000)
             CHGJOB     SWS(XXXXXX11)
 
END:
             ENDPGM
