/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas UP Load service pgm and related modules file')  */
/*********************************************************************/
/*                                                                   */
/*       Midas - Bridge                                              */
/*                                                                   */
/*       UPC000139 - Load service program member and related modules */
/*                   file UPSVPSMPD.                                 */
/*                                                                   */
/*       Called by: UPC000135                                        */
/*       Calls    : UPC000138                                        */
/*                  UP000139                                         */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2004           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Last Amend No. CUP029 *CREATE     Date 28SeP04              */
/*       Prev Amend No.                    Date                      */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CUP029 - Cross check module and program sources in a library*/
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&SRCLIB)
 
             DCL        VAR(&SRCLIB)  TYPE(*CHAR) LEN(10)
 
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)
             DCL        VAR(&CPYFYET) TYPE(*CHAR) LEN(1)
 
             DCLF       FILE(UPSVPSPD)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2004')
 
/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CHGJOB     LOG(4 0 *SECLVL) LOGCLPGM(*YES)
             CHGJOB     SWS(XXXXXX00)
 
             DLTDTAARA  DTAARA(QTEMP/MIDASMSG)
             MONMSG     MSGID(CPF0000)
             CRTDTAARA  DTAARA(QTEMP/MIDASMSG) TYPE(*CHAR) LEN(800) +
                          VALUE(' ')
 
             CLRPFM     FILE(UPSVPSMPD)
 
/* Load service program source file UPSVPSPD. */
             OVRDBF     FILE(UPSRCFLP)  TOFILE(UPSRCFLS)
 
             CALL       PGM(UPC000138) PARM(&SRCLIB 'UPSMLSTPD' 'UPSVPSPD')
             DLTOVR     FILE(UPSRCFLP)
 
/* If error, exit program. */
             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO CMDLBL(END))
 
READNEXT:
/* Get service program member record from UPSVPSPD */
             RCVF
/* Monitor end of file */
             MONMSG     MSGID(CPF0864) EXEC(GOTO CMDLBL(END))
 
/* Copy member records with '/*EXI' to input file UPIRECPD. */
             CPYF       FROMFILE(&SRCLIB/&MLFILE) TOFILE(UPIRECPD) +
                          FROMMBR(&MLNAME) MBROPT(*REPLACE) +
                          INCCHAR(*RCD 1 *CT '/*EXI') FMTOPT(*CVTSRC)
 
/* Write to UPSVPSMPD for each module listed in the header box */
             CALL       PGM(UP000139) PARM(&MLFILE &MLNAME)
 
/* If error, go to error handling. */
             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                          CMDLBL(ABNOR))
 
             GOTO       CMDLBL(READNEXT)
 
 ABNOR:
             RTVMSG     MSGID(UPM0001) MSGF(UTMSGF) +
                          MSGDTA('UPC000139') MSG(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (201 50)) VALUE('Check +
                          joblog and dump for details')
             MONMSG     MSGID(CPF0000)
             CALL       PGM(SCC0010) PARM('UPC000139' 'JOBLOG' 'Y')
             MONMSG     MSGID(CPF0000 MCH0000)
             CHGJOB     SWS(XXXXXX11)
 
END:
             ENDPGM
