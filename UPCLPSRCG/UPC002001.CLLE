/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas UP Shell to drop and create stored procs.')     */
/*********************************************************************/
/*                                                                   */
/*       Midas - Bridge                                              */
/*                                                                   */
/*       UPC002001 - Shell to drop and create stored procedures      */
/*                                                                   */
/*       (c) Finastra International Limited 2004                     */
/*                                                                   */
/*       Last Amend No. CUP041   *REWRITE  Date 27May14              */
/*       Prev Amend No. CSC043             Date 18Jun10              */
/*                      BUG27785           Date 17May10              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/*                      BUG27639           Date 07May10              */
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/*                      CUP033             Date 18Sep06              */
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*                      CSM005             Date 21Sep05              */
/*                      CUP094             Date 11Feb05              */
/*                      CPK021  *CREATE    Date 23Sep04              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CUP041 - New Bridge methodology to combine menu options     */
/*       CSC043 - MidasPlus IASP Enablement                          */
/*       BUG27785 - Use overrides rather than renames.               */
/*       BUG27639 - Rename GZSDBANKPD and GZSDBANKL1 for System      */
/*                  Values Maintenance in Bridge Library             */
/*       CUP033 - Removal of System Value /COPYs                     */
/*       CSM005 - Changes to /COPYs.  Recompile.                     */
/*       CUP094 - Upgrade not selecting between global and zone.     */
/*       CPK021 - New Bridge option.                                 */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&MODE &LAYER &PFX &BRGSRCLIB &RTNFLAG)
 
             DCL        VAR(&MODE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&LAYER) TYPE(*CHAR) LEN(7)
             DCL        VAR(&PFX) TYPE(*CHAR) LEN(2)
             DCL        VAR(&BRGSRCLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&RTNFLAG) TYPE(*CHAR) LEN(1)
 
             DCL        VAR(&XPLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CMPSTRING) TYPE(*CHAR) LEN(8)
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2004')
 
/* Global monitor message. */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ERROR))
 
/* Delete the driving file in QTEMP if it already exists. */
             DLTF       FILE(QTEMP/UPDLVC*)
             MONMSG     MSGID(CPF0000)
 
             IF         COND(&LAYER *EQ '*ZONE') THEN(DO)
                CHGVAR     VAR(&XPLIB) VALUE(&PFX *TCAT 'XPLIB')
                CHGVAR     VAR(&CMPSTRING) VALUE('SQLSRCZ ')
             ENDDO
             IF         COND(&LAYER *EQ '*GLOBAL') THEN(DO)
                CHGVAR     VAR(&XPLIB) VALUE(&PFX *TCAT 'GXPLIB')
                CHGVAR     VAR(&CMPSTRING) VALUE('SQLSRCG ')
             ENDDO
 
             IF         COND(&MODE *EQ '*IMP') THEN(DO)
/* Call program to build driving file for SQL procedures. */
                CALL       PGM(UPC000002) PARM(&BRGSRCLIB &CMPSTRING)
                IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                   GOTO       CMDLBL(ERROR)
                ENDDO
                OVRDBF     FILE(UPDLVCPD) TOFILE(QTEMP/UPDLVCPD) +
                             OVRSCOPE(*JOB)
             ENDDO
 
/* Call program to drop and create procedures. */
             CALL       PGM(UPC000001) PARM(&XPLIB &BRGSRCLIB +
                          &CMPSTRING)
             DLTOVR     FILE(UPDLVCPD) LVL(*JOB)
             MONMSG     MSGID(CPF0000)
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                GOTO       CMDLBL(ERROR)
             ENDDO
 
             GOTO       CMDLBL(ENDPGM)
ERROR:
             CHGVAR     VAR(&RTNFLAG) VALUE('E')
 
ENDPGM:
             ENDPGM
