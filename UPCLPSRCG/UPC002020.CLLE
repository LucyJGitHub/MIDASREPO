/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas UP Access on-screen compares with sys. vals.')  */
/*********************************************************************/
/*                                                                   */
/*       Midas - Upgrade Module                                      */
/*                                                                   */
/*       UPC002020 - Access on-screen compares using system values   */
/*                                                                   */
/*       (c) Finastra International Limited 2005                     */
/*                                                                   */
/*       Last Amend No. MD054605           Date 17Oct19              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/*       Prev Amend No. CUP034             Date 16Jan07              */
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*                      CSM005  *CREATE    Date 23Aug05              */
/*                                                                   */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD054605 - Deliverable Data Split for SDSVALPD              */
/*       CUP034 - Reduce size of implementation library              */
/*       CSM005 - New program to control Bridge compares.            */
/*                                                                   */
/*********************************************************************/
/**********  PGM        PARM(&WRKSVAL)                                                 */ /*CUP034*/
             PGM        PARM(&WRKSVAL &GPFXSVAL)                                          /*CUP034*/

             DCL        VAR(&WRKSVAL) TYPE(*CHAR) LEN(20)
             DCL        VAR(&GPFXSVAL) TYPE(*CHAR) LEN(20)                                /*CUP034*/

             DCL        VAR(&WRKLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&GSBSID) TYPE(*CHAR) LEN(2)                                   /*CUP034*/

             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)
             DCL        VAR(&PROGRAM) TYPE(*CHAR) LEN(10) +
                          VALUE('UPC002020')
             DCL        VAR(&BLANKSVAL) TYPE(*CHAR) LEN(1) VALUE('N')

             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2005')

/**********  DCLF       FILE(UPGSVALJ0) */                                              /*MD054605*/
             DCLF       FILE(UPGSVALJ0) ALWNULL(*YES)                                   /*MD054065*/

/* Global monitor message. */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ERROR))

             CHGJOB     SWS(00000000)

/* Check that System Values have an entry. */
VALIDATE:
             RCVF
             MONMSG     MSGID(CPF0864) EXEC(DO)
                GOTO       CMDLBL(CHECK)
             ENDDO

             IF         COND(&GISVAL *EQ &WRKSVAL) THEN(DO)
                IF         COND(&GIVAL *EQ ' ') THEN(DO)
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')
                ENDDO
                ELSE       CMD(DO)
                   CHGVAR     VAR(&WRKLIB) VALUE(&GIVAL)
                ENDDO
             ENDDO

             IF         COND(&GISVAL *EQ &GPFXSVAL) THEN(DO)                              /*CUP034*/
                IF         COND(&GIVAL *EQ ' ') THEN(DO)                                  /*CUP034*/
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')                                  /*CUP034*/
                ENDDO                                                                     /*CUP034*/
                ELSE       CMD(DO)                                                        /*CUP034*/
                   CHGVAR     VAR(&GSBSID) VALUE(&GIVAL)                                  /*CUP034*/
                ENDDO                                                                     /*CUP034*/
             ENDDO                                                                        /*CUP034*/

             GOTO       CMDLBL(VALIDATE)

 CHECK:
             IF         COND(&BLANKSVAL *EQ 'Y') THEN(DO)
                RTVMSG     MSGID(UPM0003) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
                RTVMSG     MSGID(UPM0004) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (151 50)) VALUE(&MESSAGE)
                CALL       PGM(SCC0010) PARM('UPC002020' 'ENTER' ' ')
                GOTO       CMDLBL(ENDPGM)
             ENDDO

/* Save library list and make sure work library is at top. */
/**********  SAVLIBL    SAVDTL(UPC002020)                                              */ /*CUP034*/
             CALL       PGM(GPC000016) PARM(&GSBSID)                                      /*CUP034*/
             RMVLIBLE   LIB(&WRKLIB)
             MONMSG     MSGID(CPF0000)
             ADDLIBLE   LIB(&WRKLIB)

/* Call program to perform actual compares. */
/**********  CALL       PGM(UP000030) PARM(&WRKLIB)                                    */ /*CUP034*/
             CALL       PGM(UP000531) PARM(&WRKLIB)                                       /*CUP034*/
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                CHGVAR     VAR(&PROGRAM) VALUE('UP0030    ')
                GOTO       CMDLBL(ERROR)
             ENDDO

             GOTO       CMDLBL(ENDPGM)

 ERROR:
             RTVMSG     MSGID(UPM0001) MSGF(UTMSGF) MSGDTA(&PROGRAM) +
                          MSG(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (201 50)) VALUE('Check +
                          joblog for details.')
             MONMSG     MSGID(CPF0000)
             CALL       PGM(SCC0010) PARM('UPC002020' 'JOBLOG' 'Y')
             MONMSG     MSGID(CPF0000 MCH0000)
 ENDPGM:
/**********  RSTLIBL    SAVEDLIBL(UPC002020)                                           */ /*CUP034*/
/**********  MONMSG     MSGID(CPF0000)                                                 */ /*CUP034*/

             ENDPGM
