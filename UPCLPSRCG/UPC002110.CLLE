/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas UP Check for possible recompilations')          */
/*********************************************************************/
/*                                                                   */
/*       Midas - Upgrade Module                                      */
/*                                                                   */
/*       UPC002110 - Check for recompilations                        */
/*                                                                   */
/*       (c) Finastra International Limited 2005                     */
/*                                                                   */
/*       Last Amend No. MD054605           Date 17Oct19              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. CSM005  *CREATE    Date 21Sep05              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD054605 - Deliverable Data Split for SDSVALPD              */
/*       CSM005 - New shell for recompilation checks.                */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&CHKSVAL &WRKSVAL)

             DCL        VAR(&CHKSVAL) TYPE(*CHAR) LEN(20)
             DCL        VAR(&WRKSVAL) TYPE(*CHAR) LEN(20)

             DCL        VAR(&CHKLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&WRKLIB) TYPE(*CHAR) LEN(10)

             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)
             DCL        VAR(&BLANKSVAL) TYPE(*CHAR) LEN(1) VALUE('N')

             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2005')

/**********  DCLF       FILE(UPGSVALJ0) */                                              /*MD054605*/
             DCLF       FILE(UPGSVALJ0) ALWNULL(*YES)                                   /*MD054605*/

/* Global monitor message. */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ERROR))

             CHGJOB     SWS(00000000)

/* Create data area MIDASMSG in QTEMP. */
             DLTDTAARA  DTAARA(QTEMP/MIDASMSG)
             MONMSG     MSGID(CPF0000)
             CRTDTAARA  DTAARA(QTEMP/MIDASMSG) TYPE(*CHAR) LEN(800) +
                          VALUE(' ')

/* Check that System Values have an entry. */
VALIDATE:
             RCVF
             MONMSG     MSGID(CPF0864) EXEC(DO)
                GOTO       CMDLBL(CHECK)
             ENDDO

             IF         COND(&GISVAL *EQ &CHKSVAL) THEN(DO)
                IF         COND(&GIVAL *EQ ' ') THEN(DO)
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')
                ENDDO
                ELSE       CMD(DO)
                   CHGVAR     VAR(&CHKLIB) VALUE(&GIVAL)
                ENDDO
             ENDDO

             IF         COND(&GISVAL *EQ &WRKSVAL) THEN(DO)
                IF         COND(&GIVAL *EQ ' ') THEN(DO)
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')
                ENDDO
                ELSE       CMD(DO)
                   CHGVAR     VAR(&WRKLIB) VALUE(&GIVAL)
                ENDDO
             ENDDO

             GOTO       CMDLBL(VALIDATE)

 CHECK:
             IF         COND(&BLANKSVAL *EQ 'Y') THEN(DO)
                RTVMSG     MSGID(UPM0003) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
                RTVMSG     MSGID(UPM0004) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (151 50)) VALUE(&MESSAGE)
                CALL       PGM(SCC0010) PARM('UPC002110' 'ENTER' ' ')
                GOTO       CMDLBL(ENDPGM)
             ENDDO

/* Save library list and make sure work library is at top. */
             SAVLIBL    SAVDTL(UPC002110)
             RMVLIBLE   LIB(&WRKLIB)
             MONMSG     MSGID(CPF0000)
             ADDLIBLE   LIB(&WRKLIB)

/* Display program references for all programs in the library to be checked. */
             DSPPGMREF  PGM(&CHKLIB/*ALL) OUTPUT(*OUTFILE) +
                          OBJTYPE(*ALL) OUTFILE(QTEMP/UPC002110F)

/* Find all files that are on  the deliverables list and the check library */
             OPNQRYF    FILE((UPDLVCPD) (UPC002110F)) +
                          FORMAT(UPRCP1QF) QRYSLT('WHOBJT *EQ "F"') +
                          KEYFLD((R1PNAM) (R1FNAM)) JFLD((AUMBNM +
                          WHFNAM *EQ)) MAPFLD((R1FNAM WHFNAM) +
                          (R1LIB WHLIB) (R1PNAM WHPNAM) (R1TEXT +
                          WHTEXT) (R1OTYP WHOTYP))
             CPYFRMQRYF FROMOPNID(UPDLVCPD) TOFILE(&WRKLIB/UPRCP1QF) +
                          MBROPT(*REPLACE) CRTFILE(*YES)
             CLOF       OPNID(UPDLVCPD)

/* Find and report on programs that have not been delivered.  */
             OPNQRYF    FILE((UPRCP1QF) (UPDLVCPD)) FORMAT(UPRCP2QF) +
                          KEYFLD((R2PNAM) (R2FNAM)) JFLD((R1PNAM +
                          AUMBNM *EQ)) JDFTVAL(*ONLYDFT) +
                          JORDER(*FILE) MAPFLD((R2FNAM R1FNAM) +
                          (R2LIB R1LIB) (R2PNAM R1PNAM) (R2TEXT +
                          R1TEXT) (R2OTYP R1OTYP))
             CPYFRMQRYF FROMOPNID(UPRCP1QF) TOFILE(&WRKLIB/UPRCP2QF) +
                          MBROPT(*REPLACE) CRTFILE(*YES)
             CALL       PGM(UP0152)
             CLOF       OPNID(UPRCP1QF)

             GOTO       CMDLBL(ENDPGM)

 ERROR:
             RTVMSG     MSGID(UPM0001) MSGF(UTMSGF) +
                          MSGDTA('UPC002110') MSG(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (201 50)) VALUE('Check +
                          joblog for details.')
             MONMSG     MSGID(CPF0000)
             CALL       PGM(SCC0010) PARM('UPC002110' 'JOBLOG' 'Y')
             MONMSG     MSGID(CPF0000 MCH0000)
 ENDPGM:
             RSTLIBL    SAVEDLIBL(UPC002110)
             MONMSG     MSGID(CPF0000)

             ENDPGM
