/*********************************************************************/
/*XBI    OVRDBF FILE(UPDLVCQT) TOFILE(UPDLVCPD)                      */
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas UP Review/Conflict/Process *ALL DDS Files')     */
/*********************************************************************/
/*                                                                   */
/*       Midas - Bridge                                              */
/*                                                                   */
/*       UPC008004 - Review/Conflict/Process *ALL DDS Files          */
/*                                                                   */
/*       (c) Finastra International Limited 2019                     */
/*                                                                   */
/*       Last Amend No. MD061461           Date 01Jun23              */
/*       Prev Amend No. MD054955           Date 16Dec19              */
/*                      MD055189           Date 03Feb20              */
/*                      MD054605           Date 17Oct19              */
/*                      MD054209 *CREATE   Date 31Aug19              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD061461 - Include BTD and XTD update                       */
/*       MD054955 - Deliverable Data Split for Correspondence Mgr    */
/*       MD055189 - Issue with GPWIPCTD as file has null fields      */
/*                  which cannot be handled by CMPPF command.        */
/*       MD054605 - Deliverable Data Split for SDSVALPD              */
/*       MD054209 - Deliverable Data Split for T_WIPCF               */
/*                                                                   */
/*********************************************************************/
/**********  PGM        PARM(&MODE &BRGDTALIB) */                                       /*MD054955*/
             PGM        PARM(&MODE &BRGDTALIB &SAVLIB)                                  /*MD054955*/

             DCL        VAR(&MODE) TYPE(*CHAR) LEN(9)
             DCL        VAR(&BRGDTALIB) TYPE(*CHAR) LEN(10)

             DCL        VAR(&OLDLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&RETURN) TYPE(*CHAR) LEN(1)
             DCL        VAR(&ERROR)    TYPE(*CHAR) LEN(1)
/* Parameters passed to UP008010. */
             DCL        VAR(&GENPGM)   TYPE(*CHAR) LEN(10)  VALUE('UPC8004')
             DCL        VAR(&ERRORMSG) TYPE(*CHAR) LEN(100)
             DCL        VAR(&SAVLIB) TYPE(*CHAR) LEN(10)                                /*MD054955*/

             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2019')

             DCLF       FILE(UPDLVCQT)

             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))

READNEXT:
             RCVF
             MONMSG     MSGID(CPF0864) EXEC(DO)
                GOTO       CMDLBL(END)
             ENDDO

/* Find out where the current deliverable data file is. */
             RTVOBJD    OBJ(&AUMBNM) OBJTYPE(*FILE) RTNLIB(&OLDLIB)

/* REVIEW */
             IF         COND(&MODE *EQ '*REVIEW') THEN(DO)
             ENDDO

/* CONFLICT */
             IF         COND(&MODE *EQ '*CONFLICT') THEN(DO)
                                                                                        /*MD061461*/
/* No need to report conflict in case of BTD */                                         /*MD061461*/
             IF         COND(%SUBSTRING(&AUMBNM 6 3) *EQ 'BTD') +
                          THEN(GOTO CMDLBL(READNEXT))                                   /*MD061461*/
                                                                                        /*MD061461*/
             IF         COND(&AUMBNM *NE 'GPWIPCTD') THEN(DO)                           /*MD055189*/
/**********  CALL       PGM(UPC000702) PARM(&AUMBNM &BRGDTALIB)               /*MD054605* MD054955*/
             CALL       PGM(UPC000702) PARM(&AUMBNM &BRGDTALIB &SAVLIB)                 /*MD054955*/
             ENDDO                                                                      /*MD055189*/
             ENDDO

/* PROCESS  */
             IF         COND(&MODE *EQ '*PROCESS') THEN(DO)
                CALL       PGM(UPC000700) PARM(&AUMBNM &BRGDTALIB)
             ENDDO

/* If an error, send error line and read next record. */
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                CHGJOB     SWS(XXXXXX00)
                CHGVAR     VAR(&ERROR) VALUE('Y')
                CHGVAR     VAR(&ERRORMSG) VALUE('Error when running +
                             Action File for' *BCAT &AUMBNM)
                CALL       PGM(UP008010) PARM('*WRITE' &GENPGM &ERRORMSG)
                IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                   GOTO       CMDLBL(ABNOR)
                ENDDO
             ENDDO

             GOTO       CMDLBL(READNEXT)

ABNOR:
             CHGJOB     SWS(XXXXXX11)
             GOTO       CMDLBL(ABNOREND)

END:
/* Call program to produce report. */
             IF         COND(&ERROR *EQ 'Y') THEN(DO)
                CALL       PGM(UP008010) PARM('*REPORT' &GENPGM ' ')
                IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                   GOTO       CMDLBL(ABNOREND)
                ENDDO
                CHGSPLFA   FILE(UP008010P1) SPLNBR(*LAST) USRDTA(&MODE)
             ENDDO

ABNOREND:
             ENDPGM
