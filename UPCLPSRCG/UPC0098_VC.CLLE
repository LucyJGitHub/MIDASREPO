/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas UP MRGDLVLST Validity Checker')                 */
/*********************************************************************/
/*                                                                   */
/*       Midas - Upgrade Module                                      */
/*                                                                   */
/*       UPC0098_VC - Validity checker for MRGDLVLST                 */
/*                                                                   */
/*       (c) Finastra International Limited 2001                     */
/*                                                                   */
/*       Last Amend No. CUP042 *REWRITE    Date 20May15              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/* Midas Release 4 --------------- Base -----------------------------*/
/* Midas DBA 3.02 ---------------------------------------------------*/
/*       Prev Amend No. CUP005 *CREATE     Date 29Oct99              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CUP042 - Simplification of deliverables list.               */
/*       CUP005 - Changes for DBA R3.02                              */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&BASLIB &MRGLIB &TGTLIB)
 
             DCL        VAR(&BASLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MRGLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TGTLIB) TYPE(*CHAR) LEN(10)
 
             DCL        VAR(&MSGSUB) TYPE(*CHAR) LEN(30)
             DCL        VAR(&MSGTXT) TYPE(*CHAR) LEN(132)
             DCL        VAR(&ERROR) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&RECURSIVE) TYPE(*CHAR) LEN(1) +
                          VALUE('N')
             DCL        VAR(&MSGF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MSGFLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGDTA) TYPE(*CHAR) LEN(128)
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2001')
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ERROR))
 
/* Check if the base library exists and the user has authority. */
             CHKOBJ     OBJ(&BASLIB) OBJTYPE(*LIB) AUT(*CHANGE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
                CHGVAR     VAR(&MSGSUB) VALUE(&BASLIB *CAT 'QSYS')
                RTVMSG     MSGID(CPF9801) MSGF(QCPFMSG) +
                             MSGDTA(&MSGSUB) MSG(&MSGTXT)
                CHGVAR     VAR(&ERROR) VALUE('Y')
                GOTO       CMDLBL(ERROR)
             ENDDO
             MONMSG     MSGID(CPF9802) EXEC(DO)
                CHGVAR     VAR(&MSGSUB) VALUE(&BASLIB *CAT 'QSYS')
                RTVMSG     MSGID(CPF9802) MSGF(QCPFMSG) +
                             MSGDTA(&MSGSUB) MSG(&MSGTXT)
                CHGVAR     VAR(&ERROR) VALUE('Y')
                GOTO       CMDLBL(ERROR)
             ENDDO
 
/* Check if the merge library exists and the user has authority. */
             CHKOBJ     OBJ(&MRGLIB) OBJTYPE(*LIB) AUT(*CHANGE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
                CHGVAR     VAR(&MSGSUB) VALUE(&MRGLIB *CAT 'QSYS')
                RTVMSG     MSGID(CPF9801) MSGF(QCPFMSG) +
                             MSGDTA(&MSGSUB) MSG(&MSGTXT)
                CHGVAR     VAR(&ERROR) VALUE('Y')
                GOTO       CMDLBL(ERROR)
             ENDDO
             MONMSG     MSGID(CPF9802) EXEC(DO)
                CHGVAR     VAR(&MSGSUB) VALUE(&MRGLIB *CAT 'QSYS')
                RTVMSG     MSGID(CPF9802) MSGF(QCPFMSG) +
                             MSGDTA(&MSGSUB) MSG(&MSGTXT)
                CHGVAR     VAR(&ERROR) VALUE('Y')
                GOTO       CMDLBL(ERROR)
             ENDDO
 
/* Check if the target library exists and the user has authority if '*BASELIB' */
/*  not specified.                                                             */
             IF         COND(&TGTLIB *NE '*BASELIB') THEN(DO)
                CHKOBJ     OBJ(&TGTLIB) OBJTYPE(*LIB) AUT(*CHANGE)
                MONMSG     MSGID(CPF9801) EXEC(DO)
                   CHGVAR     VAR(&MSGSUB) VALUE(&TGTLIB *CAT 'QSYS')
                   RTVMSG     MSGID(CPF9801) MSGF(QCPFMSG) +
                                MSGDTA(&MSGSUB) MSG(&MSGTXT)
                   CHGVAR     VAR(&ERROR) VALUE('Y')
                   GOTO       CMDLBL(ERROR)
                ENDDO
                MONMSG     MSGID(CPF9802) EXEC(DO)
                   CHGVAR     VAR(&MSGSUB) VALUE(&TGTLIB *CAT 'QSYS')
                   RTVMSG     MSGID(CPF9802) MSGF(QCPFMSG) +
                                MSGDTA(&MSGSUB) MSG(&MSGTXT)
                   CHGVAR     VAR(&ERROR) VALUE('Y')
                   GOTO       CMDLBL(ERROR)
                ENDDO
             ENDDO
 
/* Check if the driving file exists in the base library. */
             CHKOBJ     OBJ(&BASLIB/UPDLVCPD) OBJTYPE(*FILE) +
                          AUT(*CHANGE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
                CHGVAR     VAR(&MSGSUB) VALUE('UPDLVCPD  ' *CAT &BASLIB)
                RTVMSG     MSGID(CPF9801) MSGF(QCPFMSG) +
                             MSGDTA(&MSGSUB) MSG(&MSGTXT)
                CHGVAR     VAR(&ERROR) VALUE('Y')
                GOTO       CMDLBL(ERROR)
             ENDDO
             MONMSG     MSGID(CPF9802) EXEC(DO)
                CHGVAR     VAR(&MSGSUB) VALUE('UPDLVCPD  ' *CAT &BASLIB)
                RTVMSG     MSGID(CPF9802) MSGF(QCPFMSG) +
                             MSGDTA(&MSGSUB) MSG(&MSGTXT)
                CHGVAR     VAR(&ERROR) VALUE('Y')
                GOTO       CMDLBL(ERROR)
             ENDDO
 
/* Check if the driving file exists in the merge library. */
             CHKOBJ     OBJ(&MRGLIB/UPDLVCPD) OBJTYPE(*FILE) +
                          AUT(*CHANGE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
                CHGVAR     VAR(&MSGSUB) VALUE('UPDLVCPD  ' *CAT &MRGLIB)
                RTVMSG     MSGID(CPF9801) MSGF(QCPFMSG) +
                             MSGDTA(&MSGSUB) MSG(&MSGTXT)
                CHGVAR     VAR(&ERROR) VALUE('Y')
                GOTO       CMDLBL(ERROR)
             ENDDO
             MONMSG     MSGID(CPF9802) EXEC(DO)
                CHGVAR     VAR(&MSGSUB) VALUE('UPDLVCPD  ' *CAT &MRGLIB)
                RTVMSG     MSGID(CPF9802) MSGF(QCPFMSG) +
                             MSGDTA(&MSGSUB) MSG(&MSGTXT)
                CHGVAR     VAR(&ERROR) VALUE('Y')
                GOTO       CMDLBL(ERROR)
             ENDDO
 
             GOTO       CMDLBL(ENDPGM)
 
ERROR:
/* If validity error then send message back to command . . . */
             IF         COND(&ERROR *EQ 'Y') THEN(DO)
                CHGVAR     VAR(&MSGTXT) VALUE('0000' *TCAT &MSGTXT)
                SNDPGMMSG  MSGID(CPD0006) MSGF(QCPFMSG) MSGDTA(&MSGTXT) +
                             MSGTYPE(*DIAG)
                SNDPGMMSG  MSGID(CPF0002) MSGF(QCPFMSG) MSGTYPE(*ESCAPE)
             ENDDO
/* . . .  else send generic message. */
             ELSE       CMD(DO)
                IF         COND(&RECURSIVE *EQ 'N') THEN(DO)
                   CHGVAR     VAR(&RECURSIVE) VALUE('Y')
RCVMSG:
                   RCVMSG     MSGTYPE(*DIAG) MSGDTA(&MSGDTA) MSGID(&MSGID) +
                                MSGF(&MSGF) SNDMSGFLIB(&MSGFLIB)
                   IF         COND(&MSGID *NE ' ') THEN(DO)
                      SNDPGMMSG  MSGID(&MSGID) MSGF(&MSGFLIB/&MSGF) +
                                   MSGDTA(&MSGDTA) MSGTYPE(*DIAG)
                      GOTO       CMDLBL(RCVMSG)
                   ENDDO
                   RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSGDTA) MSGID(&MSGID) +
                                MSGF(&MSGF) SNDMSGFLIB(&MSGFLIB)
                   SNDPGMMSG  MSGID(&MSGID) MSGF(&MSGFLIB/&MSGF) +
                                MSGDTA(&MSGDTA) MSGTYPE(*ESCAPE)
                ENDDO
             ENDDO
 
ENDPGM:
             ENDPGM
