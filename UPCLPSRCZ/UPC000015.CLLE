/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas UP Save DM and DTA libraries')                  */
/*********************************************************************/
/*                                                                   */
/*       Midas - Bridge                                              */
/*                                                                   */
/*       UPC000015 - Save DM and DTA libraries                       */
/*                                                                   */
/*       (c) Finastra International Limited 2010                     */
/*                                                                   */
/*       Last Amend No. MD054605           Date 17Oct19              */
/*       Prev Amend No. MD046248           Date 27Oct17              */
/*                      CUP002  *CREATE    Date 28Jul10              */
/*                      XXXXXX             Date DDMmmYY              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD054605 - Deliverable Data Split for SDSVALPD              */
/*       MD046248 - Finastra Rebranding                              */
/*       CUP002 - Save of DM and DTA libraries.                      */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&PFXSVAL)

             DCL        VAR(&PFXSVAL) TYPE(*CHAR) LEN(20)

             DCL        VAR(&SBSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DILIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DMLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DTLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DMSAVF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DTSAVF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DATETIME) TYPE(*CHAR) LEN(20)
             DCL        VAR(&BLANKSVAL) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)

/**********  DCLF       FILE(UPSVALJ0) */                                               /*MD054605*/
             DCLF       FILE(UPSVALJ0) ALWNULL(*YES)                                    /*MD054605*/

             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2010')

/* Global monitor message. */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))

             CHGJOB     LOG(4 0 *SECLVL) LOGCLPGM(*YES)

/* Create data area /MIDASMSG in QTEMP. */
             DLTDTAARA  DTAARA(QTEMP/MIDASMSG)
             MONMSG     MSGID(CPF0000)
             CRTDTAARA  DTAARA(QTEMP/MIDASMSG) TYPE(*CHAR) LEN(800) +
                          VALUE(' ')

VALIDATE:
             RCVF
             MONMSG     MSGID(CPF0864) EXEC(DO)
                GOTO       CMDLBL(CHECK)
             ENDDO

             IF         COND(&GISVAL *EQ &PFXSVAL) THEN(DO)
                IF         COND(&GIVAL *EQ ' ') THEN(DO)
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')
                ENDDO
                ELSE       CMD(DO)
                   CHGVAR     VAR(&SBSID) VALUE(&GIVAL)
                ENDDO
                GOTO       CMDLBL(CHECK)
             ENDDO

             GOTO       CMDLBL(VALIDATE)

 CHECK:
             IF         COND(&BLANKSVAL *EQ 'Y') THEN(DO)
                RTVMSG     MSGID(UPM0003) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
                RTVMSG     MSGID(UPM0004) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (151 50)) VALUE(&MESSAGE)
                CALL       PGM(SCC0010) PARM('UPC000015' 'ENTER' ' ')
                GOTO       CMDLBL(ENDPGM)
             ENDDO

             CALL       PGM(UPC0014) PARM(&SBSID)

/* Retrieve date / time to provide unique name. */
             RTVJOBA    DATETIME(&DATETIME)

             CHGVAR     VAR(&DILIB) VALUE(&SBSID *TCAT 'DILIB')
             CHGVAR     VAR(&DMLIB) VALUE(&SBSID *TCAT 'DMLIB')
             CHGVAR     VAR(&DTLIB) VALUE(&SBSID *TCAT 'DTALIB')
             CHGVAR     VAR(&DMSAVF) VALUE('DM' *TCAT %SST(&DATETIME +
                          5 8))
             CHGVAR     VAR(&DTSAVF) VALUE('DT' *TCAT %SST(&DATETIME +
                          5 8))

             CRTSAVF    FILE(&DILIB/&DMSAVF) TEXT('Save of DMLIB -' +
                          *BCAT &DATETIME)
             CRTSAVF    FILE(&DILIB/&DTSAVF) TEXT('Save of DTALIB -' +
                          *BCAT &DATETIME)
             SAVLIB     LIB(&DMLIB) DEV(*SAVF) SAVF(&DILIB/&DMSAVF)
             SAVLIB     LIB(&DTLIB) DEV(*SAVF) SAVF(&DILIB/&DTSAVF)

             GOTO       CMDLBL(ENDPGM)
ABNOR:
/* Set up messages for Midas Information Screen  */
             RTVMSG     MSGID(UPM0001) MSGF(UTMSGF) +
                          MSGDTA('UPC000015') MSG(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (201 50)) VALUE('Check +
                          joblog for details')
             MONMSG     MSGID(CPF0000)
             CALL       PGM(SCC0010) PARM('UPC000015' 'ENTER' ' ')
             MONMSG     MSGID(CPF0000)

ENDPGM:
             ENDPGM
