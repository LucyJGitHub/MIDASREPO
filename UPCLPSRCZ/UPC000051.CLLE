/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas UP ADBU script geneation for zone')             */
/*********************************************************************/
/*                                                                   */
/*       Midas - Upgrade Module                                      */
/*                                                                   */
/*       UPC000051 - ADBU script geneation for zone                  */
/*                                                                   */
/*       (c) Finastra International Limited 2020                     */
/*                                                                   */
/*       Last Amend No. MD060960           Date 16Jan23              */
/*       Prev Amend No. CUT017 *CREATE     Date 17Mar20              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD060960 - Some dependents do not exist yet as their        */
/*                  CRTDUPOBJ has not been executed yet. Fix is to   */
/*                  rebuild the order.                               */
/*       CUT017 - Adapptive Database Upgrade                         */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&PRFX &REFSYS &BRGBRGLIB)

             DCL        VAR(&SEQC) TYPE(*DEC) LEN(15 5) VALUE(0)
             DCL        VAR(&PRFX) TYPE(*CHAR) LEN(2)
             DCL        VAR(&REFSYS) TYPE(*CHAR) LEN(2)
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&BRGBRGLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&RETURN) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FLDUPD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FLDVAL) TYPE(*CHAR) LEN(100)
             DCL        VAR(&ERROR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DTQN) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGLEN) TYPE(*DEC) LEN(5 0) VALUE(50)
             DCL        VAR(&MSGDATA) TYPE(*CHAR) LEN(50)
             DCL        VAR(&RCVWAIT) TYPE(*DEC) LEN(5 0) VALUE(-1)
             DCL        VAR(&DTAQLIBL) TYPE(*CHAR) LEN(10) VALUE('*LIBL')
             DCL        VAR(&COUNT) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&FAIL) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&WMSG) TYPE(*CHAR) LEN(50) VALUE('DONE')

             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2020')

/** Global Monitor Message */

             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO CMDLBL(ABNOR))

/* Update Script Work File job Status with 'Running' */
             CHGVAR        VAR(&FLDUPD) VALUE('UPSCRS')
             CHGVAR     VAR(&FLDVAL) VALUE('R')
             CALL UPZUPDWR ('*UPDATE   ' &PRFX &FLDUPD &FLDVAL &ERROR &RETURN)
             IF         COND(&ERROR *NE '          ') THEN(GOTO ABNOR)

/** Create DTAQ for sbmedt jobs */
             CHGVAR     VAR(&DTQN) VALUE('UP000051')
             CHGVAR     VAR(&PLIB) VALUE(&PRFX *TCAT 'DPLIB')
             DLTDTAQ    DTAQ(&PLIB/&DTQN)
             MONMSG     MSGID(CPF2105)
             CRTDTAQ    DTAQ(&PLIB/&DTQN) MAXLEN(50) TEXT('UP +
                           Server DTAQ for UP000051')

/** Clear script file */
             CLRPFM     FILE(UPZSCRTD)

/* pre-step: need to re-generate List of dependent files based on driver file */
/* as driver may have been changed */
/*If *GLOBAL */

             CLRPFM     FILE(UPZXRFPD)
             OVRDBF     FILE(UPXREFPD) TOFILE(UPZXRFPD)
             OVRDBF     FILE(UPCLOGPD) TOFILE(UPZLOGPD)

             CALL       PGM(UPXREFLS2) PARM('*ZONE')

/* 1. generate script for LF to be deleted */
             CLRPFM     FILE(UPZOPDTD)
             CALL       PGM(UP000081) PARM(&PRFX &REFSYS 'P' &ERROR)
             IF         COND(&ERROR *NE '          ') THEN(GOTO ABNOR)

             CALL       PGM(UP000050) PARM(&PRFX &SEQC &BRGBRGLIB &RETURN)
             IF         COND(&RETURN *NE '          ') THEN(GOTO ABNOR)

/* 1.1 generate script for LF to be deleted not listed yet */
/* as an example: FCRCOJW0 exists in SP23 but does not depend on SCSRDXTD which is */
/* new in SP24 */
             CLRPFM     FILE(UPZXRRPD)
             CALL       PGM(UPZXRFLSR) PARM(&REFSYS &ERROR)
             IF         COND(&ERROR *NE '          ') THEN(GOTO ABNOR)

             CLRPFM     FILE(UPZORDTD)
             CALL       PGM(UP000081) PARM(&PRFX &REFSYS 'R' &ERROR)
             IF         COND(&ERROR *NE '          ') THEN(GOTO ABNOR)

             CALL       PGM(UP000067) PARM(&PRFX &SEQC &BRGBRGLIB &RETURN)
             IF         COND(&RETURN *NE '          ') THEN(GOTO ABNOR)

/* list dependent for files to be ignored */
             CLRPFM     FILE(UPZXRIPD)
             CALL       PGM(UPZXRFLSI) PARM(&REFSYS &ERROR)

/* 2. generate script for driver file */

/*DMLIB */
             CHGVAR     VAR(&LIB) VALUE(&PRFX *TCAT 'DMLIB')
             SBMJOB     CMD(CALL PGM(UP000051) PARM(&LIB &PRFX &REFSYS &SEQC +
                          &BRGBRGLIB &RETURN)) JOB(&LIB) +
                          JOBQ(&BRGBRGLIB/SMJOBQNM)

/*DPLIB */
             CHGVAR     VAR(&LIB) VALUE(&PRFX *TCAT 'DPLIB')
             SBMJOB     CMD(CALL PGM(UP000051) PARM(&LIB &PRFX &REFSYS &SEQC +
                          &BRGBRGLIB &RETURN)) JOB(&LIB) +
                          JOBQ(&BRGBRGLIB/SMJOBQNM)

/*DTALIB */
             CHGVAR     VAR(&LIB) VALUE(&PRFX *TCAT 'DTALIB')
             SBMJOB     CMD(CALL PGM(UP000051) PARM(&LIB &PRFX &REFSYS &SEQC +
                          &BRGBRGLIB &RETURN)) JOB(&LIB) +
                          JOBQ(&BRGBRGLIB/SMJOBQNM)

/*DVLIB */
             CHGVAR     VAR(&LIB) VALUE(&PRFX *TCAT 'DVLIB')
             SBMJOB     CMD(CALL PGM(UP000051) PARM(&LIB &PRFX &REFSYS &SEQC +
                          &BRGBRGLIB &RETURN)) JOB(&LIB) +
                          JOBQ(&BRGBRGLIB/SMJOBQNM)

/* Wait for the 4 jobs to complete */
LOOP:
             CALL       PGM(QRCVDTAQ) PARM(&DTQN '*LIBL' &MSGLEN +
                          &MSGDATA &RCVWAIT)

             IF         COND(&MSGDATA *EQ 'DONE') THEN(CHGVAR +
                          VAR(&COUNT) VALUE(&COUNT + 1))

             IF         COND(&MSGDATA *EQ 'FAIL') THEN(CHGVAR +
                          VAR(&COUNT) VALUE(&COUNT + 1))

             IF         COND(&MSGDATA *EQ 'FAIL') THEN(CHGVAR +
                          VAR(&FAIL) VALUE('Y'))

             IF         COND(&COUNT *LT 4) THEN(GOTO LOOP)

/* If there is a FAIL */
             IF         COND(&FAIL *EQ 'Y') THEN(DO)
/* Update Script  Status with 'Abnormal' */
             CHGVAR        VAR(&FLDUPD) VALUE('UPSCRS')
             CHGVAR     VAR(&FLDVAL) VALUE('A')
             CALL UPZUPDWR ('*UPDATE   ' &PRFX &FLDUPD &FLDVAL &ERROR &RETURN)
             IF         COND(&ERROR *NE '          ') THEN(GOTO ABNOR)
             ENDDO


/* 3. generate script for LF creation */
             CLRPFM     FILE(UPZXRRPD)
             CALL       PGM(UPZXRFLSR) PARM(&REFSYS &ERROR)
             IF         COND(&ERROR *NE '          ') THEN(GOTO ABNOR)

             CLRPFM     FILE(UPZORDTD)
             CALL       PGM(UP000081) PARM(&PRFX &REFSYS 'R' &ERROR)
             IF         COND(&ERROR *NE '          ') THEN(GOTO ABNOR)

/* Call new program to review the rebuild order */                                      /*MD060960*/
             CLRPFM     FILE(UPZDEPTD)                                                  /*MD060960*/
             CALL       PGM(UP000077) PARM(&REFSYS &ERROR)                              /*MD060960*/
             IF         COND(&ERROR *NE '          ') THEN(GOTO ABNOR)                  /*MD060960*/
             CALL       PGM(UP000069) PARM(&ERROR)                                      /*MD060960*/
             IF         COND(&ERROR *NE '          ') THEN(GOTO ABNOR)                  /*MD060960*/
                                                                                        /*MD060960*/
/**********  CALL       PGM(UP000060) PARM(&PRFX &REFSYS &ERROR) */                     /*MD060960*/
/**********  IF         COND(&ERROR *NE '          ') THEN(GOTO ABNOR) */               /*MD060960*/
             CALL       PGM(UP000160) PARM(&PRFX &REFSYS &ERROR)                        /*MD060960*/
             IF         COND(&ERROR *NE '          ') THEN(GOTO ABNOR)                  /*MD060960*/

/* Send DONE message to server */
             CALL       PGM(QSNDDTAQ) PARM('ADBU_ZSVR' '*LIBL     ' +
                               &MSGLEN &WMSG)
/* Update Script Work File job Status with 'Complete' */
    /*       CHGVAR        VAR(&FLDUPD) VALUE('UPSCRS')         */
      /*     CHGVAR     VAR(&FLDVAL) VALUE('C')               */
        /*   CALL UPZUPDWR ('*UPDATE   ' &PRFX &FLDUPD &FLDVAL &ERROR &RETURN) */
          /* IF         COND(&ERROR *NE '          ') THEN(GOTO ABNOR)  */

             GOTO       CMDLBL(END)


/* Abnormal termination */

 ABNOR:      CHGJOB     SWS(XXXXXX11)

/* Send FAIL message to server */
             CHGVAR        VAR(&WMSG) VALUE('FAIL')
             CALL       PGM(QSNDDTAQ) PARM('ADBU_ZSVR' '*LIBL     ' +
                               &MSGLEN &WMSG)

             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program UPC000051 ended +
                          abnormally - see job log') TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)

/* End program */


 END:        ENDPGM
