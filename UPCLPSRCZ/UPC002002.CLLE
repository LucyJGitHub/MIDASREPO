/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas UP Shell to drop and create stored procs.')     */
/*********************************************************************/
/*                                                                   */
/*       Midas - Bridge                                              */
/*                                                                   */
/*       UPC002002 - Shell to drop and create stored procedures      */
/*                                                                   */
/*       (c) Finastra International Limited 2004                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. CSC043             Date 18Jun10              */
/*                      BUG27785           Date 17May10              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/*                      BUG27639           Date 07May10              */
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/*                      CUP033             Date 25Jan07              */
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*                      CPK025             Date 29May06              */
/*                      CSM005             Date 21Sep05              */
/*                      CUP094             Date 11Feb05              */
/*                      CPK021  *CREATE    Date 23Sep04              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       CSC043 - MidasPlus IASP Enablement                          */
/*       BUG27785 - Use overrides rather than renames.               */
/*       BUG27639 - Rename SDBANKPD and SDBANKL1 for System Values   */
/*                  Maintenance in Bridge Library                    */
/*       CUP033 - Make sure UPDLVC* doesn't already exist in QTEMP   */
/*       CPK025 - Packaging changes for MidasPlus 1.3.               */
/*       CSM005 - Changes to /COPYs.  Recompile.                     */
/*       CUP094 - Upgrade not selecting between global and zone.     */
/*       CPK021 - New Bridge option.                                 */
/*                                                                   */
/*********************************************************************/
/**********  PGM        PARM(&MODE)                                                   */ /*CPK025*/
             PGM        PARM(&MODE &PFXSVAL &SRCSVAL &BRGSVAL +
                          &GPPFXSVAL)                                                    /*CPK025*/
 
             DCL        VAR(&MODE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&PFXSVAL) TYPE(*CHAR) LEN(20)                                 /*CPK025*/
             DCL        VAR(&SRCSVAL) TYPE(*CHAR) LEN(20)                                 /*CPK025*/
             DCL        VAR(&BRGSVAL) TYPE(*CHAR) LEN(20)                                 /*CPK025*/
             DCL        VAR(&GPPFXSVAL) TYPE(*CHAR) LEN(20)                               /*CPK025*/
             DCL        VAR(&COMPARE) TYPE(*CHAR) LEN(8)                                  /*CUP094*/
 
/**COPY*UPCPYSRC,UPSVALDCL*                                          */ /*             */ /*CPK025*/
             DCL        VAR(&SBSID) TYPE(*CHAR) LEN(2)                                    /*CPK025*/
             DCL        VAR(&PTFSRC) TYPE(*CHAR) LEN(10)                                  /*CPK025*/
             DCL        VAR(&PTFUPG) TYPE(*CHAR) LEN(10)                                  /*CPK025*/
             DCL        VAR(&GPPFX) TYPE(*CHAR) LEN(2)                                    /*CPK025*/
             DCL        VAR(&BLANKSVAL) TYPE(*CHAR) LEN(1)                                /*CPK025*/
             DCL        VAR(&CURSYSVAL) TYPE(*CHAR) LEN(200)                              /*CPK025*/
/**********  DCL        VAR(&XLIB) TYPE(*CHAR) LEN(10)  */                                /*CSC043*/
             DCL        VAR(&XPLIB) TYPE(*CHAR) LEN(10)                                   /*CSC043*/
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2004')
 
             DCLF       FILE(UPSVALJ0)                                                    /*CPK025*/
 
/* Global monitor message. */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CHGJOB     LOG(4 0 *SECLVL) LOGCLPGM(*YES)
             CHGJOB     SWS(XXXXXX00)
 
/* Create data area /MIDASMSG in QTEMP. */
             DLTDTAARA  DTAARA(QTEMP/MIDASMSG)
             MONMSG     MSGID(CPF0000)
             CRTDTAARA  DTAARA(QTEMP/MIDASMSG) TYPE(*CHAR) LEN(800) +
                          VALUE(' ')
 
/**COPY*UPCPYSRC,UPSVALCAL1*                                         */ /*             */ /*CPK025*/
 
/* Check that System Values have an entry. */                                             /*CPK025*/
VALIDATE:                                                                                 /*CPK025*/
             RCVF                                                                         /*CPK025*/
             MONMSG     MSGID(CPF0864) EXEC(DO)                                           /*CPK025*/
                GOTO       CMDLBL(CHECK)                                                  /*CPK025*/
             ENDDO                                                                        /*CPK025*/
 
             IF         COND(&GISVAL *EQ &PFXSVAL) THEN(DO)                               /*CPK025*/
                IF         COND(&GIVAL *EQ ' ') THEN(DO)                                  /*CPK025*/
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')                                  /*CPK025*/
                ENDDO                                                                     /*CPK025*/
                ELSE       CMD(DO)                                                        /*CPK025*/
                   CHGVAR     VAR(&SBSID) VALUE(&GIVAL)                                   /*CPK025*/
                ENDDO                                                                     /*CPK025*/
             ENDDO                                                                        /*CPK025*/
 
             IF         COND(&GISVAL *EQ &SRCSVAL) THEN(DO)                               /*CPK025*/
                IF         COND(&GIVAL *EQ ' ') THEN(DO)                                  /*CPK025*/
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')                                  /*CPK025*/
                ENDDO                                                                     /*CPK025*/
                ELSE       CMD(DO)                                                        /*CPK025*/
                   CHGVAR     VAR(&PTFSRC) VALUE(&GIVAL)                                  /*CPK025*/
                ENDDO                                                                     /*CPK025*/
             ENDDO                                                                        /*CPK025*/
 
             IF         COND(&GISVAL *EQ &BRGSVAL) THEN(DO)                               /*CPK025*/
                IF         COND(&GIVAL *EQ ' ') THEN(DO)                                  /*CPK025*/
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')                                  /*CPK025*/
                ENDDO                                                                     /*CPK025*/
                ELSE       CMD(DO)                                                        /*CPK025*/
                   CHGVAR     VAR(&PTFUPG) VALUE(&GIVAL)                                  /*CPK025*/
                ENDDO                                                                     /*CPK025*/
             ENDDO                                                                        /*CPK025*/
 
             IF         COND(&MODE *EQ '*IMP') THEN(DO)                                   /*CPK025*/
                IF         COND(&GISVAL *EQ &GPPFXSVAL) THEN(DO)                          /*CPK025*/
                   IF         COND(&GIVAL *EQ ' ') THEN(DO)                               /*CPK025*/
                      CHGVAR     VAR(&BLANKSVAL) VALUE('Y')                               /*CPK025*/
                   ENDDO                                                                  /*CPK025*/
                   ELSE       CMD(DO)                                                     /*CPK025*/
                      CHGVAR     VAR(&GPPFX) VALUE(&GIVAL)                                /*CPK025*/
                   ENDDO                                                                  /*CPK025*/
                ENDDO                                                                     /*CPK025*/
             ENDDO                                                                        /*CPK025*/
 
             GOTO       CMDLBL(VALIDATE)                                                  /*CPK025*/
 
/**********  IF         COND(&SBSID *EQ ' ' *OR &PTFSRC *EQ ' ' *OR +                  */ /*CPK025*/
/**********               &PTFUPG *EQ ' ') THEN(DO)                                    */ /*CPK025*/
 CHECK:                                                                                   /*CPK025*/
             IF         COND(&BLANKSVAL *EQ 'Y') THEN(DO)                                 /*CPK025*/
                RTVMSG     MSGID(UPM0003) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
                RTVMSG     MSGID(UPM0004) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (151 50)) VALUE(&MESSAGE)
                CALL       PGM(SCC0010) PARM('UPC002002' 'ENTER' ' ')
                GOTO       CMDLBL(ENDPGM)
             ENDDO
 
/* Set up zone library list. */
             CALL       PGM(UPC0014) PARM(&SBSID)
 
/* Delete the driving file in QTEMP if it already exists. */                              /*CUP033*/
             DLTF       FILE(QTEMP/UPDLVC*)                                               /*CUP033*/
             MONMSG     MSGID(CPF0000)                                                    /*CUP033*/
 
             RMVLIBLE   LIB(&PTFUPG)
             MONMSG     MSGID(CPF0000)
             ADDLIBLE   LIB(&PTFUPG) POSITION(*FIRST)
 
/**********  CHGVAR     VAR(&XLIB) VALUE(&SBSID *TCAT 'XLIB')  */                         /*CSC043*/
             CHGVAR     VAR(&XPLIB) VALUE(&SBSID *TCAT 'XPLIB')                           /*CSC043*/
             CHGVAR     VAR(&COMPARE) VALUE('SQLSRCZ')                                    /*CUP094*/
 
/**Rename*SDBANKPD/SDBANKL1*/ /*                                        */ /*BUG27639*/ /*BUG27785*/
/**********  RNMOBJ     OBJ(SDBANKL1) OBJTYPE(*FILE) +                               */ /*BUG27785*/
/**********               NEWOBJ(SDBANKL#)                              */ /*BUG27639*/ /*BUG27785*/
/**********  MONMSG     MSGID(CPF2105)                                  */ /*BUG27639*/ /*BUG27785*/
/**********  RNMOBJ     OBJ(SDBANKPD) OBJTYPE(*FILE) +                               */ /*BUG27785*/
/**********               NEWOBJ(SDBANKP#)                              */ /*BUG27639*/ /*BUG27785*/
/**********  MONMSG     MSGID(CPF2105)                                  */ /*BUG27639*/ /*BUG27785*/
 
             IF         COND(&MODE *EQ '*IMP') THEN(DO)
/* Call program to build driving file for SQL procedures. */
/**********     CALL       PGM(UPC000002) PARM(&PTFSRC 'SQLSRCZ ')                     */ /*CUP094*/
/* Call GP009510 to ensure that the global System Value contains the */                   /*CPK025*/
/*  global sytem prefix so that the driving file (UPDLVCPD) can be   */                   /*CPK025*/
/*  populated.                                                       */                   /*CPK025*/
                CHGVAR     VAR(%SST(&CURSYSVAL 1 2)) VALUE(&GPPFX)                        /*CPK025*/
                CALL       PGM(GP009510) PARM('BrgMidasGlobalPrefix' +
                             &CURSYSVAL ' ')                                              /*CPK025*/
                CALL       PGM(UPC000002) PARM(&PTFSRC &COMPARE)                          /*CUP094*/
                IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                   GOTO       CMDLBL(ABNOR)
                ENDDO
                OVRDBF     FILE(UPDLVCPD) TOFILE(QTEMP/UPDLVCPD) +
                             OVRSCOPE(*JOB)
             ENDDO
 
/* Call program to drop and create procedures. */
/**********  CALL       PGM(UPC000001) PARM(&XLIB &PTFSRC)                             */ /*CUP094*/
/**********  CALL       PGM(UPC000001) PARM(&XLIB &PTFSRC &COMPARE)  */            /*CUP094 CSC043*/
             CALL       PGM(UPC000001) PARM(&XPLIB &PTFSRC &COMPARE)                      /*CSC043*/
             DLTOVR     FILE(UPDLVCPD) LVL(*JOB)
             MONMSG     MSGID(CPF0000)
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
/**Rename*objects*back*to*original*/ /*                                 */ /*BUG27639*/ /*BUG27785*/
/**********  RNMOBJ     OBJ(SDBANKL#) OBJTYPE(*FILE) +                               */ /*BUG27785*/
/**********               NEWOBJ(SDBANKL1)                              */ /*BUG27639*/ /*BUG27785*/
/**********  MONMSG     MSGID(CPF2105)                                  */ /*BUG27639*/ /*BUG27785*/
/**********  RNMOBJ     OBJ(SDBANKP#) OBJTYPE(*FILE) +                               */ /*BUG27785*/
/**********               NEWOBJ(SDBANKPD)                              */ /*BUG27639*/ /*BUG27785*/
/**********  MONMSG     MSGID(CPF2105)                                  */ /*BUG27639*/ /*BUG27785*/
 
             GOTO       CMDLBL(ENDPGM)
ABNOR:
             RTVMSG     MSGID(UPM0001) MSGF(UTMSGF) +
                          MSGDTA('UPC002002') MSG(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (201 50)) VALUE('Check +
                          joblog for details')
             MONMSG     MSGID(CPF0000)
             CALL       PGM(SCC0010) PARM('UPC002002' 'ENTER' ' ')
             MONMSG     MSGID(CPF0000)
 
ENDPGM:
             ENDPGM
