/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas UP On-screen compare edit using Sys. Values')   */
/*********************************************************************/
/*                                                                   */
/*       Midas - Upgrade Module                                      */
/*                                                                   */
/*       UPC002021 - On-screen compare edit using System Values      */
/*                                                                   */
/*       (c) Finastra International Limited 2005                     */
/*                                                                   */
/*       Last Amend No. MD054605           Date 17Oct19              */
/*       Prev Amend No. MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/*                      CUP034             Date 16Jan07              */
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*                      CSM005  *CREATE    Date 13Sep05              */
/*                                                                   */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD054605 - Deliverable Data Split for SDSVALPD              */
/*       MD046248 - Finastra Rebranding                              */
/*       CUP034 - Reduce size of implementation library              */
/*       CSM005 - New program to control Bridge compares.            */
/*                                                                   */
/*********************************************************************/
/**********  PGM        PARM(&WRKSVAL)                                                 */ /*CUP034*/
             PGM        PARM(&WRKSVAL &PFXSVAL)                                           /*CUP034*/

             DCL        VAR(&WRKSVAL) TYPE(*CHAR) LEN(20)
             DCL        VAR(&PFXSVAL) TYPE(*CHAR) LEN(20)                                 /*CUP034*/

             DCL        VAR(&WRKLIB) TYPE(*CHAR) LEN(10)                                  /*CUP034*/
             DCL        VAR(&SBSID) TYPE(*CHAR) LEN(2)                                    /*CUP034*/
             DCL        VAR(&BLANKSVAL) TYPE(*CHAR) LEN(1) VALUE('N')                     /*CUP034*/
             DCL        VAR(&PROGRAM) TYPE(*CHAR) LEN(10) +
                          VALUE('UPC002021')                                              /*CUP034*/
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)

/**********  DCLF       FILE(UPSVALJ0) */                                      /*CUP034* *MD054605*/
             DCLF       FILE(UPSVALJ0) ALWNULL(*YES)                                    /*MD054605*/

             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2005')

/* Global monitor message. */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ERROR))

/* Create data area MIDASMSG in QTEMP. */
             DLTDTAARA  DTAARA(QTEMP/MIDASMSG)
             MONMSG     MSGID(CPF0000)
             CRTDTAARA  DTAARA(QTEMP/MIDASMSG) TYPE(*CHAR) LEN(800) +
                          VALUE(' ')

             CHGJOB     SWS(00000000)                                                     /*CUP034*/

/**********  OVRDBF     FILE(UPGSVALJ0) TOFILE(UPSVALJ0) OVRSCOPE(*JOB)                */ /*CUP034*/
/**********  CALL       PGM(UPC002020) PARM(&WRKSVAL)                                  */ /*CUP034*/
/**********  DLTOVR     FILE(UPGSVALJ0) LVL(*JOB)                                      */ /*CUP034*/

/* Check that System Values have an entry. */                                             /*CUP034*/
VALIDATE:                                                                                 /*CUP034*/
             RCVF                                                                         /*CUP034*/
             MONMSG     MSGID(CPF0864) EXEC(DO)                                           /*CUP034*/
                GOTO       CMDLBL(CHECK)                                                  /*CUP034*/
             ENDDO                                                                        /*CUP034*/

             IF         COND(&GISVAL *EQ &WRKSVAL) THEN(DO)                               /*CUP034*/
                IF         COND(&GIVAL *EQ ' ') THEN(DO)                                  /*CUP034*/
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')                                  /*CUP034*/
                ENDDO                                                                     /*CUP034*/
                ELSE       CMD(DO)                                                        /*CUP034*/
                   CHGVAR     VAR(&WRKLIB) VALUE(&GIVAL)                                  /*CUP034*/
                ENDDO                                                                     /*CUP034*/
             ENDDO                                                                        /*CUP034*/

             IF         COND(&GISVAL *EQ &PFXSVAL) THEN(DO)                               /*CUP034*/
                IF         COND(&GIVAL *EQ ' ') THEN(DO)                                  /*CUP034*/
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')                                  /*CUP034*/
                ENDDO                                                                     /*CUP034*/
                ELSE       CMD(DO)                                                        /*CUP034*/
                   CHGVAR     VAR(&SBSID) VALUE(&GIVAL)                                   /*CUP034*/
                ENDDO                                                                     /*CUP034*/
             ENDDO                                                                        /*CUP034*/

             GOTO       CMDLBL(VALIDATE)                                                  /*CUP034*/

 CHECK:                                                                                   /*CUP034*/
             IF         COND(&BLANKSVAL *EQ 'Y') THEN(DO)                                 /*CUP034*/
                RTVMSG     MSGID(UPM0003) MSGF(UTMSGF) MSG(&MESSAGE)                      /*CUP034*/
                CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)                      /*CUP034*/
                RTVMSG     MSGID(UPM0004) MSGF(UTMSGF) MSG(&MESSAGE)                      /*CUP034*/
                CHGDTAARA  DTAARA(MIDASMSG (151 50)) VALUE(&MESSAGE)                      /*CUP034*/
                CALL       PGM(SCC0010) PARM('UPC002021' 'ENTER' ' ')                     /*CUP034*/
                GOTO       CMDLBL(ENDPGM)                                                 /*CUP034*/
             ENDDO                                                                        /*CUP034*/

             CALL       PGM(UPC0014) PARM(&SBSID)                                         /*CUP034*/
             RMVLIBLE   LIB(&WRKLIB)                                                      /*CUP034*/
             MONMSG     MSGID(CPF0000)                                                    /*CUP034*/
             ADDLIBLE   LIB(&WRKLIB)                                                      /*CUP034*/

/* Call program to check compares. */                                                     /*CUP034*/
             CALL       PGM(UP000531) PARM(&WRKLIB)                                       /*CUP034*/
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)                                  /*CUP034*/
                CHGVAR     VAR(&PROGRAM) VALUE('UP0030    ')                              /*CUP034*/
                GOTO       CMDLBL(ERROR)                                                  /*CUP034*/
             ENDDO                                                                        /*CUP034*/

             GOTO       CMDLBL(ENDPGM)

 ERROR:
/*********   RTVMSG     MSGID(UPM0001) MSGF(UTMSGF) +                                  */ /*CUP034*/
/*********                MSGDTA('UPC002021') MSG(&MESSAGE)                            */ /*CUP034*/
             RTVMSG     MSGID(UPM0001) MSGF(UTMSGF) MSGDTA(&PROGRAM) +
                          MSG(&MESSAGE)                                                   /*CUP034*/
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (201 50)) VALUE('Check +
                          joblog for details.')
             MONMSG     MSGID(CPF0000)
             CALL       PGM(SCC0010) PARM('UPC002021' 'JOBLOG' 'Y')
             MONMSG     MSGID(CPF0000 MCH0000)
 ENDPGM:
             ENDPGM
