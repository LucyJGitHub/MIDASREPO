/*********************************************************************/
/*S*D****CLPBASE******************************************************/
/*E*I****TEXT('Midas*SM*Check*shared*formats*post*upgrade')***********/
/*********************************************************************/
/*                                                                   */
/*       Midas - Upgrade Module                                      */
/*                                                                   */
/*       UPC0026 - Check shared formats after upgrade                */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2001           */
/*                                                                   */
/*       Last Amend No. CUP042 *REDUNDANT  Date 12Dec14              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/* Midas Release 4.01 -----------------------------------------------*/
/*       Prev Amend No. CPK015             Date 05Mar02              */
/* Midas Release 4 --------------- Base -----------------------------*/
/* Midas DBA 3.00 ---------------- Base -----------------------------*/
/*                      CUP002 *CREATE     Date 26Apr99              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CUP042 - Changes for Bridge deliverable data split          */
/*       CPK015 - 4.01 packaging. Add MONMSG to error processing.    */
/*       CUP002 - Introduce new post-upgrade check                   */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)
             DCL        VAR(&WFSFSHD) TYPE(*CHAR) LEN(10) VALUE(' ')
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Misys International Banking Systems Ltd. +
                          2001')
 
             DCLF       FILE(UPSFMTL0)
 
/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CHGJOB     LOG(4 0 *SECLVL) LOGCLPGM(*YES)
             CHGJOB     SWS(XXXXXX00)
 
/* Create data area /MIDASMSG in QTEMP */
             DLTDTAARA  DTAARA(QTEMP/MIDASMSG)
             MONMSG     MSGID(CPF0000)
             CRTDTAARA  DTAARA(QTEMP/MIDASMSG) TYPE(*CHAR) LEN(800) +
                          VALUE(' ')
 
/* Delete outfiles if they already exist */
             DLTF       FILE(QTEMP/FSFILE)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(QTEMP/FSFSHD)
             MONMSG     MSGID(CPF0000)
 
READNEXT:
             RCVF
             MONMSG     MSGID(CPF0864) EXEC(GOTO CMDLBL(OPNQRYF))
 
/* Display file that shares format to outfile */
             DSPFD      FILE(&FSFILE) TYPE(*RCDFMT) OUTPUT(*OUTFILE) +
                          OUTFILE(QTEMP/FSFILE) OUTMBR(*FIRST *ADD)
             MONMSG     MSGID(CPF3012)
 
/* Display file that has format shared to outfile, only if it is    */
/*  not the same as previous record.                                */
             IF         COND(&FSFSHD *NE &WFSFSHD) THEN(DO)
                DSPFD   FILE(&FSFSHD) TYPE(*RCDFMT) OUTPUT(*OUTFILE) +
                          OUTFILE(QTEMP/FSFSHD) OUTMBR(*FIRST *ADD)
                MONMSG     MSGID(CPF3012)
             ENDDO
 
             CHGVAR     VAR(&WFSFSHD) VALUE(&FSFSHD)
 
             GOTO       CMDLBL(READNEXT)
 
OPNQRYF:
             OVRDBF     FILE(UPFMTCQF) TOFILE(UPSFMTPD) SHARE(*YES)
             OPNQRYF    FILE((UPSFMTPD) (FSFILE) (FSFSHD)) +
                          FORMAT(UPFMTCQF) QRYSLT('CFLEN1 *NE +
                          CFLEN2') JFLD((1/FSFILE 2/RFFILE *EQ) +
                          (1/FSFSHD 3/RFFILE)) MAPFLD((CFLEN2 +
                          '2/RFLEN') (CFLEN1 '3/RFLEN') (CFLIB1 +
                          '3/RFLIB') (CFLIB2 '2/RFLIB') (CFFSHD +
                          FSFSHD) (CFFILE FSFILE))
             CALL       PGM(UP0026)
             CLOF       OPNID(UPSFMTPD)
             DLTOVR     FILE(UPFMTCQF)
 
             GOTO       CMDLBL(END)
 ABNOR:
/* Set up messages for Midas Information Screen  */
             RTVMSG     MSGID(UPM0001) MSGF(UTMSGF) +
                          MSGDTA('UPC0026') MSG(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
             MONMSG     MSGID(CPF0000)                                                    /*CPK015*/
             CHGDTAARA  DTAARA(MIDASMSG (201 50)) VALUE('Check +
                          joblog for details')
             MONMSG     MSGID(CPF0000)                                                    /*CPK015*/
             CALL       PGM(SCC0010) PARM('UPC0026' 'JOBLOG' 'Y')
             MONMSG     MSGID(CPF0000 MCH0000)                                            /*CPK015*/
 
END:         CHGVAR     VAR(&CPYFLD) VALUE('(c) +
                          Misys International Banking Systems Ltd.')
 
             ENDPGM
