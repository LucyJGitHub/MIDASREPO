/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas UP Menu option to update global data')          */
/*********************************************************************/
/*                                                                   */
/*       Midas - Bridge                                              */
/*                                                                   */
/*       UPC003005 - Menu option to update global data from          */
/*                     restored temporary global libraries           */
/*                                                                   */
/*       (c) Finastra International Limited 2007                     */
/*                                                                   */
/*       Last Amend No. MD054605           Date 17Oct19              */
/*       Prev Amend No. MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/*                      CSC033  *CREATE    Date 01May07              */
/*                      XXXXXXX            Date                      */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD054605 - Deliverable Data Split for SDSVALPD              */
/*       MD046248 - Finastra Rebranding                              */
/*       CSC033 - Include global libraries in backups.               */
/*                                                                   */
/*********************************************************************/
/**/
             PGM        PARM(&PFXSVAL)

             DCL        VAR(&PFXSVAL) TYPE(*CHAR) LEN(20)

             DCL        VAR(&SBSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&RSTGMLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&BLANKSVAL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)

             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2007')
/**/
/**********  DCLF       FILE(UPSVALJ0)                                                  /*MD054605*/
             DCLF       FILE(UPSVALJ0) ALWNULL(*YES)                                    /*MD054605*/
/**/
/* Global monitor message. */
/**/
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
/**/
             CHGJOB     LOG(4 0 *SECLVL) LOGCLPGM(*YES)
             CHGJOB     SWS(XXXXXX00)
/**/
/* Create data area MIDASMSG in QTEMP                                                             */
/**/
             DLTDTAARA  DTAARA(QTEMP/MIDASMSG)
             MONMSG     MSGID(CPF0000)
             CRTDTAARA  DTAARA(QTEMP/MIDASMSG) TYPE(*CHAR) LEN(800) +
                          VALUE(' ')
/**/
/*  Check that System Values have an entry.                                                       */
/**/
VALIDATE:
/**/
             RCVF
             MONMSG     MSGID(CPF0864) EXEC(DO)
                GOTO       CMDLBL(CHECK)
             ENDDO

             IF         COND(&GISVAL *EQ &PFXSVAL) THEN(DO)
                IF         COND(&GIVAL *EQ ' ') THEN(DO)
                   CHGVAR     VAR(&BLANKSVAL) VALUE('Y')
                ENDDO
                ELSE       CMD(DO)
                   CHGVAR     VAR(&SBSID) VALUE(&GIVAL)
                ENDDO
             ENDDO
/**/
             GOTO       CMDLBL(VALIDATE)
/**/
 CHECK:
             IF         COND(&BLANKSVAL *EQ 'Y') THEN(DO)
                RTVMSG     MSGID(UPM0003) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
                RTVMSG     MSGID(UPM0004) MSGF(UTMSGF) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (151 50)) VALUE(&MESSAGE)
                CALL       PGM(SCC0010) PARM('UPC003005' 'ENTER' ' ')
                GOTO       CMDLBL(ENDPGM)
             ENDDO
/**/
/*  Retrieve the name of the temporary library containing GMLIB data from data area UPSTAT.      */
/**/
             RTVDTAARA  DTAARA(UPSTAT (176 10)) RTNVAR(&RSTGMLIB)
/**/
/*  Execute the command RBDGLBDTA with a prompt.                                                  */
/**/
             ?          UPDGLBDTA ??ZONEID(&SBSID) +
                          ??RSTGMLIB(&RSTGMLIB)
             IF         COND(%SWITCH(XXXXXX1X)) THEN(DO)
              GOTO       CMDLBL(ABNOR)
             ENDDO
/**/
             GOTO       CMDLBL(ENDPGM)
ABNOR:
             RTVMSG     MSGID(UPM0001) MSGF(UTMSGF) +
                          MSGDTA('UPC003005') MSG(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (201 50)) VALUE('Check +
                          joblog for details')
             MONMSG     MSGID(CPF0000)
             CALL       PGM(SCC0010) PARM('UPC003005' 'ENTER' ' ')
             MONMSG     MSGID(CPF0000)
/**/
ENDPGM:
             RETURN
             ENDPGM
