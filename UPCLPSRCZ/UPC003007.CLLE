/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas UP Remove triggers from restored library')      */
/*********************************************************************/
/*                                                                   */
/*       Midas Plus - Bridge                                         */
/*                                                                   */
/*       UPC003007 - Remove triggers from files in temporary library */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2007           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/*       Last Amend No. CSC033    *CREATE  Date 01May07              */
/*       Prev Amend No. XXXxxxx            Date                      */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CSC033 - Include global libraries in backups.               */
/*                                                                   */
/*********************************************************************/
/**/
             PGM        PARM(&LIB)
/**/
/*/COPY WNCPYSRC,UPC3007INT                                                                       */
/**/
/*  Copyright statement definition                                                                */
/**/
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Misys International Banking Systems Ltd. +
                          2007')
/**/
/*   Declare variables                                                                            */
/**/
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10)
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2007')
             DCLF       FILE(QAFDTRG)
/**/
/*/COPY WNCPYSRC,UPC3007001                                                                       */
/**/
/*  Global Monitor Message                                                                        */
/**/
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO ABNOR)
/**/
/*/COPY WNCPYSRC,UPC3007002                                                                       */
/**/
/*  Copyright statement definition - at runtime                                                   */
/**/
             CHGVAR     VAR(&CPYFLD) VALUE('(c) +
                          Misys International Banking Systems Ltd.')
/**/
             SNDPGMMSG  MSG('Remove triggers in temporary library') TOMSGQ(MRUNQ)
/**/
/*           Create a file of names of files containing triggers.                                 */
/**/
             DLTF       FILE(QTEMP/PFTRIGS)
             MONMSG     MSGID(CPF0000)
             DSPFD      FILE(&LIB/*ALL) TYPE(*TRG) OUTPUT(*OUTFILE) +
                          FILEATR(*PF) OUTFILE(QTEMP/PFTRIGS)
             OVRDBF     FILE(QAFDTRG) TOFILE(QTEMP/PFTRIGS)
LOOPSTER:
             RCVF
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(EXITER))
             RMVPFTRG   FILE(&LIB/&TRFILE)
             MONMSG     MSGID(CPF0000)
             GOTO       CMDLBL(LOOPSTER)
EXITER:
             DLTF       FILE(QTEMP/PFTRIGS)
             MONMSG     MSGID(CPF0000)
             GOTO       CMDLBL(END)
/**/
/*  Error processing                                                                              */
/**/
 ABNOR:      SNDPGMMSG  MSG('UPC003007 Remove triggers from +
                          temporary library ENDED ABNORMALLY') +
                          TOPGMQ(*EXT) TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
/**/
/*/COPY WNCPYSRC,UPC3007ERR                                                                       */
/**/
             CHGJOB     SWS(XXXXXX11)
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
/**/
END:
/**/
/*/COPY WNCPYSRC,UPC3007END                                                                       */
/**/
             RETURN
             ENDPGM
/* */
