/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas UP UPDGLBDTA validity checker')                 */
/*********************************************************************/
/*                                                                   */
/*       Midas Plus - Bridge                                         */
/*                                                                   */
/*       UPC3006_VC - Validity checker for UPDGLBDTA                 */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2007           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/*       Last Amend No. CSC033 *CREATE     DATE 01May07              */
/*       Prev Amend No. xxxxxx             DATE ddmmmyy              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CSC033 - Include global libraries in backups                */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&ZONEID &RSTGMLIB)
/**/
/*   Declare variables                                                                            */
/**/
             DCL        VAR(&ZONEID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DMLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&RSTGMLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ERROR) TYPE(*CHAR) LEN(1)
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2007')
/**/
/*  Global monitor message                                                                        */
/**/
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
/**/
/*  Validate zone library group.                                                                  */
/**/
             CHGVAR     VAR(&DMLIB) VALUE(&ZONEID *CAT 'DMLIB')
             CHKOBJ     OBJ(QSYS/&DMLIB) OBJTYPE(*LIB) AUT(*USE)
                MONMSG     MSGID(CPF9801 CPF9802) EXEC(DO)
                   SNDPGMMSG  MSGID(CPD0006) MSGF(QCPFMSG) +
                          MSGDTA('0000Library' *BCAT &DMLIB +
                          *BCAT 'does not exist or you have +
                          insufficient authority') MSGTYPE(*DIAG)
                   CHGVAR     VAR(&ERROR) VALUE('Y')
                   GOTO       CMDLBL(ABNOR)
                ENDDO
/**/
/*  Validate restored temporary library containing GMLIB data.                                    */
/**/
             CHKOBJ     OBJ(QSYS/&RSTGMLIB) OBJTYPE(*LIB) AUT(*USE)
                MONMSG     MSGID(CPF9801 CPF9802) EXEC(DO)
                   SNDPGMMSG  MSGID(CPD0006) MSGF(QCPFMSG) +
                          MSGDTA('0000Library' *BCAT &RSTGMLIB +
                          *BCAT 'does not exist or you have +
                          insufficient authority') MSGTYPE(*DIAG)
                   CHGVAR     VAR(&ERROR) VALUE('Y')
                   GOTO       CMDLBL(ABNOR)
                ENDDO
/**/
             GOTO       CMDLBL(END)
/**/
/*  Send message to cause CPF to pass earlier messages back.                                      */
/**/
ABNOR:
            IF  COND(&ERROR *EQ 'Y') THEN(+
            SNDPGMMSG  MSGID(CPF0002) MSGF(QCPFMSG) MSGTYPE(*ESCAPE))
/**/
END:
/**/
             RETURN
/**/
             ENDPGM
