/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas UP Add job queue to subsystem - DIJOBQ')        */
/*********************************************************************/
/*                                                                   */
/*       Midas - Bridge                                              */
/*                                                                   */
/*       UPC8010 - Midas UP Add job queue to subsystem - DIJOBQ      */
/*                                                                   */
/*       (c) Finastra International Limited 2017                     */
/*                                                                   */
/*       Last Amend No. CUP047             Date 27Aug23              */
/*       Prev Amend No. MD046675 *Create   Date 31Jul17              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CUP047 - Alternative OWNER and AUTL for Bridge upgrade      */
/*       MD046675 - Create a new jobq allocated for job GLC000585.   */
/*                                                                   */
/*********************************************************************/

             PGM

             DCL        VAR(&XLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSBS) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PREFIX) TYPE(*CHAR) LEN(2)
             DCL        VAR(&OWNER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ADOPT) TYPE(*CHAR) LEN(10)
             DCL        VAR(&RTCD) TYPE(*CHAR) LEN(7)                                     /*CUP047*/
             DCL        VAR(&OPTN) TYPE(*CHAR) LEN(7)                                     /*CUP047*/
             DCL        VAR(&SARD) TYPE(*CHAR) LEN(6)                                     /*CUP047*/
             DCL        VAR(&DSFDY) TYPE(*CHAR) LEN(200)                                  /*CUP047*/
/*/COPY SDCPYSRC,SDSVALDCL                                                                /*CUP047*/

             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2017')

/* Global monitor message. */

             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))

             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&PREFIX)

             CHGVAR     VAR(&XLIB) VALUE(&PREFIX *CAT 'XLIB')
             CHGVAR     VAR(&MSBS) VALUE('M' *CAT &PREFIX *CAT 'SBS')
             CHGVAR     VAR(&OWNER) VALUE(&PREFIX *CAT 'OWNER')
             CHGVAR     VAR(&ADOPT) VALUE(&PREFIX *CAT 'ADOPT')
                                                                                          /*CUP047*/
/*  Check if feature CUP047 is on */                                                      /*CUP047*/
                                                                                          /*CUP047*/
             CHGVAR     VAR(&RTCD) VALUE('       ')                                       /*CUP047*/
             CHGVAR     VAR(&OPTN) VALUE('*VERIFY')                                       /*CUP047*/
             CHGVAR     VAR(&SARD) VALUE('CUP047')                                        /*CUP047*/
             CALL       PGM(AOSARDR0) PARM(&RTCD &OPTN &SARD &DSFDY)                      /*CUP047*/
                                                                                          /*CUP047*/
             IF         COND(&RTCD *EQ '       ') THEN(DO)                                /*CUP047*/
                                                                                          /*CUP047*/
/*  Check System Values for CUP047 */                                                     /*CUP047*/
             CALL       PGM(AOSVALR0) PARM(&RSVALRTNC  +
                          'AlternativeBrgOWNER' &SVAL1  +
                          'AlternBrgAUTLnonDB' &SVAL2  +
                          &SVALK3 &SVAL3 &SVALK4 &SVAL4 +
                          &SVALK5 &SVAL5 &SVALK6 &SVAL6 &SVALK7 +
                          &SVAL7 &SVALK8 &SVAL8 &SVALK9 &SVAL9 +
                          &SVALK10 &SVAL10)                                               /*CUP047*/
             IF         COND(&SVAL1 *NE '          ') THEN(CHGVAR +
                          VAR(&OWNER) VALUE(&SVAL1))                                      /*CUP047*/
             IF         COND(&SVAL2 *NE '          ') THEN(CHGVAR +
                          VAR(&ADOPT) VALUE(&SVAL2))                                      /*CUP047*/
             ENDDO                                                                        /*CUP047*/
                                                                                          /*CUP047*/

/* Create job description MBATCHIC */

             CRTJOBQ    JOBQ(&XLIB/DIJOBQ) TEXT('Midas DI Input +
                          Cycle JOBQ')
             MONMSG     MSGID(CPF3323)

             CHGOBJOWN  OBJ(DIJOBQ) OBJTYPE(*JOBQ) NEWOWN(&OWNER)
             GRTOBJAUT  OBJ(DIJOBQ) OBJTYPE(*JOBQ) AUTL(&ADOPT)

/* Add job queue entry */

             ADDJOBQE   SBSD(&XLIB/&MSBS) JOBQ(&XLIB/DIJOBQ) +
                          SEQNBR(340)

             GOTO       CMDLBL(ENDPGM)
ABNOR:
             CHGJOB     SWS(XXXXXX11)

ENDPGM:
             ENDPGM
