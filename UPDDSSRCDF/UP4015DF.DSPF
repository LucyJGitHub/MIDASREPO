      *****************************************************************
/*STD *  DSPFDFRWN                                                    *
/*EXI *  TEXT('Midas UP ADBU Update Monitor')
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade Module                                       *
      *                                                               *
      *  DSPF/UP4015DF - ADBU Update Monitor Screen                   *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. MD060271           Date 21Jul22               *
      *  Prev Amend No. CUT017 *CREATE     Date 17Mar20               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD060271 - Dashed line appeared trimmed                      *
      *  CUT017 - Adaptive Database Upgrade                           *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(27 132)
     A                                      HELP
     A                                      ALTHELP
     A          R RECFMTF                   OVERLAY
     A                                      PUTOVR
     A                                      CA03(03 'Exit')
     A                                      CA02(07 'Cont')
     A                                      CA07(07 'Suma')
     A                                      CA08(08 'Deta')
     A                                      CA06(06 'Exce')
     A                                      CA23(23 'RlBk')
     A            ##USR         10A  O  1  2TEXT('User ID')
     A                                      DSPATR(HI)
     A                                  1 13'UP4015'
     A                                      DSPATR(HI)
     A*                                     OVRDTA
     A            ##TME          6  0   2123TEXT('*TIME')
     A                                      OVRDTA
     A                                      EDTWRD('  :  :  ')
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      DSPATR(HI)
     A                                  2 50'Adaptive Database Upgrade - Update-
     A                                       Monitor'
     A                                      DSPATR(HI)
     A                                  7  3'Total files to update.:'
     A                                  4  3'Zone Layer............:'
     A            ##PRFX         2      4 27
     A                                  5  3'Reference System......:'
     A            ##RSYS         2      5 27
     A*N50        ##OTTC        10N 0O  7 27
     A N50        ##OTTC         5A  O  7 27
     A                                  8  3'Upgrade Step..........:'
     A N99                              8123'[Active]'
     A  09                                  DSPATR(RI)
     A  09                                  COLOR(TRQ)
     A* 39
     A* 99                                  DSPATR(ND)
     A                                      OVRATR
     A  99                              8123'[Failed]'
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(BL)
     A                                      OVRATR
     A                                  8 27'[Generating Script]'
     A                                      OVRATR
     A  10
     AO 80                                  DSPATR(RI)
     A  10                                  COLOR(TRQ)
     A  80                                  DSPATR(BL)
     A                                  8 48'[Running Backup]'
     A                                       OVRATR
     A  11
     AO 81                                  DSPATR(RI)
     A  11                                  COLOR(TRQ)
     A  81                                  DSPATR(BL)
     A                                  8 66'[Upgrading PF]'
     A                                       OVRATR
     A  12
     AO 82                                  DSPATR(RI)
     A  12                                  COLOR(TRQ)
     A  82                                  DSPATR(BL)
     A                                  8 82'[Upgrading LF]'
     A                                       OVRATR
     A  13
     AO 83                                  DSPATR(RI)
     A  13                                  COLOR(TRQ)
     A  83                                  DSPATR(BL)
     A                                  8 98'[Reconciling]'
     A                                       OVRATR
     A  14
     AO 84                                  DSPATR(RI)
     A  14                                  COLOR(TRQ)
     A  84                                  DSPATR(BL)
     A                                  4101'Process started at..:'
     A                                  5101'Elapsed time........:'
     A                                  6101'Process completed at:'
     A N37                                  DSPATR(ND)
     A                                      OVRATR
     A                                  1123DATE
     A                                      EDTCDE(Y)
     A                                      DSPATR(HI)
     A            ##STRT          T  O  4123TIMFMT(*HMS)
     A                                      TIMSEP(':')
     A                                      OVRDTA
     A            ##LAPS          T  O  5123TIMFMT(*HMS)
     A                                      TIMSEP(':')
     A                                      OVRDTA
     A  37        ##ENDT          T  O  6123
     A N37                                  DSPATR(ND)
     A                                      TIMFMT(*HMS)
     A                                      TIMSEP(':')
     A                                      OVRDTA
     A            ##CURT          T  O  7 89DSPATR(ND)
     A                                      OVRDTA
     A                                  6  3'Update Status.........:'
     A            ##UPSS        35   O  6 27
     A                                      OVRDTA

      * Start of change MD060271
     A**********                        9  3'-----------------------------------            MD060271
     A**********                            ------------------------------------            MD060271
     A**********                            ------------------------------------            MD060271
     A**********                            -------------------------'                      MD060271
     A            ##DOTL       127A     9  3
      * End of change MD060271
     A                                      DSPATR(HI)

      * to be displayed when script is running
     A          R SCRIPTF                   OVERLAY
     A                                      PUTOVR
     A                                 11  3'Total'
     A                                 11 10'Files'
     A                                 12  3'Files'
     A                                 12 10'Processed'
     A            ##SCTT         6N 0  13  3
     A                                      OVRDTA
     A            ##SCTP         6N 0  13 10
     A                                      OVRDTA
     A            ##SCSS       100   O 13 21
     A                                      OVRDTA
     A            ##SCPC         5   O 13126
     A                                      OVRDTA

     A                                 11121'Completion'
     A  29                             14 22'Running script for Logical Files .-
     A                                      ..                                 -
     A                                       '
     A                                      OVRATR

      * to be displayed when LF Update is running
     A          R LFUPDF                    OVERLAY
     A                                      PUTOVR
     A                                 11  3'Total'
     A                                 11 16'Instructions'
     A                                 12  3'Instructions'
     A                                 12 16'Processed'
     A            ##LFTT         6N 0  13  3
     A                                      OVRDTA
     A            ##LFTP         6N 0  13 16
     A                                      OVRDTA
     A            ##LFSS       100   O 13 24
     A                                      OVRDTA
     A            ##LFPC         5   O 13126
     A                                      OVRDTA

     A                                 11121'Completion'

      * to be displayed when PF Update is running
     A          R PFUPDF                    OVERLAY
     A                                      PUTOVR
     A                                 10 28'Total'
     A                                 11 28'Files'
     A                                 10  3'Libary'
     A                                 11  3'(Job submitted) '
     A                                 10 96'Status'
     A                                 10 51'Started at'
     A                                 10 66'Ended at'
     A                                 10 36'Total Files'
     A                                 11 36'Processed'

     A            ##DMLB        10A  O 14  3
     A                                      OVRDTA
     A            ##DMJB        30A  O 15  3
     A                                      OVRDTA
     A            ##DMTF         5A  O 14 28
     A                                      OVRDTA
     A            ##DMTR         5A  O 14 38
     A                                      OVRDTA
     A  40        ##DMST          T  O 14 52TIMFMT(*HMS)
     A                                      OVRDTA
     A                                      TIMSEP(':')
     A  40        ##DMED          T  O 14 66TIMFMT(*HMS)
     A                                      TIMSEP(':')
     A                                      OVRDTA
     A  31        ##DMRT          T  O 14 82TIMFMT(*HMS)
     A                                      TIMSEP(':')
     A                                      OVRDTA
     A            ##DMSS        20A    14 96
     A                                      OVRDTA
     A  30                                  DSPATR(BL)
     A  30                                  COLOR(RED)

     A            ##DPLB        10A  O 16  3
     A                                      OVRDTA
     A            ##DPJB        30A  O 17  3
     A                                      OVRDTA
     A            ##DPTF         5A  O 16 28
     A                                      OVRDTA
     A            ##DPTR         5A  O 16 38
     A                                      OVRDTA
     A  40        ##DPST          T  O 16 52TIMFMT(*HMS)
     A                                      OVRDTA
     A                                      TIMSEP(':')
     A  40        ##DPED          T  O 16 66TIMFMT(*HMS)
     A                                      TIMSEP(':')
     A                                      OVRDTA
     A            ##DPSS        20A    16 96
     A                                      OVRDTA
     A  32                                  DSPATR(BL)
     A  32                                  COLOR(RED)

     A            ##DTLB        10A  O 18  3
     A                                      OVRDTA
     A            ##DTJB        30A  O 19  3
     A                                      OVRDTA
     A            ##DTTF         5A  O 18 28
     A                                      OVRDTA
     A            ##DTTR         5A  O 18 38
     A                                      OVRDTA
     A  40        ##DTST          T  O 18 52TIMFMT(*HMS)
     A                                      TIMSEP(':')
     A                                      OVRDTA
     A  40        ##DTED          T  O 18 66TIMFMT(*HMS)
     A                                      TIMSEP(':')
     A                                      OVRDTA
     A            ##DTSS        20A    18 96
     A                                      OVRDTA
     A  34                                  DSPATR(BL)
     A  34                                  COLOR(RED)

     A                                 10 84'Run'
     A                                 11 84'Time'
     A            ##HST           T  H
     A            ##HET           T  H
     A N33                             14 82'        '
     A  33        ##DPRT          T    16 82TIMFMT(*HMS)
     A                                      TIMSEP(':')
     A                                      OVRDTA
     A N35                             14 82'        '
     A  35        ##DTRT          T    18 82TIMFMT(*HMS)
     A                                      TIMSEP(':')
     A                                      OVRDTA

      * to be displayed when
     A          R NARRATIVEF                OVERLAY
     A                                      PUTOVR
     A            ##NARR        79     16 30
     A                                      OVRDTA
     A  39                                  DSPATR(BL)
     A  39                                  DSPATR(HI)
     A N39                                  DSPATR(ND)
     A  39                                  COLOR(WHT)
     A            ##NAR1        79     17 30OVRDTA
     A  39                                  DSPATR(BL)
     A  39                                  DSPATR(HI)
     A N39                                  DSPATR(ND)
     A  39                                  COLOR(WHT)

      * to be displayed always
     A          R RCDKEYF                   OVERLAY
     A                                      PUTOVR
     A  99                             26 53'F3=Exit'
     A                                 26 53'F2=Continue  F3=Exit'
     A  99
     AON38                                  DSPATR(ND)
     A                                      OVRATR
     A N99        ##KEY         63     26 17
     A N99        ##KEYP        45     26 81
     A*N99                             26 47'F3=Exit  F7=Display Summary Report-
     A*                                       F8=Display Detailed Report  F23=R-
     A*                                     ollback Changes'
     A* 99
     A*ON37
     A*O 37 42                               DSPATR(ND)
     A*                                      OVRATR
     A* N99                             26 27'F3=Exit  F6=Exception Report  F7=D-
     A*                                        isplay Summary Report  F8=Display-
     A*                                         Detailed Report  F23=F23=Rollbac-
     A*                                         k Changes'
     A*  99
     A*ON37
     A*O 37N42                               DSPATR(ND)
     A*                                      OVRATR
     A                                 27  2'(c) Finastra International Limited-
     A                                       2020'
     A                                      DSPATR(HI)

      * to clear format
     A          R CLEARF                    OVERLAY
     A                                  8  6' '
     A                                 11  6' '
     A                                 26  6' '

      * to be displayed when back up is running
     A          R BACKUPF                   OVERLAY
     A                                      PUTOVR
     A                                 11  3'Total'
     A                                 11 16'Instructions'
     A                                 12  3'Instructions'
     A                                 12 16'Processed'
     A            ##BUTT         6N 0  13  3
     A                                      OVRDTA
     A            ##BUTP         6N 0  13 16
     A                                      OVRDTA
     A            ##BUSS       100   O 13 24
     A                                      OVRDTA
     A            ##BUPC         5   O 13126
     A                                      OVRDTA

     A                                 11121'Completion'

      * to be displayed when reconcile is running
     A          R RECONCF                   OVERLAY
     A                                      PUTOVR
     A                                 11  3'Running reconciliation reports...'

      * to be displayed whenever there is an error
     A          R ERRORF                    OVERLAY
                                            PUTOVR
     A                                      OVRDTA
     A            ERR0         118     11 13
     A            ERR1         100     13 15
     A            ERR2         100     14 15
     A            ERR3         100     15 15
     A            ERR4         100     16 15
     A            ERR5         100     17 15
