      *****************************************************************
/*S*D****PFBASE********************************************************                       CUP021
/*E*S****PF_STD********************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  PF/UPSVALPD - Midas System Values Action File.               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      *  Note: This file matches the layout of the core file          *
      *        SDSVALPD with an additional action code field.         *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 17Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.02 ----------------------------------------*
      *  Prev Amend No. CSD013 *CREATE     Date 10Sep02               *
      *                                                               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CSD013 - Generic Midas ICD project.                          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R UPSVALD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A            GIACTC         1          TEXT('Action Code')
     A                                      COLHDG('Act.' +
     A                                             'Code' )
     A            GISVAL        20A         TEXT('System Value')
     A                                      COLHDG('System Value')
      *
     A            GIMMOD         2A         TEXT('Midas Module')
     A                                      COLHDG('Midas Module')
      *
     A            GIMMSN        10A         TEXT('Midas Module Shortname')
     A                                      COLHDG('Midas Module' +
     A                                             'Shortname' )
      *
     A            GIDESC        50A         TEXT('Description')
     A                                      COLHDG('Description')
      *
     A            GIVAL        200A         TEXT('Current Setting')
     A                                      COLHDG('Current Setting')
      *
     A            GIDFT        200A         TEXT('Default Setting')
     A                                      COLHDG('Default Setting')
      *
     A            GIHELP        20A         TEXT('User Help Info')
     A                                      COLHDG('User Help' 'Information')
      *
     A            GIATTR        10A         TEXT('Attributes')
     A                                      COLHDG('Attributes')
      *
     A            GIDLEN         3A         TEXT('Data String Length')
     A                                      COLHDG('Data' 'String' 'Length')
      *
     A            GISUPP        20A         TEXT('Suppress Update')
     A                                      COLHDG('Suppress' 'Update')
      *
     A            GISWCH         6A         TEXT('Introduced By')
     A                                      COLHDG('Introduced' 'By')
      *
     A            GIALWB         1A         TEXT('Allow Blanks')
     A                                      COLHDG('Allow' 'Blanks')
      *
     A            GIJUST         1A         TEXT('Justification')
     A                                      COLHDG('Justification')
      *
     A            GIEMSG         7A         TEXT('General Error Message')
     A                                      COLHDG('Gen' 'Error' 'Message')
      *
     A            GILCD          5P 0       TEXT('Last Change Date')
     A                                      COLHDG('Last' 'Change' 'Date')
      *
     A            GILCT          1A         TEXT('Last Change Type')
     A                                      COLHDG('Last' 'Change' 'Type')
