      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMWF                                                      *
/*EXI *  TEXT('Midas UP Bridge Midas system values')
      *****************************************************************
      *                                                               *
      *  Midas - Bridge Module                                        *
      *                                                               *
      *  LF/UPSVALJ0 - Bridge Midas System Values                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Last Amend No. CAP205             Date 24Mar10               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CPK016  *CREATE    Date 13Jun03               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CAP205 - Teller Related APIs - Account Balance Check         *
      *           (Recompile)                                         *
      *  CPK016 - Migration of Bridge ICD to the system values file.  *
      *           - New file.                                         *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R @SVALJ0                   JFILE(SDSVALPD SCSARVPD +
     A                                      SCSARDPD SDMMIDPD)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A          J                           JOIN(1 2)
     A                                      JFLD(GISVAL GSSVAL)
     A          J                           JOIN(2 3)
     A                                      JFLD(GSSARN SARN)
     A          J                           JOIN(1 4)
     A                                      JFLD(GIMMSN FOMMSN)
      *
     A            GISVAL
     A            GIMMOD
     A            GIMMSN
     A            GIDESC
     A            GIVAL
     A            GIDFT
     A            GIHELP
     A            GIATTR
     A            GIDLEN
     A            GISUPP
     A            GISWCH
     A            GIJUST
     A            GIALWB
     A            GIEMSG
     A            GILCD
     A            GILCT
      *
     A            GSSARN
     A            SARN
     A            GSAVAL
     A            FOMMIN
      *
     A          K GISVAL
      *
      * Select if the system value is for the Bridge module or the
      * Setupmidas module.
      *
     A          S GIMMOD                    COMP(EQ 'UP')
      *
     A          S GIMMOD                    COMP(EQ 'SM')
