      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UP Compare jobs maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade Module                                       *
      *                                                               *
      *  DSPF/UP000030DF - Compare Jobs Maintenance                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSM005  *REWRITE   Date 30Aug05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CPK009             Date 22Dec98               *
      *                 CUP001 *CREATE     Date 22Dec98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSM005 - UP0030DF renamed and moved to global layer.         *
      *  CPK009 - Change of field                                     *
      *  CUP001 - Introduction of BRIDGE                              *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A                                      HELP
     A                                      HLPPNLGRP(COMPARE UP000030HP)
     A                                      HLPTITLE('HELP')
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(33 'Invalid: #1SEL')
     A                                      INDTXT(34 'Invalid: #1JOB')
     A                                      INDTXT(35 'Invalid: #1USER')
     A                                      INDTXT(36 'Invalid: #1FILA')
     A  84                                  SFLNXTCHG
     A            #1DBRC        35   H      TEXT('Existing record fields')
     A            #1NBR          6   H      TEXT('Job number')
     A            #1MBRA        10   H      TEXT('Reference member')
     A            #1FILB        10   H      TEXT('Compare file')
     A            #1MBRB        10   H      TEXT('Compare member')
     A            #1SEL          1   B  9  2
     A                                      VALUES('1' '2' '3' +
     A                                      'E' 'D' 'S')
     A                                      CHECK(AB)
     A  78                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A  78N33                               DSPATR(ND)
     A N78N33                               DSPATR(UL HI)
     A            #1JOB         10   B  9  7TEXT('Job')
     A  88                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N88N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
     A            #1USER        10   B  9 20TEXT('User')
     A  88                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N88N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
     A            #1FILA        10   B  9 33TEXT('Source File')
     A  77                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N77N36                               DSPATR(UL)
     A N36                                  DSPATR(HI)
     A            #1LIBA        10   B  9 48TEXT('Library')
     A  77                                  DSPATR(PR)
     A  37                                  DSPATR(RI PC)
     A N77N37                               DSPATR(UL)
     A N37                                  DSPATR(HI)
     A            #1LIBB        10   B  9 63TEXT('Compare library')
     A  77                                  DSPATR(PR)
     A  38                                  DSPATR(RI PC)
     A N77N38                               DSPATR(UL)
     A N38                                  DSPATR(HI)
     A            #1INTG         1   B  9 77TEXT('Status')
     A  77                                  DSPATR(PR)
     A  39                                  DSPATR(RI PC)
     A N77N39                               DSPATR(UL)
     A N39                                  DSPATR(HI)
      * Start CSM005
     A            #1LIBC        10   H      TEXT('New library')
      * End CSM005
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(13) SFLSIZ(14)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CA11(11 'Print.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2JOB')
     A                                      SETOF(32 'Invalid: #2USER')
     A                                      SETOF(33 'Invalid: #2FILA')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A N25                                  OVRDTA
     A                                  1 72DATE
     A                                      EDTWRD('  /  /  ')
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 35'Compare Job'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  6  7'Member'
     A                                  6 20'User'
     A                                  5 33'Source'
     A                                  6 33'File'
     A                                  6 48'Upgrade Lib'
     A                                  6 63'Compare Lib'
     A                                  6 77'Sts'
     A N89        #2JOB         10   B  7  7TEXT('Job')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  31                                  DSPATR(RI PC)
     A N79N31                               DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A N89        #2USER        10   B  7 20TEXT('User')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A N79N32                               DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A N89        #2FILA        10   B  7 33TEXT('Source File')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N79N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
