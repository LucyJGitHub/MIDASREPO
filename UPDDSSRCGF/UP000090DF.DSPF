      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UP Maintain Bridge deliverables list')           *
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP000090DF - Maintain Bridge deliverables list               *
      *                                                               *
      *  (c) Finastra International Limited 2015                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CUP042  *CREATE    Date 28Jan15               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CUP042 - Simplification of Bridge driving file.              *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A N44                                  CA09
     A                                      CA05
     A N40                                  CA10
     A N41                                  CA03
     A N43                                  CA12
     A                                      INDARA
     A          R UP000090F0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2015')
     A            DFUSER        10A     1  2
     A                                  1 13'UP000090'
     A            DFDATE         7A     1 72
     A            DFJOBN        10A     2  2
     A                                  2 28'Bridge Deliverables List'
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A          R UP000090F1                OVERLAY
     A                                 23  2'F9=Insert'
     A                                 23 14'F3=Exit'
     A                                 23 24'F5=Refresh'
      *
      * Key fields; can only be amended if insert.
     A          R UP000090F2
     A                                      OVERLAY
      * Key fields; can only be amended if insert.
     A                                  6  2'Member name'
     A                                  6 27':'
     A            DFMBNM        10A  B  6 29
     A N70                                  DSPATR(HI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  70                                  DSPATR(RI PC)
      *
     A                                  7  2'Member type'
     A                                  7 27':'
     A            DFMBTP        10A  B  7 29
     A N71                                  DSPATR(HI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  71                                  DSPATR(RI PC)
      *
     A                                  8  2'Source file'
     A                                  8 27':'
     A            DFSRCF        10A  B  8 29
     A N72                                  DSPATR(HI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  72                                  DSPATR(RI PC)
      *
      * Fields that can be amended or inserted.
     A                                  9  2'Description'
     A                                  9 27':'
     A            DFMBTX        50A  B  9 29CHECK(LC)
     A N73                                  DSPATR(HI)
     A  37                                  DSPATR(UL)
     A N37                                  DSPATR(PR)
     A  73                                  DSPATR(RI PC)
      *
     A                                 10  2'Priority'
     A                                 10 27':'
     A            DFIPTY         1D 0B 10 29
     A N74                                  DSPATR(HI)
     A  37                                  DSPATR(UL)
     A N37                                  DSPATR(PR)
     A  74                                  DSPATR(RI PC)
      *
     A                                 11  2'Ignore or process (I/O)'
     A                                 11 27':'
     A            DFDTYP         1A  B 11 29
     A N75                                  DSPATR(HI)
     A  37                                  DSPATR(UL)
     A N37                                  DSPATR(PR)
     A  75                                  DSPATR(RI PC)
      *
     A                                 12  2'Copy option'
     A                                 12 27':'
     A            DFCPYO        10A  B 12 29
     A N76                                  DSPATR(HI)
     A  37                                  DSPATR(UL)
     A N37                                  DSPATR(PR)
     A  76                                  DSPATR(RI PC)
      *
     A                                 13  2'Bef/Aft or Spc. Copy'
     A                                 13 27':'
     A            DFFMTO        10A  B 13 29
     A N77                                  DSPATR(HI)
     A  37                                  DSPATR(UL)
     A N37                                  DSPATR(PR)
     A  77                                  DSPATR(RI PC)
      *
     A                                 14  2'Layer'
     A                                 14 27':'
     A            DFDTLB         8A  B 14 29
     A N78                                  DSPATR(HI)
     A  37                                  DSPATR(UL)
     A N37                                  DSPATR(PR)
     A  78                                  DSPATR(RI PC)
      *
     A                                 15  2'Object type'
     A                                 15 27':'
     A            DFPROJ        10A  B 15 29
     A N79                                  DSPATR(HI)
     A  37                                  DSPATR(UL)
     A N37                                  DSPATR(PR)
     A  79                                  DSPATR(RI PC)
      *
     A                                 16  2'Extra information'
     A                                 16 27':'
     A            DFEXIN        50A  B 16 29CHECK(LC)
     A N80                                  DSPATR(HI)
     A  37                                  DSPATR(UL)
     A N37                                  DSPATR(PR)
     A  80                                  DSPATR(RI PC)
      *
     A                                 17  2'Status'
     A                                 17 27':'
     A            DFUSTS         1A  B 17 29
     A N81                                  DSPATR(HI)
     A  37                                  DSPATR(UL)
     A N37                                  DSPATR(PR)
     A  81                                  DSPATR(RI PC)
      *
     A          R UP000090F3                OVERLAY
     A                                 12 30'No details to display.'
     A                                      DSPATR(HI)
     A          R UP000090F4                OVERLAY
     A                                 23  2'Enter to confirm'
     A                                 23 21'F12=Cancel'
     A                                 23 34'F5=Refresh'
     A          R UP000090F5                OVERLAY
     A                                 23  2'F10=Confirm delete'
     A                                 23 31'F12=Cancel'
      *
     A          R UP000090S0                SFL
     A  27                                  SFLNXTCHG
     A            DFAORD         1A  B  8  5
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A N51                                  DSPATR(PR)
     A N51                                  DSPATR(ND)
     A            DFMBNM        10A     8 10DSPATR(HI)
     A                                      TEXT('Member name')
     A            DFMBTP        10A     8 22DSPATR(HI)
     A                                      TEXT('Member type')
     A            DFSRCF        10A     8 34DSPATR(HI)
     A                                      TEXT('Source file')
     A            DFMBTX        50A  H      TEXT('Description')
     A            DFIPTY         1D 0H      TEXT('Priority')
     A            DFDTYP         1A  H      TEXT('Ignore or process')
     A            DFCPYO        10A  H      TEXT('Copy option')
     A            DFFMTO        10A  H      TEXT('Bef/Aft or Spc. Copy')
     A            DFDTLB         8A  H      TEXT('Layer')
     A            DFPROJ        10A  H      TEXT('Object type')
     A            DFEXIN        50A  H      TEXT('Extra information')
     A            DFUSTS         1A  H      TEXT('Status')
      *
     A          R UP000090C0                SFLCTL(UP000090S0)
     A  26                                  SFLCLR
     A  22                                  SFLDSPCTL
     A  23                                  SFLDSP
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(13)
     AO 24                                  SFLEND(*MORE)
     A N24                                  ROLLUP(20)
     A                                      ROLLDOWN(21)
     A                                      OVERLAY
     A  30                              3  5'Go to:'
     A N30                                  DSPATR(ND)
     A  30        DFSRCH        10A  B  3 13
     A  30                                  DSPATR(HI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(ND)
     A N30                                  DSPATR(PR)
     A  30                              5  2'(A/D/E)'
     A N30                                  DSPATR(ND)
     A  30                              5 10'Member'
     A N30                                  DSPATR(ND)
     A  30                              5 22'Type'
     A N30                                  DSPATR(ND)
     A  30                              5 34'Src. File'
     A N30                                  DSPATR(ND)
      *
     A          R UP000090S1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            DFMSGK                    SFLMSGKEY
     A            DFPGMQ                    SFLPGMQ
      *
     A          R UP000090C1                TEXT('Program messages')
     A                                      SFLCTL(UP000090S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  28                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            DFPGMQ                    SFLPGMQ
      *
