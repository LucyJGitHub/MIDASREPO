      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UP Bridge menu options maintenance')             *
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP000101DF - Bridge menu options maintenance                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2014            *
      *                                                               *
      *  Last Amend No. CUP041   *CREATE   Date 23Apr14               *
      *  Prev Amend No. xxxxxxxx           Date ddMmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUP041 - Restructure of deliverable data files.              *
      *                                                               *
      *****************************************************************
      *
     A N44                                  CA09
     A                                      CA05
     A N40                                  CA10
     A N41                                  CA03
     A N43                                  CA12
     A                                      INDARA
     A          R UP000101F0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2014')
     A            DFUSER        10A     1  2
     A                                  1 13'UP000101'
     A            DFDATE         7A     1 72
     A            DFJOBN        10A     2  2
     A                                  2 31'Bridge Menu Options'
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A          R UP000101F1                OVERLAY
     A                                 23  2'F9=Insert'
     A                                      COLOR(BLU)
     A                                 23 14'F3=Exit'
     A                                      COLOR(BLU)
     A                                 23 24'F5=Refresh'
     A                                      COLOR(BLU)
      *
     A          R UP000101F2                OVERLAY
      * Key fields; can only be amended if insert.
     A                                  4  2'Main menu option'
     A                                  4 35':'
     A            DFMAIN        20A  B  4 37CHECK(LC)
     A N70                                  DSPATR(HI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  70                                  DSPATR(RI PC)
      *
     A                                  5  2'Menu sub-task'
     A                                  5 35':'
     A            DFSUB         20A  B  5 37CHECK(LC)
     A N71                                  DSPATR(HI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  71                                  DSPATR(RI PC)
      *
     A                                  6  2'Layer'
     A                                  6 35':'
     A            DFLAYR         7A  B  6 37
     A N72                                  DSPATR(HI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  72                                  DSPATR(RI PC)
      *
      * Mode field; can never be amended.
     A                                  7  2'Core or Bespoke'
     A                                  7 35':'
     A            DFDMOD         7A  O  7 37
      *
      * Fields that can be only be amended if bespoke or insert.
      *
      * Fields that can be amended for bespoke or core or if insert.
     A                                  8  2'Status'
     A                                  8 35':'
     A            DFSTS          1A  B  8 37
     A N73                                  DSPATR(HI)
     A  37                                  DSPATR(UL)
     A N37                                  DSPATR(PR)
     A  73                                  DSPATR(RI PC)
      *
     A                                  9  2'Logically deleted ?'
     A                                  9 35':'
     A            DFDDEL         3A  O  9 37
      *
     A          R UP000101F3                OVERLAY
     A                                 12 30'No details to display.'
     A                                      DSPATR(HI)
     A          R UP000101F4                OVERLAY
     A                                 23  2'Enter to confirm'
     A                                      COLOR(BLU)
     A                                 23 21'F12=Cancel'
     A                                      COLOR(BLU)
     A                                 23 34'F5=Refresh'
     A                                      COLOR(BLU)
     A          R UP000101F5                OVERLAY
     A                                 23  2'F10=Confirm delete / reset'
     A                                      COLOR(BLU)
     A                                 23 31'F12=Cancel'
     A                                      COLOR(BLU)
      *
     A          R UP000101S0                SFL
     A  27                                  SFLNXTCHG
     A            DFAORD         1A  B  7  4
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A            DFMAIN        20A     7 10DSPATR(HI)
     A                                      TEXT('Main menu option')
     A            DFSUB         20A     7 32DSPATR(HI)
     A                                      TEXT('Menu sub-task')
     A            DFLAYR         7A     7 54DSPATR(HI)
     A                                      TEXT('Layer')
     A            DFSTS          1A  H      TEXT('Status')
     A            DFDMOD         7A     7 62DSPATR(HI)
     A                                      TEXT('Mode')
     A            DFMODE         1A  H      TEXT('Mode')
     A            DFDDEL         3A     7 71DSPATR(HI)
     A                                      TEXT('Logically deleted')
     A            DFDEL          1A  H      TEXT('Logically deleted')
      *
     A          R UP000101C0                SFLCTL(UP000101S0)
     A  26                                  SFLCLR
     A  22                                  SFLDSPCTL
     A  23                                  SFLDSP
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(14)
     A N24
     AO 24                                  SFLEND(*MORE)
     A                                      OVERLAY
     A  30                              3  2'Go to:'
     A N30                                  DSPATR(ND)
     A  30        DFSRCH        20A  B  3  9CHECK(LC)
     A  30                                  DSPATR(HI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(ND)
     A N30                                  DSPATR(PR)
     A  30                              5  2'(A/D/E)'
     A N30                                  DSPATR(ND)
     A  30                              5 10'Main Menu Option'
     A N30                                  DSPATR(ND)
     A  30                              5 32'Menu sub-task'
     A N30                                  DSPATR(ND)
     A  30                              5 54'Layer'
     A N30                                  DSPATR(ND)
     A  30                              5 62'Mode'
     A N30                                  DSPATR(ND)
     A  30                              5 71'Del?'
     A N30                                  DSPATR(ND)
      *
     A          R UP000101S1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            DFMSGK                    SFLMSGKEY
     A            DFPGMQ                    SFLPGMQ
      *
     A          R UP000101C1                TEXT('Program messages')
     A                                      SFLCTL(UP000101S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  28                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            DFPGMQ                    SFLPGMQ
      *****************************************************************
