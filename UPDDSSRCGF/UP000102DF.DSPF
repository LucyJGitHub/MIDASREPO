      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UP Adoption programs maintenance')               *
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP000102DF - Adoption programs                               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2014            *
      *                                                               *
      *  Last Amend No. CUP041   *CREATE   Date 23Apr14               *
      *  Prev Amend No. xxxxxxxx           Date ddMmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUP041 - Restructure of deliverable data files.              *
      *                                                               *
      *****************************************************************
      *
     A N44                                  CA09
     A                                      CA05
     A N40                                  CA10
     A N41                                  CA03
     A N43                                  CA12
     A                                      INDARA
     A          R UP000102F0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2014')
     A            DFDUSR        10A     1  2
     A                                  1 13'UP000102'
     A            DFDATE         7A     1 72
     A            DFJOBN        10A     2  2
     A                                  2 32'Adoption Programs'
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A          R UP000102F1                OVERLAY
     A                                 23  2'F9=Insert'
     A                                      COLOR(BLU)
     A                                 23 14'F3=Exit'
     A                                      COLOR(BLU)
     A                                 23 24'F5=Refresh'
     A                                      COLOR(BLU)
      *
     A          R UP000102F2                OVERLAY
      * Key fields; can only be amended if insert.
     A                                  4  2'Program'
     A                                  4 35':'
     A            DFPGM         10A  B  4 37
     A N70                                  DSPATR(HI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  70                                  DSPATR(RI PC)
      *
      * Mode field; can never be amended.
     A                                  5  2'Core or Bespoke'
     A                                  5 35':'
     A            DFDMOD         7A  O  5 37
      *
      * Fields that can be only be amended if bespoke or insert.
     A                                  6  2'Layer'
     A                                  6 35':'
     A            DFLAYR         7A  B  6 37
     A N71                                  DSPATR(HI)
     A  36                                  DSPATR(UL)
     A N36                                  DSPATR(PR)
     A  71                                  DSPATR(RI PC)
      *
      *
      * Fields that can be amended for bespoke or core or if insert.
     A                                  7  2'Use adoption authority?'
     A                                  7 35':'
     A            DFUSEA         4A  B  7 37
     A N72                                  DSPATR(HI)
     A  37                                  DSPATR(UL)
     A N37                                  DSPATR(PR)
     A  72                                  DSPATR(RI PC)
      *
     A                                  8  2'Adoption setting'
     A                                  8 35':'
     A            DFUSER         6A  B  8 37
     A N73                                  DSPATR(HI)
     A  37                                  DSPATR(UL)
     A N37                                  DSPATR(PR)
     A  73                                  DSPATR(RI PC)
      *
     A                                  9  2'Logically deleted ?'
     A                                  9 35':'
     A            DFDDEL         3A  O  9 37
      *
      *
     A          R UP000102F3                OVERLAY
     A                                 12 30'No details to display.'
     A                                      DSPATR(HI)
     A          R UP000102F4                OVERLAY
     A                                 23  2'Enter to confirm'
     A                                      COLOR(BLU)
     A                                 23 21'F12=Cancel'
     A                                      COLOR(BLU)
     A                                 23 34'F5=Refresh'
     A                                      COLOR(BLU)
     A          R UP000102F5                OVERLAY
     A                                 23  2'F10=Confirm delete / reset'
     A                                      COLOR(BLU)
     A                                 23 31'F12=Cancel'
     A                                      COLOR(BLU)
      *
     A          R UP000102S0                SFL
     A  27                                  SFLNXTCHG
     A            DFAORD         1A  B  7  4
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A            DFPGM         10A     7 10DSPATR(HI)
     A                                      TEXT('Adoption program')
     A            DFUSEA         4A  H      TEXT('Use adopted authority?')
     A            DFUSER         6A  H      TEXT('Adoption setting')
     A            DFLAYR         7A     7 23DSPATR(HI)
     A                                      TEXT('Layer')
     A            DFDMOD         7A     7 56DSPATR(HI)
     A                                      TEXT('Mode')
     A            DFMODE         1A  H      TEXT('Mode')
     A            DFDDEL         3A     7 70DSPATR(HI)
     A                                      TEXT('Logically deleted')
     A            DFDEL          1A  H      TEXT('Logically deleted')
      *
     A          R UP000102C0                SFLCTL(UP000102S0)
     A  26                                  SFLCLR
     A  22                                  SFLDSPCTL
     A  23                                  SFLDSP
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(14)
     A N24
     AO 24                                  SFLEND(*MORE)
     A                                      OVERLAY
     A  30                              3  2'Go to:'
     A N30                                  DSPATR(ND)
     A  30        DFSRCH        10A  B  3  9
     A  30                                  DSPATR(HI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(ND)
     A N30                                  DSPATR(PR)
     A  30                              5  2'(A/D/E)'
     A N30                                  DSPATR(ND)
     A  30                              5 10'Program'
     A N30                                  DSPATR(ND)
     A  30                              5 23'Layer'
     A N30                                  DSPATR(ND)
     A  30                              5 56'Mode'
     A N30                                  DSPATR(ND)
     A  30                              5 70'Del?'
     A N30                                  DSPATR(ND)
      *
     A          R UP000102S1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            DFMSGK                    SFLMSGKEY
     A            DFPGMQ                    SFLPGMQ
      *
     A          R UP000102C1                TEXT('Program messages')
     A                                      SFLCTL(UP000102S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  28                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            DFPGMQ                    SFLPGMQ
      *****************************************************************
