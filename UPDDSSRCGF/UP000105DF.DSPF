      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UP IBM objects to actioned maintenance')         *
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP000105DF - IBM objects to be actioned maintenance          *
      *                                                               *
      *  (c) Finastra International Limited 2015                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CUP042  *CREATE    Date 02Jan15               *
      *                 XXXXXXXX           Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CUP042 - Restructure of deliverable data files.              *
      *                                                               *
      *****************************************************************
      *
     A N44                                  CA09
     A                                      CA05
     A N40                                  CA10
     A N41                                  CA03
     A N43                                  CA12
     A                                      INDARA
     A          R UP000105F0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2014')
     A            DFUSER        10A     1  2
     A                                  1 13'UP000105'
     A            DFDATE         7A     1 72
     A            DFJOBN        10A     2  2
     A                                  2 30'IBM Actioned Objects'
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A          R UP000105F1                OVERLAY
     A                                 23  2'F9=Insert'
     A                                      COLOR(BLU)
     A                                 23 14'F3=Exit'
     A                                      COLOR(BLU)
     A                                 23 24'F5=Refresh'
     A                                      COLOR(BLU)
      *
     A          R UP000105F2                OVERLAY
      * Key fields; can only be amended if insert.
     A                                  4  2'Object'
     A                                  4 35':'
     A            DFIBMO        10A  B  4 37
     A N70                                  DSPATR(HI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  70                                  DSPATR(RI PC)
      *
     A                                  5  2'Type'
     A                                  5 35':'
     A            DFTYPE         7A  B  5 37
     A N71                                  DSPATR(HI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  71                                  DSPATR(RI PC)
      *
     A                                  6  2'Library'
     A                                  6 35':'
     A            DFLIB         10A  B  6 37
     A N72                                  DSPATR(HI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  72                                  DSPATR(RI PC)
      *
      * Mode field; can never be amended.
     A                                  7  2'Core or Bespoke'
     A                                  7 35':'
     A            DFDMOD         7A  O  7 37
      *
      * Fields that can be only be amended if bespoke or insert.
     A                                  8  2'Profile'
     A                                  8 35':'
     A            DFPROF        10A  B  8 37
     A N73                                  DSPATR(HI)
     A  36                                  DSPATR(UL)
     A N36                                  DSPATR(PR)
     A  73                                  DSPATR(RI PC)
      *
     A                                  9  2'Authority'
     A                                  9 35':'
     A            DFAUT          8A  B  9 37
     A N74                                  DSPATR(HI)
     A  36                                  DSPATR(UL)
     A N36                                  DSPATR(PR)
     A  74                                  DSPATR(RI PC)
      *
      * Fields that can be amended for bespoke or core or if insert.
      *
      * Logically deleted field; can never be directly amended.
     A                                 10  2'Logically deleted ?'
     A                                 10 35':'
     A            DFDDEL         3A  O 10 37
      *
     A          R UP000105F3                OVERLAY
     A                                 12 30'No details to display.'
     A                                      DSPATR(HI)
     A          R UP000105F4                OVERLAY
     A                                 23  2'Enter to confirm'
     A                                      COLOR(BLU)
     A                                 23 21'F12=Cancel'
     A                                      COLOR(BLU)
     A                                 23 34'F5=Refresh'
     A                                      COLOR(BLU)
     A          R UP000105F5                OVERLAY
     A                                 23  2'F10=Confirm delete / reset'
     A                                      COLOR(BLU)
     A                                 23 31'F12=Cancel'
     A                                      COLOR(BLU)
      *
     A          R UP000105S0                SFL
     A  27                                  SFLNXTCHG
     A            DFAORD         1A  B  7  4
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A            DFIBMO        10A     7 10DSPATR(HI)
     A                                      TEXT('Object')
     A            DFTYPE         7A     7 22DSPATR(HI)
     A                                      TEXT('Type')
     A            DFLIB         10A     7 32DSPATR(HI)
     A                                      TEXT('Library')
     A            DFDMOD         7A     7 64DSPATR(HI)
     A                                      TEXT('Mode')
     A            DFMODE         1A  H      TEXT('Mode')
     A            DFPROF        10A  H      TEXT('Profile')
     A            DFAUT          8A  H      TEXT('Authority')
     A            DFDDEL         3A     7 73DSPATR(HI)
     A                                      TEXT('Logically deleted')
     A            DFDEL          1A  H      TEXT('Logically deleted')
      *
     A          R UP000105C0                SFLCTL(UP000105S0)
     A  26                                  SFLCLR
     A  22                                  SFLDSPCTL
     A  23                                  SFLDSP
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(14)
     A N24
     AO 24                                  SFLEND(*MORE)
     A                                      OVERLAY
     A  30                              3  2'Go to:'
     A N30                                  DSPATR(ND)
     A  30        DFSRCH        10A  B  3  9
     A  30                                  DSPATR(HI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(ND)
     A N30                                  DSPATR(PR)
     A  30                              5  2'(A/D/E)'
     A N30                                  DSPATR(ND)
     A  30                              5 10'Object'
     A N30                                  DSPATR(ND)
     A  30                              5 22'Type'
     A N30                                  DSPATR(ND)
     A  30                              5 32'Library'
     A N30                                  DSPATR(ND)
     A  30                              5 64'Mode'
     A N30                                  DSPATR(ND)
     A  30                              5 73'Del?'
     A N30                                  DSPATR(ND)
      *
     A          R UP000105S1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            DFMSGK                    SFLMSGKEY
     A            DFPGMQ                    SFLPGMQ
      *
     A          R UP000105C1                TEXT('Program messages')
     A                                      SFLCTL(UP000105S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  28                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            DFPGMQ                    SFLPGMQ
      *****************************************************************
