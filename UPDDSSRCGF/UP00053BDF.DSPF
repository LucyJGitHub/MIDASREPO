      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UP Edit compare')
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade Module                                       *
      *                                                               *
      *  DSPF/UP00053BDF - Edit compare                               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSM005  *REWRITE   Date 18Aug05               *
      *  Prev Amend No. CAAA02             Date 16Jul03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CUP001 *CREATE     Date 15Jul98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSM005 - UP053BDF renamed and moved to global layer.         *
      *  CAAA02 - Remove use of LOCK keyword to enable WebFacing      *
      *           to process screens.                                 *
      *  CUP001 - Introduction of BRIDGE                              *
      *                                                               *
     A*****************************************************************
     A                                      DSPSIZ(27 132 *DS4                 -
     A                                             24 80 *DS3)
     A                                      PRINT(63)
     A                                      ALTHELP
     A                                      HELP
     A          R SFL                       TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      CHGINPDFT
     A            STMT           5Y 0B  3  2
     A N71        ZEIL1         70A  O  3  8
 FS2 A  71        ZEILW         93A  O  3  8
     A  19                                  DSPATR(BL HI)
     A          R SFLCTL                    SFLCTL(SFL)
     A                                      CA03(03 'End')
     A                                      CA06(06 'Move line')
     A                                      CA07(07 'Position forward break')
     A                                      CA08(08 'Position back break')
     A                                      CA11(11 'Accept all')
     A                                      CA13(13 'Accept')
     A                                      CA14(14 'Edit')
     A                                      CA15(15 'No Acceptance')
     A N71                                  CA19(19 'Window left')
     A N71                                  CA20(20 'Window right')
     A                                      CA21(21 'CMD Line')
     A                                      CA22(22 'SCAN')
     A                                      CA24(24 'More Keys')
     A                                      ROLLUP(60 'ROLLUP')
     A                                      ROLLDOWN(61 'ROLLDOWN')
     A                                      VLDCMDKEY(25 'VLDCMDKEY')
     A                                      KEEP
     A                                      CSRLOC(LINE       COL)
     A                                      OVERLAY
     A                                      HLPTITLE('Interactive Source-Level -
     A                                      Compare - Help')
     A  50                                  SFLDSP
     A                                      SFLDSPCTL
     A N50                                  SFLCLR
     A                                      SFLSIZ(0019)
     A                                      SFLPAG(0019)
     A          H                           HLPPNLGRP('GEN' UP000530HP)
     A                                      HLPARA(01 008 01 022)
     A          H                           HLPPNLGRP('MEMBUPG' UP000530HP)
     A                                      HLPARA(01 025 01 078)
     A          H                           HLPPNLGRP('MEMBBAS' UP000530HP)
     A                                      HLPARA(02 002 02 047)
     A          H                           HLPPNLGRP('MEMBWRK' UP000530HP)
     A                                      HLPARA(02 057 02 078)
     A          H                           HLPPNLGRP('CODEWRK' UP000530HP)
     A                                      HLPARA(03 009 21 013)
     A          H                           HLPPNLGRP('STEP' UP000530HP)
     A                                      HLPARA(01 001 01 006)
     A          H                           HLPPNLGRP('CORPUS' UP000530HP)
     A                                      HLPARA(03 009 21 080)
     A          H                           HLPPNLGRP('SRCNBR' UP000530HP)
     A                                      HLPARA(03 001 21 006)
     A          H                           HLPPNLGRP('KEYS' UP000530HP)
     A                                      HLPARA(22 001 23 080)
     A            LINE           3S 0H
     A            COL            3S 0H
     A            STEP           5A  O  1  2DSPATR(HI)
     A                                  1  8'Compare utility'
     A                                      COLOR(BLU)
     A                                  1 25'Member to be upgraded'
     A                                      DSPATR(RI)
     A            LIBFIL        21A  O  1 47DSPATR(RI)
     A            MEMB          10A  O  1 69DSPATR(RI)
     A                                  2 02'Basic member'
     A                                      DSPATR(HI)
     A            BASFIL        21A  O  2 15DSPATR(HI)
     A            MEMBO         10A  O  2 38DSPATR(HI)
     A                                  2 57'Work member'
     A                                      COLOR(BLU)
     A            MEMBER        10A  O  2 69DSPATR(HI)
     A          R FORM09
     A                                      ASSUME
     A                                  1  2' '
     A          R FORM10
     A                                      CA09(09 'RETRIEVE')
     A                                      CA10(10 'RETRIEVE back')
     A                                      OVERLAY
     A                                      HLPTITLE('Interactive Source-Level -
     A                                      Compare - Help')
     A                                      WINDOW(19 3 4 70)
     A                                      WDWBORDER((*COLOR BLU) (*CHAR '+-+]-
     A                                      ]+-+'))
     A                                      RMVWDW
     A          H                           HLPPNLGRP('SCAN' UP000530HP)
     A                                      HLPARA(01 001 03 070)
     A                                  1 35'Scan Prompt'
     A                                      DSPATR(HI)
     A                                  2  5'Scan :'
     A                                      COLOR(BLU)
     A            FLDSCN        50A  B  2 12DSPATR(HI)
     A                                      DSPATR(PC)
     A                                  3  5'F9=Retrieve previous input'
     A                                      COLOR(BLU)
     A                                  3 35'F10=Retrieve last input back'
     A                                      COLOR(BLU)
     A                                  2 65'F/B'
     A            FWDBCK         1A  B  2 70VALUES('B' 'F' ' ')
     A                                      DSPATR(HI)
     A                                      DFTVAL('F')
     A          R FORM08
     A                                      OVERLAY
     A                                      HLPTITLE('Interactive Source-Level -
     A                                      Compare - Help')
     A                                      WINDOW(19 3 4 70)
     A                                      WDWBORDER((*COLOR BLU) (*CHAR '+-+]-
     A                                      ]+-+'))
     A                                      RMVWDW
     A          H                           HLPPNLGRP('BORD' UP000530HP)
     A                                      HLPARA(01 001 03 070)
     A                                  1 30'Accept-Delete From-To'
     A                                      DSPATR(HI)
     A                                  2  5'From :'
     A                                      COLOR(BLU)
     A            FROM1          5  0O  2 12DSPATR(HI)
     A                                  2 30'To :'
     A                                      COLOR(BLU)
     A            TO1            5  0B  2 35DSPATR(HI)
     A                                  2 65'L/R'
     A            LIRG           1A  B  2 70VALUES('L' 'R')
     A                                      DSPATR(PC)
     A                                      DFTVAL('R')
     A                                      DSPATR(HI)
     A          R FORM03
     A                                      OVERLAY
     A                                      HLPTITLE('Interactive Source-Level -
     A                                      Compare - Help')
     A          H                           HLPPNLGRP('GEN' UP000530HP)
     A                                      HLPARA(22 001 23 080)
     A          H                           HLPPNLGRP('KEYS' UP000530HP)
     A                                      HLPARA(22 001 23 080)
     A                                 22  2'F03=End F06=Moved Line F07=Fo-
     A                                      rward F08=Back F11=Accept All -
     A                                      F13=Accept Line'
     A                                      COLOR(BLU)
     A                                 23  2'F14=Edit Line F15=Disable Line -
     A                                      F21=Cmd F22=Scan                -
     A                                        F24=More keys'
     A                                      COLOR(BLU)
     A          R FORM04
     A                                      OVERLAY
     A                                      HLPTITLE('Interactive Source-Level -
     A                                      Compare - Help')
     A          H                           HLPPNLGRP('KEYS' UP000530HP)
     A                                      HLPARA(22 001 23 080)
     A N71                             23 02'F19=Left'
     A                                      COLOR(BLU)
     A N71                             23 11'F20=Right'
     A                                      COLOR(BLU)
     A                                 23 21'                                  -
     A                                                  F24=More keys'
     A                                      COLOR(BLU)
     A                                 22 30'                            '
     A                                 22 60'                   '
     A                                 22  2'                           '
     A          R MSFL                      SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGM                       SFLPGMQ
     A          R MSFLC                     SFLCTL(MSFL)
     A                                      OVERLAY
     A N26                                  SFLDSP
     A N26                                  SFLDSPCTL
     A N26                                  SFLINZ
     A N26                                  SFLEND
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0001)
     A            PGM                       SFLPGMQ
     A          R FORM11
     A                                  1  2' '
