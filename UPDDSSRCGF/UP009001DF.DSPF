      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UP Compare file fields - maintenance')           *
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  DSPF/UP009001DF - Compare file fields - maintenance          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2011            *
      *                                                               *
      *  Last Amend No. CUP021  *CREATE    Date 11Jan11               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *                                                               *
      *****************************************************************
      *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A                                      CF03
     A                                      CF06
     A                                      CF12
      *
      * Message subfile format.
     A          R UP009001F2                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2011')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * Message subfile control.
     A          R UP009001C2                TEXT('Message subfile control')
     A                                      SFLCTL(UP009001F2)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * Main details.
     A          R UP009001F0
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                  1 14'UP009001DF'
     A                                  1 26'Compare File Fields - Maintenance'
     A                                      OVRATR
     A                                      DSPATR(HI UL)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                  2  2USER
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4  2'File Format'
     A            AFFILE        10A  B  4 24DSPATR(HI PR)
     A                                      OVRDTA
     A                                  4 36'Field Name'
     A            AFFDNM        10A  B  4 47DSPATR(HI PR)
     A                                      OVRDTA
     A                                  5  2'Field Text'
     A            AFFDTX        50A  B  5 24DSPATR(HI PR)
     A                                      OVRDTA
     A                                  6  2'Field Type'
     A            AFFDTP         1A  B  6 24DSPATR(HI PR)
     A                                      OVRDTA
     A                                  7  2'Field Length'
     A            AFFDLN         5  0B  7 24DSPATR(HI PR)
     A                                      OVRDTA
     A                                  8  2'Decimal Position'
     A            AFFDDP         2  0B  8 24DSPATR(HI PR)
     A                                      EDTWRD(' 0')
     A                                      OVRDTA
     A                                  9  2'Buffer Position'
     A            AFFDPS         5  0B  9 24DSPATR(HI PR)
     A                                      OVRDTA
     A                                 11  2'Key Field ?'
     A            DDKEYF         1A  B 11 24DSPATR(HI)
     A                                      OVRDTA
     A N12                                  DSPATR(UL)
     A  12                                  DSPATR(RI PC)
     A                                 12  2'Ignore for Compare ?'
     A            DDIGNC         1A  B 12 24DSPATR(HI)
     A                                      OVRDTA
     A N13                                  DSPATR(UL)
     A  13                                  DSPATR(RI PC)
      *
     A                                 13  2'Difference Indicator'
     A            DDDIFF         1A  B 13 24DSPATR(HI)
     A                                      OVRDTA
     A N14                                  DSPATR(UL)
     A  14                                  DSPATR(RI PC)
      *
     A                                 14  2'Ignore for Update?'
     A            DDIGNU         1A  B 14 24DSPATR(HI)
     A                                      OVRDTA
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(RI PC)
      *
      * Footer.
     A          R UP009001F1
     A                                      PUTOVR
     A                                 23  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A                                 24  2'F3=Exit  F6=Update  F12=Cancel'
     A                                      COLOR(BLU)
     A                                      OVRATR
