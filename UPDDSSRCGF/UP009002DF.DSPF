      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UP Compare files details - maintenance')         *
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  DSPF/UP009002DF - Compare files details - maintenance        *
      *                                                               *
      *  (c) Finastra International Limited 2011                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CUP021  *CREATE    Date 11Jan11               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *                                                               *
      *****************************************************************
      *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A                                      CF03
     A                                      CF06
     A                                      CF08
     A                                      CF10
     A                                      CF12
      *
      * Message subfile format.
     A          R UP009002F2                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2011')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * Message subfile control.
     A          R UP009002C2                TEXT('Message subfile control')
     A                                      SFLCTL(UP009002F2)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * Subfile record.
     A          R UP009002F0                SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2011')
     A            DDACTN         1A  I 12  2DSPATR(HI UL)
     A            DDFLDI         9A  O 12  4
     A  62                                  DSPATR(HI)
     A N08        DDMFNM        38A  O 12 14
     A  62                                  DSPATR(HI)
     A N08        DDATR1        11A  O 12 53
     A  62                                  DSPATR(HI)
     A N08        AFFDPS         5S 0O 12 66
     A  62                                  DSPATR(HI)
     A N08        AFKEYF         1A  O 12 73
     A  62                                  DSPATR(HI)
     A N08        AFIGNC         1A  O 12 77
     A  62                                  DSPATR(HI)
     A  08        DDATR2        11A  O 12 14
     A  62                                  DSPATR(HI)
     A  08        DDFDPS         5S 0O 12 27
     A  62                                  DSPATR(HI)
     A  08        DDKEYF         1A  O 12 35
     A  62                                  DSPATR(HI)
     A  08        DDIGNC         1A  O 12 39
     A  62                                  DSPATR(HI)
     A  08        AFDIFF         1A  O 12 44
     A  62                                  DSPATR(HI)
     A  08        AFIGNU         1A  O 12 50
     A  62                                  DSPATR(HI)
     A            AFFILE        10A  H
      *
      * Subfile control.
     A          R UP009002C0                SFLCTL(UP009002F0)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A                                      OVRATR
     A                                      ROLLDOWN(05)
     A                                      ROLLUP(06)
     A                                  1  2'UP009002DF'
     A                                  1 29'Compare Files Details +
     A                                      - Maintenance'
     A                                      OVRATR
     A                                      DSPATR(HI UL)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                  2  2USER
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4  2'Target File'
     A            FLNAME        10A  B  4 16DSPATR(HI)
     A                                      OVRDTA
     A N10 60                               DSPATR(UL)
     A  10                                  DSPATR(RI PC)
     A N60                                  DSPATR(PR)
     A                                  4 32'File for Keys'
     A            FLKYLF        10A  B  4 46DSPATR(HI)
     A                                      OVRDTA
     A N11 60                               DSPATR(UL)
     A  11                                  DSPATR(RI PC)
     A N60                                  DSPATR(PR)
     A                                  5  2'Description'
     A            FLFTXT        50A  B  5 16DSPATR(HI)
     A                                      OVRDTA
     A N12                                  DSPATR(UL)
     A  12                                  DSPATR(RI PC)
     A                                  6  2'Core File'
     A            FLREFF        10A  B  6 16DSPATR(HI)
     A                                      OVRDTA
     A N14                                  DSPATR(UL)
     A  14                                  DSPATR(RI PC)
     A                                  6 32'Action File?'
     A            FLACTF         1A  B  6 46DSPATR(HI)
     A                                      OVRDTA
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(RI PC)
     A                                  7  2'Field Offset'
     A            DPOFFS         3A  B  7 22DSPATR(HI)
     A                                      OVRDTA
     A N16                                  DSPATR(UL)
     A  16                                  DSPATR(RI PC)
     A                                  7 32'For Compare?'
     A            DPCMP          1A  B  7 46DSPATR(HI)
     A                                      OVRDTA
     A N17                                  DSPATR(UL)
     A  17                                  DSPATR(RI PC)
     A                                  8  2'Create Fields'
     A            DDCRFD         1A  B  8 20DSPATR(HI)
     A                                      OVRDTA
     A N27                                  DSPATR(UL)
     A  27                                  DSPATR(RI PC)
     A            DDHEAD        78A    10  2
     A                                      OVRDTA
     A                                      DSPATR(UL)
      *
      * Footer.
     A          R UP009002F1
     A                                      PUTOVR
     A                                 23  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A                                 24  2'F3=Exit  F6=Update  F8=Left/Right +
     A                                      F10=Delete  F12=Cancel +
     A                                           Rollup/down'
