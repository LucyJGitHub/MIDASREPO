      *****************************************************************
/*S*D ***DSPFBASE******************************************************
/*E*I ***TEXT('Midas*UP*Deliverables*list*-*collection*maint.')********
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade Module                                       *
      *                                                               *
      *  DSPF/UP0090D# - Deliverables list - collection maintenance.  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CUP042 *REDUNDANT  Date 15May15               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CUP001 *CREATE     Date 29Dec98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUP042 - Simplification of Bridge deliverables list.         *
      *  CUP001 - Introduction of BRIDGE                              *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(34 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1CAPR         1   H      TEXT('Change vs. Previous Rel.')
     A            #1IPTY         1  0H      TEXT('Installation Priority')
     A            #1DTYP         1   H      TEXT('Delivery Type')
     A            #1CPYO        10   H      TEXT('Copy Option')
     A            #1FMTO        10   H      TEXT('Format option')
     A            #1SRCF        10   H      TEXT('Source File')
     A            #1DTLB         8   H      TEXT('Destination Library')
     A            #1PROJ        10   H      TEXT('Project')
     A            #1EXIN        50   H      TEXT('Extra Information')
     A            #1USTS         1   H      TEXT('Upgrade Status')
     A            #1SEL          1   B  9  2
     A                                      VALUES('E' +
     A                                             'A' +
     A                                             'D' +
     A                                                 )
     A                                      CHECK(AB)
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A            #1MBNM        10      9  6TEXT('Member Name')
     A                                      DSPATR(HI)
     A            #1MBTP        10      9 18TEXT('Member Type')
     A                                      DSPATR(HI)
     A            #1MBTX        50      9 30TEXT('Member Text')
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(13) SFLSIZ(14)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF09(09 'Go to ''Add'' mode.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2MBNM')
     A                                      SETOF(32 'Invalid: #2MBTP')
     A                                      SETOF(33 'Invalid: #2MBTX')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##AANA         7      1 72TEXT('midas rundate')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 27'Deliverables List Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #2CAPR         1   H      TEXT('Change vs. Previous Rel.')
     A            #2IPTY         1  0H      TEXT('Installation Priority')
     A            #2DTYP         1   H      TEXT('Delivery Type')
     A            #2CPYO        10   H      TEXT('Copy Option')
     A            #2FMTO        10   H      TEXT('Format option')
     A            #2SRCF        10   H      TEXT('Source File')
     A            #2DTLB         8   H      TEXT('Destination Library')
     A            #2PROJ        10   H      TEXT('Project')
     A            #2EXIN        50   H      TEXT('Extra Information')
     A            #2USTS         1   H      TEXT('Upgrade Status')
     A                                  5  6'Member'
     A                                  5 18'Member'
     A                                  5 30'Member Text'
     A                                  6  6'Name'
     A                                  6 18'Type'
     A            #2MBNM        10   B  7  6TEXT('Member Name')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2MBTP        10   B  7 18TEXT('Member Type')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2MBTX        50   B  7 30TEXT('Member Text')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  2'A=Amend  D=Delete  E=Enquire  F3=-
     A                                      Main menu  F5=Refresh  F9=Go to ''-
     A                                      Add'' Mode'
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
