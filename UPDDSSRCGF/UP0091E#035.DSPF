      *****************************************************************
/*STD *  DSPFBASE                                                     *                   /*CUT003*/
/*EXI *  TEXT('Midas UP Deliverables list enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade Module                                       *
      *                                                               *
      *  DSPF/UP0091E# - Deliverables list enquiry.                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CUP001 *CREATE     Date 29Dec98               *
      *  Prev Amend No. nnnnnn             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUP001 - Introduction of BRIDGE                              *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,UP0091E#99
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*/COPY WNCPYSRC,UP0091E#98
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1MBNM')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'UP0091E1'
     A                                      DSPATR(HI)
     A            ##AANA         7      1 72TEXT('midas rundate')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 23'Display Deliverables list KEY SCR-
     A                                      EEN'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1MBTP        10   H      TEXT('Member Type')
     A                                  4  2'Member Name...:'
     A            #1MBNM        10   B  4 18TEXT('Member Name')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,UP0091E#97
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,UP0091E#96
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Exit'
     A*=========================================================================
     A*/COPY WNCPYSRC,UP0091E#95
     A          R #RCDDTL1                  TEXT('Details')
     A*/COPY WNCPYSRC,UP0091E#94
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'UP0091E2'
     A                                      DSPATR(HI)
     A            ##AANA         7      1 72TEXT('midas rundate')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 29'Deliverables List Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  5  2'Member Name...............:'
     A            #1MBNM        10      5 30TEXT('Member Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Member Type...............:'
     A            #1MBTP        10      6 30TEXT('Member Type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'Member Text...............:'
     A            #1MBTX        50      7 30TEXT('Member Text')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  9  2'Change vs. Previous Rel...:'
     A            #1CAPR         1      9 30TEXT('Change vs. Previous Rel.')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 10  2'Installation Priority.....:'
     A            #1IPTY         1  0  10 30TEXT('Installation Priority')
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 11  2'Delivery Type.............:'
     A            #1DTYP         1     11 30TEXT('Delivery Type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 12  2'Copy Option...............:'
     A            #1CPYO        10     12 30TEXT('Copy Option')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 13  2'Format Option.............:'
     A            #1FMTO        10     13 30TEXT('Format option')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 14  2'Source File...............:'
     A            #1SRCF        10     14 30TEXT('Source File')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 15  2'Destination Library.......:'
     A            #1DTLB         8     15 30TEXT('Destination Library')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 16  2'Project...................:'
     A            #1PROJ        10     16 30TEXT('Project')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 17  2'Upgrade Status............:'
     A            #1USTS         1     17 30TEXT('Upgrade Status')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 18  2'Extra Information.........:'
     A            #1EXIN        50     18 30TEXT('Extra Information')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,UP0091E#93
     A          R #CMDTXT2                  TEXT('Command keys')
     A*/COPY WNCPYSRC,UP0091E#92
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Main menu   F12=Previous'
     A*=========================================================================
     A*/COPY WNCPYSRC,UP0091E#91
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,UP0091E#90
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,UP0091E#89
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,UP0091E#88
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,UP0091E#87
