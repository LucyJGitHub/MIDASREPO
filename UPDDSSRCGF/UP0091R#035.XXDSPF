      *****************************************************************
/*S*D ***DSPFBASE******************************************************                   /*CUT003*/
/*E*I ***TEXT('Midas*UP*Deliverables*list*maintenance')****************
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade Module                                       *
      *                                                               *
      *  DSPF/UP0091R# - Deliverables list maintenance                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CUP042 *REDUNDANT  Date 15May15               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CUP001 *CREATE     Date 29Dec98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUP042 - Simplification of Bridge deliverables list.         *
      *  CUP001 - Introduction of BRIDGE                              *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,UP0091R#99
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*/COPY WNCPYSRC,UP0091R#98
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF09(09 'Change mode.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1MBNM')
     A                                      SETOF(32 'Invalid: #1MBTP')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'UP0091R1'
     A                                      DSPATR(HI)
     A            ##AANA         7      1 72TEXT('midas rundate')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 37'New Item'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  5  2'Member Name...............:'
     A            #1MBNM        10   B  5 30TEXT('Member Name')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  6  2'Member Type...............:'
     A            #1MBTP        10   B  6 30TEXT('Member Type')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,UP0091R#97
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,UP0091R#96
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,UP0091R#95
     A          R #RCDDTL1                  TEXT('Details')
     A*/COPY WNCPYSRC,UP0091R#94
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A N89                                  CF10(10 'Delete.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1MBTX')
     A                                      SETOF(32 'Invalid: #1CAPR')
     A                                      SETOF(33 'Invalid: #DINPS')
     A                                      SETOF(34 'Invalid: #1DTYP')
     A                                      SETOF(35 'Invalid: #1CPYO')
     A                                      SETOF(36 'Invalid: #1FMTO')
     A                                      SETOF(37 'Invalid: #1SRCF')
     A                                      SETOF(38 'Invalid: #1DTLB')
     A                                      SETOF(39 'Invalid: #1PROJ')
     A                                      SETOF(40 'Invalid: #1USTS')
     A                                      SETOF(41 'Invalid: #1EXIN')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'UP0091R2'
     A                                      DSPATR(HI)
     A            ##AANA         7      1 72TEXT('midas rundate')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 27'Deliverables List Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1IPTY         1  0H      TEXT('Installation Priority')
     A                                  5  2'Member Name...............:'
     A            #1MBNM        10      5 30TEXT('Member Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Member Type...............:'
     A            #1MBTP        10      6 30TEXT('Member Type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'Member Text...............:'
     A            #1MBTX        50   B  7 30TEXT('Member Text')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  9  2'Change vs. Previous Rel...:'
     A            #1CAPR         1   B  9 30TEXT('Change vs. Previous Rel.')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 10  2'Installation Priority.....:'
     A            #DINPS         1   B 10 30TEXT('Install Priority Screen')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 11  2'Delivery Type.............:'
     A            #1DTYP         1   B 11 30TEXT('Delivery Type')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 12  2'Copy Option...............:'
     A            #1CPYO        10   B 12 30TEXT('Copy Option')
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 13  2'Format Option.............:'
     A            #1FMTO        10   B 13 30TEXT('Format option')
     A N25                                  OVRDTA
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 14  2'Source File...............:'
     A            #1SRCF        10   B 14 30TEXT('Source File')
     A N25                                  OVRDTA
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 15  2'Destination Library.......:'
     A            #1DTLB         8   B 15 30TEXT('Destination Library')
     A N25                                  OVRDTA
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 16  2'Project...................:'
     A            #1PROJ        10   B 16 30TEXT('Project')
     A N25                                  OVRDTA
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 17  2'Upgrade Status............:'
     A            #1USTS         1   B 17 30TEXT('Upgrade Status')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 18  2'Extra Information.........:'
     A            #1EXIN        50   B 18 30TEXT('Extra Information')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,UP0091R#93
     A          R #CMDTXT2                  TEXT('Command keys')
     A*/COPY WNCPYSRC,UP0091R#92
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,UP0091R#91
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,UP0091R#90
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,UP0091R#89
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,UP0091R#88
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,UP0091R#87
