      *****************************************************************
/*STD *  DSPFBASE                                                     *
/**** *  TEXT('Midas UP Change job descriptions' library lists')                            MD060753
/*EXI *  TEXT('Midas UP Change job descriptions  library lists')                            MD060753
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  DSPF/UP0126DF - Change job description's library lists       *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD060753           Date 10Nov22               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSC017 *CREATE     Date 26Feb03               *
      *                 xxxxxx             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD060753 - Error in source compilation                       *
      *  MD046248 - Finastra Rebranding                               *
      *  CSC017 - Change library lists to 250.                        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *
      * Subfile
     A          R UP0126F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      SFL
     A  60                                  SFLNXTCHG
     A            #SEQ1          4   B  7  4TEXT('Sequence 1')
     A                                      DSPATR(UL)
     A  51                                  DSPATR(RI PC)
     A            #LIB1         10   B  7 12TEXT('Library 1')
     A                                      DSPATR(UL)
     A  61                                  DSPATR(PC)
     A            #SEQ2          4   B  7 31TEXT('Sequence 2')
     A N42                                  DSPATR(UL)
     A  42                                  DSPATR(PR ND)
     A  52                                  DSPATR(RI PC)
     A            #LIB2         10   B  7 39TEXT('Library 2')
     A N42                                  DSPATR(UL)
     A  42                                  DSPATR(PR ND)
     A  62                                  DSPATR(PC)
     A            #SEQ3          4   B  7 58TEXT('Sequence 3')
     A N43                                  DSPATR(UL)
     A  43                                  DSPATR(PR ND)
     A  53                                  DSPATR(RI PC)
     A            #LIB3         10   B  7 66TEXT('Library 3')
     A N43                                  DSPATR(UL)
     A  43                                  DSPATR(PR ND)
     A  63                                  DSPATR(PC)
      *
      * Subfile control
     A          R UP0126F2                  TEXT('Subfile control')
     A                                      SFLCTL(UP0126F1)
     A                                      SFLPAG(15) SFLSIZ(90)
     A  86                                  PUTOVR
     A  80                                  SFLDSP
     A  81                                  SFLDSPCTL
     A N80N81                               SFLCLR
     A  82                                  SFLEND(*MORE)
      * Command keys
     A                                      CA03(03 'Exit')
     A                                      CF05(05 'Refresh')
     A                                      CA12(12 'Cancel')
      *
     A            SRECNO         4S 0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A                                  1 13'UP0126DF'
     A                                  1 24'Edit Job Description Library List'
     A                                      DSPATR(HI)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A            ##JOB         10      2  2TEXT('*JOB')
     A            HEADING       40      2 24
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  5  2'Sequence'
     A                                      DSPATR(HI)
     A                                  5 29'Sequence'
     A                                      DSPATR(HI)
     A                                  5 56'Sequence'
     A                                      DSPATR(HI)
     A                                  6  3'Number'
     A                                      DSPATR(HI)
     A                                  6 12'Library'
     A                                      DSPATR(HI)
     A                                  6 30'Number'
     A                                      DSPATR(HI)
     A                                  6 39'Library'
     A                                      DSPATR(HI)
     A                                  6 57'Number'
     A                                      DSPATR(HI)
     A                                  6 66'Library'
     A                                      DSPATR(HI)
      *
      * Command keys line
     A          R UP0126C1                  TEXT('Command keys')
     A  86                                  OVERLAY PUTOVR
     A                                 23  2'F3=Exit'
     A                                 23 12'F5=Refresh'
     A                                 23 25'F12=Cancel'
      *
      * Messages
     A          R UP0126M1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            #MSGKEY                   SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      * Messages subfile control
     A          R UP0126M2                  TEXT('Program messages')
     A                                      SFLCTL(UP0126M1)
     A                                      SFLPAG(01) SFLSIZ(99)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
