      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UP Display objects not copied')
      *****************************************************************
      *                                                               *
      *  Midas - UP Module                                            *
      *                                                               *
      *  DSPF/UP0261DF  - Display objects not moved between libraries *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD058323           Date 16Jun21               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CPK025             Date 03Jul06               *
      *                 CPK016             Date 25Apr04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CUP006             Date 17Nov99               *
      *                 CPK010  *CREATE    Date 19Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD058323 - Error encountered while running CHDP              *
      *             (DP Lib migration check)during the Vanilla        *
      *             package installation.                             *
      *             Increase the subfile size record to 9999.         *
      *  MD046248 - Finastra Rebranding                               *
      *  CPK025 - MidasPlus 1.3 packaging.  Moved to global layer.    *
      *           For some reason this wasn't moved last time.        *
      *  CPK018 - MidasPlus packaging.  Moved to global layer.        *
      *  CUP006 - Enhancements to utility                             *
      *           Display message when no records left                *
      *  CPK010 - Utility for DBA R3.00                               *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A          R FMT1                      SFL
      *
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            XXOPT          1A  B  7  2VALUES('D' 'O' 'X' 'V' ' ')
     A            ODOBNM        10A  O  7  4
     A            ODLBNM        10A  O  7 15
     A            XXTYPE         6A  O  7 26
     A            XXTEXT        47A  O  7 33
     A            ODOBTP         8A  H
     A            ODSRCF        10A  H
     A            ODSRCL        10A  H
     A            ODSRCM        10A  H
      *
     A          R FMT2                      SFLCTL(FMT1)

     A                                      CHANGE(01 'On if any field changes')

      *
      ** Start of change MD058323
     A**********                            SFLSIZ(0030)                                    MD058323
     A                                      SFLSIZ(9999)
      ** End of change MD058323
      *
     A                                      SFLPAG(0014)
     A                                      TEXT('Subfile control format')
     A                                      CA03(03 'Exit')
     A                                      CA05(05 'Refresh')
     A                                      OVERLAY
     A                                      SFLDSP
     A N30                                  SFLDSPCTL
     A  30                                  SFLCLR
     A  50                                  SFLEND(*MORE)
     A            SUSER         10A  O  1  2
     A                                  1 13'UP0261'
     A                                  1 35'UP Module'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A            SWSID         10A  O  2  2
     A                                  2 25'   Display objects not moved   '
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  4  1'Go to'
     A            XXPOS         10A  B  4  7DSPATR(PC)
     A                                  4 52'Subset by type'
     A            XXSUB          6A  B  4 67
     A                                  6  4'Name'
     A                                      DSPATR(UL)
     A                                  6 15'Library'
     A                                      DSPATR(UL)
     A                                  6 26'Type'
     A                                      DSPATR(UL)
     A                                  6 33'Description'
     A                                      DSPATR(UL)
      *
     A          R FMT0
      *
     A                                 23  2'D=Delete'
     A                                      DSPATR(HI)
     A                                 23 12'O=Display object description'
     A                                      DSPATR(HI)
     A                                 23 42'V=View object'
     A                                      DSPATR(HI)
     A                                 23 57'X=Process request'
     A                                      DSPATR(HI)
     A                                 24  2'F3=Exit'
     A                                 24 13'F5=Refresh'
      *                                               Begin /CUP006/
     A          R NORCD

     A                                      TEXT('Blank format - no records')
     A                                      CA03(03 'Exit')
     A                                      CA05(05 'Refresh')
     A                                      OVERLAY
     A            SUSER         10A  O  1  2
     A                                  1 13'UP0261'
     A                                  1 35'UP Module'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A            SWSID         10A  O  2  2
     A                                  2 25'   Display objects not moved   '
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  4  1'Go to'
     A            XXPOS         10A  B  4  7DSPATR(PC)
     A                                  4 52'Subset by type'
     A            XXSUB          6A  B  4 67
     A                                  8 26'No records found'
     A                                      DSPATR(HI)
     A                                  6  4'Name'
     A                                      DSPATR(UL)
     A                                  6 15'Library'
     A                                      DSPATR(UL)
     A                                  6 26'Type'
     A                                      DSPATR(UL)
     A                                  6 33'Description'
     A                                      DSPATR(UL)
      *
     A                                 23  2'D=Delete'
     A                                      DSPATR(HI)
     A                                 23 12'O=Display object description'
     A                                      DSPATR(HI)
     A                                 23 42'V=View object'
     A                                      DSPATR(HI)
     A                                 23 57'X=Process request'
     A                                      DSPATR(HI)
     A                                 24  2'F3=Exit'
     A                                 24 13'F5=Refresh'
      *                                               End   /CUP006/
