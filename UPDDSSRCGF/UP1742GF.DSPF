      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UP Add menu')                                    *
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP1742GF - Add menu                                          *
      *                                                               *
      *  (c) Finastra International Limited 2023                      *
      *                                                               *
      *  Last Amend No. MD061576 *CREATE   Date 23Jul23               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD061576 - Deliverable Data Split for T_GRMENU*              *
      *                                                               *
      *****************************************************************

     A                                      CA03
     A                                      CA09
     A                                      CA10
     A                                      INDARA
     A          R UP1742F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2023')
     A            DFDUSR        10A     1  2
     A                                  1 13'UP1742'
     A            DFDATE         7A     1 72
     A            DFJOBN        10A     2  2
     A                                  3 27'Bridge utility - Add menu code'     -
     A                                      DSPATR(HI)
     A                                  2 71TIME


     A          R UP1742F2                  OVERLAY
     A                                  6  3'Menu code to be added.............-
     A                                      :'
     A            XMENU          9   B  6 39
     A  30N20                               DSPATR(RI PC)
     A N30N20                               DSPATR(HI)
     A  20                                  DSPATR(PR)
     A N40                              8  3'Group associated to hierarchy.....-
     A                                      :'
     A                                  7  3'Hierarchy id associated to menu...-
     A                                      :'
     A            XGROUP        15   B  8 39
     A  40                                  DSPATR(ND)
     A  31N21                               DSPATR(RI PC)
     A N31N21                               DSPATR(HI)
     A  21                                  DSPATR(PR)
     A            XHID          15   B  7 39
     A  32N20                               DSPATR(RI PC)
     A N32N20                               DSPATR(HI)
     A  20                                  DSPATR(PR)

     A          R UP1742F22                 OVERLAY
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A  21                             23 11'F9=Add in work file'
     A                                      COLOR(BLU)
     A  21                             23 32'F10=Replace in work file'
     A                                      COLOR(BLU)

     A          R UP1742S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            DFMSGK                    SFLMSGKEY
     A            DFPGMQ                    SFLPGMQ
      *
     A          R UP1742C1                  TEXT('Program messages')
     A                                      SFLCTL(UP1742S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  28                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            DFPGMQ                    SFLPGMQ
      *****************************************************************
