      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UP ADBU Update Driver file')
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP4020GF - ADBU Update Driver file                           *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. CUT017 *CREATE     Date 17Mar20               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUT017 - Adaptive Database Upgrade                           *
      *                                                               *
      *****************************************************************

     A                                      DSPSIZ(27 132)
     A N43                                  CA08
     A N44                                  CA11
     A                                      CA05
     A N43                                  CA10
     A N41                                  CA03
     A N43                                  CA12
     A                                      INDARA
     A          R UP4020F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2020')
     A            DFDUSR        10A     1  2
     A                                  1 13'UP4020'
     A            DFDATE         7A     1 72
     A            DFJOBN        10A     2  2
     A                                  2 21'Adaptive Database Upgrade - Driver-
     A                                       Items List'
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A          R UP4020F1                  OVERLAY
     A                                 23  2'F9=Insert'
     A                                      COLOR(BLU)
     A                                 23 14'F3=Exit'
     A                                      COLOR(BLU)
     A                                 23 24'F5=Refresh'
     A                                      COLOR(BLU)
     A                                 23 36'F11=Include/Exclude IGNORE'
     A                                      COLOR(BLU)
      *
     A          R UP4020F2                  OVERLAY
      * Key fields; can only be amended if insert.
     A                                  4  2'File................:'
     A            DFFILE        10A  B  4 24
     A N70                                  DSPATR(HI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  70                                  DSPATR(RI PC)
      *
      * Mode field; can never be amended.
     A                                  5  2'Library.............:'
     A            DFLIBR        10A  O  5 24
      *
      * Fields that can be only be amended if bespoke or insert.
     A                                  6  2'Action..............:'
     A            DFACTN        40A  B  6 24
     A N71                                  DSPATR(HI)
     A  36                                  DSPATR(UL)
     A N36                                  DSPATR(PR)
     A  71                                  DSPATR(RI PC)
     A                                      CHECK(LC)
      *
      *
      * Fields that can be amended for bespoke or core or if insert.
     A                                  7  2'Change Allowed?.....:'
     A            DFALLC         1A  O  7 24
      *
      *
     A          R UP4020F3                  OVERLAY
     A                                 12 30'No details to display.'
     A                                      DSPATR(HI)
     A          R UP4020F4                  OVERLAY
     A                                 23  2'Enter to confirm'
     A                                      COLOR(BLU)
     A                                 23 21'F12=Cancel'
     A                                      COLOR(BLU)
     A                                 23 34'F5=Refresh'
     A                                      COLOR(BLU)
     A          R UP4020F5                  OVERLAY
     A                                 23  2'F10=Confirm delete / reset'
     A                                      COLOR(BLU)
     A                                 23 31'F12=Cancel'
     A                                      COLOR(BLU)
      * for enquiry
     A          R UP4020F6                  OVERLAY
     A                                 27  3'Enter to confirm'
     A                                      COLOR(BLU)
     A                                 27 22'F8=Details'
     A                                      COLOR(BLU)
     A                                 27 56'F12=Cancel'
     A                                      COLOR(BLU)
     A            DFNAR1        76   O  8  3
     A            DFNAR2       123   O  9  5
     A            DFNAR3       123   O 10  5
     A            DFNAR4       123   O 11  5
     A            DFNAR5       123   O 12  5
     A            DFNAR6       127     13  5
     A            DFNAR7        78   O 14  3
     A            DFNAR8       123   O 15  5
     A            DFNAR9       123   O 16  5
     A            DFNA10       123   O 17  5
     A            DFNA11       123   O 18  5
     A            DFNA12       127   O 19  5
     A                                 22 16'New File'
     A                                 22 25'¦'
     A                                 22 27'Missing File'
     A                                 22 56'¦'
     A                                 22 58'Field Added'
     A                                 22 71'¦'
     A                                 22 73'Field Removed'
     A                                 22 88'¦'
     A                                 22 90'Field Changed'
     A                                 22 14'¦'
     A                                 23 14'¦----------------------------------
     A                                      ------------------------------------
     A                                      ----------------------¦'
     A                                 24 14'¦'
     A                                 24105'¦'
     A                                 22105'¦'
     A                                 19 15' '
     A                                 25 14'¦----------------------------------
     A                                      ------------------------------------
     A                                      ----------------------¦'
     A                                 21 14'¦----------------------------------
     A                                      ------------------------------------
     A                                      ----------------------¦'
     A                                 22 42'Amended File'
     A                                 22 40'¦'
     A            D_NF           1   O 24 19
     A                                      DSPATR(HI)
     A                                 24 25'¦'
     A            D_MF           1   O 24 32
     A                                      DSPATR(HI)
     A                                 24 40'¦'
     A            D_AF           1   O 24 48
     A                                      DSPATR(HI)
     A                                 24 56'¦'
     A            D_FA           1   O 24 63
     A                                      DSPATR(HI)
     A                                 24 71'¦'
     A            D_FR           1   O 24 78
     A                                      DSPATR(HI)
     A                                 24 88'¦'
     A            D_FC           1   O 24 96
     A                                      DSPATR(HI)
     A                                 27 35'F10=Script Details'
     A                                      COLOR(BLU)
      *
     A          R UP4020S0                  SFL
     A  27                                  SFLNXTCHG
     A            DFACTC         1A  B  7  4
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A            DFFILE        10A     7 10DSPATR(HI)
     A                                      TEXT('File')
     A            DFLIBR        10A     7 21DSPATR(HI)
     A                                      TEXT('Lib')
     A            DFACTN        40A     7 33DSPATR(HI)
     A                                      TEXT('Action')
     A            DFALLC         1A     7 94DSPATR(HI)
     A                                      TEXT('Change allowed')
     A            DFDEL          1A  H      TEXT('Logically deleted')
     A            DFEXCP         1A     7106DSPATR(HI)
     A                                      TEXT('Change allowed')
     A            DFFLCH         1A     7 81DSPATR(HI)
     A                                      TEXT('Attribute change')
     A          R UP4020C0                  SFLCTL(UP4020S0)
     A  26                                  SFLCLR
     A  22                                  SFLDSPCTL
     A  23                                  SFLDSP
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(14)
     A N24
     AO 24                                  SFLEND(*MORE)
     A                                      OVERLAY
     A  30                              3  2'Go to:'
     A N30                                  DSPATR(ND)
     A  30        DFSRCH        10A  B  3 10
     A  30                                  DSPATR(HI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(ND)
     A N30                                  DSPATR(PR)
     A  30                              5  2'(A/E)'
     A N30                                  DSPATR(ND)
     A  30                              5 10'File'
     A N30                                  DSPATR(ND)
     A  30                              5 21'Library'
     A N30                                  DSPATR(ND)
     A  30                              5 33'Update Action'
     A N30                                  DSPATR(ND)
     A  30                              5 94'Change'
     A N30                                  DSPATR(ND)
     A  30        DFSRCHL       10A  B  3 21
     A  30                                  DSPATR(HI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(ND)
     A N30                                  DSPATR(PR)
     A  30        DFSRCHA       40A  B  3 33
     A  30                                  DSPATR(HI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(ND)
     A N30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A  30        DFSRCHB        1A  B  3 81
     A  30                                  DSPATR(HI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(ND)
     A N30                                  DSPATR(PR)
     A  30                              5105'Exception'
     A N30                                  DSPATR(ND)
     A  30        DFSRCHC        1A  B  3 94
     A  30                                  DSPATR(HI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(ND)
     A N30                                  DSPATR(PR)
     A  30                              5105'Exception'
     A N30                                  DSPATR(ND)
     A  30        DFSRCHE        1A  B  3105
     A  30                                  DSPATR(HI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(ND)
     A N30                                  DSPATR(PR)
     A  30                              6 81'Attributes'
     A N30                                  DSPATR(ND)
     A  30                              5 81'Changed'
     A N30                                  DSPATR(ND)
     A  30                              6 94'Allowed'
     A N30                                  DSPATR(ND)
      *
     A          R UP4020S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            DFMSGK                    SFLMSGKEY
     A            DFPGMQ                    SFLPGMQ
      *
     A          R UP4020C1                  TEXT('Program messages')
     A                                      SFLCTL(UP4020S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  28                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            DFPGMQ                    SFLPGMQ
      *****************************************************************
