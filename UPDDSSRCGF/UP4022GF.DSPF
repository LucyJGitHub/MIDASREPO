      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UP ADBU System Values substitute')               *
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP4022GF - ADBU System Values substitute                     *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. CUT017 *CREATE     Date 17Mar20               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUT017 - Adaptive Database Upgrade                           *
      *                                                               *
      *****************************************************************

     A                                      CA03
     A N43                                  CA12
     A                                      INDARA
     A          R UP4022F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2020')
     A            DFDUSR        10A     1  2
     A                                  1 13'UP4022'
     A            DFDATE         7A     1 72
     A            DFJOBN        10A     2  2
     A                                  3 16'Adaptive Database Upgrade - Refere--
     A                                      nce Prefixes Prompt'
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A                                  6  3'Enter system prefix for Reference -
     A                                      System - Global Layer:'
     A                                  7  3'Enter system prefix for Reference -
     A                                      System - Zone Layer..:'
     A            G_REF          2   B  6 60
     A  35N20                               DSPATR(RI PC)
     A N35N20                               DSPATR(HI)
     A  20                                  DSPATR(PR)
     A            Z_REF          2   B  7 60
     A  36N20                               DSPATR(RI PC)
     A N36N20                               DSPATR(HI)
     A  20                                  DSPATR(PR)
     A  99        ERR_L1        76   O  9  3
     A                                      DSPATR(HI)
     A  99        ERR_L2        76   O 10  3
     A                                      DSPATR(HI)
     A  99        ERR_L3        76   O 11  3
     A                                      DSPATR(HI)
     A  99        ERR_L4        76   O 12  3
     A                                      DSPATR(HI)
     A                                 23  2'Enter=Confirm'
     A                                      COLOR(BLU)
     A                                 23 17'F3=Exit'
     A                                      COLOR(BLU)
      *
      *
     A          R UP4022S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            DFMSGK                    SFLMSGKEY
     A            DFPGMQ                    SFLPGMQ
      *
     A          R UP4022C1                  TEXT('Program messages')
     A                                      SFLCTL(UP4022S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  28                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            DFPGMQ                    SFLPGMQ
      *****************************************************************
