      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UP Save BTD/XTD tables')                         *
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP4030GF - Save BTD/XTD tables                               *
      *                                                               *
      *  (c) Finastra International Limited 2021                      *
      *                                                               *
      *  Last Amend No. MD059476 *CREATE   Date 01Dec21               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD059476 - Bridge improvement for refresh                    *
      *                                                               *
      *****************************************************************

     A**                                    DSPSIZ(27 132)
     A                                      CA05
     A                                      CA12
     A                                      CF09
     A                                      CA03
     A                                      CF06
     A                                      INDARA
     A          R UP4030F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2021')
     A            DFDUSR        10A     1  2
     A                                  1 13'UP4030'
     A            DFDATE         7A     1 72
     A            DFJOBN        10A     2  2
     A                                  2 19'Bespoke and Extension tables to sa-
     A                                      ve (Global)'
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A          R UP4030F1                  OVERLAY
     A*                                23  2'F9=Insert'
     A*                                     COLOR(BLU)
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 23 11'F5=Refresh'
     A                                      COLOR(BLU)
     A                                 23 23'F6=Select ALL'
     A                                      COLOR(BLU)
     A                                 23 38'F9=Save'
     A                                      COLOR(BLU)
      *
     A          R UP4030F2                  OVERLAY
      * Key fields; can only be amended if insert.
     A                                  4  2'File................:'
     A            DFFILE        10A  B  4 24
     A N70                                  DSPATR(HI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  70                                  DSPATR(RI PC)
      *
      * Mode field; can never be amended.
     A                                  5  2'Library.............:'
     A            DFLIBR        10A  O  5 24
      *
      * Fields that can be only be amended if bespoke or insert.
     A                                  6  2'Action..............:'
     A            DFACTN        40A  B  6 24
     A N71                                  DSPATR(HI)
     A  36                                  DSPATR(UL)
     A N36                                  DSPATR(PR)
     A  71                                  DSPATR(RI PC)
     A                                      CHECK(LC)
      *
      *
      * Fields that can be amended for bespoke or core or if insert.
     A                                  7  2'Change Allowed?.....:'
     A            DFALLC         1A  O  7 24
      *
      *
     A          R UP4030F3                  OVERLAY
     A                                 12 30'No details to display.'
     A                                      DSPATR(HI)
     A          R UP4030F4                  OVERLAY
     A            DFCONF        79     22  2
     A                                 23  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R UP4030F5                  OVERLAY
     A                                 23  2'F10=Confirm delete / reset'
     A                                      COLOR(BLU)
     A                                 23 31'F12=Cancel'
     A                                      COLOR(BLU)
      *
     A          R UP4030S0                  SFL
     A  27                                  SFLNXTCHG
     A            DFACTC         1A  B  7  3
     A  31                                  DSPATR(PR)
     A N31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A                                      DSPATR(PC)
     A            DFFILE        10A     7  6DSPATR(HI)
     A                                      TEXT('File')
     A            DFLIBR        10A     7 18DSPATR(HI)
     A                                      TEXT('Lib')
     A            DFDESC        50A     7 29DSPATR(HI)
     A                                      TEXT('Action')
     A          R UP4030C0                  SFLCTL(UP4030S0)
     A  26                                  SFLCLR
     A  22                                  SFLDSPCTL
     A  23                                  SFLDSP
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(14)
     A N24
     AO 24                                  SFLEND(*MORE)
     A                                      OVERLAY
     A N31                              5  2'(X)'
     A  31                                  DSPATR(ND)
     A  30                              5  6'Table'
     A N30                                  DSPATR(ND)
     A  30                              5 18'Library'
     A N30                                  DSPATR(ND)
     A  30                              5 29'Table Description'
     A N30                                  DSPATR(ND)
      *
     A          R UP4030S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            DFMSGK                    SFLMSGKEY
     A            DFPGMQ                    SFLPGMQ
      *
     A          R UP4030C1                  TEXT('Program messages')
     A                                      SFLCTL(UP4030S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  28                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            DFPGMQ                    SFLPGMQ
      *****************************************************************
