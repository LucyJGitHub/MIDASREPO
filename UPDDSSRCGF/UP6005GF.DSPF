      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UP OTU attention key screen')                    *
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP6005GF - OTU attention key screen                          *
      *                                                               *
      *  (c) Finastra International Limited 2022                      *
      *                                                               *
      *  Last Amend No. MD061706           Date 01Aug23               *
      *  Prev Amend No. CUP044 *CREATE     Date 22May22               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD061706 - Various changes for transition to aDBU            *
      *  CUP044 - One Touch Bridge Automation Changes - Release/      *
      *           Patch Upgrade                                       *
      *                                                               *
      *****************************************************************

     A                                      CA03
      * Start of MD061706
     A                                      CA24(24 'aDBU')
      * End of MD061706
     A                                      INDARA
     A          R UP6005F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2022')
     A            DFDUSR        10A     1  2
     A                                  1 13'UP6005'
     A            DFDATE         7A     1 72
     A            DFJOBN        10A     2  2
     A                                  3 23'One Touch Bridge - Upgrade Monitor' -
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A                                  6  3'Press F3 to signoff and leave the -
     A                                      One Touch Bridge Upgrade running.'
     A                                  7  3'Or press ENTER to get back to the -
     A                                      monitor screen.                  '
      * Start of MD061706
     A  90                              9  3'Press F24 to display aDBU monitor -
     A                                      screens (Compare or Update phase)'
      * End of MD061706
      *
     A          R UP6005S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            DFMSGK                    SFLMSGKEY
     A            DFPGMQ                    SFLPGMQ
      *
     A          R UP6005C1                  TEXT('Program messages')
     A                                      SFLCTL(UP6005S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  28                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            DFPGMQ                    SFLPGMQ
      *****************************************************************
