      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UP OTU restore package libraries')               *
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP6005GF - OTU restore package libraries                     *
      *                                                               *
      *  (c) Finastra International Limited 2022                      *
      *                                                               *
      *  Last Amend No. MD060362           Date 19Aug22               *
      *  Prev Amend No. CUP044 *CREATE     Date 22May22               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD060362 - DSPF does not compile                             *
      *  CUP044 - One Touch Bridge Automation Changes - Release/      *
      *           Patch Upgrade                                       *
      *                                                               *
      *****************************************************************

     A                                      CA03
     A N43                                  CA12
     A                                      INDARA
     A          R UP6006F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2022')
     A            DFDUSR        10A     1  2
     A                                  1 13'UP6006'
     A            DFDATE         7A     1 72
     A            DFJOBN        10A     2  2
     A                                  3 15'One Touch Bridge - Upgrade - Resto- -
     A                                      re package libraries'
     A                                      DSPATR(HI)
     A                                  2 71TIME


     A          R UP6006F2                  OVERLAY

     A                                  7  3'Install cumulative package (Y/N):'
     A            XCUM           1   B  7 37
     A  29N19                               DSPATR(RI PC)
     A N29N19                               DSPATR(HI)
     A  19                                  DSPATR(PR)

     A N18 19                           9  3'Enter package name specified in th-
     A                                      e Installation Notes:'
     A  18 19                           9  3'Enter package names specified in t-
     A                                      he Installation Notes:'
     A     19                          10 13'(Core package)'
     A     19     XPACK          9   B 10  3
     A  30N20                               DSPATR(RI PC)
     A N30N20                               DSPATR(HI)
     A  20                                  DSPATR(PR)
     A  18 19                          11 13'(Client package)'
     A  18 19     XPACK2         9   B 11  3
     A  31N20                               DSPATR(RI PC)
     A N31N20                               DSPATR(HI)
     A  20                                  DSPATR(PR)
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)

     A          R UP6006F22                 OVERLAY
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A  21                             23 10'Enter=Confirm restore of libraries'
     A                                      COLOR(BLU)

     A          R UP6006F21                 OVERLAY
      * Start MD060362
     A**********                       10  3'Enter library name where these sav-            MD060362
     A**********                            e files are placed. Each save file -            MD060362
     A**********                            contains'                                       MD060362
     A**********                       11  3'one installation library that will -           MD060362
     A**********                            be restored:'                                   MD060362
     A            ##DOT1        77A    10  3
     A            ##DOT2        77A    11  3
      * End MD060362
     A                                 13  3'Library'
     A                                 13 14'Save file'
     A            #LIBSFB       10   B 14  3
     A  51N21                               DSPATR(RI PC)
     A N51N21                               DSPATR(HI)
     A  21                                  DSPATR(PR)
     A            #LIBB         30   O 14 14
     A  51 21                               DSPATR(RI PC)
     A            #LIBSFD       10   B 15  3
     A  52N21                               DSPATR(RI PC)
     A N52N21                               DSPATR(HI)
     A  21                                  DSPATR(PR)
     A            #LIBD         30   O 15 14
     A  52 21                               DSPATR(RI PC)
     A            #LIBSFQ       10   B 16  3
     A  53N21                               DSPATR(RI PC)
     A N53N21                               DSPATR(HI)
     A  21                                  DSPATR(PR)
     A            #LIBQ         30   O 16 14
     A  53 21                               DSPATR(RI PC)
     A            #LIBSFU       10   B 17  3
     A  54N21                               DSPATR(RI PC)
     A N54N21                               DSPATR(HI)
     A  21                                  DSPATR(PR)
     A            #LIBU         30   O 17 14
     A  54 21                               DSPATR(RI PC)

     A          R UP6006F23                 OVERLAY
      * Start MD060362
     A**********                        7  3'Enter library name where these sav-            MD060362
     A**********                            e files are placed. Each save file -            MD060362
     A**********                            contains'                                       MD060362
     A**********                        8  3'one installation library that will -           MD060362
     A**********                            be restored:'                                   MD060362
     A            ##DOT3        77A     7  3
     A            ##DOT4        77A     8  3
      * End MD060362
     A                                 10  3'Library'
     A                                 10 14'Save file'
     A            #LIBCBN       10   B 11  3
     A  51N21                               DSPATR(RI PC)
     A N51N21                               DSPATR(HI)
     A  21                                  DSPATR(PR)
     A            #LIBCB        65   O 11 14
     A  51 21                               DSPATR(RI PC)

     A            #LIBCDN       10   B 12  3
     A  52N21                               DSPATR(RI PC)
     A N52N21                               DSPATR(HI)
     A  21                                  DSPATR(PR)
     A            #LIBCD        65   O 12 14
     A  52 21                               DSPATR(RI PC)

     A            #LIBCMN       10   B 13  3
     A  53N21                               DSPATR(RI PC)
     A N53N21                               DSPATR(HI)
     A  21                                  DSPATR(PR)
     A            #LIBCM        65   O 13 14
     A  53 21                               DSPATR(RI PC)

     A            #LIBCNN       10   B 14  3
     A  54N21                               DSPATR(RI PC)
     A N54N21                               DSPATR(HI)
     A  21                                  DSPATR(PR)
     A            #LIBCN        65   O 14 14
     A  54 21                               DSPATR(RI PC)

     A            #LIBCPN       10   B 15  3
     A  55N21                               DSPATR(RI PC)
     A N55N21                               DSPATR(HI)
     A  21                                  DSPATR(PR)
     A            #LIBCP        65   O 15 14
     A  55 21                               DSPATR(RI PC)

     A            #LIBCUN       10   B 16  3
     A  56N21                               DSPATR(RI PC)
     A N56N21                               DSPATR(HI)
     A  21                                  DSPATR(PR)
     A            #LIBCU        65   O 16 14
     A  56 21                               DSPATR(RI PC)

     A            #LIBCGN       10   B 17  3
     A  57N21                               DSPATR(RI PC)
     A N57N21                               DSPATR(HI)
     A  21                                  DSPATR(PR)
     A            #LIBCG        65   O 17 14
     A  57 21                               DSPATR(RI PC)

     A            #LIBCQN       10   B 18  3
     A  58N21                               DSPATR(RI PC)
     A N58N21                               DSPATR(HI)
     A  21                                  DSPATR(PR)
     A            #LIBCQ        65   O 18 14
     A  58 21                               DSPATR(RI PC)

     A            #LIBCZN       10   B 19  3
     A  59N21                               DSPATR(RI PC)
     A N59N21                               DSPATR(HI)
     A  21                                  DSPATR(PR)
     A            #LIBCZ        65   O 19 14
     A  59 21                               DSPATR(RI PC)

     A          R UP6006S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            DFMSGK                    SFLMSGKEY
     A            DFPGMQ                    SFLPGMQ
      *
     A          R UP6006C1                  TEXT('Program messages')
     A                                      SFLCTL(UP6006S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  28                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            DFPGMQ                    SFLPGMQ
      *****************************************************************
