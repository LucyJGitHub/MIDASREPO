      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UP OTR select layer screen')                     *
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP7000GF - OTR select layer screen                           *
      *                                                               *
      *  (c) Finastra International Limited 2022                      *
      *                                                               *
      *  Last Amend No. CUP045 *CREATE     Date 09Dec22               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUP045 - One Touch Bridge - refresh                          *
      *         - Applied for MD060769.                               *
      *                                                               *
      *****************************************************************

     A                                      CA03
     A                                      INDARA
     A          R UP7000F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2022')
     A                                      OVERLAY
     A            DFDUSR        10A     1  2
     A                                  1 13'UP7000'
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            DFJOBN        10A     2  2
     A                                  3 23'One Touch Bridge - Refresh Monitor'  -
     A                                      DSPATR(HI)
     A                                  2 71TIME

     A          R UP7000F1                  OVERLAY
     A                                  6  3'Use ''X'' to select the layer for -
     A                                      which refresh must run:'
     A                                 11  3'And enter system prefix of library-
     A                                       used for the refresh:'
     A  40        XSELG          1   B  8  3
     A  30N20                               DSPATR(RI PC)
     A N30N20                               DSPATR(HI)
     A  20                                  DSPATR(PR)
     A  41        XSELZ1         1   B  9  3
     A  31N20                               DSPATR(RI PC)
     A N31N20                               DSPATR(HI)
     A  20                                  DSPATR(PR)
     A  40        XNARG         53   O  8  5
     A  41        XNARZ1        53   O  9  5

     A                                 23 12'Enter=Confirm'
     A                                      COLOR(BLU)
     A                                 23  3'F3=Exit'
     A                                      COLOR(BLU)
     A            XLIBP          2   B 11 60
     A  32N20                               DSPATR(RI PC)
     A N32N20                               DSPATR(HI)
     A  20                                  DSPATR(PR)
      *
     A          R UP7000S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            DFMSGK                    SFLMSGKEY
     A            DFPGMQ                    SFLPGMQ
      *
     A          R UP7000C1                  TEXT('Program messages')
     A                                      SFLCTL(UP7000S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  28                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            DFPGMQ                    SFLPGMQ
      *****************************************************************
