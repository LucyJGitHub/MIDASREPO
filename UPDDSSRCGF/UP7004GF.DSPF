      *****************************************************************
/*STD *  DSPFDFRWN                                                    *
/*EXI *  TEXT('Midas UP OTR main monitor screen')                     *
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP7004GF - OTR main monitor screen                           *
      *                                                               *
      *  (c) Finastra International Limited 2022                      *
      *                                                               *
      *  Last Amend No. CUP045 *CREATE     Date 09Dec22               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUP045 - One Touch Bridge - refresh                          *
      *         - Applied for MD060769.                               *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(27 132)
     A                                      HELP
     A                                      ALTHELP
     A          R RECFMT
     A*                                     PUTOVR
     A                                      CA03(03 'Exit')
     A                                      CA22(22 'Selc')
     A            ##USR         10A  O  1  2TEXT('User ID')
     A                                      DSPATR(HI)
     A                                  1 13'UP7004'
     A                                      DSPATR(HI)
     A*                                     OVRDTA
     A            ##TME          6  0   2123TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      DSPATR(HI)
     A                                  2 50'One Touch Bridge - Refresh Monitor'
     A                                      DSPATR(HI)
     A                                  6  3'Total components to run...:'
     A*N50        ##OTTC        10N 0O  6 27
     A N50        ##OTTC         2A  O  6 31
     A                                  4102'Process started at.:'
     A                                  5102'Elapsed time.......:'
     A  18                              6102'Process competed at:'
     A N18                                  DSPATR(ND)
     A                                  1123DATE
     A                                      EDTCDE(Y)
     A                                      DSPATR(HI)
     A            ##STRT          T  O  4123TIMFMT(*HMS)
     A                                      TIMSEP(':')
     A            ##LAPS          T  O  5123TIMFMT(*HMS)
     A                                      TIMSEP(':')
     A  18        ##ENDT          T  O  6123
     A N18                                  DSPATR(ND)
     A                                      TIMFMT(*HMS)
     A                                      TIMSEP(':')
     A            ##CURT          T  O  7123DSPATR(ND)
     A                                      TIMFMT(*EUR)
     A                                 27  1'(c) Finastra International Limited-
     A                                       2022'
     A                                      DSPATR(HI)
     A            ##DOTL       127A     9  3
     A                                      DSPATR(HI)
     A                                  7  3'Components completed......:'
     A            ##CMSS         5   O  7 31
     A                                 10  6'Refresh Component'
     A N20                             10 94'Status'
     A N20                             10 55'Started at'
     A N20                             10 69'Ended at'
     A N20                             10 82'Run Time'

      ** the components
     A N20        ##STR1         8A    12 55
     A  31N20     ##END1         8A    12 69
     A N20        ##STS1        11A    12 94
     A  30                                  DSPATR(BL)
     A  30                                  COLOR(RED)
     A N31N20                          12 82'        '
     A  31N20     ##RTM1          T  O 12 82TIMFMT(*HMS)
     A                                      TIMSEP(':')
     A N20        ##STR11        8A    13 55
     A  81N20     ##END11        8A    13 69
     A N20        ##STS11       11A    13 94
     A  80                                  DSPATR(BL)
     A  80                                  COLOR(RED)
     A N81N20                          13 82'        '
     A  81N20     ##RTM11         T  O 13 82TIMFMT(*HMS)
     A                                      TIMSEP(':')
     A N20        ##STR2         8A    14 55
     A  33N20     ##END2         8A    14 69
     A N20        ##STS2        11A    14 94
     A  32                                  DSPATR(BL)
     A  32                                  COLOR(RED)
     A N33N20                          14 82'        '
     A  33N20     ##RTM2          T  O 14 82TIMFMT(*HMS)
     A                                      TIMSEP(':')
     A N20        ##STR3         8A    15 55
     A  35N20     ##END3         8A    15 69
     A N20        ##STS3        11A    15 94
     A  34                                  DSPATR(BL)
     A  34                                  COLOR(RED)
     A N35N20                          15 82'        '
     A  35N20     ##RTM3          T  O 15 82TIMFMT(*HMS)
     A                                      TIMSEP(':')
     A N20        ##STR4         8A    16 55
     A  37N20     ##END4         8A    16 69
     A N20        ##STS4        11A    16 94
     A  36                                  DSPATR(BL)
     A  36                                  COLOR(RED)
     A N37N20                          16 82'        '
     A  37N20     ##RTM4          T  O 16 82TIMFMT(*HMS)
     A                                      TIMSEP(':')
     A N20        ##STR5         8A    17 55
     A  39N20     ##END5         8A    17 69
     A N20        ##STS5        11A    17 94
     A  38                                  DSPATR(BL)
     A  38                                  COLOR(RED)
     A N39N20                          17 82'        '
     A  39N20     ##RTM5          T  O 17 82TIMFMT(*HMS)
     A                                      TIMSEP(':')
     A N20        ##STR6         8A    18 55
     A  41N20     ##END6         8A    18 69
     A N20        ##STS6        11A    18 94
     A  40                                  DSPATR(BL)
     A  40                                  COLOR(RED)
     A N41N20                          18 82'        '
     A  41N20     ##RTM6          T  O 18 82TIMFMT(*HMS)
     A                                      TIMSEP(':')
     A N20        ##STR7         8A    19 55
     A  43N20     ##END7         8A    19 69
     A N20        ##STS7        11A    19 94
     A  42                                  DSPATR(BL)
     A  42                                  COLOR(RED)
     A N43N20                          19 82'        '
     A  43N20     ##RTM7          T  O 19 82TIMFMT(*HMS)
     A                                      TIMSEP(':')
     A N20        ##STR8         8A    20 55
     A  45N20     ##END8         8A    20 69
     A N20        ##STS8        11A    20 94
     A  44                                  DSPATR(BL)
     A  44                                  COLOR(RED)
     A N45N20                          20 82'        '
     A  45N20     ##RTM8          T  O 20 82TIMFMT(*HMS)
     A                                      TIMSEP(':')

      ** the jobs
     A  20        ##JOB1        28A    12 56
     A  20        ##JOB11       28A    13 56
     A  20        ##JOB2        28A    14 56
     A  20        ##JOB3        28A    15 56
     A  20        ##JOB4        28A    16 56
     A  20        ##JOB5        28A    17 56
     A  20        ##JOB6        28A    18 56
     A  20        ##JOB7        28A    19 56
     A  20        ##JOB8        28A    20 56


     A            ##NARR        36     23 73
     A  89                                  DSPATR(BL)
     A  89                                  DSPATR(HI)
     A N89                                  DSPATR(ND)
     A  89                                  COLOR(WHT)

     A  18N20                          26  6'F3=Exit  F22=Display jobs'
     A  18 20                          26  6'F3=Exit  F22=Display jobs  V-
     A                                      =View joblog'
     A  25                             26  6'Attention key'
     A  18                             24 54'Refresh is now complete'
     A                                      DSPATR(HI)
     A  61N18
     AO 91N18
     AO 62N18
     AO 63N18
     AO 64N18
     AO 65N69N18
     AO 66N18
     AO 67N18
     AO 68N18                          26  6'V=View joblog  R=Restart'
     A  65 69N18                       26  6'V=View joblog  R=Restart  I=Ignore'
     A  20                             26  6'V=View joblog'

     A                                  4  3'Fusion Midas system.......:'
     A            ##GPRF         2      4 31
     A            ##HST           T  H
     A            ##HET           T  H
     A                                  5  3'Layer selected for refresh:'
     A            ##LYRS        33      5 31
     A                                 12  6'System Values validity checking'
     A                                 13  6'Deliverable Data Split save'
     A                                 14  6'Initialize refresh (end SBS-TRG-JR-
     A                                      N and CLRDB)'
     A                                 15  6'Run upgrade programs before refres-
     A                                      h'
     A                                 16  6'Refresh database'
     A                                 17  6'Deliverable Data Split restore'
     A                                 18  6'Run upgrade programs after refresh'
     A                                 19  6'Rebuild GZ* files with GOC0100A-
     A                                       (zone only)'
     A                                 20  6'Post refresh (start JRN-TRG-SBS)'

      * Action code fields to view or restart
     A  61
     AO 91
     AO 62
     AO 63
     AO 64
     AO 65N69
     AO 66
     AO 67
     AO 68                             10108'V or R'
     A  65 69                          10108'V, R or I'
     A  20                             10108'View joblog'
     A  20                             10 56'Jobs'
     A  20 22
     AO 61        ##ACT1         1   B 12108
     A N61N20                               DSPATR(ND)
     A  20 22
     A  20 22
     AO 91        ##ACT11        1   B 13108
     A N91N20                               DSPATR(ND)
     A  20 22
     AO 62        ##ACT2         1   B 14108
     A N62N20                               DSPATR(ND)
     A  20 22
     AO 63        ##ACT3         1   B 15108
     A N63N20                               DSPATR(ND)
     A  20 22
     AO 64        ##ACT4         1   B 16108
     A N64N20                               DSPATR(ND)
     A  20 22
     AO 65        ##ACT5         1   B 17108
     A N65N20                               DSPATR(ND)
     A  20 22
     AO 66        ##ACT6         1   B 18108
     A N66N20                               DSPATR(ND)
     A  20 22
     AO 67        ##ACT7         1   B 19108
     A N67N20                               DSPATR(ND)
     A  20 22
     AO 68        ##ACT8         1   B 20108
     A N68N20                               DSPATR(ND)
     A                                  4 34'Global'
     A            ##ZPRF         2   O  4 42
     A                                  4 45'Zone'

