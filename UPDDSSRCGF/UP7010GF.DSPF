      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UP OTR initialize')                              *
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP7010GF - OTR initialize screen                             *
      *                                                               *
      *  (c) Finastra International Limited 2022                      *
      *                                                               *
      *  Last Amend No. CUP045 *CREATE     Date 09Dec22               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUP045 - One Touch Bridge - refresh                          *
      *         - Applied for MD060769.                               *
      *                                                               *
      *****************************************************************

     A                                      CA03
     A                                      INDARA
     A          R UP7010F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2022')
     A                                      OVERLAY
     A            DFDUSR        10A     1  2
     A                                  1 13'UP7010'
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            DFJOBN        10A     2  2
     A                                  3 23'One Touch Bridge - Refresh Initial-  -
     A                                      ize'
     A                                      DSPATR(HI)
     A                                  2 71TIME

     A          R UP7010F1                  OVERLAY
     A                                  6  3'Enter the following system values:'
     A                                  8  3'System '
     A                                  9  3'Value'
     A                                  8 15'Description'
     A            #BRGLIB       10   B 11  3
     A  51N21                               DSPATR(RI PC)
     A N51N21                               DSPATR(HI)
     A  21                                  DSPATR(PR)
     A                                 11 15'BrgDeliveredBrgLib: Delivered Brid-
     A                                      ge library (*B)'
     A            #GLBPFX        2   B 12  3
     A  52N21                               DSPATR(RI PC)
     A N52N21                               DSPATR(HI)
     A  21                                  DSPATR(PR)
     A                                 12 15'BrgMidasGlobalPrefix: Global Layer-
     A                                       prefix'
     A            #ZONPFX        2   B 13  3
     A  53N21                               DSPATR(RI PC)
     A N53N21                               DSPATR(HI)
     A  21                                  DSPATR(PR)
     A                                 13 15'BrgMidasSystemPrefix: Zone Layer-
     A                                       prefix'
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A  21                             23 11'Enter=Confirm and update system va-
     A                                      lues'
     A                                      COLOR(BLU)

     A          R UP7010S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            DFMSGK                    SFLMSGKEY
     A            DFPGMQ                    SFLPGMQ
      *
     A          R UP7010C1                  TEXT('Program messages')
     A                                      SFLCTL(UP7010S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  28                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            DFPGMQ                    SFLPGMQ
      *****************************************************************
