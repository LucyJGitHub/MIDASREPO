      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas UP Action File for T_GRMENUL')
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  PF/UPGMULPD - Action File for T_GRMENUL                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      *  Note: This file matches the layout of the core file          *
      *        T_GRMENUL with an additional action code field and     *
      *        positional data.                                       *
      *        The field MLPOS should have value of *AFTER except a   *
      *        record is inserted/moved to be 1st record of T_GRMENUL *
      *        and it will be *BEFORE.                                *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CUP031 *CREATE     Date 05Jan05               *
      *  Prev Amend No. xxxxxx             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUP031 - Creation of new set of Action File utilities.       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R UPGMULD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
      *
     A            MLACTC         1          TEXT('Action Code')
     A                                      COLHDG('Act.' +
     A                                             'Code' )
     A            MLHYID         9B 0       TEXT('Hierarchy ID')
     A                                      COLHDG('Hierarchy ID')
     A            MLID          18B 0       TEXT('Link ID')
     A                                      COLHDG('Link ID')
     A            MLENDID       18B 0       TEXT('End ID')
     A                                      COLHDG('End ID')
     A            MLGRPID       18B 0       TEXT('Group ID')
     A                                      COLHDG('Group ID')
     A                                      DFT(*NULL)  ALWNULL
     A            MLLEVEL        4B 0       TEXT('Level')
     A                                      COLHDG('Level')
     A            MLPARENT      18B 0       TEXT('Parent Group')
     A                                      COLHDG('Parent Group')
     A            MLMENUCODE    10          TEXT('Menu Code')
     A                                      COLHDG('Menu Code')
     A                                      DFT(*NULL)  ALWNULL
     A            MLPOS          8          TEXT('Position')
     A                                      COLHDG('Position')
     A            MLKHYID        9B 0       TEXT('Key Hierarchy ID')
     A                                      COLHDG('Key' 'Hierarchy ID')
     A            MLKGRPID      18B 0       TEXT('Key Group ID')
     A                                      COLHDG('Key Group ID')
     A                                      DFT(*NULL)  ALWNULL
     A            MLKLEVEL       4B 0       TEXT('Key Level')
     A                                      COLHDG('Key' 'Level')
     A            MLKPARENT     18B 0       TEXT('Key Parent Group')
     A                                      COLHDG('Key' 'Parent Group')
     A            MLKMENUCOD    10          TEXT('Key Menu Code')
     A                                      COLHDG('Key' 'Menu Code')
     A                                      DFT(*NULL)  ALWNULL
