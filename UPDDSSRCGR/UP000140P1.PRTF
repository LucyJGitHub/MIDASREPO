      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas UP Module & program cross check report')
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  PRTF/UP000140P1 - Module & program cross check report        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CUP029 *CREATE     Date 30Sep04               *
      *  Prev Amend No. xxxxxx             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUP029 - Utility to cross check module and program sources   *
      *           in a library.                                       *
      *                                                               *
      *****************************************************************
      *
      *  Formats for Program UP000140
      *
     A          R HEAD                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SKIPB(2)
      *
     A            PSUSER        10         3
     A                                    30'Cross Check of Modules and +
     A                                       Programs in Library '
     A            @LIBLYR       33        77
     A                                   119'Date'
     A                                   124DATE
     A                                      EDTWRD('  /  /  ')
     A                                      SPACEA(1)
     A            PSJOBNAME     10         3
     A            @DELV         18        56
     A                                   119'Time'
     A                                   124TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                     3'UP000140P1'
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(3)
     A                                      SPACEA(2)
      *
     A  41                                 3'Modules that do not have any +
     A                                       matched programs nor in any +
     A                                       service programs:'
     A                                      SPACEA(2)  HIGHLIGHT
     A  42                                 3'Programs do not have any +
     A                                       matched modules delivered:'
     A                                      SPACEA(2) HIGHLIGHT
     A  43                                 3'Modules duplicated in +
     A                                       service programs:'
     A                                      SPACEA(2) HIGHLIGHT
     A  44                                 3'Modules listed in service +
     A                                       programs but not delivered:'
     A                                      SPACEA(2) HIGHLIGHT
      *
     A                                     3'Source File'
      *
     A  41                                17'Module'
     A  42                                17'Program'
     A  43
     AO 44                                17'Service Pgm'
      *
     A  41
     AO 42                                31'Description'
     A                                      SPACEA(2)
     A  43
     AO 44                                31'Module'
     A                                      SPACEA(2)
      *
     A          R DETAIL
     A
     A  25        @SRCFILE      10         3
     AO 26        @MBRNAME      10        17
     A  41
     AO 42        @MBRTXT       50        31
     A                                      SPACEA(1)
     A  44        @MODULE       10        31
     A                                      SPACEA(1)
      *
      * Details for duplicated module report
     A          R DETAILDM
     A  25        @SRCFILE      10         3
     A N27        @MBRNAM1      10        17
     A                                      SPACEA(1)
     A  27        @MBRNAME      10        17
     A  27        @MODULE       10        31
     A                                      SPACEA(1)
      *
     A          R NORECS
                                          48'*** No details to report ***'
     A                                      SPACEA(2)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP
     A                                      SPACEB(2)
     A                                    51'***  END OF REPORT  ***'
