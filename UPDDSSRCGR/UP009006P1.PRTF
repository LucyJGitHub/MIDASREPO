      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas UP Report for file compare')                     *
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  PRTF/UP009006P1 - Report for File Compare                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2011            *
      *                                                               *
      *  Last Amend No. A1084804           Date 01Feb13               *
      *  Prev Amend No. CUP021  *CREATE    Date 11Jan11               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  A1084804 - New *MRG option.                                  *
      *  CUP021 - Rewrite of Action File processing                   *
      *                                                               *
      *****************************************************************
      *
     A          R HEADH                     TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2011')
     A                                      SKIPB(2)
      *
     A                                     6'Reference'
     A            RPGM          10A       16
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(2)
     A            RHDR          90A       27
     A                                      SPACEA(2)
     A                                    42'Produced on'
     A                                    54DATE
     A                                      EDTWRD('  /  /  ')
     A                                    63'at'
     A                                    66TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
     A            RFL1T         13A       42
     A            RFIL1         21A       60
     A                                      SPACEA(1)
     A            RFL2T         13A       42
     A            RFIL2         21A       60
     A                                      SPACEA(2)
     A                                     5'Action'
     A                                      SPACEA(1)
     A                                     5'Type'
     A                                    30'Compare'
     A                                      SPACEA(1)
     A                                     5'------'
     A                                    30'-------------------------------+
     A                                      ---------------------------------+
     A                                      ---------------------------------+
     A                                      ---'
     A                                      SPACEA(2)
      *
     A          R AMENDERR
     A                                      SPACEB(5)
     A                                    40'*** Error - incorrect number of +
     A                                      Amends ***'
     A                                      SPACEA(5)
     A                                    37'*** Correct the Action File and -
     A                                      re-run report ***'
     A                                      SPACEA(2)
      *
     A          R KEYERR
     A                                      SPACEB(5)
     A                                    40'Key is not setup / incorrect'
      *
     A          R NODIFS
     A                                      SPACEB(5)
     A                                    40'*** No conflicts to report ***'
      *
     A          R NOERRS
     A                                      SPACEB(2)
     A                                    51'***  No difference to report ***'
      *
      * Start A1084804
     A          R NORECS
     A                                      SPACEB(3)
     A                                    40'*** No records to report ***'
      * End   A1084804
      *
     A          R DTL1
     A N41                                  SPACEB(1)
     A            RDNUM          5  0     24EDTWRD('    0')
     A            RCNT         100A       30
     A                                      SPACEA(1)
     A            RACT           7A        5
      *
     A  42                                15'Identical:'
     A  39N42                             15'Exists:'
     A N39N42                             15'Missing:'
     A            RUPDL        100        30
     A                                      SPACEA(2)
      *
     A          R DTL2
     A N41                                  SPACEB(1)
     A            RDNUM          5  0     24EDTWRD('    0')
     A            RCNT         100A       30
     A                                      SPACEA(1)
      *
     A            RACT           7A        5
     A            RTTXT         12A       15
     A            RSYSL        100A       30
     A                                      SPACEA(1)
     A                                    15'Differences:'
     A            RINDL        100A       30
     A                                      SPACEA(1)
     A            RFTXT         12A       15
     A            RUPDL        100A       30
     A                                      SPACEA(2)
      * Start A1084804
     A          R MRG1
     A                                      SPACEB(1)
     A            RACTO          7A        5
     A            RFILO         10A       13
     A            RKEYO        100A       24
     A                                      SPACEA(1)
     A            RACTN          7A        5
     A                                      SPACEA(2)
      * End   A1084804
      *
      * Report trailer.
     A          R TRAILP
     A                                      SPACEB(2)
     A                                    51'***  End of Report  ***'
