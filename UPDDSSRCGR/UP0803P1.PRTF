      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas UP Conflict report for deliverable data')
      *****************************************************************
      *                                                               *
      *  Midas - UP Module                                            *
      *                                                               *
      *  PRTF/UP0803P1 - Action file compatibility report for         *
      *                  UPCMPNPD and CBCMPNL0                        *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CPK018             Date 25Apr04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CUP010 *C *CREATE  Date 15Aug98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CPK018 - MidasPlus packaging.  Moved to global layer.        *
      *  CUP010 - Action File compatibilty report                     *
      *                                                               *
      *****************************************************************
      *
     A          R HEADH                     TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2000')
     A                                      SKIPB(2)
      *
     A                                     6'REFERENCE'
     A            DBPGM         10A       16
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(2)
     A            HEAD          90A       27
     A                                      SPACEA(2)
     A                                    42'Produced on'
     A                                    54DATE
     A                                      EDTWRD('  /  /  ')
     A                                    63'at'
     A                                    66TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
     A                                    42'Existing File:'
     A            SYSFIL        10A       60
     A                                      SPACEA(1)
     A                                    42'Action File:'
     A            UPDFIL        10A       60
     A                                      SPACEA(2)
     A                                     5'Action'
     A                                      SPACEA(1)
     A                                     5'Type'
     A                                    30'Compare'
     A                                      SPACEA(1)
     A                                     5'------'
     A                                    30'-------------------------------+
     A                                      ---------------------------------+
     A                                      ---------------------------------+
     A                                      ---'
     A                                      SPACEA(2)
      *
     A          R AMENDERR
     A                                      SPACEB(5)
     A                                    40'*** ERROR - INCORRECT NUMBER OF -
     A                                      AMENDS ***'
     A                                      SPACEA(5)
     A                                    37'*** Correct the ACTION FILE and -
     A                                      rerun report ***'
     A                                      SPACEA(2)
      *
     A          R NODIFS
     A                                      SPACEB(5)
     A                                    40'*** No conflicts to report ***'
      *
     A          R DTL1
     A N41                                  SPACEB(1)
     A            COUNT        100A       30
     A                                      SPACEA(1)
     A N41        ACTION         7A        5
      *
     A  42                                15'Identical:'
     A  39N42                             15'Exists:'
     A N39N42                             15'Missing:'
     A            UPDLIN       100        30
     A                                      SPACEA(2)
      *
     A          R DTL2
     A N41                                  SPACEB(1)
     A            COUNT        100A       30
     A                                      SPACEA(1)
      *
     A N41        ACTION         7A        5
     A                                    15'Existing File:'
     A            SYSLIN       100A       30
     A                                      SPACEA(1)
     A                                    15'Differences:'
     A            INDLIN       100A       30
     A                                      SPACEA(1)
     A                                    15'Action File:'
     A            UPDLIN       100A       30
     A                                      SPACEA(2)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP
     A                                      SPACEB(2)
     A                                    51'***  END OF REPORT  ***'
