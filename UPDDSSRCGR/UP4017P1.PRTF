      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas UP ADBU Summary Reconcile Report')
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade Module                                       *
      *                                                               *
      *  PRTF/UP4017P1 - Midas UP ADBU Summary Reconcile Report       *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. MD061992           Date 23Oct23               *
      *  Prev Amend No. CUT017 *CREATE     Date 17Mar20               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD061992 - Allow 3 char prefix for reference libraries       *
      *  CUT017 - Adaptive Database Upgrade                           *
      *                                                               *
      *****************************************************************
      *
      *
     A          R HEADH                     TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2020')
     A                                      SKIPB(2)
     A                                     5'Reference: UP4017P1'
     A                                      SPACEB(0)
     A  20                                34'Adaptive Database Upgrade - Reconc-
     A                                      ile Report for Global Layer - Summa-
     A                                      ry'
     A N20                                34'Adaptive Database Upgrade - Reconc-
     A                                      ile Report for Zone Layer - Summary'
      *
     A                                   119'Date'
     A                                   124DATE
     A                                      EDTWRD('  /  /  ')
     A                                   119'Time'
     A                                      SPACEB(1)
     A                                   124TIME
     A                                      EDTWRD('  :  :  ')
     A                                   119'Page'
     A                                      SPACEB(1)
     A                                   124PAGNBR
     A                                      EDTCDE(3)
     A                                      SPACEA(2)
      *
     A          R NORECS
     A            R_NORLC       20         5
      * Start MD061992
     A**********  R_NORLR       20        48SPACEA(2)                                       MD061992
     A            R_NORLR       21        48SPACEA(2)
      * End MD061992
     A                                    40'*** No details to report ***'
     A                                      SPACEA(2)
     A          R SUMMARY
     A                                     5'System compared:'
     A            R_SYS          2        22
     A                                    48'Reference System:'
      * Start MD061992
     A**********  R_SYSR         2        66                                                MD061992
     A            R_SYSR         3        66
      * End MD061992
     A                                     4'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------'
     A                                      SPACEB(1)
     A                                      SPACEA(3)
     A            R_MLIB        10        10SPACEB(2)
     A                                    22'New files....:'
     A            R_M_NEW       12        37
     A            R_PLIB        10        69
     A                                    81'New files....:'
     A            R_P_NEW       12        96
     A                                    22'Amended files:'
     A                                      SPACEB(1)
     A            R_M_AMD       12        37
     A                                    81'Amended files:'
     A            R_P_AMD       12        96
     A                                    22'Missing files:'
     A                                      SPACEB(1)
     A            R_M_MIS       12        37
     A                                    81'Missing files:'
     A            R_P_MIS       12        96
     A            R_TLIB        10        10SPACEB(2)
     A                                    22'New files....:'
     A            R_T_NEW       12        37
     A            R_VLIB        10        69
     A                                    81'New files....:'
     A            R_V_NEW       12        96
     A                                    22'Amended files:'
     A                                      SPACEB(1)
     A            R_T_AMD       12        37
     A                                    81'Amended files:'
     A            R_V_AMD       12        96
     A                                    22'Missing files:'
     A                                      SPACEB(1)
     A            R_T_MIS       12        37
     A                                    81'Missing files:'
     A            R_V_MIS       12        96
     A                                     7'Overview of the changes is availab-
     A                                      le here below.'
     A                                      SPACEB(6)
     A                                     7'Full detail of the changes is avai-
     A                                      lable in report UP4016P1.'
     A                                      SPACEB(1)
     A          R OVERVIEWH
     A                                     5'Library compared:'
     A            R_LIB         10        23
     A                                    48'Reference Library:'
     A            R_LIBR        10        67
     A                                   113'Overview'
     A                                     5'File'
     A                                      SPACEB(2)
     A                                    20'New file'
     A                                    35'Number of'
     A                                    56'Number of field'
     A                                    90'Number of'
     A                                   113'File missing'
     A                                    35'field added'
     A                                      SPACEB(1)
     A                                    56'attribute changed'
     A                                    90'field removed'
     A                                     5'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ----------------------'
     A                                      SPACEB(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP
     A                                      SPACEB(2)
     A                                    51'***  END OF REPORT  ***'
     A          R OVERVIEWD                 SPACEB(1)
     A            R_OFILE       10         7
     A            R_ONWF         1        24
     A            R_OFAD         5        39
     A            R_OFAC         5        63
     A            R_OFRM         5        98
     A            R_OFMS         1       118
