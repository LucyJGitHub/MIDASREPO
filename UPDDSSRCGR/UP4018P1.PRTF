      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas UP ADBU Exception Report')
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade Module                                       *
      *                                                               *
      *  PRTF/UP4018P1 - Midas UP ADBU Exeption Report                *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. MD061992           Date 23Oct23               *
      *  Prev Amend No. CUT017 *CREATE     Date 17Mar20               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD061992 - Allow 3 char prefix for reference libraries       *
      *  CUT017 - Adaptive Database Upgrade                           *
      *                                                               *
      *****************************************************************
      *
      *
     A          R HEADH                     TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2020')
     A                                      SKIPB(2)
     A                                     5'Reference: UP4018P1'
     A                                      SPACEB(0)
     A N20                                34'Adaptive Database Upgrade - Except-
     A                                      ion Report for Zone Layer'
     A  20                                34'Adaptive Database Upgrade - Except-
     A                                      ion Report for Global Layer'
      *
     A                                   119'Date'
     A                                   124DATE
     A                                      EDTWRD('  /  /  ')
     A                                   119'Time'
     A                                      SPACEB(1)
     A                                   124TIME
     A                                      EDTWRD('  :  :  ')
     A                                   119'Page'
     A                                      SPACEB(1)
     A                                   124PAGNBR
     A                                      EDTCDE(3)
     A                                      SPACEA(2)
      *
     A          R NORECS
                                          40'*** No details to report ***'
     A                                      SPACEB(2)
     A                                      SPACEA(2)
     A          R LIBRARY
     A                                     5'Current System:'
     A            E_LIB1         2        21
     A                                    48'Reference System:'
      * Start MD061992
     A**********  E_LIB2         2        66                                                MD061992
     A            E_LIB2         3        66
      * End MD061992
     A                                     4'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ----------------------'
     A                                      SPACEB(1)
      *
     A          R DETAIL
     A                                     5'Object:'
     A                                      SPACEB(2)
     A            E_OBJ         10        13
     A                                    26'Error in program:'
     A            E_PGM         10        44
     A                                    56'Error in job:'
     A            E_JOB         23        70
     A                                     5'Error details:'
     A                                      SPACEB(1)
     A            E_DET        109        20
     A                                     5'Command in error:'
     A                                      SPACEB(1)
     A            E_CMD        109        23
     A                                     5'Timestamp:'
     A                                      SPACEB(1)
     A            E_TMST          Z       16
     A                                     5'- - - - - - - - - - - - - - - - - -
     A                                      - - - - - - - - - - - - - - - - - --
     A                                       - - - - - - - - - - - - - - - - - -
     A                                      - - - - - - - - - - - -'
     A                                      SPACEB(1)
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP
     A                                      SPACEB(2)
     A                                    51'***  END OF REPORT  ***'
