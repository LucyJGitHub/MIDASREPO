      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas UP DDS Sanity report')
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade Module                                       *
      *                                                               *
      *  PRTF/UP4032P1 - Midas UP DDS Sanity report                   *
      *                                                               *
      *  (c) Finastra International Limited 2021                      *
      *                                                               *
      *  Last Amend No. MD059476 *CREATE   Date 01Dec21               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD059476 - Bridge improvement for refresh                    *
      *                                                               *
      *****************************************************************
      *
      *
     A          R HEADH                     TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2021')
     A                                      SKIPB(2)
     A                                     5'Reference: UP4032P1'
     A                                      SPACEB(0)
     A                                    40'Deliverable Data Split - sanity re-
     A                                      port'
      *
     A                                   119'Date'
     A                                   124DATE
     A                                      EDTWRD('  /  /  ')
     A                                   119'Time'
     A                                      SPACEB(1)
     A                                   124TIME
     A                                      EDTWRD('  :  :  ')
     A                                   119'Page'
     A                                      SPACEB(1)
     A                                   124PAGNBR
     A                                      EDTCDE(3)
     A                                      SPACEA(2)
      *
     A          R NORECS
     A            R_NORLC       20         5
     A            R_NORLR       20        48SPACEA(2)
                                          40'*** No details to report ***'
     A                                      SPACEA(2)
     A          R HEADER
     A                                     5'Table'
     A                                      SPACEB(1)
     A                                    23'Current number of records'
     A                                    61'Number of XTD'
     A                                    78'Number of XTD records'
     A                                   103'Number of XTD records'
     A                                    25'CTD'
     A                                      SPACEB(1)
     A                                    34'BTD'
     A                                    43'XTD'
     A                                    60'records deleted'
     A                                    79'added to match CTD'
     A                                   104'added to match BTD'
     A                                     5'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ----------------------'
     A                                      SPACEB(1)
      *
     A          R DETAIL
     A                                      SPACEB(2)
     A  71
     AO 70                                 2'!!'
     A            R_FILE        10         5
     A            R_CTD          5        25
     A            R_BTD          5        34
     A            R_XTD          5        44
     A            R_DEL          5        68
     A            R_XAC          5        89
     A            R_XAB          5       114
      *
     A          R ERROR                     SPACEB(1)
     A            R_ERR        120         5
      *
     A          R SDEP                      SPACEB(1)
     A            R_DEP        113        16
     A          R SDEP2                     SPACEB(1)
     A            R_DEP2       113        18
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP
     A                                      SPACEB(2)
     A                                    51'***  END OF REPORT  ***'
