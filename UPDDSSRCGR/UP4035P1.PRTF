      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas UP Bridge DDS sanity report')
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade Module                                       *
      *                                                               *
      *  PRTF/UP4035P1 - Midas UP Bridge DDS sanity report            *
      *                                                               *
      *  (c) Finastra International Limited 2022                      *
      *                                                               *
      *  Last Amend No. MD059732 *CREATE   Date 18Aug22               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD059732 - DDS improvements, report conflict only            *
      *           - Applied for MD-60290.                             *      
      *                                                               *
      *****************************************************************
      *
      *
     A          R HEADH                     TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2022')
     A                                      SKIPB(2)
     A                                     5'Reference: UP4035P1'
     A                                      SPACEB(0)
     A                                    40'Deliverable Data Split - sanity re-
     A                                      port for'
     A            R_LAYER       14        83
      *
     A                                   119'Date'
     A                                   124DATE
     A                                      EDTWRD('  /  /  ')
     A                                   119'Time'
     A                                      SPACEB(1)
     A                                   124TIME
     A                                      EDTWRD('  :  :  ')
     A                                   119'Page'
     A                                      SPACEB(1)
     A                                   124PAGNBR
     A                                      EDTCDE(3)
     A                                      SPACEA(2)
      *
     A          R NORECS
     A            R_NORLC       20         5
     A            R_NORLR       20        48SPACEA(2)
                                          40'*** No details to report ***'
     A                                      SPACEA(2)
     A          R HEADER
     A                                     5'Error, if any, are reported with '-
     A                                      '!!''.'
     A                                      SPACEB(1)
     A                                     5'Table'
     A                                      SPACEB(1)
     A                                    23'Current number of records'
     A  72                                51'Number of XTD'
     A  72                                68'Number of XTD records'
     A  72                                93'Number of XTD records'
     A  72                               116'Number of dup'
     A                                    25'CTD'
     A                                      SPACEB(1)
     A                                    34'BTD'
     A                                    43'XTD'
     A  72                                50'records deleted'
     A  72                                69'added to match CTD'
     A  72                                94'added to match BTD'
     A  72                               117'removed from BTD'
     A                                     5'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ----------------------'
     A                                      SPACEB(1)
      *
     A          R DETAIL
     A                                      SPACEB(2)
     A  71
     AO 70                                 2'!!'
     A            R_FILE        10         5
     A            R_CTD          5        25
     A            R_BTD          5        34
     A            R_XTD          5        43
     A  72        R_DEL          5        56
     A  72        R_XAC          5        75
     A  72        R_XAB          5       101
     A  72        R_DUP          5       122
      *
     A          R ERROR                     SPACEB(1)
     A            R_ERR        120         5
      *
     A          R SDEP                      SPACEB(1)
     A            R_DEP        113        16
     A          R SDEP2                     SPACEB(1)
     A            R_DEP2       113        18
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP
     A                                      SPACEB(2)
     A                                    51'***  END OF REPORT  ***'
