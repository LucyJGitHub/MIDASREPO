      *****************************************************************
/*S*D ***LFBASE********************************************************
/*E*S ***LF_IM*********************************************************
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade Module                                       *
      *                                                               *
      *  LF/UPDLVCLA - LFs; global                                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *  Last Amend No. CUP041  *REDUNDANT Date 27May14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CPK017  *CREATE    Date 02Jan04               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP041 - Reduce menu options for the Bridge                  *
      *  CPK017 - MidasPlus packaging.                                *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
     A          R UPDLVCD0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      PFILE(UPDLVCPD)
      *
     A            AUMBNM                    TEXT('Member Name')
     A            AUMBTP                    TEXT('Member Type')
     A            AUMBTX                    TEXT('Member Text')
     A            AUCAPR                    TEXT('Change vs. Previous Rel.')
     A            AUIPTY                    TEXT('Installation Priority')
     A            AUDTYP                    TEXT('Delivery Type')
     A            AUCPYO                    TEXT('Copy Option')
     A            AUFMTO                    TEXT('Format option')
     A            AUSRCF                    TEXT('Source File')
     A            AUDTLB                    TEXT('Destination Library')
     A            AUPROJ                    TEXT('Project')
     A            AUEXIN                    TEXT('Extra Information')
     A            AUUSTS                    TEXT('Upgrade Status')
      *
      * Key fields, priority and member name
     A          K AUIPTY                    DESCEND
     A          K AUMBNM
      * Member type equal to LF
     A          S AUMBTP                    COMP(EQ 'LF')
      * and destination equal to '*ZONE'
     A            AUDTLB                    COMP(EQ '*ZONE')
      * and status not equal to 'C'
     A            AUUSTS                    COMP(NE 'C')
      * and delivery type not equal to 'I' (Ignore)
     A            AUDTYP                    COMP(NE 'I')
      *
     A          S AUMBTP                    COMP(EQ 'SQLVIEW')
     A            AUDTLB                    COMP(EQ '*ZONE')
     A            AUUSTS                    COMP(NE 'C')
     A            AUDTYP                    COMP(NE 'I')
      *
