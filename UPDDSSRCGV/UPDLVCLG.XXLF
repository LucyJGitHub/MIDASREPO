      *****************************************************************
/*S*D ***LFBASE********************************************************
/*E*S ***LF_IM*********************************************************
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade Module                                       *
      *                                                               *
      *  LF/UPDLVCLG - PFs, LFs and PRTFs                             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CUP041  *REDUNDANT Date 27May14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CPK017             Date 08Dec03               *
      *                 CPK016             Date 28Aug03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CUP001 *CREATE     Date 27Nov97               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP041 - Reduce menu options for the Bridge                  *
      *  CPK017 - MidasPlus packaging.  The purpose of this file has  *
      *           been changed significantly.                         *
      *  CPK016 - Release 5 packaging.  Introduction of SQL types.    *
      *  CUP001 - Introduction of BRIDGE                              *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
     A          R UPDLVCD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      PFILE(UPDLVCPD)
     A*---------------------------------------------------------------------
     A            AUMBNM                    TEXT('Member Name')
     A            AUMBTP                    TEXT('Member Type')
     A            AUMBTX                    TEXT('Member Text')
     A            AUCAPR                    TEXT('Change vs. Previous Rel.')
     A            AUIPTY                    TEXT('Installation Priority')
     A            AUDTYP                    TEXT('Delivery Type')
     A            AUCPYO                    TEXT('Copy Option')
     A            AUFMTO                    TEXT('Format option')
     A            AUSRCF                    TEXT('Source File')
     A            AUDTLB                    TEXT('Destination Library')
     A            AUPROJ                    TEXT('Project')
     A            AUEXIN                    TEXT('Extra Information')
     A            AUUSTS                    TEXT('Upgrade Status')
     A*.....................................................................
     A* Key fields, priority and member name
     A          K AUIPTY
     A          K AUMBNM
      * Member type equal to LF or PF or PRTF
     A          S AUMBTP                    COMP(EQ 'PF')
      * Start of CPK017
      * and destination equal to '*GLOBAL'
     A            AUDTLB                    COMP(EQ '*GLOBAL')
      * and status not equal to 'C'
     A            AUUSTS                    COMP(NE 'C')
      * End of CPK017
      *  and Delivery Type not equal to 'I' (Ignore)
     A            AUDTYP                    COMP(NE 'I')
      *
     A          S AUMBTP                    COMP(EQ 'LF')
      * Start of CPK017
     A            AUDTLB                    COMP(EQ '*GLOBAL')
     A            AUUSTS                    COMP(NE 'C')
      * End of CPK017
     A            AUDTYP                    COMP(NE 'I')
      *
      * Start of CPK017
     A**********S AUMBTP                    COMP(EQ 'PRTF')                                   CPK017
     A**********  AUDTYP                    COMP(NE 'I')                                      CPK017
      * End of CPK017
      *
      * Start of CPK016
     A          S AUMBTP                    COMP(EQ 'SQLTBL')
      * Start of CPK017
     A            AUDTLB                    COMP(EQ '*GLOBAL')
     A            AUUSTS                    COMP(NE 'C')
      * End of CPK017
     A            AUDTYP                    COMP(NE 'I')
      *
     A          S AUMBTP                    COMP(EQ 'SQLVIEW')
      * Start of CPK017
     A            AUDTLB                    COMP(EQ '*GLOBAL')
     A            AUUSTS                    COMP(NE 'C')
      * End of CPK017
     A            AUDTYP                    COMP(NE 'I')
      * End of CPK016
     A*=====================================================================
