      *****************************************************************
/*S*D ***LFBASE********************************************************
/*E*S ***LF_IM*********************************************************
/*E*I ***TEXT('Midas*UP*Deliverables*list*-*DATA*types*global')********
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade Module                                       *
      *                                                               *
      *  LF/UPDLVCLI - Items with type DATA global                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CUP042 *REDUNDANT  Date 22May15               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CPK017             Date 08Dec03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CUP003 *CREATE     DATE 22Jul99               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP042 - Simplification of deliverables list.                *
      *  CPK017 - MidasPlus packaging.  The purpose of this file has  *
      *           been changed significantly.                         *
      *  CUP003 - Addition of utility to copy files with data         *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
     A          R UPDLVCD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      PFILE(UPDLVCPD)
     A*---------------------------------------------------------------------
     A            AUMBNM                    TEXT('Member Name')
     A            AUMBTP                    TEXT('Member Type')
     A            AUMBTX                    TEXT('Member Text')
     A            AUCAPR                    TEXT('Change vs. Previous Rel.')
     A            AUIPTY                    TEXT('Installation Priority')
     A            AUDTYP                    TEXT('Delivery Type')
     A            AUCPYO                    TEXT('Copy Option')
     A            AUFMTO                    TEXT('Format option')
     A            AUSRCF                    TEXT('Source File')
     A            AUDTLB                    TEXT('Destination Library')
     A            AUPROJ                    TEXT('Project')
     A            AUEXIN                    TEXT('Extra Information')
     A            AUUSTS                    TEXT('Upgrade Status')
     A*.....................................................................
     A* Key fields, priority and member name
     A          K AUIPTY
     A          K AUMBNM
      * Member type equal to data area
     A          S AUMBTP                    COMP(EQ 'DATA')
      * Start of CPK017
      * and destination equal to '*GLOBAL'
     A            AUDTLB                    COMP(EQ '*GLOBAL')
      * and status not equal to 'C'
     A            AUUSTS                    COMP(NE 'C')
      * End of CPK017
      * and delivery type not equal to 'I' (Ignore)
     A            AUDTYP                    COMP(NE 'I')
      *
     A*=====================================================================
