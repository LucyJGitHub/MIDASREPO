      *****************************************************************
/*S*D ***LFBASE********************************************************
/*E*S ***LF_IM*********************************************************
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade Module                                       *
      *                                                               *
      *  LF/UPDLVCLM - DTAQs global                                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      *  Last Amend No. CUP041  *REDUNDANT Date 27May14               *
      *  Prev Amend No. AR656906           Date 13Oct10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CPK017             Date 08Dec03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CPK015 *CREATE     DATE 27May02               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP041 - Reduce menu options for the Bridge                  *
      *  AR656906 - Handle tables (non-SQL)                           *
      *  CPK017 - MidasPlus packaging.  The purpose of this file has  *
      *           been changed significantly.                         *
      *  CPK015 - 4.01 packaging.  Handle data queues.                *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
     A          R UPDLVCD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      PFILE(UPDLVCPD)
     A*---------------------------------------------------------------------
     A            AUMBNM                    TEXT('Member Name')
     A            AUMBTP                    TEXT('Member Type')
     A            AUMBTX                    TEXT('Member Text')
     A            AUCAPR                    TEXT('Change vs. Previous Rel.')
     A            AUIPTY                    TEXT('Installation Priority')
     A            AUDTYP                    TEXT('Delivery Type')
     A            AUCPYO                    TEXT('Copy Option')
     A            AUFMTO                    TEXT('Format option')
     A            AUSRCF                    TEXT('Source File')
     A            AUDTLB                    TEXT('Destination Library')
     A            AUPROJ                    TEXT('Project')
     A            AUEXIN                    TEXT('Extra Information')
     A            AUUSTS                    TEXT('Upgrade Status')
     A*.....................................................................
     A* Key fields, priority and member name
     A          K AUIPTY
     A          K AUMBNM
      * Member type equal to data queue
     A          S AUMBTP                    COMP(EQ 'DTAQ')
      * Start of CPK017
      * and destination equal to '*GLOBAL'
     A            AUDTLB                    COMP(EQ '*GLOBAL')
      * and status not equal to 'C'
     A            AUUSTS                    COMP(NE 'C')
      * End of CPK017
      * and delivery type not equal to 'I' (Ignore)
     A            AUDTYP                    COMP(NE 'I')
      * Start AR656906
      * Member type equal to table
     A          S AUMBTP                    COMP(EQ 'TBL')
      * and destination equal to '*ZONE'
     A            AUDTLB                    COMP(EQ '*GLOBAL')
      * and status not equal to 'C'
     A            AUUSTS                    COMP(NE 'C')
      * and delivery type not equal to 'I' (Ignore)
     A            AUDTYP                    COMP(NE 'I')
      * End   AR656906
      *
     A*=====================================================================
