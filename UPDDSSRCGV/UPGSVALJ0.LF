      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMWF                                                      *
/*EXI *  TEXT('Midas UP Global Layer Bridge Midas system values')
      *****************************************************************
      *                                                               *
      *  Midas - Bridge Module                                        *
      *                                                               *
      *  LF/UPGSVALJ0 - Global Layer Bridge Midas System Values       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CUP027  *CREATE    Date 23Mar04               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP027 - Global layer migration of Bridge ICD to the system  *
      *           values file.                                        *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R @SVALJ0                   JFILE(GPSVALPD GPSARVPD +
     A                                      GPSARDPD GPMMIDPD)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
      *
     A          J                           JOIN(1 2)
     A                                      JFLD(GISVAL GSSVAL)
     A          J                           JOIN(2 3)
     A                                      JFLD(GSSARN SARN)
     A          J                           JOIN(1 4)
     A                                      JFLD(GIMMSN FOMMSN)
      *
     A            GISVAL
     A            GIMMOD
     A            GIMMSN
     A            GIDESC
     A            GIVAL
     A            GIDFT
     A            GIHELP
     A            GIATTR
     A            GIDLEN
     A            GISUPP
     A            GISWCH
     A            GIJUST
     A            GIALWB
     A            GIEMSG
     A            GILCD
     A            GILCT
      *
     A            GSSARN
     A            SARN
     A            GSAVAL
     A            FOMMIN
      *
     A          K GISVAL
      *
      * Select if the system value is for the Bridge module or the
      * Setupmidas module.
      *
     A          S GIMMOD                    COMP(EQ 'UP')
      *
     A          S GIMMOD                    COMP(EQ 'SM')
