      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas UP Menu items differences report')
      *****************************************************************
      *                                                               *
      *  Midas - UP Module                                            *
      *                                                               *
      *  PRTF/UP0821P1 - Menu items differences                       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CUP004 *CREATE     Date 09Apr98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CUP004 - Make DTALIB upgrade easier                          *
      *                                                               *
      *****************************************************************
      *
      *  Formats for Program UP0821
      *
     A          R HEADH                     TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(2)
      *
     A            HDRTXT        59        35
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                    42'Produced on'
     A                                    54DATE
     A                                      EDTWRD('  /  /  ')
     A                                    63'at'
     A                                    66TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A          R NORECS
                                          40'*** No details to report ***'
     A                                      SPACEA(2)
      *
     A          R DTL1
     A  60                                  SKIPB(5)
     A  60                                10'Amended records'
     A  60                                10'Amended records'
     A                                      SPACEA(2)
     A N60 61                             15'*** There are no amended +
     A                                      records ***'
     A N60 61                               SPACEA(1)
     A N60N61 62                          10'Group'
     A N60N61 62                          16'Code'
     A                                      SPACEA(2)
      *
     A N60N61N62  #MGRP          4        10
     A N60N61N62  #ICDE          4        16
     A                                      SPACEA(1)
     A  63
     AAN60N61N62                          28'Field:'
     A  64
     AAN60N61N62                          28'Before:'
     A  65
     AAN60N61N62                          28'After: '
     A N60N61N62  #VALUE        90        37
     A  65                                  SPACEA(1)
      *
     A          R DTL2
     A  70                                  SKIPB(5)
     A  70                                10'Deleted records'
     A  70                                10'Deleted records'
     A                                      SPACEA(2)
     A N70 71                             15'*** There are no deleted +
     A                                      records ***'
     A N70N71 72                          10'Group'
     A N70N71 72                          16'Code'
     A N70N71 72                          22'Description'
     A                                      SPACEA(1)
     A N70N71N72  #MGRP          4        10
     A N70N71N72  #ICDE          4        16
     A N70N71N72  #INAR         45        22
     A                                      SPACEA(1)
      *
     A          R DTL3
     A  80                                  SKIPB(5)
     A  80                                10'Inserted records'
     A  80                                10'Inserted records'
     A                                      SPACEA(2)
     A N80 81                             15'*** There are no inserted +
     A                                      records ***'
     A N80N81 82                          10'Group'
     A N80N81 82                          16'Code'
     A N80N81 82                          22'Description'
     A                                      SPACEA(1)
     A N80N81N82  #MGRP          4        10
     A N80N81N82  #ICDE          4        16
     A N80N81N82  #INAR         45        22
     A                                      SPACEA(1)
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP
     A                                      SPACEB(2)
     A                                    51'***  END OF REPORT  ***'
