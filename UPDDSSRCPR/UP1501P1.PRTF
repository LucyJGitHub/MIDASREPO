      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas UP Compare database attributes report')
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade Module                                       *
      *                                                               *
      *  PRTF/UP1501P1 - Comapre database attributes report           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. CUP015 *CREATE     Date 01Jun01               *
      *  Prev Amend No. xxxxxx             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUP015 - New utility                                         *
      *                                                               *
      *****************************************************************
      *
     A          R HEADH                     TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(2)
     A                                      SPACEA(1)
      *
     A            USER          10         5
     A                                    39'Report of field differences +
     A                                      between two databases'
     A                                   119'Date'
     A                                   124DATE
     A                                      EDTWRD('  /  /  ')
     A                                      SPACEA(1)
     A            WSID          10         5
     A                                   119'Time'
     A                                   124TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                     5'UP1501P1'
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(3)
     A                                      SPACEA(2)
      *
      * Description of contents of report
     A                                     5'Key:'
     A                                    10'Ref = '
     A            REFLIB        10        16
     A                                    27'Cmp = '
     A            CMPLIB        10        33
     A                                      SPACEA(2)
     A                                     5'File'
     A                                      UNDERLINE
     A                                    18'Description'
     A                                      UNDERLINE
     A                                    73'Position'
     A                                      UNDERLINE
     A                                    89'Length'
     A                                      UNDERLINE
     A                                   101'Numeric Attr'
     A                                      UNDERLINE
     A                                   117'Field Type'
     A                                      UNDERLINE
     A                                      SPACEA(1)
     A                                    71'Ref      Cmp'
     A                                    86'Ref      Cmp'
     A                                   101'Ref      Cmp'
     A                                   115'Ref        Cmp'
      *
     A          R NORECS
                                          40'*** No details to report ***'
     A                                      SPACEA(2)
      *
     A          R DTL
     A                                      SPACEB(1)
     A                                      SPACEA(1)
     A            @FILE         10         5
     A            @DESC         50        18
     A  31        @RPOS          5  0     71EDTCDE(Z)
     A  31        @CPOS          5  0     78EDTCDE(Z)
     A  31                                71'____________'
     A  32        @RLEN          5  0     86EDTCDE(Z)
     A  32        @CLEN          5  0     93EDTCDE(Z)
     A  32                                86'____________'
     A  33        @RDEC          5       101
     A  33        @CDEC          5       107
     A  33                               101'____________'
     A  34        @RTYP          6       115
     A  34        @CTYP          6       123
     A  34                               115'______________'
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP
     A                                      SPACEB(2)
     A                                    51'***  END OF REPORT  ***'
