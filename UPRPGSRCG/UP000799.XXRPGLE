     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2004')
      *****************************************************************
/*X*I****OVRDBF*FILE(UPACTFL0)*TOFILE(UPGPACTFL0)**********************                       CUP021
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP000799 - Action File handler                               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 09Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CUP022 *CREATE     Date 09Feb04               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP022 - Standardise action file processing.                 *
      *                                                               *
      *****************************************************************
      * Function of indicators                                        *
      *                                                               *
      *       30      Chain - record not found                        *
      *                                                               *
      *****************************************************************
     FUPACTFL0  IF   E           K DISK    INFSR(*PSSR)
     FUPGPACTFL0IF   E           K DISK    INFSR(*PSSR)
     F                                     USROPN
     F                                     RENAME(UPACTFD0:GPACTFD0)
      *
      /EJECT
     D LDA           E DS           256    EXTNAME(LDA)
      *
      * Local data area for database error details
      * *LOCK IN LDA immediately before and OUT LDA immediately
      * after each database error handling.
      *
      *                                     134 141 DBFILE
      *       Defines:                      142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *
     D PSDS           SDS
      *
      * Program Status Data Structure.
      *
     D  PGM              *PROC
     D  WSID                 244    253
     D  USER                 254    263
      *
     D KFile           S             10
      *
     C     *ENTRY        PLIST
     C                   PARM                    PFile            10
     C                   PARM                    PActionFile      10
     C                   PARM                    PLogicFile       10
     C                   PARM                    PTemplate        10
     C                   PARM                    PBLDProg         10
     C                   PARM                    PRVWProg         10
     C                   PARM                    PCNFProg         10
     C                   PARM                    PPRCProg         10
     C                   PARM                    PAFCProg         10
     C                   PARM                    POvrRideFile     10
     C                   PARM                    PResetProg       10
     C                   PARM                    PRvwType          1
      *
     C                   Eval      KFile = PFile
      *
     C     KFile         CHAIN     UPACTFL0                           30
      *
      * If record not found, check if the file in UPGPACTFL0.
     C                   IF        *IN30         = *ON
     C                   OPEN      UPGPACTFL0
     C     KFile         CHAIN     UPGPACTFL0                         30
     C                   CLOSE     UPGPACTFL0
     C                   ENDIF
      *
      * Record should be there as validity checker would have done check.
     C                   IF        *IN30 = *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C                   EVAL      PActionFile   = AAFRMF                       Copy from file
     C                   EVAL      PLogicFile    = AAAFLF                       LF used by AF utilit
     C                   EVAL      PTemplate     = AAUPPF                       PF template for AF
     C                   EVAL      PBLDProg      = AABLDP                       Build program
     C                   EVAL      PRVWProg      = AARVWP                       Review program
     C                   EVAL      PCNFProg      = AACNFP                       Conflict program
     C                   EVAL      PPRCProg      = AAPRCP                       Process program
     C                   EVAL      PAFCProg      = AAPROG                       AF CPP
     C                   EVAL      POvrRideFile  = AAUPLF                       Override file
     C                   EVAL      PResetProg    = AARSTP                       Reset program
     C                   EVAL      PRvwType      = AARTAY                       Review type applica
      *
     C     ENDPGM        TAG
     C                   SETON                                        LR
     C                   RETURN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR                                                  ** *PSSR SR **
      *
     C     @RUN          IFEQ      *BLANK
     C                   MOVE      'Y'           @RUN              1
     C                   DUMP
     C                   END
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
      *
      ********************************************************************
