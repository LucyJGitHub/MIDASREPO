     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas UP Report on files with shared format')
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade Module                                       *
      *                                                               *
      *  UP0025 - Report on files with shared formats                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CUP002             Date 15Apr99               *
      *  Prev Amend No. CUP001  *CREATE    Date 30Dec98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUP002 - Make the report more user friendly                  *
      *  CUP001 - Introduction of BRIDGE                              *
      *                                                               *
      *****************************************************************
     FUPSFMTQFIF  E                    DISK
     FUPDLVCL0IF  E           K        DISK
     FUPSFMTPDIF  E           K        DISK                               CUP002
     FUP0025P1O   E             37     PRINTER
      /EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      /SPACE 3
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           MBR       KLIST
     C                     KFLD           SFFILE
      *                                                                   CUP002
     C           SF        KLIST                                          CUP002
     C                     KFLD           SFMBNM                          CUP002
      *
     C                     WRITEHEADH                  69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
     C           1         SETLLUPSFMTD0
      *
     C                     READ UPSFMTD0               6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
      * If there are no records at all write 'no records'
     C           *IN20     IFEQ *ON
     C                     WRITENORECS                 69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     WRITETRAILP                 69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     GOTO ENDPGM
      *
     C                     ENDIF
      *
     C           *IN20     DOUEQ*ON
      *
      * Chain to Deliverables list to see if file has been delivered
     C           *LOVAL    SETLL@DLVCL0
     C           MBR       CHAIN@DLVCL0              4069
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      * If record was found then read next record
     C           *IN40     IFEQ *ON
      *                                                                   CUP002
      * Check supplied shared format file; if file already shares format  CUP002
      *  then do not report                                               CUP002
     C           *LOVAL    SETLLUPSFMTD1                                  CUP002
     C           SF        CHAINUPSFMTD1             4169                 CUP002
     C           *IN69     IFEQ *ON                                       CUP002
     C                     EXSR *PSSR                                     CUP002
     C                     GOTO ENDPGM                                    CUP002
     C                     ENDIF                                          CUP002
      * If record was found then read next record                         CUP002
     C           *IN41     IFEQ *ON                                       CUP002
      * If source file different from previous then write to spool
     C           SFSRCF    IFEQ WSRCF
     C           *IN38     ANDNE*ON                                       CUP002
     C                     MOVE *BLANKS   @SRCF
     C                     ELSE
     C                     MOVELSFSRCF    @SRCF
     C                     WRITEBLNK                   69
     C                     ENDIF
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      * If file different from previous then write to spool
     C           SFMBNM    IFEQ WMBNM
     C           *IN38     ANDNE*ON                                       CUP002
     C                     MOVE *BLANKS   @MBNM
     C                     ELSE
     C                     MOVELSFMBNM    @MBNM
     C                     ENDIF
      *
     C                     SETOF                     38                   CUP002
     C                     MOVELSFLIB1    @LIB1
     C                     MOVELSFNAME    @NAME
     C                     MOVELSFFILE    @FILE
     C                     MOVELSFLIB2    @LIB2
     C**********           MOVELSFLIB1    @LIB1                           CUP002
      *                                                                   CUP002
      * If 'File that shares format' and Library are the same then do     CUP002
      *  not write                                                        CUP002
     C           SFFILE    IFNE WFILE                                     CUP002
     C           SFLIB2    ORNE WLIB2                                     CUP002
      *                                                                   CUP002
     C                     WRITEDTL
     C                     SETON                     45
     C                     ENDIF                                          CUP002
      *
      * If overflow on then write header
     C           *IN37     IFEQ *ON
     C                     WRITEHEADH                  69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     SETOF                     37
     C                     SETON                     38                   CUP002
     C                     ENDIF
      *
      * Store 'Files that share format' and Library and source file and   CUP002
      *  member name                                                      CUP002
     C                     MOVELSFFILE    WFILE  10                       CUP002
     C                     MOVELSFLIB2    WLIB2  10                       CUP002
     C                     MOVELSFSRCF    WSRCF  10
     C                     MOVELSFMBNM    WMBNM  10
      *
     C                     ENDIF                                          CUP002
     C                     ENDIF
      *
     C                     READ UPSFMTD0               6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
     C                     ENDDO
      *                                                                **
      * Write trailer and end program
      * If *IN45 still off this means there were no details to report
     C           *IN45     IFEQ *OFF
     C                     WRITENORECS
     C                     ENDIF
     C                     WRITETRAILP                 69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           ENDPGM    TAG
     C                     SETON                     LR
     C                     RETRN
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
      ************ END PROGRAM IF DBERRCTL NOT CALLED ********************
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
