     H        1
      *****************************************************************
/*X*IA***OVRDBF*FILE(UTMU1)*TOFILE(UPMENUTPD)                         *                CUP019 CUP022
/*X*IB***OVRDBF*FILE(UTMU2)*TOFILE(UPMENUTPD)                         *                CUP019 CUP022
/*X*IA***OVRDBF*FILE(MENU1)*TOFILE(UPMENUTPD)**************************                CUP022 CUP021
/*X*IB***OVRDBF*FILE(MENU2)*TOFILE(UPMENUTPD)**************************                CUP022 CUP021
/*S*D****RPGBASE*******************************************************                       CUP021
/*O*R ***OVRDBF*UTMU1*UTMENUL0*****************************************                       CUP019
/*O*R ***OVRDBF*UTMU2*UTMENUL0*****************************************                       CUP019
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade module                                       *
      *                                                               *
      *  UP1000 - Compare two utility menu records                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CUP022             Date 22Mar04               *
      *                 CPK018  *MOVED     Date 24Apr04               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 CUP019             Date 10Sep02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CUP001             Date 28Feb99               *
      *                 CUP004             Date 12Jun98               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP022 - Change the name of the input files so that command  *
      *           ACTIONFILE (*NONE, *OVERRIDE) can be used to call   *
      *           this program.                                       *
      *  CPK018 - MidasPlus packaging.  Moved to global layer.        *
      *  CUP019 - Use templates for Action Files (including change of *
      *           key).                                               *
      *  CUP001  -  Introduction of BRIDGE.                           *
      *  CUP004  -  Make upgrade of DTALIB files easier               *
      *                                                               *
      *****************************************************************
      * ID F  C  H  L    FUNCTION OF INDICATORS
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write or chain fail
      *       70 - 78 Indicators for changed fields
      *
     F*****UTMU1   IF  E                    DISK         KINFSR *PSSR                         CUP022
     F*****UTMU2   IF  E           K        DISK         KINFSR *PSSR                         CUP022
     FMENU1   IF  E                    DISK         KINFSR *PSSR                              CUP022
     FMENU2   IF  E           K        DISK         KINFSR *PSSR                              CUP022
     F********    @MENUL0                           KRENAMEUTMUF                              CUP019
     F            UTMENUD0                          KRENAMEUTMUF                              CUP019
     FUPMENUPDO   E                    DISK         KINFSR *PSSR
      *
      /EJECT
     E                    CPY@    1   1 80
      *
      * Fields with the same name will need to be renamed.
      *
     IUTMUF
     I              RECI                            RECIX
     I              MGRP                            MGRPX
     I              ICDE                            ICDEX
     I              INAR                            INARX
     I              SECL                            SECLX
     I              MODC                            MODCX
     I              PCMD                            PCMDX
     I              PARA                            PARAX
     I              HDOC                            HDOCX
     I              HFLR                            HFLRX
     I              HLBL                            HLBLX
     I              ISEQ                            ISEQX                 CUP001
      *
      ** Array containing Copyright statement
      *
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *ENTRY    PLIST
     C                     PARM           MODE    1
      *
     C           COMP      KLIST
     C                     KFLD           MGRP
     C                     KFLD           ISEQ                                               CUP019
     C                     KFLD           ICDE
      *
     C*****                READ UTMU1                  6920                                   CUP022
     C                     READ MENU1                  6920                                   CUP022
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           *IN20     DOWEQ'0'
     C           COMP      CHAINUTMUF                3069
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * If record not found then record has been deleted or added
      *  depending on mode
     C           *IN30     IFEQ *ON
     C                     MOVE MODE      MIACTC
     C                     WRITEUPMENUD0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
      * Need to find differences but this should only be run once;
      *  run only if mode is 'Delete'
     C           MODE      IFEQ 'D'
      *
      * If record found then look for any differences; only look in
      *  fields that have data all of the time.
     C           *IN30     IFEQ *OFF
     C                     MOVE 'A'       MIACTC
      * Item narrative
     C           INAR      IFNE INARX
     C                     SETON                     70
     C                     ENDIF
      * Security level
     C           SECL      IFNE SECLX
     C                     SETON                     71
     C                     ENDIF
      * Module code
     C           MODC      IFNE MODCX
     C                     SETON                     72
     C                     ENDIF
      * Menu program
     C           PCMD      IFNE PCMDX
     C                     SETON                     73
     C                     ENDIF
      * Parameters
     C           PARA      IFNE PARAX
     C                     SETON                     74
     C                     ENDIF
      * Help document
     C           HDOC      IFNE HDOCX
     C                     SETON                     75
     C                     ENDIF
      * Help folder
     C           HFLR      IFNE HFLRX
     C                     SETON                     76
     C                     ENDIF
      * Help label
     C           HLBL      IFNE HLBLX
     C                     SETON                     77
     C                     ENDIF
      *
      **Sequence*                                                                      CUP001 CUP019
     C***********ISEQ      IFNE ISEQX                                                  CUP001 CUP019
     C***********          SETON                     78                                CUP001 CUP019
     C***********          ENDIF                                                       CUP001 CUP019
      *                                                                   CUP001
     C***********          MOVEA*IN,70    INTEST  8                       CUP001
     C***********          MOVE INTEST    INTNUM  80                      CUP001
     C                     MOVEA*IN,70    INTEST  9                       CUP001
     C                     MOVE INTEST    INTNUM  90                      CUP001
      * If any indicators are set on then write record
     C           INTNUM    IFNE 0
     C                     WRITEUPMENUD0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * Set values to write second record
     C                     MOVELINARX     INAR
      *
     C                     Z-ADDSECLX     SECL
      *
     C                     MOVELMODCX     MODC
      *
     C                     MOVELPCMDX     PCMD
      *
     C                     MOVELPARAX     PARA
      *
     C                     MOVELHDOCX     HDOC
      *
     C                     MOVELHFLRX     HFLR
      *
     C                     MOVELHLBLX     HLBL
      *
     C***********          MOVELISEQX     ISEQ                                         CUP001 CUP019
      ***********                                                                      CUP001 CUP019
     C                     WRITEUPMENUD0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
      * Read next record
     C                     MOVEA'00000000'*IN,70
     C                     MOVEA'0'       *IN,78                          CUP001
     C******               READ UTMU1                  6920                                   CUP022
     C                     READ MENU1                  6920                                   CUP022
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           ENDPGM    TAG
     C                     SETON                     LR
     C                     RETRN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      ************ CALL DBERRCTL IF INTERACTIVE PROGRAM ***************
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
