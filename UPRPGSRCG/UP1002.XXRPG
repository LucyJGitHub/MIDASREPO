     H        1
      *****************************************************************
/*S*D****RPGBASE*******************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade module                                       *
      *                                                               *
      *  UP1002 - Process UTMENUPD item changes                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CPK018  *MOVED     Date 24Apr04               *
      * Midas Release 4.01.03 ----------------------------------------*
      *                 CPK015             Date 09Dec02               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 CUP019             Date 03Oct02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CUP001             Date 28Feb99               *
      *                 CUP004             Date 12Jun98               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CPK018 - MidasPlus packaging.  Moved to global layer.        *
      *  CPK015  -  Improvements to Action Files.                     *
      *  CUP019 - Change of key for logical file.                     *
      *  CUP001  -  Introduction of BRIDGE.                           *
      *  CUP004  -  Make upgrade of DTALIB files easier               *
      *                                                               *
      *****************************************************************
      * ID F  C  H  L    FUNCTION OF INDICATORS
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write, update or delete fail
      *
     FUPMENUL0IF  E           K        DISK         KINFSR *PSSR
     FUTMENUL0UF  E           K        DISK         KINFSR *PSSR A
     F            @MENUL0                           KRENAMEUTMUF
      *
      /EJECT
      * Array containing Copyright statement
     E                    CPY@    1   1 80
      *
      * Fields with the same name will need to be renamed.
     IUTMUF
     I              RECI                            RECIX
     I              MGRP                            MGRPX
     I              ICDE                            ICDEX
     I              INAR                            INARX
     I              SECL                            SECLX
     I              MODC                            MODCX
     I              PCMD                            PCMDX
     I              PARA                            PARAX
     I              HDOC                            HDOCX
     I              HFLR                            HFLRX
     I              HLBL                            HLBLX
     I              ISEQ                            ISEQX                 CUP001
      *
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C************ENTRY    PLIST                                                              CPK015
     C************         PARM           AMEND   4                                           CPK015
      *
     C           COMP      KLIST
     C                     KFLD           MGRP
     C                     KFLD           ISEQ                                                CUP019
     C                     KFLD           ICDE
      *
     C           *LOVAL    SETLLUPMENUL0
      *
     C                     READ UPMENUL0               6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
     C           *IN20     DOWEQ'0'
      *
     C           MIACTC    IFEQ 'I'
     C                     EXSR SETVAL
     C                     WRITEUTMUF                  69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     ENDIF
      *
     C           MIACTC    IFEQ 'D'
     C           COMP      CHAINUTMENUL0             3069
     C           *IN69     IFEQ *ON
     C           *IN30     OREQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
      *
     C                     ELSE
     C***********          EXSR SETVAL                                                        CUP019
     C                     DELETUTMUF                  69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
      * If Action code = AMEND then read next record
     C           MIACTC    IFEQ 'A'
     C                     Z-ADDMGRP      MGRPX   40
     C                     MOVELISEQ      ISEQX                                               CUP019
     C                     MOVELICDE      ICDEX   4
     C                     READ UPMENUL0               6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
      * If menu group and code do not match exit
      *  - or if EOF indicator is on
     C           MGRP      IFNE MGRPX
     C           ISEQ      ANDNEISEQX                                                         CUP019
     C           ICDE      ANDNEICDEX
     C           *IN20     OREQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
      **Only*process*amended*records*if*requested*                                            CPK015
     C***********AMEND     IFEQ '*YES'                                                        CPK015
     C           COMP      CHAINUTMENUL0             3069
     C           *IN69     IFEQ *ON
     C           *IN30     OREQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
      *
     C                     ELSE
      *
     C                     EXSR SETVAL
     C                     UPDATUTMUF                  69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     ENDIF
     C***********          ENDIF                                                              CPK015
      *
     C                     ENDIF
      *
     C                     READ UPMENUL0               6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           ENDPGM    TAG
     C                     SETON                     LR
     C                     RETRN
      *
      *****************************************************************
      *
      * SETVAL subroutine - set values for deletes/writes/updates
      *
      *****************************************************************
     C           SETVAL    BEGSR
      * Set values to update record
     C                     MOVELRECI      RECIX
     C                     Z-ADDMGRP      MGRPX
     C                     MOVELICDE      ICDEX
     C                     MOVELINAR      INARX
     C***********          Z-ADDSECL      SECLX                                               CPK015
     C***********          MOVELMODC      MODCX                                               CPK015
     C                     MOVELPCMD      PCMDX
     C                     MOVELPARA      PARAX
     C***********          MOVELHDOC      HDOCX                                               CPK015
     C***********          MOVELHFLR      HFLRX                                               CPK015
     C***********          MOVELHLBL      HLBLX                                               CPK015
     C                     MOVELISEQ      ISEQX                           CUP001
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
