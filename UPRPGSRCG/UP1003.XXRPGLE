     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2001')
      *****************************************************************
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - BRIDGE                                               *
      *                                                               *
      *  UP1003 - Action File Compatibilty Report for UPMENUPD        *
      *                                                               *
      *  Function:  This program compares the differences between     *
      *             Action file delivered and the system.             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CPK018  *MOVED     Date 24Apr04               *
      * Midas Release 4.01.02 ----------------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *                 CUP018             Date 30May02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CUP010  *CREATE    Date 15Aug00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CPK018 - MidasPlus packaging.  Moved to global layer.        *
      *  CUP018 - Show 'after' image instead of 'before' on report.   *
      *  CUP010 - Program to view conflicts with upgrade action file  *
      *           and existing file                                   *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Use of indicators.                                           *
      *                                                               *
      *    30         Error on Read of UPMENUPD                       *
      *    31         EOF on Read of UPMENUPD                         *
      *    32         Chain Fail on Insert routine                    *
      *    33         Chain Fail on Delete routine                    *
      *    34         Record there on chain but different             *
      *    35         Chain Fail on Amend routine                     *
      *    36         Record there on Chain but different             *
      *    37         Compare Indicator for Delete Routine            *
      *    38         Compare Indicator for Amend Routine             *
      *    39         Switched on if a record is found on file(Delete)*
      *    40         Conflicts detected                              *
      *    41         First line of record written                    *
      *    42         Same Record in Sys and UP for Insert            *
      *    43         Compare Indicator for Insert Routine            *
      *    60         Do not run report if error on Amends            *
      *    70         Error on Write                                  *
      *    98         Date Format                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E  I N D E X                               *
      *                                                               *
      * *PSSR - Error processing                                      *
      * *INZSR - Initialise                                           *
      *                                                               *
      *    The *INZSR subroutine will only get called automatically   *
      *    on entry to the module the first time it is run            *
      *    (unless you end the program with LR on).  Similarly        *
      *    D-spec initialisation only happens the first time.  Use    *
      *    RESET for subsequent passes.                               *
      *                                                               *
      *****************************************************************
      ** +--------------------------------------+
      ** ¦ F-specs                              ¦
      ** ¦ =======                              ¦
      ** +--------------------------------------+
 
     FUPMENUPD  IF   E             DISK    INFSR(*PSSR) Prefix(UP_) USROPN
     F*UTMENUL0*IF   E           K DISK    INFSR(*PSSR) Prefix(SYS_) USROPN                   CUP019
     FUTMENUL1  IF   E           K DISK    INFSR(*PSSR) Prefix(SYS_) USROPN                   CUP019
     FUP0803P1  O    E             PRINTER INFDS(SPOOL)
 
      *****************************************************************
      /EJECT
      *****************************************************************
      ** +--------------------------------------+
      ** ¦ D-specs                              ¦
      ** ¦ =======                              ¦
      ** +--------------------------------------+
     D LDA           E DS           256    EXTNAME(LDA)
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     D*RUNDAT***     E DS                  EXTNAME(RUNDAT)                                    CPK018
      **********                                                                              CPK018
      ***Data*Area*giving*Installation*Control*Details*                                       CPK018
 
      ** The following /COPY line includes all the defined fields in the
      ** PSDS.  They have meaningful names, prefixed by 'PS'.
 
     D/COPY GPCPYSRCG,PSDS
      *
      ** Program Status Data Structure
      *
      ** +--------------------------------------+
      ** ¦ Named constants                      ¦
      ** ¦ ===============                      ¦
      ** +--------------------------------------+
 
     D Header          C                   'Report of the differences between -
     D                                     the Existing System Record and the -
     D                                     Replacement Record'
 
     D Counter         C                   '---------+---------+---------+------
     D                                     ----+---------+---------+---------+--
     D                                     --------+---------+---------+'
 
      ** +--------------------------------------+
      ** ¦ Arrays and Data Structures           ¦
      ** ¦ ==========================           ¦
      ** +--------------------------------------+
      ** Array for the update file record
     D UpdatArray      S              1A   DIM(258)
 
      ** Array for the Core system record
     D SysArray        S              1A   DIM(258)
 
      ** Array for the Error Indicator
     D ErrorArray      S              1A   DIM(258)
 
      ** Data Structure for the Action file
     D                 DS
     D UP_RECI                        1
     D UP_MGRP                        4  0
     D UP_ICDE                        4
     D UP_INAR                       45
     D UP_SECL                        4  0
     D UP_MODC                        2
     D UP_PCMD                       20
     D UP_PARA                      100
     D UP_HDOC                       12
     D UP_HFLR                       51
     D UP_HLBL                       10
     D UP_ISEQ                        5
     D  Updfield               1    258A
     D  UPDLIN1                1    100A
     D  UPDLIN2              101    200A
     D  UPDLIN3              201    258A
 
      ** Data Structure for the Core file
     D                 DS
     D SYS_RECI                       1
     D SYS_MGRP                       4  0
     D SYS_ICDE                       4
     D SYS_INAR                      45
     D SYS_SECL                       4S 0
     D SYS_MODC                       2
     D SYS_PCMD                      20
     D SYS_PARA                     100
     D SYS_HDOC                      12
     D SYS_HFLR                      51
     D SYS_HLBL                      10
     D SYS_ISEQ                       5
     D  CoreFld                1    258A
     D  SYSLIN1                1    100A
     D  SYSLIN2              101    200A
     D  SYSLIN3              201    258A
 
      ** Data Structure for the Error indicators
     D                 DS           258
     D  Indfld                 1    258A
     D  INDLIN1                1    100A
     D  INDLIN2              101    200A
     D  INDLIN3              201    258A
 
     DSPOOL            DS
     D SFILE                  83     92
     D SFNUM                 123    124B 0
     D OFLLN                 188    189B 0
     D PRTLN                 367    368B 0
 
      ** +--------------------------------------+
      ** ¦ Declared variables                   ¦
      ** ¦ ==================                   ¦
      ** +--------------------------------------+
 
     D @RUN            S              1
     D KEY_MGRP        S                   LIKE(UP_MGRP)
     D KEY_ISEQ        S                   LIKE(UP_ISEQ)                                      CUP019
     D KEY_ICDE        S                   LIKE(UP_ICDE)
     D x               S              5  0
     D UPDFIL          S             10
     D SYSFIL          S             10
     D ACTION          S              7
     D RMLN            S              2  0
     D Amends          S              5  1
     D COUNT           S            100
     D Count58         S             58
     D WAmends         S              5  1
     D Remainder       S              1
 
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ** +--- Start of Main processing -----------------------------------+
      ** ¦                                                                ¦
      ** ¦ Initial processing is performed automatically: the *INZSR is   ¦
      ** ¦ executed at program activation.                                ¦
      ** ¦                                                                ¦
      ** +----------------------------------------------------------------+
 
      *********************************************************************
      * MAIN PROCEDURE                                                    *
      *********************************************************************
      *
      ** Read in Installation Data
      *
     C******DTAARA       DEFINE                  RUNDAT                                       CPK018
     C     *DTAARA       DEFINE                  LDA
     C******             IN        RUNDAT                                                     CPK018
      ******                                                                                  CPK018
      ***Set*Date*Format*Indicator**IN98**on*if*date*format*is*MMDDYY*                        CPK018
      ******                                                                                  CPK018
     C******             IF        AGDFF = 'M'                                                CPK018
     C******             EVAL      *IN98 = *ON                                                CPK018
     C******             END                                                                  CPK018
 
      *
      ** Sub Routine to check that there are an equal number or Amends
      ** before running reports.
      *
     C                   EXSR      CHKA
 
      *There was no problem with an Odd number of Amends
     C                   IF        *IN60 = '0'
      * Set up key list for Switchable feature No. and Pgmnam
     C     KEY           KLIST
     C                   KFLD                    KEY_MGRP
     C                   KFLD                    KEY_ISEQ                                     CUP019
     C                   KFLD                    KEY_ICDE
 
     C                   READ      UPMENUPD                             3031
      *
      * If an Error or End of file reached
     C                   IF        *IN30 = '1'
      *
     C     *LOCK         IN        LDA
     C                   EVAL      *INU7 = *ON
     C                   EVAL      *INU8 = *ON
     C                   EVAL      DBFILE =  'UPMENUPD'
     C                   EVAL      DBKEY  =  '(key)'
     C                   EVAL      DBPGM  =  PSProcPgm
     C                   EVAL      DBASE  =  001
     C                   EVAL      DBMOD  =  PSProcMod
     C                   EVAL      DBPROC =  'SR/MAIN'
     C                   OUT       LDA
     C                   EXSR      *PSSR
     C                   ELSE
      *
     C                   WRITE     HEADH                                70
      *
      * Do while a record exists
     C                   DOW       *IN31 = '0'
     C                   EVAL      ACTION = *BLANKS
     C                   EVAL      Indfld = *BLANKS
      *
      * If Action Code is Insert
     C                   IF        UP_MIACTC = 'I'
     C                   MOVEL     UP_MGRP       KEY_MGRP
     C                   MOVEL     UP_ISEQ       KEY_ISEQ                                     CUP019
     C                   MOVEL     UP_ICDE       KEY_ICDE
     C*****KEY           CHAIN     UTMENUL0                           32                      CUP019
     C     KEY           CHAIN     UTMENUL1                           32                      CUP019
      *
      * If a record is found on the chain then print out a message
     C                   IF        *IN32 = '0'
      * If The two records are different - Move along Character by Character indicating
      * differences on the third Array (ErrorArray) ELSE the record saying Identical
     C                   IF        CoreFld <> Updfield
     C                   MOVEA     Updfield      UpdatArray
     C                   MOVEA     CoreFld       SysArray
     C                   CLEAR                   ErrorArray
     C                   Z-ADD     1             x
     C                   DOU       X > 258
     C     SysArray(x)   COMP      UpdatArray(x)                          43
     C                   IF        *IN43 = '0'
     C                   EVAL      *IN39 = '1'
     C                   MOVEA     '*'           ErrorArray(x)
     C                   ENDIF
     C                   ADD       1             x
     C                   ENDDO
      * Move error indicator array to field for output
     C                   MOVEA     ErrorArray    IndFld
      *
     C                   MOVEL     'INSERT'      ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ELSE
     C                   EVAL      *IN42 = '1'
     C                   MOVEL     'INSERT'      ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
      * If Action Code is Delete
     C                   IF        UP_MIACTC = 'D'
     C                   MOVEL     UP_MGRP       KEY_MGRP
     C                   MOVEL     UP_ISEQ       KEY_ISEQ                                     CUP019
     C                   MOVEL     UP_ICDE       KEY_ICDE
     C*****KEY           CHAIN     UTMENUL0                           33                      CUP019
     C     KEY           CHAIN     UTMENUL1                           33                      CUP019
      *
      * If a file is not found on the chain print out a message
     C                   IF        *IN33 = '1'
     C                   MOVEL     'DELETE'      ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ELSE
      *
      * If The two records are different - Move along Character by Character indicating
      * differences on the third Array (ErrorArray)
     C                   IF        Corefld <> Updfield
     C                   MOVEA     Updfield      UpdatArray
     C                   MOVEA     CoreFld       SysArray
     C                   CLEAR                   ErrorArray
     C                   Z-ADD     1             x
     C                   DOU       x > 258
     C     SysArray(x)   COMP      UpdatArray(x)                          36
     C                   IF        *IN36 = '0'
     C                   EVAL      *IN34 = '1'
     C                   MOVEA     '*'           ErrorArray(x)
     C                   EndIf
     C                   ADD       1             x
     C                   EndDo
      * Move error indicator array to field for output
     C                   MOVEA     ErrorArray    IndFld
      *
      * Write out all three data structures with the indicator showing
      * where the error is
     C                   MOVEL     'DELETE'      ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
 
      *
      * If Action Code is Amend
     C                   IF        UP_MIACTC = 'A'
     C                   MOVEL     UP_MGRP       KEY_MGRP
     C                   MOVEL     UP_ISEQ       KEY_ISEQ                                     CUP019
     C                   MOVEL     UP_ICDE       KEY_ICDE
     C*****KEY           CHAIN     UTMENUL0                           35                      CUP019
     C     KEY           CHAIN     UTMENUL1                           35                      CUP019
      *
      * If a file is not found on the chain print out a message
     C                   IF        *IN35 = '1'
     C                   MOVEL     'AMEND'       ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
      * Because amends come in pairs read the after image of amend.                           CUP018
     C                   READ      UPMENUPD                               31                  CUP018
     C                   ELSE
      * If the before image record and core record are different, read                        CUP018
      * the after amend image record and compare it with the core record.                     CUP018
     C                   IF        CoreFld <> Updfield                                        CUP018
     C                   READ      UPMENUPD                               31                  CUP018
      *
      * If The two records are different - Move along Character by Character indicating
      * differences on the third Array (ErrorArray)
     C                   IF        CoreFld <> Updfield
     C                   MOVEA     Updfield      UpdatArray
     C                   MOVEA     CoreFld       SysArray
     C                   CLEAR                   ErrorArray
     C                   Z-ADD     1             x
     C                   DOU       X > 258
     C     SysArray(x)   COMP      UpdatArray(x)                          38
     C                   IF        *IN38 = '0'
     C                   EVAL      *IN36 = '1'
     C                   MOVEA     '*'           ErrorArray(x)
     C                   ENDIF
     C                   ADD       1             x
     C                   ENDDO
      * Move error indicator array to field for output
     C                   MOVEA     ErrorArray    IndFld
      *
      * Write out all three data structures with the indicator showing
      * where the error is
      * Write the after image data.                                                           CUP018
     C                   MOVEL     'AMEND'       ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ENDIF
     C                   ELSE                                                                 CUP018
      *  If the before image record is same as core record,                                   CUP018
      *  read the after image record but nothing is done.                                     CUP018
     C                   READ      UPMENUPD                               31                  CUP018
     C                   ENDIF                                                                CUP018
     C                   ENDIF
      **Because*amends*come*in pairs*read*the*next*record*here.                               CUP018
     C*****              READ      UPMENUPD                               31                  CUP018
     C                   ENDIF
      *
     C                   MOVEA     '000000000'   *IN(30)
     C                   MOVEA     '0'           *IN(39)
     C                   MOVEA     '00'          *IN(41)
     C                   READ      UPMENUPD                             3031
      *
      * If an Error
     C                   IF        *IN30 = '1'
      *
     C     *LOCK         IN        LDA
     C                   EVAL      *INU7 = *ON
     C                   EVAL      *INU8 = *ON
     C                   EVAL      DBFILE =  'UPMENUPD'
     C                   EVAL      DBKEY  =  '(key)'
     C                   EVAL      DBPGM  =  PSProcPgm
     C                   EVAL      DBASE  =  002
     C                   EVAL      DBMOD  =  PSProcMod
     C                   EVAL      DBPROC =  'SR/MAIN'
     C                   OUT       LDA
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDDO
     C                   ENDIF
      * If there are no differences between the two files UP and Sys
      * Print NO Differences
     C                   IF        *IN40 = '0'
     C                   WRITE     NODIFS                               70
     C                   ENDIF
      *
      *ELSE Error in No of Amend Records
     C                   ELSE
     C                   WRITE     HEADH
     C                   WRITE     AMENDERR
     C                   ENDIF
      *
     C                   WRITE     TRAILP                               70
 
 
     C                   SETON                                        LR
     C                   RETURN
      **********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * REPORT - WRITE To the printer file                            *
      *                                                               *
      * Called by: Main Prcessing                                     *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     REPORT        BEGSR
      *
      *
     C                   IF        (*IN33 = '1') OR
     C                             (*IN35 = '1') OR
     C                             (*IN42 = '1')
      *Overflow check for an Insert that exists or an Amend/Delete that is missing
     C                   EXSR      OFLOW
      * Write first 100 characters
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     UPDLIN1       UPDLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL1                                 70
      * Set on *IN41 to stop the appearance of Action
     C                   EVAL      *IN41 = '1'
      * Write next 100 characters
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN2       UPDLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL1                                 70
      * Write last 63 characters
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       COUNT
     C                   MOVEL     UPDLIN3       UPDLIN
     C                   MOVEL     Counter       Count58
     C                   MOVEL     Count58       COUNT
     C                   WRITE     DTL1                                 70
      * Set Off *IN41
     C                   EVAL      *IN41 ='0'
     C                   ENDIF
      *
     C                   IF        (*IN34 = '1') OR
     C                             (*IN36 = '1') OR
     C                             (*IN39 = '1')
      *Overflow check for an Insert that exists or an Amend/Delete that is missing
     C                   EXSR      OFLOW2
      * Write first 100 characters
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN1       UPDLIN
     C                   MOVEL     INDLIN1       INDLIN
     C                   MOVEL     SYSLIN1       SYSLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL2                                 70
      * Set on *IN41 to stop the appearance of Action
     C                   EVAL      *IN41 = '1'
      * Write next 100 characters
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN2       UPDLIN
     C                   MOVEL     INDLIN2       INDLIN
     C                   MOVEL     SYSLIN2       SYSLIN
     C                   WRITE     DTL2                                 70
      * Write last 63 characters
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     *BLANKS       COUNT
     C                   MOVEL     UPDLIN3       UPDLIN
     C                   MOVEL     INDLIN3       INDLIN
     C                   MOVEL     SYSLIN3       SYSLIN
     C                   MOVEL     Counter       Count58
     C                   MOVEL     Count58       COUNT
     C                   WRITE     DTL2                                 70
      * Set Off *IN41
     C                   EVAL      *IN41 ='0'
     C                   ENDIF
 
     C                   ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * OFLOW - OVERFLOW Processing  for fields that                  *
      *                 For Inserts that are there and Amends and     *
      *                 Deletes that are missing                      *
      *                                                               *
      * Called by: Main Prcessing                                     *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     OFLOW         BEGSR
      *
     C     OFLLN         SUB       PRTLN         RMLN
     C                   IF        RMLN <= 8
     C                   WRITE     HEADH                                70
     C                   ENDIF
 
     C                   ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * OFLOW2- OVERFLOW Processing for differences on >200 chars     *
      *         with file differences                                 *
      *                                                               *
      * Called by: Main Prcessing                                     *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     OFLOW2        BEGSR
      *
     C     OFLLN         SUB       PRTLN         RMLN
     C                   IF        RMLN <= 14
     C                   WRITE     HEADH                                70
     C                   ENDIF
 
     C                   ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * CHKA  - Check there are an even number of Amend Records       *
      *         with file differences                                 *
      *                                                               *
      * Called by: Main Prcessing                                     *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     CHKA          BEGSR
      *
     C                   READ      UPMENUPD                               70
     C                   DOW       *IN70 = '0'
     C                   IF        UP_MIACTC = 'A'
     C                   ADD       1             Amends
     C                   ENDIF
     C                   READ      UPMENUPD                               70
     C                   ENDDO
      *
      * Check to see if the No. of Amends is divisible by 2 and not equal 0
     C                   IF        Amends <> 0
     C     Amends        DIV       2             WAmends
     C                   MOVE      WAmends       Remainder
      *If the remainder is ='5' Set on indicator to stop File processing
     C                   IF        REMAINDER ='5'
     C                   EVAL      *IN60 = '1'
     C                   ENDIF
     C                   ENDIF
     C     *START        SETLL     UPMENUPD
 
     C                   ENDSR
      **********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *INZSR - Program Initialisation routine                       *
      *                                                               *
      * Called by: Implicitly on program activation                   *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     *INZSR        BEGSR
      ** Initialize program name
      *
     C                   MOVEL     'UP1003'      DBPGM
      *
      * MOVE IN PROGRAM FIELDS
     C                   MOVEL     'UTMENUPD'    SYSFIL
     C                   MOVEL     'UPMENUPD'    UPDFIL
     C                   MOVEL     HEADER        HEAD
      *
      * Open Files
     C                   OPEN      UPMENUPD
     C*****              OPEN      UTMENUL0                                                   CUP019
     C                   OPEN      UTMENUL1                                                   CUP019
     C                   ENDSR
 
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR
      *
     C                   IF        @RUN = *BLANK
     C                   EVAL      @RUN = 'Y'
     C                   DUMP
      *
     C                   ENDIF
      *
     C                   EVAL      *INU7 = *ON
     C                   EVAL      *INU8 = *ON
     C                   EVAL      *INLR = *ON
     C                   RETURN
      *
     C                   ENDSR
      *
