     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2002')
      *****************************************************************
/*X*IA***OVRDBF*FILE(SVAL1)*TOFILE(UPSVALTPD)**************************                       CUP021
/*X*IB***OVRDBF*FILE(SVAL2)*TOFILE(UPSVALTPD)**************************                       CUP021
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      ***UP1275 - Build Action File for SDSVALPD                      *                       CPK019
      *  UP1275 - Build Action File for GPSVALPD                      *                       CPK019
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CPK019  *MOVED     Date 02Aug04               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 CSD013 *CREATE     Date 10Sep02               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CPK019 - Use UPSVAL2PD instead of UPSVALPD.                  *
      *  CSD013 - Generic Midas ICD project.                          *
      *                                                               *
      *****************************************************************
      * Function of indicators
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write or chain fail
      *       70 - 81 Indicators for changed fields
      *
     FSVAL1     IF   E             DISK    INFSR(*PSSR)
     FSVAL2     IF   E           K DISK    INFSR(*PSSR)
     F*************************************RENAME(SDSVALD0:SVALF) PREFIX(X)                   CPK019
     F                                     RENAME(GPSVALD0:SVALF) PREFIX(X)                   CPK019
     F*UPSVALPD  O    E             DISK    INFSR(*PSSR)                                      CPK019
     FUPSVAL2PD O    E             DISK    INFSR(*PSSR)                                       CPK019
      *
      /EJECT
     D LDA           E DS           256    EXTNAME(LDA)
      *
      * Local data area for database error details
      * *LOCK IN LDA immediately before and OUT LDA immediately
      * after each database error handling.
      *
      *                                     134 141 DBFILE
      *       Defines:                      142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *
     D PSDS           SDS
      *
      * Program Status Data Structure.
      *
     D  PGM              *PROC
     D  WSID                 244    253
     D  USER                 254    263
      *
     C     *ENTRY        PLIST
     C                   PARM                    MODE              1
      *
     C     COMP          KLIST
     C                   KFLD                    GISVAL
      *
     C                   READ      SVAL1                                6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C     *IN20         DOWEQ     '0'
     C     COMP          CHAIN     SVALF                              3069
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * If record not found then record has been deleted or added
      *  depending on mode.
     C     *IN30         IFEQ      *ON
     C                   MOVE      MODE          GIACTC
     C                   WRITE     UPSVALD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
      * Need to find differences but this should only be run once;
      *  run only if mode is 'Delete'.
     C     MODE          IFEQ      'D'
      *
      * If record found then look for any differences; only look in
      *  fields that have data all of the time.
     C     *IN30         IFEQ      *OFF
     C                   MOVE      'A'           GIACTC
      * Midas module
     C     GIMMOD        IFNE      XGIMMOD
     C                   SETON                                        70
     C                   ENDIF
      * Midas module shortname
     C     GIMMSN        IFNE      XGIMMSN
     C                   SETON                                        71
     C                   ENDIF
      * Description
     C     GIDESC        IFNE      XGIDESC
     C                   SETON                                        72
     C                   ENDIF
      * Default setting
     C     GIDFT         IFNE      XGIDFT
     C                   SETON                                        73
     C                   ENDIF
      * User Help Information
     C     GIHELP        IFNE      XGIHELP
     C                   SETON                                        74
     C                   ENDIF
      * Attributes
     C     GIATTR        IFNE      XGIATTR
     C                   SETON                                        75
     C                   ENDIF
      * Data string length
     C     GIDLEN        IFNE      XGIDLEN
     C                   SETON                                        76
     C                   ENDIF
      * Supress Update?
     C     GISUPP        IFNE      XGISUPP
     C                   SETON                                        77
     C                   ENDIF
      * Introduced by
     C     GISWCH        IFNE      XGISWCH
     C                   SETON                                        78
     C                   ENDIF
      * Allow blanks
     C     GIALWB        IFNE      XGIALWB
     C                   SETON                                        79
     C                   ENDIF
      * Justification
     C     GIJUST        IFNE      XGIJUST
     C                   SETON                                        80
     C                   ENDIF
      * General error message
     C     GIEMSG        IFNE      XGIEMSG
     C                   SETON                                        81
     C                   ENDIF
      *
     C                   MOVEA     *IN(70)       INTEST           12
     C                   MOVE      INTEST        INTNUM           12 0
      * If any indicators are set on then write record.
     C     INTNUM        IFNE      0
     C                   WRITE     UPSVALD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * Set values to write second record.
     C                   MOVEL     XGIMMOD       GIMMOD
     C                   MOVEL     XGIMMSN       GIMMSN
     C                   MOVEL     XGIDESC       GIDESC
     C                   MOVEL     XGIVAL        GIVAL
     C                   MOVEL     XGIDFT        GIDFT
     C                   MOVEL     XGIHELP       GIHELP
     C                   MOVEL     XGIATTR       GIATTR
     C                   MOVEL     XGIDLEN       GIDLEN
     C                   MOVEL     XGISUPP       GISUPP
     C                   MOVEL     XGISWCH       GISWCH
     C                   MOVEL     XGIALWB       GIALWB
     C                   MOVEL     XGIJUST       GIJUST
     C                   MOVEL     XGIEMSG       GIEMSG
     C                   MOVEL     XGILCD        GILCD
     C                   MOVEL     XGILCT        GILCT
      *
     C                   WRITE     UPSVALD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
     C                   ENDIF
      *
      * Read next record
     C                   MOVEA     '000000000000'*IN(70)
     C                   READ      SVAL1                                6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C                   ENDDO
      *
     C     ENDPGM        TAG
     C                   SETON                                        LR
     C                   RETURN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR                                                  ** *PSSR SR **
      *
     C     @RUN          IFEQ      *BLANK
     C                   MOVE      'Y'           @RUN              1
     C                   DUMP
     C                   END
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
      *
      ********************************************************************
