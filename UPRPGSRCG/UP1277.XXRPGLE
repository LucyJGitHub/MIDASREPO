     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2002')
      *****************************************************************
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      ***UP1277 - Process Action File for SDSVALPD                    *                       CPK019
      *  UP1277 - Process Action File for GPSVALPD                    *                       CPK019
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CPK019  *MOVED     Date 02Aug04               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 CSD013 *CREATE     Date 10Sep02               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CPK019 - Amended to use UPSVAL2L0 and GPSVALL0  intead of    *
      *           files in zone.                                      *
      *  CSD013 - Generic Midas ICD project.                          *
      *                                                               *
      *****************************************************************
      * Function of indicators
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write, update or delete fail
      *
     F*UPSVALL0  IF   E           K DISK    INFSR(*PSSR)                                      CPK019
     FUPSVAL2L0 IF   E           K DISK    INFSR(*PSSR)                                       CPK019
     F*SDSVALL0  UF A E           K DISK    INFSR(*PSSR)                                      CPK019
     FGPSVALL0  UF A E           K DISK    INFSR(*PSSR)                                       CPK019
     F                                     RENAME(@SVALL0:SVALF) PREFIX(X)
      /EJECT
      *
     D LDA           E DS           256    EXTNAME(LDA)
      *
      * Local data area for database error details
      * *LOCK IN LDA immediately before and OUT LDA immediately
      * after each database error handling.
      *
      *                                     134 141 DBFILE
      *        Defines:                     142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *
     D PSDS           SDS
      *
      ** Program Status Data Structure.
      *
     D  PGM              *PROC
     D  WSID                 244    253
     D  USER                 254    263
      *
     C     COMP          KLIST
     C                   KFLD                    GISVAL
      *
     C******LOVAL        SETLL     UPSVALL0                                                   CPK019
     C     *LOVAL        SETLL     UPSVAL2L0                                                  CPK019
      *
     C*****              READ      UPSVALL0                             6920                  CPK019
     C                   READ      UPSVAL2L0                            6920                  CPK019
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C     *IN20         DOWEQ     '0'
      *
     C     GIACTC        IFEQ      'I'
     C                   EXSR      SETVAL
     C                   WRITE     SVALF                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
     C                   ENDIF
      *
     C     GIACTC        IFEQ      'D'
     C*****COMP          CHAIN     SDSVALL0                           3069                    CPK019
     C     COMP          CHAIN     GPSVALL0                           3069                    CPK019
     C     *IN69         IFEQ      *ON
     C     *IN30         OREQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
      *
     C                   ELSE
     C                   DELETE    SVALF                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
      *
      * If Action code = AMEND then read next record.
     C     GIACTC        IFEQ      'A'
     C                   MOVEL     GISVAL        SVALX            20
     C******             READ      UPSVALL0                             6920                  CPK019
     C                   READ      UPSVAL2L0                            6920                  CPK019
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
      * If key fields do not match exit
      *  - or if EOF indicator is on.
     C     GISVAL        IFNE      SVALX
     C     *IN20         OREQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C*****COMP          CHAIN     SDSVALL0                           3069                    CPK019
     C     COMP          CHAIN     GPSVALL0                           3069                    CPK019
     C     *IN69         IFEQ      *ON
     C     *IN30         OREQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
      *
     C                   ELSE
      *
     C                   EXSR      SETVAL
     C                   UPDATE    SVALF                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
      *
     C*****              READ      UPSVALL0                             6920                  CPK019
     C                   READ      UPSVAL2L0                            6920                  CPK019
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C                   ENDDO
      *
     C     ENDPGM        TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      *****************************************************************
      *                                                               *
      * SETVAL subroutine - set values for writes and updates         *
      *                                                               *
      *****************************************************************
     C     SETVAL        BEGSR
      * Set values to update record.
     C                   MOVEL     GISVAL        XGISVAL
     C                   MOVEL     GIMMOD        XGIMMOD
     C                   MOVEL     GIMMSN        XGIMMSN
     C                   MOVEL     GIDESC        XGIDESC
     C                   MOVEL     GIDFT         XGIDFT
     C                   MOVEL     GIHELP        XGIHELP
     C                   MOVEL     GIATTR        XGIATTR
     C                   MOVEL     GIDLEN        XGIDLEN
     C                   MOVEL     GISUPP        XGISUPP
     C                   MOVEL     GISWCH        XGISWCH
     C                   MOVEL     GIALWB        XGIALWB
     C                   MOVEL     GIJUST        XGIJUST
     C                   MOVEL     GIEMSG        XGIEMSG
      * Only set values for some fields if insert
     C     GIACTC        IFEQ      'I'
     C                   MOVEL     GIVAL         XGIVAL
     C                   MOVEL     GILCD         XGILCD
     C                   MOVEL     GILCT         XGILCT
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR                                                  ** *PSSR SR **
      *
     C     @RUN          IFEQ      *BLANK
     C                   MOVE      'Y'           @RUN              1
     C                   DUMP
     C                   END
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
      ********************************************************************
