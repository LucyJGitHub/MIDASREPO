     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2002')
      *****************************************************************
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      ***UP1278 - Action file compatibilty report for UPSVALPD        *                       CPK019
      *  UP1278 - Action file compatibilty report for UPSVAL2PD       *                       CPK019
      *                                                               *
      *  Function:  This program compares the differences between     *
      *             Action file delivered and the system.             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CPK019  *MOVED     Date 02Aug04               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 CSD013  *CREATE    Date 10Sep02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CPK019 - Change to use GPSAVLL0 and UPSVAL2L0 instead.       *
      *           Use data area ACTIONFDTA to retrieve data for       *
      *           report heading.                                     *
      *           Remove use of RUNDAT for global program.            *
      *  CSD013 - Generic Midas ICD project.                          *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Use of indicators.                                           *
      *                                                               *
      *    30         Error on Read                                   *
      *    31         EOF on Read                                     *
      *    32         Chain Fail on Insert routine                    *
      *    33         Chain Fail on Delete routine                    *
      *    34         Record there on chain but different             *
      *    35         Chain Fail on Amend routine                     *
      *    36         Record there on Chain but different             *
      *    37         Compare Indicator for Delete Routine            *
      *    38         Compare Indicator for Amend Routine             *
      *    39         Switched on if a record is found on file(Delete)*
      *    40         Conflicts detected                              *
      *    41         First line of record written                    *
      *    42         Same Record in Sys and UP for Insert            *
      *    60         Do not run report if error on Amends            *
      *    70         Error on Write                                  *
      *****98         Date Format                                     *                       CPK019
      *                                                               *
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E  I N D E X                               *
      *                                                               *
      * *PSSR - Error processing                                      *
      * *INZSR - Initialise                                           *
      *                                                               *
      *    The *INZSR subroutine will only get called automatically   *
      *    on entry to the module the first time it is run            *
      *    (unless you end the program with LR on).  Similarly        *
      *    D-spec initialisation only happens the first time.  Use    *
      *    RESET for subsequent passes.                               *
      *                                                               *
      *****************************************************************
      ** +--------------------------------------+
      ** ¦ F-specs                              ¦
      ** ¦ =======                              ¦
      ** +--------------------------------------+
     F*UPSVALPD  IF   E             DISK    INFSR(*PSSR) Prefix(UP_) USROPN                   CPK019
     FUPSVAL2L0 IF   E             DISK    INFSR(*PSSR) Prefix(UP_) USROPN                    CPK019
     F*SDSVALL0  IF   E           K DISK    INFSR(*PSSR) Prefix(SYS_) USROPN                  CPK019
     FGPSVALL0  IF   E           K DISK    INFSR(*PSSR) Prefix(SYS_) USROPN                   CPK019
     F*UP0803P1  O    E             PRINTER INFDS(SPOOL)                                      CPK019
     FUP0803P1  O    E             PRINTER INFSR(*PSSR) INFDS(SPOOL)                          CPK019
 
      *****************************************************************
      /EJECT
      *****************************************************************
      ** +--------------------------------------+
      ** ¦ D-specs                              ¦
      ** ¦ =======                              ¦
      ** +--------------------------------------+
     D LDA           E DS           256    EXTNAME(LDA)
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     D*RUNDAT        E DS                  EXTNAME(RUNDAT)                                    CPK019
      *
      ***Data*Area*giving*Installation*Control*Details                                        CPK019
                                                                                              CPK019
     D ACTIONFDTA     UDS           256                                                       CPK019
     D SYSFIL                  1     10                                                       CPK019
     D UPDFIL                 31     40                                                       CPK019
      *                                                                                       CPK019
      ** Data Area in QTEMP giving report header data                                         CPK019
 
      ** The following /COPY line includes all the defined fields in the
      ** PSDS.  They have meaningful names, prefixed by 'PS'.
 
     D*****/COPY ZACPYSRC,PSDS                                                                CPK019
     D/COPY GPCPYSRCG,PSDS                                                                    CPK019
      *
      ** Program Status Data Structure
      *
      ** +--------------------------------------+
      ** ¦ Named constants                      ¦
      ** ¦ ===============                      ¦
      ** +--------------------------------------+
 
     D Header          C                   'Report of the differences between -
     D                                     the Existing System Record and the -
     D                                     Replacement Record'
 
     D Counter         C                   '---------+---------+---------+------
     D                                     ----+---------+---------+---------+--
     D                                     --------+---------+---------+'
 
      ** +--------------------------------------+
      ** ¦ Arrays and Data Structures           ¦
      ** ¦ ==========================           ¦
      ** +--------------------------------------+
     D UpdatArray      S              1A   DIM(554)
 
     D SysArray        S              1A   DIM(554)
 
     D ErrorArray      S              1A   DIM(554)
 
      ** Data Structure for the Update file
     D                 DS
     D UP_GISVAL
     D UP_GIMMOD
     D UP_GIMMSN
     D UP_GIDESC
     D NCU_GIVAL                           LIKE(UP_GIVAL)
     D UP_GIDFT
     D UP_GIHELP
     D UP_GIATTR
     D UP_GIDLEN
     D UP_GISUPP
     D UP_GISWCH
     D UP_GIALWB
     D UP_GIJUST
     D UP_GIEMSG
     D NCU_GILCD                           LIKE(UP_GILCD)
     D NCU_GILCT                           LIKE(UP_GILCT)
     D  Updfield               1    554A
     D  UPDLIN1                1    100A
     D  UPDLIN2              101    200A
     D  UPDLIN3              201    300A
     D  UPDLIN4              301    400A
     D  UPDLIN5              401    500A
     D  UPDLIN6              501    554A
 
      ** Data Structure for the Core file
     D                 DS
     D SYS_GISVAL
     D SYS_GIMMOD
     D SYS_GIMMSN
     D SYS_GIDESC
     D NCS_GIVAL                           LIKE(SYS_GIVAL)
     D SYS_GIDFT
     D SYS_GIHELP
     D SYS_GIATTR
     D SYS_GIDLEN
     D SYS_GISUPP
     D SYS_GISWCH
     D SYS_GIALWB
     D SYS_GIJUST
     D SYS_GIEMSG
     D NCS_GILCD                           LIKE(SYS_GILCD)
     D NCS_GILCT                           LIKE(SYS_GILCT)
     D  CoreFld                1    554A
     D  SYSLIN1                1    100A
     D  SYSLIN2              101    200A
     D  SYSLIN3              201    300A
     D  SYSLIN4              301    400A
     D  SYSLIN5              401    500A
     D  SYSLIN6              501    554A
 
      ** Data Structure for the Error indicators
     D                 DS           554
     D  Indfld                 1    554A
     D  INDLIN1                1    100A
     D  INDLIN2              101    200A
     D  INDLIN3              201    300A
     D  INDLIN4              301    400A
     D  INDLIN5              401    500A
     D  INDLIN6              501    554A
 
     DSPOOL            DS
     D SFILE                  83     92
     D SFNUM                 123    124B 0
     D OFLLN                 188    189B 0
     D PRTLN                 367    368B 0
 
      ** +--------------------------------------+
      ** ¦ Declared variables                   ¦
      ** ¦ ==================                   ¦
      ** +--------------------------------------+
 
     D @RUN            S              1
     D KEY_GISVAL      S                   LIKE(UP_GISVAL)
     D X               S              5  0
     D*UPDFIL          S             10                                                       CPK019
     D*SYSFIL          S             10                                                       CPK019
     D ACTION          S              7
     D RMLN            S              2  0
     D Amends          S              5  1
     D COUNT           S            100
     D COUNT54         S             54
     D WAmends         S              5  1
     D Remainder       S              1
 
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ** +--- Start of Main processing -----------------------------------+
      ** ¦                                                                ¦
      ** ¦ Initial processing is performed automatically: the *INZSR is   ¦
      ** ¦ executed at program activation.                                ¦
      ** ¦                                                                ¦
      ** +----------------------------------------------------------------+
 
      *********************************************************************
      * MAIN PROCEDURE                                                    *
      *********************************************************************
      *
      * Fill fields not to be compared with '='s, so that they  will not be
      * any different between the system file and the update file.
     C                   EVAL      NCU_GIVAL  = '====================+
     C                                           ====================+
     C                                           ====================+
     C                                           ====================+
     C                                           ====================+
     C                                           ====================+
     C                                           ====================+
     C                                           ====================+
     C                                           ====================+
     C                                           ===================='
     C                   EVAL      NCS_GIVAL  = '====================+
     C                                           ====================+
     C                                           ====================+
     C                                           ====================+
     C                                           ====================+
     C                                           ====================+
     C                                           ====================+
     C                                           ====================+
     C                                           ====================+
     C                                           ===================='
     C                   EVAL      NCU_GILCD  = 0
     C                   EVAL      NCS_GILCD  = 0
     C                   EVAL      NCU_GILCT  = '='
     C                   EVAL      NCS_GILCT  = '='
      *
      ** Read in Installation Data
      *
     C******DTAARA       DEFINE                  RUNDAT                                       CPK019
     C     *DTAARA       DEFINE                  LDA
     C******             IN        RUNDAT                                                     CPK019
      *
      ***Set*Date*Format*Indicator**IN98**on*if*date*format*is*MMDDYY                         CPK019
      *
     C*****              IF        AGDFF = 'M'                                                CPK019
     C*****              EVAL      *IN98 = *ON                                                CPK019
     C*****              END                                                                  CPK019
      *
      ** Sub Routine to check that there are an equal number or Amends
      ** before running reports.
      *
     C                   EXSR      CHKA
 
      *There was no problem with an Odd number of Amends
     C                   IF        *IN60 = '0'
      * Set up key list for Switchable feature No. and Pgmnam
     C     KEY           KLIST
     C                   KFLD                    KEY_GISVAL
 
     C*****              READ      UPSVALPD                             3031                  CPK019
     C                   READ      UPSVAL2L0                            3031                  CPK019
      *
      * If an Error or End of file reached
     C                   IF        *IN30 = '1'
      *
     C     *LOCK         IN        LDA
     C                   EVAL      *INU7 = *ON
     C                   EVAL      *INU8 = *ON
     C*****              EVAL      DBFILE =  'UPSVALPD'                                       CPK019
     C                   EVAL      DBFILE =  'UPSVAL2L0'                                      CPK019
     C                   EVAL      DBKEY  =  '(key)'
     C                   EVAL      DBPGM  =  PSProcPgm
     C                   EVAL      DBASE  =  001
     C                   EVAL      DBMOD  =  PSProcMod
     C                   EVAL      DBPROC =  'SR/MAIN'
     C                   OUT       LDA
     C                   EXSR      *PSSR
     C                   ELSE
      *
      * Do while a record exists
     C                   DOW       *IN31 = '0'
     C                   EVAL      ACTION = *BLANKS
     C                   EVAL      Indfld = *BLANKS
      *
      * If Action Code is Insert
     C                   IF        UP_GIACTC = 'I'
     C                   MOVEL     UP_GISVAL     KEY_GISVAL
     C*****KEY           CHAIN     SDSVALL0                           32                      CPK019
     C     KEY           CHAIN     GPSVALL0                           32                      CPK019
      *
      * If a record is found on the chain then print out a message
     C                   IF        *IN32 = '0'
      * If the two records are different - Move along Character by Character indicating
      * differences on the third Array (ErrorArray) ELSE the record saying Identical
     C                   IF        CoreFld <> Updfield
     C                   MOVEA     Updfield      UpdatArray
     C                   MOVEA     CoreFld       SysArray
     C                   CLEAR                   ErrorArray
     C                   Z-ADD     1             X
     C                   DOU       X > 554
     C     SysArray(X)   COMP      UpdatArray(X)                          43
     C                   IF        *IN43 = '0'
     C                   EVAL      *IN39 = '1'
     C                   MOVEA     '*'           ErrorArray(X)
     C                   ENDIF
     C                   ADD       1             X
     C                   ENDDO
      * Move error indicator array to field for output
     C                   MOVEA     ErrorArray    IndFld
      *
     C                   MOVEL     'INSERT'      ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ELSE
     C                   EVAL      *IN42 = '1'
     C                   MOVEL     'INSERT'      ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
      * If Action Code is Delete
     C                   IF        UP_GIACTC = 'D'
     C                   MOVEL     UP_GISVAL     KEY_GISVAL
     C*****KEY           CHAIN     SDSVALL0                           33                      CPK019
     C     KEY           CHAIN     GPSVALL0                           33                      CPK019
      *
      * If a file is not found on the chain print out a message
     C                   IF        *IN33 = '1'
     C                   MOVEL     'DELETE'      ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ELSE
      *
      * If the two records are different - Move along Character by Character indicating
      * differences on the third Array (ErrorArray)
     C                   IF        Corefld <> Updfield
     C                   MOVEA     Updfield      UpdatArray
     C                   MOVEA     CoreFld       SysArray
     C                   CLEAR                   ErrorArray
     C                   Z-ADD     1             X
     C                   DOU       X > 554
     C     SysArray(X)   COMP      UpdatArray(X)                          36
     C                   IF        *IN36 = '0'
     C                   EVAL      *IN34 = '1'
     C                   MOVEA     '*'           ErrorArray(X)
     C                   EndIf
     C                   ADD       1             X
     C                   EndDo
      * Move error indicator array to field for output
     C                   MOVEA     ErrorArray    IndFld
      *
      * Write out all three data structures with the indicator showing
      * where the error is
     C                   MOVEL     'DELETE'      ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
      * If Action Code is Amend
     C                   IF        UP_GIACTC = 'A'
     C                   MOVEL     UP_GISVAL     KEY_GISVAL
     C*****KEY           CHAIN     SDSVALL0                           35                      CPK019
     C     KEY           CHAIN     GPSVALL0                           35                      CPK019
      *
      * If a file is not found on the chain print out a message
     C                   IF        *IN35 = '1'
     C                   MOVEL     'AMEND'       ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
      * Because amends come in pairs read the after image of amend.
     C*****              READ      UPSVALPD                               31                  CPK019
     C                   READ      UPSVAL2L0                              31                  CPK019
     C                   ELSE
      * If the before image record and core record are different, read
      * the after amend image record and compare it with the core record.
     C                   IF        CoreFld <> Updfield
     C*****              READ      UPSVALPD                               31                  CPK019
     C                   READ      UPSVAL2L0                              31                  CPK019
      *
      * If The two records are different - Move along Character by Character indicating
      * differences on the third Array (ErrorArray)
     C                   IF        CoreFld <> Updfield
     C                   MOVEA     Updfield      UpdatArray
     C                   MOVEA     CoreFld       SysArray
     C                   CLEAR                   ErrorArray
     C                   Z-ADD     1             X
     C                   DOU       X > 554
     C     SysArray(X)   COMP      UpdatArray(X)                          38
     C                   IF        *IN38 = '0'
     C                   EVAL      *IN36 = '1'
     C                   MOVEA     '*'           ErrorArray(X)
     C                   ENDIF
     C                   ADD       1             X
     C                   ENDDO
      * Move error indicator array to field for output
     C                   MOVEA     ErrorArray    IndFld
      *
      * Write out all three data structures with the indicator showing
      *  where the error is.
      * Write the after image data.
     C                   MOVEL     'AMEND'       ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ENDIF
     C                   ELSE
      *  If the before image record is same as core record,
      *  read the after image record but nothing is done.
     C*****              READ      UPSVALPD                               31                  CPK019
     C                   READ      UPSVAL2L0                              31                  CPK019
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
     C                   MOVEA     '000000000'   *IN(30)
     C                   MOVEA     '0'           *IN(39)
     C                   MOVEA     '00'          *IN(41)
     C*****              READ      UPSVALPD                             3031                  CPK019
     C                   READ      UPSVAL2L0                            3031                  CPK019
      *
      * If an Error
     C                   IF        *IN30 = '1'
      *
     C     *LOCK         IN        LDA
     C                   EVAL      *INU7 = *ON
     C                   EVAL      *INU8 = *ON
     C*****              EVAL      DBFILE =  'UPSVALPD'                                       CPK019
     C                   EVAL      DBFILE =  'UPSVAL2L0'                                      CPK019
     C                   EVAL      DBKEY  =  '(key)'
     C                   EVAL      DBPGM  =  PSProcPgm
     C                   EVAL      DBASE  =  002
     C                   EVAL      DBMOD  =  PSProcMod
     C                   EVAL      DBPROC =  'SR/MAIN'
     C                   OUT       LDA
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDDO
     C                   ENDIF
      * If there are no differences between the two files UP and Sys
      * Print No Differences ...
     C                   IF        *IN40 = '0'
     C                   WRITE     HEADH                                70
     C                   WRITE     NODIFS                               70
     C                   ENDIF
      *
      * ... else Error in No of Amend Records
     C                   ELSE
     C                   WRITE     HEADH
     C                   WRITE     AMENDERR
     C                   ENDIF
      *
     C                   WRITE     TRAILP                               70
 
 
     C                   SETON                                        LR
     C                   RETURN
      **********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * REPORT - WRITE To the printer file                            *
      *                                                               *
      * Called by: Main Prcessing                                     *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     REPORT        BEGSR
      *
      *
     C                   IF        (*IN33 = '1') OR
     C                             (*IN35 = '1') OR
     C                             (*IN42 = '1')
     C                   WRITE     HEADH                                70
      * Write first 100  characters
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     UPDLIN1       UPDLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL1                                 70
      * Set on *IN41 to stop the appearance of Action
     C                   EVAL      *IN41 = '1'
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN2       UPDLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL1                                 70
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN3       UPDLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL1                                 70
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN4       UPDLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL1                                 70
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN5       UPDLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL1                                 70
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       COUNT
     C                   MOVEL     UPDLIN6       UPDLIN
     C                   MOVEL     Counter       COUNT54
     C                   MOVEL     COUNT54       COUNT
     C                   WRITE     DTL1                                 70
      * Set Off *IN41
     C                   EVAL      *IN41 ='0'
     C                   ENDIF
      *
     C                   IF        (*IN34 = '1') OR
     C                             (*IN36 = '1') OR
     C                             (*IN39 = '1')
     C                   WRITE     HEADH                                70
      * Write first 100 characters
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN1       UPDLIN
     C                   MOVEL     INDLIN1       INDLIN
     C                   MOVEL     SYSLIN1       SYSLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL2                                 70
      * Set on *IN41 to stop the appearance of Action
     C                   EVAL      *IN41 = '1'
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN2       UPDLIN
     C                   MOVEL     INDLIN2       INDLIN
     C                   MOVEL     SYSLIN2       SYSLIN
     C                   WRITE     DTL2                                 70
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN3       UPDLIN
     C                   MOVEL     INDLIN3       INDLIN
     C                   MOVEL     SYSLIN3       SYSLIN
     C                   WRITE     DTL2                                 70
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN4       UPDLIN
     C                   MOVEL     INDLIN4       INDLIN
     C                   MOVEL     SYSLIN4       SYSLIN
     C                   WRITE     DTL2                                 70
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN5       UPDLIN
     C                   MOVEL     INDLIN5       INDLIN
     C                   MOVEL     SYSLIN5       SYSLIN
     C                   WRITE     DTL2                                 70
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     *BLANKS       COUNT
     C                   MOVEL     UPDLIN6       UPDLIN
     C                   MOVEL     INDLIN6       INDLIN
     C                   MOVEL     SYSLIN6       SYSLIN
     C                   MOVEL     Counter       COUNT54
     C                   MOVEL     COUNT54       COUNT
     C                   WRITE     DTL2                                 70
      * Set Off *IN41
     C                   EVAL      *IN41 ='0'
     C                   ENDIF
 
     C                   ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * CHKA  - Check there are an even number of Amend Records       *
      *         with file differences                                 *
      *                                                               *
      * Called by: Main Prcessing                                     *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     CHKA          BEGSR
      *
     C*****              READ      UPSVALPD                               70                  CPK019
     C                   READ      UPSVAL2L0                              70                  CPK019
     C                   DOW       *IN70 = '0'
     C                   IF        UP_GIACTC = 'A'
     C                   ADD       1             Amends
     C                   ENDIF
     C*****              READ      UPSVALPD                               70                  CPK019
     C                   READ      UPSVAL2L0                              70                  CPK019
     C                   ENDDO
      *
      * Check to see if the No. of Amends is divisible by 2 and not equal 0
     C                   IF        Amends <> 0
     C     Amends        DIV       2             WAmends
     C                   MOVE      WAmends       Remainder
      * If the remainder is ='5' Set on indicator to stop File processing
     C                   IF        REMAINDER ='5'
     C                   EVAL      *IN60 = '1'
     C                   ENDIF
     C                   ENDIF
     C******START        SETLL     UPSVALPD                                                   CPK019
     C     *START        SETLL     UPSVAL2L0                                                  CPK019
 
     C                   ENDSR
      **********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *INZSR - Program Initialisation routine                       *
      *                                                               *
      * Called by: Implicitly on program activation                   *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     *INZSR        BEGSR
      * Initialise program name
      *
     C                   MOVEL     'UP1278'      DBPGM
      *
      * Move in program fields
     C*****              MOVEL     'SDSVALPD'    SYSFIL                                       CPK019
     C*****              MOVEL     'UPSVALPD'    UPDFIL                                       CPK019
     C                   MOVEL     HEADER        HEAD
      * Open files
     C*****              OPEN      UPSVALPD                                                   CPK019
     C                   OPEN      UPSVAL2L0                                                  CPK019
     C*****              OPEN      SDSVALL0                                                   CPK019
     C                   OPEN      GPSVALL0                                                   CPK019
     C                   ENDSR
 
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR
      *
     C                   IF        @RUN = *BLANK
     C                   EVAL      @RUN = 'Y'
     C                   DUMP
      *
     C                   ENDIF
      *
     C                   EVAL      *INU7 = *ON
     C                   EVAL      *INU8 = *ON
     C                   EVAL      *INLR = *ON
     C                   RETURN
      *
     C                   ENDSR
      *
