      *****************************************************************
     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2002')
      *****************************************************************
/*X*IA***OVRDBF*FILE(ACTF1)*TOFILE(UPACTFTPD)**************************                       CUP021
/*X*IB***OVRDBF*FILE(ACTF2)*TOFILE(UPACTFTPD)**************************                       CUP021
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP1295 - Build Action File for UPACTFPD                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CPK019             Date 13Jul04               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 CUP020 *CREATE     Date 08Nov02               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CPK019 - Include the compare of newly added fields.          *
      *           Use UPACTF2PD instead of UPACTAPD.                  *
      *  CUP020 - Creation of new set of Action File utilities.       *
      *                                                               *
      *****************************************************************
      * Function of indicators
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write or chain fail
      ******* 70 - 77 Indicators for changed fields                                           CPK019
      *       70 - 83 Indicators for changed fields                                           CPK019
      *
     FACTF1     IF   E             DISK    INFSR(*PSSR)
     FACTF2     IF   E           K DISK    INFSR(*PSSR)
     F                                     RENAME(UPCPYFD0:ACTFF) PREFIX(X)
     F*UPACTFAPD O    E             DISK    INFSR(*PSSR)                                      CPK019
     FUPACTF2PD O    E             DISK    INFSR(*PSSR)                                       CPK019
      *
      /EJECT
     D LDA           E DS           256    EXTNAME(LDA)
      *
      * Local data area for database error details
      * *LOCK IN LDA immediately before and OUT LDA immediately
      * after each database error handling.
      *
      *                                     134 141 DBFILE
      *       Defines:                      142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *
     D PSDS           SDS
      *
      * Program Status Data Structure.
      *
     D  PGM              *PROC
     D  WSID                 244    253
     D  USER                 254    263
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    MODE              1
      *
     C     COMP          KLIST
     C                   KFLD                    AABONF
      *
     C                   READ      ACTF1                                6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C     *IN20         DOWEQ     '0'
     C     COMP          CHAIN     ACTFF                              3069
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * If record not found then record has been deleted or added
      *  depending on mode.
     C     *IN30         IFEQ      *ON
     C                   MOVE      MODE          AAACTC
     C                   WRITE     UPACTFAD0                            69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
      * Need to find differences but this should only be run once;
      *  run only if mode is 'Delete'.
     C     MODE          IFEQ      'D'
      *
      * If record found then look for any differences; only look in
      *  fields that have data all of the time.
     C     *IN30         IFEQ      *OFF
     C                   MOVE      'A'           AAACTC
     C     AAFRMF        IFNE      XAAFRMF
     C                   SETON                                        70
     C                   ENDIF
     C     AATOFL        IFNE      XAATOFL
     C                   SETON                                        71
     C                   ENDIF
     C     AACOPY        IFNE      XAACOPY
     C                   SETON                                        72
     C                   ENDIF
     C     AALIBR        IFNE      XAALIBR
     C                   SETON                                        73
     C                   ENDIF
     C     AAAFLF        IFNE      XAAAFLF
     C                   SETON                                        74
     C                   ENDIF
     C     AAUPPF        IFNE      XAAUPPF
     C                   SETON                                        75
     C                   ENDIF
     C     AAUPLF        IFNE      XAAUPLF
     C                   SETON                                        76
     C                   ENDIF
     C     AAPROG        IFNE      XAAPROG
     C                   SETON                                        77
     C                   ENDIF
     C     AABLDP        IFNE      XAABLDP                                                    CPK019
     C                   SETON                                        78                      CPK019
     C                   ENDIF                                                                CPK019
     C     AARVWP        IFNE      XAARVWP                                                    CPK019
     C                   SETON                                        79                      CPK019
     C                   ENDIF                                                                CPK019
     C     AAPRCP        IFNE      XAAPRCP                                                    CPK019
     C                   SETON                                        80                      CPK019
     C                   ENDIF                                                                CPK019
     C     AACNFP        IFNE      XAACNFP                                                    CPK019
     C                   SETON                                        81                      CPK019
     C                   ENDIF                                                                CPK019
     C     AARSTP        IFNE      XAARSTP                                                    CPK019
     C                   SETON                                        82                      CPK019
     C                   ENDIF                                                                CPK019
     C     AARTAY        IFNE      XAARTAY                                                    CPK019
     C                   SETON                                        83                      CPK019
     C                   ENDIF                                                                CPK019
      *
     C*****              MOVEA     *IN(70)       INTEST            8                          CPK019
     C                   MOVEA     *IN(70)       INTEST           14                          CPK019
     C*****              MOVE      INTEST        INTNUM            8 0                        CPK019
     C                   MOVE      INTEST        INTNUM           14 0                        CPK019
      * If any indicators are set on then write record.
     C     INTNUM        IFNE      0
     C                   WRITE     UPACTFAD0                            69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * Set values to write second record.
     C                   MOVEL     XAAFRMF       AAFRMF
     C                   MOVEL     XAATOFL       AATOFL
     C                   MOVEL     XAACOPY       AACOPY
     C                   MOVEL     XAALIBR       AALIBR
     C                   MOVEL     XAAAFLF       AAAFLF
     C                   MOVEL     XAAUPPF       AAUPPF
     C                   MOVEL     XAAUPLF       AAUPLF
     C                   MOVEL     XAAPROG       AAPROG
     C                   MOVEL     XAABLDP       AABLDP                                       CPK019
     C                   MOVEL     XAARVWP       AARVWP                                       CPK019
     C                   MOVEL     XAAPRCP       AAPRCP                                       CPK019
     C                   MOVEL     XAACNFP       AACNFP                                       CPK019
     C                   MOVEL     XAARSTP       AARSTP                                       CPK019
     C                   MOVEL     XAARTAY       AARTAY                                       CPK019
      *
     C                   WRITE     UPACTFAD0                            69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
     C                   ENDIF
      *
      * Read next record
     C                   MOVEA     '00000000'    *IN(70)
     C                   MOVEA     '000000'      *IN(78)                                      CPK019
     C                   READ      ACTF1                                6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C                   ENDDO
      *
     C     ENDPGM        TAG
     C                   SETON                                        LR
     C                   RETURN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR                                                  ** *PSSR SR **
      *
     C     @RUN          IFEQ      *BLANK
     C                   MOVE      'Y'           @RUN              1
     C                   DUMP
     C                   END
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
      *
      ********************************************************************
