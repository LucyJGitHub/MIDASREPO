      *****************************************************************
     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2003')
      *****************************************************************
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      ***UP1473 - Action file compatibilty report for UPMNU2PD        *                       CUP028
      *  UP1473 - Action file compatibilty report for UPMNU2PD        *                       CUP028
      *           and UPMNU3PD                                        *                       CUP028
      *                                                               *
      *  Function:  This program compares the differences between     *
      *             Action file delivered and the system.             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CUP028  *MOVED     Date 08Sep04               *
      *                 CUP022             Date 11Mar04               *
      *                 CUP024  *CREATE    Date 11Sep03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP028 - Moved from zone to global.                          *
      *           Amended to use global files and not to use data area*
      *           RUNDAT.                                             *
      *           Include new field MICON. (not activated, so not     *
      *           compared.)                                          *
      *  CUP022 - Standardise Action File processing.                 *
      *           Retrieve from dtaara ACTIONFDTA for report header.  *
      *  CUP024 - Creation of new set of Action File utilities.       *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Use of indicators.                                           *
      *                                                               *
      *    30         Error on Read                                   *
      *    31         EOF on Read                                     *
      *    32         Chain Fail on Insert routine                    *
      *    33         Chain Fail on Delete routine                    *
      *    34         Record there on chain but different             *
      *    35         Chain Fail on Amend routine                     *
      *    36         Record there on Chain but different             *
      *    37         Compare Indicator for Delete Routine            *
      *    38         Compare Indicator for Amend Routine             *
      *    39         Switched on if a record is found on file(Delete)*
      *    40         Conflicts detected                              *
      *    41         First line of record written                    *
      *    42         Same Record in Sys and UP for Insert            *
      *    60         Do not run report if error on Amends            *
      *    70         Error on Write                                  *
      *****98         Date Format                                     *                       CUP028
      *                                                               *
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E  I N D E X                               *
      *                                                               *
      * *PSSR - Error processing                                      *
      * *INZSR - Initialise                                           *
      *                                                               *
      *    The *INZSR subroutine will only get called automatically   *
      *    on entry to the module the first time it is run            *
      *    (unless you end the program with LR on).  Similarly        *
      *    D-spec initialisation only happens the first time.  Use    *
      *    RESET for subsequent passes.                               *
      *                                                               *
      *****************************************************************
      ** +--------------------------------------+
      ** ¦ F-specs                              ¦
      ** ¦ =======                              ¦
      ** +--------------------------------------+
     F*UPMNU2L0  IF   E           K DISK    INFSR(*PSSR) Prefix(UP_) USROPN                   CUP028
     FUPMNU3L0  IF   E             DISK    INFSR(*PSSR) Prefix(UP_) USROPN                    CUP028
     F*SFMENUL0  IF   E           K DISK    INFSR(*PSSR) Prefix(SYS_) USROPN                  CUP028
     FGPMENUL0  IF   E           K DISK    INFSR(*PSSR) Prefix(SYS_) USROPN                   CUP028
     F*UP0803P1  O    E             PRINTER INFDS(SPOOL)                                      CUP028
     FUP0803P1  O    E             PRINTER INFSR(*PSSR) INFDS(SPOOL)                          CUP028
 
      *****************************************************************
      /EJECT
      *****************************************************************
      ** +--------------------------------------+
      ** ¦ D-specs                              ¦
      ** ¦ =======                              ¦
      ** +--------------------------------------+
     D LDA           E DS           256    EXTNAME(LDA)
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     D*RUNDAT        E DS                  EXTNAME(RUNDAT)                                    CUP028
      *
      ** Data Area giving Installation Control Details
 
     D ACTIONFDTA     UDS           256                                                       CUP022
     D SYSFIL                  1     10                                                       CUP022
     D UPDFIL                 31     40                                                       CUP022
      *
      ** Data Area in QTEMP giving report header data details                                 CUP022
      *
      ** The following /COPY line includes all the defined fields in the
      ** PSDS.  They have meaningful names, prefixed by 'PS'.
 
     D*/COPY ZACPYSRC,PSDS                                                                   CUP028
     D/COPY GPCPYSRCG,PSDS                                                                   CUP028
      *
      ** Program Status Data Structure
      *
      ** +--------------------------------------+
      ** ¦ Named constants                      ¦
      ** ¦ ===============                      ¦
      ** +--------------------------------------+
 
     D Header          C                   'Report of the differences between -
     D                                     the Existing System Record and the -
     D                                     Replacement Record'
 
     D Counter         C                   '---------+---------+---------+------
     D                                     ----+---------+---------+---------+--
     D                                     --------+---------+---------+'
 
      ** +--------------------------------------+
      ** ¦ Arrays and Data Structures           ¦
      ** ¦ ==========================           ¦
      ** +--------------------------------------+
     D*UpdatArray      S              1A   DIM(198)                                           CUP028
     D UpdatArray      S              1A   DIM(199)                                           CUP028
 
     D*SysArray        S              1A   DIM(198)                                           CUP028
     D SysArray        S              1A   DIM(199)                                           CUP028
 
     D*ErrorArray      S              1A   DIM(198)                                           CUP028
     D ErrorArray      S              1A   DIM(199)                                           CUP028
 
      ** Data Structure for the Update file
     D                 DS
     D UP_MIZONE
     D UP_MIINDX
     D UP_MISECL
     D UP_MIMODC
     D UP_MIMMSN
     D UP_MIEXTP
     D UP_MIFRAM
     D UP_MIPATH
     D UP_MIAAC1
     D UP_MIAAC2
     D UP_MIAAC3
     D UP_MIAAC4
     D UP_MIAAC5
     D UP_MIAAC6
     D UP_MIAAC7
     D UP_MIAAC8
     D UP_MIAAC9
     D UP_MIAAC10
     D UP_MIACOB
     D UP_MIAPI1
     D UP_MIAPI2
     D UP_MIAPI3
     D UP_MIAPI4
     D UP_MIAPI5
     D UP_MIINS
     D NCU_MICON                           LIKE(UP_MICON)                                     CUP028
     D**Updfield               1    198A                                                      CUP028
     D  Updfield               1    199A                                                      CUP028
     D  UPDLIN1                1    100A
     D**UPDLIN2              101    198A                                                      CUP028
     D  UPDLIN2              101    199A                                                      CUP028
 
      ** Data Structure for the Core file
     D                 DS
     D SYS_MIZONE
     D SYS_MIINDX
     D SYS_MISECL
     D SYS_MIMODC
     D SYS_MIMMSN
     D SYS_MIEXTP
     D SYS_MIFRAM
     D SYS_MIPATH
     D SYS_MIAAC1
     D SYS_MIAAC2
     D SYS_MIAAC3
     D SYS_MIAAC4
     D SYS_MIAAC5
     D SYS_MIAAC6
     D SYS_MIAAC7
     D SYS_MIAAC8
     D SYS_MIAAC9
     D SYS_MIAAC10
     D SYS_MIACOB
     D SYS_MIAPI1
     D SYS_MIAPI2
     D SYS_MIAPI3
     D SYS_MIAPI4
     D SYS_MIAPI5
     D SYS_MIINS
     D NCS_MICON                           LIKE(SYS_MICON)                                    CUP028
     D**CoreFld                1    198A                                                      CUP028
     D  CoreFld                1    199A                                                      CUP028
     D  SYSLIN1                1    100A
     D**SYSLIN2              101    198A                                                      CUP028
     D  SYSLIN2              101    199A                                                      CUP028
 
      ** Data Structure for the Error indicators
     D*****************DS           198                                                       CUP028
     D                 DS           199                                                       CUP028
     D**Indfld                 1    198A                                                      CUP028
     D  Indfld                 1    199A                                                      CUP028
     D  INDLIN1                1    100A
     D**INDLIN2              101    198A                                                      CUP028
     D  INDLIN2              101    199A                                                      CUP028
 
     DSPOOL            DS
     D SFILE                  83     92
     D SFNUM                 123    124B 0
     D OFLLN                 188    189B 0
     D PRTLN                 367    368B 0
 
      ** +--------------------------------------+
      ** ¦ Declared variables                   ¦
      ** ¦ ==================                   ¦
      ** +--------------------------------------+
 
     D @RUN            S              1
     D KEY_MIINDX      S                   LIKE(UP_MIINDX)
     D X               S              5  0
     D*UPDFIL          S             10                                                       CUP022
     D*SYSFIL          S             10                                                       CUP022
     D ACTION          S              7
     D RMLN            S              2  0
     D Amends          S              5  1
     D COUNT           S            100
     D*COUNT98         S             98                                                       CUP028
     D COUNT99         S             99                                                       CUP028
     D WAmends         S              5  1
     D Remainder       S              1
 
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ** +--- Start of Main processing -----------------------------------+
      ** ¦                                                                ¦
      ** ¦ Initial processing is performed automatically: the *INZSR is   ¦
      ** ¦ executed at program activation.                                ¦
      ** ¦                                                                ¦
      ** +----------------------------------------------------------------+
 
      *********************************************************************
      * MAIN PROCEDURE                                                    *
      *********************************************************************
      *
      * Fill fields not to be compared with '='s, so that they  will not be                   CUP028
      * any different between the system file and the update file.                            CUP028
     C                   EVAL      NCU_MICON   = '='                                          CUP028
     C                   EVAL      NCS_MICON   = '='                                          CUP028
      *                                                                                       CUp028
      ** Read in Installation Data
      *
     C******DTAARA       DEFINE                  RUNDAT                                       CUP028
     C     *DTAARA       DEFINE                  LDA
     C******             IN        RUNDAT                                                     CUP028
      *
      ***Set*Date*Format*Indicator**IN98**on*if*date*format*is*MMDDYY                         CUP028
      *
     C******             IF        AGDFF = 'M'                                                CUP028
     C******             EVAL      *IN98 = *ON                                                CUP028
     C******             END                                                                  CUP028
      *
      ** Sub Routine to check that there are an equal number or Amends
      ** before running reports.
      *
     C                   EXSR      CHKA
 
      *There was no problem with an Odd number of Amends
     C                   IF        *IN60 = '0'
      * Set up key list
     C     KEY           KLIST
     C                   KFLD                    KEY_MIINDX
 
     C*****              READ      UPMNU2L0                             3031                  CUP028
     C                   READ      UPMNU3L0                             3031                  CUP028
      *
      * If an Error or End of file reached
     C                   IF        *IN30 = '1'
      *
     C     *LOCK         IN        LDA
     C                   EVAL      *INU7 = *ON
     C                   EVAL      *INU8 = *ON
     C*****              EVAL      DBFILE =  'UPMNU2L0'                                       CUP028
     C                   EVAL      DBFILE =  'UPMNU3L0'                                       CUP028
     C                   EVAL      DBKEY  =  UP_MIINDX
     C                   EVAL      DBPGM  =  PSProcPgm
     C                   EVAL      DBASE  =  001
     C                   EVAL      DBMOD  =  PSProcMod
     C                   EVAL      DBPROC =  'SR/MAIN'
     C                   OUT       LDA
     C                   EXSR      *PSSR
     C                   ELSE
      *
     C                   WRITE     HEADH                                70
      *
      * Do while a record exists
     C                   DOW       *IN31 = '0'
     C                   EVAL      ACTION = *BLANKS
     C                   EVAL      Indfld = *BLANKS
      *
      * If Action Code is Insert
     C                   IF        UP_MIACTC = 'I'
     C                   MOVEL     UP_MIINDX     KEY_MIINDX
     C*****KEY           CHAIN     SFMENUL0                           32                      CUP028
     C     KEY           CHAIN     GPMENUL0                           32                      CUP028
      *
      * If a record is found on the chain then print out a message
     C                   IF        *IN32 = '0'
      * If the two records are different - Move along Character by Character indicating
      * differences on the third Array (ErrorArray) ELSE the record saying Identical
     C                   IF        CoreFld <> Updfield
     C                   MOVEA     Updfield      UpdatArray
     C                   MOVEA     CoreFld       SysArray
     C                   CLEAR                   ErrorArray
     C                   Z-ADD     1             X
     C******             DOU       X > 198                                                    CUP028
     C                   DOU       X > 199                                                    CUP028
     C     SysArray(X)   COMP      UpdatArray(X)                          43
     C                   IF        *IN43 = '0'
     C                   EVAL      *IN39 = '1'
     C                   MOVEA     '*'           ErrorArray(X)
     C                   ENDIF
     C                   ADD       1             X
     C                   ENDDO
      * Move error indicator array to field for output
     C                   MOVEA     ErrorArray    IndFld
      *
     C                   MOVEL     'INSERT'      ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ELSE
     C                   EVAL      *IN42 = '1'
     C                   MOVEL     'INSERT'      ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
      * If Action Code is Delete
     C                   IF        UP_MIACTC = 'D'
     C                   MOVEL     UP_MIINDX     KEY_MIINDX
     C*****KEY           CHAIN     SFMENUL0                           33                      CUP028
     C     KEY           CHAIN     GPMENUL0                           33                      CUP028
      *
      * If a file is not found on the chain print out a message
     C                   IF        *IN33 = '1'
     C                   MOVEL     'DELETE'      ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ELSE
      *
      * If the two records are different - Move along Character by Character indicating
      * differences on the third Array (ErrorArray)
     C                   IF        Corefld <> Updfield
     C                   MOVEA     Updfield      UpdatArray
     C                   MOVEA     CoreFld       SysArray
     C                   CLEAR                   ErrorArray
     C                   Z-ADD     1             X
     C*****              DOU       X > 198                                                    CUP028
     C                   DOU       X > 199                                                    CUp028
     C     SysArray(X)   COMP      UpdatArray(X)                          36
     C                   IF        *IN36 = '0'
     C                   EVAL      *IN34 = '1'
     C                   MOVEA     '*'           ErrorArray(X)
     C                   EndIf
     C                   ADD       1             X
     C                   EndDo
      * Move error indicator array to field for output
     C                   MOVEA     ErrorArray    IndFld
      *
      * Write out all three data structures with the indicator showing
      * where the error is
     C                   MOVEL     'DELETE'      ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
      * If Action Code is Amend
     C                   IF        UP_MIACTC = 'A'
     C                   MOVEL     UP_MIINDX     KEY_MIINDX
     C*****KEY           CHAIN     SFMENUL0                           35                      CUP028
     C     KEY           CHAIN     GPMENUL0                           35                      CUP028
      *
      * If a file is not found on the chain print out a message
     C                   IF        *IN35 = '1'
     C                   MOVEL     'AMEND'       ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
      * Because amends come in pairs read the after image of amend.
     C*****              READ      UPMNU2L0                               31                  CUP028
     C                   READ      UPMNU3L0                               31                  CUP028
     C                   ELSE
      * If the before image record and core record are different, read
      * the after amend image record and compare it with the core record.
     C                   IF        CoreFld <> Updfield
     C*****              READ      UPMNU2L0                               31                  CUP028
     C                   READ      UPMNU3L0                               31                  CUP028
      *
      * If The two records are different - Move along Character by Character indicating
      * differences on the third Array (ErrorArray)
     C                   IF        CoreFld <> Updfield
     C                   MOVEA     Updfield      UpdatArray
     C                   MOVEA     CoreFld       SysArray
     C                   CLEAR                   ErrorArray
     C                   Z-ADD     1             X
     C*****              DOU       X > 198                                                    CUP028
     C                   DOU       X > 199                                                    CUP028
     C     SysArray(X)   COMP      UpdatArray(X)                          38
     C                   IF        *IN38 = '0'
     C                   EVAL      *IN36 = '1'
     C                   MOVEA     '*'           ErrorArray(X)
     C                   ENDIF
     C                   ADD       1             X
     C                   ENDDO
      * Move error indicator array to field for output
     C                   MOVEA     ErrorArray    IndFld
      *
      * Write out all three data structures with the indicator showing
      *  where the error is.
      * Write the after image data.
     C                   MOVEL     'AMEND'       ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ENDIF
     C                   ELSE
      *  If the before image record is same as core record,
      *  read the after image record but nothing is done.
     C*****              READ      UPMNU2L0                               31                  CUP028
     C                   READ      UPMNU3L0                               31                  CUP028
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
     C                   MOVEA     '000000000'   *IN(30)
     C                   MOVEA     '0'           *IN(39)
     C                   MOVEA     '00'          *IN(41)
     C*****              READ      UPMNU2L0                             3031                  CUP028
     C                   READ      UPMNU3L0                             3031                  CUP028
      *
      * If an Error
     C                   IF        *IN30 = '1'
      *
     C     *LOCK         IN        LDA
     C                   EVAL      *INU7 = *ON
     C                   EVAL      *INU8 = *ON
     C*****              EVAL      DBFILE =  'UPMNU2L0'                                       CUP028
     C                   EVAL      DBFILE =  'UPMNU3L0'                                       CUP028
     C                   EVAL      DBKEY  =  UP_MIINDX
     C                   EVAL      DBPGM  =  PSProcPgm
     C                   EVAL      DBASE  =  002
     C                   EVAL      DBMOD  =  PSProcMod
     C                   EVAL      DBPROC =  'SR/MAIN'
     C                   OUT       LDA
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDDO
     C                   ENDIF
      * If there are no differences between the two files UP and Sys
      * Print No Differences ...
     C                   IF        *IN40 = '0'
     C                   WRITE     NODIFS                               70
     C                   ENDIF
      *
      * ... else Error in No of Amend Records
     C                   ELSE
     C                   WRITE     HEADH
     C                   WRITE     AMENDERR
     C                   ENDIF
      *
     C                   WRITE     TRAILP                               70
 
 
     C                   SETON                                        LR
     C                   RETURN
      **********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * REPORT - WRITE To the printer file                            *
      *                                                               *
      * Called by: Main Prcessing                                     *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     REPORT        BEGSR
      *
      *
     C                   IF        (*IN33 = '1') OR
     C                             (*IN35 = '1') OR
     C                             (*IN42 = '1')
      * Overflow check for an Insert that exists or an Amend/Delete that is missing
     C                   EXSR      OFLOW
      * Write first 100  characters
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     UPDLIN1       UPDLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL1                                 70
      * Set on *IN41 to stop the appearance of Action
     C                   EVAL      *IN41 = '1'
      **Write*last*98*characters                                                              CUP028
      * Write last 99 characters                                                              CUP028
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       COUNT
     C                   MOVEL     UPDLIN2       UPDLIN
     C*****              MOVEL     Counter       COUNT98                                      CUP028
     C*****              MOVEL     COUNT98       COUNT                                        CUP028
     C                   MOVEL     Counter       COUNT99                                      CUP028
     C                   MOVEL     COUNT99       COUNT                                        CUP028
     C                   WRITE     DTL1                                 70
      * Set Off *IN41
     C                   EVAL      *IN41 ='0'
     C                   ENDIF
      *
     C                   IF        (*IN34 = '1') OR
     C                             (*IN36 = '1') OR
     C                             (*IN39 = '1')
      * Overflow check for an Insert that exists or an Amend/Delete that is missing
     C                   EXSR      OFLOW2
      * Write first 100 characters
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN1       UPDLIN
     C                   MOVEL     INDLIN1       INDLIN
     C                   MOVEL     SYSLIN1       SYSLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL2                                 70
      * Set on *IN41 to stop the appearance of Action
     C                   EVAL      *IN41 = '1'
      **Write*last*98*characters                                                              CUP028
      * Write last 99 characters                                                              CUP028
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     *BLANKS       COUNT
     C                   MOVEL     UPDLIN2       UPDLIN
     C                   MOVEL     INDLIN2       INDLIN
     C                   MOVEL     SYSLIN2       SYSLIN
     C*****              MOVEL     Counter       COUNT98                                      CUP028
     C*****              MOVEL     COUNT98       COUNT                                        CUP028
     C                   MOVEL     Counter       COUNT99                                      CUP028
     C                   MOVEL     COUNT99       COUNT                                        CUP028
     C                   WRITE     DTL2                                 70
      * Set Off *IN41
     C                   EVAL      *IN41 ='0'
     C                   ENDIF
 
     C                   ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * OFLOW - OVERFLOW Processing  for fields that                  *
      *                 For Inserts that are there and Amends and     *
      *                 Deletes that are missing                      *
      *                                                               *
      * Called by: Main Prcessing                                     *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     OFLOW         BEGSR
      *
     C     OFLLN         SUB       PRTLN         RMLN
     C                   IF        RMLN <= 8
     C                   WRITE     HEADH                                70
     C                   ENDIF
 
     C                   ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * OFLOW2- OVERFLOW Processing for differences on >200 chars     *
      *         with file differences                                 *
      *                                                               *
      * Called by: Main Prcessing                                     *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     OFLOW2        BEGSR
      *
     C     OFLLN         SUB       PRTLN         RMLN
     C                   IF        RMLN <= 14
     C                   WRITE     HEADH                                70
     C                   ENDIF
 
     C                   ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * CHKA  - Check there are an even number of Amend Records       *
      *         with file differences                                 *
      *                                                               *
      * Called by: Main Prcessing                                     *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     CHKA          BEGSR
      *
     C*****              READ      UPMNU2L0                               70                  CUP028
     C                   READ      UPMNU3L0                               70                  CUP028
     C                   DOW       *IN70 = '0'
     C                   IF        UP_MIACTC = 'A'
     C                   ADD       1             Amends
     C                   ENDIF
     C*****              READ      UPMNU2L0                               70                  CUP028
     C                   READ      UPMNU3L0                               70                  CUP028
     C                   ENDDO
      *
      * Check to see if the No. of Amends is divisible by 2 and not equal 0
     C                   IF        Amends <> 0
     C     Amends        DIV       2             WAmends
     C                   MOVE      WAmends       Remainder
      * If the remainder is ='5' Set on indicator to stop File processing
     C                   IF        REMAINDER ='5'
     C                   EVAL      *IN60 = '1'
     C                   ENDIF
     C                   ENDIF
     C******START        SETLL     UPMNU2L0                                                   CUP028
     C     *START        SETLL     UPMNU3L0                                                   CUP028
 
     C                   ENDSR
      **********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *INZSR - Program Initialisation routine                       *
      *                                                               *
      * Called by: Implicitly on program activation                   *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     *INZSR        BEGSR
      * Initialise program name
      *
     C                   MOVEL     'UP1473'      DBPGM
      *
      **Move*in program fields                                                                CUP022
     C*******            MOVEL     'SFMENUPD'    SYSFIL                                       CUP022
     C*******            MOVEL     'UPMNU2L0'    UPDFIL                                       CUP022
     C                   MOVEL     HEADER        HEAD
      * Open files
     C******             OPEN      UPMNU2L0                                                   CUP028
     C                   OPEN      UPMNU3L0                                                   CUP028
     C******             OPEN      SFMENUL0                                                   CUP028
     C                   OPEN      GPMENUL0                                                   CUP028
     C                   ENDSR
 
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR
      *
     C                   IF        @RUN = *BLANK
     C                   EVAL      @RUN = 'Y'
     C                   DUMP
      *
     C                   ENDIF
      *
     C                   EVAL      *INU7 = *ON
     C                   EVAL      *INU8 = *ON
     C                   EVAL      *INLR = *ON
     C                   RETURN
      *
     C                   ENDSR
      *
