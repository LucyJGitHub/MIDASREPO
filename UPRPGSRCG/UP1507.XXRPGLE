     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2004')
      *****************************************************************
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP1507 - Process Action File for SC/GPSARTPD                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CUP029 *REWRITE    Date 19Oct04               *
      *                        *MOVED                                 *
      *                 CUP022 *CREATE     Date 04Mar04               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP029 - Moved from zone to global.                          *
      *           SCSARTPD and GPSARTPD share action file utilities.  *
      *           Change key Unique Id to file, time, event, seq.     *
      *  CUP022 - Creation of new set of Action File utilities.       *
      *                                                               *
      *****************************************************************
      * Function of indicators
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write, update or delete fail
      *
     FUPSART2L0 IF   E           K DISK    INFSR(*PSSR)
     FGPSARTL0  UF A E           K DISK    INFSR(*PSSR)
     F                                     RENAME(SCSARTD0:SARTF) PREFIX(X)
      /EJECT
      *
     D LDA           E DS           256    EXTNAME(LDA)
      *
      * Local data area for database error details
      * *LOCK IN LDA immediately before and OUT LDA immediately
      * after each database error handling.
      *
      *                                     134 141 DBFILE
      *        Defines:                     142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *
     D PSDS           SDS
      *
      ** Program Status Data Structure.
      *
     D  PGM              *PROC
     D  WSID                 244    253
     D  USER                 254    263
      *
     C     COMP          KLIST
     C                   KFLD                    GUSARN
     C                   KFLD                    GUFILE
     C                   KFLD                    GUTIME
     C                   KFLD                    GUEVNT
     C                   KFLD                    GUSEQ
      *
     C     *LOVAL        SETLL     UPSART2L0
      *
     C                   READ      UPSART2L0                            6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C     *IN20         DOWEQ     '0'
      *
     C     GUACTC        IFEQ      'I'
     C                   EXSR      SETVAL
     C                   WRITE     SARTF                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
     C                   ENDIF
      *
     C     GUACTC        IFEQ      'D'
     C     COMP          CHAIN     GPSARTL0                           3069
     C     *IN69         IFEQ      *ON
     C     *IN30         OREQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
      *
     C                   ELSE
     C                   DELETE    SARTF                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
      *
      * If Action code = AMEND then read next record.
     C     GUACTC        IFEQ      'A'
      *
     C                   MOVEL     GUSARN        SARNX             6
     C                   MOVEL     GUFILE        FILEX            10
     C                   MOVEL     GUTIME        TIMEX             1
     C                   MOVEL     GUEVNT        EVNTX             1
     C                   MOVEL     GUSEQ         SEQX              2
     C                   READ      UPSART2L0                            6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
      * If key fields do not match exit
      *  - or if EOF indicator is on.
     C     GUSARN        IFNE      SARNX
     C     GUFILE        ORNE      FILEX
     C     GUTIME        ORNE      TIMEX
     C     GUEVNT        ORNE      EVNTX
     C     GUSEQ         ORNE      SEQX
     C     *IN20         OREQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C     COMP          CHAIN     GPSARTL0                           3069
     C     *IN69         IFEQ      *ON
     C     *IN30         OREQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
      *
     C                   ELSE
      *
     C                   EXSR      SETVAL
     C                   UPDATE    SARTF                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
      *
     C                   READ      UPSART2L0                            6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C                   ENDDO
      *
     C     ENDPGM        TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      *****************************************************************
      *                                                               *
      * SETVAL subroutine - set values for deletes/writes/updates     *
      *                                                               *
      *****************************************************************
     C     SETVAL        BEGSR
      *
      * Set values to update record.
     C                   MOVEL     GUSARN        XGUSARN
     C                   MOVEL     GUAVAL        XGUAVAL
     C                   MOVEL     GUFILE        XGUFILE
     C                   MOVEL     GUTIME        XGUTIME
     C                   MOVEL     GUEVNT        XGUEVNT
     C                   MOVEL     GUSEQ         XGUSEQ
     C     GUACTC        IFEQ      'I'
     C                   MOVEL     GUUNQI        XGUUNQI
     C                   MOVEL     GULCD         XGULCD
     C                   MOVEL     GULCT         XGULCT
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR                                                  ** *PSSR SR **
      *
     C     @RUN          IFEQ      *BLANK
     C                   MOVE      'Y'           @RUN              1
     C                   DUMP
     C                   END
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
      ********************************************************************
