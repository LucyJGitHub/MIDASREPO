     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2004')
      *****************************************************************
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP1512 - Process Action File for SC/GPTRIGPD                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CUP029 *REWRITE    Date 20Oct04               *
      *                        *MOVED                                 *
      *                 CUP022 *CREATE     Date 03Mar04               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP029 - Moved from zone to global.                          *
      *           SCTRIGPD and GPTRIGPD share action file utilities.  *
      *           Change key Unique Id to file, time, event, seq.     *
      *  CUP022 - Creation of new set of Action File utilities.       *
      *                                                               *
      *****************************************************************
      * Function of indicators
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write, update or delete fail
      *
     FUPTRIG2L0 IF   E           K DISK    INFSR(*PSSR)
     FGPTRIGL0  UF A E           K DISK    INFSR(*PSSR)
     F                                     RENAME(SCTRIGD0:TRIGF) PREFIX(X)
      /EJECT
      *
     D LDA           E DS           256    EXTNAME(LDA)
      *
      * Local data area for database error details
      * *LOCK IN LDA immediately before and OUT LDA immediately
      * after each database error handling.
      *
      *                                     134 141 DBFILE
      *        Defines:                     142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *
     D PSDS           SDS
      *
      ** Program Status Data Structure.
      *
     D  PGM              *PROC
     D  WSID                 244    253
     D  USER                 254    263
      *
     C     COMP          KLIST
     C                   KFLD                    TRFILE
     C                   KFLD                    TRTIME
     C                   KFLD                    TREVNT
     C                   KFLD                    TRSEQ
      *
     C     *LOVAL        SETLL     UPTRIG2L0
      *
     C                   READ      UPTRIG2L0                            6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C     *IN20         DOWEQ     '0'
      *
     C     TRACTC        IFEQ      'I'
     C                   EXSR      SETVAL
     C                   WRITE     TRIGF                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
     C                   ENDIF
      *
     C     TRACTC        IFEQ      'D'
     C     COMP          CHAIN     GPTRIGL0                           3069
     C     *IN69         IFEQ      *ON
     C     *IN30         OREQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
      *
     C                   ELSE
     C                   DELETE    TRIGF                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
      *
      * If Action code = AMEND then read next record.
     C     TRACTC        IFEQ      'A'
      *
     C                   MOVEL     TRFILE        FILEX            10
     C                   MOVEL     TRTIME        TIMEX             1
     C                   MOVEL     TREVNT        EVNTX             1
     C                   MOVEL     TRSEQ         SEQX              2
     C                   READ      UPTRIG2L0                            6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
      * If key fields do not match exit
      *  - or if EOF indicator is on.
     C     TRFILE        IFNE      FILEX
     C     TRTIME        ORNE      TIMEX
     C     TREVNT        ORNE      EVNTX
     C     TRSEQ         ORNE      SEQX
     C     *IN20         OREQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C     COMP          CHAIN     GPTRIGL0                           3069
     C     *IN69         IFEQ      *ON
     C     *IN30         OREQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
      *
     C                   ELSE
      *
     C                   EXSR      SETVAL
     C                   UPDATE    TRIGF                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
      *
     C                   READ      UPTRIG2L0                            6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C                   ENDDO
      *
     C     ENDPGM        TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      *****************************************************************
      *                                                               *
      * SETVAL subroutine - set values for deletes/writes/updates     *
      *                                                               *
      *****************************************************************
     C     SETVAL        BEGSR
      *
      * Set values to update record.
     C                   MOVEL     TRACTV        XTRACTV
     C                   MOVEL     TRFILE        XTRFILE
     C                   MOVEL     TRTIME        XTRTIME
     C                   MOVEL     TREVNT        XTREVNT
     C                   MOVEL     TRSEQ         XTRSEQ
     C                   MOVEL     TRPGM         XTRPGM
     C                   MOVEL     TRPGML        XTRPGML
     C                   MOVEL     TRMMSN        XTRMMSN
     C     TRACTC        IFEQ      'I'
     C                   MOVEL     TRUNQI        XTRUNQI
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR                                                  ** *PSSR SR **
      *
     C     @RUN          IFEQ      *BLANK
     C                   MOVE      'Y'           @RUN              1
     C                   DUMP
     C                   END
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
      ********************************************************************
