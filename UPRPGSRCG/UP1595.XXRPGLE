     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2004')
      *****************************************************************
/*X*IA***OVRDBF*FILE(CTRY1)*TOFILE(UPCTRYTPD)**************************                       CUP021
/*X*IB***OVRDBF*FILE(CTRY2)*TOFILE(UPCTRYTPD)**************************                       CUP021
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP1595 - Build Action File for GPCTRYPD                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CUP022 *CREATE     Date 22Mar04               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP022 - Creation of new set of Action File utilities.       *
      *                                                               *
      *****************************************************************
      * Function of indicators
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write or chain fail
      *       70 - 79 Indicators for changed fields
      *
     FCTRY1     IF   E             DISK    INFSR(*PSSR)
     FCTRY2     IF   E           K DISK    INFSR(*PSSR)
     F                                     RENAME(GPCTRYD0:CTRYF) PREFIX(X)
     FUPCTRYPD  O    E             DISK    INFSR(*PSSR)
      *
      /EJECT
     D LDA           E DS           256    EXTNAME(LDA)
      *
      * Local data area for database error details
      * *LOCK IN LDA immediately before and OUT LDA immediately
      * after each database error handling.
      *
      *                                     134 141 DBFILE
      *       Defines:                      142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *
     D PSDS           SDS
      *
      * Program Status Data Structure.
      *
     D  PGM              *PROC
     D  WSID                 244    253
     D  USER                 254    263
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    MODE              1
      *
     C     COMP          KLIST
     C                   KFLD                    A4CNCD
      *
     C                   READ      CTRY1                                6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C     *IN20         DOWEQ     '0'
     C     COMP          CHAIN     CTRYF                              3069
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * If record not found then record has been deleted or added
      *  depending on mode.
     C     *IN30         IFEQ      *ON
     C                   MOVE      MODE          A4ACTC
     C                   WRITE     UPCTRYD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
      * Need to find differences but this should only be run once;
      *  run only if mode is 'Delete'.
     C     MODE          IFEQ      'D'
      *
      * If record found then look for any differences; only look in
      *  fields that have data all of the time.
     C     *IN30         IFEQ      *OFF
     C                   MOVE      'A'           A4ACTC
      *
     C     A4CNCD        IFNE      XA4CNCD
     C                   SETON                                        70
     C                   ENDIF
     C     A4RGCD        IFNE      XA4RGCD
     C                   SETON                                        71
     C                   ENDIF
     C     A4CNNM        IFNE      XA4CNNM
     C                   SETON                                        72
     C                   ENDIF
     C     A4MLOC        IFNE      XA4MLOC
     C                   SETON                                        73
     C                   ENDIF
     C     A4BBAN        IFNE      XA4BBAN
     C                   SETON                                        74
     C                   ENDIF
     C     A4BBRL        IFNE      XA4BBRL
     C                   SETON                                        75
     C                   ENDIF
     C     A4CTAY        IFNE      XA4CTAY
     C                   SETON                                        76
     C                   ENDIF
     C     A4CTZP        IFNE      XA4CTZP
     C                   SETON                                        77
     C                   ENDIF
     C     A4CTWC        IFNE      XA4CTWC
     C                   SETON                                        78
     C                   ENDIF
     C     A4CUSR        IFNE      XA4CUSR
     C                   SETON                                        79
     C                   ENDIF
      *
     C                   MOVEA     *IN(70)       INTEST           10
     C                   MOVE      INTEST        INTNUM           10 0
      * If any indicators are set on then write record.
     C     INTNUM        IFNE      0
     C                   WRITE     UPCTRYD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * Set values to write second record.
      *
     C                   MOVEL     XA4RGCD       A4RGCD
      *
     C                   MOVEL     XA4CNNM       A4CNNM
      *
     C                   MOVEL     XA4MLOC       A4MLOC
      *
     C                   MOVEL     XA4BBAN       A4BBAN
      *
     C                   MOVEL     XA4BBRL       A4BBRL
      *
     C                   MOVEL     XA4CTAY       A4CTAY
      *
     C                   MOVEL     XA4CTZP       A4CTZP
      *
     C                   MOVEL     XA4CTWC       A4CTWC
      *
     C                   MOVEL     XA4CUSR       A4CUSR
      *
     C                   WRITE     UPCTRYD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
     C                   ENDIF
      *
      * Read next record
      *
     C                   MOVEA     '0000000000'  *IN(70)
     C                   MOVEA     '0000000000'  *IN(80)
     C                   READ      CTRY1                                6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C                   ENDDO
      *
     C     ENDPGM        TAG
     C                   SETON                                        LR
     C                   RETURN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR                                                  ** *PSSR SR **
      *
     C     @RUN          IFEQ      *BLANK
     C                   MOVE      'Y'           @RUN              1
     C                   DUMP
     C                   END
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
      *
      ********************************************************************
