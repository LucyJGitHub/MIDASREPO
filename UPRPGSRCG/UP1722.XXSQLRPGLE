     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2004')
      *****************************************************************
/*S*D****RPGSQLBND*****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP1722 - Process Action File for T_GRMENUG                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CUP031 *CREATE     Date 16Dec04               *
      *                                                               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP031 - Creation of new set of Action File utilities.       *
      *                                                               *
      *****************************************************************
      *
     D PSDS           SDS
      *
      ** Program Status Data Structure.
      *
     D  PGM              *PROC
     D  WSID                 244    253
     D  USER                 254    263
      *
     DACTFDS         E DS                  EXTNAME(UPGMUGPD)  PREFIX(X)
      *
     DDeletedInd       S              2B 0                                      Ind. var. MGDELETED
      *
     C/EXEC SQL WHENEVER SQLERROR GO TO TAGSQLERR
     C/END-EXEC
      *
      * Set pointer for retrieving record from action file.
     C/EXEC SQL DECLARE ActionF_crsr CURSOR FOR
     C+  Select *
     C+  from UPGMUGPD
     C+  Order by MGACTC, MGID
     C/END-EXEC
      *
      * Open Action file.
     C/EXEC SQL
     C+  Open ActionF_crsr
     C/END-EXEC
      *
      * Get record from Action file into fields prefixed with X.
     C/EXEC SQL
     C+  Fetch ActionF_crsr  into
     C+    :XMGACTC,
     C+    :XMGID,
     C+    :XMGHYID,
     C+    :XMGNAME,
     C+    :XMGDELETED:DeletedInd
     C/END-EXEC
      *
      * Process while there is no error or not EOF.
     C                   DOW       SQLCOD =  0
      *
     C                   IF        XMGACTC = 'I'
      *
      * If insert, insert record into T_GRMENUG overriding the system
      *    generated value of field MHID with XMHID.
     C/EXEC SQL
     C+ Insert into T_GRMENUG (
     C+ MGID,
     C+ MGHYID,
     C+ MGNAME,
     C+ MGDELETED
     C+ )
     C+ OVERRIDING SYSTEM VALUE
     C+ values(
     C+ :XMGID,
     C+ :XMGHYID,
     C+ :XMGNAME,
     C+ :XMGDELETED:DeletedInd
     C+ )
     C/END-EXEC
      *
     C                   ENDIF                                                  FI Insert
      *
      * If Delete, delete record from file T_GRMENUG.
     C                   IF        XMGACTC = 'D'
      *
     C/EXEC SQL
     C+ Delete from T_GRMENUG
     C+ where MGID = :XMGID
     C/END-EXEC
      *
      * Error if no record found, run *PSSR.
     C                   IF        SQLCOD = 100                                 Record not found
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C                   ENDIF                                                  FI Delete
      *
      * If Action code = AMEND then read next record.
     C                   IF        XMGACTC = 'A'
      *
     C                   MOVEL     XMGID         IDX              15 0
      *
      * Get next record from Action file.
     C/EXEC SQL
     C+  Fetch ActionF_crsr  into
     C+    :XMGACTC,
     C+    :XMGID,
     C+    :XMGHYID,
     C+    :XMGNAME,
     C+    :XMGDELETED:DeletedInd
     C/END-EXEC
      *
      * If key fields of the next record from UPGMUGPD do not match or
      * no more record, run *PSSR.
     C                   IF        XMGID <> IDX  OR
     C                             SQLCOD = 100
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * Update T_GRMENUG with values from UPGMUGPD record.
     C/EXEC SQL
     C+ Update T_GRMENUG  set
     C+    MGHYID      = :XMGHYID,
     C+    MGNAME      = :XMGNAME,
     C+    MGDELETED   = :XMGDELETED:DeletedInd
     C+ where MGID  = :XMGID
     C/END-EXEC
      *
      * Error if no record found in _T_GRMENUG
     C                   IF        SQLCOD = 100                                 Record not found
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C                   ENDIF                                                  FI Amend
      *
      * Get next record from Action file
     C/EXEC SQL
     C+  Fetch ActionF_crsr  into
     C+    :XMGACTC,
     C+    :XMGID,
     C+    :XMGHYID,
     C+    :XMGNAME,
     C+    :XMGDELETED:DeletedInd
     C/END-EXEC
      *
     C                   ENDDO                                                  ODW SQLCOD = 0
      *
     C/EXEC SQL
     C+  Close  ActionF_crsr
     C/END-EXEC
      *
     C                   SETON                                        LR
     C                   RETURN
      *
     C     TAGSQLERR     TAG
      *
     C                   EXSR      *PSSR
      *
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR                                                  ** *PSSR SR **
      *
     C     @RUN          IFEQ      *BLANK
     C                   MOVE      'Y'           @RUN              1
     C                   DUMP
     C                   END
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
