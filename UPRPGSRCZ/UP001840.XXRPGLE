     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2010')
      *****************************************************************
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP001840 - Process Action File for CBRAMEPD                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      *  Prev Amend No. BUG27914*CREATE    Date 21Jul10               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  BUG27914 - RAME Design Changes                               *
      *                                                               *
      *****************************************************************
      *                                                               *
      * Function of indicators                                        *
      *                                                               *
      *       20      End of file                                     *
      *       30      Chain - record not found                        *
      *       69      Read, write, update or delete fail              *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E  I N D E X                               *
      *                                                               *
      * *PSSR - Error processing                                      *
      * *INZSR - Initialise                                           *
      *                                                               *
      *    The *INZSR subroutine will only get called automatically   *
      *    on entry to the module the first time it is run            *
      *    (unless you end the program with LR on).  Similarly        *
      *    D-spec initialisation only happens the first time.  Use    *
      *    RESET for subsequent passes.                               *
      *                                                               *
      *****************************************************************
      ** +--------------------------------------+
      ** ¦ F-specs                              ¦
      ** ¦ =======                              ¦
      ** +--------------------------------------+
      **
      *
     FUPRAMEL0  IF   E           K DISK    INFSR(*PSSR)
     FCBRAMEL0  UF A E           K DISK    INFSR(*PSSR)
     F                                     RENAME(CBRAMED0:RAMEF) PREFIX(X)
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      ** +--------------------------------------+
      ** ¦ D-specs                              ¦
      ** ¦ =======                              ¦
      ** +--------------------------------------+
     D LDA           E DS           256    EXTNAME(LDA)
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
      ** The following /COPY line includes all the defined fields in the
      ** PSDS.  They have meaningful names, prefixed by 'PS'.
      *
     D/COPY ZACPYSRC,PSDS
      *
      ** Program Status Data Structure.
      *
      ** +--------------------------------------+
      ** ¦ Named constants                      ¦
      ** ¦ ===============                      ¦
      ** +--------------------------------------+
      *
      ** +--------------------------------------+
      ** ¦ Arrays and Data Structures           ¦
      ** ¦ ==========================           ¦
      ** +--------------------------------------+
      *
      ** +--------------------------------------+
      ** ¦ Declared variables                   ¦
      ** ¦ ==================                   ¦
      ** +--------------------------------------+
      *
     D @RUN            S              1
     D COTTX           S             10
     D CSEQX           S              5
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ** +--- Start of Main processing -----------------------------------+
      ** ¦                                                                ¦
      ** ¦ Initial processing is performed automatically: the *INZSR is   ¦
      ** ¦ executed at program activation.                                ¦
      ** ¦                                                                ¦
      ** +----------------------------------------------------------------+
      *
      *********************************************************************
      * MAIN PROCEDURE                                                    *
      *********************************************************************
      *
     C     *LOVAL        SETLL     UPRAMEL0
      *
     C                   READ      UPRAMEL0                             6920
     C                   IF        *IN69 = *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C                   DOW       *IN20 = '0'
      *
     C                   IF        REACTC = 'I'
     C                   EXSR      SETVAL
     C                   WRITE     RAMEF                                69
     C                   IF        *IN69 = *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
     C                   IF        REACTC = 'D'
     C     COMP          CHAIN     CBRAMEL0                           3069
     C                   IF        *IN69 = *ON OR
     C                             *IN30 = *ON
     C                   EXSR      *PSSR
      *
     C                   ELSE
     C                   DELETE    RAMEF                                69
     C                   IF        *IN69 = *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
      *
      ** If Action code = AMEND then read next record.
      *
     C                   IF        REACTC = 'A'
     C                   EVAL      COTTX = RECOTT
     C                   EVAL      CSEQX = RECSEQ
     C                   READ      UPRAMEL0                             6920
     C                   IF        *IN69 = *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      ** If key fields do not match exit
      **  - or if EOF indicator is on.
      *
     C                   IF        RECOTT <> COTTX AND
     C                             RECSEQ <> CSEQX OR
     C                             *IN20 = *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C     COMP          CHAIN     CBRAMEL0                           3069
     C                   IF        *IN69 = *ON OR
     C                             *IN30 = *ON
     C                   EXSR      *PSSR
      *
     C                   ELSE
      *
     C                   EXSR      SETVAL
     C                   UPDATE    RAMEF                                69
     C                   IF        *IN69 = *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
      *
     C                   READ      UPRAMEL0                             6920
     C                   IF        *IN69 = *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C                   ENDDO
      *
     C                   SETON                                        LR
     C                   RETURN
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * SETVAL subroutine - set values for deletes/writes/updates     *
      *                                                               *
      *****************************************************************
      *
     C     SETVAL        BEGSR
      *
      ** Set values to update record.
      *
     C                   EVAL      XRECOTT = RECOTT
     C                   EVAL      XRECSEQ = RECSEQ
     C                   EVAL      XREFLAG = REFLAG
      *
     C                   ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *INZSR - Program Initialisation routine                       *
      *                                                               *
      * Called by: Implicitly on program activation                   *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *INZSR        BEGSR
      *
     C     COMP          KLIST
     C                   KFLD                    RECOTT
     C                   KFLD                    RECSEQ
      *
     C                   ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR
      *
     C                   IF        @RUN = *BLANK
     C                   EVAL      @RUN = 'Y'
     C                   DUMP
      *
     C                   ENDIF
      *
     C                   EVAL      *INU7 = *ON
     C                   EVAL      *INU8 = *ON
     C                   EVAL      *INLR = *ON
     C                   RETURN
      *
     C                   ENDSR
      *
