     H        1
      *****************************************************************
/*S*D****RPGBASE*******************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade module                                       *
      *                                                               *
      *  UP0802 - Process CoB Component changes                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.03 ----------------------------------------*
      *  Prev Amend No. CPK015             Date 09Dec02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CUP004 *CREATE     Date 23Mar98               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CPK015  -  Improvements to Action Files.                     *
      *  CUP004  -  Make upgrade of DTALIB files easier               *
      *                                                               *
      *****************************************************************
      * ID F  C  H  L    FUNCTION OF INDICATORS
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write, update or delete fail
      *
     FUPCMPNL0IF  E           K        DISK         KINFSR *PSSR
     FCBCMPNL1UF  E           K        DISK         KINFSR *PSSR A
     F            @DHREGH                           KRENAMECMPNF
      *
      /EJECT
      * Array containing Copyright statement
     E                    CPY@    1   1 80
      *
      *
      * Fields with the same name will need to be renamed.
     ICMPNF
     I              DHCOTT                          COTT
     I              DHCSEQ                          CSEQ
     I              DHCSTS                          CSTS
     I              DHCSHP                          CSHP
     I              DHCEPY                          CEPY
     I              DHCTSL                          CTSL
     I              DHCFOB                          CFOB
     I              DHCTXT                          CTXT
     I              DHCPRM                          CPRM
     I              DHCRQD                          CRQD
     I              DHCMOD                          CMOD
     I              DHCEMI                          CEMI
     I              DHCRES                          CRES
     I              DHCODP                          CODP
     I              DHCMEF                          CMEF
     I              DHCFRQ                          CFRQ
     I              DHCSDE                          CSDE
     I              DHCSTI                          CSTI
     I              DHCEDE                          CEDE
     I              DHCETI                          CETI
     I              DHCHTB                          CHTB
     I              DHCHTA                          CHTA
     I              DHCFAL                          CFAL
     I              DHEODT                          EODT
     I              DHEOMT                          EOMT
     I              DHBOMT                          BOMT
     I              DHEOYT                          EOYT
     I              DHEODC                          EODC
     I              DHEOMC                          EOMC
     I              DHBOMC                          BOMC
     I              DHEOYC                          EOYC
     I              DHABIC                          ABIC
     I              DHCBSN                          CBSN
     I              DHTASK                          TASK
     I              DHTSNO                          TSNO
      *
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C************ENTRY    PLIST                                                              CPK015
     C************         PARM           AMEND   4                                           CPK015
      *
     C           COMP      KLIST
     C                     KFLD           DHCOTT
     C                     KFLD           DHCSEQ
      *
     C           *LOVAL    SETLLUPCMPNL0
      *
     C                     READ UPCMPNL0               6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
     C           *IN20     DOWEQ'0'
      *
     C           DHACTC    IFEQ 'I'
     C                     EXSR SETVAL
     C                     WRITECMPNF                  69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     ENDIF
      *
     C           DHACTC    IFEQ 'D'
     C           COMP      CHAINCBCMPNL1             3069
     C           *IN69     IFEQ *ON
     C           *IN30     OREQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
      *
     C                     ELSE
     C                     EXSR SETVAL
     C                     DELETCMPNF                  69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
      * If Action code = AMEND then read next record
     C           DHACTC    IFEQ 'A'
     C                     MOVELDHCOTT    COTTX  10
     C                     MOVELDHCSEQ    CSEQX   5
     C                     READ UPCMPNL0               6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
      * If Component and sequence number do not match exit
      *  - or if EOF indicator is on
     C           DHCOTT    IFNE COTTX
     C           DHCSEQ    ANDNECSEQX
     C           *IN20     OREQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
      **Only*process*amended*records*if*requested*                                            CPK015
     C***********AMEND     IFEQ '*YES'                                                        CPK015
     C           COMP      CHAINCBCMPNL1             3069
     C           *IN69     IFEQ *ON
     C           *IN30     OREQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
      *
     C                     ELSE
      *
     C                     EXSR SETVAL
     C                     UPDATCMPNF                  69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     ENDIF
     C***********          ENDIF                                                              CPK015
      *
     C                     ENDIF
      *
     C                     READ UPCMPNL0               6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           ENDPGM    TAG
     C                     SETON                     LR
     C                     RETRN
      *
      *****************************************************************
      *
      * SETVAL subroutine - set values for deletes/writes/updates
      *
      *****************************************************************
     C           SETVAL    BEGSR
      * Set values to update record
     C                     MOVELDHCOTT    COTT
     C                     MOVELDHCSEQ    CSEQ
     C                     Z-ADDDHCSHP    CSHP
     C***********          Z-ADDDHCEPY    CEPY                                                CPK015
     C***********          Z-ADDDHCTSL    CTSL                                                CPK015
     C                     MOVELDHCFOB    CFOB
     C                     MOVELDHCTXT    CTXT
     C                     MOVELDHCPRM    CPRM
     C                     MOVELDHCMOD    CMOD
     C                     MOVELDHCEMI    CEMI
     C                     MOVELDHCMEF    CMEF
     C                     MOVELDHCFRQ    CFRQ
     C                     Z-ADDDHEODT    EODT
     C                     Z-ADDDHEOMT    EOMT
     C                     Z-ADDDHBOMT    BOMT
     C                     Z-ADDDHEOYT    EOYT
     C                     MOVELDHABIC    ABIC
     C                     MOVELDHTASK    TASK
     C                     MOVELDHTSNO    TSNO
      * Only set values for some fields if insert                                             CPK015
     C           DHACTC    IFEQ 'I'                                                           CPK015
     C                     Z-ADDDHCEPY    CEPY                                                CPK015
     C                     Z-ADDDHCTSL    CTSL                                                CPK015
     C                     ENDIF                                                              CPK015
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
