     H        1
      *****************************************************************
/*X*IA***OVRDBF*FILE(DPRL1)*TOFILE(UPDPRLTPD)**************************                CUP019 CUP021
/*X*IB***OVRDBF*FILE(DPRL2)*TOFILE(UPDPRLTPD)**************************                CUP019 CUP021
/*S*D****RPGBASE*******************************************************                       CUP021
/*O*R ***OVRDBF*DPRL1*CBDPRLL5*****************************************                       CUP019
/*O*R ***OVRDBF*DPRL2*CBDPRLL5*****************************************                       CUP019
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade module                                       *
      *                                                               *
      *  UP0800 - Compare two CoB dependency files                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.02 ----------------------------------------*
      *  Prev Amend No. CUP019             Date 10Sep02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CUP004             Date 16Mar98               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP019 - Use templates for Action Files.                     *
      *  CUP004  -  Make upgrade of DTALIB files easier               *
      *                                                               *
      *****************************************************************
      * ID F  C  H  L    FUNCTION OF INDICATORS
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write or chain fail
      *       70 - 85 Indicators for changed fields
      *
     FDPRL1   IF  E           K        DISK         KINFSR *PSSR
     FDPRL2   IF  E           K        DISK         KINFSR *PSSR
     F********    @DPRLL5                           KRENAMEDPRLF                              CUP019
     F            CBDPRLD0                          KRENAMEDPRLF                              CUP019
     FUPDPRLPDO   E                    DISK         KINFSR *PSSR
      *
      /EJECT
     E                    CPY@    1   1 80
      *
      * Fields with the same name will need to be renamed.
      *
     IDPRLF
     I              DIDSON                          DSON
     I              DIDSOS                          DSOS
     I              DIDRUN                          DRUN
     I              DIDDON                          DDON
     I              DIDDOS                          DDOS
      *
     I            DS
     I                                        1   30UNQNO
     I                                        1   3 UNQNOA
      *
      ** Array containing Copyright statement
      *
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *ENTRY    PLIST
     C                     PARM           MODE    1
      *
     C           COMP      KLIST
     C                     KFLD           DIDSON
     C                     KFLD           DIDSOS
     C                     KFLD           DIDDON
     C                     KFLD           DIDDOS
      *
     C           *LOVAL    SETLLDPRL1
     C                     READ DPRL1                  6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           *IN20     DOWEQ'0'
     C           COMP      CHAINDPRLF                3069
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * If record not found then record has been deleted or added
      *  depending on mode
     C           *IN30     IFEQ *ON
     C                     MOVE MODE      DIACTC
      * If Insert then check if component and sequence are same;
      *  - if they are add 1 to DIDRUN, if not reset to 900
     C           MODE      IFEQ 'I'
     C           DIDSON    IFEQ DSON1
     C           DIDSOS    ANDEQDSOS1
     C                     ADD  1         UNQNO
     C                     ELSE
     C                     Z-ADD900       UNQNO
     C                     ENDIF
     C                     MOVE UNQNOA    DIDRUN
     C                     ENDIF
     C                     WRITEUPDPRLD0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      * Store values for next write if mode is 'Insert'
     C           MODE      IFEQ 'I'
     C                     MOVELDIDSON    DSON1  10
     C                     MOVELDIDSOS    DSOS1   5
     C                     ENDIF
     C                     ENDIF
      *
     C                     READ DPRL1                  6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           ENDPGM    TAG
     C                     SETON                     LR
     C                     RETRN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      ************ CALL DBERRCTL IF INTERACTIVE PROGRAM ***************
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
