     H        1
      *****************************************************************
/*S*D****RPGBASE*******************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade module                                       *
      *                                                               *
      *  UP0813 - Process CoB Mut. Excl. file changes                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. CUP033             Date 07Nov06               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CUP099             Date 19May05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CUP004 *CREATE     Date 01Apr98               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP033 - Unique number is 2 long not 3                       *
      *  CUP099 - Make handling of 'unique no.' easier.               *
      *  CUP004  -  Make upgrade of DTALIB files easier               *
      *                                                               *
      *****************************************************************
      * ID F  C  H  L    FUNCTION OF INDICATORS
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write, update or delete fail
      *
     FUPMEXCL0IF  E           K        DISK         KINFSR *PSSR
     FCBMEXCL3UF  E           K        DISK         KINFSR *PSSR A
     F            @MEXCL3                           KRENAMEMEXCF
      *
      /EJECT
      * Array containing Copyright statement
     E                    CPY@    1   1 80
      *
      *
      * Fields with the same name will need to be renamed.
     IMEXCF
     I              DKCOTT                          COTT
     I              DKCSEQ                          DSOS
     I              DKMEUN                          MEUN
     I              DKMECN                          MECN
     I              DKMECS                          MECS
      *
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
     I            DS                                                                          CUP099
     I**********                              1   3 WUNQA                              CUP099 CUP033
     I**********                              1   30WUNQN                              CUP099 CUP033
     I                                        1   2 WUNQA                                     CUP033
     I                                        1   20WUNQN                                     CUP033
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           COMP      KLIST
     C                     KFLD           DKCOTT
     C                     KFLD           DKCSEQ
     C                     KFLD           DKMECN
     C                     KFLD           DKMECS
      *
      * Initialise unique number to maximum.                                                  CUP099
     C**********           MOVE '999'     WUNQA                                        CUP099 CUP033
     C                     MOVE '99'      WUNQA                                               CUP033
      *
     C           *LOVAL    SETLLUPMEXCL0
      *
     C                     READ UPMEXCL0               6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
     C           *IN20     DOWEQ'0'
      *
     C           DKACTC    IFEQ 'I'
     C                     EXSR SETVAL
     C                     WRITEMEXCF                  69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     ENDIF
      *
     C           DKACTC    IFEQ 'D'
     C           COMP      CHAINCBMEXCL3             3069
     C           *IN69     IFEQ *ON
     C           *IN30     OREQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
      *
     C                     ELSE
     C                     DELETMEXCF                  69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     READ UPMEXCL0               6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
     C                     ENDDO
      *
      * After Action File has been processed then call program to reset unique number.        CUP099
     C                     CALL 'UP0814'                                                      CUP099
      *
     C           ENDPGM    TAG
     C                     SETON                     LR
     C                     RETRN
      *
      *****************************************************************
      *
      * SETVAL subroutine - set values for deletes/writes/updates
      *
      *****************************************************************
     C           SETVAL    BEGSR
      * Set values to update record
     C                     MOVELDKCOTT    COTT
     C                     MOVELDKCSEQ    DSOS
     C***********          MOVELDKMEUN    MEUN                                                CUP099
     C                     MOVELWUNQA     MEUN                                                CUP099
     C                     MOVELDKMECN    MECN
     C                     MOVELDKMECS    MECS
      *
      * Set unique number.                                                                    CUP099
     C                     SUB  1         WUNQN                                               CUP099
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
