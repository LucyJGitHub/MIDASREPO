     H        1
      *****************************************************************
/*X*IA***OVRDBF*FILE(MDCN1)*TOFILE(UPMDCNTPD)**************************                CUP019 CUP021
/*X*IB***OVRDBF*FILE(MDCN2)*TOFILE(UPMDCNTPD)**************************                CUP019 CUP021
/*S*D****RPGBASE*******************************************************                       CUP021
/*O*R ***OVRDBF*MDCN1*CBMDCNL1*****************************************                       CUP019
/*O*R ***OVRDBF*MDCN2*CBMDCNL1*****************************************                       CUP019
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade module                                       *
      *                                                               *
      *  UP0815 - Compare two CoB mod cond files                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.02 ----------------------------------------*
      *  Prev Amend No. CUP019             Date 10Sep02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CUP004             Date 06Apr98               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP019 - Use templates for Action Files.                     *
      *  CUP004  -  Make upgrade of DTALIB files easier               *
      *                                                               *
      *****************************************************************
      * ID F  C  H  L    FUNCTION OF INDICATORS
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write or chain fail
      *       70 - 89 Indicators for changed fields
      *
     FMDCN1   IF  E           K        DISK         KINFSR *PSSR
     FMDCN2   IF  E           K        DISK         KINFSR *PSSR
     F********    @DJREGN                           KRENAMEMDCNF                              CUP019
     F            CBMDCND0                          KRENAMEMDCNF                              CUP019
     FUPMDCNPDO   E                    DISK         KINFSR *PSSR
      *
      /EJECT
     E                    CPY@    1   1 80
      *
      * Fields with the same name will need to be renamed.
      *
     IMDCNF
     I              DJCOTT                          COTT
     I              DJCSEQ                          CSEQ
     I              DJMCUN                          MCUN
     I              DJMD01                          MD01
     I              DJMP01                          MP01
     I              DJMD02                          MD02
     I              DJMP02                          MP02
     I              DJMD03                          MD03
     I              DJMP03                          MP03
     I              DJMD04                          MD04
     I              DJMP04                          MP04
     I              DJMD05                          MD05
     I              DJMP05                          MP05
     I              DJMD06                          MD06
     I              DJMP06                          MP06
     I              DJMD07                          MD07
     I              DJMP07                          MP07
     I              DJMD08                          MD08
     I              DJMP08                          MP08
     I              DJMD09                          MD09
     I              DJMP09                          MP09
     I              DJMD10                          MD10
     I              DJMP10                          MP10
      *
     I            DS
     I                                        1   20UNQNO
     I                                        1   2 UNQNOA
      *
      ** Array containing Copyright statement
      *
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *ENTRY    PLIST
     C                     PARM           MODE    1
      *
     C           COMP      KLIST
     C                     KFLD           DJCOTT
     C                     KFLD           DJCSEQ
     C                     KFLD           DJMCUN
      *
     C           *LOVAL    SETLLMDCN1
     C                     READ MDCN1                  6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           *IN20     DOWEQ'0'
     C           COMP      CHAINMDCNF                3069
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * If record not found then record has been deleted or added
      *  depending on mode
     C           *IN30     IFEQ *ON
     C                     MOVE MODE      DJACTC
      * If Insert then check if component and sequence are same;
      *  - if they are add 1 to DJMCUN, if not reset to 90
     C           MODE      IFEQ 'I'
     C           DJCOTT    IFEQ COTT1
     C           DJCSEQ    ANDEQCSEQ1
     C                     ADD  1         UNQNO
     C                     ELSE
     C                     Z-ADD90        UNQNO
     C                     ENDIF
     C                     MOVE UNQNOA    DJMCUN
     C                     ENDIF
     C                     WRITEUPMDCND0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      * Store values for next write if mode is 'Insert'
     C           MODE      IFEQ 'I'
     C                     MOVELDJCOTT    COTT1  10
     C                     MOVELDJCSEQ    CSEQ1   5
     C                     ENDIF
     C                     ENDIF
      *
      * Need to find differences but this should only be run once;
      *  run only if mode is 'Delete'
     C           MODE      IFEQ 'D'
      *
      * If record found then look for any differences; only look in
      *  fields that have data all of the time.
     C           *IN30     IFEQ *OFF
     C                     MOVE 'A'       DJACTC
      * Module 01
     C           DJMD01    IFNE MD01
     C                     SETON                     70
     C                     ENDIF
     C           DJMP01    IFNE MP01
     C                     SETON                     71
     C                     ENDIF
      *
      * Module 02
     C           DJMD02    IFNE MD02
     C                     SETON                     72
     C                     ENDIF
     C           DJMP02    IFNE MP02
     C                     SETON                     73
     C                     ENDIF
      *
      * Module 03
     C           DJMD03    IFNE MD03
     C                     SETON                     74
     C                     ENDIF
     C           DJMP03    IFNE MP03
     C                     SETON                     75
     C                     ENDIF
      *
      * Module 04
     C           DJMD04    IFNE MD04
     C                     SETON                     76
     C                     ENDIF
     C           DJMP04    IFNE MP04
     C                     SETON                     77
     C                     ENDIF
      *
      * Module 05
     C           DJMD05    IFNE MD05
     C                     SETON                     78
     C                     ENDIF
     C           DJMP05    IFNE MP05
     C                     SETON                     79
     C                     ENDIF
      *
      * Module 06
     C           DJMD06    IFNE MD06
     C                     SETON                     80
     C                     ENDIF
     C           DJMP06    IFNE MP06
     C                     SETON                     81
     C                     ENDIF
      *
      * Module 07
     C           DJMD07    IFNE MD07
     C                     SETON                     82
     C                     ENDIF
     C           DJMP07    IFNE MP07
     C                     SETON                     83
     C                     ENDIF
      *
      * Module 08
     C           DJMD08    IFNE MD08
     C                     SETON                     84
     C                     ENDIF
     C           DJMP08    IFNE MP08
     C                     SETON                     85
     C                     ENDIF
      *
      * Module 09
     C           DJMD09    IFNE MD09
     C                     SETON                     86
     C                     ENDIF
     C           DJMP09    IFNE MP09
     C                     SETON                     87
     C                     ENDIF
      *
      * Module 10
     C           DJMD10    IFNE MD10
     C                     SETON                     88
     C                     ENDIF
     C           DJMP10    IFNE MP10
     C                     SETON                     89
     C                     ENDIF
      *
      *
     C                     MOVEA*IN,70    INTEST 20
     C                     MOVE INTEST    INTNUM 200
      * If any indicators are set on then write record
     C           INTNUM    IFNE 0
     C                     WRITEUPMDCND0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * Set values to write second record
     C                     MOVELMD01      DJMD01
     C                     MOVELMP01      DJMP01
      *
     C                     MOVELMD02      DJMD02
     C                     MOVELMP02      DJMP02
      *
     C                     MOVELMD03      DJMD03
     C                     MOVELMP03      DJMP03
      *
     C                     MOVELMD04      DJMD04
     C                     MOVELMP04      DJMP04
      *
     C                     MOVELMD05      DJMD05
     C                     MOVELMP05      DJMP05
      *
     C                     MOVELMD06      DJMD06
     C                     MOVELMP06      DJMP06
      *
     C                     MOVELMD07      DJMD07
     C                     MOVELMP07      DJMP07
      *
     C                     MOVELMD08      DJMD08
     C                     MOVELMP08      DJMP08
      *
     C                     MOVELMD09      DJMD09
     C                     MOVELMP09      DJMP09
      *
     C                     MOVELMD10      DJMD10
     C                     MOVELMP10      DJMP10
      *
      *
     C                     WRITEUPMDCND0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
      * Read next record
     C                     MOVEA'00000000'*IN,70
     C                     MOVEA'00000000'*IN,78
     C                     MOVEA'0000'    *IN,86
     C                     READ MDCN1                  6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           ENDPGM    TAG
     C                     SETON                     LR
     C                     RETRN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      ************ CALL DBERRCTL IF INTERACTIVE PROGRAM ***************
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
