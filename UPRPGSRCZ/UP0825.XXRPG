     H        1
      *****************************************************************
/*X*IA***OVRDBF*FILE(REPT1)*TOFILE(UPREPTTPD)**************************                CUP019 CUP021
/*X*IB***OVRDBF*FILE(REPT2)*TOFILE(UPREPTTPD)**************************                CUP019 CUP021
/*S*D****RPGBASE*******************************************************                       CUP021
/*O*R ***OVRDBF*REPT1*FCREPTL1*****************************************                       CUP019
/*O*R ***OVRDBF*REPT2*FCREPTL1*****************************************                       CUP019
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade module                                       *
      *                                                               *
      *  UP0825 - Compare two Report Description Files                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.02 ----------------------------------------*
      *  Prev Amend No. CUP019             Date 10Sep02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CUP004             Date 27Apr98               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP019 - Use templates for Action Files.                     *
      *  CUP004  -  Make upgrade of DTALIB files easier               *
      *                                                               *
      *****************************************************************
      * ID F  C  H  L    FUNCTION OF INDICATORS
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write or chain fail
      *       70 - 81 Indicators for changed fields
      *
     FREPT1   IF  E                    DISK         KINFSR *PSSR
     FREPT2   IF  E           K        DISK         KINFSR *PSSR
     F********    @DXREIK                           KRENAMEREPTF                              CUP019
     F            FCREPTD0                          KRENAMEREPTF                              CUP019
     FUPREPTPDO   E                    DISK         KINFSR *PSSR
      *
      /EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      * Fields with the same name will need to be renamed.
      *
     IREPTF
     I              DXRNAM                          RNAM
     I              DXLCD                           LCD
     I              DXCHTP                          CHTP
     I              DXCMOD                          CMOD
     I              DXRDSC                          RDSC
     I              DXGLVL                          GLVL
     I              DXCLVL                          CLVL
     I              DXBLVL                          BLVL
     I              DXIPCY                          IPCY
     I              DXCLBP                          CLBP
     I              DXPDFL                          PDFL
     I              DXDFTO                          DFTO
     I              DXMIDQ                          MIDQ
     I              DXALVL                          ALVL
     I              DXDLVL                          DLVL
      *
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *ENTRY    PLIST
     C                     PARM           MODE    1
      *
     C           COMP      KLIST
     C                     KFLD           DXRNAM
      *
     C                     READ REPT1                  6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           *IN20     DOWEQ'0'
     C           COMP      CHAINREPTF                3069
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * If record not found then record has been deleted or added
      *  depending on mode
     C           *IN30     IFEQ *ON
     C                     MOVE MODE      DXACTC
     C                     WRITEUPREPTD0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
      * Need to find differences but this should only be run once;
      *  run only if mode is 'Delete'
     C           MODE      IFEQ 'D'
      *
      * If record found then look for any differences; only look in
      *  fields that have data all of the time.
     C           *IN30     IFEQ *OFF
     C                     MOVE 'A'       DXACTC
      * Report name
     C           DXRNAM    IFNE RNAM
     C                     SETON                     70
     C                     ENDIF
      * Module
     C           DXCMOD    IFNE CMOD
     C                     SETON                     71
     C                     ENDIF
      * Report Description
     C           DXRDSC    IFNE RDSC
     C                     SETON                     72
     C                     ENDIF
      * Bank Level
     C           DXGLVL    IFNE GLVL
     C                     SETON                     73
     C                     ENDIF
      * Company Level
     C           DXCLVL    IFNE CLVL
     C                     SETON                     74
     C                     ENDIF
      * Branch Level
     C           DXBLVL    IFNE BLVL
     C                     SETON                     75
     C                     ENDIF
      * Account Officer Level
     C           DXALVL    IFNE ALVL
     C                     SETON                     76
     C                     ENDIF
      * Department Level
     C           DXDLVL    IFNE DLVL
     C                     SETON                     77
     C                     ENDIF
      * Input Cycle
     C           DXIPCY    IFNE IPCY
     C                     SETON                     78
     C                     ENDIF
      * COB
     C           DXCLBP    IFNE CLBP
     C                     SETON                     79
     C                     ENDIF
      * Periodic
     C           DXPDFL    IFNE PDFL
     C                     SETON                     80
     C                     ENDIF
      * Midas/Q
     C           DXMIDQ    IFNE MIDQ
     C                     SETON                     81
     C                     ENDIF
      *
     C                     MOVEA*IN,70    INTEST 12
     C                     MOVE INTEST    INTNUM 120
      * If any indicators are set on then write record
     C           INTNUM    IFNE 0
     C                     WRITEUPREPTD0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * Set values to write second record
     C                     MOVELRNAM      DXRNAM
      *
     C                     MOVELCMOD      DXCMOD
      *
     C                     MOVELRDSC      DXRDSC
      *
     C                     MOVELGLVL      DXGLVL
      *
     C                     MOVELCLVL      DXCLVL
      *
     C                     MOVELBLVL      DXBLVL
      *
     C                     MOVELALVL      DXALVL
      *
     C                     MOVELDLVL      DXDLVL
      *
     C                     MOVELIPCY      DXIPCY
      *
     C                     MOVELCLBP      DXCLBP
      *
     C                     MOVELPDFL      DXPDFL
      *
     C                     MOVELMIDQ      DXMIDQ
      *
     C                     WRITEUPREPTD0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
      * Read next record
     C                     MOVEA'00000000'*IN,70
     C                     MOVEA'0000'    *IN,78
     C                     READ REPT1                  6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           ENDPGM    TAG
     C                     SETON                     LR
     C                     RETRN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      ************ CALL DBERRCTL IF INTERACTIVE PROGRAM ***************
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
