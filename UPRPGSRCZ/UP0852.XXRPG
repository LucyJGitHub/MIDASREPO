     H        1
      *****************************************************************
/*S*D****RPGBASE*******************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade module                                       *
      *                                                               *
      *  UP0852 - Process Switchable Features Menu Item Changes       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSF002             Date 14Jun04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CUP004 *CREATE     Date 07May98               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CSF002 - Change of key for SCSARIPD.                         *
      *           Replace SCSARIL1 with SCSARIL4.                     *
      *  CUP004  -  Make upgrade of DTALIB files easier               *
      *                                                               *
      *****************************************************************
      * ID F  C  H  L    FUNCTION OF INDICATORS
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write, update or delete fail
      *
     FUPSARIL0IF  E           K        DISK         KINFSR *PSSR
     F*****SCSARIL1UF  E           K        DISK         KINFSR *PSSR A                       CSF002
     FSCSARIL4UF  E           K        DISK         KINFSR *PSSR A                            CSF002
     F************@SARIL1                           KRENAMESARIF                              CSF002
     F            SCSARID0                          KRENAMESARIF                              CSF002
      *
      /EJECT
      * Array containing Copyright statement
     E                    CPY@    1   1 80
      *
      * Fields with the same name will need to be renamed.
     ISARIF
     I              ADSARN                          SARN
     I              ADMGRP                          MGRP
     I              ADICDE                          ICDE
     I              ADRECI                          RECI
     I              ADAVAL                          AVAL
     I              ADINAR                          INAR
     I              ADINDX                          INDX                                      CSF002
     I              ADZONE                          ZONE                                      CSF002
      *
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           COMP      KLIST
     C                     KFLD           ADSARN
     C***********          KFLD           ADMGRP                                              CSF002
     C***********          KFLD           ADICDE                                              CSF002
     C                     KFLD           ADINDX                                              CSF002
      *
     C           *LOVAL    SETLLUPSARIL0
      *
     C                     READ UPSARIL0               6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
     C           *IN20     DOWEQ'0'
      *
     C           ADACTC    IFEQ 'I'
     C                     EXSR SETVAL
     C                     WRITESARIF                  69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     ENDIF
      *
     C           ADACTC    IFEQ 'D'
     C***********COMP      CHAINSCSARIL1             3069                                     CSF002
     C           COMP      CHAINSCSARIL4             3069                                     CSF002
     C           *IN69     IFEQ *ON
     C           *IN30     OREQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
      *
     C                     ELSE
     C*********************EXSR SETVAL                                                        CSF002
     C                     DELETSARIF                  69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
      * If Action code = AMEND then read next record
     C           ADACTC    IFEQ 'A'
     C                     MOVELADSARN    SARNX   6
     C***********          MOVELADMGRP    MGRPX   4                                           CSF002
     C***********          MOVELADICDE    ICDEX   4                                           CSF002
     C                     MOVELADINDX    INDXX   4                                           CSF002
     C                     READ UPSARIL0               6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
      * If SF number, cmpt name and cmpt seq no do not match exit
      *  - or if EOF indicator is on
     C           ADSARN    IFNE SARNX
     C***********ADMGRP    ANDNEMGRPX                                                         CSF002
     C***********ADICDE    ANDNEICDEX                                                         CSF002
     C           ADINDX    ANDNEINDXX                                                         CSF002
     C           *IN20     OREQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
     C***********COMP      CHAINSCSARIL1             3069                                     CSF002
     C           COMP      CHAINSCSARIL4             3069                                     CSF002
     C           *IN69     IFEQ *ON
     C           *IN30     OREQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
      *
     C                     ELSE
      *
     C                     EXSR SETVAL
     C                     UPDATSARIF                  69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     READ UPSARIL0               6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           ENDPGM    TAG
     C                     SETON                     LR
     C                     RETRN
      *
      *****************************************************************
      *
      * SETVAL subroutine - set values for deletes/writes/updates
      *
      *****************************************************************
     C           SETVAL    BEGSR
      * Set values to update record
     C                     MOVELADSARN    SARN
     C                     MOVELADMGRP    MGRP
     C                     MOVELADICDE    ICDE
     C                     MOVELADRECI    RECI
     C                     MOVELADAVAL    AVAL
     C                     MOVELADINAR    INAR
     C                     MOVELADINDX    INDX                                                CSF002
     C                     MOVELADZONE    ZONE                                                CSF002
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
