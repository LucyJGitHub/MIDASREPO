     H        1
      *****************************************************************
/*X*IA***OVRDBF*FILE(SARS1)*TOFILE(UPSARSTPD)**************************                CUP019 CUP021
/*X*IB***OVRDBF*FILE(SARS2)*TOFILE(UPSARSTPD)**************************                CUP019 CUP021
/*S*D****RPGBASE*******************************************************                       CUP021
/*O*R ***OVRDBF*SARS1*SCSARSL1*****************************************                       CUP019
/*O*R ***OVRDBF*SARS2*SCSARSL1*****************************************                       CUP019
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade module                                       *
      *                                                               *
      *  UP1015 - Compare two Switchable Features Details Files       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.02 ----------------------------------------*
      *  Prev Amend No. CUP019             Date 10Sep02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CUP004             Date 17Sep99               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP019 - Use templates for Action Files.                     *
      *  CUP004  -  Make upgrade of DTALIB items easier               *
      *                                                               *
      *****************************************************************
      * ID F  C  H  L    FUNCTION OF INDICATORS
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write or chain fail
      *       70 - 81 Indicators for changed fields
      *
     FSARS1   IF  E                    DISK         KINFSR *PSSR
     FSARS2   IF  E           K        DISK         KINFSR *PSSR
     F            SCSARSD0                          KRENAMESARSF
     FUPSARSPDO   E                    DISK         KINFSR *PSSR
      *
      /EJECT
     E                    CPY@    1   1 80
      *
      * Fields with the same name will need to be renamed.
      *
     ISARSF
     I              AXSARN                          SARN
     I              AXSARD                          SARD
     I              AXPRIM                          PRIM
     I              AXCEMI                          CEMI
     I              AXRELL                          RELL
     I              AXIREL                          IREL
     I              AXCLVL                          CLVL
     I              AXRSTS                          RSTS
     I              AXDFLT                          DFLT
     I              AXMNTB                          MNTB
     I              AXCHRG                          CHRG
     I              AXFLRN                          FLRN
     I              AXDOCN                          DOCN
     I              AXHELP                          HELP
      *
      ** Array containing Copyright statement
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *ENTRY    PLIST
     C                     PARM           MODE    1
      *
     C           COMP      KLIST
     C                     KFLD           AXSARN
      *
     C                     READ SARS1                  6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           *IN20     DOWEQ'0'
     C           COMP      CHAINSARSF                3069
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * If record not found then record has been deleted or added
      *  depending on mode
     C           *IN30     IFEQ *ON
     C                     MOVE MODE      AXACTC
     C                     WRITEUPSARSD0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
      * Need to find differences but this should only be run once;
      *  run only if mode is 'Delete'
     C           MODE      IFEQ 'D'
      *
      * If record found then look for any differences; only look in
      *  fields that have data all of the time.
     C           *IN30     IFEQ *OFF
     C                     MOVE 'A'       AXACTC
      * Record ID
     C           AXSARD    IFNE SARD
     C                     SETON                     70
     C                     ENDIF
      * Available if feature present
     C           AXPRIM    IFNE PRIM
     C                     SETON                     71
     C                     ENDIF
      * Menu item narrative
     C           AXCEMI    IFNE CEMI
     C                     SETON                     72
     C                     ENDIF
      * Record Identifier
     C           AXRELL    IFNE RELL
     C                     SETON                     73
     C                     ENDIF
      * Element Type
     C           AXIREL    IFNE IREL
     C                     SETON                     74
     C                     ENDIF
      * Formatting Options
     C           AXCLVL    IFNE CLVL
     C                     SETON                     75
     C                     ENDIF
      * Maximum Length
     C           AXRSTS    IFNE RSTS
     C                     SETON                     76
     C                     ENDIF
      * Example Data
     C           AXDFLT    IFNE DFLT
     C                     SETON                     77
     C                     ENDIF
      * Allow Derived Information
     C           AXMNTB    IFNE MNTB
     C                     SETON                     78
     C                     ENDIF
      * Derived Information Source
     C           AXCHRG    IFNE CHRG
     C                     SETON                     79
     C                     ENDIF
      * Derived Data Internal Reference
     C           AXFLRN    IFNE FLRN
     C                     SETON                     80
     C                     ENDIF
      * Start Position
     C           AXDOCN    IFNE DOCN
     C                     SETON                     81
     C                     ENDIF
      * End Position
     C           AXHELP    IFNE HELP
     C                     SETON                     82
     C                     ENDIF
      *
     C                     MOVEA*IN,70    INTEST 13
     C                     MOVE INTEST    INTNUM 130
      * If any indicators are set on then write record
     C           INTNUM    IFNE 0
     C                     WRITEUPSARSD0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * Set values to write second record
     C                     MOVELSARD      AXSARD
      *
     C                     MOVELPRIM      AXPRIM
      *
     C                     MOVELCEMI      AXCEMI
      *
     C                     MOVELRELL      AXRELL
      *
     C                     MOVELIREL      AXIREL
      *
     C                     MOVELCLVL      AXCLVL
      *
     C                     MOVELRSTS      AXRSTS
      *
     C                     MOVELDFLT      AXDFLT
      *
     C                     MOVELMNTB      AXMNTB
      *
     C                     MOVELCHRG      AXCHRG
      *
     C                     MOVELFLRN      AXFLRN
      *
     C                     MOVELDOCN      AXDOCN
      *
     C                     MOVELHELP      AXHELP
      *
     C                     WRITEUPSARSD0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
      * Read next record
     C                     MOVEA'00000000'*IN,70
     C                     MOVEA'00000'   *IN,78
     C                     READ SARS1                  6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           ENDPGM    TAG
     C                     SETON                     LR
     C                     RETRN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      ************ CALL DBERRCTL IF INTERACTIVE PROGRAM ***************
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
