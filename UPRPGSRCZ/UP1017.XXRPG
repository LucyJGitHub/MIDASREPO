     H        1
      *****************************************************************
/*S*D****RPGBASE*******************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade module                                       *
      *                                                               *
      *  UP1017 - Process Switchable Features Details Changes         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *  Prev Amend No. CUP004             Date 17Sep99               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP004  -  Make upgrade of DTALIB items easier               *
      *                                                               *
      *****************************************************************
      * ID F  C  H  L    FUNCTION OF INDICATORS
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write, update or delete fail
      *
     FUPSARSL0IF  E           K        DISK         KINFSR *PSSR
     FSCSARSL1UF  E           K        DISK         KINFSR *PSSR A
     F            SCSARSD0                          KRENAMESARSF
      *
      /EJECT
      * Array containing Copyright statement
     E                    CPY@    1   1 80
      *
      * Fields with the same name will need to be renamed.
     ISARSF
     I              AXSARN                          SARN
     I              AXSARD                          SARD
     I              AXPRIM                          PRIM
     I              AXCEMI                          CEMI
     I              AXRELL                          RELL
     I              AXIREL                          IREL
     I              AXCLVL                          CLVL
     I              AXRSTS                          RSTS
     I              AXDFLT                          DFLT
     I              AXMNTB                          MNTB
     I              AXCHRG                          CHRG
     I              AXFLRN                          FLRN
     I              AXDOCN                          DOCN
     I              AXHELP                          HELP
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           COMP      KLIST
     C                     KFLD           AXSARN
      *
     C           *LOVAL    SETLLUPSARSL0
      *
     C                     READ UPSARSL0               6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
     C           *IN20     DOWEQ'0'
      *
     C           AXACTC    IFEQ 'I'
     C                     EXSR SETVAL
     C                     WRITESARSF                  69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     ENDIF
      *
     C           AXACTC    IFEQ 'D'
     C           COMP      CHAINSCSARSL1             3069
     C           *IN69     IFEQ *ON
     C           *IN30     OREQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
      *
     C                     ELSE
     C                     EXSR SETVAL
     C                     DELETSARSF                  69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
      * If Action code = AMEND then read next record
     C           AXACTC    IFEQ 'A'
     C                     MOVELAXSARN    SARNX   6
     C                     READ UPSARSL0               6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
      * If SF number, cmpt name and cmpt seq no do not match exit
      *  - or if EOF indicator is on
     C           AXSARN    IFNE SARNX
     C           *IN20     OREQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
     C           COMP      CHAINSCSARSL1             3069
     C           *IN69     IFEQ *ON
     C           *IN30     OREQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
      *
     C                     ELSE
      *
     C                     EXSR SETVAL
     C                     UPDATSARSF                  69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     READ UPSARSL0               6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           ENDPGM    TAG
     C                     SETON                     LR
     C                     RETRN
      *
      *****************************************************************
      *
      * SETVAL subroutine - set values for deletes/writes/updates
      *
      *****************************************************************
     C           SETVAL    BEGSR
      * Set values to update record
     C                     MOVELAXSARN    SARN
     C                     MOVELAXSARD    SARD
     C                     MOVELAXPRIM    PRIM
     C                     MOVELAXCEMI    CEMI
     C                     MOVELAXRELL    RELL
     C                     MOVELAXIREL    IREL
     C                     MOVELAXCLVL    CLVL
     C                     MOVELAXRSTS    RSTS
     C                     MOVELAXDFLT    DFLT
     C                     MOVELAXMNTB    MNTB
     C                     MOVELAXCHRG    CHRG
     C                     MOVELAXFLRN    FLRN
     C                     MOVELAXDOCN    DOCN
     C                     MOVELAXHELP    HELP
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
