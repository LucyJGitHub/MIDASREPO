     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2001')
      *****************************************************************
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - BRIDGE                                               *
      *                                                               *
      *  UP1018 - Action file compatibilty report for UPSARSPD        *
      *                                                               *
      *  Function:  This program compares the differences between     *
      *             Action file delivered and the system.             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CUP018             Date 30May02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CUP010  *CREATE    Date 15Aug00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP018 - Show 'after' image instead of 'before' on report.   *
      *  CUP010 - Program to view conflicts with upgrade action file  *
      *           and existing file                                   *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Use of indicators.                                           *
      *                                                               *
      *    30         Error on Read of UPXXXXXX                       *
      *    31         EOF on Read of UPXXXXXX                         *
      *    32         Chain Fail on Insert routine                    *
      *    33         Chain Fail on Delete routine                    *
      *    34         Record there on chain but different             *
      *    35         Chain Fail on Amend routine                     *
      *    36         Record there on Chain but different             *
      *    37         Compare Indicator for Delete Routine            *
      *    38         Compare Indicator for Amend Routine             *
      *    39         Switched on if a record is found on file(Delete)*
      *    40         Conflicts detected                              *
      *    41         First line of record written                    *
      *    42         Same Record in Sys and  UP for Insert           *
      *    60         Do not run report if error on Amends            *
      *    70         Error on Write                                  *
      *    98         Date Format                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E  I N D E X                               *
      *                                                               *
      * *PSSR - Error processing                                      *
      * *INZSR - Initialise                                           *
      *                                                               *
      *    The *INZSR subroutine will only get called automatically   *
      *    on entry to the module the first time it is run            *
      *    (unless you end the program with LR on).  Similarly        *
      *    D-spec initialisation only happens the first time.  Use    *
      *    RESET for subsequent passes.                               *
      *                                                               *
      *****************************************************************
      ** +--------------------------------------+
      ** ¦ F-specs                              ¦
      ** ¦ =======                              ¦
      ** +--------------------------------------+
 
     FUPSARSPD  IF   E             DISK    INFSR(*PSSR) Prefix(UP_) USROPN
     FSCSARSL0  IF   E           K DISK    INFSR(*PSSR) Prefix(SYS_) USROPN
     FUP0803P1  O    E             PRINTER INFDS(SPOOL)
 
      *****************************************************************
      /EJECT
      *****************************************************************
      ** +--------------------------------------+
      ** ¦ D-specs                              ¦
      ** ¦ =======                              ¦
      ** +--------------------------------------+
     D LDA           E DS           256    EXTNAME(LDA)
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     D RUNDAT        E DS                  EXTNAME(RUNDAT)
      *
      ** Data Area giving Installation Control Details
 
      ** The following /COPY line includes all the defined fields in the
      ** PSDS.  They have meaningful names, prefixed by 'PS'.
 
     D/COPY ZACPYSRC,PSDS
      *
      ** Program Status Data Structure
      *
      ** +-----------------------------------------------------------------+
      ** D specs of the following types should be inserted after the
      ** relevant box.  *** Remove this text afterwards. ***
      ** +-----------------------------------------------------------------+
 
      ** +--------------------------------------+
      ** ¦ Named constants                      ¦
      ** ¦ ===============                      ¦
      ** +--------------------------------------+
 
     D Header          C                   'Report of the differences between -
     D                                     the Existing System Record and the -
     D                                     Replacement Record'
 
     D Counter         C                   '---------+---------+---------+------
     D                                     ----+---------+---------+---------+--
     D                                     --------+---------+---------+'
 
      ** +--------------------------------------+
      ** ¦ Arrays and Data Structures           ¦
      ** ¦ ==========================           ¦
      ** +--------------------------------------+
      ** Array for the update file record
     D UpdatArray      S              1A   DIM(137)
 
      ** Array for the Core system record
     D SysArray        S              1A   DIM(137)
 
      ** Array for the Error Indicator
     D ErrorArray      S              1A   DIM(137)
 
      ** Data Structure for the Update file
     D                 DS
     D UP_AXSARN                      6
     D UP_AXSARD                     50
     D UP_AXPRIM                      2
     D UP_AXCEMI                      1
     D UP_AXRELL                     10
     D UP_AXIREL                      3
     D UP_AXCLVL                      4
     D UP_AXRSTS                      1
     D UP_AXDFLT                      3
     D UP_AXMNTB                      1
     D UP_AXCHRG                      1
     D UP_AXFLRN                     35
     D UP_AXDOCN                     11
     D UP_AXHELP                      8
     D  Updfield               1    137A
     D  UPDLIN1                1    100A
     D  UPDLIN2              101    137A
 
      ** Data Structure for the Core file
     D                 DS
     D SYS_AXSARN                     6
     D SYS_AXSARD                    50
     D SYS_AXPRIM                     2
     D SYS_AXCEMI                     1
     D SYS_AXRELL                    10
     D SYS_AXIREL                     3
     D SYS_AXCLVL                     4
     D SYS_AXRSTS                     1
     D SYS_AXDFLT                     3
     D SYS_AXMNTB                     1
     D SYS_AXCHRG                     1
     D SYS_AXFLRN                    35
     D SYS_AXDOCN                    11
     D SYS_AXHELP                     8
     D  CoreFld                1    137A
     D  SYSLIN1                1    100A
     D  SYSLIN2              101    137A
 
      ** Data Structure for the Error indicators
     D                 DS           137
     D  Indfld                 1    137A
     D  INDLIN1                1    100A
     D  INDLIN2              101    137A
 
     DSPOOL            DS
     D SFILE                  83     92
     D SFNUM                 123    124B 0
     D OFLLN                 188    189B 0
     D PRTLN                 367    368B 0
 
      ** +--------------------------------------+
      ** ¦ Declared variables                   ¦
      ** ¦ ==================                   ¦
      ** +--------------------------------------+
 
     D @RUN            S              1
     D KEY_AXSARN      S                   LIKE(UP_AXSARN)
     D x               S              5  0
     D UPDFIL          S             10
     D SYSFIL          S             10
     D ACTION          S              7
     D RMLN            S              2  0
     D Amends          S              5  1
     D COUNT           S            100
     D Count37         S             37
     D WAmends         S              5  1
     D Remainder       S              1
 
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ** +--- Start of Main processing -----------------------------------+
      ** ¦                                                                ¦
      ** ¦ Initial processing is performed automatically: the *INZSR is   ¦
      ** ¦ executed at program activation.                                ¦
      ** ¦                                                                ¦
      ** +----------------------------------------------------------------+
 
      *********************************************************************
      * MAIN PROCEDURE                                                    *
      *********************************************************************
      *
      ** Read in Installation Data
      *
     C     *DTAARA       DEFINE                  RUNDAT
     C     *DTAARA       DEFINE                  LDA
     C                   IN        RUNDAT
      *
      ** Set Date Format Indicator *IN98  on if date format is MMDDYY
      *
     C                   IF        AGDFF = 'M'
     C                   EVAL      *IN98 = *ON
     C                   END
      *
      ** Sub Routine to check that there are an equal number or Amends
      ** before running reports.
      *
     C                   EXSR      CHKA
 
      *There was no problem with an Odd number of Amends
     C                   IF        *IN60 = '0'
 
      * Set up key list for Switchable feature No. and Pgmnam
     C     KEY           KLIST
     C                   KFLD                    KEY_AXSARN
 
     C                   READ      UPSARSPD                             3031
      *
      * If an Error or End of file reached
     C                   IF        *IN30 = '1'
      *
     C     *LOCK         IN        LDA
     C                   EVAL      *INU7 = *ON
     C                   EVAL      *INU8 = *ON
     C                   EVAL      DBFILE =  'UPSARSPD'
     C                   EVAL      DBKEY  =  '(key)'
     C                   EVAL      DBPGM  =  PSProcPgm
     C                   EVAL      DBASE  =  001
     C                   EVAL      DBMOD  =  PSProcMod
     C                   EVAL      DBPROC =  'SR/MAIN'
     C                   OUT       LDA
     C                   EXSR      *PSSR
     C                   ELSE
      *
     C                   WRITE     HEADH                                70
      *
      * Do while a record exists
     C                   DOW       *IN31 = '0'
     C                   EVAL      ACTION = *BLANKS
     C                   EVAL      Indfld = *BLANKS
      *
      * If Action Code is Insert
     C                   IF        UP_AXACTC = 'I'
     C                   MOVEL     UP_AXSARN     KEY_AXSARN
     C     KEY           CHAIN     SCSARSL0                           32
      *
      * If a record is found on the chain then print out a message
     C                   IF        *IN32 = '0'
      * If The two records are different - Move along Character by Character indicating
      * differences on the third Array (ErrorArray) ELSE the record saying Identical
     C                   IF        CoreFld <> Updfield
     C                   MOVEA     Updfield      UpdatArray
     C                   MOVEA     CoreFld       SysArray
     C                   CLEAR                   ErrorArray
     C                   Z-ADD     1             x
     C                   DOU       X > 137
     C     SysArray(x)   COMP      UpdatArray(x)                          43
     C                   IF        *IN43 = '0'
     C                   EVAL      *IN39 = '1'
     C                   MOVEA     '*'           ErrorArray(x)
     C                   ENDIF
     C                   ADD       1             x
     C                   ENDDO
      * Move error indicator array to field for output
     C                   MOVEA     ErrorArray    IndFld
      *
     C                   MOVEL     'INSERT'      ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ELSE
     C                   EVAL      *IN42 = '1'
     C                   MOVEL     'INSERT'      ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
      * If Action Code is Delete
     C                   IF        UP_AXACTC = 'D'
     C                   MOVEL     UP_AXSARN     KEY_AXSARN
     C     KEY           CHAIN     SCSARSL0                           33
      *
      * If a file is not found on the chain print out a message
     C                   IF        *IN33 = '1'
     C                   MOVEL     'DELETE'      ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ELSE
      *
      * If The two records are different - Move along Character by Character indicating
      * differences on the third Array (ErrorArray)
     C                   IF        Corefld <> Updfield
     C                   MOVEA     Updfield      UpdatArray
     C                   MOVEA     CoreFld       SysArray
     C                   CLEAR                   ErrorArray
     C                   Z-ADD     1             x
     C                   DOU       X > 137
     C     SysArray(x)   COMP      UpdatArray(x)                          36
     C                   IF        *IN36 = '0'
     C                   EVAL      *IN34 = '1'
     C                   MOVEA     '*'           ErrorArray(x)
     C                   EndIf
     C                   ADD       1             x
     C                   EndDo
      * Move error indicator array to field for output
     C                   MOVEA     ErrorArray    IndFld
      *
      * Write out all three data structures with the indicator showing
      * where the error is
     C                   MOVEL     'DELETE'      ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
 
      *
      * If Action Code is Amend
     C                   IF        UP_AXACTC = 'A'
     C                   MOVEL     UP_AXSARN     KEY_AXSARN
     C     KEY           CHAIN     SCSARSL0                           35
      *
      * If a file is not found on the chain print out a message
     C                   IF        *IN35 = '1'
     C                   MOVEL     'AMEND'       ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
      * Because amends come in pairs read the after image of amend.                           CUP018
     C                   READ      UPSARSPD                               31                  CUP018
     C                   ELSE
      * If the before image record and core record are different, read                        CUP018
      * the after amend image record and compare it with the core record.                     CUP018
     C                   IF        CoreFld <> Updfield                                        CUP018
     C                   READ      UPSARSPD                               31                  CUP018
      *
      * If The two records are different - Move along Character by Character indicating
      * differences on the third Array (ErrorArray)
     C                   IF        CoreFld <> Updfield
     C                   MOVEA     Updfield      UpdatArray
     C                   MOVEA     CoreFld       SysArray
     C                   CLEAR                   ErrorArray
     C                   Z-ADD     1             x
     C                   DOU       X > 137
     C     SysArray(x)   COMP      UpdatArray(x)                          38
     C                   IF        *IN38 = '0'
     C                   EVAL      *IN36 = '1'
     C                   MOVEA     '*'           ErrorArray(x)
     C                   ENDIF
     C                   ADD       1             x
     C                   ENDDO
      * Move error indicator array to field for output
     C                   MOVEA     ErrorArray    IndFld
      *
      * Write out all three data structures with the indicator showing
      * where the error is
      * Write the after image data.                                                           CUP018
     C                   MOVEL     'AMEND'       ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ENDIF
     C                   ELSE                                                                 CUP018
      *  If the before image record is same as core record,                                   CUP018
      *  read the after image record but nothing is done.                                     CUP018
     C                   READ      UPSARSPD                               31                  CUP018
     C                   ENDIF                                                                CUP018
     C                   ENDIF
      **Because*amends*come*in pairs*read*the*next*record*here.                               CUP018
     C*****              READ      UPSARSPD                               31                  CUP018
     C                   ENDIF
      *
     C                   MOVEA     '000000000'   *IN(30)
     C                   MOVEA     '0'           *IN(39)
     C                   MOVEA     '00'          *IN(41)
     C                   READ      UPSARSPD                             3031
      *
      * If an Error
     C                   IF        *IN30 = '1'
      *
     C     *LOCK         IN        LDA
     C                   EVAL      *INU7 = *ON
     C                   EVAL      *INU8 = *ON
     C                   EVAL      DBFILE =  'UPSARSPD'
     C                   EVAL      DBKEY  =  '(key)'
     C                   EVAL      DBPGM  =  PSProcPgm
     C                   EVAL      DBASE  =  002
     C                   EVAL      DBMOD  =  PSProcMod
     C                   EVAL      DBPROC =  'SR/MAIN'
     C                   OUT       LDA
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDDO
     C                   ENDIF
      * If there are no differences between the two files UP and Sys
      * Print NO Differences
     C                   IF        *IN40 = '0'
     C                   WRITE     NODIFS                               70
     C                   ENDIF
      *
      *ELSE Error in No of Amend Records
     C                   ELSE
     C                   WRITE     HEADH
     C                   WRITE     AMENDERR
     C                   ENDIF
      *
     C                   WRITE     TRAILP                               70
 
 
     C                   SETON                                        LR
     C                   RETURN
      **********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * REPORT - WRITE To the printer file                            *
      *                                                               *
      * Called by: Main Prcessing                                     *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     REPORT        BEGSR
      *
      *
     C                   IF        (*IN33 = '1') OR
     C                             (*IN35 = '1') OR
     C                             (*IN42 = '1')
      *Overflow check for an Insert that exists or an Amend/Delete that is missing
     C                   EXSR      OFLOW
      * Write first 100 characters
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     UPDLIN1       UPDLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL1                                 70
      * Set on *IN41 to stop the appearance of Action
     C                   EVAL      *IN41 = '1'
      * Write last 37 characters
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       COUNT
     C                   MOVEL     UPDLIN2       UPDLIN
     C                   MOVEL     Counter       Count37
     C                   MOVEL     Count37       COUNT
     C                   WRITE     DTL1                                 70
      * Set Off *IN41
     C                   EVAL      *IN41 ='0'
     C                   ENDIF
      *
     C                   IF        (*IN34 = '1') OR
     C                             (*IN36 = '1') OR
     C                             (*IN39 = '1')
      *Overflow check for an Insert that exists or an Amend/Delete that is missing
     C                   EXSR      OFLOW2
      * Write first 100 characters
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN1       UPDLIN
     C                   MOVEL     INDLIN1       INDLIN
     C                   MOVEL     SYSLIN1       SYSLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL2                                 70
      * Set on *IN41 to stop the appearance of Action
     C                   EVAL      *IN41 = '1'
      * Write last 37  characters
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     *BLANKS       COUNT
     C                   MOVEL     UPDLIN2       UPDLIN
     C                   MOVEL     INDLIN2       INDLIN
     C                   MOVEL     SYSLIN2       SYSLIN
     C                   MOVEL     Counter       Count37
     C                   MOVEL     Count37       COUNT
     C                   WRITE     DTL2                                 70
      * Set Off *IN41
     C                   EVAL      *IN41 ='0'
     C                   ENDIF
 
     C                   ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * OFLOW - OVERFLOW Processing  for fields that                  *
      *                 For Inserts that are there and Amends and     *
      *                 Deletes that are missing                      *
      *                                                               *
      * Called by: Main Prcessing                                     *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     OFLOW         BEGSR
      *
     C     OFLLN         SUB       PRTLN         RMLN
     C                   IF        RMLN <= 8
     C                   WRITE     HEADH                                70
     C                   ENDIF
 
     C                   ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * OFLOW2- OVERFLOW Processing for differences on >200 chars     *
      *         with file differences                                 *
      *                                                               *
      * Called by: Main Prcessing                                     *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     OFLOW2        BEGSR
      *
     C     OFLLN         SUB       PRTLN         RMLN
     C                   IF        RMLN <= 9
     C                   WRITE     HEADH                                70
     C                   ENDIF
 
     C                   ENDSR
      **********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * CHKA  - Check there are an even number of Amend Records       *
      *         with file differences                                 *
      *                                                               *
      * Called by: Main Prcessing                                     *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     CHKA          BEGSR
      *
     C                   READ      UPSARSPD                               70
     C                   DOW       *IN70 = '0'
     C                   IF        UP_AXACTC = 'A'
     C                   ADD       1             Amends
     C                   ENDIF
     C                   READ      UPSARSPD                               70
     C                   ENDDO
      *
      * Check to see if the No. of Amends is divisible by 2 and not equal 0
     C                   IF        Amends <> 0
     C     Amends        DIV       2             WAmends
     C                   MOVE      WAmends       Remainder
      *If the remainder is ='5' Set on indicator to stop File processing
     C                   IF        REMAINDER ='5'
     C                   EVAL      *IN60 = '1'
     C                   ENDIF
     C                   ENDIF
     C     *START        SETLL     UPSARSPD
 
     C                   ENDSR
      **********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *INZSR - Program Initialisation routine                       *
      *                                                               *
      * Called by: Implicitly on program activation                   *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     *INZSR        BEGSR
      ** Initialize program name
      *
     C                   MOVEL     'UP1018'      DBPGM
      *
      * MOVE IN PROGRAM FIELDS
     C                   MOVEL     'SCSARSPD'    SYSFIL
     C                   MOVEL     'UPSARSPD'    UPDFIL
     C                   MOVEL     HEADER        HEAD
      *OPEN FILES
     C                   OPEN      UPSARSPD
     C                   OPEN      SCSARSL0
     C                   ENDSR
 
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR
      *
     C                   IF        @RUN = *BLANK
     C                   EVAL      @RUN = 'Y'
     C                   DUMP
      *
     C                   ENDIF
      *
     C                   EVAL      *INU7 = *ON
     C                   EVAL      *INU8 = *ON
     C                   EVAL      *INLR = *ON
     C                   RETURN
      *
     C                   ENDSR
      *
