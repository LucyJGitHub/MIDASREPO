     H        1
      *****************************************************************
/*X*IA***OVRDBF*FILE(OREF1)*TOFILE(UPOREFTPD)**************************                CUP019 CUP021
/*X*IB***OVRDBF*FILE(OREF2)*TOFILE(UPOREFTPD)**************************                CUP019 CUP021
/*S*D****RPGBASE*******************************************************                       CUP021
/*O*R ***OVRDBF*OREF1*SDOREFL1*****************************************                       CUP019
/*O*R ***OVRDBF*OREF2*SDOREFL1*****************************************                       CUP019
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade module                                       *
      *                                                               *
      *  UP1035 - Compare SD Optional windows reference files         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.02 ----------------------------------------*
      *  Prev Amend No. CUP019             Date 10Sep02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CUP006 *CREATE     Date 22Nov99               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP019 - Use templates for Action Files.                     *
      *  CUP006  -  Make upgrade of DTALIB files easier               *
      *                                                               *
      *****************************************************************
      * ID F  C  H  L    FUNCTION OF INDICATORS
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write or chain fail
      *       70 - 81 Indicators for changed fields
      *
     FOREF1   IF  E                    DISK         KINFSR *PSSR
     FOREF2   IF  E           K        DISK         KINFSR *PSSR
     F********    @DMREGW                           KRENAMEOREFF                              CUP019
     F            SDOREFD0                          KRENAMEOREFF                              CUP019
     FUPOREFPDO   E                    DISK         KINFSR *PSSR
      *
      /EJECT
     E                    CPY@    1   1 80
      *
      * Fields with the same name will need to be renamed.
      *
     IOREFF
     I              DMNMCP                          NMCP
     I              DMFNKY                          FNKY
     I              DMWNCN                          WNCN
     I              DMLGWD                          LGWD
     I              DMWDWD                          WDWD
     I              DMTTWD                          TTWD
     I              DMSRWD                          SRWD
     I              DMSCWD                          SCWD
     I              DMPGMT                          PGMT
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *ENTRY    PLIST
     C                     PARM           MODE    1
      *
     C           COMP      KLIST
     C                     KFLD           DMNMCP
     C                     KFLD           DMFNKY
      *
     C                     READ OREF1                  6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           *IN20     DOWEQ'0'
     C           COMP      CHAINOREFF                3069
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * If record not found then record has been deleted or added
      *  depending on mode
     C           *IN30     IFEQ *ON
     C                     MOVE MODE      DMACTC
     C                     WRITEUPOREFD0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
      * Need to find differences but this should only be run once;
      *  run only if mode is 'Delete'
     C           MODE      IFEQ 'D'
      *
      * If record found then look for any differences.
     C           *IN30     IFEQ *OFF
     C                     MOVE 'A'       DMACTC
      * Name of calling program
     C           DMNMCP    IFNE NMCP
     C                     SETON                     70
     C                     ENDIF
      * Function Key
     C           DMFNKY    IFNE FNKY
     C                     SETON                     71
     C                     ENDIF
      * Window controller name
     C           DMWNCN    IFNE WNCN
     C                     SETON                     73
     C                     ENDIF
      * Length of Window
     C           DMLGWD    IFNE LGWD
     C                     SETON                     74
     C                     ENDIF
      * Width of Window
     C           DMWDWD    IFNE WDWD
     C                     SETON                     75
     C                     ENDIF
      * Title of Window
     C           DMTTWD    IFNE TTWD
     C                     SETON                     76
     C                     ENDIF
      * Starting Row of Window
     C           DMSRWD    IFNE SRWD
     C                     SETON                     77
     C                     ENDIF
      * Starting Column of Window
     C           DMSCWD    IFNE SCWD
     C                     SETON                     78
     C                     ENDIF
      * Program Text
     C           DMPGMT    IFNE PGMT
     C                     SETON                     79
     C                     ENDIF
      *
     C                     MOVEA*IN,70    INTEST 10
     C                     MOVE INTEST    INTNUM 100
      * If any indicators are set on then write record
     C           INTNUM    IFNE 0
     C                     WRITEUPOREFD0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * Set values to write second record
     C                     MOVELNMCP      DMNMCP
      *
     C                     MOVELFNKY      DMFNKY
      *
     C                     MOVELWNCN      DMWNCN
      *
     C                     MOVELLGWD      DMLGWD
      *
     C                     MOVELWDWD      DMWDWD
      *
     C                     MOVELTTWD      DMTTWD
      *
     C                     MOVELSRWD      DMSRWD
      *
     C                     MOVELSCWD      DMSCWD
      *
     C                     MOVELPGMT      DMPGMT
      *
     C                     WRITEUPOREFD0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
      * Read next record
     C                     MOVEA'00000000'*IN,70
     C                     MOVEA'00'      *IN,78
     C                     READ OREF1                  6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           ENDPGM    TAG
     C                     SETON                     LR
     C                     RETRN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      ************ CALL DBERRCTL IF INTERACTIVE PROGRAM ***************
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
