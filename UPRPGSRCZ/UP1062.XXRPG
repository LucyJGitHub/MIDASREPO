     H        1
      *****************************************************************
/*S*D****RPGBASE*******************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade module                                       *
      *                                                               *
      *  UP1062 - Process CB Task Split Component Relationships       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CUP006 *CREATE     Date 29Nov99               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP006  -  Make upgrade of DTALIB files easier               *
      *                                                               *
      *****************************************************************
      * ID F  C  H  L    FUNCTION OF INDICATORS
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write, update or delete fail
      *
     FUPTPGML0IF  E           K        DISK         KINFSR *PSSR
     FCBTPGML0UF  E           K        DISK         KINFSR *PSSR A
     F            @TPGML0                           KRENAMETPGMF
      *
      /EJECT
      * Array containing Copyright statement
     E                    CPY@    1   1 80
      *
      * Fields with the same name will need to be renamed.
     ITPGMF
     I              CBOLDP                          OLDP
     I              CBOLDS                          OLDS
     I              CBSTRP                          STRP
     I              CBSTRS                          STRS
     I              CBSRVP                          SRVP
     I              CBSRVS                          SRVS
     I              CBSTSK                          STSK
     I              CBSTKS                          STKS
     I              CBEPRC                          EPRC
     I              CBEPRS                          EPRS
     I              CBESTK                          ESTK
     I              CBESTS                          ESTS
     I              CBESPT                          ESPT
     I              CBESPS                          ESPS
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           COMP      KLIST
     C                     KFLD           CBOLDP
     C                     KFLD           CBOLDS
      *
     C           *LOVAL    SETLLUPTPGML0
      *
     C                     READ UPTPGML0               6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
     C           *IN20     DOWEQ'0'
      *
     C           CBACTC    IFEQ 'I'
     C                     EXSR SETVAL
     C                     WRITETPGMF                  69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     ENDIF
      *
     C           CBACTC    IFEQ 'D'
     C           COMP      CHAINCBTPGML0             3069
     C           *IN69     IFEQ *ON
     C           *IN30     OREQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
      *
     C                     ELSE
     C                     EXSR SETVAL
     C                     DELETTPGMF                  69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
      * If Action code = AMEND then read next record
     C           CBACTC    IFEQ 'A'
     C                     MOVELCBOLDP    OLDPX  10
     C                     MOVELCBOLDS    OLDSX   5
     C                     READ UPTPGML0               6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
      * If Task Split and component and sequence number do not match
      *  - exit or if EOF indicator is on
     C           CBOLDP    IFNE OLDPX
     C           CBOLDS    ANDNEOLDSX
     C           *IN20     OREQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
     C           COMP      CHAINCBTPGML0             3069
     C           *IN69     IFEQ *ON
     C           *IN30     OREQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
      *
     C                     ELSE
      *
     C                     EXSR SETVAL
     C                     UPDATTPGMF                  69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     READ UPTPGML0               6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           ENDPGM    TAG
     C                     SETON                     LR
     C                     RETRN
      *
      *****************************************************************
      *
      * SETVAL subroutine - set values for deletes/writes/updates
      *
      *****************************************************************
     C           SETVAL    BEGSR
      * Set values to update record
     C                     MOVELCBOLDP    OLDP
     C                     MOVELCBOLDS    OLDS
     C                     MOVELCBSTRP    STRP
     C                     MOVELCBSTRS    STRS
     C                     MOVELCBSRVP    SRVP
     C                     MOVELCBSRVS    SRVS
     C                     MOVELCBSTSK    STSK
     C                     MOVELCBSTKS    STKS
     C                     MOVELCBEPRC    EPRC
     C                     MOVELCBEPRS    EPRS
     C                     MOVELCBESTK    ESTK
     C                     MOVELCBESTS    ESTS
     C                     MOVELCBESPT    ESPT
     C                     MOVELCBESPS    ESPS
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
