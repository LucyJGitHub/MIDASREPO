     H        1
      *****************************************************************
/*S*D****RPGBASE*******************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade module                                       *
      *                                                               *
      *  UP1112 - Process MG Cedel/Euroclear restrictions file        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CUP006 *CREATE     Date 08DEC99               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP006  -  Make upgrade of DTALIB files easier               *
      *                                                               *
      *****************************************************************
      * ID F  C  H  L    FUNCTION OF INDICATORS
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write, update or delete fail
      *
     FUPECONL0IF  E           K        DISK         KINFSR *PSSR
     FMGECONL0UF  E           K        DISK         KINFSR *PSSR A
     F            MGECOND1                          KRENAMEECONF
      *
      /EJECT
      * Array containing Copyright statement
     E                    CPY@    1   1 80
      *
      * Fields with the same name will need to be renamed.
     IECONF
     I              MGNTRY                          NTRY
     I              MGXFUN                          XFUN
     I              MGXCCT                          XCCT
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *LOVAL    SETLLUPECONL0
      *
     C                     READ UPECONL0               6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
     C           *IN20     DOWEQ'0'
      *
     C           MGACTC    IFEQ 'I'
     C                     EXSR SETVAL
     C                     WRITEECONF                  69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     ENDIF
      *
     C           MGACTC    IFEQ 'D'
     C           MGNTRY    CHAINMGECONL0             3069
     C           *IN69     IFEQ *ON
     C           *IN30     OREQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
      *
     C                     ELSE
     C                     EXSR SETVAL
     C                     DELETECONF                  69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
      * If Action code = AMEND then read next record
     C           MGACTC    IFEQ 'A'
     C                     MOVELMGNTRY    NTRYX  20
     C                     READ UPECONL0               6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
      * If Task Split and component and sequence number do not match
      *  - exit or if EOF indicator is on
     C           MGNTRY    IFNE NTRYX
     C           *IN20     OREQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
     C           MGNTRY    CHAINMGECONL0             3069
     C           *IN69     IFEQ *ON
     C           *IN30     OREQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
      *
     C                     ELSE
      *
     C                     EXSR SETVAL
     C                     UPDATECONF                  69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     READ UPECONL0               6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           ENDPGM    TAG
     C                     SETON                     LR
     C                     RETRN
      *
      *****************************************************************
      *
      * SETVAL subroutine - set values for deletes/writes/updates
      *
      *****************************************************************
     C           SETVAL    BEGSR
      * Set values to update record
     C                     MOVELMGNTRY    NTRY
     C                     MOVELMGXFUN    XFUN
     C                     MOVELMGXCCT    XCCT
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
