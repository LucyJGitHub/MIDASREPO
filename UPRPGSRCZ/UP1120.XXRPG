     H        1
      *****************************************************************
/*X*IA***OVRDBF*FILE(INFM1)*TOFILE(UPINFMTPD)**************************                CUP019 CUP021
/*X*IB***OVRDBF*FILE(INFM2)*TOFILE(UPINFMTPD)**************************                CUP019 CUP021
/*S*D****RPGBASE*******************************************************                       CUP021
/*O*R ***OVRDBF*INFM1*MEINFML2*****************************************                       CUP019
/*O*R ***OVRDBF*INFM2*MEINFML2*****************************************                       CUP019
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade module                                       *
      *                                                               *
      *  UP1040 - Compare Incoming message translation                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. BUG24370           Date 16Jun09               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.02 ----------------------------------------*
      *                 CUP019             Date 10Sep02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CUP006 *CREATE     Date 07Dec99               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  BUG24370 - New fields tags & sequences are missing           *
      *  CUP019 - Use templates for Action Files.                     *
      *  CUP006  -  Make upgrade of DTALIB files easier               *
      *                                                               *
      *****************************************************************
      * ID F  C  H  L    FUNCTION OF INDICATORS
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write or chain fail
      *       70 - 81 Indicators for changed fields
      *
     FINFM1   IF  E                    DISK         KINFSR *PSSR
     FINFM2   IF  E           K        DISK         KINFSR *PSSR
     F********    @INFML2                           KRENAMEINFMF                              CUP019
     F            @INFMPD                           KRENAMEINFMF                              CUP019
     FUPINFMPDO   E                    DISK         KINFSR *PSSR
      *
      /EJECT
     E                    CPY@    1   1 80
      *
      * Fields with the same name will need to be renamed.
      *
     IINFMF
     I              FMNWRK                          NWRK
     I              FMMTPY                          MTPY
     I              FMMTAG                          MTAG
     I              FMMSEQ                          MSEQ                                    BUG24370
     I              FMFTFD                          FTFD
     I              FMFTSP                          FTSP
     I              FMNARR                          NARR
     I              FMRECI                          RECI
     I              FMAJOB                          AJOB
     I              FMAUSR                          AUSR
     I              FMATIM                          ATIM
     I              FMARDT                          ARDT
     I              FMAACT                          AACT
     I              FMRDNB                          RDNB
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *ENTRY    PLIST
     C                     PARM           MODE    1
      *
     C           COMP      KLIST
     C                     KFLD           FMNWRK
     C                     KFLD           FMMTPY
     C                     KFLD           FMMSEQ                                            BUG24370
     C                     KFLD           FMMTAG
     C                     KFLD           FMFTFD
      *
     C                     READ INFM1                  6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           *IN20     DOWEQ'0'
     C           COMP      CHAININFMF                3069
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * If record not found then record has been deleted or added
      *  depending on mode
     C           *IN30     IFEQ *ON
     C                     MOVE MODE      FMACTC
     C                     WRITEUPINFMD0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
      * Need to find differences but this should only be run once;
      *  run only if mode is 'Delete'
     C           MODE      IFEQ 'D'
      *
      * If record found then look for any differences; only look in
      *  fields that have data all of the time.
     C           *IN30     IFEQ *OFF
     C                     MOVE 'A'       FMACTC
      * Network
     C           FMNWRK    IFNE NWRK
     C                     SETON                     70
     C                     ENDIF
      * Message Type
     C           FMMTPY    IFNE MTPY
     C                     SETON                     73
     C                     ENDIF
      * Message Sequence                                                                    BUG24370
     C           FMMSEQ    IFNE MSEQ                                                        BUG24370
     C                     SETON                     74                                     BUG24370
     C                     ENDIF                                                            BUG24370
      * Message Field Tag
     C           FMMTAG    IFNE MTAG
     C                     SETON                     74
     C                     ENDIF
      * FT Field Name
     C           FMFTFD    IFNE FTFD
     C                     SETON                     75
     C                     ENDIF
      * Special Instructions
     C           FMFTSP    IFNE FTSP
     C                     SETON                     76
     C                     ENDIF
      * Narrative
     C           FMNARR    IFNE NARR
     C                     SETON                     77
     C                     ENDIF
      * Record Identifier
     C           FMRECI    IFNE RECI
     C                     SETON                     78
     C                     ENDIF
      *
     C                     MOVEA*IN,70    INTEST 10
     C                     MOVE INTEST    INTNUM 100
      * If any indicators are set on then write record
     C           INTNUM    IFNE 0
     C                     WRITEUPINFMD0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * Set values to write second record
     C                     MOVELNWRK      FMNWRK
      *
     C                     MOVELMTPY      FMMTPY
      *
     C                     MOVELMSEQ      FMMSEQ                                            BUG24370
      *                                                                                     BUG24370
     C                     MOVELMTAG      FMMTAG
      *
     C                     MOVELFTFD      FMFTFD
      *
     C                     MOVELFTSP      FMFTSP
      *
     C                     MOVELNARR      FMNARR
      *
     C                     MOVELRECI      FMRECI
      *
     C                     MOVELAJOB      FMAJOB
      *
     C                     MOVELAUSR      FMAUSR
      *
     C                     MOVELATIM      FMATIM
      *
     C                     MOVELARDT      FMARDT
      *
     C                     MOVELAACT      FMAACT
      *
     C                     MOVELRDNB      FMRDNB
      *
     C                     WRITEUPINFMD0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
      * Read next record
     C                     MOVEA'00000000'*IN,70
     C                     MOVEA'00'      *IN,78
     C                     READ INFM1                  6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           ENDPGM    TAG
     C                     SETON                     LR
     C                     RETRN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      ************ CALL DBERRCTL IF INTERACTIVE PROGRAM ***************
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
