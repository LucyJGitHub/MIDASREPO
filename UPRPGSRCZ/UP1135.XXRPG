     H        1
      *****************************************************************
/*X*IA***OVRDBF*FILE(X5401)*TOFILE(UP540XTPD)**************************                CUP019 CUP021
/*X*IB***OVRDBF*FILE(X5402)*TOFILE(UP540XTPD)**************************                CUP019 CUP021
/*S*D****RPGBASE*******************************************************                       CUP021
/*O*R ***OVRDBF*X5401*MQ540XLF*****************************************                       CUP019
/*O*R ***OVRDBF*X5402*MQ540XLF*****************************************                       CUP019
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade module                                       *
      *                                                               *
      *  UP1135 - Compare MQ RTEVENTS                                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CUP022             Date 07Apr04               *
      *                 BUG1894            Date 06Apr04               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 CUP019             Date 10Sep02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CUP006 *CREATE     Date 09Dec99               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP022 - Replace file UP540XPF with UPX540PD.                *
      *  BUG1894- CGL029 changed MQ540XPF by making ACODE 10 long     *
      *           rather than 4.  This was wrong and has been         *
      *           reversed.  Recompile.                               *
      *  CUP019 - Use templates for Action Files.                     *
      *  CUP006  -  Make upgrade of DTALIB files easier               *
      *                                                               *
      *****************************************************************
      * ID F  C  H  L    FUNCTION OF INDICATORS
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write or chain fail
      *       70 - 81 Indicators for changed fields
      *
     FX5401   IF  E                    DISK         KINFSR *PSSR
     FX5402   IF  E           K        DISK         KINFSR *PSSR
     F            ALLEV                             KRENAMEX540F
     F*****UP540XPFO   E                    DISK         KINFSR *PSSR                        CUP022
     FUPX540PDO   E                    DISK         KINFSR *PSSR                             CUP022
      *
      /EJECT
     E                    CPY@    1   1 80
      *
      * Fields with the same name will need to be renamed.
      *
     IX540F
     I              ACODE                           CODE
     I              AMOD                            MOD
     I              ADESC                           DESC
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *ENTRY    PLIST
     C                     PARM           MODE    1
      *
     C           COMP      KLIST
     C                     KFLD           AMOD
     C                     KFLD           ACODE
      *
     C                     READ X5401                  6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           *IN20     DOWEQ'0'
     C           COMP      CHAINX540F                3069
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * If record not found then record has been deleted or added
      *  depending on mode
     C           *IN30     IFEQ *ON
     C                     MOVE MODE      MQACTC
     C*****                WRITEUP540XD0               69                                    CUP022
     C                     WRITEUPX540D0               69                                    CUP022
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
      * Need to find differences but this should only be run once;
      *  run only if mode is 'Delete'
     C           MODE      IFEQ 'D'
      *
      * If record found then look for any differences; only look in
      *  fields that have data all of the time.
     C           *IN30     IFEQ *OFF
     C                     MOVE 'A'       MQACTC
      * Code
     C           ACODE     IFNE CODE
     C                     SETON                     70
     C                     ENDIF
      * Mod
     C           AMOD      IFNE MOD
     C                     SETON                     71
     C                     ENDIF
      * Description
     C           ADESC     IFNE DESC
     C                     SETON                     73
     C                     ENDIF
      *
     C                     MOVEA*IN,70    INTEST 10
     C                     MOVE INTEST    INTNUM 100
      * If any indicators are set on then write record
     C           INTNUM    IFNE 0
     C******               WRITEUP540XD0               69                                    CUP022
     C                     WRITEUPX540D0               69                                    CUP022
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * Set values to write second record
     C                     MOVELCODE      ACODE
     C                     MOVELMOD       AMOD
     C                     MOVELDESC      ADESC
      *
     C******               WRITEUP540XD0               69                                    CUP022
     C                     WRITEUPX540D0               69                                    CUP022
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
      * Read next record
     C                     MOVEA'00000000'*IN,70
     C                     MOVEA'00'      *IN,78
     C                     READ X5401                  6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           ENDPGM    TAG
     C                     SETON                     LR
     C                     RETRN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      ************ CALL DBERRCTL IF INTERACTIVE PROGRAM ***************
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
