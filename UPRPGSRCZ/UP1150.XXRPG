     H        1
      *****************************************************************
/*X*IA***OVRDBF*FILE(CONP1)*TOFILE(UPCONPTPD)**************************                CUP019 CUP021
/*X*IB***OVRDBF*FILE(CONP2)*TOFILE(UPCONPTPD)**************************                CUP019 CUP021
/*S*D****RPGBASE*******************************************************                       CUP021
/*O*R ***OVRDBF*CONP1*EMCONPL0*****************************************                       CUP019
/*O*R ***OVRDBF*CONP2*EMCONPL0*****************************************                       CUP019
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade module                                       *
      *                                                               *
      *  UP1150 - Compare EM link enquiry control file                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.02 ----------------------------------------*
      *  Prev Amend No. CUP019             Date 10Sep02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CUP006             Date 14Dec99               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP019 - Use templates for Action Files.                     *
      *  CUP006  -  Make upgrade of DTALIB files easier               *
      *                                                               *
      *****************************************************************
      * ID F  C  H  L    FUNCTION OF INDICATORS
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write or chain fail
      *       70 - 81 Indicators for changed fields
      *
     FCONP1   IF  E                    DISK         KINFSR *PSSR
     FCONP2   IF  E           K        DISK         KINFSR *PSSR
     F            EMCONPFF                          KRENAMECONPF
     FUPCONPPDO   E                    DISK         KINFSR *PSSR
      *
      /EJECT
     E                    CPY@    1   1 80
      *
      * Fields with the same name will need to be renamed.
      *
     ICONPF
     I              RECI                            CI
     I              PGN1                            N1
     I              PGT1                            T1
     I              PGL1                            L1
     I              PGO1                            O1
     I              PGN2                            N2
     I              PGT2                            T2
     I              PGL2                            L2
     I              PGO2                            O2
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *ENTRY    PLIST
     C                     PARM           MODE    1
      *
     C           COMP      KLIST
     C                     KFLD           PGN1
     C                     KFLD           PGN2
     C                     KFLD           PGO1
      *
     C                     READ CONP1                  6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           *IN20     DOWEQ'0'
     C           COMP      CHAINCONPF                3069
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * If record not found then record has been deleted or added
      *  depending on mode
     C           *IN30     IFEQ *ON
     C                     MOVE MODE      EMACTC
     C                     WRITEUPCONPFF               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
      * Need to find differences but this should only be run once;
      *  run only if mode is 'Delete'
     C           MODE      IFEQ 'D'
      *
      * If record found then look for any differences; only look in
      *  fields that have data all of the time.
     C           *IN30     IFEQ *OFF
     C                     MOVE 'A'       EMACTC
      * Record Identifier
     C           RECI      IFNE CI
     C                     SETON                     70
     C                     ENDIF
      * Program name (1)
     C           PGN1      IFNE N1
     C                     SETON                     71
     C                     ENDIF
      * Program Text (1)
     C           PGT1      IFNE T1
     C                     SETON                     72
     C                     ENDIF
      * Program level (1)
     C           PGL1      IFNE L1
     C                     SETON                     73
     C                     ENDIF
      * Program option (1)
     C           PGO1      IFNE O1
     C                     SETON                     74
     C                     ENDIF
      * Program name (2)
     C           PGN2      IFNE N2
     C                     SETON                     75
     C                     ENDIF
      * Program Text (2)
     C           PGT2      IFNE T2
     C                     SETON                     76
     C                     ENDIF
      * Program level (2)
     C           PGL2      IFNE L2
     C                     SETON                     77
     C                     ENDIF
      * Program option (1)
     C           PGO2      IFNE O2
     C                     SETON                     78
     C                     ENDIF
      *
     C                     MOVEA*IN,70    INTEST 10
     C                     MOVE INTEST    INTNUM 100
      * If any indicators are set on then write record
     C           INTNUM    IFNE 0
     C                     WRITEUPCONPFF               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * Set values to write second record
     C                     MOVELCI        RECI
      *
     C                     MOVELN1        PGN1
      *
     C                     MOVELT1        PGT1
      *
     C                     MOVELL1        PGL1
      *
     C                     MOVELO1        PGO1
      *
     C                     MOVELN2        PGN2
      *
     C                     MOVELT2        PGT2
      *
     C                     MOVELL2        PGL2
      *
     C                     MOVELO2        PGO2
      *
     C                     WRITEUPCONPFF               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
      * Read next record
     C                     MOVEA'00000000'*IN,70
     C                     READ CONP1                  6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           ENDPGM    TAG
     C                     SETON                     LR
     C                     RETRN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      ************ CALL DBERRCTL IF INTERACTIVE PROGRAM ***************
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
