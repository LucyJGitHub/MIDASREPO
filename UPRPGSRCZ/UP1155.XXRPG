     H        1
      *****************************************************************
/*X*IA***OVRDBF*FILE(BAIC1)*TOFILE(UPBAICTPD)**************************                CUP019 CUP021
/*X*IB***OVRDBF*FILE(BAIC2)*TOFILE(UPBAICTPD)**************************                CUP019 CUP021
/*S*D****RPGBASE*******************************************************                       CUP021
/*O*R ***OVRDBF*BAIC1*SDBAICL0*****************************************                       CUP019
/*O*R ***OVRDBF*BAIC2*SDBAICL0*****************************************                       CUP019
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade module                                       *
      *                                                               *
      *  UP1155 - Compare SD CL configuration file                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.02 ----------------------------------------*
      *  Prev Amend No. CUP019             Date 10Sep02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CUP006 *CREATE     Date 14Dec99               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP019 - Use templates for Action Files.                     *
      *  CUP006  -  Make upgrade of DMLIB files easier                *
      *                                                               *
      *****************************************************************
      * ID F  C  H  L    FUNCTION OF INDICATORS
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write or chain fail
      *       70 - 81 Indicators for changed fields
      *
     FBAIC1   IF  E                    DISK         KINFSR *PSSR
     FBAIC2   IF  E           K        DISK         KINFSR *PSSR
     F********    @BAICL0                           KRENAMEBAICF                              CUP019
     F            SDBAICD0                          KRENAMEBAICF                              CUP019
     FUPBAICPDO   E                    DISK         KINFSR *PSSR
      *
      /EJECT
     E                    CPY@    1   1 80
      *
      * Fields with the same name will need to be renamed.
      *
     IBAICF
     I              CFBFUP                          BFUP
     I              CFFCDE                          FCDE
     I              CFFNDS                          FNDS
     I              CFPGDF                          PGDF
     I              CFPMIN                          PMIN
     I              CFPMAX                          PMAX
     I              CFDQLN                          DQLN
     I              CFPGPM                          PGPM
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *ENTRY    PLIST
     C                     PARM           MODE    1
      *
     C           COMP      KLIST
     C                     KFLD           CFBFUP
     C                     KFLD           CFFCDE
      *
     C                     READ BAIC1                  6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           *IN20     DOWEQ'0'
     C           COMP      CHAINBAICF                3069
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * If record not found then record has been deleted or added
      *  depending on mode
     C           *IN30     IFEQ *ON
     C                     MOVE MODE      CFACTC
     C                     WRITEUPBAICD0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
      * Need to find differences but this should only be run once;
      *  run only if mode is 'Delete'
     C           MODE      IFEQ 'D'
      *
      * If record found then look for any differences; only look in
      *  fields that have data all of the time.
     C           *IN30     IFEQ *OFF
     C                     MOVE 'A'       CFACTC
      * Banking function description
     C           CFFNDS    IFNE FNDS
     C                     SETON                     70
     C                     ENDIF
      * Default number of pgm start
     C           CFPGDF    IFNE PGDF
     C                     SETON                     71
     C                     ENDIF
      * Min number of programs allowed
     C           CFPMIN    IFNE PMIN
     C                     SETON                     72
     C                     ENDIF
      * Max number of programs allowed
     C           CFPMAX    IFNE PMAX
     C                     SETON                     73
     C                     ENDIF
      * Programs data queue length
     C           CFDQLN    IFNE DQLN
     C                     SETON                     74
     C                     ENDIF
      * Programs parameters
     C           CFPGPM    IFNE PGPM
     C                     SETON                     75
     C                     ENDIF
      *
     C                     MOVEA*IN,70    INTEST  6
     C                     MOVE INTEST    INTNUM  60
      * If any indicators are set on then write record
     C           INTNUM    IFNE 0
     C                     WRITEUPBAICD0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * Set values to write second record
     C                     MOVELFNDS      CFFNDS
     C                     MOVELPGDF      CFPGDF
     C                     MOVELPMIN      CFPMIN
     C                     MOVELPMAX      CFPMAX
     C                     MOVELDQLN      CFDQLN
     C                     MOVELPGPM      CFPGPM
      *
     C                     WRITEUPBAICD0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
      * Read next record
     C                     MOVEA'000000'  *IN,70
     C                     READ BAIC1                  6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           ENDPGM    TAG
     C                     SETON                     LR
     C                     RETRN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
