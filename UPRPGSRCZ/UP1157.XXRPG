     H        1
      *****************************************************************
/*S*D****RPGBASE*******************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade module                                       *
      *                                                               *
      *  UP1157 - Process Report SD CL configuration file             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. CUP006 *CREATE     Date 14DEC99               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP006  -  Make upgrade of DTALIB files easier               *
      *                                                               *
      *****************************************************************
      * ID F  C  H  L    FUNCTION OF INDICATORS
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write, update or delete fail
      *
     FUPBAICL0IF  E           K        DISK         KINFSR *PSSR
     FSDBAICL0UF  E           K        DISK         KINFSR *PSSR A
     F            @BAICL0                           KRENAMEBAICF
      *
      /EJECT
      * Array containing Copyright statement
     E                    CPY@    1   1 80
      *
      * Fields with the same name will need to be renamed.
     IBAICF
     I              CFBFUP                          BFUP
     I              CFFCDE                          FCDE
     I              CFFNDS                          FNDS
     I              CFPGDF                          PGDF
     I              CFPMIN                          PMIN
     I              CFPMAX                          PMAX
     I              CFDQLN                          DQLN
     I              CFPGPM                          PGPM
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           COMP      KLIST
     C                     KFLD           CFBFUP
     C                     KFLD           CFFCDE
      *
     C           *LOVAL    SETLLUPBAICL0
      *
     C                     READ UPBAICL0               6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
     C           *IN20     DOWEQ'0'
      *
     C           CFACTC    IFEQ 'I'
     C                     EXSR SETVAL
     C                     WRITEBAICF                  69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     ENDIF
      *
     C           CFACTC    IFEQ 'D'
     C           COMP      CHAINSDBAICL0             3069
     C           *IN69     IFEQ *ON
     C           *IN30     OREQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
      *
     C                     ELSE
     C                     EXSR SETVAL
     C                     DELETBAICF                  69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
      * If Action code = AMEND then read next record
     C           CFACTC    IFEQ 'A'
     C                     MOVELCFBFUP    BFUPX  10
     C                     MOVELCFFCDE    FCDEX   3
     C                     READ UPBAICL0               6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
      * If Banking function pgm and SD/RTS function code don't match
      *  - or if EOF indicator is on
     C           CFBFUP    IFNE BFUPX
     C           CFFCDE    ANDNEFCDEX
     C           *IN20     OREQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
     C           COMP      CHAINSDBAICL0             3069
     C           *IN69     IFEQ *ON
     C           *IN30     OREQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
      *
     C                     ELSE
      *
     C                     EXSR SETVAL
     C                     UPDATBAICF                  69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     READ UPBAICL0               6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           ENDPGM    TAG
     C                     SETON                     LR
     C                     RETRN
      *
      *****************************************************************
      *
      * SETVAL subroutine - set values for deletes/writes/updates
      *
      *****************************************************************
     C           SETVAL    BEGSR
      * Set values to update record
     C                     MOVELCFBFUP    BFUP
     C                     MOVELCFFCDE    FCDE
     C                     MOVELCFFNDS    FNDS
     C                     MOVELCFPGDF    PGDF
     C                     MOVELCFPMIN    PMIN
     C                     MOVELCFPMAX    PMAX
     C                     MOVELCFDQLN    DQLN
     C                     MOVELCFPGPM    PGPM
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
