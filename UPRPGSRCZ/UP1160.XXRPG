     H        1
      *****************************************************************
/*X*IA***OVRDBF*FILE(BAIP1)*TOFILE(UPBAIPTPD)**************************                CUP019 CUP021
/*X*IB***OVRDBF*FILE(BAIP2)*TOFILE(UPBAIPTPD)**************************                CUP019 CUP021
/*S*D****RPGBASE*******************************************************                       CUP021
/*O*R ***OVRDBF*BAIP1*SDBAIPL0*****************************************                       CUP019
/*O*R ***OVRDBF*BAIP2*SDBAIPL0*****************************************                       CUP019
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade module                                       *
      *                                                               *
      *  UP1160 - Compare SD CI products and communications           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.02 ----------------------------------------*
      *  Prev Amend No. CUP019             Date 10Sep02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CUP006             Date 15Dec99               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP019 - Use templates for Action Files.                     *
      *  CUP006  -  Make upgrade of DTALIB files easier               *
      *                                                               *
      *****************************************************************
      * ID F  C  H  L    FUNCTION OF INDICATORS
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write or chain fail
      *       70 - 81 Indicators for changed fields
      *
     FBAIP1   IF  E                    DISK         KINFSR *PSSR
     FBAIP2   IF  E           K        DISK         KINFSR *PSSR
     F********    @BAIPL0                           KRENAMEBAIPF                              CUP019
     F            SDBAIPD0                          KRENAMEBAIPF                              CUP019
     FUPBAIPPDO   E                    DISK         KINFSR *PSSR
      *
      /EJECT
     E                    CPY@    1   1 80
      *
      * Fields with the same name will need to be renamed.
      *
     IBAIPF
     I              FZPROD                          PROD
     I              FZCOMP                          COMP
     I              FZPDSC                          PDSC
     I              FZCPDS                          CPDS
     I              FZCOMM                          COMM
     I              FZCONV                          CONV
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *ENTRY    PLIST
     C                     PARM           MODE    1
      *
     C           KCOMP     KLIST
     C                     KFLD           FZPROD
     C                     KFLD           FZCOMP
      *
     C                     READ BAIP1                  6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           *IN20     DOWEQ'0'
     C           KCOMP     CHAINBAIPF                3069
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * If record not found then record has been deleted or added
      *  depending on mode
     C           *IN30     IFEQ *ON
     C                     MOVE MODE      FZACTC
     C                     WRITEUPBAIPD0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
      * Need to find differences but this should only be run once;
      *  run only if mode is 'Delete'
     C           MODE      IFEQ 'D'
      *
      * If record found then look for any differences; only look in
      *  fields that have data all of the time.
     C           *IN30     IFEQ *OFF
     C                     MOVE 'A'       FZACTC
      * Retail banking hall description
     C           FZPDSC    IFNE PDSC
     C                     SETON                     70
     C                     ENDIF
      * Comms protocol description
     C           FZCPDS    IFNE CPDS
     C                     SETON                     71
     C                     ENDIF
      * Communications program
     C           FZCOMM    IFNE COMM
     C                     SETON                     72
     C                     ENDIF
      * No. Convs. for comms pgm
     C           FZCONV    IFNE CONV
     C                     SETON                     73
     C                     ENDIF
      *
     C                     MOVEA*IN,70    INTEST  4
     C                     MOVE INTEST    INTNUM  40
      * If any indicators are set on then write record
     C           INTNUM    IFNE 0
     C                     WRITEUPBAIPD0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * Set values to write second record
     C                     MOVELPDSC      FZPDSC
      *
     C                     MOVELCPDS      FZCPDS
      *
     C                     MOVELCOMM      FZCOMM
      *
     C                     MOVELCONV      FZCONV
      *
     C                     WRITEUPBAIPD0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
      * Read next record
     C                     MOVEA'0000'    *IN,70
     C                     READ BAIP1                  6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           ENDPGM    TAG
     C                     SETON                     LR
     C                     RETRN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
