     H        1
      *****************************************************************
/*X*IA***OVRDBF*FILE(BAIT1)*TOFILE(UPBAITTPD)**************************                CUP019 CUP021
/*X*IB***OVRDBF*FILE(BAIT2)*TOFILE(UPBAITTPD)**************************                CUP019 CUP021
/*S*D****RPGBASE*******************************************************                       CUP021
/*O*R ***OVRDBF*BAIT1*SDBAITL0*****************************************                       CUP019
/*O*R ***OVRDBF*BAIT2*SDBAITL0*****************************************                       CUP019
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade module                                       *
      *                                                               *
      *  UP1175 - Compare SD transaction types                        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.02 ----------------------------------------*
      *  Prev Amend No. CUP019             Date 10Sep02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CUP006 *CREATE     Date 15DEC99               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP019 - Use templates for Action Files.                     *
      *  CUP006  -  Make upgrade of DMLIB files easier                *
      *                                                               *
      *****************************************************************
      * ID F  C  H  L    FUNCTION OF INDICATORS
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write or chain fail
      *       70 - 81 Indicators for changed fields
      *
     FBAIT1   IF  E                    DISK         KINFSR *PSSR
     FBAIT2   IF  E           K        DISK         KINFSR *PSSR
     F********    @BAITL0                           KRENAMEBAITF                              CUP019
     F            SDBAITD0                          KRENAMEBAITF                              CUP019
     FUPBAITPDO   E                    DISK         KINFSR *PSSR
      *
      /EJECT
     E                    CPY@    1   1 80
      *
      * Fields with the same name will need to be renamed.
      *
     IBAITF
     I              FXBAIT                          BAIT
     I              FXTXDS                          TXDS
     I              FXTTFC                          TTFC
     I              FXDQNM                          DQNM
     I              FXDQLN                          DQLN
     I              FXRVFN                          RVFN
     I              FXRVDQ                          RVDQ
     I              FXRVDL                          RVDL
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *ENTRY    PLIST
     C                     PARM           MODE    1
      *
      *
     C                     READ BAIT1                  6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           *IN20     DOWEQ'0'
     C           FXBAIT    CHAINBAITF                3069
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * If record not found then record has been deleted or added
      *  depending on mode
     C           *IN30     IFEQ *ON
     C                     MOVE MODE      FXACTC
     C                     WRITEUPBAITD0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
      * Need to find differences but this should only be run once;
      *  run only if mode is 'Delete'
     C           MODE      IFEQ 'D'
      *
      * If record found then look for any differences; only look in
      *  fields that have data all of the time.
     C           *IN30     IFEQ *OFF
     C                     MOVE 'A'       FXACTC
      * BAI Transaction type desc.
     C           FXTXDS    IFNE TXDS
     C                     SETON                     70
     C                     ENDIF
      * Transaction Type function code
     C           FXTTFC    IFNE TTFC
     C                     SETON                     71
     C                     ENDIF
      * Data queue for process
     C           FXDQNM    IFNE DQNM
     C                     SETON                     72
     C                     ENDIF
      * pgms data queue length
     C           FXDQLN    IFNE DQLN
     C                     SETON                     73
     C                     ENDIF
      * reversal function code
     C           FXRVFN    IFNE RVFN
     C                     SETON                     74
     C                     ENDIF
      * reversal data queue
     C           FXRVDQ    IFNE RVDQ
     C                     SETON                     75
     C                     ENDIF
      * reversal data queue length
     C           FXRVDL    IFNE RVDL
     C                     SETON                     76
     C                     ENDIF
      *
     C                     MOVEA*IN,70    INTEST  7
     C                     MOVE INTEST    INTNUM  70
      * If any indicators are set on then write record
     C           INTNUM    IFNE 0
     C                     WRITEUPBAITD0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * Set values to write second record
     C                     MOVELTXDS      FXTXDS
     C                     MOVELTTFC      FXTTFC
     C                     MOVELDQNM      FXDQNM
     C                     MOVELDQLN      FXDQLN
     C                     MOVELRVFN      FXRVFN
     C                     MOVELRVDQ      FXRVDQ
     C                     MOVELRVDL      FXRVDL
      *
     C                     WRITEUPBAITD0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
      * Read next record
     C                     MOVEA'0000000' *IN,70
     C                     READ BAIT1                  6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           ENDPGM    TAG
     C                     SETON                     LR
     C                     RETRN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      ************ CALL DBERRCTL IF INTERACTIVE PROGRAM ***************
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
