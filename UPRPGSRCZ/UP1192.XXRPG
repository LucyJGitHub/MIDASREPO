     H        1
      *****************************************************************
/*S*D****RPGBASE*******************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade module                                       *
      *                                                               *
      *  UP1192 - Process of SD RTS Function codes file               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. CUP006 *CREATE     Date 22Dec99               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP006  -  Make upgrade of DMLIB files easier                *
      *                                                               *
      *****************************************************************
      * ID F  C  H  L    FUNCTION OF INDICATORS
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write, update or delete fail
      *
     FUPFNCDL0IF  E           K        DISK         KINFSR *PSSR
     FSDFNCDL0UF  E           K        DISK         KINFSR *PSSR A
     F            @FNCDL0                           KRENAMEFNCDF
      *
      /EJECT
      * Array containing Copyright statement
     E                    CPY@    1   1 80
      *
      * Fields with the same name will need to be renamed.
     IFNCDF
     I              FQFNCD                          FNCD
     I              FQRNNR                          RNNR
     I              FQDRC1                          DRC1
     I              FQDRC2                          DRC2
     I              FQCRC1                          CRC1
     I              FQCRC2                          CRC2
     I              FQVWIN                          VWIN
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      *
     C           *LOVAL    SETLLUPFNCDL0
      *
     C                     READ UPFNCDL0               6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
     C           *IN20     DOWEQ'0'
      *
     C           FQACTC    IFEQ 'I'
     C                     EXSR SETVAL
     C                     WRITEFNCDF                  69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     ENDIF
      *
     C           FQACTC    IFEQ 'D'
     C           FQFNCD    CHAINSDFNCDL0             3069
     C           *IN69     IFEQ *ON
     C           *IN30     OREQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
      *
     C                     ELSE
     C                     EXSR SETVAL
     C                     DELETFNCDF                  69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
      * If Action code = AMEND then read next record
     C           FQACTC    IFEQ 'A'
     C                     MOVELFQFNCD    FNCDX   3
     C                     READ UPFNCDL0               6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
      * If the Function code does not match
      * - or if EOF indicator is on
     C           FQFNCD    IFNE FNCDX
     C           *IN20     OREQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
     C           FQFNCD    CHAINSDFNCDL0             3069
     C           *IN69     IFEQ *ON
     C           *IN30     OREQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
      *
     C                     ELSE
      *
     C                     EXSR SETVAL
     C                     UPDATFNCDF                  69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     READ UPFNCDL0               6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     GOTO ENDPGM
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           ENDPGM    TAG
     C                     SETON                     LR
     C                     RETRN
      *
      *****************************************************************
      *
      * SETVAL subroutine - set values for deletes/writes/updates
      *
      *****************************************************************
     C           SETVAL    BEGSR
      * Set values to update record
     C                     MOVELFQFNCD    FNCD
     C                     MOVELFQRNNR    RNNR
     C                     MOVELFQDRC1    DRC1
     C                     MOVELFQDRC2    DRC2
     C                     MOVELFQCRC1    CRC1
     C                     MOVELFQCRC2    CRC2
     C                     MOVELFQVWIN    VWIN
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
