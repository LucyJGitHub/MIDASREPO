     H        1
      *****************************************************************
/*X*IA***OVRDBF*FILE(MMID1)*TOFILE(UPMMIDTPD)**************************                CUP019 CUP021
/*X*IB***OVRDBF*FILE(MMID2)*TOFILE(UPMMIDTPD)**************************                CUP019 CUP021
/*S*D****RPGBASE*******************************************************                       CUP021
/*O*R ***OVRDBF*MMID1*SDMMIDL0*****************************************                       CUP019
/*O*R ***OVRDBF*MMID2*SDMMIDL0*****************************************                       CUP019
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade module                                       *
      *                                                               *
      *  UP1185 - Compare SD Midas modules information                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.02 ----------------------------------------*
      *  Prev Amend No. CUP019             Date 10Sep02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CUP006 *CREATE     Date 22DEC99               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP019 - Use templates for Action Files.                     *
      *  CUP006  -  Make upgrade of DMLIB files easier                *
      *                                                               *
      *****************************************************************
      * ID F  C  H  L    FUNCTION OF INDICATORS
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write or chain fail
      *       70 - 85 Indicators for changed fields
      *
     FMMID1   IF  E                    DISK         KINFSR *PSSR
     FMMID2   IF  E           K        DISK         KINFSR *PSSR
     F********    @MMIDL0                           KRENAMEMMIDF                              CUP019
     F            SDMMIDD0                          KRENAMEMMIDF                              CUP019
     FUPMMIDPDO   E                    DISK         KINFSR *PSSR
      *
      /EJECT
     E                    CPY@    1   1 80
      *
      * Fields with the same name will need to be renamed.
      *
     IMMIDF
     I              FOMMSN                          MMSN
     I              FOTYLC                          TYLC
     I              FOLCD                           LCD
     I              FOUNIQ                          UNIQ
     I              FOMMIN                          MMIN
     I              FOMODD                          MODD
     I              FOOBJN                          OBJN
     I              FOMODF                          MODF
     I              FOMODP                          MODP
     I              FOCOBM                          COBM
     I              FORCFM                          RCFM
     I              FOSPFM                          SPFM
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *ENTRY    PLIST
     C                     PARM           MODE    1
      *
      *
     C                     READ MMID1                  6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           *IN20     DOWEQ'0'
     C           FOMMSN    CHAINMMIDF                3069
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * If record not found then record has been deleted or added
      *  depending on mode
     C           *IN30     IFEQ *ON
     C                     MOVE MODE      FOACTC
     C                     WRITEUPMMIDD0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
      * Need to find differences but this should only be run once;
      *  run only if mode is 'Delete'
     C           MODE      IFEQ 'D'
      *
      * If record found then look for any differences; only look in
      *  fields that have data all of the time.
     C           *IN30     IFEQ *OFF
     C                     MOVE 'A'       FOACTC
      * Module identifier
     C           FOUNIQ    IFNE UNIQ
     C                     SETON                     70
     C                     ENDIF
      * Midas Module installed
     C           FOMMIN    IFNE MMIN
     C                     SETON                     71
     C                     ENDIF
      * Module description
     C           FOMODD    IFNE MODD
     C                     SETON                     72
     C                     ENDIF
      * Name of obj holding data
     C           FOOBJN    IFNE OBJN
     C                     SETON                     73
     C                     ENDIF
      * Field name
     C           FOMODF    IFNE MODF
     C                     SETON                     74
     C                     ENDIF
      * Position on object
     C           FOMODP    IFNE MODP
     C                     SETON                     75
     C                     ENDIF
      * Module identifier - COB
     C           FOCOBM    IFNE COBM
     C                     SETON                     76
     C                     ENDIF
      * Module identifier - RCF
     C           FORCFM    IFNE RCFM
     C                     SETON                     77
     C                     ENDIF
      * Module identifier - SPF
     C           FOSPFM    IFNE SPFM
     C                     SETON                     78
     C                     ENDIF
      *
     C                     MOVEA*IN,70    INTEST  9
     C                     MOVE INTEST    INTNUM  90
      * If any indicators are set on then write record
     C           INTNUM    IFNE 0
     C                     WRITEUPMMIDD0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * Set values to write second record
     C                     MOVELUNIQ      FOUNIQ
     C                     MOVELMMIN      FOMMIN
     C                     MOVELMODD      FOMODD
     C                     MOVELOBJN      FOOBJN
     C                     MOVELMODF      FOMODF
     C                     MOVELMODP      FOMODP
     C                     MOVELCOBM      FOCOBM
     C                     MOVELRCFM      FORCFM
     C                     MOVELSPFM      FOSPFM
      *
     C                     WRITEUPMMIDD0               69
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
      * Read next record
     C                     MOVEA'00000000'*IN,70
     C                     MOVEA'0'       *IN,78
     C                     READ MMID1                  6920
     C           *IN69     IFEQ *ON
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           ENDPGM    TAG
     C                     SETON                     LR
     C                     RETRN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      ************ CALL DBERRCTL IF INTERACTIVE PROGRAM ***************
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
