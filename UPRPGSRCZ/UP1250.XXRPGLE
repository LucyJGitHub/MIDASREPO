     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2002')
      *****************************************************************
/*X*IA***OVRDBF*FILE(INFM1)*TOFILE(UPINFMTPD)**************************                       CUP022
/*X*IB***OVRDBF*FILE(INFM2)*TOFILE(UPINFMTPD)**************************                       CUP022
/*X*IA***OVRDBF*FILE(INF21)*TOFILE(UPINFMTPD)**************************                       CUP022
/*X*IB***OVRDBF*FILE(INF22)*TOFILE(UPINFMTPD)**************************                       CUP022
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP1250 - Build Action File for MEINFMPD                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. BUG24370           Date 16Jun09               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CUP022             Date 18Mar04               *
      * Midas Release 4.01.03 ----------------------------------------*
      *                 CUP019 *CREATE     Date 30May02               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  BUG24370 - New fields tags & sequences are missing           *
      *  CUP022 - Change the name of the input files so that command  *
      *           ACTIONFILE (*NONE, *OVERRIDE) can be used to call   *
      *           this program.                                       *
      *  CUP019 - Creation of new set of Action File utilities.       *
      *                                                               *
      *****************************************************************
      * Function of indicators
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write or chain fail
      *       70 - 72 Indicators for changed fields
      *
     F*****INFM1     IF   E             DISK    INFSR(*PSSR)                                  CUP022
     F*****INFM2     IF   E           K DISK    INFSR(*PSSR)                                  CUP022
     FINF21     IF   E             DISK    INFSR(*PSSR)                                       CUP022
     FINF22     IF   E           K DISK    INFSR(*PSSR)                                       CUP022
     F                                     RENAME(@INFMPD:INFMF) PREFIX(X)
     FUPINF2PD  O    E             DISK    INFSR(*PSSR)
      *
      /EJECT
     D LDA           E DS           256    EXTNAME(LDA)
      *
      * Local data area for database error details
      * *LOCK IN LDA immediately before and OUT LDA immediately
      * after each database error handling.
      *
      *                                     134 141 DBFILE
      *       Defines:                      142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *
     D PSDS           SDS
      *
      * Program Status Data Structure.
      *
     D  PGM              *PROC
     D  WSID                 244    253
     D  USER                 254    263
      *
     C     *ENTRY        PLIST
     C                   PARM                    MODE              1
      *
     C     COMP          KLIST
     C                   KFLD                    FMNWRK
     C                   KFLD                    FMMTPY
     C                   KFLD                    FMMSEQ                                     BUG24370
     C                   KFLD                    FMMTAG
     C                   KFLD                    FMFTFD
      *
     C*****              READ      INFM1                                6920                  CUP022
     C                   READ      INF21                                6920                  CUP022
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C     *IN20         DOWEQ     '0'
     C     COMP          CHAIN     INFMF                              3069
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * If record not found then record has been deleted or added
      *  depending on mode.
     C     *IN30         IFEQ      *ON
     C                   MOVE      MODE          FMACTC
     C                   WRITE     UPINF2D0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
      * Need to find differences but this should only be run once;
      *  run only if mode is 'Delete'.
     C     MODE          IFEQ      'D'
      *
      * If record found then look for any differences; only look in
      *  fields that have data all of the time.
     C     *IN30         IFEQ      *OFF
     C                   MOVE      'A'           FMACTC
      * Special Instructions
     C     FMFTSP        IFNE      XFMFTSP
     C                   SETON                                        70
     C                   ENDIF
      * Narrative
     C     FMNARR        IFNE      XFMNARR
     C                   SETON                                        71
     C                   ENDIF
      * Record ID
     C     FMRECI        IFNE      XFMRECI
     C                   SETON                                        72
     C                   ENDIF
      *
     C                   MOVEA     *IN(70)       INTEST            3
     C                   MOVE      INTEST        INTNUM            3 0
      * If any indicators are set on then write record.
     C     INTNUM        IFNE      0
     C                   WRITE     UPINF2D0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * Set values to write second record.
     C                   MOVEL     XFMFTSP       FMFTSP
      *
     C                   MOVEL     XFMNARR       FMNARR
      *
     C                   MOVEL     XFMRECI       FMRECI
      *
     C                   WRITE     UPINF2D0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
     C                   ENDIF
      *
      * Read next record
     C                   MOVEA     '000'         *IN(70)
     C*****              READ      INFM1                                6920                  CUP022
     C                   READ      INF21                                6920                  CUP022
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C                   ENDDO
      *
     C     ENDPGM        TAG
     C                   SETON                                        LR
     C                   RETURN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR                                                  ** *PSSR SR **
      *
     C     @RUN          IFEQ      *BLANK
     C                   MOVE      'Y'           @RUN              1
     C                   DUMP
     C                   END
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
      *
      ********************************************************************
