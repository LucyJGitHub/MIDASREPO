     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2002')
      *****************************************************************
/*X*IA***OVRDBF*FILE(INMV1)*TOFILE(UPINMVTPD)**************************                       CUP021
/*X*IB***OVRDBF*FILE(INMV2)*TOFILE(UPINMVTPD)**************************                       CUP021
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP1255 - Build Action File for MEINMVPD                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.03 ----------------------------------------*
      *  Prev Amend No. CUP019 *CREATE     Date 30May02               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP019 - Creation of new set of Action File utilities.       *
      *                                                               *
      *****************************************************************
      * Function of indicators
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write or chain fail
      *       70 - 75 Indicators for changed fields
      *
     FINMV1     IF   E             DISK    INFSR(*PSSR)
     FINMV2     IF   E           K DISK    INFSR(*PSSR)
     F                                     RENAME(@INMVPD:INMVF) PREFIX(X)
     FUPINMVPD  O    E             DISK    INFSR(*PSSR)
      *
      /EJECT
     D LDA           E DS           256    EXTNAME(LDA)
      *
      * Local data area for database error details
      * *LOCK IN LDA immediately before and OUT LDA immediately
      * after each database error handling.
      *
      *                                     134 141 DBFILE
      *       Defines:                      142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *
     D PSDS           SDS
      *
      * Program Status Data Structure.
      *
     D  PGM              *PROC
     D  WSID                 244    253
     D  USER                 254    263
      *
     C     *ENTRY        PLIST
     C                   PARM                    MODE              1
      *
     C     COMP          KLIST
     C                   KFLD                    MVNWRK
     C                   KFLD                    MVMTPY
     C                   KFLD                    MVSNDK
      *
     C                   READ      INMV1                                6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C     *IN20         DOWEQ     '0'
     C     COMP          CHAIN     INMVF                              3069
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * If record not found then record has been deleted or added
      *  depending on mode.
     C     *IN30         IFEQ      *ON
     C                   MOVE      MODE          MVACTC
     C                   WRITE     UPINMVD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
      * Need to find differences but this should only be run once;
      *  run only if mode is 'Delete'.
     C     MODE          IFEQ      'D'
      *
      * If record found then look for any differences; only look in
      *  fields that have data all of the time.
     C     *IN30         IFEQ      *OFF
     C                   MOVE      'A'           MVACTC
      * Record ID
     C     MVRECI        IFNE      XMVRECI
     C                   SETON                                        70
     C                   ENDIF
      * Access Point
     C     MVACPT        IFNE      XMVACPT
     C                   SETON                                        71
     C                   ENDIF
      * Include/Exclude
     C     MVINEX        IFNE      XMVINEX
     C                   SETON                                        72
     C                   ENDIF
      * Branch Code
     C     MVBRCA        IFNE      XMVBRCA
     C                   SETON                                        73
     C                   ENDIF
      * Payment Message Flag
     C     MVPMFG        IFNE      XMVPMFG
     C                   SETON                                        74
     C                   ENDIF
      * Narrative
     C     MVNARR        IFNE      XMVNARR
     C                   SETON                                        75
     C                   ENDIF
      *
     C                   MOVEA     *IN(70)       INTEST            5
     C                   MOVE      INTEST        INTNUM            5 0
      * If any indicators are set on then write record.
     C     INTNUM        IFNE      0
     C                   WRITE     UPINMVD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * Set values to write second record.
     C                   MOVEL     XMVRECI       MVRECI
     C                   MOVEL     XMVACPT       MVACPT
     C                   MOVEL     XMVINEX       MVINEX
     C                   MOVEL     XMVBRCA       MVBRCA
     C                   MOVEL     XMVPMFG       MVPMFG
     C                   MOVEL     XMVNARR       MVNARR
     C                   MOVEL     XMVAJOB       MVAJOB
     C                   MOVEL     XMVAUSR       MVAUSR
     C                   MOVEL     XMVATIM       MVATIM
     C                   MOVEL     XMVARDT       MVARDT
     C                   MOVEL     XMVAACT       MVAACT
     C                   MOVEL     XMVRDNB       MVRDNB
      *
     C                   WRITE     UPINMVD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
     C                   ENDIF
      *
      * Read next record
     C                   MOVEA     '00000'       *IN(70)
     C                   READ      INMV1                                6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C                   ENDDO
      *
     C     ENDPGM        TAG
     C                   SETON                                        LR
     C                   RETURN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR                                                  ** *PSSR SR **
      *
     C     @RUN          IFEQ      *BLANK
     C                   MOVE      'Y'           @RUN              1
     C                   DUMP
     C                   END
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
      *
      ********************************************************************
