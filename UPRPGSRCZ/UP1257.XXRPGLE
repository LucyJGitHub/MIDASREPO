     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2002')
      *****************************************************************
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP1257 - Process Action File for MEINMVPD                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.03 ----------------------------------------*
      *  Prev Amend No. CUP019 *CREATE     Date 30May02               *
      *                                                               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP019 - Creation of new set of Action File utilities.       *
      *                                                               *
      *****************************************************************
      * Function of indicators
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write, update or delete fail
      *
     FUPINMVL0  IF   E           K DISK    INFSR(*PSSR)
     FMEINMVL0  UF A E           K DISK    INFSR(*PSSR)
     F                                     RENAME(@INMVL0:INMVF)
      /EJECT
      *
     D LDA           E DS           256    EXTNAME(LDA)
      *
      * Local data area for database error details
      * *LOCK IN LDA immediately before and OUT LDA immediately
      * after each database error handling.
      *
      *                                     134 141 DBFILE
      *        Defines:                     142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *
     D PSDS           SDS
      *
      ** Program Status Data Structure.
      *
     D  PGM              *PROC
     D  WSID                 244    253
     D  USER                 254    263
      *
      * Fields with the same name will need to be renamed.
     IINMVF
     I              MVNWRK                      NWRK
     I              MVMTPY                      MTPY
     I              MVSNDK                      SNDK
     I              MVRECI                      RECI
     I              MVACPT                      ACPT
     I              MVINEX                      INEX
     I              MVBRCA                      BRCA
     I              MVPMFG                      PMFG
     I              MVNARR                      NARR
     I              MVAJOB                      AJOB
     I              MVAUSR                      AUSR
     I              MVATIM                      ATIM
     I              MVARDT                      ARDT
     I              MVAACT                      AACT
     I              MVRDNB                      RDNB
      *
     C     COMP          KLIST
     C                   KFLD                    MVNWRK
     C                   KFLD                    MVMTPY
     C                   KFLD                    MVSNDK
      *
     C     *LOVAL        SETLL     UPINMVL0
      *
     C                   READ      UPINMVL0                             6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C     *IN20         DOWEQ     '0'
      *
     C     MVACTC        IFEQ      'I'
     C                   EXSR      SETVAL
     C                   WRITE     INMVF                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
     C                   ENDIF
      *
     C     MVACTC        IFEQ      'D'
     C     COMP          CHAIN     MEINMVL0                           3069
     C     *IN69         IFEQ      *ON
     C     *IN30         OREQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
      *
     C                   ELSE
     C                   DELETE    INMVF                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
      *
      * If Action code = AMEND then read next record.
     C     MVACTC        IFEQ      'A'
     C                   MOVEL     MVNWRK        NWRKX             6
     C                   MOVEL     MVMTPY        MTPYX             3
     C                   MOVEL     MVSNDK        SNDKX            20
     C                   READ      UPINMVL0                             6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
      * If key fields do not match exit
      *  - or if EOF indicator is on.
     C     MVNWRK        IFNE      NWRKX
     C     MVMTPY        ANDNE     MTPYX
     C     MVSNDK        ANDNE     SNDKX
     C     *IN20         OREQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C     COMP          CHAIN     MEINMVL0                           3069
     C     *IN69         IFEQ      *ON
     C     *IN30         OREQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
      *
     C                   ELSE
      *
     C                   EXSR      SETVAL
     C                   UPDATE    INMVF                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
      *
     C                   READ      UPINMVL0                             6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C                   ENDDO
      *
     C     ENDPGM        TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      *****************************************************************
      *                                                               *
      * SETVAL subroutine - set values for deletes/writes/updates     *
      *                                                               *
      *****************************************************************
     C     SETVAL        BEGSR
      * Set values to update record.
     C                   MOVEL     MVNWRK        NWRK
     C                   MOVEL     MVMTPY        MTPY
     C                   MOVEL     MVSNDK        SNDK
     C                   MOVEL     MVRECI        RECI
     C                   MOVEL     MVACPT        ACPT
     C                   MOVEL     MVINEX        INEX
     C                   MOVEL     MVBRCA        BRCA
     C                   MOVEL     MVPMFG        PMFG
     C                   MOVEL     MVNARR        NARR
     C                   MOVEL     MVAJOB        AJOB
     C                   MOVEL     MVAUSR        AUSR
     C                   MOVEL     MVATIM        ATIM
     C                   MOVEL     MVARDT        ARDT
     C                   MOVEL     MVAACT        AACT
     C                   MOVEL     MVRDNB        RDNB
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR                                                  ** *PSSR SR **
      *
     C     @RUN          IFEQ      *BLANK
     C                   MOVE      'Y'           @RUN              1
     C                   DUMP
     C                   END
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
      ********************************************************************
