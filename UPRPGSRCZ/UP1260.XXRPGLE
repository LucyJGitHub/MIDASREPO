     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2002')
      *****************************************************************
/*X*IA***OVRDBF*FILE(ISO21)*TOFILE(UPISO2TPD)**************************                       CUP021
/*X*IB***OVRDBF*FILE(ISO22)*TOFILE(UPISO2TPD)**************************                       CUP021
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP1260 - Build Action File for MEISOCPD                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.03 ----------------------------------------*
      *  Prev Amend No. CUP019 *CREATE     Date 30May02               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP019 - Creation of new set of Action File utilities.       *
      *                                                               *
      *****************************************************************
      * Function of indicators
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write or chain fail
      *       70 - 72 Indicators for changed fields
      *
     FISO21     IF   E             DISK    INFSR(*PSSR)
     FISO22     IF   E           K DISK    INFSR(*PSSR)
     F                                     RENAME(MEISOCD0:ISO2F) PREFIX(X)
     FUPISO2PD  O    E             DISK    INFSR(*PSSR)
      *
      /EJECT
     D LDA           E DS           256    EXTNAME(LDA)
      *
      * Local data area for database error details
      * *LOCK IN LDA immediately before and OUT LDA immediately
      * after each database error handling.
      *
      *                                     134 141 DBFILE
      *       Defines:                      142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *
     D PSDS           SDS
      *
      * Program Status Data Structure.
      *
     D  PGM              *PROC
     D  WSID                 244    253
     D  USER                 254    263
      *
     C     *ENTRY        PLIST
     C                   PARM                    MODE              1
      *
     C     COMP          KLIST
     C                   KFLD                    M3ISOC
      *
     C                   READ      ISO21                                6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C     *IN20         DOWEQ     '0'
     C     COMP          CHAIN     ISO2F                              3069
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * If record not found then record has been deleted or added
      *  depending on mode.
     C     *IN30         IFEQ      *ON
     C                   MOVE      MODE          M3ACTC
     C                   WRITE     UPISO2D0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
      * Need to find differences but this should only be run once;
      *  run only if mode is 'Delete'.
     C     MODE          IFEQ      'D'
      *
      * If record found then look for any differences; only look in
      *  fields that have data all of the time.
     C     *IN30         IFEQ      *OFF
     C                   MOVE      'A'           M3ACTC
      * ISO Country Code Description
     C     M3ISOD        IFNE      XM3ISOD
     C                   SETON                                        70
     C                   ENDIF
      * Supports Charge Transparency?
     C     M3SCHT        IFNE      XM3SCHT
     C                   SETON                                        71
     C                   ENDIF
      * EEA Member
      * This field has been added so it may not be present at earlier releases; if
      *  it is not found in either file then do not compare.
     C                   MOVEL     'M3EEAM'      FIELD            10
     C                   MOVE      *BLANKS       STATUS            5
     C                   CALL      'UP0891'
     C                   PARM                    FIELD
     C                   PARM                    STATUS
      * If found in one only then set field to value of the found field
     C     STATUS        IFEQ      '*OLD'
     C                   MOVEL     M3EEAM        XM3EEAM
     C                   ENDIF
     C     STATUS        IFEQ      '*NEW'
     C                   MOVEL     XM3EEAM       M3EEAM
     C                   ENDIF
     C     M3EEAM        IFNE      XM3EEAM
     C                   SETON                                        72
     C                   ENDIF
      *
     C                   MOVEA     *IN(70)       INTEST            3
     C                   MOVE      INTEST        INTNUM            3 0
      * If any indicators are set on then write record.
     C     INTNUM        IFNE      0
     C                   WRITE     UPISO2D0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * Set values to write second record.
     C                   MOVEL     XM3ISOD       M3ISOD
     C                   MOVEL     XM3SCHT       M3SCHT
     C                   MOVEL     XM3ACJN       M3ACJN
     C                   MOVEL     XM3ACUN       M3ACUN
     C                   MOVEL     XM3ACTM       M3ACTM
     C                   MOVEL     XM3ACDA       M3ACDA
     C                   MOVEL     XM3ACTY       M3ACTY
     C                   MOVEL     XM3EEAM       M3EEAM
      *
     C                   WRITE     UPISO2D0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
     C                   ENDIF
      *
      * Read next record
     C                   MOVEA     '000'         *IN(70)
     C                   READ      ISO21                                6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C                   ENDDO
      *
     C     ENDPGM        TAG
     C                   SETON                                        LR
     C                   RETURN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR                                                  ** *PSSR SR **
      *
     C     @RUN          IFEQ      *BLANK
     C                   MOVE      'Y'           @RUN              1
     C                   DUMP
     C                   END
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
      *
      ********************************************************************
