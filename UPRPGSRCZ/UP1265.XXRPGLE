     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2002')
      *****************************************************************
/*X*IA***OVRDBF*FILE(COMD1)*TOFILE(UPCOMDTPD)**************************                       CUP021
/*X*IB***OVRDBF*FILE(COMD2)*TOFILE(UPCOMDTPD)**************************                       CUP021
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP1265 - Build Action File for MXCOMDPD                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.03 ----------------------------------------*
      *  Prev Amend No. CUP019 *CREATE     Date 30May02               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP019 - Creation of new set of Action File utilities.       *
      *                                                               *
      *****************************************************************
      * Function of indicators
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write or chain fail
      *       70 - 79 Indicators for changed fields
      *
     FCOMD1     IF   E             DISK    INFSR(*PSSR)
     FCOMD2     IF   E           K DISK    INFSR(*PSSR)
     F                                     RENAME(MXCOMDP0:COMDF) PREFIX(X)
     FUPCOMDPD  O    E             DISK    INFSR(*PSSR)
      *
      /EJECT
     D LDA           E DS           256    EXTNAME(LDA)
      *
      * Local data area for database error details
      * *LOCK IN LDA immediately before and OUT LDA immediately
      * after each database error handling.
      *
      *                                     134 141 DBFILE
      *       Defines:                      142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *
     D PSDS           SDS
      *
      * Program Status Data Structure.
      *
     D  PGM              *PROC
     D  WSID                 244    253
     D  USER                 254    263
      *
     C     *ENTRY        PLIST
     C                   PARM                    MODE              1
      *
     C     COMP          KLIST
     C                   KFLD                    CDCOMI
     C                   KFLD                    CDEXTT
      *
     C                   READ      COMD1                                6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C     *IN20         DOWEQ     '0'
     C     COMP          CHAIN     COMDF                              3069
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * If record not found then record has been deleted or added
      *  depending on mode.
     C     *IN30         IFEQ      *ON
     C                   MOVE      MODE          CDACTC
     C                   WRITE     UPCOMDD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
      * Need to find differences but this should only be run once;
      *  run only if mode is 'Delete'.
     C     MODE          IFEQ      'D'
      *
      * If record found then look for any differences; only look in
      *  fields that have data all of the time.
     C     *IN30         IFEQ      *OFF
     C                   MOVE      'A'           CDACTC
      * Description
     C     CDDESC        IFNE      XCDDESC
     C                   SETON                                        70
     C                   ENDIF
      * Data Group
     C     CDDTAG        IFNE      XCDDTAG
     C                   SETON                                        71
     C                   ENDIF
      * Message Type
     C     CDMSGT        IFNE      XCDMSGT
     C                   SETON                                        72
     C                   ENDIF
      * Message Queue
     C     CDMSGQ        IFNE      XCDMSGQ
     C                   SETON                                        73
     C                   ENDIF
      * COMs per MQ Msg
     C     CDCOMM        IFNE      XCDCOMM
     C                   SETON                                        74
     C                   ENDIF
      * Log Performance (Y/N)
     C     CDLOGP        IFNE      XCDLOGP
     C                   SETON                                        75
     C                   ENDIF
      * Message Transaction Report Type
     C     CDMTRT        IFNE      XCDMTRT
     C                   SETON                                        76
     C                   ENDIF
      * Export Frequency
     C     CDEXFQ        IFNE      XCDEXFQ
     C                   SETON                                        77
     C                   ENDIF
      * Enabled
     C     CDENAB        IFNE      XCDENAB
     C                   SETON                                        78
     C                   ENDIF
      * Extract Member
     C     CDXMEM        IFNE      XCDXMEM
     C                   SETON                                        79
     C                   ENDIF
      *
     C                   MOVEA     *IN(70)       INTEST           10
     C                   MOVE      INTEST        INTNUM           10 0
      * If any indicators are set on then write record.
     C     INTNUM        IFNE      0
     C                   WRITE     UPCOMDD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * Set values to write second record.
     C                   MOVEL     XCDDESC       CDDESC
     C                   MOVEL     XCDDTAG       CDDTAG
     C                   MOVEL     XCDMSGT       CDMSGT
     C                   MOVEL     XCDMSGQ       CDMSGQ
     C                   MOVEL     XCDCOMM       CDCOMM
     C                   MOVEL     XCDLOGP       CDLOGP
     C                   MOVEL     XCDMTRT       CDMTRT
     C                   MOVEL     XCDEXFQ       CDEXFQ
     C                   MOVEL     XCDEXDY       CDEXDY
     C                   MOVEL     XCDEXND       CDEXND
     C                   MOVEL     XCDENAB       CDENAB
     C                   MOVEL     XCDLDTX       CDLDTX
     C                   MOVEL     XCDLSTX       CDLSTX
     C                   MOVEL     XCDLETX       CDLETX
     C                   MOVEL     XCDNOTX       CDNOTX
     C                   MOVEL     XCDNTSX       CDNTSX
     C                   MOVEL     XCDXCTL       CDXCTL
     C                   MOVEL     XCDXMEM       CDXMEM
     C                   MOVEL     XCDLDTT       CDLDTT
     C                   MOVEL     XCDLSTT       CDLSTT
     C                   MOVEL     XCDLETT       CDLETT
     C                   MOVEL     XCDNOTT       CDNOTT
     C                   MOVEL     XCDNTST       CDNTST
     C                   MOVEL     XCDTCTL       CDTCTL
      *
     C                   WRITE     UPCOMDD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
     C                   ENDIF
      *
      * Read next record
     C                   MOVEA     '0000000000'  *IN(70)
     C                   READ      COMD1                                6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C                   ENDDO
      *
     C     ENDPGM        TAG
     C                   SETON                                        LR
     C                   RETURN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR                                                  ** *PSSR SR **
      *
     C     @RUN          IFEQ      *BLANK
     C                   MOVE      'Y'           @RUN              1
     C                   DUMP
     C                   END
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
      *
      ********************************************************************
