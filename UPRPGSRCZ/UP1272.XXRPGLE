     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2003')
      *****************************************************************
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP1272 - Process Action File for MXCOMFPD                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CUP022 *CREATE     Date 13Jan03               *
      *                                                               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP022 - Creation of new set of Action File utilities.       *
      *                                                               *
      *****************************************************************
      * Function of indicators
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write, update or delete fail
      *
     FUPCOMFL0  IF   E           K DISK    INFSR(*PSSR)
     FMXCOMFL2  UF A E           K DISK    INFSR(*PSSR)
     F                                     RENAME(MXCOMFP0:COMFF) PREFIX(X)
      /EJECT
      *
     D LDA           E DS           256    EXTNAME(LDA)
      *
      * Local data area for database error details
      * *LOCK IN LDA immediately before and OUT LDA immediately
      * after each database error handling.
      *
      *                                     134 141 DBFILE
      *        Defines:                     142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *
     D PSDS           SDS
      *
      ** Program Status Data Structure.
      *
     D  PGM              *PROC
     D  WSID                 244    253
     D  USER                 254    263
      *
     C     COMP          KLIST
     C                   KFLD                    CFCOMI
     C                   KFLD                    CFEXTT
     C                   KFLD                    CFSEQM
      *
     C     *LOVAL        SETLL     UPCOMFL0
      *
     C                   READ      UPCOMFL0                             6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C     *IN20         DOWEQ     '0'
      *
     C     CFACTC        IFEQ      'I'
     C                   EXSR      SETVAL
     C                   WRITE     COMFF                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
     C                   ENDIF
      *
     C     CFACTC        IFEQ      'D'
     C     COMP          CHAIN     MXCOMFL2                           3069
     C     *IN69         IFEQ      *ON
     C     *IN30         OREQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
      *
     C                   ELSE
     C                   DELETE    COMFF                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
      *
      * If Action code = AMEND then read next record.
     C     CFACTC        IFEQ      'A'
     C                   MOVEL     CFCOMI        COMIX             8
     C                   MOVEL     CFEXTT        EXTTX             1
     C                   MOVEL     CFSEQM        SEQMX             2
     C                   READ      UPCOMFL0                             6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
      * If key fields do not match exit
      *  - or if EOF indicator is on.
     C     CFCOMI        IFNE      COMIX
     C     CFEXTT        ANDNE     EXTTX
     C     CFSEQM        ANDNE     SEQMX
     C     *IN20         OREQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C     COMP          CHAIN     MXCOMFL2                           3069
     C     *IN69         IFEQ      *ON
     C     *IN30         OREQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
      *
     C                   ELSE
      *
     C                   EXSR      SETVAL
     C                   UPDATE    COMFF                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
      *
     C                   READ      UPCOMFL0                             6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C                   ENDDO
      *
     C     ENDPGM        TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      *****************************************************************
      *                                                               *
      * SETVAL subroutine - set values for deletes/writes/updates     *
      *                                                               *
      *****************************************************************
     C     SETVAL        BEGSR
      * Set values to update record.
     C                   MOVEL     CFCOMI        XCFCOMI
     C                   MOVEL     CFEXTT        XCFEXTT
     C                   MOVEL     CFSEQM        XCFSEQM
     C                   MOVEL     CFFMT         XCFFMT
     C                   MOVEL     CFPRSQ        XCFPRSQ
     C                   MOVEL     CFDESC        XCFDESC
     C                   MOVEL     CFMULT        XCFMULT
     C                   MOVEL     CFEXPT        XCFEXPT
     C                   MOVEL     CFFILT        XCFFILT
     C                   MOVEL     CFOPTF        XCFOPTF
     C                   MOVEL     CFLFMT        XCFLFMT
     C                   MOVEL     CFLPRS        XCFLPRS
     C                   Z-ADD     CFLBP1        XCFLBP1
     C                   Z-ADD     CFLBP2        XCFLBP2
     C                   Z-ADD     CFLBP3        XCFLBP3
     C                   Z-ADD     CFLBP4        XCFLBP4
     C                   Z-ADD     CFLBP5        XCFLBP5
     C                   Z-ADD     CFLBP6        XCFLBP6
     C                   Z-ADD     CFLLN1        XCFLLN1
     C                   Z-ADD     CFLLN2        XCFLLN2
     C                   Z-ADD     CFLLN3        XCFLLN3
     C                   Z-ADD     CFLLN4        XCFLLN4
     C                   Z-ADD     CFLLN5        XCFLLN5
     C                   Z-ADD     CFLLN6        XCFLLN6
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR                                                  ** *PSSR SR **
      *
     C     @RUN          IFEQ      *BLANK
     C                   MOVE      'Y'           @RUN              1
     C                   DUMP
     C                   END
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
      ********************************************************************
