     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2002')
      *****************************************************************
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP1312 - Process Action File for MEBEISPD                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.03 ----------------------------------------*
      *  Prev Amend No. CUP019 *CREATE     Date 30May02               *
      *                                                               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP019 - Creation of new set of Action File utilities.       *
      *                                                               *
      *****************************************************************
      * Function of indicators
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write, update or delete fail
      *
     FUPBEISL0  IF   E           K DISK    INFSR(*PSSR)
     FMEBEISL0  UF A E           K DISK    INFSR(*PSSR)
     F                                     RENAME(MEBEISD0:BEISF) PREFIX(X)
      /EJECT
      *
     D LDA           E DS           256    EXTNAME(LDA)
      *
      * Local data area for database error details
      * *LOCK IN LDA immediately before and OUT LDA immediately
      * after each database error handling.
      *
      *                                     134 141 DBFILE
      *        Defines:                     142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *
     D PSDS           SDS
      *
      ** Program Status Data Structure.
      *
     D  PGM              *PROC
     D  WSID                 244    253
     D  USER                 254    263
      *
     C     COMP          KLIST
     C                   KFLD                    #0BEID
     C                   KFLD                    #0INSE
     C                   KFLD                    #0INDE
      *
     C     *LOVAL        SETLL     UPBEISL0
      *
     C                   READ      UPBEISL0                             6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C     *IN20         DOWEQ     '0'
      *
     C     #0ACTC        IFEQ      'I'
     C                   EXSR      SETVAL
     C                   WRITE     BEISF                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
     C                   ENDIF
      *
     C     #0ACTC        IFEQ      'D'
     C     COMP          CHAIN     MEBEISL0                           3069
     C     *IN69         IFEQ      *ON
     C     *IN30         OREQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
      *
     C                   ELSE
     C                   DELETE    BEISF                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
      *
      * If Action code = AMEND then read next record.
     C     #0ACTC        IFEQ      'A'
      * Move keys to storage fields
     C                   MOVEL     #0BEID        BEIDX             4
     C                   MOVEL     #0INSE        INSEX             6
     C                   MOVEL     #0INDE        INDEX             6
     C                   READ      UPBEISL0                             6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
      * If key fields do not match exit
      *  - or if EOF indicator is on.
     C     #0BEID        IFNE      BEIDX
     C     #0INSE        ANDNE     INSEX
     C     #0INDE        ANDNE     INDEX
     C     *IN20         OREQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C     COMP          CHAIN     MEBEISL0                           3069
     C     *IN69         IFEQ      *ON
     C     *IN30         OREQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
      *
     C                   ELSE
      *
     C                   EXSR      SETVAL
     C                   UPDATE    BEISF                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
      *
     C                   READ      UPBEISL0                             6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C                   ENDDO
      *
     C     ENDPGM        TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      *****************************************************************
      *                                                               *
      * SETVAL subroutine - set values for deletes/writes/updates     *
      *                                                               *
      *****************************************************************
     C     SETVAL        BEGSR
      * Set values to update record.
     C                   MOVEL     #0BEID        X#0BEID
     C                   MOVEL     #0INSE        X#0INSE
     C                   MOVEL     #0INDE        X#0INDE
     C                   MOVEL     #0DESC        X#0DESC
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR                                                  ** *PSSR SR **
      *
     C     @RUN          IFEQ      *BLANK
     C                   MOVE      'Y'           @RUN              1
     C                   DUMP
     C                   END
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
      ********************************************************************
