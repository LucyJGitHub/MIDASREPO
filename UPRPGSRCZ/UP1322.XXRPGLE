     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2003')
      *****************************************************************
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP1322 - Process Action File for MXDGDTPD                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CUP022 *CREATE     Date 13Jan03               *
      *                                                               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP022 - Creation of new set of Action File utilities.       *
      *                                                               *
      *****************************************************************
      * Function of indicators
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write, update or delete fail
      *
     FUPDGDTL0  IF   E           K DISK    INFSR(*PSSR)
     FMXDGDTL1  UF A E           K DISK    INFSR(*PSSR)
     F                                     RENAME(MXDGDTP0:DGDTF) PREFIX(X)
      /EJECT
      *
     D LDA           E DS           256    EXTNAME(LDA)
      *
      * Local data area for database error details
      * *LOCK IN LDA immediately before and OUT LDA immediately
      * after each database error handling.
      *
      *                                     134 141 DBFILE
      *        Defines:                     142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *
     D PSDS           SDS
      *
      ** Program Status Data Structure.
      *
     D  PGM              *PROC
     D  WSID                 244    253
     D  USER                 254    263
      *
     C     COMP          KLIST
     C                   KFLD                    DGDTAG
      *
     C     *LOVAL        SETLL     UPDGDTL0
      *
     C                   READ      UPDGDTL0                             6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C     *IN20         DOWEQ     '0'
      *
     C     DGACTC        IFEQ      'I'
     C                   EXSR      SETVAL
     C                   WRITE     DGDTF                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
     C                   ENDIF
      *
     C     DGACTC        IFEQ      'D'
     C     COMP          CHAIN     MXDGDTL1                           3069
     C     *IN69         IFEQ      *ON
     C     *IN30         OREQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
      *
     C                   ELSE
     C                   DELETE    DGDTF                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
      *
      * If Action code = AMEND then read next record.
     C     DGACTC        IFEQ      'A'
     C                   MOVEL     DGDTAG        DTAGX             4
     C                   READ      UPDGDTL0                             6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
      * If key fields do not match exit
      *  - or if EOF indicator is on.
     C     DGDTAG        IFNE      DTAGX
     C     *IN20         OREQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C     COMP          CHAIN     MXDGDTL1                           3069
     C     *IN69         IFEQ      *ON
     C     *IN30         OREQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
      *
     C                   ELSE
      *
     C                   EXSR      SETVAL
     C                   UPDATE    DGDTF                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
      *
     C                   READ      UPDGDTL0                             6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C                   ENDDO
      *
     C     ENDPGM        TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      *****************************************************************
      *                                                               *
      * SETVAL subroutine - set values for deletes/writes/updates     *
      *                                                               *
      *****************************************************************
     C     SETVAL        BEGSR
      * Set values to update record.
     C                   MOVEL     DGDTAG        XDGDTAG
     C                   MOVEL     DGDESC        XDGDESC
     C                   MOVEL     DGAKEV        XDGAKEV
     C                   MOVEL     DGAK01        XDGAK01
     C                   MOVEL     DGAK02        XDGAK02
     C                   MOVEL     DGAK03        XDGAK03
     C                   MOVEL     DGAK04        XDGAK04
     C                   MOVEL     DGAK05        XDGAK05
     C                   MOVEL     DGAK06        XDGAK06
     C                   MOVEL     DGAK07        XDGAK07
     C                   MOVEL     DGAK08        XDGAK08
     C                   MOVEL     DGAK09        XDGAK09
     C                   MOVEL     DGAK10        XDGAK10
     C                   MOVEL     DGAK11        XDGAK11
     C                   MOVEL     DGAK12        XDGAK12
     C                   MOVEL     DGAK13        XDGAK13
     C                   MOVEL     DGAK14        XDGAK14
     C                   MOVEL     DGAK15        XDGAK15
     C                   MOVEL     DGAK16        XDGAK16
     C                   MOVEL     DGAK17        XDGAK17
     C                   MOVEL     DGAK18        XDGAK18
     C                   MOVEL     DGAK19        XDGAK19
     C                   MOVEL     DGAK20        XDGAK20
     C                   MOVEL     DGENAB        XDGENAB
     C                   MOVEL     DGNOMB        XDGNOMB
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR                                                  ** *PSSR SR **
      *
     C     @RUN          IFEQ      *BLANK
     C                   MOVE      'Y'           @RUN              1
     C                   DUMP
     C                   END
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
      ********************************************************************
