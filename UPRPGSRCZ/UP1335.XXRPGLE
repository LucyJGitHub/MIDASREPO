     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2003')
      *****************************************************************
/*X*IA***OVRDBF*FILE(EXFF1)*TOFILE(UPEXFFTPD)**************************                       CUP021
/*X*IB***OVRDBF*FILE(EXFF2)*TOFILE(UPEXFFTPD)**************************                       CUP021
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP1335 - Build Action File for MXEXFFPD                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CUP022 *CREATE     Date 13Jan03               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP022 - Creation of new set of Action File utilities.       *
      *                                                               *
      *****************************************************************
      * Function of indicators
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write or chain fail
      *       70 - 92 Indicators for changed fields
      *
     FEXFF1     IF   E             DISK    INFSR(*PSSR)
     FEXFF2     IF   E           K DISK    INFSR(*PSSR)
     F                                     RENAME(MXEXFFP0:EXFFF) PREFIX(X)
     FUPEXFFPD  O    E             DISK    INFSR(*PSSR)
      *
      /EJECT
     D LDA           E DS           256    EXTNAME(LDA)
      *
      * Local data area for database error details
      * *LOCK IN LDA immediately before and OUT LDA immediately
      * after each database error handling.
      *
      *                                     134 141 DBFILE
      *       Defines:                      142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *
     D PSDS           SDS
      *
      * Program Status Data Structure.
      *
     D  PGM              *PROC
     D  WSID                 244    253
     D  USER                 254    263
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    MODE              1
      *
     C     COMP          KLIST
     C                   KFLD                    WHFILE
     C                   KFLD                    WHFSQN
     C                   KFLD                    WHFLDI
      *
     C                   READ      EXFF1                                6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C     *IN20         DOWEQ     '0'
     C     COMP          CHAIN     EXFFF                              3069
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * If record not found then record has been deleted or added
      *  depending on mode.
     C     *IN30         IFEQ      *ON
     C                   MOVE      MODE          WHACTC
     C                   WRITE     UPEXFFD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
      * Need to find differences but this should only be run once;
      *  run only if mode is 'Delete'.
     C     MODE          IFEQ      'D'
      *
      * If record found then look for any differences; only look in
      *  fields that have data all of the time.
     C     *IN30         IFEQ      *OFF
     C                   MOVE      'A'           WHACTC
      * Type of file
     C     WHFTYP        IFNE      XWHFTYP
     C                   SETON                                        70
     C                   ENDIF
      * Output buffer position
     C     WHFOBO        IFNE      XWHFOBO
     C                   SETON                                        71
     C                   ENDIF
      * Input buffer position
     C     WHIBO         IFNE      XWHIBO
     C                   SETON                                        72
     C                   ENDIF
      * Field length
     C     WHFLDB        IFNE      XWHFLDB
     C                   SETON                                        73
     C                   ENDIF
      * Number of digits
     C     WHFLDD        IFNE      XWHFLDD
     C                   SETON                                        74
     C                   ENDIF
      * Decimal positions
     C     WHFLDP        IFNE      XWHFLDP
     C                   SETON                                        75
     C                   ENDIF
      * Field description
     C     WHFTXT        IFNE      XWHFTXT
     C                   SETON                                        76
     C                   ENDIF
      * Field type
     C     WHFLDT        IFNE      XWHFLDT
     C                   SETON                                        77
     C                   ENDIF
      * Include in export
     C     WHINEX        IFNE      XWHINEX
     C                   SETON                                        78
     C                   ENDIF
      * Meridian field name
     C     WHMFNM        IFNE      XWHMFNM
     C                   SETON                                        79
     C                   ENDIF
      * Description 1
     C     WHDS01        IFNE      XWHDS01
     C                   SETON                                        80
     C                   ENDIF
      * Description 2
     C     WHDS02        IFNE      XWHDS02
     C                   SETON                                        81
     C                   ENDIF
      * Description 3
     C     WHDS03        IFNE      XWHDS03
     C                   SETON                                        82
     C                   ENDIF
      * Description 4
     C     WHDS04        IFNE      XWHDS04
     C                   SETON                                        83
     C                   ENDIF
      * Description 5
     C     WHDS05        IFNE      XWHDS05
     C                   SETON                                        84
     C                   ENDIF
      * Description 6
     C     WHDS06        IFNE      XWHDS06
     C                   SETON                                        85
     C                   ENDIF
      * Description 7
     C     WHDS07        IFNE      XWHDS07
     C                   SETON                                        86
     C                   ENDIF
      * Description 8
     C     WHDS08        IFNE      XWHDS08
     C                   SETON                                        87
     C                   ENDIF
      * Description 9
     C     WHDS09        IFNE      XWHDS09
     C                   SETON                                        88
     C                   ENDIF
      * Description 10
     C     WHDS10        IFNE      XWHDS10
     C                   SETON                                        89
     C                   ENDIF
      * Description 11
     C     WHDS11        IFNE      XWHDS11
     C                   SETON                                        90
     C                   ENDIF
      * Link sequence
     C     WHLSEQ        IFNE      XWHLSEQ
     C                   SETON                                        91
     C                   ENDIF
      * Sequence in reference
     C     WHSREF        IFNE      XWHSREF
     C                   SETON                                        92
     C                   ENDIF
      *
     C                   MOVEA     *IN(70)       INTEST           23
     C                   MOVE      INTEST        INTNUM           23 0
      * If any indicators are set on then write record.
     C     INTNUM        IFNE      0
     C                   WRITE     UPEXFFD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * Set values to write second record.
     C                   MOVEL     XWHFTYP       WHFTYP
     C                   MOVEL     XWHFOBO       WHFOBO
     C                   MOVEL     XWHIBO        WHIBO
     C                   MOVEL     XWHFLDB       WHFLDB
     C                   MOVEL     XWHFLDD       WHFLDD
     C                   MOVEL     XWHFLDP       WHFLDP
     C                   MOVEL     XWHFTXT       WHFTXT
     C                   MOVEL     XWHFLDT       WHFLDT
     C                   MOVEL     XWHINEX       WHINEX
     C                   MOVEL     XWHMFNM       WHMFNM
     C                   MOVEL     XWHDS01       WHDS01
     C                   MOVEL     XWHDS02       WHDS02
     C                   MOVEL     XWHDS03       WHDS03
     C                   MOVEL     XWHDS04       WHDS04
     C                   MOVEL     XWHDS05       WHDS05
     C                   MOVEL     XWHDS06       WHDS06
     C                   MOVEL     XWHDS07       WHDS07
     C                   MOVEL     XWHDS08       WHDS08
     C                   MOVEL     XWHDS09       WHDS09
     C                   MOVEL     XWHDS10       WHDS10
     C                   MOVEL     XWHDS11       WHDS11
     C                   MOVEL     XWHLSEQ       WHLSEQ
     C                   MOVEL     XWHSREF       WHSREF
      *
     C                   WRITE     UPEXFFD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
     C                   ENDIF
      *
      * Read next record
     C                   MOVEA     '0000000000'  *IN(70)
     C                   MOVEA     '0000000000'  *IN(80)
     C                   MOVEA     '000'         *IN(90)
     C                   READ      EXFF1                                6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C                   ENDDO
      *
     C     ENDPGM        TAG
     C                   SETON                                        LR
     C                   RETURN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR                                                  ** *PSSR SR **
      *
     C     @RUN          IFEQ      *BLANK
     C                   MOVE      'Y'           @RUN              1
     C                   DUMP
     C                   END
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
      *
      ********************************************************************
