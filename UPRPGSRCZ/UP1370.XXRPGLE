      *****************************************************************
     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2003')
      *****************************************************************
/*X*IA***OVRDBF*FILE(ISTS1)*TOFILE(UPISTSTPD)**************************                       CUP021
/*X*IB***OVRDBF*FILE(ISTS2)*TOFILE(UPISTSTPD)**************************                       CUP021
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP1370 - Build Action File for CGISTSPD                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CUP022 *CREATE     Date 29Jan03               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP022 - Creation of new set of Action File utilities.       *
      *                                                               *
      *****************************************************************
      * Function of indicators
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write or chain fail
      *       70 - 72 Indicators for changed fields
      *
     FISTS1     IF   E             DISK    INFSR(*PSSR)
     FISTS2     IF   E           K DISK    INFSR(*PSSR)
     F                                     RENAME(@ITSTPD:ISTSF) PREFIX(X)
     FUPISTSPD  O    E             DISK    INFSR(*PSSR)
      *
      /EJECT
     D LDA           E DS           256    EXTNAME(LDA)
      *
      * Local data area for database error details
      * *LOCK IN LDA immediately before and OUT LDA immediately
      * after each database error handling.
      *
      *                                     134 141 DBFILE
      *       Defines:                      142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *
     D PSDS           SDS
      *
      * Program Status Data Structure.
      *
     D  PGM              *PROC
     D  WSID                 244    253
     D  USER                 254    263
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    MODE              1
      *
     C     COMP          KLIST
     C                   KFLD                    ISISTS
      *
     C                   READ      ISTS1                                6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C     *IN20         DOWEQ     '0'
     C     COMP          CHAIN     ISTSF                              3069
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * If record not found then record has been deleted or added
      *  depending on mode.
     C     *IN30         IFEQ      *ON
     C                   MOVE      MODE          ISACTC
     C                   WRITE     UPISTSD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
      * Need to find differences but this should only be run once;
      *  run only if mode is 'Delete'.
     C     MODE          IFEQ      'D'
      *
      * If record found then look for any differences; only look in
      *  fields that have data all of the time.
     C     *IN30         IFEQ      *OFF
     C                   MOVE      'A'           ISACTC
      * Record Identifier
     C     ISRECI        IFNE      XISRECI
     C                   SETON                                        70
     C                   ENDIF
      * Item Status Mlang Msgid
     C     ISIMSG        IFNE      XISIMSG
     C                   SETON                                        71
     C                   ENDIF
      * Description
     C     ISDESC        IFNE      XISDESC
     C                   SETON                                        72
     C                   ENDIF
      *
     C                   MOVEA     *IN(70)       INTEST            3
     C                   MOVE      INTEST        INTNUM            3 0
      * If any indicators are set on then write record.
     C     INTNUM        IFNE      0
     C                   WRITE     UPISTSD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * Set values to write second record.
     C                   MOVEL     XISRECI       ISRECI
     C                   MOVEL     XISIMSG       ISIMSG
     C                   MOVEL     XISDESC       ISDESC
      *
     C                   WRITE     UPISTSD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
     C                   ENDIF
      *
      * Read next record
     C                   MOVEA     '0000000000'  *IN(70)
     C                   READ      ISTS1                                6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C                   ENDDO
      *
     C     ENDPGM        TAG
     C                   SETON                                        LR
     C                   RETURN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR                                                  ** *PSSR SR **
      *
     C     @RUN          IFEQ      *BLANK
     C                   MOVE      'Y'           @RUN              1
     C                   DUMP
     C                   END
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
      *
      ********************************************************************
