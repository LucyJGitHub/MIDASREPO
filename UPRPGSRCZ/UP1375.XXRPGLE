      *****************************************************************
     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2003')
      *****************************************************************
/*X*IA***OVRDBF*FILE(PATH1)*TOFILE(UPPATHTPD)**************************                       CUP021
/*X*IB***OVRDBF*FILE(PATH2)*TOFILE(UPPATHTPD)**************************                       CUP021
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP1375 - Build Action File for CGPATHPD                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CUP022 *CREATE     Date 29Jan03               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP022 - Creation of new set of Action File utilities.       *
      *                                                               *
      *****************************************************************
      * Function of indicators
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write or chain fail
      *       70 - 81 Indicators for changed fields
      *
     FPATH1     IF   E             DISK    INFSR(*PSSR)
     FPATH2     IF   E           K DISK    INFSR(*PSSR)
     F                                     RENAME(@PATHPD:PATHF) PREFIX(X)
     FUPPATHPD  O    E             DISK    INFSR(*PSSR)
      *
      /EJECT
     D LDA           E DS           256    EXTNAME(LDA)
      *
      * Local data area for database error details
      * *LOCK IN LDA immediately before and OUT LDA immediately
      * after each database error handling.
      *
      *                                     134 141 DBFILE
      *       Defines:                      142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *
     D PSDS           SDS
      *
      * Program Status Data Structure.
      *
     D  PGM              *PROC
     D  WSID                 244    253
     D  USER                 254    263
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    MODE              1
      *
     C     COMP          KLIST
     C                   KFLD                    EPPATH
      *
     C                   READ      PATH1                                6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C     *IN20         DOWEQ     '0'
     C     COMP          CHAIN     PATHF                              3069
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * If record not found then record has been deleted or added
      *  depending on mode.
     C     *IN30         IFEQ      *ON
     C                   MOVE      MODE          EPACTC
     C                   WRITE     UPPATHD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
      * Need to find differences but this should only be run once;
      *  run only if mode is 'Delete'.
     C     MODE          IFEQ      'D'
      *
      * If record found then look for any differences; only look in
      *  fields that have data all of the time.
     C     *IN30         IFEQ      *OFF
     C                   MOVE      'A'           EPACTC
      * Record Identifier
     C     EPRECI        IFNE      XEPRECI
     C                   SETON                                        70
     C                   ENDIF
      * Level 1 Group Set Name
     C     EPLEV1        IFNE      XEPLEV1
     C                   SETON                                        71
     C                   ENDIF
      * Level 2 Group Set Name
     C     EPLEV2        IFNE      XEPLEV2
     C                   SETON                                        72
     C                   ENDIF
      * Level 3 Group Set Name
     C     EPLEV3        IFNE      XEPLEV3
     C                   SETON                                        73
     C                   ENDIF
      * Level 4 Group Set Name
     C     EPLEV4        IFNE      XEPLEV4
     C                   SETON                                        74
     C                   ENDIF
      * Level 5 Group Set Name
     C     EPLEV5        IFNE      XEPLEV5
     C                   SETON                                        75
     C                   ENDIF
      * Level 6 Group Set Name
     C     EPLEV6        IFNE      XEPLEV6
     C                   SETON                                        76
     C                   ENDIF
      * Level 7 Group Set Name
     C     EPLEV7        IFNE      XEPLEV7
     C                   SETON                                        77
     C                   ENDIF
      * Level 8 Group Set Name
     C     EPLEV8        IFNE      XEPLEV8
     C                   SETON                                        78
     C                   ENDIF
      * Level 9 Group Set Name
     C     EPLEV9        IFNE      XEPLEV9
     C                   SETON                                        79
     C                   ENDIF
      * Level 10 Group Set Name
     C     EPLEV0        IFNE      XEPLEV0
     C                   SETON                                        80
     C                   ENDIF
      * Description
     C     EPDESC        IFNE      XEPDESC
     C                   SETON                                        81
     C                   ENDIF
      *
     C                   MOVEA     *IN(70)       INTEST           12
     C                   MOVE      INTEST        INTNUM           12 0
      * If any indicators are set on then write record.
     C     INTNUM        IFNE      0
     C                   WRITE     UPPATHD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * Set values to write second record.
     C                   MOVEL     XEPRECI       EPRECI
     C                   MOVEL     XEPLEV1       EPLEV1
     C                   MOVEL     XEPLEV2       EPLEV2
     C                   MOVEL     XEPLEV3       EPLEV3
     C                   MOVEL     XEPLEV4       EPLEV4
     C                   MOVEL     XEPLEV5       EPLEV5
     C                   MOVEL     XEPLEV6       EPLEV6
     C                   MOVEL     XEPLEV7       EPLEV7
     C                   MOVEL     XEPLEV8       EPLEV8
     C                   MOVEL     XEPLEV9       EPLEV9
     C                   MOVEL     XEPLEV0       EPLEV0
     C                   MOVEL     XEPDESC       EPDESC
      *
     C                   WRITE     UPPATHD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
     C                   ENDIF
      *
      * Read next record
     C                   MOVEA     '0000000000'  *IN(70)
     C                   MOVEA     '0000000000'  *IN(80)
     C                   READ      PATH1                                6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C                   ENDDO
      *
     C     ENDPGM        TAG
     C                   SETON                                        LR
     C                   RETURN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR                                                  ** *PSSR SR **
      *
     C     @RUN          IFEQ      *BLANK
     C                   MOVE      'Y'           @RUN              1
     C                   DUMP
     C                   END
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
      *
      ********************************************************************
