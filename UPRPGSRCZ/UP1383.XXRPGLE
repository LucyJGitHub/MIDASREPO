      *****************************************************************
     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2003')
      *****************************************************************
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP1383 - Action file compatibilty report for UPPRTPPD        *
      *                                                               *
      *  Function:  This program compares the differences between     *
      *             Action file delivered and the system.             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CUP022  *CREATE    Date 29Jan03               *
      *                                                               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP022 - Creation of new set of Action File utilities.       *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Use of indicators.                                           *
      *                                                               *
      *    30         Error on Read                                   *
      *    31         EOF on Read                                     *
      *    32         Chain Fail on Insert routine                    *
      *    33         Chain Fail on Delete routine                    *
      *    34         Record there on chain but different             *
      *    35         Chain Fail on Amend routine                     *
      *    36         Record there on Chain but different             *
      *    37         Compare Indicator for Delete Routine            *
      *    38         Compare Indicator for Amend Routine             *
      *    39         Switched on if a record is found on file(Delete)*
      *    40         Conflicts detected                              *
      *    41         First line of record written                    *
      *    42         Same Record in Sys and UP for Insert            *
      *    60         Do not run report if error on Amends            *
      *    70         Error on Write                                  *
      *    98         Date Format                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E  I N D E X                               *
      *                                                               *
      * *PSSR - Error processing                                      *
      * *INZSR - Initialise                                           *
      *                                                               *
      *    The *INZSR subroutine will only get called automatically   *
      *    on entry to the module the first time it is run            *
      *    (unless you end the program with LR on).  Similarly        *
      *    D-spec initialisation only happens the first time.  Use    *
      *    RESET for subsequent passes.                               *
      *                                                               *
      *****************************************************************
      ** +--------------------------------------+
      ** ¦ F-specs                              ¦
      ** ¦ =======                              ¦
      ** +--------------------------------------+
     FUPPRTPPD  IF   E             DISK    INFSR(*PSSR) Prefix(UP_) USROPN
     FCGPRTPL0  IF   E           K DISK    INFSR(*PSSR) Prefix(SYS_) USROPN
     FUP0803P1  O    E             PRINTER INFDS(SPOOL)
 
      *****************************************************************
      /EJECT
      *****************************************************************
      ** +--------------------------------------+
      ** ¦ D-specs                              ¦
      ** ¦ =======                              ¦
      ** +--------------------------------------+
     D LDA           E DS           256    EXTNAME(LDA)
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     D RUNDAT        E DS                  EXTNAME(RUNDAT)
      *
      ** Data Area giving Installation Control Details
 
      ** The following /COPY line includes all the defined fields in the
      ** PSDS.  They have meaningful names, prefixed by 'PS'.
 
     D/COPY ZACPYSRC,PSDS
      *
      ** Program Status Data Structure
      *
      ** +--------------------------------------+
      ** ¦ Named constants                      ¦
      ** ¦ ===============                      ¦
      ** +--------------------------------------+
 
     D Header          C                   'Report of the differences between -
     D                                     the Existing System Record and the -
     D                                     Replacement Record'
 
     D Counter         C                   '---------+---------+---------+------
     D                                     ----+---------+---------+---------+--
     D                                     --------+---------+---------+'
 
      ** +--------------------------------------+
      ** ¦ Arrays and Data Structures           ¦
      ** ¦ ==========================           ¦
      ** +--------------------------------------+
     D UpdatArray      S              1A   DIM(1387)
 
     D SysArray        S              1A   DIM(1387)
 
     D ErrorArray      S              1A   DIM(1387)
 
      ** Data Structure for the Update file
     D                 DS
     D UP_FPPRTF
     D UP_FPRECI
     D UP_FPDESC
     D UP_FPPRTN
     D UP_FPAPF
     D UP_FPAPFS
     D UP_FPOVRP
     D UP_FPSPLF
     D UP_FPORCF
     D NCU_FPAJOB                          LIKE(UP_FPAJOB)
     D NCU_FPAUSR                          LIKE(UP_FPAUSR)
     D NCU_FPATIM                     4
     D NCU_FPARDT                          LIKE(UP_FPARDT)
     D NCU_FPAACT                          LIKE(UP_FPAACT)
     D NCU_FPRDNB                     3
     D UP_FPOVR2
     D UP_FPOVR3
     D UP_FPOVR4
     D  Updfield               1   1387A
     D  UPDLIN1                1    100A
     D  UPDLIN2              101    200A
     D  UPDLIN3              201    300A
     D  UPDLIN4              301    400A
     D  UPDLIN5              401    500A
     D  UPDLIN6              501    600A
     D  UPDLIN7              601    700A
     D  UPDLIN8              701    800A
     D  UPDLIN9              801    900A
     D  UPDLIN10             901   1000A
     D  UPDLIN11            1001   1100A
     D  UPDLIN12            1101   1200A
     D  UPDLIN13            1201   1300A
     D  UPDLIN14            1301   1387A
 
      ** Data Structure for the Core file
     D                 DS
     D SYS_FPPRTF
     D SYS_FPRECI
     D SYS_FPDESC
     D SYS_FPPRTN
     D SYS_FPAPF
     D SYS_FPAPFS
     D SYS_FPOVRP
     D SYS_FPSPLF
     D SYS_FPORCF
     D NCS_FPAJOB                          LIKE(SYS_FPAJOB)
     D NCS_FPAUSR                          LIKE(SYS_FPAUSR)
     D NCS_FPATIM                     4
     D NCS_FPARDT                          LIKE(SYS_FPARDT)
     D NCS_FPAACT                          LIKE(SYS_FPAACT)
     D NCS_FPRDNB                     3
     D SYS_FPOVR2
     D SYS_FPOVR3
     D SYS_FPOVR4
     D  CoreFld                1   1387A
     D  SYSLIN1                1    100A
     D  SYSLIN2              101    200A
     D  SYSLIN3              201    300A
     D  SYSLIN4              301    400A
     D  SYSLIN5              401    500A
     D  SYSLIN6              501    600A
     D  SYSLIN7              601    700A
     D  SYSLIN8              701    800A
     D  SYSLIN9              801    900A
     D  SYSLIN10             901   1000A
     D  SYSLIN11            1001   1100A
     D  SYSLIN12            1101   1200A
     D  SYSLIN13            1201   1300A
     D  SYSLIN14            1301   1387A
 
      ** Data Structure for the Error indicators
     D                 DS          1387
     D  Indfld                 1   1387A
     D  INDLIN1                1    100A
     D  INDLIN2              101    200A
     D  INDLIN3              201    300A
     D  INDLIN4              301    400A
     D  INDLIN5              401    500A
     D  INDLIN6              501    600A
     D  INDLIN7              601    700A
     D  INDLIN8              701    800A
     D  INDLIN9              801    900A
     D  INDLIN10             901   1000A
     D  INDLIN11            1001   1100A
     D  INDLIN12            1101   1200A
     D  INDLIN13            1201   1300A
     D  INDLIN14            1301   1387A
 
     DSPOOL            DS
     D SFILE                  83     92
     D SFNUM                 123    124B 0
     D OFLLN                 188    189B 0
     D PRTLN                 367    368B 0
 
      ** +--------------------------------------+
      ** ¦ Declared variables                   ¦
      ** ¦ ==================                   ¦
      ** +--------------------------------------+
 
     D @RUN            S              1
     D KEY_FPPRTF      S                   LIKE(UP_FPPRTF)
     D X               S              5  0
     D UPDFIL          S             10
     D SYSFIL          S             10
     D ACTION          S              7
     D RMLN            S              2  0
     D Amends          S              5  1
     D COUNT           S            100
     D COUNT87         S             87
     D WAmends         S              5  1
     D Remainder       S              1
 
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ** +--- Start of Main processing -----------------------------------+
      ** ¦                                                                ¦
      ** ¦ Initial processing is performed automatically: the *INZSR is   ¦
      ** ¦ executed at program activation.                                ¦
      ** ¦                                                                ¦
      ** +----------------------------------------------------------------+
 
      *********************************************************************
      * MAIN PROCEDURE                                                    *
      *********************************************************************
      *
      * Fill fields not to be compared with '='s, so that they  will not be
      * any different between the system file and the update file.
     C                   EVAL      NCU_FPAJOB  = '=========='
     C                   EVAL      NCU_FPAUSR  = '=========='
     C                   EVAL      NCU_FPATIM  = '===='
     C                   EVAL      NCU_FPARDT  = '======='
     C                   EVAL      NCU_FPAACT  = '='
     C                   EVAL      NCU_FPRDNB  = '==='
     C                   EVAL      NCS_FPAJOB  = '=========='
     C                   EVAL      NCS_FPAUSR  = '=========='
     C                   EVAL      NCS_FPATIM  = '===='
     C                   EVAL      NCS_FPARDT  = '======='
     C                   EVAL      NCS_FPAACT  = '='
     C                   EVAL      NCS_FPRDNB  = '==='
      *
      ** Read in Installation Data
      *
     C     *DTAARA       DEFINE                  RUNDAT
     C     *DTAARA       DEFINE                  LDA
     C                   IN        RUNDAT
      *
      ** Set Date Format Indicator *IN98  on if date format is MMDDYY
      *
     C                   IF        AGDFF = 'M'
     C                   EVAL      *IN98 = *ON
     C                   END
      *
      ** Sub Routine to check that there are an equal number or Amends
      ** before running reports.
      *
     C                   EXSR      CHKA
 
      *There was no problem with an Odd number of Amends
     C                   IF        *IN60 = '0'
      * Set up key list
     C     KEY           KLIST
     C                   KFLD                    KEY_FPPRTF
 
     C                   READ      UPPRTPPD                             3031
      *
      * If an Error or End of file reached
     C                   IF        *IN30 = '1'
      *
     C     *LOCK         IN        LDA
     C                   EVAL      *INU7 = *ON
     C                   EVAL      *INU8 = *ON
     C                   EVAL      DBFILE =  'UPPRTPPD'
     C                   EVAL      DBKEY  =  UP_FPPRTF
     C                   EVAL      DBPGM  =  PSProcPgm
     C                   EVAL      DBASE  =  001
     C                   EVAL      DBMOD  =  PSProcMod
     C                   EVAL      DBPROC =  'SR/MAIN'
     C                   OUT       LDA
     C                   EXSR      *PSSR
     C                   ELSE
      *
     C                   WRITE     HEADH                                70
      *
      * Do while a record exists
     C                   DOW       *IN31 = '0'
     C                   EVAL      ACTION = *BLANKS
     C                   EVAL      Indfld = *BLANKS
      *
      * If Action Code is Insert
     C                   IF        UP_FPACTC = 'I'
     C                   MOVEL     UP_FPPRTF     KEY_FPPRTF
     C     KEY           CHAIN     CGPRTPL0                           32
      *
      * If a record is found on the chain then print out a message
     C                   IF        *IN32 = '0'
      * If the two records are different - Move along Character by Character indicating
      * differences on the third Array (ErrorArray) ELSE the record saying Identical
     C                   IF        CoreFld <> Updfield
     C                   MOVEA     Updfield      UpdatArray
     C                   MOVEA     CoreFld       SysArray
     C                   CLEAR                   ErrorArray
     C                   Z-ADD     1             X
     C                   DOU       X > 1387
     C     SysArray(X)   COMP      UpdatArray(X)                          43
     C                   IF        *IN43 = '0'
     C                   EVAL      *IN39 = '1'
     C                   MOVEA     '*'           ErrorArray(X)
     C                   ENDIF
     C                   ADD       1             X
     C                   ENDDO
      * Move error indicator array to field for output
     C                   MOVEA     ErrorArray    IndFld
      *
     C                   MOVEL     'INSERT'      ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ELSE
     C                   EVAL      *IN42 = '1'
     C                   MOVEL     'INSERT'      ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
      * If Action Code is Delete
     C                   IF        UP_FPACTC = 'D'
     C                   MOVEL     UP_FPPRTF     KEY_FPPRTF
     C     KEY           CHAIN     CGPRTPL0                           33
      *
      * If a file is not found on the chain print out a message
     C                   IF        *IN33 = '1'
     C                   MOVEL     'DELETE'      ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ELSE
      *
      * If the two records are different - Move along Character by Character indicating
      * differences on the third Array (ErrorArray)
     C                   IF        Corefld <> Updfield
     C                   MOVEA     Updfield      UpdatArray
     C                   MOVEA     CoreFld       SysArray
     C                   CLEAR                   ErrorArray
     C                   Z-ADD     1             X
     C                   DOU       X > 1387
     C     SysArray(X)   COMP      UpdatArray(X)                          36
     C                   IF        *IN36 = '0'
     C                   EVAL      *IN34 = '1'
     C                   MOVEA     '*'           ErrorArray(X)
     C                   EndIf
     C                   ADD       1             X
     C                   EndDo
      * Move error indicator array to field for output
     C                   MOVEA     ErrorArray    IndFld
      *
      * Write out all three data structures with the indicator showing
      * where the error is
     C                   MOVEL     'DELETE'      ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
      * If Action Code is Amend
     C                   IF        UP_FPACTC = 'A'
     C                   MOVEL     UP_FPPRTF     KEY_FPPRTF
     C     KEY           CHAIN     CGPRTPL0                           35
      *
      * If a file is not found on the chain print out a message
     C                   IF        *IN35 = '1'
     C                   MOVEL     'AMEND'       ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
      * Because amends come in pairs read the after image of amend.
     C                   READ      UPPRTPPD                               31
     C                   ELSE
      * If the before image record and core record are different, read
      * the after amend image record and compare it with the core record.
     C                   IF        CoreFld <> Updfield
     C                   READ      UPPRTPPD                               31
      *
      * If The two records are different - Move along Character by Character indicating
      * differences on the third Array (ErrorArray)
     C                   IF        CoreFld <> Updfield
     C                   MOVEA     Updfield      UpdatArray
     C                   MOVEA     CoreFld       SysArray
     C                   CLEAR                   ErrorArray
     C                   Z-ADD     1             X
     C                   DOU       X > 1387
     C     SysArray(X)   COMP      UpdatArray(X)                          38
     C                   IF        *IN38 = '0'
     C                   EVAL      *IN36 = '1'
     C                   MOVEA     '*'           ErrorArray(X)
     C                   ENDIF
     C                   ADD       1             X
     C                   ENDDO
      * Move error indicator array to field for output
     C                   MOVEA     ErrorArray    IndFld
      *
      * Write out all three data structures with the indicator showing
      *  where the error is.
      * Write the after image data.
     C                   MOVEL     'AMEND'       ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ENDIF
     C                   ELSE
      *  If the before image record is same as core record,
      *  read the after image record but nothing is done.
     C                   READ      UPPRTPPD                               31
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
     C                   MOVEA     '000000000'   *IN(30)
     C                   MOVEA     '0'           *IN(39)
     C                   MOVEA     '00'          *IN(41)
     C                   READ      UPPRTPPD                             3031
      *
      * If an Error
     C                   IF        *IN30 = '1'
      *
     C     *LOCK         IN        LDA
     C                   EVAL      *INU7 = *ON
     C                   EVAL      *INU8 = *ON
     C                   EVAL      DBFILE =  'UPPRTPPD'
     C                   EVAL      DBKEY  =  UP_FPPRTF
     C                   EVAL      DBPGM  =  PSProcPgm
     C                   EVAL      DBASE  =  002
     C                   EVAL      DBMOD  =  PSProcMod
     C                   EVAL      DBPROC =  'SR/MAIN'
     C                   OUT       LDA
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDDO
     C                   ENDIF
      * If there are no differences between the two files UP and Sys
      * Print No Differences ...
     C                   IF        *IN40 = '0'
     C                   WRITE     NODIFS                               70
     C                   ENDIF
      *
      * ... else Error in No of Amend Records
     C                   ELSE
     C                   WRITE     HEADH
     C                   WRITE     AMENDERR
     C                   ENDIF
      *
     C                   WRITE     TRAILP                               70
 
 
     C                   SETON                                        LR
     C                   RETURN
      **********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * REPORT - WRITE To the printer file                            *
      *                                                               *
      * Called by: Main Prcessing                                     *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     REPORT        BEGSR
      *
     C                   IF        (*IN33 = '1') OR
     C                             (*IN35 = '1') OR
     C                             (*IN42 = '1')
      * Overflow check for an Insert that exists or an Amend/Delete that is missing
     C                   EXSR      OFLOW
      * Write first 100  characters
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     UPDLIN1       UPDLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL1                                 70
      * Set on *IN41 to stop the appearance of Action
     C                   EVAL      *IN41 = '1'
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     UPDLIN2       UPDLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL1                                 70
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     UPDLIN3       UPDLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL1                                 70
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     UPDLIN4       UPDLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL1                                 70
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     UPDLIN5       UPDLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL1                                 70
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     UPDLIN6       UPDLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL1                                 70
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     UPDLIN7       UPDLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL1                                 70
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     UPDLIN8       UPDLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL1                                 70
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     UPDLIN9       UPDLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL1                                 70
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     UPDLIN10      UPDLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL1                                 70
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     UPDLIN11      UPDLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL1                                 70
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     UPDLIN12      UPDLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL1                                 70
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     UPDLIN13      UPDLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL1                                 70
      * Write last 87 characters
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       COUNT
     C                   MOVEL     UPDLIN14      UPDLIN
     C                   MOVEL     Counter       COUNT87
     C                   MOVEL     COUNT87       COUNT
     C                   WRITE     DTL1                                 70
      * Set Off *IN41
     C                   EVAL      *IN41 ='0'
     C                   ENDIF
      *
     C                   IF        (*IN34 = '1') OR
     C                             (*IN36 = '1') OR
     C                             (*IN39 = '1')
      * Overflow check for an Insert that exists or an Amend/Delete that is missing
     C                   EXSR      OFLOW2
      * Write first 100 characters
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN1       UPDLIN
     C                   MOVEL     INDLIN1       INDLIN
     C                   MOVEL     SYSLIN1       SYSLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL2                                 70
      * Set on *IN41 to stop the appearance of Action
     C                   EVAL      *IN41 = '1'
      * Write the following lines
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN2       UPDLIN
     C                   MOVEL     INDLIN2       INDLIN
     C                   MOVEL     SYSLIN2       SYSLIN
     C                   WRITE     DTL2                                 70
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN3       UPDLIN
     C                   MOVEL     INDLIN3       INDLIN
     C                   MOVEL     SYSLIN3       SYSLIN
     C                   WRITE     DTL2                                 70
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN4       UPDLIN
     C                   MOVEL     INDLIN4       INDLIN
     C                   MOVEL     SYSLIN4       SYSLIN
     C                   WRITE     DTL2                                 70
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN5       UPDLIN
     C                   MOVEL     INDLIN5       INDLIN
     C                   MOVEL     SYSLIN5       SYSLIN
     C                   WRITE     DTL2                                 70
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN6       UPDLIN
     C                   MOVEL     INDLIN6       INDLIN
     C                   MOVEL     SYSLIN6       SYSLIN
     C                   WRITE     DTL2                                 70
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN7       UPDLIN
     C                   MOVEL     INDLIN7       INDLIN
     C                   MOVEL     SYSLIN7       SYSLIN
     C                   WRITE     DTL2                                 70
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN8       UPDLIN
     C                   MOVEL     INDLIN8       INDLIN
     C                   MOVEL     SYSLIN8       SYSLIN
     C                   WRITE     DTL2                                 70
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN9       UPDLIN
     C                   MOVEL     INDLIN9       INDLIN
     C                   MOVEL     SYSLIN9       SYSLIN
     C                   WRITE     DTL2                                 70
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN10      UPDLIN
     C                   MOVEL     INDLIN10      INDLIN
     C                   MOVEL     SYSLIN10      SYSLIN
     C                   WRITE     DTL2                                 70
      * First page for the record is full. Start a new page
     C                   WRITE     HEADH                                70
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN11      UPDLIN
     C                   MOVEL     INDLIN11      INDLIN
     C                   MOVEL     SYSLIN11      SYSLIN
     C                   WRITE     DTL2                                 70
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN12      UPDLIN
     C                   MOVEL     INDLIN12      INDLIN
     C                   MOVEL     SYSLIN12      SYSLIN
     C                   WRITE     DTL2                                 70
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN13      UPDLIN
     C                   MOVEL     INDLIN13      INDLIN
     C                   MOVEL     SYSLIN13      SYSLIN
     C                   WRITE     DTL2                                 70
      * Write last 87 characters
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     *BLANKS       COUNT
     C                   MOVEL     UPDLIN14      UPDLIN
     C                   MOVEL     INDLIN14      INDLIN
     C                   MOVEL     SYSLIN14      SYSLIN
     C                   MOVEL     Counter       COUNT87
     C                   MOVEL     COUNT87       COUNT
     C                   WRITE     DTL2                                 70
      * Set Off *IN41
     C                   EVAL      *IN41 ='0'
     C                   ENDIF
 
     C                   ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * OFLOW - OVERFLOW Processing  for fields that                  *
      *                 For Inserts that are there and Amends and     *
      *                 Deletes that are missing                      *
      *                                                               *
      * Called by: Main Prcessing                                     *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     OFLOW         BEGSR
      *
     C     OFLLN         SUB       PRTLN         RMLN
     C                   IF        RMLN <= 44
     C                   WRITE     HEADH                                70
     C                   ENDIF
 
     C                   ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * OFLOW2- OVERFLOW Processing for differences on >200 chars     *
      *         with file differences                                 *
      *                                                               *
      * Called by: Main Prcessing                                     *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     OFLOW2        BEGSR
      *
     C     OFLLN         SUB       PRTLN         RMLN
     C                   IF        RMLN <= 46
     C                   WRITE     HEADH                                70
     C                   ENDIF
 
     C                   ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * CHKA  - Check there are an even number of Amend Records       *
      *         with file differences                                 *
      *                                                               *
      * Called by: Main Prcessing                                     *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     CHKA          BEGSR
      *
     C                   READ      UPPRTPPD                               70
     C                   DOW       *IN70 = '0'
     C                   IF        UP_FPACTC = 'A'
     C                   ADD       1             Amends
     C                   ENDIF
     C                   READ      UPPRTPPD                               70
     C                   ENDDO
      *
      * Check to see if the No. of Amends is divisible by 2 and not equal 0
     C                   IF        Amends <> 0
     C     Amends        DIV       2             WAmends
     C                   MOVE      WAmends       Remainder
      * If the remainder is ='5' Set on indicator to stop File processing
     C                   IF        REMAINDER ='5'
     C                   EVAL      *IN60 = '1'
     C                   ENDIF
     C                   ENDIF
     C     *START        SETLL     UPPRTPPD
 
     C                   ENDSR
      **********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *INZSR - Program Initialisation routine                       *
      *                                                               *
      * Called by: Implicitly on program activation                   *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     *INZSR        BEGSR
      * Initialise program name
      *
     C                   MOVEL     'UP1383'      DBPGM
      *
      * Move in program fields
     C                   MOVEL     'CGPRTPPD'    SYSFIL
     C                   MOVEL     'UPPRTPPD'    UPDFIL
     C                   MOVEL     HEADER        HEAD
      * Open files
     C                   OPEN      UPPRTPPD
     C                   OPEN      CGPRTPL0
     C                   ENDSR
 
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR
      *
     C                   IF        @RUN = *BLANK
     C                   EVAL      @RUN = 'Y'
     C                   DUMP
      *
     C                   ENDIF
      *
     C                   EVAL      *INU7 = *ON
     C                   EVAL      *INU8 = *ON
     C                   EVAL      *INLR = *ON
     C                   RETURN
      *
     C                   ENDSR
      *
