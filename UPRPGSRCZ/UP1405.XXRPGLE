      *****************************************************************
     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2003')
      *****************************************************************
/*X*IA***OVRDBF*FILE(RDTA1)*TOFILE(UPRDTATPD)**************************                       CUP021
/*X*IB***OVRDBF*FILE(RDTA2)*TOFILE(UPRDTATPD)**************************                       CUP021
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP1405 - Build Action File for CGRDTAPD                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CUP022 *CREATE     Date 29Jan03               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP022 - Creation of new set of Action File utilities.       *
      *                                                               *
      *****************************************************************
      * Function of indicators
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write or chain fail
      *       70 - 80 Indicators for changed fields
      *
     FRDTA1     IF   E             DISK    INFSR(*PSSR)
     FRDTA2     IF   E           K DISK    INFSR(*PSSR)
     F                                     RENAME(@RDTAPD:RDTAF) PREFIX(X)
     FUPRDTAPD  O    E             DISK    INFSR(*PSSR)
      *
      /EJECT
     D LDA           E DS           256    EXTNAME(LDA)
      *
      * Local data area for database error details
      * *LOCK IN LDA immediately before and OUT LDA immediately
      * after each database error handling.
      *
      *                                     134 141 DBFILE
      *       Defines:                      142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *
     D PSDS           SDS
      *
      * Program Status Data Structure.
      *
     D  PGM              *PROC
     D  WSID                 244    253
     D  USER                 254    263
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    MODE              1
      *
     C     COMP          KLIST
     C                   KFLD                    RDRDE
      *
     C                   READ      RDTA1                                6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C     *IN20         DOWEQ     '0'
     C     COMP          CHAIN     RDTAF                              3069
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * If record not found then record has been deleted or added
      *  depending on mode.
     C     *IN30         IFEQ      *ON
     C                   MOVE      MODE          RDACTC
     C                   WRITE     UPRDTAD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
      * Need to find differences but this should only be run once;
      *  run only if mode is 'Delete'.
     C     MODE          IFEQ      'D'
      *
      * If record found then look for any differences; only look in
      *  fields that have data all of the time.
     C     *IN30         IFEQ      *OFF
     C                   MOVE      'A'           RDACTC
      * Record Identifier
     C     RDRECI        IFNE      XRDRECI
     C                   SETON                                        70
     C                   ENDIF
      * Element Type
     C     RDELYP        IFNE      XRDELYP
     C                   SETON                                        71
     C                   ENDIF
      * Formatting Options
     C     RDFOPT        IFNE      XRDFOPT
     C                   SETON                                        72
     C                   ENDIF
      * Maximum Length
     C     RDMAXL        IFNE      XRDMAXL
     C                   SETON                                        73
     C                   ENDIF
      * Example Data
     C     RDETXT        IFNE      XRDETXT
     C                   SETON                                        74
     C                   ENDIF
      * Allow Derived Information
     C     RDDRVD        IFNE      XRDDRVD
     C                   SETON                                        75
     C                   ENDIF
      * Derived Information Src
     C     RDDSRC        IFNE      XRDDSRC
     C                   SETON                                        76
     C                   ENDIF
      * Derived Data internal ref
     C     RDDINT        IFNE      XRDDINT
     C                   SETON                                        77
     C                   ENDIF
      * Start Position
     C     RDSPOS        IFNE      XRDSPOS
     C                   SETON                                        78
     C                   ENDIF
      * End Position
     C     RDEPOS        IFNE      XRDEPOS
     C                   SETON                                        79
     C                   ENDIF
      * Description
     C     RDDESC        IFNE      XRDDESC
     C                   SETON                                        80
     C                   ENDIF
      *
     C                   MOVEA     *IN(70)       INTEST           11
     C                   MOVE      INTEST        INTNUM           11 0
      * If any indicators are set on then write record.
     C     INTNUM        IFNE      0
     C                   WRITE     UPRDTAD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * Set values to write second record.
     C                   MOVEL     XRDRECI       RDRECI
     C                   MOVEL     XRDELYP       RDELYP
     C                   MOVEL     XRDFOPT       RDFOPT
     C                   Z-ADD     XRDMAXL       RDMAXL
     C                   MOVEL     XRDETXT       RDETXT
     C                   MOVEL     XRDDRVD       RDDRVD
     C                   MOVEL     XRDDSRC       RDDSRC
     C                   MOVEL     XRDDINT       RDDINT
     C                   Z-ADD     XRDSPOS       RDSPOS
     C                   Z-ADD     XRDEPOS       RDEPOS
     C                   MOVEL     XRDDESC       RDDESC
      *
     C                   WRITE     UPRDTAD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
     C                   ENDIF
      *
      * Read next record
     C                   MOVEA     '0000000000'  *IN(70)
     C                   MOVEA     '0000000000'  *IN(80)
     C                   READ      RDTA1                                6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C                   ENDDO
      *
     C     ENDPGM        TAG
     C                   SETON                                        LR
     C                   RETURN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR                                                  ** *PSSR SR **
      *
     C     @RUN          IFEQ      *BLANK
     C                   MOVE      'Y'           @RUN              1
     C                   DUMP
     C                   END
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
      *
      ********************************************************************
