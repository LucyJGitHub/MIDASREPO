     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2003')
      *****************************************************************
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP1413 - Action file compatibilty report for UPCSCHPD        *
      *                                                               *
      *  Function:  This program compares the differences between     *
      *             Action file delivered and the system.             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CUP022  *CREATE    Date 29Jan03               *
      *                                                               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP022 - Creation of new set of Action File utilities.       *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Use of indicators.                                           *
      *                                                               *
      *    30         Error on Read                                   *
      *    31         EOF on Read                                     *
      *    32         Chain Fail on Insert routine                    *
      *    33         Chain Fail on Delete routine                    *
      *    34         Record there on chain but different             *
      *    35         Chain Fail on Amend routine                     *
      *    36         Record there on Chain but different             *
      *    37         Compare Indicator for Delete Routine            *
      *    38         Compare Indicator for Amend Routine             *
      *    39         Switched on if a record is found on file(Delete)*
      *    40         Conflicts detected                              *
      *    41         First line of record written                    *
      *    42         Same Record in Sys and UP for Insert            *
      *    60         Do not run report if error on Amends            *
      *    70         Error on Write                                  *
      *    98         Date Format                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E  I N D E X                               *
      *                                                               *
      * *PSSR - Error processing                                      *
      * *INZSR - Initialise                                           *
      *                                                               *
      *    The *INZSR subroutine will only get called automatically   *
      *    on entry to the module the first time it is run            *
      *    (unless you end the program with LR on).  Similarly        *
      *    D-spec initialisation only happens the first time.  Use    *
      *    RESET for subsequent passes.                               *
      *                                                               *
      *****************************************************************
      ** +--------------------------------------+
      ** ¦ F-specs                              ¦
      ** ¦ =======                              ¦
      ** +--------------------------------------+
     FUPCSCHPD  IF   E             DISK    INFSR(*PSSR) Prefix(UP_) USROPN
     FCGCSCHL0  IF   E           K DISK    INFSR(*PSSR) Prefix(SYS_) USROPN
     FUP0803P1  O    E             PRINTER INFDS(SPOOL)
 
      *****************************************************************
      /EJECT
      *****************************************************************
      ** +--------------------------------------+
      ** ¦ D-specs                              ¦
      ** ¦ =======                              ¦
      ** +--------------------------------------+
     D LDA           E DS           256    EXTNAME(LDA)
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     D RUNDAT        E DS                  EXTNAME(RUNDAT)
      *
      ** Data Area giving Installation Control Details
 
      ** The following /COPY line includes all the defined fields in the
      ** PSDS.  They have meaningful names, prefixed by 'PS'.
 
     D/COPY ZACPYSRC,PSDS
      *
      ** Program Status Data Structure
      *
      ** +--------------------------------------+
      ** ¦ Named constants                      ¦
      ** ¦ ===============                      ¦
      ** +--------------------------------------+
 
     D Header          C                   'Report of the differences between -
     D                                     the Existing System Record and the -
     D                                     Replacement Record'
 
     D Counter         C                   '---------+---------+---------+------
     D                                     ----+---------+---------+---------+--
     D                                     --------+---------+---------+'
 
      ** +--------------------------------------+
      ** ¦ Arrays and Data Structures           ¦
      ** ¦ ==========================           ¦
      ** +--------------------------------------+
     D UpdatArray      S              1A   DIM(196)
 
     D SysArray        S              1A   DIM(196)
 
     D ErrorArray      S              1A   DIM(196)
 
      ** Data Structure for the Update file
     D                 DS
     D UP_CPCORR
     D UP_CPPTYP
     D UP_CPPSTP
     D UP_CPSEQN
     D NCU_CPRECI                          LIKE(UP_CPRECI)
     D NCU_CPPSCH                          LIKE(UP_CPPSCH)
     D NCU_CPPCOR                          LIKE(UP_CPPCOR)
     D NCU_CPLGID                          LIKE(UP_CPLGID)
     D NCU_CPACCD                          LIKE(UP_CPACCD)
     D NCU_CPNPDT                          LIKE(UP_CPNPDT)
     D NCU_CPMACT                          LIKE(UP_CPMACT)
     D NCU_CPPLAB                          LIKE(UP_CPPLAB)
     D NCU_CPSFDY                          LIKE(UP_CPSFDY)
     D NCU_CPRETP                          LIKE(UP_CPRETP)
     D NCU_CPMSHC                          LIKE(UP_CPMSHC)
     D NCU_CPAPDF                          LIKE(UP_CPAPDF)
     D NCU_CPOSQN                          LIKE(UP_CPOSQN)
     D NCU_CPNPAG                          LIKE(UP_CPNPAG)
     D NCU_CPTPAG                          LIKE(UP_CPTPAG)
     D NCU_CPRDTE                          LIKE(UP_CPRDTE)
     D NCU_CPRFRQ                          LIKE(UP_CPRFRQ)
     D NCU_CPRSDN                          LIKE(UP_CPRSDN)
     D NCU_CPCCCY                          LIKE(UP_CPCCCY)
     D NCU_CPCITM                          LIKE(UP_CPCITM)
     D NCU_CPBSNB                          LIKE(UP_CPBSNB)
     D NCU_CPCACC                          LIKE(UP_CPCACC)
     D NCU_CPCFRQ                          LIKE(UP_CPCFRQ)
     D NCU_CPNTDT                          LIKE(UP_CPNTDT)
     D NCU_CPACHG                          LIKE(UP_CPACHG)
     D NCU_CPCACT                          LIKE(UP_CPCACT)
     D NCU_CPPDAY                          LIKE(UP_CPPDAY)
     D NCU_CPAJOB                          LIKE(UP_CPAJOB)
     D NCU_CPAUSR                          LIKE(UP_CPAUSR)
     D NCU_CPATIM                          LIKE(UP_CPATIM)
     D NCU_CPARDT                          LIKE(UP_CPARDT)
     D NCU_CPAACT                          LIKE(UP_CPAACT)
     D NCU_CPRDNB                          LIKE(UP_CPRDNB)
     D  Updfield               1    196A
     D  UPDLIN1                1    100A
     D  UPDLIN2              101    196A
 
      ** Data Structure for the Core file
     D                 DS
     D SYS_CPCORR
     D SYS_CPPTYP
     D SYS_CPPSTP
     D SYS_CPSEQN
     D NCS_CPRECI                          LIKE(SYS_CPRECI)
     D NCS_CPPSCH                          LIKE(SYS_CPPSCH)
     D NCS_CPPCOR                          LIKE(SYS_CPPCOR)
     D NCS_CPLGID                          LIKE(SYS_CPLGID)
     D NCS_CPACCD                          LIKE(SYS_CPACCD)
     D NCS_CPNPDT                          LIKE(SYS_CPNPDT)
     D NCS_CPMACT                          LIKE(SYS_CPMACT)
     D NCS_CPPLAB                          LIKE(SYS_CPPLAB)
     D NCS_CPSFDY                          LIKE(SYS_CPSFDY)
     D NCS_CPRETP                          LIKE(SYS_CPRETP)
     D NCS_CPMSHC                          LIKE(SYS_CPMSHC)
     D NCS_CPAPDF                          LIKE(SYS_CPAPDF)
     D NCS_CPOSQN                          LIKE(SYS_CPOSQN)
     D NCS_CPNPAG                          LIKE(SYS_CPNPAG)
     D NCS_CPTPAG                          LIKE(SYS_CPTPAG)
     D NCS_CPRDTE                          LIKE(SYS_CPRDTE)
     D NCS_CPRFRQ                          LIKE(SYS_CPRFRQ)
     D NCS_CPRSDN                          LIKE(SYS_CPRSDN)
     D NCS_CPCCCY                          LIKE(SYS_CPCCCY)
     D NCS_CPCITM                          LIKE(SYS_CPCITM)
     D NCS_CPBSNB                          LIKE(SYS_CPBSNB)
     D NCS_CPCACC                          LIKE(SYS_CPCACC)
     D NCS_CPCFRQ                          LIKE(SYS_CPCFRQ)
     D NCS_CPNTDT                          LIKE(SYS_CPNTDT)
     D NCS_CPACHG                          LIKE(SYS_CPACHG)
     D NCS_CPCACT                          LIKE(SYS_CPCACT)
     D NCS_CPPDAY                          LIKE(SYS_CPPDAY)
     D NCS_CPAJOB                          LIKE(SYS_CPAJOB)
     D NCS_CPAUSR                          LIKE(SYS_CPAUSR)
     D NCS_CPATIM                          LIKE(SYS_CPATIM)
     D NCS_CPARDT                          LIKE(SYS_CPARDT)
     D NCS_CPAACT                          LIKE(SYS_CPAACT)
     D NCS_CPRDNB                          LIKE(SYS_CPRDNB)
     D  CoreFld                1    196A
     D  SYSLIN1                1    100A
     D  SYSLIN2              101    196A
 
      ** Data Structure for the Error indicators
     D                 DS           196
     D  Indfld                 1    196A
     D  INDLIN1                1    100A
     D  INDLIN2              101    196A
 
     DSPOOL            DS
     D SFILE                  83     92
     D SFNUM                 123    124B 0
     D OFLLN                 188    189B 0
     D PRTLN                 367    368B 0
 
      ** +--------------------------------------+
      ** ¦ Declared variables                   ¦
      ** ¦ ==================                   ¦
      ** +--------------------------------------+
 
     D @RUN            S              1
     D KEY_CPCORR      S                   LIKE(UP_CPCORR)
     D KEY_CPPTYP      S                   LIKE(UP_CPPTYP)
     D KEY_CPPSTP      S                   LIKE(UP_CPPSTP)
     D KEY_CPSEQN      S                   LIKE(UP_CPSEQN)
     D X               S              5  0
     D UPDFIL          S             10
     D SYSFIL          S             10
     D ACTION          S              7
     D RMLN            S              2  0
     D Amends          S              5  1
     D COUNT           S            100
     D COUNT96         S             96
     D WAmends         S              5  1
     D Remainder       S              1
 
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ** +--- Start of Main processing -----------------------------------+
      ** ¦                                                                ¦
      ** ¦ Initial processing is performed automatically: the *INZSR is   ¦
      ** ¦ executed at program activation.                                ¦
      ** ¦                                                                ¦
      ** +----------------------------------------------------------------+
 
      *********************************************************************
      * MAIN PROCEDURE                                                    *
      *********************************************************************
      *
      * Fill fields not to be compared with '='s, so that they  will not be
      * any different between the system file and the update file.
     C                   EVAL      NCU_CPRECI  = '='
     C                   EVAL      NCU_CPPSCH  = '======'
     C                   EVAL      NCU_CPPCOR  = '=========='
     C                   EVAL      NCU_CPLGID  = '=='
     C                   EVAL      NCU_CPACCD  = '===='
     C                   EVAL      NCU_CPNPDT  = 0
     C                   EVAL      NCU_CPMACT  = '=================='
     C                   EVAL      NCU_CPPLAB  = '='
     C                   EVAL      NCU_CPSFDY  = 0
     C                   EVAL      NCU_CPRETP  = 0
     C                   EVAL      NCU_CPMSHC  = '=========='
     C                   EVAL      NCU_CPAPDF  = '='
     C                   EVAL      NCU_CPOSQN  = 0
     C                   EVAL      NCU_CPNPAG  = 0
     C                   EVAL      NCU_CPTPAG  = 0
     C                   EVAL      NCU_CPRDTE  = 0
     C                   EVAL      NCU_CPRFRQ  = '='
     C                   EVAL      NCU_CPRSDN  = 0
     C                   EVAL      NCU_CPCCCY  = '==='
     C                   EVAL      NCU_CPCITM  = 0
     C                   EVAL      NCU_CPBSNB  = 0
     C                   EVAL      NCU_CPCACC  = 0
     C                   EVAL      NCU_CPCFRQ  = '='
     C                   EVAL      NCU_CPNTDT  = 0
     C                   EVAL      NCU_CPACHG  = '='
     C                   EVAL      NCU_CPCACT  = '=================='
     C                   EVAL      NCU_CPPDAY  = 0
     C                   EVAL      NCU_CPAJOB  = '=========='
     C                   EVAL      NCU_CPAUSR  = '=========='
     C                   EVAL      NCU_CPATIM  = 0
     C                   EVAL      NCU_CPARDT  = '======='
     C                   EVAL      NCU_CPAACT  = '='
     C                   EVAL      NCU_CPRDNB  = 0
      *
     C                   EVAL      NCS_CPRECI  = '='
     C                   EVAL      NCS_CPPSCH  = '======'
     C                   EVAL      NCS_CPPCOR  = '=========='
     C                   EVAL      NCS_CPLGID  = '=='
     C                   EVAL      NCS_CPACCD  = '===='
     C                   EVAL      NCS_CPNPDT  = 0
     C                   EVAL      NCS_CPMACT  = '=================='
     C                   EVAL      NCS_CPPLAB  = '='
     C                   EVAL      NCS_CPSFDY  = 0
     C                   EVAL      NCS_CPRETP  = 0
     C                   EVAL      NCS_CPMSHC  = '=========='
     C                   EVAL      NCS_CPAPDF  = '='
     C                   EVAL      NCS_CPOSQN  = 0
     C                   EVAL      NCS_CPNPAG  = 0
     C                   EVAL      NCS_CPTPAG  = 0
     C                   EVAL      NCS_CPRDTE  = 0
     C                   EVAL      NCS_CPRFRQ  = '='
     C                   EVAL      NCS_CPRSDN  = 0
     C                   EVAL      NCS_CPCCCY  = '==='
     C                   EVAL      NCS_CPCITM  = 0
     C                   EVAL      NCS_CPBSNB  = 0
     C                   EVAL      NCS_CPCACC  = 0
     C                   EVAL      NCS_CPCFRQ  = '='
     C                   EVAL      NCS_CPNTDT  = 0
     C                   EVAL      NCS_CPACHG  = '='
     C                   EVAL      NCS_CPCACT  = '=================='
     C                   EVAL      NCS_CPPDAY  = 0
     C                   EVAL      NCS_CPAJOB  = '=========='
     C                   EVAL      NCS_CPAUSR  = '=========='
     C                   EVAL      NCS_CPATIM  = 0
     C                   EVAL      NCS_CPARDT  = '======='
     C                   EVAL      NCS_CPAACT  = '='
     C                   EVAL      NCS_CPRDNB  = 0
      *
      ** Read in Installation Data
      *
     C     *DTAARA       DEFINE                  RUNDAT
     C     *DTAARA       DEFINE                  LDA
     C                   IN        RUNDAT
      *
      ** Set Date Format Indicator *IN98  on if date format is MMDDYY
      *
     C                   IF        AGDFF = 'M'
     C                   EVAL      *IN98 = *ON
     C                   END
      *
      ** Sub Routine to check that there are an equal number or Amends
      ** before running reports.
      *
     C                   EXSR      CHKA
 
      *There was no problem with an Odd number of Amends
     C                   IF        *IN60 = '0'
      * Set up key list
     C     KEY           KLIST
     C                   KFLD                    KEY_CPCORR
     C                   KFLD                    KEY_CPPTYP
     C                   KFLD                    KEY_CPPSTP
     C                   KFLD                    KEY_CPSEQN
 
     C                   READ      UPCSCHPD                             3031
      *
      * If an Error or End of file reached
     C                   IF        *IN30 = '1'
      *
     C     *LOCK         IN        LDA
     C                   EVAL      *INU7 = *ON
     C                   EVAL      *INU8 = *ON
     C                   EVAL      DBFILE =  'UPCSCHPD'
     C                   EVAL      DBKEY  =  UP_CPCORR + UP_CPPTYP
     C                                       + UP_CPPSTP
     C                   EVAL      DBPGM  =  PSProcPgm
     C                   EVAL      DBASE  =  001
     C                   EVAL      DBMOD  =  PSProcMod
     C                   EVAL      DBPROC =  'SR/MAIN'
     C                   OUT       LDA
     C                   EXSR      *PSSR
     C                   ELSE
      *
     C                   WRITE     HEADH                                70
      *
      * Do while a record exists
     C                   DOW       *IN31 = '0'
     C                   EVAL      ACTION = *BLANKS
     C                   EVAL      Indfld = *BLANKS
      *
      * If Action Code is Insert
     C                   IF        UP_CPACTC = 'I'
     C                   MOVEL     UP_CPCORR     KEY_CPCORR
     C                   MOVEL     UP_CPPTYP     KEY_CPPTYP
     C                   MOVEL     UP_CPPSTP     KEY_CPPSTP
     C                   MOVEL     UP_CPSEQN     KEY_CPSEQN
     C     KEY           CHAIN     CGCSCHL0                           32
      *
      * If a record is found on the chain then print out a message
     C                   IF        *IN32 = '0'
      * If the two records are different - Move along Character by Character indicating
      * differences on the third Array (ErrorArray) ELSE the record saying Identical
     C                   IF        CoreFld <> Updfield
     C                   MOVEA     Updfield      UpdatArray
     C                   MOVEA     CoreFld       SysArray
     C                   CLEAR                   ErrorArray
     C                   Z-ADD     1             X
     C                   DOU       X > 196
     C     SysArray(X)   COMP      UpdatArray(X)                          43
     C                   IF        *IN43 = '0'
     C                   EVAL      *IN39 = '1'
     C                   MOVEA     '*'           ErrorArray(X)
     C                   ENDIF
     C                   ADD       1             X
     C                   ENDDO
      * Move error indicator array to field for output
     C                   MOVEA     ErrorArray    IndFld
      *
     C                   MOVEL     'INSERT'      ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ELSE
     C                   EVAL      *IN42 = '1'
     C                   MOVEL     'INSERT'      ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
      * If Action Code is Delete
     C                   IF        UP_CPACTC = 'D'
     C                   MOVEL     UP_CPCORR     KEY_CPCORR
     C                   MOVEL     UP_CPPTYP     KEY_CPPTYP
     C                   MOVEL     UP_CPPSTP     KEY_CPPSTP
     C                   MOVEL     UP_CPSEQN     KEY_CPSEQN
     C     KEY           CHAIN     CGCSCHL0                           33
      *
      * If a file is not found on the chain print out a message
     C                   IF        *IN33 = '1'
     C                   MOVEL     'DELETE'      ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ELSE
      *
      * If the two records are different - Move along Character by Character indicating
      * differences on the third Array (ErrorArray)
     C                   IF        Corefld <> Updfield
     C                   MOVEA     Updfield      UpdatArray
     C                   MOVEA     CoreFld       SysArray
     C                   CLEAR                   ErrorArray
     C                   Z-ADD     1             X
     C                   DOU       X > 196
     C     SysArray(X)   COMP      UpdatArray(X)                          36
     C                   IF        *IN36 = '0'
     C                   EVAL      *IN34 = '1'
     C                   MOVEA     '*'           ErrorArray(X)
     C                   EndIf
     C                   ADD       1             X
     C                   EndDo
      * Move error indicator array to field for output
     C                   MOVEA     ErrorArray    IndFld
      *
      * Write out all three data structures with the indicator showing
      * where the error is
     C                   MOVEL     'DELETE'      ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
      * If Action Code is Amend
     C                   IF        UP_CPACTC = 'A'
     C                   MOVEL     UP_CPCORR     KEY_CPCORR
     C                   MOVEL     UP_CPPTYP     KEY_CPPTYP
     C                   MOVEL     UP_CPPSTP     KEY_CPPSTP
     C                   MOVEL     UP_CPSEQN     KEY_CPSEQN
     C     KEY           CHAIN     CGCSCHL0                           35
      *
      * If a file is not found on the chain print out a message
     C                   IF        *IN35 = '1'
     C                   MOVEL     'AMEND'       ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
      * Because amends come in pairs read the after image of amend.
     C                   READ      UPCSCHPD                               31
     C                   ELSE
      * If the before image record and core record are different, read
      * the after amend image record and compare it with the core record.
     C                   IF        CoreFld <> Updfield
     C                   READ      UPCSCHPD                               31
      *
      * If The two records are different - Move along Character by Character indicating
      * differences on the third Array (ErrorArray)
     C                   IF        CoreFld <> Updfield
     C                   MOVEA     Updfield      UpdatArray
     C                   MOVEA     CoreFld       SysArray
     C                   CLEAR                   ErrorArray
     C                   Z-ADD     1             X
     C                   DOU       X > 196
     C     SysArray(X)   COMP      UpdatArray(X)                          38
     C                   IF        *IN38 = '0'
     C                   EVAL      *IN36 = '1'
     C                   MOVEA     '*'           ErrorArray(X)
     C                   ENDIF
     C                   ADD       1             X
     C                   ENDDO
      * Move error indicator array to field for output
     C                   MOVEA     ErrorArray    IndFld
      *
      * Write out all three data structures with the indicator showing
      *  where the error is.
      * Write the after image data.
     C                   MOVEL     'AMEND'       ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ENDIF
     C                   ELSE
      *  If the before image record is same as core record,
      *  read the after image record but nothing is done.
     C                   READ      UPCSCHPD                               31
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
     C                   MOVEA     '000000000'   *IN(30)
     C                   MOVEA     '0'           *IN(39)
     C                   MOVEA     '00'          *IN(41)
     C                   READ      UPCSCHPD                             3031
      *
      * If an Error
     C                   IF        *IN30 = '1'
      *
     C     *LOCK         IN        LDA
     C                   EVAL      *INU7 = *ON
     C                   EVAL      *INU8 = *ON
     C                   EVAL      DBFILE =  'UPCSCHPD'
     C                   EVAL      DBKEY  =  UP_CPCORR + UP_CPPTYP
     C                                       + UP_CPPSTP
     C                   EVAL      DBPGM  =  PSProcPgm
     C                   EVAL      DBASE  =  002
     C                   EVAL      DBMOD  =  PSProcMod
     C                   EVAL      DBPROC =  'SR/MAIN'
     C                   OUT       LDA
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDDO
     C                   ENDIF
      * If there are no differences between the two files UP and Sys
      * Print No Differences ...
     C                   IF        *IN40 = '0'
     C                   WRITE     NODIFS                               70
     C                   ENDIF
      *
      * ... else Error in No of Amend Records
     C                   ELSE
     C                   WRITE     HEADH
     C                   WRITE     AMENDERR
     C                   ENDIF
      *
     C                   WRITE     TRAILP                               70
 
 
     C                   SETON                                        LR
     C                   RETURN
      **********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * REPORT - WRITE To the printer file                            *
      *                                                               *
      * Called by: Main Prcessing                                     *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     REPORT        BEGSR
      *
      *
     C                   IF        (*IN33 = '1') OR
     C                             (*IN35 = '1') OR
     C                             (*IN42 = '1')
      * Overflow check for an Insert that exists or an Amend/Delete that is missing
     C                   EXSR      OFLOW
      * Write first 100  characters
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     UPDLIN1       UPDLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL1                                 70
      * Set on *IN41 to stop the appearance of Action
     C                   EVAL      *IN41 = '1'
      * Write last 96 characters
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       COUNT
     C                   MOVEL     UPDLIN2       UPDLIN
     C                   MOVEL     Counter       COUNT96
     C                   MOVEL     COUNT96       COUNT
     C                   WRITE     DTL1                                 70
      * Set Off *IN41
     C                   EVAL      *IN41 ='0'
     C                   ENDIF
      *
     C                   IF        (*IN34 = '1') OR
     C                             (*IN36 = '1') OR
     C                             (*IN39 = '1')
      * Overflow check for an Insert that exists or an Amend/Delete that is missing
     C                   EXSR      OFLOW2
      * Write first 100 characters
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN1       UPDLIN
     C                   MOVEL     INDLIN1       INDLIN
     C                   MOVEL     SYSLIN1       SYSLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL2                                 70
      * Set on *IN41 to stop the appearance of Action
     C                   EVAL      *IN41 = '1'
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     *BLANKS       COUNT
     C                   MOVEL     UPDLIN2       UPDLIN
     C                   MOVEL     INDLIN2       INDLIN
     C                   MOVEL     SYSLIN2       SYSLIN
     C                   MOVEL     Counter       COUNT96
     C                   MOVEL     COUNT96       COUNT
     C                   WRITE     DTL2                                 70
      * Set Off *IN41
     C                   EVAL      *IN41 ='0'
     C                   ENDIF
 
     C                   ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * OFLOW - OVERFLOW Processing  for fields that                  *
      *                 For Inserts that are there and Amends and     *
      *                 Deletes that are missing                      *
      *                                                               *
      * Called by: Main Prcessing                                     *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     OFLOW         BEGSR
      *
     C     OFLLN         SUB       PRTLN         RMLN
     C                   IF        RMLN <= 8
     C                   WRITE     HEADH                                70
     C                   ENDIF
 
     C                   ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * OFLOW2- OVERFLOW Processing for differences on >200 chars     *
      *         with file differences                                 *
      *                                                               *
      * Called by: Main Prcessing                                     *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     OFLOW2        BEGSR
      *
     C     OFLLN         SUB       PRTLN         RMLN
     C                   IF        RMLN <= 14
     C                   WRITE     HEADH                                70
     C                   ENDIF
 
     C                   ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * CHKA  - Check there are an even number of Amend Records       *
      *         with file differences                                 *
      *                                                               *
      * Called by: Main Prcessing                                     *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     CHKA          BEGSR
      *
     C                   READ      UPCSCHPD                               70
     C                   DOW       *IN70 = '0'
     C                   IF        UP_CPACTC = 'A'
     C                   ADD       1             Amends
     C                   ENDIF
     C                   READ      UPCSCHPD                               70
     C                   ENDDO
      *
      * Check to see if the No. of Amends is divisible by 2 and not equal 0
     C                   IF        Amends <> 0
     C     Amends        DIV       2             WAmends
     C                   MOVE      WAmends       Remainder
      * If the remainder is ='5' Set on indicator to stop File processing
     C                   IF        REMAINDER ='5'
     C                   EVAL      *IN60 = '1'
     C                   ENDIF
     C                   ENDIF
     C     *START        SETLL     UPCSCHPD
 
     C                   ENDSR
      **********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *INZSR - Program Initialisation routine                       *
      *                                                               *
      * Called by: Implicitly on program activation                   *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     *INZSR        BEGSR
      * Initialise program name
      *
     C                   MOVEL     'UP1413'      DBPGM
      *
      * Move in program fields
     C                   MOVEL     'CGCSCHPD'    SYSFIL
     C                   MOVEL     'UPCSCHPD'    UPDFIL
     C                   MOVEL     HEADER        HEAD
      * Open files
     C                   OPEN      UPCSCHPD
     C                   OPEN      CGCSCHL0
     C                   ENDSR
 
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR
      *
     C                   IF        @RUN = *BLANK
     C                   EVAL      @RUN = 'Y'
     C                   DUMP
      *
     C                   ENDIF
      *
     C                   EVAL      *INU7 = *ON
     C                   EVAL      *INU8 = *ON
     C                   EVAL      *INLR = *ON
     C                   RETURN
      *
     C                   ENDSR
      *
