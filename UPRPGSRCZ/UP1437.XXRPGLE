      *****************************************************************
     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2003')
      *****************************************************************
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP1437 - Process Action File for SDCODEPD                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CUP022 *CREATE     Date 25Mar03               *
      *                                                               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP022 - Creation of new set of Action File utilities.       *
      *                                                               *
      *****************************************************************
      * Function of indicators
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write, update or delete fail
      *
     FUPCODEL0  IF   E           K DISK    INFSR(*PSSR)
     FSDCODEL0  UF A E           K DISK    INFSR(*PSSR)
     F                                     RENAME(@CODEL0:CODEF) PREFIX(X)
      /EJECT
      *
     D LDA           E DS           256    EXTNAME(LDA)
      *
      * Local data area for database error details
      * *LOCK IN LDA immediately before and OUT LDA immediately
      * after each database error handling.
      *
      *                                     134 141 DBFILE
      *        Defines:                     142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *
     D PSDS           SDS
      *
      ** Program Status Data Structure.
      *
     D  PGM              *PROC
     D  WSID                 244    253
     D  USER                 254    263
      *
     C     COMP          KLIST
     C                   KFLD                    FYSWCD
     C                   KFLD                    FYSEQ1
     C                   KFLD                    FYSEQ2
     C                   KFLD                    FYSEQ3
      *
     C     *LOVAL        SETLL     UPCODEL0
      *
     C                   READ      UPCODEL0                             6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C     *IN20         DOWEQ     '0'
      *
     C     FYACTC        IFEQ      'I'
     C                   EXSR      SETVAL
     C                   WRITE     CODEF                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
     C                   ENDIF
      *
     C     FYACTC        IFEQ      'D'
     C     COMP          CHAIN     SDCODEL0                           3069
     C     *IN69         IFEQ      *ON
     C     *IN30         OREQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
      *
     C                   ELSE
     C                   DELETE    CODEF                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
      *
      * If Action code = AMEND then read next record.
     C     FYACTC        IFEQ      'A'
     C                   MOVEL     FYSWCD        SWCDX            20
     C                   Z-ADD     FYSEQ1        SEQ1X             7 0
     C                   Z-ADD     FYSEQ2        SEQ2X             6 0
     C                   Z-ADD     FYSEQ3        SEQ3X             5 0
     C                   READ      UPCODEL0                             6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
      * If key fields do not match exit
      *  - or if EOF indicator is on.
     C     FYSWCD        IFNE      SWCDX
     C     FYSEQ1        ANDNE     SEQ1X
     C     FYSEQ2        ANDNE     SEQ2X
     C     FYSEQ3        ANDNE     SEQ3X
     C     *IN20         OREQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C     COMP          CHAIN     SDCODEL0                           3069
     C     *IN69         IFEQ      *ON
     C     *IN30         OREQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
      *
     C                   ELSE
      *
     C                   EXSR      SETVAL
     C                   UPDATE    CODEF                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
      *
     C                   READ      UPCODEL0                             6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C                   ENDDO
      *
     C     ENDPGM        TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      *****************************************************************
      *                                                               *
      * SETVAL subroutine - set values for deletes/writes/updates     *
      *                                                               *
      *****************************************************************
     C     SETVAL        BEGSR
      * Set values to update record.
     C                   MOVEL     FYSWCD        XFYSWCD
     C                   Z-ADD     FYSEQ1        XFYSEQ1
     C                   Z-ADD     FYSEQ2        XFYSEQ2
     C                   Z-ADD     FYSEQ3        XFYSEQ3
     C                   MOVEL     FYRECI        XFYRECI
     C                   MOVEL     FYSMTP        XFYSMTP
     C                   MOVEL     FYMMID        XFYMMID
     C                   MOVEL     FYDCUS        XFYDCUS
     C                   MOVEL     FYCDES        XFYCDES
     C     FYACTC        IFEQ      'I'
     C                   MOVEL     FYACJN        XFYACJN
     C                   MOVEL     FYACUN        XFYACUN
     C                   MOVEL     FYACTM        XFYACTM
     C                   MOVEL     FYACDA        XFYACDA
     C                   MOVEL     FYLCD         XFYLCD
     C                   MOVEL     FYTYLC        XFYTYLC
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR                                                  ** *PSSR SR **
      *
     C     @RUN          IFEQ      *BLANK
     C                   MOVE      'Y'           @RUN              1
     C                   DUMP
     C                   END
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
      ********************************************************************
