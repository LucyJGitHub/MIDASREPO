     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2004')
      *****************************************************************
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP1481 - Report Action File for SCHELPPD                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CUP022 *CREATE     Date 24Feb04               *
      *                                                               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP022 - Creation of new set of Action File utilities.       *
      *                                                               *
      *****************************************************************
      * Function of indicators
      *
      *       20      End of file indicator
      *       37      Overflow indicator
      *       39      Overflow indicator to signal printing of keys
      *       41      KEY2 has value and must be printed
      *       69      Read or write errors
      *       60      Amended records
      *       61      No amended records to report
      *       62      Amended records headers
      *       63      Print field description for Amend
      *       64      Print 'before' for Amend
      *       65      Print 'after' for Amend
      *       66      Skip 1 line
      *       70      Deleted records
      *       71      No deleted records to report
      *       72      Deleted records headers
      *       80      Inserted records
      *       81      No inserted records to report
      *       82      Inserted records headers
      *
     FUPHELPL0  IF   E           K DISK    INFSR(*PSSR)
     FUP1481P1  O    E             PRINTER INFDS(SPOOL)
     F                                     OFLIND(*IN37)
     F                                     USROPN
      *
      /EJECT
      *
      * File information data structure for P1 file; used for page end
      *   handling
     D SPOOL           DS
     D  SFILE                 83     92
     D  SFNUM                123    124B 0
     D  OFLLN                188    189B 0
     D  PRTLN                367    368B 0
      *
     D LDA           E DS           256    EXTNAME(LDA)
      *
      * Local data area for database error details
      * *LOCK IN LDA immediately before and OUT LDA immediately
      * after each database error handling.
      *
      *                                     134 141 DBFILE
      *        Defines:                     142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *
     D PSDS           SDS
      *
      * Program Status Data Structure.
      *
     D  PGM              *PROC
     D  WSID                 244    253
     D  USER                 254    263
      *
     D KEYS            DS
     D  KEY0                   1    100
     D  KEY1                 101    200
     D VALUES          DS
     D  VALUE0                 1    100
     D  VALUE1               101    200
     D  VALUE2               201    300
     D  VALUE3               301    400
     D  VALUE4               401    500
     D  VALUE5               501    600
     D  X                             3  0
     D  Y                             3  0
     D  Z                             5  0
      *
     C     *ENTRY        PLIST
     C                   PARM                    HDRTXT
      *
     C                   OPEN      UP1481P1
      *
     C                   WRITE     HEADH                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C     *LOVAL        SETLL     UPHELPD0
      *
     C                   READ      UPHELPL0                             6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * If there are no records at all write 'no records'.
     C     *IN20         IFEQ      *ON
     C                   WRITE     NORECS                               69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   WRITE     TRAILP                               69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   GOTO      ENDPGM
      *
     C                   ELSE
     C                   SETON                                        60
     C                   WRITE     DTL1
     C                   SETOFF                                       60
     C                   ENDIF
      *
     C     HPACTC        IFNE      'A'
     C                   SETON                                        61
     C                   WRITE     DTL1
     C                   SETOFF                                       61
     C                   ELSE
     C                   SETON                                        62
     C                   WRITE     DTL1
     C                   SETOFF                                       62
     C                   ENDIF
      *
     C                   EVAL      X = 0
     C                   EVAL      Y = 0
     C     HPACTC        DOWEQ     'A'
     C     *IN20         ANDNE     *ON
      *
      * Write headers if overflow on.
     C     *IN37         IFEQ      *ON
     C                   WRITE     HEADH                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   SETON                                        60
     C                   WRITE     DTL1
     C                   SETOFF                                       60
     C                   SETON                                        62
     C                   WRITE     DTL1
     C                   SETOFF                                       6237
     C                   ENDIF
      *
     C                   EXSR      AMEND
      *
     C                   READ      UPHELPL0                             6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDDO
      *
     C                   WRITE     HEADH                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   SETON                                        70
     C                   WRITE     DTL2
     C                   SETOFF                                       70
      *
     C     HPACTC        IFNE      'D'
     C                   SETON                                        71
     C                   WRITE     DTL2
     C                   SETOFF                                       71
     C                   ELSE
     C                   SETON                                        72
     C                   WRITE     DTL2
     C                   SETOFF                                       72
     C                   ENDIF
      *
     C     HPACTC        DOWEQ     'D'
     C     *IN20         ANDNE     *ON
      *
      * Write headers if overflow on.
     C     *IN37         IFEQ      *ON
     C                   WRITE     HEADH                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   SETON                                        70
     C                   WRITE     DTL2
     C                   SETOFF                                       70
     C                   SETON                                        72
     C                   WRITE     DTL2
     C                   SETOFF                                       7237
     C                   ENDIF
      *
     C                   EXSR      DELETE
      *
     C                   READ      UPHELPL0                             6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDDO
      *
     C                   WRITE     HEADH                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   SETON                                        80
     C                   WRITE     DTL3
     C                   SETOFF                                       80
      *
     C     HPACTC        IFNE      'I'
     C                   SETON                                        81
     C                   WRITE     DTL3
     C                   SETOFF                                       81
     C                   ELSE
     C                   SETON                                        82
     C                   WRITE     DTL3
     C                   SETOFF                                       82
     C                   ENDIF
      *
     C     HPACTC        DOWEQ     'I'
     C     *IN20         ANDNE     *ON
      *
      * Write headers if overflow on.
     C     *IN37         IFEQ      *ON
     C                   WRITE     HEADH                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   SETON                                        80
     C                   WRITE     DTL3
     C                   SETOFF                                       80
     C                   SETON                                        82
     C                   WRITE     DTL3
     C                   SETOFF                                       8237
     C                   ENDIF
      *
     C                   EXSR      INSERT
      *
     C                   READ      UPHELPL0                             6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDDO
      *
     C                   WRITE     TRAILP                               69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C     ENDPGM        TAG
     C                   CLOSE     UP1481P1
     C                   SETON                                        LR
     C                   RETURN
      *****************************************************************
      *                                                               *
      * AMEND subroutine - process amended records                    *
      *                                                               *
      *****************************************************************
     C     AMEND         BEGSR
      *
     C                   SETOFF                                         41
     C                   MOVEL     *BLANKS       KEYS
     C                   MOVEL     HPJSP         KEYS
     C                   MOVEL     KEY0          #JSP1
      *
     C     KEY1          IFNE      *BLANKS
     C                   MOVEL     KEY1          #JSP2
     C                   SETON                                          41
     C                   ENDIF
      *
      * Check values for first and second amended records of pair
      *
     C                   MOVEL     HPHTAG        HTAG1           600
      *
      * Read next record so that values can be compared.
     C                   READ      UPHELPL0                             6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C                   MOVEL     HPHTAG        HTAG2           600
      *
      * Compare values and write records to report.
      *
     C     HTAG1         IFNE      HTAG2
     C                   MOVEL     'HPHTAG'      FIELD
      * Write 'before' value of field.
     C                   EXSR      SR01
     C                   MOVEL     *BLANKS       #JSP1
     C                   MOVEL     *BLANKS       #JSP2
     C                   MOVEL     *BLANKS       FIELD
     C                   MOVEL     *BLANKS       #VALUE
     C                   MOVEL     *BLANKS       VALUES
     C                   MOVEL     HTAG1         VALUES
      *
     C                   SELECT
     C     VALUE5        WHENNE    *BLANKS
     C                   EVAL      X = 5
     C     VALUE4        WHENNE    *BLANKS
     C                   EVAL      X = 4
     C     VALUE3        WHENNE    *BLANKS
     C                   EVAL      X = 3
     C     VALUE2        WHENNE    *BLANKS
     C                   EVAL      X = 2
     C     VALUE1        WHENNE    *BLANKS
     C                   EVAL      X = 1
     C                   OTHER
     C                   EVAL      X = 0
     C                   ENDSL
      *
      * Write 'after' value of field.
     C                   EVAL      Y = 0
     C                   DOW       (Y <= X)
     C                   EVAL      Z = (Y * 100) + 1
     C                   EVAL      #VALUE = *BLANKS
     C                   EVAL      #VALUE = %SUBST(VALUES:Z:100)
     C                   EXSR      SR02
     C                   EVAL      Y = Y + 1
     C                   IF        *IN39 = '1'
     C                   MOVEL     *BLANKS       #JSP1
     C                   MOVEL     *BLANKS       #JSP2
     C                   SETOFF                                       41
     C                   SETOFF                                       39
     C                   ENDIF
     C                   ENDDO
      *
     C                   MOVEL     *BLANKS       #VALUE
     C                   MOVEL     *BLANKS       VALUES
     C                   MOVEL     HTAG2         VALUES
     C                   EVAL      #VALUE = VALUE0
      *
     C                   SELECT
     C     VALUE5        WHENNE    *BLANKS
     C                   EVAL      X = 5
     C     VALUE4        WHENNE    *BLANKS
     C                   EVAL      X = 4
     C     VALUE3        WHENNE    *BLANKS
     C                   EVAL      X = 3
     C     VALUE2        WHENNE    *BLANKS
     C                   EVAL      X = 2
     C     VALUE1        WHENNE    *BLANKS
     C                   EVAL      X = 1
     C                   OTHER
     C                   EVAL      X = 0
     C                   ENDSL
      *
     C                   EVAL      Y = 0
     C                   DOW       (Y <= X)
     C                   EVAL      Z = (Y * 100) + 1
     C                   EVAL      #VALUE = *BLANKS
     C                   EVAL      #VALUE = %SUBST(VALUES:Z:100)
     C                   EXSR      SR03
     C                   EVAL      Y = Y + 1
     C                   IF        *IN39 = '1'
     C                   MOVEL     *BLANKS       #JSP1
     C                   MOVEL     *BLANKS       #JSP2
     C                   SETOFF                                       41
     C                   SETOFF                                       39
     C                   ENDIF
     C                   ENDDO
      *
     C                   ENDIF
      *
     C                   ENDSR
      *****************************************************************
      *                                                               *
      * DELETE subroutine - process deleted records                   *
      *                                                               *
      *****************************************************************
     C     DELETE        BEGSR
      *
     C                   SETOFF                                         74
     C                   MOVEL     *BLANKS       KEYS
     C                   MOVEL     HPJSP         KEYS
     C                   MOVEL     KEY0          #JSP1
      *
     C     KEY1          IFNE      *BLANKS
     C                   MOVEL     KEY1          #JSP2
     C                   SETON                                          74
     C                   ENDIF
      *
     C                   WRITE     DTL2
     C                   SETOFF                                         74
     C     *IN69         IFEQ      *ON
     C                   SETON                                        U7U8
     C                   DUMP
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C                   ENDSR
      *****************************************************************
      *                                                               *
      * INSERT subroutine - process inserted records                  *
      *                                                               *
      *****************************************************************
     C     INSERT        BEGSR
      *
     C                   SETOFF                                         84
     C                   MOVEL     *BLANKS       KEYS
     C                   MOVEL     HPJSP         KEYS
     C                   MOVEL     KEY0          #JSP1
      *
     C     KEY1          IFNE      *BLANKS
     C                   MOVEL     KEY1          #JSP2
     C                   SETON                                          84
     C                   ENDIF
      *
     C                   WRITE     DTL3
     C                   SETOFF                                         84
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C                   ENDSR
      *****************************************************************
      *                                                               *
      * OFLOW subroutine - handle overflow for print of field details *
      *                                                               *
      *****************************************************************
     C     OFLOW         BEGSR
      *
     C     OFLLN         SUB       PRTLN         RMLN              2 0
     C     3             IFGE      RMLN
      *
     C                   WRITE     HEADH                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C                   SETON                                        60
     C                   WRITE     DTL1
     C                   SETOFF                                       60
     C                   SETON                                        62
     C                   WRITE     DTL1
     C                   SETOFF                                       62
      *
     C                   MOVEL     KEY0          #JSP1
     C     KEY1          IFNE      *BLANKS
     C                   MOVEL     KEY1          #JSP2
     C                   SETON                                        41
     C                   ELSE
     C                   SETOFF                                       41
     C                   ENDIF
      *
     C                   SETON                                        39
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SR01 subroutine - 1st part of field compare                   *
      *                                                               *
      *****************************************************************
     C     SR01          BEGSR
      *
      * Find description of field.
     C                   CALL      'UPC0890'
     C                   PARM                    FIELD            10
     C                   PARM                    #FDESC           50
      * Write description of field.
     C                   MOVEL     *BLANKS       #VALUE
      * Check overflow before writing next lines.
     C                   EXSR      OFLOW
     C                   MOVEL     #FDESC        #VALUE
     C                   SETON                                        63
     C                   WRITE     DTL1                                 69
     C                   SETOFF                                       63
     C                   SETOFF                                       41
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C     ESR01         ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SR02 subroutine - 2nd part of field compare                   *
      *                                                               *
      *****************************************************************
     C     SR02          BEGSR
      *
     C                   EXSR      OFLOW
     C                   IF        Y = 0
     C                   SETON                                        64
     C                   ELSE
     C                   SETOFF                                       64
     C                   ENDIF
      *
     C                   WRITE     DTL1                                 69
     C                   SETOFF                                       64
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C     ESR02         ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SR03 subroutine - 3rd part of field compare                   *
      *                                                               *
      *****************************************************************
     C     SR03          BEGSR
      *
     C                   EXSR      OFLOW
     C                   IF        Y = 0
     C                   SETON                                        65
     C                   ELSE
     C                   SETOFF                                       65
     C                   ENDIF
      *
     C                   IF        Y = X
     C                   SETON                                        66
     C                   ELSE
     C                   SETOFF                                       66
     C                   ENDIF
     C                   WRITE     DTL1                                 69
     C                   SETOFF                                       65
     C                   SETOFF                                       66
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C     ESR03         ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR                                                  ** *PSSR SR **
      *
     C     @RUN          IFEQ      *BLANK
     C                   MOVE      'Y'           @RUN              1
     C                   DUMP
     C                   END
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
      *
      ********************************************************************
