     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2004')
      *****************************************************************
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP1671 - Report Action File for RPMENUPD                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CUP032             Date 29Dec05               *
      *                 CUP022 *CREATE     Date 05Apr04               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP032 - Use one printer file for Action Files.              *
      *  CUP022 - Creation of new set of Action File utilities.       *
      *                                                               *
      *****************************************************************
      * Function of indicators
      *
      *       20      End of file indicator
      *       37      Overflow indicator
      *       69      Read or write errors
      *       60      Amended records
      *       61      No amended records to report
      *       62      Amended records headers
      *       63      Print field description for Amend
      *       64      Print 'before' for Amend
      *       65      Print 'after' for Amend
      *       70      Deleted records
      *       71      No deleted records to report
      *       72      Deleted records headers
      *       80      Inserted records
      *       81      No inserted records to report
      *       82      Inserted records headers
      *
     FUPMNU4L0  IF   E           K DISK    INFSR(*PSSR)
     F*UP1671P1 O    E             PRINTER INFDS(SPOOL)                                       CUP032
     FUP0799P1  O    E             PRINTER INFDS(SPOOL)                                       CUP032
     F                                     OFLIND(*IN37)
     F                                     USROPN
      *
      /EJECT
      *
      * File information data structure for P1 file; used for page end
      *   handling
     D SPOOL           DS
     D  SFILE                 83     92
     D  SFNUM                123    124B 0
     D  OFLLN                188    189B 0
     D  PRTLN                367    368B 0
      *
     D                 DS                                                                     CUP032
     D KEYHDR                  1    132                                                       CUP032
     D  HDR1                  10     24    INZ('Menu Group Code')                             CUP032
     D  HDR2                  27     46    INZ('Menu Item Short Code')                        CUP032
     D  HDR3                  49     59                                                       CUP032
      *
     D                 DS                                                                     CUP032
     D KEYDTA                  1    132                                                       CUP032
     D  DTA1                  10     11                                                       CUP032
     D  DTA1N                 10     11  0                                                    CUP032
     D  DTA2                  27     29                                                       CUP032
     D  DTA2N                 27     29  0                                                    CUP032
     D  DTA3                  49     83                                                       CUP032
      *
     D LDA           E DS           256    EXTNAME(LDA)
      *
      * Local data area for database error details
      * *LOCK IN LDA immediately before and OUT LDA immediately
      * after each database error handling.
      *
      *                                     134 141 DBFILE
      *        Defines:                     142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *
     D PSDS           SDS
      *
      * Program Status Data Structure.
      *
     D  PGM              *PROC
     D  WSID                 244    253
     D  USER                 254    263
      *
     D HDR3C           C                   'Description'                                      CUP032
      *
     C     *ENTRY        PLIST
     C                   PARM                    HDRTXT
      *
     C**********         OPEN      UP1671P1                                                   CUP032
     C                   OPEN      UP0799P1                                                   CUP032
      *
     C                   WRITE     HEADH                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C     *LOVAL        SETLL     UPMNU4D0
      *
     C                   READ      UPMNU4L0                             6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * If there are no records at all write 'no records'.
     C     *IN20         IFEQ      *ON
     C                   WRITE     NORECS                               69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   WRITE     TRAILP                               69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   GOTO      ENDPGM
      *
     C                   ELSE
     C                   SETON                                        60
     C                   WRITE     DTL1
     C                   SETOFF                                       60
     C                   ENDIF
      *
     C     MNACTC        IFNE      'A'
     C                   EVAL      HDR3 = HDR3C                                               CUP032
     C                   SETON                                        61
     C                   WRITE     DTL1
     C                   SETOFF                                       61
     C                   ELSE
     C                   EVAL      HDR3 = *blanks                                             CUP032
     C                   SETON                                        62
     C                   WRITE     DTL1
     C                   SETOFF                                       62
     C                   ENDIF
      *
     C     MNACTC        DOWEQ     'A'
     C     *IN20         ANDNE     *ON
      *
      * Write headers if overflow on.
     C     *IN37         IFEQ      *ON
     C                   WRITE     HEADH                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   SETON                                        60
     C                   WRITE     DTL1
     C                   SETOFF                                       60
     C                   SETON                                        62
     C                   WRITE     DTL1
     C                   SETOFF                                       6237
     C                   ENDIF
      *
     C                   EXSR      AMEND
      *
     C                   READ      UPMNU4L0                             6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDDO
      *
     C                   WRITE     HEADH                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   SETON                                        70
     C                   WRITE     DTL2
     C                   SETOFF                                       70
      *
     C     MNACTC        IFNE      'D'
     C                   SETON                                        71
     C                   WRITE     DTL2
     C                   SETOFF                                       71
     C                   ELSE
     C                   EVAL      HDR3 = HDR3C                                               CUP032
     C                   SETON                                        72
     C                   WRITE     DTL2
     C                   SETOFF                                       72
     C                   ENDIF
      *
     C     MNACTC        DOWEQ     'D'
     C     *IN20         ANDNE     *ON
      *
      * Write headers if overflow on.
     C     *IN37         IFEQ      *ON
     C                   WRITE     HEADH                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   SETON                                        70
     C                   WRITE     DTL2
     C                   SETOFF                                       70
     C                   SETON                                        72
     C                   WRITE     DTL2
     C                   SETOFF                                       7237
     C                   ENDIF
      *
     C                   EXSR      DELETE
      *
     C                   READ      UPMNU4L0                             6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDDO
      *
     C                   WRITE     HEADH                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   SETON                                        80
     C                   WRITE     DTL3
     C                   SETOFF                                       80
      *
     C     MNACTC        IFNE      'I'
     C                   SETON                                        81
     C                   WRITE     DTL3
     C                   SETOFF                                       81
     C                   ELSE
     C                   EVAL      HDR3 = HDR3C                                               CUP032
     C                   SETON                                        82
     C                   WRITE     DTL3
     C                   SETOFF                                       82
     C                   ENDIF
      *
     C     MNACTC        DOWEQ     'I'
     C     *IN20         ANDNE     *ON
      *
      * Write headers if overflow on.
     C     *IN37         IFEQ      *ON
     C                   WRITE     HEADH                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   SETON                                        80
     C                   WRITE     DTL3
     C                   SETOFF                                       80
     C                   SETON                                        82
     C                   WRITE     DTL3
     C                   SETOFF                                       8237
     C                   ENDIF
      *
     C                   EXSR      INSERT
      *
     C                   READ      UPMNU4L0                             6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDDO
      *
     C                   WRITE     TRAILP                               69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C     ENDPGM        TAG
     C**********         CLOSE     UP1671P1                                                   CUP032
     C                   CLOSE     UP0799P1                                                   CUP032
     C                   SETON                                        LR
     C                   RETURN
      *****************************************************************
      *                                                               *
      * AMEND subroutine - process amended records                    *
      *                                                               *
      *****************************************************************
     C     AMEND         BEGSR
      *
     C**********         MOVEL     MNMGRP        #MGRP                                        CUP032
     C**********         MOVEL     MNICDE        #ICDE                                        CUP032
     C                   EVAL      DTA1N = MNMGRP                                             CUP032
     C                   EVAL      DTA2N = MNICDE                                             CUP032
      *
      * Check values for first and second amended records of pair
      *
     C                   MOVEL     MNINAR        INAR1            35
     C                   MOVEL     MNPCMD        PCMD1            20
     C                   MOVEL     MNPARA        PARA1            70
     C                   MOVEL     MNACTI        ACTI1             1
      *
      * Read next record so that values can be compared.
     C                   READ      UPMNU4L0                             6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C                   MOVEL     MNINAR        INAR2            35
     C                   MOVEL     MNPCMD        PCMD2            20
     C                   MOVEL     MNPARA        PARA2            70
     C                   MOVEL     MNACTI        ACTI2             1
      *
      * Compare values and write records to report.
      *
     C     INAR1         IFNE      INAR2
     C                   MOVEL     'MNINAR'      FIELD
      * Write 'before' value of field.
     C                   EXSR      SR01
     C**********         MOVEL     *BLANKS       #MGRP                                        CUP032
     C**********         MOVEL     *BLANKS       #ICDE                                        CUP032
     C                   EVAL      KEYDTA = *blanks                                           CUP032
     C                   MOVEL     *BLANKS       FIELD
     C                   MOVEL     *BLANKS       #VALUE
     C                   MOVEL     INAR1         #VALUE
      * Write 'after' value of field.
     C                   EXSR      SR02
     C                   MOVEL     *BLANKS       #VALUE
     C                   MOVEL     INAR2         #VALUE
     C                   EXSR      SR03
     C                   ENDIF
      *
     C     PCMD1         IFNE      PCMD2
     C                   MOVEL     'MNPCMD'      FIELD
      * Write 'before' value of field.
     C                   EXSR      SR01
     C**********         MOVEL     *BLANKS       #MGRP                                        CUP032
     C**********         MOVEL     *BLANKS       #ICDE                                        CUP032
     C                   EVAL      KEYDTA = *blanks                                           CUP032
     C                   MOVEL     *BLANKS       FIELD
     C                   MOVEL     *BLANKS       #VALUE
     C                   MOVEL     PCMD1         #VALUE
      * Write 'after' value of field.
     C                   EXSR      SR02
     C                   MOVEL     *BLANKS       #VALUE
     C                   MOVEL     PCMD2         #VALUE
     C                   EXSR      SR03
     C                   ENDIF
      *
     C     PARA1         IFNE      PARA2
     C                   MOVEL     'MNPARA'      FIELD
      * Write 'before' value of field.
     C                   EXSR      SR01
     C**********         MOVEL     *BLANKS       #MGRP                                        CUP032
     C**********         MOVEL     *BLANKS       #ICDE                                        CUP032
     C                   EVAL      KEYDTA = *blanks                                           CUP032
     C                   MOVEL     *BLANKS       FIELD
     C                   MOVEL     *BLANKS       #VALUE
     C                   MOVEL     PARA1         #VALUE
      * Write 'after' value of field.
     C                   EXSR      SR02
     C                   MOVEL     *BLANKS       #VALUE
     C                   MOVEL     PARA2         #VALUE
     C                   EXSR      SR03
     C                   ENDIF
      *
     C     ACTI1         IFNE      ACTI2
     C                   MOVEL     'MNACTI'      FIELD
      * Write 'before' value of field.
     C                   EXSR      SR01
     C**********         MOVEL     *BLANKS       #MGRP                                        CUP032
     C**********         MOVEL     *BLANKS       #ICDE                                        CUP032
     C                   EVAL      KEYDTA = *blanks                                           CUP032
     C                   MOVEL     *BLANKS       FIELD
     C                   MOVEL     *BLANKS       #VALUE
     C                   MOVEL     ACTI1         #VALUE
      * Write 'after' value of field.
     C                   EXSR      SR02
     C                   MOVEL     *BLANKS       #VALUE
     C                   MOVEL     ACTI2         #VALUE
     C                   EXSR      SR03
     C                   ENDIF
      *
     C                   ENDSR
      *****************************************************************
      *                                                               *
      * DELETE subroutine - process deleted records                   *
      *                                                               *
      *****************************************************************
     C     DELETE        BEGSR
      *
     C**********         MOVEL     MNMGRP        #MGRP                                        CUP032
     C**********         MOVEL     MNICDE        #ICDE                                        CUP032
     C                   EVAL      DTA1N = MNMGRP                                             CUP032
     C                   EVAL      DTA2N = MNICDE                                             CUP032
     C                   EVAL      DTA3 = MNINAR                                              CUP032
      *
      **If*description*field**exists*then*move*this*                                          CUP032
     C**********         MOVEL     MNINAR        #TEXT                                        CUP032
     C                   WRITE     DTL2                                 69
     C     *IN69         IFEQ      *ON
     C                   SETON                                        U7U8
     C                   DUMP
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C                   ENDSR
      *****************************************************************
      *                                                               *
      * INSERT subroutine - process inserted records                  *
      *                                                               *
      *****************************************************************
     C     INSERT        BEGSR
      *
     C**********         MOVEL     MNMGRP        #MGRP                                        CUP032
     C**********         MOVEL     MNICDE        #ICDE                                        CUP032
     C                   EVAL      DTA1N = MNMGRP                                             CUP032
     C                   EVAL      DTA2N = MNICDE                                             CUP032
     C                   EVAL      DTA3 = MNINAR                                              CUP032
      *
      **If*description*field**exists*then*move*this*                                          CUP032
     C**********         MOVEL     MNINAR        #TEXT                                        CUP032
     C                   WRITE     DTL3                                 69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C                   ENDSR
      *****************************************************************
      *                                                               *
      * OFLOW subroutine - handle overflow for print of field details *
      *                                                               *
      *****************************************************************
     C     OFLOW         BEGSR
      *
     C     OFLLN         SUB       PRTLN         RMLN              2 0
     C     3             IFGE      RMLN
      *
     C                   WRITE     HEADH                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C                   SETON                                        60
     C                   WRITE     DTL1
     C                   SETOFF                                       60
     C                   SETON                                        62
     C                   WRITE     DTL1
     C                   SETOFF                                       62
      *
     C**********         MOVEL     MNMGRP        #MGRP                                        CUP032
     C**********         MOVEL     MNICDE        #ICDE                                        CUP032
     C                   EVAL      DTA1N = MNMGRP                                             CUP032
     C                   EVAL      DTA2N = MNICDE                                             CUP032
     C                   MOVEL     *BLANKS       #VALUE
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SR01 subroutine - 1st part of field compare                   *
      *                                                               *
      *****************************************************************
     C     SR01          BEGSR
      *
      * Find description of field.
     C                   CALL      'UPC0890'
     C                   PARM                    FIELD            10
     C                   PARM                    #FDESC           50
      * Write description of field.
     C                   MOVEL     *BLANKS       #VALUE
      * Check overflow before writing next lines.
     C                   EXSR      OFLOW
     C                   MOVEL     #FDESC        #VALUE
     C                   SETON                                        63
     C                   WRITE     DTL1                                 69
     C                   SETOFF                                       63
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C     ESR01         ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SR02 subroutine - 2nd part of field compare                   *
      *                                                               *
      *****************************************************************
     C     SR02          BEGSR
      *
     C                   SETON                                        64
     C                   WRITE     DTL1                                 69
     C                   SETOFF                                       64
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C     ESR02         ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SR03 subroutine - 3rd part of field compare                   *
      *                                                               *
      *****************************************************************
     C     SR03          BEGSR
      *
     C                   SETON                                        65
     C                   WRITE     DTL1                                 69
     C                   SETOFF                                       65
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C     ESR03         ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR                                                  ** *PSSR SR **
      *
     C     @RUN          IFEQ      *BLANK
     C                   MOVE      'Y'           @RUN              1
     C                   DUMP
     C                   END
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
      *
      ********************************************************************
