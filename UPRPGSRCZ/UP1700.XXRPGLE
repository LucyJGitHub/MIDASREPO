     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2010')
      *****************************************************************
/*X*IA***OVRDBF*FILE(TBTZ1)*TOFILE(UPTBTZTPD)**************************                       CUP021
/*X*IB***OVRDBF*FILE(TBTZ2)*TOFILE(UPTBTZTPD)**************************                       CUP021
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP1700 - Build Action File for TABLETZ                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      *  Prev Amend No. CUP003  *CREATE    Date 06Aug10               *
      *                                                               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CUP003 - New Action File                                     *
      *                                                               *
      *****************************************************************
      * Function of indicators
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write or chain fail
      *       70 - 75 Indicators for changed fields
      *
     FTBTZ1     IF   E             DISK    INFSR(*PSSR)
     FTBTZ2     IF   E           K DISK    INFSR(*PSSR)
     F                                     RENAME(TABLETZF:TBTZF) PREFIX(X)
     FUPTBTZPD  O    E             DISK    INFSR(*PSSR)
      *
      /EJECT
     D LDA           E DS           256    EXTNAME(LDA)
      *
      * Local data area for database error details
      * *LOCK IN LDA immediately before and OUT LDA immediately
      * after each database error handling.
      *
      *                                     134 141 DBFILE
      *       Defines:                      142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *
     D PSDS           SDS
      *
      * Program Status Data Structure.
      *
     D  PGM              *PROC
     D  WSID                 244    253
     D  USER                 254    263
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    MODE              1
      *
     C     COMP          KLIST
     C                   KFLD                    TRAT
      *
     C                   READ      TBTZ1                                6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C     *IN20         DOWEQ     '0'
     C     COMP          CHAIN     TBTZF                              3069
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * If record not found then record has been deleted or added
      *  depending on mode.
     C     *IN30         IFEQ      *ON
     C                   MOVE      MODE          A1ACTC
     C                   WRITE     UPTBTZD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
      * Need to find differences but this should only be run once;
      *  run only if mode is 'Delete'.
     C     MODE          IFEQ      'D'
      *
      * If record found then look for any differences; only look in
      *  fields that have data all of the time.
     C     *IN30         IFEQ      *OFF
     C                   MOVE      'A'           A1ACTC
      * Record ID
     C     RECI          IFNE      XRECI
     C                   SETON                                        70
     C                   ENDIF
      * Record Type
     C     RECT          IFNE      XRECT
     C                   SETON                                        71
     C                   ENDIF
      * Retail Transaction Name
     C     TRAN          IFNE      XTRAN
     C                   SETON                                        72
     C                   ENDIF
      * Customer Originated Ind.
     C     CUOR          IFNE      XCUOR
     C                   SETON                                        73
     C                   ENDIF
      * Debit/Credit Indicator
     C     DCIN          IFNE      XDCIN
     C                   SETON                                        74
     C                   ENDIF
      * Reversal Indicator
     C     REVI          IFNE      XREVI
     C                   SETON                                        75
     C                   ENDIF
      * Print Advices Indicator
     C     ADVI          IFNE      XADVI
     C                   SETON                                        76
     C                   ENDIF
      * General purpose indicator
     C     ACCOM         IFNE      XACCOM
     C                   SETON                                        77
     C                   ENDIF
      *
      * Change length of INTEST and INTNUM to match number of fields compared
     C                   MOVEA     *IN(70)       INTEST            8
     C                   MOVE      INTEST        INTNUM            8 0
      * If any indicators are set on then write record.
     C     INTNUM        IFNE      0
     C                   WRITE     UPTBTZD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * Set values to write second record.
     C                   MOVEL     XRECI         RECI
     C                   MOVEL     XRECT         RECT
     C                   MOVEL     XTRAN         TRAN
     C                   MOVEL     XCUOR         CUOR
     C                   MOVEL     XDCIN         DCIN
     C                   MOVEL     XREVI         REVI
     C                   MOVEL     XADVI         ADVI
     C                   MOVEL     XACCOM        ACCOM
      *
     C                   WRITE     UPTBTZD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
     C                   ENDIF
      *
      * Read next record
     C                   MOVEA     '0000000000'  *IN(70)
     C                   READ      TBTZ1                                6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C                   ENDDO
      *
     C     ENDPGM        TAG
     C                   SETON                                        LR
     C                   RETURN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR                                                  ** *PSSR SR **
      *
     C     @RUN          IFEQ      *BLANK
     C                   MOVE      'Y'           @RUN              1
     C                   DUMP
     C                   END
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
      *
      ********************************************************************
