     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2010')
      *****************************************************************
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP1733 - Action file compatibilty report for UPTBV4PD        *
      *                                                               *
      *  Function:  This program compares the differences between     *
      *             Action file delivered and the system.             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      *  Prev Amend No. BUG27793  *CREATE  Date 16Jun10               *
      *                                                               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  BUG27793 - New action file.                                  *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Use of indicators.                                           *
      *                                                               *
      *    30         Error on Read                                   *
      *    31         EOF on Read                                     *
      *    32         Chain Fail on Insert routine                    *
      *    33         Chain Fail on Delete routine                    *
      *    34         Record there on chain but different             *
      *    35         Chain Fail on Amend routine                     *
      *    36         Record there on Chain but different             *
      *    37         Compare Indicator for Delete Routine            *
      *    38         Compare Indicator for Amend Routine             *
      *    39         Switched on if a record is found on file(Delete)*
      *    40         Conflicts detected                              *
      *    41         First line of record written                    *
      *    42         Same Record in Sys and UP for Insert            *
      *    60         Do not run report if error on Amends            *
      *    70         Error on Write                                  *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E  I N D E X                               *
      *                                                               *
      * *PSSR - Error processing                                      *
      * *INZSR - Initialise                                           *
      *                                                               *
      *    The *INZSR subroutine will only get called automatically   *
      *    on entry to the module the first time it is run            *
      *    (unless you end the program with LR on).  Similarly        *
      *    D-spec initialisation only happens the first time.  Use    *
      *    RESET for subsequent passes.                               *
      *                                                               *
      *****************************************************************
      ** +--------------------------------------+
      ** ¦ F-specs                              ¦
      ** ¦ =======                              ¦
      ** +--------------------------------------+
     FUPTBV4L0  IF   E           K DISK    INFSR(*PSSR) Prefix(UP_) USROPN
     FTABCGU1   IF   E           K DISK    INFSR(*PSSR) Prefix(SYS_) USROPN
     FUP0803P1  O    E             PRINTER INFSR(*PSSR) INFDS(SPOOL)
 
      *****************************************************************
      /EJECT
      *****************************************************************
      ** +--------------------------------------+
      ** ¦ D-specs                              ¦
      ** ¦ =======                              ¦
      ** +--------------------------------------+
     D LDA           E DS           256    EXTNAME(LDA)
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
     D                 DS
     D RecordTypeA             1      2
     D RecordTypeN             1      2  0
      *
     D ACTIONFDTA     UDS           256
     D SYSFIL                  1     10
     D UPDFIL                 31     40
      *
      ** Data Area in QTEMP giving report header data
      *
     D/COPY ZACPYSRC,PSDS
      *
      ** Program Status Data Structure
      *
      ** +--------------------------------------+
      ** ¦ Named constants                      ¦
      ** ¦ ===============                      ¦
      ** +--------------------------------------+
 
     D Header          C                   'Report of the differences between -
     D                                     the Existing System Record and the -
     D                                     Replacement Record'
 
     D Counter         C                   '---------+---------+---------+------
     D                                     ----+---------+---------+---------+--
     D                                     --------+---------+---------+'
 
      ** +--------------------------------------+
      ** ¦ Arrays and Data Structures           ¦
      ** ¦ ==========================           ¦
      ** +--------------------------------------+
     D UpdatArray      S              1A   DIM(211)
     D SysArray        S              1A   DIM(211)
     D ErrorArray      S              1A   DIM(211)
 
      ** Data Structure for the Update file
     D                 DS
     D UP_RECI
     D UP_RECT
     D UP_ZZ010
     D NCU_GRPN                            LIKE(UP_GRPN)
     D NCU_MDBI                            LIKE(UP_MDBI)
     D NCU_ACMI                            LIKE(UP_ACMI)
     D NCU_DINI                            LIKE(UP_DINI)
     D NCU_INAI                            LIKE(UP_INAI)
     D NCU_MINI                            LIKE(UP_MINI)
     D NCU_DYSI                            LIKE(UP_DYSI)
     D NCU_FIXI                            LIKE(UP_FIXI)
     D NCU_MDBAQQ                     4  0
     D NCU_ACMAQQ                     4  0
     D NCU_DINAQQ                     4  0
     D NCU_INAAQQ                     4  0
     D NCU_MINAQQ                     4  0
     D NCU_DYSAQQ                     4  0
     D NCU_FIXAQQ                     4  0
     D NCU_TIMV                       7P 0
     D NCU_ZZ007                           LIKE(UP_ZZ007)
     D NCU_LCD                        5P 0
     D NCU_CHTP                            LIKE(UP_CHTP)
     D NCU_TNLU                       5P 0
     D NCU_MDBA                      10S 0
     D NCU_ACMA                      10S 0
     D NCU_DINA                      10S 0
     D NCU_INAA                      10S 0
     D NCU_MINA                      10S 0
     D NCU_DYSA                      10S 0
     D NCU_FIXA                      10S 0
     D NCU_ZONE                            LIKE(UP_ZONE)
     D NCU_IUSR                            LIKE(UP_IUSR)
     D NCU_AUSR                            LIKE(UP_AUSR)
     D NCU_XUSR                            LIKE(UP_XUSR)
     D NCU_AACC                            LIKE(UP_AACC)
     D NCU_EACC                       5P 0
     D NCU_GPDT                            LIKE(UP_GPDT)
     D  Updfield               1    211A
     D  UPDLIN1                1    100A
     D  UPDLIN2              101    200A
     D  UPDLIN3              201    211A
 
      ** Data Structure for the Core file
     D                 DS
     D SYS_RECI
     D SYS_RECT
     D SYS_ZZ010
     D NCS_GRPN                            LIKE(SYS_GRPN)
     D NCS_MDBI                            LIKE(SYS_MDBI)
     D NCS_ACMI                            LIKE(SYS_ACMI)
     D NCS_DINI                            LIKE(SYS_DINI)
     D NCS_INAI                            LIKE(SYS_INAI)
     D NCS_MINI                            LIKE(SYS_MINI)
     D NCS_DYSI                            LIKE(SYS_DYSI)
     D NCS_FIXI                            LIKE(SYS_FIXI)
     D NCS_MDBAQQ                     4  0
     D NCS_ACMAQQ                     4  0
     D NCS_DINAQQ                     4  0
     D NCS_INAAQQ                     4  0
     D NCS_MINAQQ                     4  0
     D NCS_DYSAQQ                     4  0
     D NCS_FIXAQQ                     4  0
     D NCS_TIMV                       7P 0
     D NCS_ZZ007                           LIKE(SYS_ZZ007)
     D NCS_LCD                        5P 0
     D NCS_CHTP                            LIKE(SYS_CHTP)
     D NCS_TNLU                       5P 0
     D NCS_MDBA                      10S 0
     D NCS_ACMA                      10S 0
     D NCS_DINA                      10S 0
     D NCS_INAA                      10S 0
     D NCS_MINA                      10S 0
     D NCS_DYSA                      10S 0
     D NCS_FIXA                      10S 0
     D NCS_ZONE                            LIKE(SYS_ZONE)
     D NCS_IUSR                            LIKE(SYS_IUSR)
     D NCS_AUSR                            LIKE(SYS_AUSR)
     D NCS_XUSR                            LIKE(SYS_XUSR)
     D NCS_AACC                            LIKE(SYS_AACC)
     D NCS_EACC                       5P 0
     D NCS_GPDT                            LIKE(SYS_GPDT)
     D  CoreFld                1    211A
     D  SYSLIN1                1    100A
     D  SYSLIN2              101    200A
     D  SYSLIN3              201    211A
 
      ** Data Structure for the Error indicators
     D                 DS           211
     D  Indfld                 1    211A
     D  INDLIN1                1    100A
     D  INDLIN2              101    200A
     D  INDLIN3              201    211A
 
     DSPOOL            DS
     D SFILE                  83     92
     D SFNUM                 123    124B 0
     D OFLLN                 188    189B 0
     D PRTLN                 367    368B 0
 
      ** +--------------------------------------+
      ** ¦ Declared variables                   ¦
      ** ¦ ==================                   ¦
      ** +--------------------------------------+
 
     D @RUN            S              1
     D KEY_RECT        S                   LIKE(UP_RECT)
     D KEY_ZZ010       S                   LIKE(UP_ZZ010)
     D X               S              5  0
     D ACTION          S              7
     D RMLN            S              2  0
     D Amends          S              5  1
     D COUNT           S            100
     D COUNT11         S             11
     D WAmends         S              5  1
     D Remainder       S              1
 
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ** +--- Start of Main processing -----------------------------------+
      ** ¦                                                                ¦
      ** ¦ Initial processing is performed automatically: the *INZSR is   ¦
      ** ¦ executed at program activation.                                ¦
      ** ¦                                                                ¦
      ** +----------------------------------------------------------------+
 
      *********************************************************************
      * MAIN PROCEDURE                                                    *
      *********************************************************************
      *
      * Fill fields not to be compared with '='s, so that they  will not be
      * any different between the system file and the update file.
     C                   EVAL      NCU_GRPN   = '=============================='
     C                   EVAL      NCU_MDBI   = '='
     C                   EVAL      NCU_ACMI   = '='
     C                   EVAL      NCU_DINI   = '='
     C                   EVAL      NCU_INAI   = '='
     C                   EVAL      NCU_MINI   = '='
     C                   EVAL      NCU_DYSI   = '='
     C                   EVAL      NCU_FIXI   = '='
     C                   EVAL      NCU_MDBAQQ = 0
     C                   EVAL      NCU_ACMAQQ = 0
     C                   EVAL      NCU_DINAQQ = 0
     C                   EVAL      NCU_INAAQQ = 0
     C                   EVAL      NCU_MINAQQ = 0
     C                   EVAL      NCU_DYSAQQ = 0
     C                   EVAL      NCU_FIXAQQ = 0
     C                   EVAL      NCU_TIMV   = 0
     C                   EVAL      NCU_ZZ007  = '======='
     C                   EVAL      NCU_LCD    = 0
     C                   EVAL      NCU_CHTP   = '='
     C                   EVAL      NCU_MDBA = 0
     C                   EVAL      NCU_ACMA = 0
     C                   EVAL      NCU_DINA = 0
     C                   EVAL      NCU_INAA = 0
     C                   EVAL      NCU_MINA = 0
     C                   EVAL      NCU_DYSA = 0
     C                   EVAL      NCU_FIXA = 0
     C                   EVAL      NCU_ZONE   = '=========='
     C                   EVAL      NCU_IUSR   = '=========='
     C                   EVAL      NCU_AUSR   = '=========='
     C                   EVAL      NCU_XUSR   = '=========='
     C                   EVAL      NCU_AACC   = '='
     C                   EVAL      NCU_EACC   = 0
     C                   EVAL      NCU_GPDT   = '='
      *
     C                   EVAL      NCS_GRPN   = '=============================='
     C                   EVAL      NCS_MDBI   = '='
     C                   EVAL      NCS_ACMI   = '='
     C                   EVAL      NCS_DINI   = '='
     C                   EVAL      NCS_INAI   = '='
     C                   EVAL      NCS_MINI   = '='
     C                   EVAL      NCS_DYSI   = '='
     C                   EVAL      NCS_FIXI   = '='
     C                   EVAL      NCS_MDBAQQ = 0
     C                   EVAL      NCS_ACMAQQ = 0
     C                   EVAL      NCS_DINAQQ = 0
     C                   EVAL      NCS_INAAQQ = 0
     C                   EVAL      NCS_MINAQQ = 0
     C                   EVAL      NCS_DYSAQQ = 0
     C                   EVAL      NCS_FIXAQQ = 0
     C                   EVAL      NCS_TIMV   = 0
     C                   EVAL      NCS_ZZ007  = '======='
     C                   EVAL      NCS_LCD    = 0
     C                   EVAL      NCS_CHTP   = '='
     C                   EVAL      NCS_ZONE   = '=========='
     C                   EVAL      NCS_IUSR   = '=========='
     C                   EVAL      NCS_AUSR   = '=========='
     C                   EVAL      NCS_XUSR   = '=========='
     C                   EVAL      NCS_AACC   = '='
     C                   EVAL      NCS_EACC   = 0
     C                   EVAL      NCS_GPDT   = '='
      *
      ** Read in Installation Data
     C     *DTAARA       DEFINE                  LDA
      *
      ** Sub Routine to check that there are an equal number or Amends
      ** before running reports.
     C                   EXSR      CHKA
 
      *There was no problem with an Odd number of Amends
     C                   IF        *IN60 = '0'
      * Set up key list
     C     KEY           KLIST
     C                   KFLD                    KEY_RECT
     C                   KFLD                    KEY_ZZ010
 
     C                   READ      UPTBV4L0                             3031
      *
      * If an Error or End of file reached
     C                   IF        *IN30 = '1'
      *
     C     *LOCK         IN        LDA
     C                   EVAL      *INU7 = *ON
     C                   EVAL      *INU8 = *ON
     C                   EVAL      DBFILE =  'UPTBV4L0'
     C                   EVAL      RecordTypeN = UP_RECT
     C                   EVAL      DBKEY  =  RecordTypeA + UP_ZZ010
     C                   EVAL      DBPGM  =  PSProcPgm
     C                   EVAL      DBASE  =  001
     C                   EVAL      DBMOD  =  PSProcMod
     C                   EVAL      DBPROC =  'SR/MAIN'
     C                   OUT       LDA
     C                   EXSR      *PSSR
     C                   ELSE
      *
     C                   WRITE     HEADH                                70
      *
      * Do while a record exists
     C                   DOW       *IN31 = '0'
     C                   EVAL      ACTION = *BLANKS
     C                   EVAL      Indfld = *BLANKS
      *
      * If Action Code is Insert
     C                   IF        UP_V4ACTC = 'I'
     C                   Z-ADD     UP_RECT       KEY_RECT
     C                   MOVEL     UP_ZZ010      KEY_ZZ010
     C     KEY           CHAIN     TABCGU1                            32
      *
      * If a record is found on the chain then print out a message
     C                   IF        *IN32 = '0'
      * If the two records are different - Move along Character by Character indicating
      * differences on the third Array (ErrorArray) ELSE the record saying Identical
     C                   IF        CoreFld <> Updfield
     C                   MOVEA     Updfield      UpdatArray
     C                   MOVEA     CoreFld       SysArray
     C                   CLEAR                   ErrorArray
     C                   Z-ADD     1             X
     C                   DOU       X > 211
     C     SysArray(X)   COMP      UpdatArray(X)                          43
     C                   IF        *IN43 = '0'
     C                   EVAL      *IN39 = '1'
     C                   MOVEA     '*'           ErrorArray(X)
     C                   ENDIF
     C                   ADD       1             X
     C                   ENDDO
      * Move error indicator array to field for output
     C                   MOVEA     ErrorArray    IndFld
      *
     C                   MOVEL     'INSERT'      ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ELSE
     C                   EVAL      *IN42 = '1'
     C                   MOVEL     'INSERT'      ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
      * If Action Code is Delete
     C                   IF        UP_V4ACTC = 'D'
     C                   Z-ADD     UP_RECT       KEY_RECT
     C                   MOVEL     UP_ZZ010      KEY_ZZ010
     C     KEY           CHAIN     TABCGU1                            33
      *
      * If a file is not found on the chain print out a message
     C                   IF        *IN33 = '1'
     C                   MOVEL     'DELETE'      ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ELSE
      *
      * If the two records are different - Move along Character by Character indicating
      * differences on the third Array (ErrorArray)
     C                   IF        Corefld <> Updfield
     C                   MOVEA     Updfield      UpdatArray
     C                   MOVEA     CoreFld       SysArray
     C                   CLEAR                   ErrorArray
     C                   Z-ADD     1             X
     C                   DOU       X > 211
     C     SysArray(X)   COMP      UpdatArray(X)                          36
     C                   IF        *IN36 = '0'
     C                   EVAL      *IN34 = '1'
     C                   MOVEA     '*'           ErrorArray(X)
     C                   EndIf
     C                   ADD       1             X
     C                   EndDo
      * Move error indicator array to field for output
     C                   MOVEA     ErrorArray    IndFld
      *
      * Write out all three data structures with the indicator showing
      * where the error is
     C                   MOVEL     'DELETE'      ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
      * If Action Code is Amend
     C                   IF        UP_V4ACTC = 'A'
     C                   Z-ADD     UP_RECT       KEY_RECT
     C                   MOVEL     UP_ZZ010      KEY_ZZ010
     C     KEY           CHAIN     TABCGU1                            35
      *
      * If a file is not found on the chain print out a message
     C                   IF        *IN35 = '1'
     C                   MOVEL     'AMEND'       ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
      * Because amends come in pairs read the after image of amend.
     C                   READ      UPTBV4L0                               31
     C                   ELSE
      * If the before image record and core record are different, read
      * the after amend image record and compare it with the core record.
     C                   IF        CoreFld <> Updfield
     C                   READ      UPTBV4L0                               31
      *
      * If The two records are different - Move along Character by Character indicating
      * differences on the third Array (ErrorArray)
     C                   IF        CoreFld <> Updfield
     C                   MOVEA     Updfield      UpdatArray
     C                   MOVEA     CoreFld       SysArray
     C                   CLEAR                   ErrorArray
     C                   Z-ADD     1             X
     C                   DOU       X > 211
     C     SysArray(X)   COMP      UpdatArray(X)                          38
     C                   IF        *IN38 = '0'
     C                   EVAL      *IN36 = '1'
     C                   MOVEA     '*'           ErrorArray(X)
     C                   ENDIF
     C                   ADD       1             X
     C                   ENDDO
      * Move error indicator array to field for output
     C                   MOVEA     ErrorArray    IndFld
      *
      * Write out all three data structures with the indicator showing
      *  where the error is.
      * Write the after image data.
     C                   MOVEL     'AMEND'       ACTION
     C                   EXSR      REPORT
     C                   EVAL      *IN40 = '1'
     C                   ENDIF
     C                   ELSE
      *  If the before image record is same as core record,
      *  read the after image record but nothing is done.
     C                   READ      UPTBV4L0                               31
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
     C                   MOVEA     '000000000'   *IN(30)
     C                   MOVEA     '0'           *IN(39)
     C                   MOVEA     '00'          *IN(41)
     C                   READ      UPTBV4L0                             3031
      *
      * If an Error
     C                   IF        *IN30 = '1'
      *
     C     *LOCK         IN        LDA
     C                   EVAL      *INU7 = *ON
     C                   EVAL      *INU8 = *ON
     C                   EVAL      DBFILE =  'UPTBV4L0'
     C                   EVAL      RecordTypeN = UP_RECT
     C                   EVAL      DBKEY  =  RecordTypeA + UP_ZZ010
     C                   EVAL      DBPGM  =  PSProcPgm
     C                   EVAL      DBASE  =  002
     C                   EVAL      DBMOD  =  PSProcMod
     C                   EVAL      DBPROC =  'SR/MAIN'
     C                   OUT       LDA
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDDO
     C                   ENDIF
      * If there are no differences between the two files UP and Sys
      * Print No Differences ...
     C                   IF        *IN40 = '0'
     C                   WRITE     NODIFS                               70
     C                   ENDIF
      *
      * ... else Error in No of Amend Records
     C                   ELSE
     C                   WRITE     HEADH
     C                   WRITE     AMENDERR
     C                   ENDIF
      *
     C                   WRITE     TRAILP                               70
      *
     C                   SETON                                        LR
     C                   RETURN
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * REPORT - WRITE To the printer file                            *
      *                                                               *
      * Called by: Main Prcessing                                     *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     REPORT        BEGSR
      *
      *
     C                   IF        (*IN33 = '1') OR
     C                             (*IN35 = '1') OR
     C                             (*IN42 = '1')
      * Overflow check for an Insert that exists or an Amend/Delete that is missing
     C                   EXSR      OFLOW
      * Write first 100  characters
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     UPDLIN1       UPDLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL1                                 70
      * Set on *IN41 to stop the appearance of Action
      * Write next 100  characters
     C                   EVAL      *IN41 = '1'
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     UPDLIN2       UPDLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL1                                 70
      * Write remaining characters
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       COUNT
     C                   MOVEL     UPDLIN3       UPDLIN
     C                   MOVEL     Counter       COUNT11
     C                   MOVEL     COUNT11       COUNT
     C                   WRITE     DTL1                                 70
      * Set Off *IN41
     C                   EVAL      *IN41 ='0'
     C                   ENDIF
      *
     C                   IF        (*IN34 = '1') OR
     C                             (*IN36 = '1') OR
     C                             (*IN39 = '1')
      * Overflow check for an Insert that exists or an Amend/Delete that is missing
     C                   EXSR      OFLOW2
      *
      * Write first 100 characters
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN1       UPDLIN
     C                   MOVEL     INDLIN1       INDLIN
     C                   MOVEL     SYSLIN1       SYSLIN
     C                   MOVEL     Counter       COUNT
     C                   WRITE     DTL2                                 70
      * Set on *IN41 to stop the appearance of Action
     C                   EVAL      *IN41 = '1'
      * Write next 100 characters
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     UPDLIN2       UPDLIN
     C                   MOVEL     INDLIN2       INDLIN
     C                   MOVEL     SYSLIN2       SYSLIN
     C                   WRITE     DTL2                                 70
      * Write remaining characters
     C                   MOVEL     *BLANKS       SYSLIN
     C                   MOVEL     *BLANKS       UPDLIN
     C                   MOVEL     *BLANKS       INDLIN
     C                   MOVEL     *BLANKS       COUNT
     C                   MOVEL     UPDLIN3       UPDLIN
     C                   MOVEL     INDLIN3       INDLIN
     C                   MOVEL     SYSLIN3       SYSLIN
     C                   MOVEL     Counter       COUNT11
     C                   MOVEL     COUNT11       COUNT
     C                   WRITE     DTL2                                 70
      * Set Off *IN41
     C                   EVAL      *IN41 ='0'
     C                   ENDIF
 
     C                   ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * OFLOW - OVERFLOW Processing  for fields that                  *
      *                 For Inserts that are there and Amends and     *
      *                 Deletes that are missing                      *
      *                                                               *
      * Called by: Main Prcessing                                     *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     OFLOW         BEGSR
      *
     C     OFLLN         SUB       PRTLN         RMLN
     C                   IF        RMLN <= 8
     C                   WRITE     HEADH                                70
     C                   ENDIF
 
     C                   ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * OFLOW2- OVERFLOW Processing for differences on >200 chars     *
      *         with file differences                                 *
      *                                                               *
      * Called by: Main Prcessing                                     *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     OFLOW2        BEGSR
      *
     C     OFLLN         SUB       PRTLN         RMLN
     C                   IF        RMLN <= 14
     C                   WRITE     HEADH                                70
     C                   ENDIF
 
     C                   ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * CHKA  - Check there are an even number of Amend Records       *
      *         with file differences                                 *
      *                                                               *
      * Called by: Main Prcessing                                     *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
 
     C     CHKA          BEGSR
      *
     C                   READ      UPTBV4L0                               70
     C                   DOW       *IN70 = '0'
     C                   IF        UP_V4ACTC = 'A'
     C                   ADD       1             Amends
     C                   ENDIF
     C                   READ      UPTBV4L0                               70
     C                   ENDDO
      *
      * Check to see if the No. of Amends is divisible by 2 and not equal 0
     C                   IF        Amends <> 0
     C     Amends        DIV       2             WAmends
     C                   MOVE      WAmends       Remainder
      * If the remainder is ='5' Set on indicator to stop File processing
     C                   IF        REMAINDER ='5'
     C                   EVAL      *IN60 = '1'
     C                   ENDIF
     C                   ENDIF
     C     *START        SETLL     UPTBV4L0
      *
     C                   ENDSR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * *INZSR - Program Initialisation routine                       *
      *                                                               *
      * Called by: Implicitly on program activation                   *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *INZSR        BEGSR
      * Initialise program name
      *
     C                   MOVEL     'UP1733'      DBPGM
      *
     C                   MOVEL     HEADER        HEAD
      * Open files
     C                   OPEN      UPTBV4L0
     C                   OPEN      TABCGU1
     C                   ENDSR
 
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR
      *
     C                   IF        @RUN = *BLANK
     C                   EVAL      @RUN = 'Y'
     C                   DUMP
      *
     C                   ENDIF
      *
     C                   EVAL      *INU7 = *ON
     C                   EVAL      *INU8 = *ON
     C                   EVAL      *INLR = *ON
     C                   RETURN
      *
     C                   ENDSR
      *
