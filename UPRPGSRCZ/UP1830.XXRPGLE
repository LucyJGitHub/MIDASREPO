     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2010')
      *****************************************************************
/*X*IA***OVRDBF*FILE(CWFC1)*TOFILE(UPCWFCTPD)**************************                       CUP021
/*X*IB***OVRDBF*FILE(CWFC2)*TOFILE(UPCWFCTPD)**************************                       CUP021
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP1830 - Build Action File for SDCWFCPD                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      *  Prev Amend No. CSD083 *CREATE     Date 27May10               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CSD083 - Watch List Compliance Upgrade                       *
      *                                                               *
      *****************************************************************
      * Function of indicators
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write or chain fail
      *       70 - 73 Indicators for changed fields
      *
     FCWFC1     IF   E             DISK    INFSR(*PSSR)
     FCWFC2     IF   E           K DISK    INFSR(*PSSR)
     F                                     RENAME(SDCWFCD0:CWFCF) PREFIX(X)
     FUPCWFCPD  O    E             DISK    INFSR(*PSSR)
      *
      /EJECT
     D LDA           E DS           256    EXTNAME(LDA)
      *
      * Local data area for database error details
      * *LOCK IN LDA immediately before and OUT LDA immediately
      * after each database error handling.
      *
      *                                     134 141 DBFILE
      *       Defines:                      142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *
     D PSDS           SDS
      *
      * Program Status Data Structure.
      *
     D  PGM              *PROC
     D  WSID                 244    253
     D  USER                 254    263
      *
     C     *ENTRY        PLIST
     C                   PARM                    MODE              1
      *
     C     COMP          KLIST
     C                   KFLD                    WFMODU
     C                   KFLD                    WFGROP
     C                   KFLD                    WFSEQN
      *
     C                   READ      CWFC1                                6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C     *IN20         DOWEQ     '0'
     C     COMP          CHAIN     CWFCF                              3069
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * If record not found then record has been deleted or added
      *  depending on mode.
     C     *IN30         IFEQ      *ON
     C                   MOVE      MODE          WFACTC
     C                   WRITE     UPCWFCD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
      * Need to find differences but this should only be run once;
      *  run only if mode is 'Delete'.
     C     MODE          IFEQ      'D'
      *
      * If record found then look for any differences; only look in
      *  fields that have data all of the time.
     C     *IN30         IFEQ      *OFF
     C                   MOVE      'A'           WFACTC
      * Module ID
     C     WFMODU        IFNE      XWFMODU
     C                   SETON                                        70
     C                   ENDIF
      * Field Name
     C     WFFLDN        IFNE      XWFFLDN
     C                   SETON                                        71
     C                   ENDIF
      * Format Code
     C     WFFRMC        IFNE      XWFFRMC
     C                   SETON                                        72
     C                   ENDIF
      * Description
     C     WFDESC        IFNE      XWFDESC
     C                   SETON                                        73
     C                   ENDIF
      * Sequence Number
     C     WFSEQN        IFNE      XWFSEQN
     C                   SETON                                        74
     C                   ENDIF
      * Group Sequence
     C     WFGROP        IFNE      XWFGROP
     C                   SETON                                        75
     C                   ENDIF
      * Begin XML Tag
     C     WFBXML        IFNE      XWFBXML
     C                   SETON                                        76
     C                   ENDIF
      * End XML Tag
     C     WFEXML        IFNE      XWFEXML
     C                   SETON                                        77
     C                   ENDIF
      * Field Start
     C     WFSTRT        IFNE      XWFSTRT
     C                   SETON                                        78
     C                   ENDIF
      * Field Length
     C     WFFLDB        IFNE      WFFLDB
     C                   SETON                                        79
     C                   ENDIF
      *
     C                   MOVEA     *IN(70)       INTEST           10
     C                   MOVE      INTEST        INTNUM           10 0
      * If any indicators are set on then write record.
     C     INTNUM        IFNE      0
     C                   WRITE     UPCWFCD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      * Set values to write second record.
     C                   MOVEL     XWFMODU       WFMODU
      *
     C                   MOVEL     XWFFLDN       WFFLDN
      *
     C                   MOVEL     XWFFRMC       WFFRMC
      *
     C                   MOVEL     XWFDESC       WFDESC
      *
     C                   MOVEL     XWFGROP       WFGROP
      *
     C                   MOVEL     XWFBXML       WFBXML
      *
     C                   MOVEL     XWFEXML       WFEXML
      *
     C                   MOVEL     XWFSTRT       WFSTRT
      *
     C                   MOVEL     XWFFLDB       WFFLDB
      *
     C                   WRITE     UPCWFCD0                             69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
     C                   ENDIF
      *
      * Read next record
     C                   MOVEA     '0000000000'  *IN(70)
     C                   READ      CWFC1                                6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C                   ENDDO
      *
     C     ENDPGM        TAG
     C                   SETON                                        LR
     C                   RETURN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR                                                  ** *PSSR SR **
      *
     C     @RUN          IFEQ      *BLANK
     C                   MOVE      'Y'           @RUN              1
     C                   DUMP
     C                   END
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
      *
      ********************************************************************
