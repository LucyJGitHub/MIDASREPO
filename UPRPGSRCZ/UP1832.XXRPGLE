     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2010')
      *****************************************************************
/*S*D****RPGBASEBND****************************************************                       CUP021
      *****************************************************************
      *                                                               *
      *  Midas - Bridge                                               *
      *                                                               *
      *  UP1832 - Process Action File for SDCWFCPD                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      *  Last Amend No. CUP021  *REDUNDANT Date 11Feb11               *
      *  Prev Amend No. CSD083 *CREATE     Date 27May10               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUP021 - Rewrite of Action File processing                   *
      *  CSD083 - Watch List Compliance Upgrade                       *
      *                                                               *
      *****************************************************************
      * Function of indicators
      *
      *       20      End of file
      *       30      Chain - record not found
      *       69      Read, write, update or delete fail
      *
     FUPCWFCL0  IF   E           K DISK    INFSR(*PSSR)
     FSDCWFCL0  UF A E           K DISK    INFSR(*PSSR)
     F                                     RENAME(SDCWFCD0:FUNCF)
      /EJECT
      *
     D LDA           E DS           256    EXTNAME(LDA)
      *
      * Local data area for database error details
      * *LOCK IN LDA immediately before and OUT LDA immediately
      * after each database error handling.
      *
      *                                     134 141 DBFILE
      *        Defines:                     142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *
     D PSDS           SDS
      *
      ** Program Status Data Structure.
      *
     D  PGM              *PROC
     D  WSID                 244    253
     D  USER                 254    263
      *
      * Fields with the same name will need to be renamed.
     IFUNCF
     I              WFMODU                      MODU
     I              WFFLDN                      FLDN
     I              WFFRMC                      FRMC
     I              WFDESC                      DESC
     I              WFSEQN                      SEQN
     I              WFGROP                      GROP
     I              WFBXML                      BXML
     I              WFEXML                      EXML
     I              WFSTRT                      STRT
     I              WFFLDB                      FLDB
      *
     C     COMP          KLIST
     C                   KFLD                    WFMODU
     C                   KFLD                    WFGROP
     C                   KFLD                    WFSEQN
      *
     C     *LOVAL        SETLL     UPCWFCL0
      *
     C                   READ      UPCWFCL0                             6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C     *IN20         DOWEQ     '0'
      *
     C     WFACTC        IFEQ      'I'
     C                   EXSR      SETVAL
     C                   WRITE     FUNCF                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
     C                   ENDIF
      *
     C     WFACTC        IFEQ      'D'
     C     COMP          CHAIN     SDCWFCL0                           3069
     C     *IN69         IFEQ      *ON
     C     *IN30         OREQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
      *
     C                   ELSE
     C                   DELETE    FUNCF                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
      *
      * If Action code = AMEND then read next record.
     C     WFACTC        IFEQ      'A'
     C                   MOVEL     WFMODU        MODUX             4
     C                   MOVEL     WFGROP        GROPX             2 0
     C                   MOVEL     WFSEQN        SEQNX             6 0
     C                   READ      UPCWFCL0                             6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
      * If key fields do not match exit
      *  - or if EOF indicator is on.
     C     WFMODU        IFNE      MODUX
     C     WFGROP        ANDNE     GROPX
     C     WFSEQN        ANDNE     SEQNX
     C     *IN20         OREQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C     COMP          CHAIN     SDCWFCL0                           3069
     C     *IN69         IFEQ      *ON
     C     *IN30         OREQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
      *
     C                   ELSE
      *
     C                   EXSR      SETVAL
     C                   UPDATE    FUNCF                                69
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
      *
     C                   READ      UPCWFCL0                             6920
     C     *IN69         IFEQ      *ON
     C                   EXSR      *PSSR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *
     C                   ENDDO
      *
     C     ENDPGM        TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      *****************************************************************
      *                                                               *
      * SETVAL subroutine - set values for deletes/writes/updates     *
      *                                                               *
      *****************************************************************
     C     SETVAL        BEGSR
      * Set values to update record.
     C                   MOVEL     WFMODU        MODU
     C                   MOVEL     WFFLDN        FLDN
     C                   MOVEL     WFFRMC        FRMC
     C                   MOVEL     WFDESC        DESC
     C                   MOVEL     WFSEQN        SEQN
     C                   MOVEL     WFGROP        GROP
     C                   MOVEL     WFBXML        BXML
     C                   MOVEL     WFEXML        EXML
     C                   MOVEL     WFSTRT        STRT
     C                   MOVEL     WFFLDB        FLDB
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: None                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C     *PSSR         BEGSR                                                  ** *PSSR SR **
      *
     C     @RUN          IFEQ      *BLANK
     C                   MOVE      'Y'           @RUN              1
     C                   DUMP
     C                   END
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
      ********************************************************************
