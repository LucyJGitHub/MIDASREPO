/*********************************************************************/
/*XBI    OVRDBF FILE(RMVALLTRGF) TOFILE(QAFDTRG)                     */
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas UT RMVALLTRG CPP')                              */
/*********************************************************************/
/*                                                                   */
/*       Midas - Utilities Module                                    */
/*                                                                   */
/*       UTC000015 - Remove all triggers from a library.             */
/*                                                                   */
/*       (c) Finastra International Limited 2004                     */
/*                                                                   */
/*       Last Amend No. MD051743           Date 22Aug18              */
/*       Prev Amend No. MD051564           Date 16Jul18              */
/*                      MD051000           Date 01Jun18              */
/*                      MD047609           Date 10Nov17              */
/*                      MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*                      CPK020  *CREATE    Date 19Aug04              */
/*                      XXXXXX             Date DDMmmYY              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD051743 - Installation error UPC000148 encountered during  */
/*                  GINZ (Global Initialization)                     */
/*       MD051564 - CHGJOB command should not be called in IC        */
/*       MD051000 - SCRMVALTRG is insensitive to RMVPFTRG errors     */
/*       MD047609 - Component SCRMVALTRG have LCKW status            */
/*       MD046248 - Finastra Rebranding                              */
/*       CPK020 - New utility to remove triggers from a library.     */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&LIB)

             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10)

             DCL        VAR(&LASTFILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ERRORFLAG) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&IDX)       TYPE(*DEC)  LEN(1) VALUE(0)                     /*MD047609*/
             DCL        VAR(&MPHAS) TYPE(*CHAR) LEN(1)                                  /*MD051000*/

             DCLF       FILE(RMVALLTRGF)

             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2004')

/* Global monitor message. */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ERROR))

             CHGJOB     SWS(00000000)

/* Retrieve the phase of day */                                                         /*MD051000*/
/**********  RTVDTAARA  DTAARA(MPHAS (1 1)) RTNVAR(&MPHAS)           */        /*MD051000 MD051743*/
                                                                                        /*MD051000*/
/* Create temporary message queue for error handling. */
             DLTMSGQ    MSGQ(QTEMP/RMVALLTRG)
             MONMSG     MSGID(CPF0000)
             CRTMSGQ    MSGQ(QTEMP/RMVALLTRG) TEXT('Temporary +
                          message queue for RMVALLTRG utility')

/* Display triggers of all files in library to outfile. */
             DSPFD      FILE(&LIB/*ALL) TYPE(*TRG) OUTPUT(*OUTFILE) +
                          FILEATR(*PF) OUTFILE(QTEMP/RMVALLTRGF)
             MONMSG     MSGID(CPF3012) EXEC(DO)
                GOTO       CMDLBL(ENDPGM)
             ENDDO

READNEXT:
             RCVF
             MONMSG     MSGID(CPF0864) EXEC(DO)
                GOTO       CMDLBL(ENDPGM)
             ENDDO

             IF         COND(&TRFILE *NE &LASTFILE) THEN(DO)
             IF         COND(&TRFILE *EQ 'SDMMIDPD') THEN(DO)                           /*MD047609*/
                CHGVAR     VAR(&IDX) VALUE(1)                                           /*MD047609*/
/* Wait for 5 seconds and repeated up to 3 times */                                     /*MD047609*/
 ALCOBJECT:     ALCOBJ     OBJ((&TRFILE *FILE *EXCL *FIRST)) WAIT(5)                    /*MD047609*/
                MONMSG     MSGID(CPF0000) EXEC(DO)                                      /*MD047609*/
                    IF         COND(&IDX *LE 3) THEN(DO)                                /*MD047609*/
                       CHGVAR     VAR(&IDX) VALUE(&IDX + 1)                             /*MD047609*/
                       GOTO       CMDLBL(ALCOBJECT)                                     /*MD047609*/
                    ENDDO                                                               /*MD047609*/
                    ELSE       CMD(DO)                                                  /*MD047609*/
                       GOTO       CMDLBL(ERROR)                                         /*MD047609*/
                    ENDDO                                                               /*MD047609*/
                ENDDO                                                                   /*MD047609*/
                RMVPFTRG   FILE(&LIB/&TRFILE)                                           /*MD047609*/
                MONMSG     MSGID(CPF0000) EXEC(DO)                                      /*MD047609*/
                   SNDMSG     MSG('Unable to remove trigger for file' +
                                *BCAT &TRFILE) TOMSGQ(QTEMP/RMVALLTRG)                  /*MD047609*/
                   CHGVAR     VAR(&ERRORFLAG) VALUE('Y')                                /*MD047609*/
                ENDDO                                                                   /*MD047609*/
                DLCOBJ     OBJ((&TRFILE *FILE *EXCL *FIRST))                            /*MD047609*/
                MONMSG     MSGID(CPF0000)                                               /*MD047609*/
                ENDDO                                                                   /*MD047609*/
             ELSE       CMD(DO)                                                         /*MD047609*/

                RMVPFTRG   FILE(&LIB/&TRFILE)
                MONMSG     MSGID(CPF0000) EXEC(DO)
                   SNDMSG     MSG('Unable to remove trigger for file' +
                                *BCAT &TRFILE) TOMSGQ(QTEMP/RMVALLTRG)
                   CHGVAR     VAR(&ERRORFLAG) VALUE('Y')
                ENDDO
             ENDDO                                                                      /*MD047609*/
             ENDDO

             CHGVAR     VAR(&LASTFILE) VALUE(&TRFILE)

             GOTO       CMDLBL(READNEXT)

             GOTO       CMDLBL(ENDPGM)

ERROR:
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          UTC000015 ended abnormally.  See joblog +
                          for more details') TOPGMQ(*PRV) +
                          MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000)

ENDPGM:
             IF         COND(&ERRORFLAG *EQ 'Y') THEN(DO)
                DSPMSG     MSGQ(QTEMP/RMVALLTRG) OUTPUT(*PRINT)
                                                                                        /*MD051743*/
                 CALL       PGM(SCGETMPHAS) PARM(&MPHAS)                                /*MD051743*/
                                                                                        /*MD051000*/
/**********      IF  COND((&MPHAS *NE 'A') *OR (&MPHAS *NE 'G')) THEN(DO)      /*MD051000 MD051564*/
                 IF  COND((&MPHAS *NE 'A') *AND (&MPHAS *NE 'G')) THEN(DO)              /*MD051564*/
                     CHGJOB SWS(XXXXXX11)                                               /*MD051000*/
                 ENDDO                                                                  /*MD051000*/
                                                                                        /*MD051000*/
             ENDDO

             ENDPGM
