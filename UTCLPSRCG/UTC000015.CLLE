/*********************************************************************/
/*XBI    OVRDBF FILE(RMVALLTRGF) TOFILE(QAFDTRG)                     */
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas UT RMVALLTRG CPP')                              */
/*********************************************************************/
/*                                                                   */
/*       Midas - Utilities Module                                    */
/*                                                                   */
/*       UTC000015 - Remove all triggers from a library.             */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2004           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Last Amend No. CPK020  *CREATE    Date 19Aug04              */
/*       Prev Amend No. xxxxxx             Date ddMmmyy              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CPK020 - New utility to remove triggers from a library.     */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&LIB)
 
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10)
 
             DCL        VAR(&LASTFILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ERRORFLAG) TYPE(*CHAR) LEN(1) VALUE('N')
 
             DCLF       FILE(RMVALLTRGF)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2004')
 
/* Global monitor message. */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ERROR))
 
             CHGJOB     SWS(00000000)
 
/* Create temporary message queue for error handling. */
             DLTMSGQ    MSGQ(QTEMP/RMVALLTRG)
             MONMSG     MSGID(CPF0000)
             CRTMSGQ    MSGQ(QTEMP/RMVALLTRG) TEXT('Temporary +
                          message queue for RMVALLTRG utility')
 
/* Display triggers of all files in library to outfile. */
             DSPFD      FILE(&LIB/*ALL) TYPE(*TRG) OUTPUT(*OUTFILE) +
                          FILEATR(*PF) OUTFILE(QTEMP/RMVALLTRGF)
             MONMSG     MSGID(CPF3012) EXEC(DO)
                GOTO       CMDLBL(ENDPGM)
             ENDDO
 
READNEXT:
             RCVF
             MONMSG     MSGID(CPF0864) EXEC(DO)
                GOTO       CMDLBL(ENDPGM)
             ENDDO
 
             IF         COND(&TRFILE *NE &LASTFILE) THEN(DO)
                RMVPFTRG   FILE(&LIB/&TRFILE)
                MONMSG     MSGID(CPF0000) EXEC(DO)
                   SNDMSG     MSG('Unable to remove trigger for file' +
                                *BCAT &TRFILE) TOMSGQ(QTEMP/RMVALLTRG)
                   CHGVAR     VAR(&ERRORFLAG) VALUE('Y')
                ENDDO
             ENDDO
 
             CHGVAR     VAR(&LASTFILE) VALUE(&TRFILE)
 
             GOTO       CMDLBL(READNEXT)
 
             GOTO       CMDLBL(ENDPGM)
 
ERROR:
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          UTC000015 ended abnormally.  See joblog +
                          for more details') TOPGMQ(*PRV) +
                          MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000)
 
ENDPGM:
             IF         COND(&ERRORFLAG *EQ 'Y') THEN(DO)
                DSPMSG     MSGQ(QTEMP/RMVALLTRG) OUTPUT(*PRINT)
             ENDDO
 
             ENDPGM
