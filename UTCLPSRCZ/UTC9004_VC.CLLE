/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI *  TEXT('Midas UT Validity checker for GENFLDNOS')             */
/*********************************************************************/
/*                                                                   */
/*       Midas - Utilitiies module                                   */
/*                                                                   */
/*       UTC9004_VC - Validity checker for GENFLDNOS                 */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2003           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/* Midas Release 4.01.03 --------------------------------------------*/
/*       Last Amend No. CUP021 *CREATE     Date 12Jan03              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CUP021 - Re-write of GENFLDNOS, to make it file driven.     */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&APINAME &SUBM &JOBQLIB)
 
             DCL        VAR(&APINAME) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SUBM) TYPE(*CHAR) LEN(1)
             DCL        VAR(&JOBQLIB) TYPE(*CHAR) LEN(20)
 
             DCL        VAR(&JOBQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ERROR) TYPE(*CHAR) LEN(1)
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)
 
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Misys International Banking Systems Ltd. +
                          2003')
 
             DCLF       FILE(UTGENFL0)
 
/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* Create data area MIDASMSG in QTEMP */
             DLTDTAARA  DTAARA(QTEMP/MIDASMSG)
             MONMSG     MSGID(CPF0000)
             CRTDTAARA  DTAARA(QTEMP/MIDASMSG) TYPE(*CHAR) LEN(800) +
                          VALUE(' ')
 
/* If submit requested extract job batch queue name and check     */
/* existence and authority.                                       */
             IF         COND(&SUBM *EQ 'Y') THEN(DO)
                CHGVAR     VAR(&JOBQ) VALUE(%SST(&JOBQLIB 1 10))
                CHGVAR     VAR(&LIB) VALUE(%SST(&JOBQLIB 11 10))
                CHKOBJ     OBJ(&LIB/&JOBQ) OBJTYPE(*JOBQ) AUT(*USE)
                MONMSG     MSGID(CPF0000) EXEC(DO)
 
/** If error found, send message                                  */
                   SNDPGMMSG  MSGID(CPD0006) MSGF(QCPFMSG) MSGDTA('0000Job +
                          queue does not exist or you have +
                          insufficient authority') MSGTYPE(*DIAG)
                   CHGVAR     VAR(&ERROR) VALUE('Y')
                ENDDO
             ENDDO
 
/* API must exist in file                                         */
/* If single API requested, use OPNQRYF to override to that API   */
             IF         COND(&APINAME *NE '*ALL') THEN(DO)
                OPNQRYF    FILE((UTGENFL0)) QRYSLT('HFAPIN *EQ "' *TCAT +
                          &APINAME *TCAT '"')
                MONMSG     MSGID(CPF4174) EXEC(DO)
                   CLOF       OPNID(UTGENFL0)
                   OPNQRYF    FILE((UTGENFL0)) QRYSLT('HFAPIN *EQ "' *TCAT +
                             &APINAME *TCAT '"')
                ENDDO
             ENDDO
             RCVF
             MONMSG     MSGID(CPF0864) EXEC(DO)
 
/* Error - no records or API not found - send message             */
                CHGVAR     VAR(&ERROR) VALUE('Y')
                IF         COND(&APINAME *EQ '*ALL') THEN(DO)
                   SNDPGMMSG  MSGID(CPD0006) MSGF(QCPFMSG) MSGDTA('0000No +
                             records found in UTGENFPD') MSGTYPE(*DIAG)
                   GOTO       CMDLBL(END)
                ENDDO
                SNDPGMMSG  MSGID(CPD0006) MSGF(QCPFMSG) MSGDTA('0000API +
                          ' *CAT &APINAME *CAT ' not found in +
                          UTGENFPD') MSGTYPE(*DIAG)
             ENDDO
 
             GOTO       CMDLBL(END)
 ABNOR:
/* Set up messages for Midas Information Screen  */
             RTVMSG     MSGID(UPM0001) MSGF(UTMSGF) MSGDTA(UTC9008) +
                          MSG(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
             MONMSG     MSGID(CPF0000)
             CHGDTAARA  DTAARA(MIDASMSG (201 50)) VALUE('Check +
                          joblog for details')
             MONMSG     MSGID(CPF0000)
             CALL       PGM(SCC0010) PARM('GENFLDNOS' 'JOBLOG' 'Y')
             MONMSG     MSGID(CPF0000 MCH0000)
 
END:
             CHGVAR     VAR(&CPYFLD) VALUE('(c) +
                          Misys International Banking Systems Ltd.')
             IF         COND(&ERROR *EQ 'Y') THEN(SNDPGMMSG +
                          MSGID(CPF0002) MSGF(QCPFMSG) +
                          MSGTYPE(*ESCAPE))
 
             CLOF       OPNID(UTGENFL0)
             MONMSG     MSGID(CPF0000 MCH0000)
 
             ENDPGM
