/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas UT Initialisation program for CER050')          */
/*********************************************************************/
/*                                                                   */
/*       Midas - CER050 Initialisation program                       */
/*                                                                   */
/*       UTCER050 - Midas UT Initialisation for CER050               */
/*                                                                   */
/*       (c) Finastra International Limited 2016                     */
/*                                                                   */
/*       Last Amend No. MD054289           Date 08Sep19              */
/*       Prev Amend No. MD054279           Date 05Sep19              */
/*                      CER050  *Create    Date 16Jun19              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD054289 - DLAPRHTD did not populate after initialization   */
/*                  of CER050 was run                                */
/*       MD054279 - Triggers were not attached even after GPST/ZPST  */
/*                  has been run                                     */
/*       CER050 - Annualised Percentage Rate                         */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&RTNCDE &IPMODE)

             DCL        VAR(&RTNCDE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&IPMODE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
             DCL        VAR(&PRTCD) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MSG) TYPE(*CHAR) LEN(90)
             DCL        VAR(&FMT) TYPE(*CHAR) LEN(200)
             DCL        VAR(&PARM) TYPE(*CHAR) LEN(6) VALUE('*ALL')
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Finastra International Limited 2016')

/* Global monitor message                                            */

             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))

             CHGJOB     SWS(XXXXXX00)

/** Mode 1 - Calling program for the first time                      */

             IF         COND(&IPMODE = '1') THEN(DO)
                                                                                        /*MD054279*/
/** Add triggers to the files after switching on CER050              */                 /*MD054279*/
                                                                                        /*MD054279*/
             CALL       PGM(SCMNTTRIG) PARM('*ADDTRIG' 'CLOANCL' +
                          '*LIVETRIG')                                                  /*MD054279*/
             CALL       PGM(SCMNTTRIG) PARM('*ADDTRIG' 'CLOANCK' +
                          '*LIVETRIG')                                                  /*MD054279*/
             CALL       PGM(SCMNTTRIG) PARM('*ADDTRIG' 'LEFEED' +
                          '*LIVETRIG')                                                  /*MD054279*/
             CALL       PGM(SCMNTTRIG) PARM('*ADDTRIG' 'LOAMSDK' +
                          '*LIVETRIG')                                                  /*MD054279*/
             CALL       PGM(SCMNTTRIG) PARM('*ADDTRIG' 'DEALSDC' +
                          '*LIVETRIG')                                                  /*MD054279*/
             CALL       PGM(SCMNTTRIG) PARM('*ADDTRIG' 'DEAMSDI' +
                          '*LIVETRIG')                                                  /*MD054279*/

             CALL       PGM(SD6002)
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                CHGVAR     VAR(&MSG) VALUE('Unable to initialise +
                             SDACUSPD flag properly.')
                GOTO       CMDLBL(ERROR)
             ENDDO

             CALL       PGM(LE6001)
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                CHGVAR     VAR(&MSG) VALUE('Unable to initialise +
                             CLOANCL APR flag properly.')
                GOTO       CMDLBL(ERROR)
             ENDDO

             CALL       PGM(GL6002)
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                CHGVAR     VAR(&MSG) VALUE('Unable to initialise +
                             ACCNTBXC APR flag properly.')
                GOTO       CMDLBL(ERROR)
             ENDDO

             CALL       PGM(DL6003)
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                CHGVAR     VAR(&MSG) VALUE('Unable to initialise +
                             DEALSDC APR flag properly.')
                GOTO       CMDLBL(ERROR)
             ENDDO

             CALL       PGM(LE6007) PARM(&PARM)
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                CHGVAR     VAR(&MSG) VALUE('Unable to initialise +
                             LE APR Calculation file properly.')
                GOTO       CMDLBL(ERROR)
             ENDDO

             CALL       PGM(DL6008)
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                CHGVAR     VAR(&MSG) VALUE('Unable to initialise +
                             DL DLHISTTD properly.')
                GOTO       CMDLBL(ERROR)
             ENDDO
                                                                                        /*MD054289*/
             CHGVAR     VAR(&PARM) VALUE('*ALL')                                        /*MD054289*/

             CALL       PGM(DL6010) PARM(&PARM)
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                CHGVAR     VAR(&MSG) VALUE('Unable to initialise +
                             DL APR Calculation file properly.')
                GOTO       CMDLBL(ERROR)
             ENDDO

             GOTO       CMDLBL(END)
             ENDDO

/** Mode 2 - Re-calling program if already previously used           */

             IF         COND(&IPMODE = '2') THEN(DO)
               CHGJOB     SWS(XXXXXX00)
               GOTO       CMDLBL(END)
             ENDDO

/** Mode 3 - Switching off the SAR                                   */

             IF         COND(&IPMODE = '3') THEN(DO)
               CHGJOB     SWS(XXXXXX00)
               GOTO       CMDLBL(END)
             ENDDO
ERROR:
             CHGVAR     VAR(&RTNCDE) VALUE('*ERROR*')
             SNDPGMMSG   MSG(&MSG) TOPGMQ(*EXT) TOMSGQ(MOPERQ) +
                           MSGTYPE(*INFO)
             MONMSG      MSGID(CPF0000 MCH0000)
             GOTO        CMDLBL(END)

 ABNOR:

             CHGVAR     VAR(&RTNCDE) VALUE('*ERROR*')
             CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          UTCER050 ended abnormally - see job log') +
                          TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)

 END:
             CHGVAR     VAR(&CPYFLD) VALUE('(c) +
                          Finastra International Limited 2016')

             ENDPGM
