/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas UT End User Jobs in ACTIVE status')             */
/*********************************************************************/
/*                                                                   */
/*       Midas - Utilities Module                                    */
/*                                                                   */
/*       UTENDUSJOB - Midas UT End User Jobs in ACTIVE status        */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2017           */
/*                                                                   */
/*       Last Amend No. MD044282  *CREATE  Date 15May17              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD044282 - End User Jobs in ACTIVE status                   */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&USRNAME)

             DCL        VAR(&USRNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBNO) TYPE(*CHAR) LEN(6)
             DCL        VAR(&CTR) TYPE(*DEC) LEN(2 0) VALUE(0)

             DCLF       FILE(QTEMP/QPDSPSBJ)

             COPYRIGHT  TEXT('(c) Misys International Banking +
                          Systems Ltd. 2016')

/* Global monitor message */

             MONMSG     MSGID(CPF0000 CPD0000 MCH0000) EXEC(GOTO +
                          CMDLBL(END))

             CRTPF      FILE(QTEMP/QPDSPSBJ) RCDLEN(132)
             MONMSG     MSGID(CPF5813 CPF7302)

             DLYJOB     DLY(1)

/* List active jobs of the user */

             WRKUSRJOB  USER(&USRNAME) STATUS(*ACTIVE) +
                          OUTPUT(*PRINT)
             CPYSPLF    FILE(QPDSPSBJ) TOFILE(QTEMP/QPDSPSBJ) +
                          SPLNBR(*LAST) MBROPT(*REPLACE)

 RCVF:
             IF         COND(&CTR *LT 5) THEN(DO)
                RCVF
                CHGVAR     VAR(&CTR) VALUE(&CTR + 1)
                GOTO       CMDLBL(RCVF)
             ENDDO

/* Job details starts at Line 6 */

 READ:
             RCVF
             CHGVAR     VAR(&JOBNAME) VALUE(%SST(&QPDSPSBJ 4 10))
             CHGVAR     VAR(&JOBNO) VALUE(%SST(&QPDSPSBJ 30 6))

/* End program if no active jobs */

             IF         COND((&JOBNAME *EQ '  No jobs ') *OR +
                          (&JOBNAME *EQ ' ')) THEN(DO)
                GOTO CMDLBL(END)
             ENDDO

/* End active jobs */

             ENDJOB     JOB(&JOBNO/&USRNAME/&JOBNAME) OPTION(*IMMED) +
                          SPLFILE(*NO) LOGLMT(0)

             GOTO       CMDLBL(READ)
 END:
             ENDPGM
