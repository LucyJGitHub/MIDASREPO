      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UT Update teller cash values')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  DSPF/UT0050DF - Update Teller Cash Values                    *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CRT005  *CREATE    Date 09Aug01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRT005 - Cashier Enhancement for Cash In/Cash Out.           *
      *                                                               *
      *****************************************************************
     A                                      MSGLOC(24)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CF01
     A                                      CA03
      *
      ** Prompt for teller id
      *
     A          R UT0050F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A                                  1  2USER
     A                                  1 13'UT0050'
     A                                  1 26'TELLER/BATCH TOTALS UPDATE'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 65'Date'
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 65'Time'
     A                                  2 70TIME
     A                                  6  3'Enter Teller Id'
     A            SCTLLR         3A  B  6 22
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(RI)
      *
      ** Subfile of old and new teller cash values
      *
     A          R UT0050F1                  SFL
     A            RRN            5  0H      TEXT('REL REC NO')
     A            SFLCDP         2  0H      TEXT('CCY DEC PLACES')
     A            SFLCCY         3A  O  8  6
     A            SFLVCA        15Y 3O  8 12EDTCDE(J)
     A            SFLNEW        15Y 3B  8 35EDTCDE(J)
     A            SFLMSG        15A  O  8 58
      *
      ** Subfile control record
      *
     A          R UT0050F2                  SFLCTL(UT0050F1)
     A                                      SFLSIZ(0024)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A  51                                  SFLDSP
     A  52                                  SFLDSPCTL
     A  53                                  SFLCLR
     A N54                                  SFLEND(*MORE)
     A                                  1  2USER
     A                                  1 13'UT0050'
     A                                  1 26'TELLER/BATCH TOTALS UPDATE'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 65'Date'
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 65'Time'
     A                                  2 70TIME
     A                                  4  1'Teller'
     A            SCTLLR         3A  O  4  9
     A                                  6  6'Ccy'
     A                                  6 12'Current Cash Value'
     A                                  6 35'New Cash Value'
     A                                      DSPATR(HI)
      *
      ** Command key screen
      *
     A          R UT0050F3
     A
     A                                 23  4'F1=Confirm'
     A                                 23 18'F3=Cancel'
      *
