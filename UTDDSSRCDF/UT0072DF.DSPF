      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UT Edit Data Area')
      *****************************************************************
      *                                                               *
      *  Midas - Utilities Module                                     *
      *                                                               *
      *  DSPF/UT0072DF - Display data area                            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CREATE             Date 05MAR96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CREATE - Rewrite AMDDTAARA                                   *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(QSYSPRT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R CSHARMF0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*=========================================================================
     A          R #SFLRCD                   TEXT('Subfile record.')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(33 'Invalid: #2SEL')
     A                                      INDTXT(34 'Invalid: #2DCHR')
     A                                      INDTXT(35 'Invalid: #2HEX')
     A            #2OLRC         1   H      TEXT('Existing record indicator')
     A            #2CHAR        50   H      TEXT('Existing record fields')
     A            #2RSTL         5  0H      TEXT('Start position of record')
     A            #2REND         5  0H      TEXT('End position of record')
     A            #2DSTL         4  0   9  7TEXT('Start position of record')
     A                                      EDTCDE(J)
     A            #2DEND         4  0   9 14TEXT('Start position of record')
     A                                      EDTCDE(J)
     A            #2SEL          1   B  9 22
     A                                      VALUES('H' 'C')
     A                                      CHECK(AB)
     A     78                               DSPATR(ND PR)
     A  33N78                               DSPATR(RI PC)
     A N33N78                               DSPATR(UL HI)
     A            #RDCHR        50   B  9 26TEXT('Displayable Characters')
     A                                      CHECK(LC)
     A  79
     AO 78                                  DSPATR(PR)
     A  34N79N78                            DSPATR(RI PC)
     A N34N79N78                            DSPATR(UL HI)
     A            #RHEX1        50   B 10 26TEXT('First hex character')
     A N79
     AO 78                                  DSPATR(PR)
     A  35 79N78                            DSPATR(RI PC)
     A N35 79N78                            DSPATR(UL HI)
     A            #RHEX2        50   B 11 26TEXT('Second hex character')
     A N79
     AO 78                                  DSPATR(PR)
     A  35 79N78                            DSPATR(RI PC)
     A N35 79N78                            DSPATR(UL HI)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(04) SFLSIZ(05)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP SFLEND(*MORE)
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CA05(30 'Reset.')
     A  77                                  CF10(10 'Update')
     A N88                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1CCYN')
     A                                      SETOF(32 'Invalid: #1MCCY')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  1 26'          M i d a s          '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  2 30'Edit Data Area Function'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4 10'Name.:'
     A            @1NAME        10      4 17TEXT('File name')
     A N25                                  OVRDTA
     A                                  4 30'Library.:'
     A            @1LIBN        10      4 41TEXT('File library')
     A N25                                  OVRDTA
     A                                  4 54'Type...:'
     A            @1TYPE        10      4 63TEXT('File member')
     A N25                                  OVRDTA
     A                                  5 10'Data Area Length ...:'
     A            @1DATL         8  0   5 32TEXT('Data Length')
     A                                      EDTCDE(J)
     A N25                                  OVRDTA
     A  76        @1DECP         1      5 44TEXT('Decimal Places')
     A N25 76                               OVRDTA
     A  76                              5 46'Numeric'
     A                                  5 54'Action.:'
     A            @1ACT         10      5 63TEXT('Action')
     A                                      DSPATR(HI) COLOR(RED)
     A N25                                  OVRDTA
     A                                  6  6'  Position                   1-
     A                                               2         3         4-
     A                                               5       '
     A                                  7  6'From     To    Sel  1234567890-
     A                                      123456789012345678901234567890-
     A                                      1234567890       '
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 22  2'C=Characters    H=hexadecimal'
     A                                 23  2'F3=Exit         F5=Refresh'
     A  77                             23 34'F10=Update'
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
