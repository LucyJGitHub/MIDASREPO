      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UT View File Records')
      *****************************************************************
      *                                                               *
      *  Midas - Utilities Module                                     *
      *                                                               *
      *  DSPF/UT0076DF - Display physical file data                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CREATE             Date 05MAR96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CREATE - Rewrite UPDPF                                       *
      *                                                               *
      *****************************************************************
      *================================================================
     A                                      INDARA
     A                                      PRINT(QSYSPRT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R CSHAREF0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*=========================================================================
     A          R #SFLRCD                   TEXT('Subfile record.')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(32 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1RREL         8  0  10  7TEXT('Relative Record Number')
     A                                      EDTCDE(J)
     A            #1SEL          1   B 10 22
     A  87                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A N87N32                               DSPATR(UL)
     A N32                                  DSPATR(HI)
     A            #RDCHR        50   B 10 26TEXT('Displayable Characters')
     A                                      CHECK(LC)
     A                                      DSPATR(PR)
     A                                      DSPATR(UL)
     A            #RHEX1        50   B   +30TEXT('First hex character')
     A                                      DSPATR(PR)
     A            #RHEX2        50   B   +30TEXT('Second hex character')
     A                                      DSPATR(PR)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(04) SFLSIZ(05)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CA05(30 'Reset.')
     A                                      CF06(06 'Add')
     A                                      SFLDROP(CF11)
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2CCY')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  1 26'          M i d a s          '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  2 32'View File Records'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4 10'File.:'
     A            @1FFL         10      4 17TEXT('File name')
     A N25                                  OVRDTA
     A                                  4 30'Library.:'
     A            @1FLB         10      4 41TEXT('File library')
     A N25                                  OVRDTA
     A                                  4 54'Member.:'
     A            @1FMB         10      4 63TEXT('File member')
     A N25                                  OVRDTA
     A                                  5 10'Format Record Length:'
     A            @1RECL         8  0   5 32TEXT('Record Length')
     A                                      EDTCDE(J)
     A N25                                  OVRDTA
     A                                  5 54'Format.:'
     A            @1RECF        10      5 63TEXT('Record Format name')
     A N25                                  OVRDTA
     A                                  6 10'Relative record no..:'
     A            @1RREL         8  0B  6 32TEXT('Relative record no.')
     A                                      DSPATR(HI UL)
     A                                      COLOR(RED)
     A                                      EDTCDE(J)
     A N25                                  OVRDTA
     A                                  6 54'Window.:'
     A            @1ACT          4  0B  6 63TEXT('Window')
     A                                      DSPATR(HI UL)
     A                                      COLOR(RED)
     A N25                                  OVRDTA
     A                                  7  6'  Rel. Rec.'
     A            @OFFS1        53   B  7 23TEXT('Offset 1')
     A                                      DSPATR(PR)
     A N25                                  OVRDTA
     A                                  8  6'   Number      Sel'
     A            @OFFS2        50   B  8 26TEXT('Offset 2')
     A                                      DSPATR(PR)
     A N25                                  OVRDTA
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            @CMD1         74     22  2
     A                                 23  2'F3=Exit   F5=Refresh   F11=Hexadec-
     A                                      imal'
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
