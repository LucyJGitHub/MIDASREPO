      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UT Display of CMPF results')
      *****************************************************************
      *                                                               *
      *  Midas - Utilities Menu                                       *
      *                                                               *
      *  DSPF/UT000020DF - Display results of CMPF                    *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. BUG27790 *CREATE   Date 23Jun10               *
      *                 XXXXXX             Date DDMmmYY               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG27790 - New utility                                       *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(*DS4)
     A                                      PRINT(MPRINT)
     A                                      CF03
      *
     A          R SUBFLREC                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2010')
     A                                      SFL
     A            SSACTN         1A  I  8  2DSPATR(HI)
     A       N40                            DSPATR(UL)
     A        40                            DSPATR(PR)
     A            SSTYPE        10A  O  8  4DSPATR(HI)
     A            CFSEQN        10S 0B  8 15DSPATR(PR)
     A            CFFIL1        10A  O  8 27DSPATR(HI)
     A            SSRRN1         7A  O  8 38DSPATR(HI)
     A            CFFIL2        10A  O  8 50DSPATR(HI)
     A            SSRRN2         7A  O  8 61DSPATR(HI)
     A            SSDATA        60A  O  8 70
      *
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0015)
     A                                      BLINK
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A                                      ROLLDOWN(05)
     A                                      ROLLUP(06)
     A            SSUSER        10      1  2TEXT('*USER')
     A                                  1 13'UT000020DF'
     A                                  1 43'CMPF - Comparison Details'
     A                                      DSPATR(HI)
     A                                  1100DATE
     A                                      EDTWRD('0  /  /  ')
     A            SSJOB         10      2  2TEXT('*JOB')
     A                                  2100TIME
     A                                      EDTWRD('0  :  :  ')
     A                                  4  2'File:'
     A            SSTFIL        10A  O  4  8DSPATR(HI)
     A                                  4 20'Key:'
     A            SSKEYF        10A  B  4 25DSPATR(HI)
     A                                  4 36 '/'
     A            SSKF01        10A  B  4 38DSPATR(HI)
     A            SSKF02        10A  B  4 49DSPATR(HI)
     A            SSKF03        10A  B  4 60DSPATR(HI)
     A            SSKF04        10A  B  4 71DSPATR(HI)
     A            SSKF05        10A  B  4 82DSPATR(HI)
     A            SSKF06        10A  B  4 93DSPATR(HI)
     A            SSKF07        10A  B  5 38DSPATR(HI)
     A            SSKF08        10A  B  5 49DSPATR(HI)
     A            SSKF09        10A  B  5 60DSPATR(HI)
     A            SSKF10        10A  B  5 71DSPATR(HI)
     A            SSKF11        10A  B  5 82DSPATR(HI)
     A            SSKF12        10A  B  5 93DSPATR(HI)
     A                                  6  2'Next Seq. No.'
     A            SSSEQA        10A  B  6 16DSPATR(HI UL)
     A                                  6 27'T=Top, B=Bottom'
     A                                  6 45'Search'
     A            SSSRCH        50A  B  6 52DSPATR(HI UL)
     A                                  6108'Match'
     A            SSMTCH         1A  B  6114DSPATR(HI UL)
     A                                  6116'M,D,1,2'
     A                                  7  4'Type   '
     A                                  7 15'Seq. No.'
     A                                  7 27'File 1 '
     A                                  7 38'RRN 1  '
     A                                  7 50'File 2 '
     A                                  7 61'RRN 2  '
     A                                  7 70'Record             '
     A*
     A          R SUBMESS
     A                                      PUTOVR
     A                                 23  2'F3=Exit,    Rollup,    Rolldown'
     A            SSERR        076A  O 24  2OVRDTA
     A                                      DSPATR(HI)
