      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UT Display fields used for CMPF')
      *****************************************************************
      *                                                               *
      *  Midas - Utilities Menu                                       *
      *                                                               *
      *  DSPF/UT000021DF - Display fields used for CMPF               *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. CUT017             Date 17Mar20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 BUG27790 *CREATE   Date 28Jun10               *
      *                 XXXXXX             Date DDMmmYY               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CUT017 - Adaptive Database Upgrade                           *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG27790 - New utility                                       *
      *                                                               *
      *****************************************************************
      * Start CUT017
     A                                      DSPSIZ(27 132)
      * End CUT017
     A                                      PRINT(MPRINT)
     A                                      CF01
     A                                      CF03
     A                                      CF04
     A                                      CF07
     A                                      CF08
     A                                      CF10
     A                                      CF12
      *
     A          R SUBFLREC                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2010')
     A                                      SFL
     A            FDRPGN         6A  O  5  2
      * Start CUT017
     A**********  FDDESC        30A  O  5  9                                                  CUT017
     A            FDDESC        50A  O  5  9
      * End CUT017
     A  20                                  DSPATR(RI)
     A            SSDATA        41A  O  5 61DSPATR(HI)
     A  20                                  DSPATR(RI)
     A            FDSEQN         3S 0H
     A            FDFLDN         3S 0H
      *
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0017)
     A                                      BLINK
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A                                      ROLLDOWN(05)
     A                                      ROLLUP
     A            SSUSER        10      1  2TEXT('*USER')
     A                                  1 13'UT000021DF'
     A                                  1 40'CMPF - Field Details'
     A                                      DSPATR(HI)
     A                                  1 65DATE
     A                                      EDTWRD('0  /  /  ')
     A            SSJOB         10      2  2TEXT('*JOB')
     A                                  2 65TIME
     A                                      EDTWRD('0  :  :  ')
     A                                  4  6'Field details for file'
     A            FDFILE        10A  O  4 29DSPATR(HI)
     A            SSNRR1        10A  O  4 61DSPATR(HI)
     A            SSNRR2        10A  O  4 82DSPATR(HI)
     A          R SUBMESS
     A                                      PUTOVR
     A                                 23  2'F1=Fields,  F3=Exit,  +
     A                                      F7/8=Window left/right,  +
     A                                      F10=Before/After,  F12=Cancel'
     A            SSERR        076A  O 24  2
     A                                      DSPATR(HI)
