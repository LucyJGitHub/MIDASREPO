      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UT Display fields for CMPF / CMPFMNT')
      *****************************************************************
      *                                                               *
      *  Midas - Utilities Menu                                       *
      *                                                               *
      *  DSPF/UT000022DF - Display fields used for CMPF / CMPFMNT     *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. BUG27790 *CREATE   Date 02Jul10               *
      *                 XXXXXX             Date DDMmmYY               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG27790 - New utility                                       *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CF03
     A                                      CF10
     A                                      CF12
     A                                      CF16
      *
     A          R SUBFLREC                  SFL
     A            SSSEQN         3A  B  5  2DSPATR(HI)
     A            FDRPGN         6A  O  5  6
     A            SSDESC        30A  B  5 13DSPATR(HI)
     A                                      CHECK(LC)
     A            SSCOL1        17A  B  5 44DSPATR(HI)
     A                                      CHECK(LC)
     A            SSCOL2        17A  B  5 62DSPATR(HI)
     A                                      CHECK(LC)
     A            FDSEQN         3S 0H
     A            FDFLDN         3S 0H
      *
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0017)
     A                                      BLINK
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A                                      ROLLDOWN(05)
     A                                      ROLLUP
     A            SSUSER        10      1  2TEXT('*USER')
     A                                  1 13'UT000021DF'
     A                                  1 35'CMPF - Field Details'
     A                                      DSPATR(HI)
     A                                  1 65DATE
     A                                      EDTWRD('0  /  /  ')
     A            SSJOB         10      2  2TEXT('*JOB')
     A                                  2 65TIME
     A                                      EDTWRD('0  :  :  ')
     A                                  4 20'Field details for file'
     A            FDFILE        10A  O  4 43
      *
     A          R SUBMESS
     A                                      PUTOVR
     A                                 23  2'F3=Exit,  F10=DELETE all,  +
     A                                      F12=Cancel,  +
     A                                      F16=Omit all, else change'
     A            SSERR        076A  O 24  2
     A                                      DSPATR(HI)
