      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UT Validation List Objects Maintenance')         *
      *****************************************************************
      *                                                               *
      *  Midas - Utilities Module                                     *
      *                                                               *
      *  DSPF/UT000030DF - Midas UT Validation List Objects           *
      *                    Maintenance Display                        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      *                                                               *
      *  Last Amend No. BUG28082           Date 31Aug10               *
      *  Prev Amend No. BUG27989*CREATE    Date 08Aug10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG28082 - Unable to display the correct information in the  *
      *             screen. (Recompile)                               *
      *  BUG27989 - HATS should run with/without LDAP                 *
      *                                                               *
      *****************************************************************
      *
      ** Subfile record
      ** ==============
      *
     A          R UT000030S0                SFL
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2010')
     A  34                                  SFLNXTCHG
      *
     A            ##OPTN         2A  B  6  2TEXT('Action Code')
     A  48                                  DSPATR(RI)
     A  48                                  DSPATR(PC)
     A            ##OBNM        10A  O  6  6
     A            ##OBTX        50A  O  6 30
     A            ##LBNM        10A  O  6 18
      *
      ** Subfile record control
      ** ======================
      *
     A          R UT000030C0                SFLCTL(UT000030S0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('Subfile ControlRecord Format')
     A N33                                  ROLLUP(27)
     A N33                                  ROLLDOWN
     A                                      CF03(03)
     A                                      CF05(05)
     A                                      OVERLAY
     A  30                                  SFLDSP
     A                                      SFLDSPCTL
     A  31                                  SFLCLR
     A  33                                  SFLEND
      *
     A            D0SFRN         4S 0H      SFLRCDNBR
      *
     A            D0USER        10A  O  1  2TEXT('Current User')
     A                                  1 13'UT000030'
     A            D0MRDT         8Y 0O  1 71EDTWRD('  /  /    ')
      *
     A            D0WID         10A  O  2  2TEXT('WorkStation ID')
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                  5  4'Validation List'
     A                                  5 44'Description'
     A                                  1 29'Validation List Manager'
     A                                      DSPATR(HI)
     A                                  2 34'Work with List'
     A                                      DSPATR(HI)
     A                                  4 61'Library:'
     A            #1LIBL        10A  B  4 70
     A  47                                  DSPATR(RI)
     A  47                                  DSPATR(PC)
     A            #1HLIBL       10A  H
      *
      ** Screen Footer
      ** =============
      *
     A          R UT000030F0
     A                                      TEXT('Footer RecordFormat')
     A                                      OVERLAY
     A                                 22 41'F3=Exit'
     A                                      COLOR(BLU)
     A                                 22 50'F5=Refresh'
     A                                      COLOR(BLU)
     A                                 22  2'12=Work with Validation List Entri-
     A                                      es'
     A                                      COLOR(BLU)
      *
      ** Message subfile record
      ** ======================
      *
     A          R UT000030S1                SFL
     A                                      TEXT('Message Subfile Record Format-
     A                                      ')
     A                                      SFLMSGRCD(24)
     A            D1SFKY                    SFLMSGKEY
     A            D1PGMQ                    SFLPGMQ(10)
      *
      ** Message subfile record control
      ** ==============================
      *
     A          R UT000030C1                SFLCTL(UT000030S1)
     A                                      TEXT('Message Subfile Control Recor-
     A                                      d Format')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  32                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            D1PGMQ                    SFLPGMQ(10)
      *
      ** No records
      ** ==========
      *
     A          R UT000030C2
     A                                      CF03(03)
     A                                      CF05(05)
     A                                      OVERLAY
      *
     A            D0USER        10A  O  1  2TEXT('Current User')
     A                                  1 13'UT000030'
     A            D0MRDT         8Y 0O  1 71EDTWRD('  /  /    ')
      *
     A            D0WID         10A  O  2  2TEXT('WorkStation ID')
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                  5  4'Validation List'
     A                                  5 44'Description'
     A                                  1 29'Validation List Manager'
     A                                      DSPATR(HI)
     A                                  2 34'Work with List'
     A                                      DSPATR(HI)
     A                                  4 61'Library:'
     A            #1LIBL        10A  B  4 70
     A  47                                  DSPATR(RI)
     A  47                                  DSPATR(PC)
     A            #1HLIBL       10A  H
     A                                  7 34'No record found'
     A                                      DSPATR(HI)
     A                                 22  2'F3=EXit'
     A                                      COLOR(BLU)
