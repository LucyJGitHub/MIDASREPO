      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UT Validation List Objects Maintenance')         *
      *****************************************************************
      *                                                               *
      *  Midas - Utilities Module                                     *
      *                                                               *
      *  DSPF/UT000030DF - Midas UT Validation List Objects           *
      *                    Maintenance Display                        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      *  Last Amend No. CUT007  *REWRITE   Date 15Dec10               *
      *  Prev Amend No. BUG28082           Date 31Aug10               *
      *                 BUG27989*CREATE    Date 08Aug10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUT007 - Rewrite of UTWRKVLDL utility.                       *
      *  BUG28082 - Unable to display the correct information in the  *
      *             screen. (Recompile)                               *
      *  BUG27989 - HATS should run with/without LDAP                 *
      *                                                               *
      *****************************************************************
      *
      * Subfile record.
     A          R UT000030S0                SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2010')
     A  34                                  SFLNXTCHG
      *
     A            D0OPTN         2A  B  6  2TEXT('Action Code')
     A  48                                  DSPATR(RI)
     A  48                                  DSPATR(PC)
     A            D0OBNM        10A  O  6  6
     A            D0LBNM        10A  O  6 18
     A            D0OBTX        50A  O  6 30
      *
      * Subfile record control.
     A          R UT000030C0                SFLCTL(UT000030S0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('Subfile control record +
     A                                      format')
     A N33                                  ROLLUP(27)
     A N33                                  ROLLDOWN
     A                                      CF03(03)
     A                                      OVERLAY
     A  30                                  SFLDSP
     A                                      SFLDSPCTL
     A  31                                  SFLCLR
     A  33                                  SFLEND
      *
     A            D0SFRN         4S 0H      SFLRCDNBR
      *
     A            D0USER        10A  O  1  2TEXT('Current User')
     A                                  1 13'UT000030'
     A                                  1 29'Validation List Manager'
     A                                      DSPATR(HI)
     A            D0MRDT         8Y 0O  1 71EDTWRD('  /  /    ')
      *
     A            D0WID         10A  O  2  2TEXT('WorkStation ID')
     A                                  2 34'Work with List'
     A                                      DSPATR(HI)
     A*           D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                  2 72TIME
     A                                      EDTWRD('0 :  :  ')
      *
     A                                  4 61'Library:'
     A            D0LIBL        10A  O  4 70
     A                                      DSPATR(HI)
      *
     A                                  5  6'List'
     A                                      DSPATR(HI)
     A                                  5 18'Library'
     A                                      DSPATR(HI)
     A                                  5 30'Description'
     A                                      DSPATR(HI)
      *
      * Screen footer.
     A          R UT000030F0
     A                                      TEXT('Footer record format')
     A                                      OVERLAY
     A                                 22  2'E=Enquire'
     A                                      COLOR(BLU)
     A                                 22 14'12=Work with validation list +
     A                                      entries'
     A                                      COLOR(BLU)
     A                                 22 53'F3=Exit'
     A                                      COLOR(BLU)
      *
      * Message subfile record.
     A          R UT000030S1                SFL
     A                                      TEXT('Message subfile record +
     A                                      format')
     A                                      SFLMSGRCD(24)
     A            D1SFKY                    SFLMSGKEY
     A            D1PGMQ                    SFLPGMQ(10)
      *
      * Message subfile record control.
     A          R UT000030C1                SFLCTL(UT000030S1)
     A                                      TEXT('Message subfile control +
     A                                      record format')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  32                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            D1PGMQ                    SFLPGMQ(10)
      *
      * No records.
     A          R UT000030C2
     A                                      CF03(03)
     A                                      OVERLAY
      *
     A            D0USER        10A  O  1  2TEXT('Current user')
     A                                  1 13'UT000030'
     A                                  1 29'Validation List Manager'
     A                                      DSPATR(HI)
     A            D0MRDT         8Y 0O  1 71EDTWRD('  /  /    ')
      *
     A            D0WID         10A  O  2  2TEXT('WorkStation ID')
     A                                  2 34'Work with List'
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                      EDTWRD('0 :  :  ')
      *
     A                                  4 61'Library:'
     A            D0LIBL        10A  O  4 70
     A                                  5  6'List'
     A                                      DSPATR(HI)
     A                                  5 18'Library'
     A                                      DSPATR(HI)
     A                                  5 30'Description'
     A                                      DSPATR(HI)
     A                                  7 34'No record found'
     A                                      DSPATR(HI)
     A                                 22  2'F3=EXit'
     A                                      COLOR(BLU)
      *
      ** Validation list field information window.
     A          R UT000030W0
     A                                      CF12(12 'Cancel')
     A                                      WINDOW(5 20 10 38)
      *
     A                                  1  7'Validation List Field Info'
     A                                      DSPATR(HI)
     A            D1OBNM        10A  O  2 16
     A                                      DSPATR(HI)
      *
     A                                  4  1'Name'
     A                                      DSPATR(HI)
     A                                  4 32'Length'
     A                                      DSPATR(HI)
      *
     A            D1IDTX        30A  O  5  1
     A                                  6  1'Password'
     A                                  7  1'Description'
     A                                  9  1'F12=Cancel'
     A                                      COLOR(BLU)
      *
     A            D1IDLN         4Y 0O  5 33EDTCDE(1)
     A            D1ENLN         4Y 0O  6 33EDTCDE(1)
     A            D1DSLN         4Y 0O  7 33EDTCDE(1)
