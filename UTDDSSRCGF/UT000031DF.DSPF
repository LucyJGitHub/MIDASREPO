      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UT Validation List Entries Maintenance')         *
      *****************************************************************
      *                                                               *
      *  Midas - Utilities Module                                     *
      *                                                               *
      *  DSPF/UT000031DF - Validation list entries maintenance        *
      *                    display                                    *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CUT007 *REWRITE    Date 02Dec10               *
      *                 BUG28082           Date 31Aug10               *
      *                 BUG27989*CREATE    Date 08Aug10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CUT007 - Make utility file-driven.                           *
      *  BUG28082 - Unable to display the correct information in the  *
      *             screen. (Recompile)                               *
      *  BUG27989 - HATS should run with/without LDAP                 *
      *                                                               *
      *****************************************************************
      *
      * Subfile record.
     A          R UT000031S0                SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2010')
     A  34                                  SFLNXTCHG
      *
     A            D0OPTN         1A  B  7  3TEXT('Action code')
     A  48                                  DSPATR(RI)
     A  48                                  DSPATR(PC)
     A            D0ENID        30A  O  7  6
     A            D0DESC        44A  O  7 37
     A            D0HFLG        10A  H
     A            D0HEID       100A  H
     A            D0HDSC      1000A  H
      *
      * Subfile record control.
     A          R UT000031C0                SFLCTL(UT000031S0)
     A                                      TEXT('Subfile control record +
     A                                      format')
     A N33                                  ROLLUP(27)
     A N33                                  ROLLDOWN
     A                                      CF12(12)
     A                                      CF05(05)
     A                                      CF09(09)
     A                                      RTNCSRLOC(&D0HRCD &D0HFLD)
     A                                      CA22(22)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&D0HCUR)
     A  55                                  CSRLOC(D0HROW D0HCOL)
     A  30                                  SFLDSP
     A                                      SFLDSPCTL
     A  31                                  SFLCLR
     A  33                                  SFLEND
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
      *
     A            D0SFRN         4S 0H      SFLRCDNBR
      *
     A            D0USER        10A  O  1  2TEXT('Current user')
     A                                  1 13'UT000031'
     A                                  1 29'Validation List Manager'
     A                                      DSPATR(HI)
     A            D0MRDT         8Y 0O  1 71EDTWRD('  /  /    ')
      *
     A            D0WID         10A  O  2  2TEXT('Workstation ID')
     A                                  2 30'Work with List Entries'
     A                                      DSPATR(HI)
     A            D0TIME         6Y 0O  2 72TEXT('Current time')
     A                                      EDTWRD('0 :  :  ')
      *
     A                                  4 43'Validation list:'
     A            D0VLDL        21A  O  4 60DSPATR(HI)
      *
     A                                  5  2'Opt'
     A            D0IDTX        30A  O  5 06
     A                                  5 37'Description'
      *
     A            D0SIDE        30A  B  6  6DSPATR(HI)
     A                                      CHECK(LC)
     A            D0HSID        40   H
     A            D0HFLD        10A  H
     A            D0HRCD        10A  H
     A            D0HCUR         5  0H
     A            D0HROW         3  0H
     A            D0HCOL         3  0H
      *
      * Screen footer.
     A          R UT000031F0
     A                                      OVERLAY
     A                                      TEXT('Footer record format')
     A                                 22 32'F12=Previous'
     A                                      COLOR(BLU)
     A                                 22 46'F5=Refresh'
     A                                      COLOR(BLU)
     A                                 22 58'F9=Insert'
     A                                      COLOR(BLU)
     A                                 22  2'A=Amend'
     A                                      COLOR(BLU)
     A                                 22 11'E=Enquire'
     A                                      COLOR(BLU)
     A                                 22 22'D=Delete'
     A                                      COLOR(BLU)
     A                                 23 02'F22=Display entire field'
     A                                      COLOR(BLU)
      *
      * Record format insert.
     A          R UT000031R0
     A                                      TEXT('Insert record format')
     A                                      CF12(12 'Previous')
     A                                      OVERLAY
     A            D0USER        10A  O  1  2TEXT('Current user')
     A                                  1 13'UT000031'
     A            D0MRDT         8Y 0O  1 71EDTWRD('  /  /    ')
      *
     A            D0WID         10A  O  2  2TEXT('Workstation ID')
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current time')
     A                                      EDTWRD('0  :  :  ')
     A                                  1 29'Validation List Manager'
     A                                      DSPATR(HI)
     A                                  2 30'Work with List Entries'
     A                                      DSPATR(HI)
     A                                  5  1'Type the following and press ENTER'
     A                                      COLOR(BLU)
     A                                  7  1'ID Data . . . . . . . . .'
     A            D1IDTA       256A  B  7 27
     A  49                                  DSPATR(RI)
     A                                      CHECK(LC)
     A                                 11  1'Encrypt data  . . . . . .'
     A            D1EDTA       256A  B 11 27DSPATR(ND)
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A                                      CHECK(LC)
     A                                 15  1'Confirm Encrypt Data  . .'
     A            D1EDTC       256A  B 15 27
     A  51                                  DSPATR(RI)
     A                                      DSPATR(ND)
     A  51                                  DSPATR(PC)
     A                                      CHECK(LC)
     A                                 19  1'Description       . . . .'
     A            D1DDTA        50A  B 19 27CHECK(LC)
     A                                 23  1'F12=Previous'
     A                                      COLOR(BLU)
     A            D1ACTI        15A  O  3 65DSPATR(HI)
      *
      * Record format amend.
     A          R UT000031R1
     A                                      TEXT('Amend record format')
     A                                      CF12(12 'Previous')
     A                                      OVERLAY
     A            D0USER        10A  O  1  2TEXT('Current user')
     A                                  1 13'UT000031'
     A            D0MRDT         8Y 0O  1 71EDTWRD('  /  /    ')
      *
     A            D0WID         10A  O  2  2TEXT('Workstation ID')
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current time')
     A                                      EDTWRD('0 :  :  ')
     A                                  1 29'Validation List Manager'
     A                                      DSPATR(HI)
     A                                  2 30'Work with List Entries'
     A                                      DSPATR(HI)
     A                                  5  1'Type the following and press ENTER'
     A                                      COLOR(BLU)
     A                                  6  1'ID Data . . . . . . . . .'
     A            D2IDTA       256A  O  6 27DSPATR(HI)
     A                                 10  1'Encrypt Data  . . . . . .'
     A            D2EDTA       256A  B 10 27
     A  50                                  DSPATR(RI)
     A                                      DSPATR(ND)
     A  50                                  DSPATR(PC)
     A                                      CHECK(LC)
     A                                 14  1'New Encrypt Data  . . . .'
     A            D2EDTN       256A  B 14 27
     A  51                                  DSPATR(RI)
     A                                      DSPATR(ND)
     A  51                                  DSPATR(PC)
     A                                      CHECK(LC)
     A                                 22  1'Description       . . . .'
     A            D2DESC        50A  B 22 27CHECK(LC)
     A                                 23  1'F12=Previous'
     A                                      COLOR(BLU)
     A                                 18  1'Confirm New Encrypt Data '
     A            D2EDTC       256A  B 18 27
     A  52                                  DSPATR(RI)
     A                                      DSPATR(ND)
     A  53                                  DSPATR(PC)
     A                                      CHECK(LC)
     A            D2ACTI        15A  O  3 65DSPATR(HI)
     A            D2HEDT       256A  H
     A            D2HDDT       256A  H
      *
      * Record format delete / view.
     A          R UT000031R2
     A                                      TEXT('Delete / view record format')
     A                                      CF10(10 'Confirm')
     A                                      CF12(12 'Cancel')
     A                                      OVERLAY
     A            D0USER        10A  O  1  2TEXT('Current user')
     A                                  1 13'UT000031'
     A            D0MRDT         8Y 0O  1 71EDTWRD('  /  /    ')
     A            D0WID         10A  O  2  2TEXT('Workstation ID')
     A            D0TIME         6Y 0O  2 72TEXT('Current time')
     A                                      EDTWRD('0  :  :  ')
     A                                  1 29'Validation List Manager'
     A                                      DSPATR(HI)
     A                                  2 30'Work with List Entries'
     A                                      DSPATR(HI)
     A            D0MODE        30A  O  3 26DSPATR(HI)
     A            D3IDTX        30A     7  3
     A            D3IDTA       100A  O  7 34DSPATR(HI)
     A                                 10  3'Description . . . . . . . +
     A                                       . .:'
     A                                 23  1'F12=Cancel'
     A                                      COLOR(BLU)
     A            D3HEDT       256A  H
     A            D3ACTI        15A  O  3 65DSPATR(HI)
     A            D3DDTA      1000A  O 10 34DSPATR(HI)
     A            D3LABL        75A  O  5  2COLOR(BLU)
      *
      * Message subfile record.
     A          R UT000031S1                SFL
     A                                      TEXT('Message subfile record +
     A                                      format')
     A                                      SFLMSGRCD(24)
     A            D1SFKY                    SFLMSGKEY
     A            D1PGMQ                    SFLPGMQ(10)
      *
      * Message subfile record control.
     A          R UT000031C1                SFLCTL(UT000031S1)
     A                                      TEXT('Message subfile control +
     A                                      format')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  32                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            D1PGMQ                    SFLPGMQ(10)
      *
      * Record format to display entire field.
     A          R UT000031W1
     A                                      TEXT('Entire field display format')
     A                                      WINDOW(2 2 17 74)
     A                                      CF12(12 'Cancel')
     A                                 16  1'F12=Cancel'
     A                                      COLOR(BLU)
     A            D4IDSC        30A  O  1 29
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A            D4IDT1        74   O  2  1
     A            D4IDT2        74   O  3  1
     A            D4IDT3        74   O  4  1
     A            D4IDT4        74   O  5  1
     A            D4IDT5        74   O  6  1
     A            D4IDT6        74   O  7  1
     A            D4IDT7        74   O  8  1
     A            D4IDT8        74   O  9  1
     A            D4IDT9        74   O 10  1
     A            D4IDT10       74   O 11  1
     A            D4IDT11       74   O 12  1
     A            D4IDT12       74   O 13  1
     A            D4IDT13       74   O 14  1
     A            D4IDT14       38   O 15  1
