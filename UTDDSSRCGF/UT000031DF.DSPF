      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UT Validation List Entries Maintenance')         *
      *****************************************************************
      *                                                               *
      *  Midas - Utilities Modules                                    *
      *                                                               *
      *  DSPF/UT000031DF - Midas GP Validation List Entries           *
      *                    Maintenance Display                        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2010            *
      *                                                               *
      *                                                               *
      *  Last Amend No. BUG28082           Date 31Aug10               *
      *  Prev Amend No. BUG27989*CREATE    Date 08Aug10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG28082 - Unable to display the correct information in the  *
      *             screen. (Recompile)                               *
      *  BUG27989 - HATS should run with/without LDAP                 *
      *                                                               *
      *****************************************************************
      *
      ** Subfile record
      ** ==============
      *
     A          R UT000031S0                SFL
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2010')
     A  34                                  SFLNXTCHG
      *
     A            ##OPTN         1A  B  7  2TEXT('Action Code')
     A  48                                  DSPATR(RI)
     A  48                                  DSPATR(PC)
     A            ##USER        40A  O  7  4
     A            ##DESC        35A  O  7 45
     A            #HUSER       256A  H
     A            #HDESC       256A  H
      *
      ** Subfile record control
      ** ======================
      *
     A          R UT000031C0                SFLCTL(UT000031S0)
     A                                      TEXT('Subfile ControlRecord Format')
     A N33                                  ROLLUP(27)
     A N33                                  ROLLDOWN
     A                                      CF12(12)
     A                                      CF05(05)
     A                                      CF09(09)
     A                                      OVERLAY
     A  30                                  SFLDSP
     A                                      SFLDSPCTL
     A  31                                  SFLCLR
     A  33                                  SFLEND
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
      *
     A            D0SFRN         4S 0H      SFLRCDNBR
      *
     A            D0USER        10A  O  1  2TEXT('Current User')
     A                                  1 13'UT000031'
     A            D0MRDT         8Y 0O  1 71EDTWRD('  /  /    ')
      *
     A            D0WID         10A  O  2  2TEXT('WorkStation ID')
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                  5 11'Validation List ID'
     A                                  5 57'Description'
     A                                  1 29'Validation List Manager'
     A                                      DSPATR(HI)
     A                                  2 30'Work with List Entries'
     A                                      DSPATR(HI)
     A            #SUSER        40A  B  6  4DSPATR(HI)
     A                                      CHECK(LC)
     A                                  5  2'Opt'
     A            #1VLDL        10A  O  3 70DSPATR(HI)
     A            #SHUSER       40   H
      *
      ** Screen Footer
      ** =============
      *
     A          R UT000031F0
     A                                      OVERLAY
     A                                      TEXT('Footer RecordFormat')
     A                                 23 32'F12=Previous'
     A                                      COLOR(BLU)
     A                                 23 46'F5=Refresh'
     A                                      COLOR(BLU)
     A                                 23 58'F9=Insert'
     A                                      COLOR(BLU)
     A                                 23  2'A=Amend'
     A                                      COLOR(BLU)
     A                                 23 11'E=Enquire'
     A                                      COLOR(BLU)
     A                                 23 22'D=Delete'
     A                                      COLOR(BLU)
      *
      ** Record Format Insert
      ** ====================
      *
     A          R UT000031R0
     A                                      TEXT('Footer RecordFormat')
     A                                      CF12(12 'Previous')
     A                                      OVERLAY
     A            D0USER        10A  O  1  2TEXT('Current User')
     A                                  1 13'UT000031'
     A            D0MRDT         8Y 0O  1 71EDTWRD('  /  /    ')
      *
     A            D0WID         10A  O  2  2TEXT('WorkStation ID')
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                  1 29'Validation List Manager'
     A                                      DSPATR(HI)
     A                                  2 30'Work with List Entries'
     A                                      DSPATR(HI)
     A                                  5  1'Type the following and press ENTER'
     A                                      COLOR(BLU)
     A                                  7  1'User Name . . . . . . . .'
     A            #2USER       256A  B  7 27
     A  49                                  DSPATR(RI)
     A                                      CHECK(LC)
     A                                 11  1'Password  . . . . . . . .'
     A            #2PAS1       256A  B 11 27DSPATR(ND)
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A                                      CHECK(LC)
     A                                 15  1'Confirm Password  . . . .'
     A            #2PAS2       256A  B 15 27
     A  51                                  DSPATR(RI)
     A                                      DSPATR(ND)
     A  51                                  DSPATR(PC)
     A                                      CHECK(LC)
     A                                 19  1'Description       . . . .'
     A            #2DESC        50A  B 19 27CHECK(LC)
     A                                 23  1'F12=Previous'
     A                                      COLOR(BLU)
     A            #2ACTI        15A  O  3 65DSPATR(HI)
      *
      ** Record Format Amend
      ** ===================
      *
     A          R UT000031R1
     A                                      TEXT('Footer RecordFormat')
     A                                      CF12(12 'Previous')
     A                                      OVERLAY
     A            D0USER        10A  O  1  2TEXT('Current User')
     A                                  1 13'UT000031'
     A            D0MRDT         8Y 0O  1 71EDTWRD('  /  /    ')
      *
     A            D0WID         10A  O  2  2TEXT('WorkStation ID')
      *
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                  1 29'Validation List Manager'
     A                                      DSPATR(HI)
     A                                  2 30'Work with List Entries'
     A                                      DSPATR(HI)
     A                                  5  1'Type the following and press ENTER'
     A                                      COLOR(BLU)
     A                                  6  1'User Name . . . . . . . .'
     A            #3USER       256A  O  6 27DSPATR(HI)
     A                                 10  1'Current Pasword . . . . .'
     A            #3PAS1       256A  B 10 27
     A  50                                  DSPATR(RI)
     A                                      DSPATR(ND)
     A  50                                  DSPATR(PC)
     A                                      CHECK(LC)
     A                                 14  1'New Password  . . . . . .'
     A            #3PAS2       256A  B 14 27
     A  51                                  DSPATR(RI)
     A                                      DSPATR(ND)
     A  51                                  DSPATR(PC)
     A                                      CHECK(LC)
     A                                 22  1'Description       . . . .'
     A            #3DESC        50A  B 22 27CHECK(LC)
     A                                 23  1'F12=Previous'
     A                                      COLOR(BLU)
     A                                 18  1'Confirm New Password  . .'
     A            #3PAS3       256A  B 18 27
     A  52                                  DSPATR(RI)
     A                                      DSPATR(ND)
     A  53                                  DSPATR(PC)
     A                                      CHECK(LC)
     A            #3ACTI        15A  O  3 65DSPATR(HI)
     A            #3HPAS1      256A  H
     A            #3HDESC      256A  H
      *
      ** Record Format Delete/View
      ** =========================
      *
     A          R UT000031R2
     A                                      TEXT('Footer RecordFormat')
     A                                      CF10(10 'Confirm')
     A                                      CF12(12 'Cancel')
     A                                      OVERLAY
     A            D0USER        10A  O  1  2TEXT('Current User')
     A                                  1 13'UT000031'
     A            D0MRDT         8Y 0O  1 71EDTWRD('  /  /    ')
     A            D0WID         10A  O  2  2TEXT('WorkStation ID')
     A            D0TIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                  1 29'Validation List Manager'
     A                                      DSPATR(HI)
     A                                  2 30'Work with List Entries'
     A                                      DSPATR(HI)
     A            #1MODE        30A  O  3 26DSPATR(HI)
     A                                  7  1'User Name . . . . . . . .'
     A            #4USER       256A  O  7 27DSPATR(HI)
     A                                 12  1'Description       . . . .'
     A                                 23  1'F12=Cancel'
     A                                      COLOR(BLU)
     A            #HPAS1       256A  H
     A            #4ACTI        15A  O  3 65DSPATR(HI)
     A            #4DESC        50A  O 12 27DSPATR(HI)
     A            #4LABL        75A  O  5  1COLOR(BLU)
      *
      ** Message subfile record
      ** ======================
      *
     A          R UT000031S1                SFL
     A                                      TEXT('Message Subfile Record Format-
     A                                      ')
     A                                      SFLMSGRCD(24)
     A            D1SFKY                    SFLMSGKEY
     A            D1PGMQ                    SFLPGMQ(10)
      *
      ** Message subfile record control
      ** ==============================
      *
     A          R UT000031C1                SFLCTL(UT000031S1)
     A                                      TEXT('Message Subfile Control Recor-
     A                                      d Format')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  32                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            D1PGMQ                    SFLPGMQ(10)
