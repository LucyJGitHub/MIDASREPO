      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UT FNDUPDSRC amend source code')                 *
      *****************************************************************
      *                                                               *
      *  Midas - Utilities                                            *
      *                                                               *
      *  UT000051DF - Amend code                                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2012            *
      *                                                               *
      *  Last Amend No. CUT015   *CREATE   Date 19Apr12               *
      *  Prev Amend No. xxxxxxxx           Date ddMmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUT015 - New utility to replace UPDSRC.                      *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(27 132 *DS4)
     A                                      ALTHELP
     A                                      CA03(03 'Return')
     A                                      CF11(11 'Jump to')
     A                                      PRINT
      *
     A          R UT000051F1                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2012')
      *
     A                                      OVERLAY
     A            SUSER         10   O  1  2TEXT('User')
     A                                  1 13'UT000051F1'
     A                                  1 30'Edit Source Code Lines'
     A                                      DSPATR(UL HI)
     A                                  1 67'Date '
     A                                  1 73DATE
     A                                      EDTWRD('  /  /  ')
     A            SWSID         10   O  2  2TEXT('Workstation ID')
     A                                  2 67'Time '
     A                                  2 73TIME
     A                                      EDTWRD('  :  :  ')
      *
     A          R UT000051C1                SFL
     A            SOLDS        100A  O  4  1
      *
     A          R UT000051S1                SFLCTL(UT000051C1)
     A                                      TEXT('Subfile control')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLCSRRRN(&SRRN)
     A N10                                  SFLDSP
     A                                      SFLDSPCTL
     A  10                                  SFLCLR
     A N10                                  SFLEND(*MORE)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0009)
     A                                      HELP
     A                                      HLPTITLE('Amend Selected Source')
     A          H                           HLPPNLGRP('HEADER' UT000051DF)
     A                                      HLPARA(01 002 02 100)
     A          H                           HLPPNLGRP('OLDSRC' UT000051DF)
     A                                      HLPARA(04 001 22 100)
     A          H                           HLPPNLGRP('OLDSRCFLG' UT000051DF)
     A                                      HLPARA(23 002 23 004)
     A          H                           HLPPNLGRP('CURPAGE' UT000051DF)
     A                                      HLPARA(23 007 23 023)
     A          H                           HLPPNLGRP('GOTOPAGE' UT000051DF)
     A                                      HLPARA(23 026 23 034)
     A          H                           HLPPNLGRP('SCANFOR' UT000051DF)
     A                                      HLPARA(23 037 23 066)
     A          H                           HLPPNLGRP('CASESENSE' UT000051DF)
     A                                      HLPARA(23 068 23 084)
     A          H                           HLPPNLGRP('CMDKEY' UT000051DF)
     A                                      HLPARA(24 001 24 100)
     A                                 23  2'Old'
     A                                      DSPATR(HI)
     A                                 23  7'Current page:'
     A            SCURP          3Y 0O 23 21DSPATR(HI)
     A                                      OVRDTA
     A                                 23 26'Goto:'
     A            SNXTP          3Y 0B 23 32DSPATR(HI)
     A                                      OVRDTA
     A                                 23 37'Scan for:'
     A            SSCNF         20A  B 23 47DSPATR(HI)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                 23 68'Case sensitive:'
     A            SCASE          1A  B 23 84DSPATR(HI)
     A                                      OVRDTA
     A                                 24  2'F3=Exit, F11=Next unedited line, +
     A                                      Enter=Amend mode'
     A            SRRN           5S 0H
     A            SPSTN          4S 0H      SFLRCDNBR
      *
     A          R UT000051C2                SFL
     A            SNEWS        100A  B  4  1DSPATR(HI)
     A                                      CHECK(LC)
     A            SOLDS        100A  H
      *
     A          R UT000051S2                SFLCTL(UT000051C2)
     A                                      TEXT('Subfile control')
     A                                      CF07(07 'Enquire old')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLCSRRRN(&SRRN)
     A N10                                  SFLDSP
     A N10                                  SFLDSPCTL
     A  10                                  SFLCLR
     A N10                                  SFLEND(*MORE)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0009)
     A                                      HELP
     A                                      HLPTITLE('Amend Selected Source')
     A          H                           HLPPNLGRP('HEADER' UT000051DF)
     A                                      HLPARA(01 002 02 100)
     A          H                           HLPPNLGRP('NEWSRC' UT000051DF)
     A                                      HLPARA(04 001 22 100)
     A          H                           HLPPNLGRP('NEWSRCFLG' UT000051DF)
     A                                      HLPARA(23 002 23 004)
     A          H                           HLPPNLGRP('CURPAGE' UT000051DF)
     A                                      HLPARA(23 007 23 023)
     A          H                           HLPPNLGRP('GOTOPAGE' UT000051DF)
     A                                      HLPARA(23 026 23 034)
     A          H                           HLPPNLGRP('SCANFOR' UT000051DF)
     A                                      HLPARA(23 037 23 066)
     A          H                           HLPPNLGRP('CASESENSE' UT000051DF)
     A                                      HLPARA(23 068 23 084)
     A          H                           HLPPNLGRP('CMDKEY' UT000051DF)
     A                                      HLPARA(24 001 24 100)
     A                                 23  2'New'
     A                                      DSPATR(HI)
     A                                 23  7'Current page:'
     A            SCURP          3Y 0O 23 21DSPATR(HI)
     A                                      OVRDTA
     A                                 23 26'Goto:'
     A            SNXTP          3Y 0B 23 32DSPATR(HI)
     A                                      OVRDTA
     A                                 23 37'Scan for:'
     A            SSCNF         20A  B 23 47DSPATR(HI)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                 23 68'Case sensitive:'
     A            SCASE          1A  B 23 84DSPATR(HI)
     A                                      OVRDTA
     A                                 24  2'F3=Exit, F7=Enquire old, F11=Next +
     A                                      unedited line, Enter=Update'
     A            SRRN           5S 0H
     A            SPSTN          4S 0H      SFLRCDNBR
      *
     A          R UT000051F2
     A                                      OVERLAY
     A            SUSER         10   O  1  2TEXT('User')
     A                                  1 13'UT000051F1'
     A                                  1 30'Edit Source Code Lines'
     A                                      DSPATR(UL HI)
     A                                  1 67'Date '
     A                                  1 73DATE
     A                                      EDTWRD('  /  /  ')
     A            SWSID         10   O  2  2TEXT('Workstation ID')
     A                                  2 67'Time '
     A                                  2 73TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                 12 13'No records were found with the spe-
     A                                      cified character string'
     A                                      DSPATR(HI)
