      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas UT Utilities menu screen')
      *****************************************************************
      *                                                               *
      *  Midas -  UT Utilities Menu                                   *
      *                                                               *
      *  DSPF/UT0930DF  -  Midas Utilities Menu Display File          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CPK019             Date 23Jul04               *
      *  Prev Amend No. CAAA02             Date 16Jul03               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 CUP019             Date 03Oct02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CUP002             Date 22Jun99               *
      *                 CUP001             Date 01Mar99               *
      *                 CUP004             Date 28APR98               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CPK019 - Remove valid values 'H' 'h' for field HLPOPT because*
      *           HELP function has already been taken out.           *
      *  CAAA02 - Remove use of INVITE keyword to enable WebFacing    *
      *           to process screens.                                 *
      *           INVITE is in this DSPF but is not used.             *
      *  CUP019  -  Take out HELP function.                           *
      *  CUP002  -  Addition of EXIT text and screen identifier.      *
      *  CUP001  -  Introduction of BRIDGE.                           *
      *  CUP004  -  Make upgrade of DTALIB files easier               *
      *                                                               *
      *****************************************************************
     A                                      MSGLOC(22)
     A                                      INDARA
     A                                      PRINT
      ** Start of CAAA02
     A**11**************                    INVITE
      ** End of CAAA02
     A                                      CF12
     A                                      CF16
     A          R UT0930F1                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            WSNUM          4Y 0O  6  8TEXT('Option number')
     A                                      EDTCDE(3)
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(ND)
     A            ICDE           4A  O  6 19TEXT('Item short code')
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(ND)
     A            INAR          45A  O  6 26TEXT('item narrative')
     A  27                                  DSPATR(HI)
     A            PCMD          20A  H      TEXT('Program/command')
      *                                     Start of change /CUP001/
     A**********  PARA          70A  H      TEXT('Parameters')
     A            PARA         100A  H      TEXT('Parameters')
      *                                       End of change /CUP001/
     A            MGRP           4S 0H      TEXT('Item group no.')
     A            HLPOPT         1A  I  6 15
     A  27                                  DSPATR(ND PR)
      * Start of change for CPK019 */
     A***********                           VALUES('h' 'H' ' ' 'X' 'x')
     A                                      VALUES(' ' 'X' 'x')
      * End of change for CPK019 */
     A          R UT0930F2                  SFLCTL(UT0930F1)
     A                                      TEXT('Screen header')
     A                                      BLINK
     A                                      LOCK
     A                                      OVERLAY
     A                                      PUTOVR
     A N12                                  ERASE(UT0930F1)
     A  12                                  SFLDSP
     A  11                                  SFLDSPCTL
     A  10                                  SFLCLR
     A  13                                  SFLEND
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0014)
     A            WSUSER        10A  O  1  2TEXT('User ID')
     A                                      OVRDTA
      *                                     Start of change /CUP002/
     A                                  1 13'UT0930F1'
      *                                       End of change /CUP002/
     A                                  1 30'Midas Utilities Menu'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            WSJOB         10A  O  2  2TEXT('Job name')
     A                                      OVRDTA
     A            WSTITL        45A  O  2 18TEXT('Menu title')
     A                                      OVRDTA
     A                                  2 71TIME
     A                                      OVRATR
     A                                  4 11'Enter option number or code:'
     A                                      OVRATR
     A            WSOPT          4A  B  4 41TEXT('Option selection field')
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A  15                                  ERRMSG('The option requested is not-
     A                                       valid. Press ''ERROR RESET'' and t-
     A                                      ry again.')
     A  16                                  ERRMSG('The requested option ended -
     A                                      in error. Press ''ERROR RESET'' and-
     A                                       try again.')
     A  18                                  ERRMSG('Help is not available for t-
     A                                      his option. Press ''ERROR RESET''')
     A            WSPOS          4S 0H      TEXT('Subfile page position')
     A                                      SFLRCDNBR
     A            SYSNAM         8A  O  1 13
     A          R UT0930F3
     A                                      TEXT('Bottom line')
     A                                      LOCK
     A  17                                  OVERLAY
      *                                     Start of change /CUP002/
     A**********                       23  3'F12=Previous Menu  F16=Initial Me-
     A**********                            nu'
     A                                 23  3'Type EXIT to Exit Menu   F12=Previ-
     A                                      ous Menu   F16=Initial Menu'
      *                                       End of change /CUP002/
     A                                 24  3'(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001'
     A                                      DSPATR(HI)
     A                                 21  4'Enter'
      * Start of change CUP019
     A**********                       21 10'H'                                               CUP019
     A**********                            DSPATR(HI)                                        CUP019
     A**********                       21 12'for Help,'                                       CUP019
     A**********                       21 22'X'                                               CUP019
     A**********                            DSPATR(HI)                                        CUP019
     A**********                       21 24'to Select'                                       CUP019
     A                                 21 10'X'
     A                                      DSPATR(HI)
     A                                 21 12'to Select'
      * End of change CUP019
