      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas UT Source compare')
      *****************************************************************
      *                                                               *
      *  Midas - Utilities                                            *
      *                                                               *
      *  PRTF/UT000002P1 - Source comparison output                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2007            *
      *                                                               *
      *  Last Amend No. BUG27884 *CREATE   Date 08Jul10               *
      *  Prev Amend No. CUT010             Date 15May07               *
      *                 xxxxxx             Date ddMmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG27884 - Applied CUT010.                                   *
      *  CUT010 - New utility.                                        *
      *                                                               *
      *****************************************************************
      *
     A          R HEAD01                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2007')
     A                                      SKIPB(2)
      *
     A                                     5'CMPSRC'
     A                                    53'Compare Source File Members'
     A                                    96DATE
     A                                      EDTWRD('  /  /  ')
     A                                   106TIME
     A                                      EDTWRD('0 :  :  ')
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(2)
     A                                      SPACEA(2)
      *
     A          R HEAD02
     A            SPHREF       116         4
     A                                      SPACEA(1)
     A            SPHMAT       116         9
     A                                      SPACEA(1)
     A            SPHCMP       116        14
     A                                      SPACEA(2)
      *
     A          R HEAD03
     A                                      SPACEB(1)
     A                                     4'Reference:'
     A            SPSCRL        10        15
     A                                    25'/'
     A            SPSCRF        10        26
     A                                    40'Comparison:'
     A            SPSCCL        10        52
     A                                    62'/'
     A            SPSCCF        10        63
     A                                      SPACEA(2)
     A                                     4 'Member'
     A                                    15 'Ref.'
     A                                    23 'Cmp.'
     A                                      SPACEA(1)
     A                                     4 '======'
     A                                    15 '===='
     A                                    23 '===='
     A                                      SPACEA(1)
      *
     A          R DETAIL
     A            SPDTL        132         1
     A                                      SPACEA(1)
      *
     A          R SUM
     A            SPMEMB        10         4
     A            SPRREC         5        15
     A            SPCREC         5        23
     A                                      SPACEA(1)
     A          R TRAIL01
     A                                      SPACEB(2)
     A                                    56'***  End of Compare  ***'
     A          R TRAIL02
     A                                      SPACEB(2)
     A                                    56'***  End of Report  ***'
