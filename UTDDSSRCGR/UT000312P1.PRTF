      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas UT Report of incorrect source/object x-ref')
      *****************************************************************
      *                                                               *
      *  Midas - Utilities Module                                     *
      *                                                               *
      *  PRTF/UT000312P1 - Incorrect source/object x-ref              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. CUT011  *RENAMED   Date 12Dec07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CPK019  *MOVED     Date 08Jul04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CUP002 *CREATE     Date 07May99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CUT011 - SRCOBJXCHK renamed and title position changed.      *
      *  CPK019 - handle global layer.                                *
      *  CUP002 - Post-upgrade checks                                 *
      *                                                               *
      *****************************************************************
      *
      *  Formats for Program UT000312
      *
     A          R HEADH                     TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(2)
      *
     A            USER          10         5
      * Start CPK019
     A**********  @TITLE        86        22                                                  CPK019
      * Start CUT011
     A**********  @TITLE       100        16                                                  CUT011
     A            @TITLE       100        25
      * End CUT011
      * End   CPK019
     A                                   119'Date'
     A                                   124DATE
     A                                      EDTWRD('  /  /  ')
     A                                      SPACEA(1)
     A            WSID          10         5
     A                                   119'Time'
     A                                   124TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                     5'UP0106P1'
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(3)
     A                                      SPACEA(2)
      *
     A          R NORECS
                                          40'*** No details to report ***'
     A                                      SPACEA(2)
     A          R OHEAD
     A                                    40'Objects with +
     A                                      no matching source'
     A                                      SPACEA(2)
     A                                     5'Object name'
     A                                    17'Type'
     A                                    41'Description'
     A                                      SPACEA(2)
      *
     A          R ODTL
     A            @OBJ          10         5
     A            @OBJT         10        17
     A            @OTXT         50        41
     A                                      SPACEA(1)
      *
     A          R SHEAD
     A                                    40'Source with no +
     A                                      matching objects'
     A                                      SPACEA(2)
     A                                     5'Member'
     A                                    17'Type'
     A                                    29'Source file'
     A                                    41'Description'
     A                                      SPACEA(2)
      *
     A          R SDTL
     A            @MBNM         10         5
     A            @MBTP         10        17
     A            @SRCF         10        29
     A            @MTXT         50        41
     A                                      SPACEA(1)
      *
     A          R MHEAD
     A                                    30'Matching objects +
     A                                      and source where +
     A                                      last change dates +
     A                                      are out of step'
     A                                      SPACEA(2)
     A                                    94'Last changed +
     A                                      date'
     A                                      SPACEA(1)
     A                                     5'Object'
     A                                    17'Type'
     A                                    29'Source file'
     A                                    41'Description'
     A                                    93'Source'
     A                                   107'Object'
     A                                      SPACEA(2)
      *
     A          R MDTL
     A            @MBNM         10         5
     A            @MBTP         10        17
     A            @SRCF         10        29
     A            @MTXT         50        41
     A            @SDAT          8        92
     A            @ODAT          8       106
     A                                      SPACEA(1)
     A          R BLNK
     A                                     1' '
     A                                      SPACEA(1)
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP
     A                                      SPACEB(2)
     A                                    51'***  END OF REPORT  ***'
