      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas UT EXPSRC report')                               *
     A*****************************************************************
     A*                                                               *
     A*  Midas Utilities Module                                       *
     A*                                                               *
     A*  PRTF/UT0015P1 - Expand source report                         *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      *  Last Amend No. CUT008             Date 24Mar11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CPK008             Date 06Jun97               *
     A*                 092401             DATE 29Aug95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CUT008 - CRTOBJ rewrite:                                     *
      *           - Extend field length of EXTFIL to 10 for ILE pgm   *
      *             sources.                                          *
      *  CPK008 - DBA R2 packaging:                                   *
      *           - Extend SRCDTA to reflect extention to 100.        *
     A*  092401 - Ensure that the /COPY member library is reported.   *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
      *
     A                                       REF(MFRFL)
     A          R HEADPR                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A                                  1 31'EXPANDED SOURCE LISTING'
     A                                  1 80DATE EDTCDE(Y)
     A                                  1100TIME
     A                                  1120'PAGE :'
     A                                    +1PAGNBR EDTCDE(4)
     A*
     A                                  3  1'SOURCE FILE . . . . +
     A                                      . . .'
     A            SRCFIL        10        +1
     A                                    +1'/'
     A            SRCLIB        10        +1
     A*
     A                                  4  1'MEMBER  . . . . . . +
     A                                      . . .'
     A            SRCMBR        10        +1
     A*
     A                                  6  1'SEQNBR*...*... 1 +
     A                                      ...*... 2 ...*... 3 +
     A                                      ...*... 4 ...*... 5 +
     A                                      ...*... 6 ...*... 7 +
     A                                      ...*... 8 ...*... 9 +
     A                                      ...*... 0'
     A*
     A          R DETAIL                    TEXT('SOURCE CODE')
     A                                      SPACEB(1)
     A            SRCSEQ         6  2      1EDTCDE(Z)
     A  19                                  HIGHLIGHT
      *                                     Start of change /CPK008/
     A*********** SRCDTA        80         8TEXT('SOURCE DATA')
     A            SRCDTA       100         8TEXT('SOURCE DATA')
      *                                       End of change /CPK008/
     A  19                                  HIGHLIGHT
     A*
     A**                 'F' SPEC CODE INSERTS
     A*
     A          R FHDR1                     TEXT('F SPEC DETAIL')
     A  19                                  HIGHLIGHT
     A                                      SPACEB(1)
     A                                     1'****** EXPANDED +
     A                                      SOURCE FOR'
     A                                    +1'FILE'
      * Start CUT008
     A**********  EXTFIL         8        +1                                                  CUT008
     A            EXTFIL        10        +1
      * End   CUT008
     A                                    +1'FORMAT'
     A            RCDFMH        10        +1
     A*
     A*
     A          R EXTREC                    TEXT('DSPFFD RECORD')
     A  19                                  HIGHLIGHT
     A                                      SPACEB(1)
     A                                     1'EXPAND'
     A  11        DTATYP         1        43
     A            FROM           4  0    + 0EDTCDE(Z)
     A            END            4  0    + 0EDTCDE(Z)
     A  10        DECP           1  0    + 0
     A            FLDNAM        10       + 0
     A            DESCR         50       + 1
     A*
     A          R ENDF                      TEXT('F SPEC END')
     A  19                                  HIGHLIGHT
     A                                      SPACEB(1)
     A                                     1'****** END OF EXPANDED +
     A                                      SOURCE ******'
     A*
     A**                 ERROR REPORT STATEMENT
     A*
     A          R ERRF                      TEXT('F SPEC ERROR')
     A  19                                  HIGHLIGHT
     A                                      SPACEB(1)
     A                                     1'****** EXTERNAL FILE +
     A                                      NOT FOUND ******'
     A*
     A**                 '/COPY'CODE INSERTS
     A*
     A          R CLINED                    TEXT('/COPY DETAIL')
     A  19                                  HIGHLIGHT
     A                                      SPACEB(1)
     A                                     1'****** EXPANDED +
     A                                      SOURCE FOR'
     A                                    +1'/COPY MEMBER NAME :'
     A            CPYSRC        10        +1TEXT('/COPY MEMBER-NAME')
     A                                    +1'FILE NAME :'
     A            CPYFIL        10        +1TEXT('FILE NAME')
      *                                     Start of change /092401/
     A                                    +1'LIBRARY NAME :'
     A            CPYLIB        10        +1TEXT('LIBRARY NAME')
      *                                       End of change /092401/
     A*
     A          R ENDC                      TEXT('/COPY MEMBER END')
     A  19                                  HIGHLIGHT
     A                                      SPACEB(1)
     A                                     1'****** END OF EXPANDED +
     A                                      SOURCE ******'
     A*
     A**                 ERROR REPORT STATEMENT
     A*
     A          R ERRC1                     TEXT('/COPY ERROR')
     A  19                                  HIGHLIGHT
     A                                      SPACEB(1)
     A                                     1'****** /COPY MEMBER IS +
     A                                      EMPTY ******'
     A*
     A          R ERRC2                     TEXT('/COPY ERROR')
     A  19                                  HIGHLIGHT
     A                                      SPACEB(1)
     A                                     1'****** /COPY MEMBER NOT +
     A                                      FOUND ******'
     A*
     A          R ERRC3                     TEXT('/COPY ERROR')
     A  19                                  HIGHLIGHT
     A                                      SPACEB(1)
     A                                     1'****** /COPY SOURCE FILE +
     A                                      NOT FOUND ******'
     A*
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    25'* * * *  E N D  O F  E X P A N D E+
     A                                       D  S O U R C E  * * * *'
     A                                      SPACEB(3)
