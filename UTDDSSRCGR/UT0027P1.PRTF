      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas UT Printer file for CMPMSGF')
      *****************************************************************
      *                                                               *
      *  Midas - UT Module                                            *
      *                                                               *
      *  PRTF/UT0027P1 - Compare message files report                 *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CUP002 *CREATE     Date 08Apr99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CUP002 - Message files utilities                             *
      *                                                               *
      *****************************************************************
      *
      *  Formats for Program UT0027
      *
     A          R HEADH                     TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(2)
      *
     A                                    10'UT0027P1'
     A                                    50'CMPMSGF utility'
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                    42'Produced on'
     A                                    54DATE
     A                                      EDTWRD('  /  /  ')
     A                                    63'at'
     A                                    66TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
     A          R HEAD1
     A                                    10'Message files -'
     A                                    26'Before:'
     A            #LIB1         10A       34
     A                                    44'/'
     A N80        CMFILE        10A       45
     A  80        #MSGFB        10A       45
     A                                    55'(Base file)'
     A                                      SPACEA(1)
     A                                    26'After: '
     A            #LIB2         10A       34
     A                                    44'/'
     A N80        CMFIL1        10A       45
     A  80        #MSGFC        10A       45
     A                                    55'(Compare file)'
     A                                      SPACEA(1)
      *
     A          R HEAD2
     A                                    10'Compare criteria -'
     A                                    29'Base files:'
     A            #MSGLB        10A       44
     A                                    55'/'
     A            #MSGFB        10A       56
     A                                      SPACEA(1)
     A                                    29'Compare files:'
     A            #MSGLC        10A       44
     A                                    55'/'
     A            #MSGFC        10A       56
     A                                      SPACEA(1)
      *
     A          R NORECS
     A            CMFILE    R             10SPACEB(4)
     A                                    40'*** No files found ***'
      *****************************************************************
     A          R DTLA
     A                                      SPACEB(1)
     A                                    10'Amended messages'
     A                                    10'Amended messages'
     A                                      SPACEA(2)
     A                                     8'Message ID'
      *
     A          R DTLA1                     SPACEB(2)
     A N74        CMMIDR         7A       10
     A  70                                 5'**Overflow**'
     A                                    20'Field:'
     A            #TEXT         25A       30
     A                                    20'Before:'
     A                                      SPACEB(1)
     A            #VAL1A        90A       30
     A  41        #VAL1B        90A       30SPACEB(1)
     A  42        #VAL1C        90A       30SPACEB(1)
     A  43        #VAL1D        90A       30SPACEB(1)
     A  44        #VAL1E        90A       30SPACEB(1)
     A  45        #VAL1F        90A       30SPACEB(1)
     A  46        #VAL1G        90A       30SPACEB(1)
     A  47        #VAL1H        90A       30SPACEB(1)
     A  72                                75'***Unknown record format***'
     A*
     A  71                                 5'**Overflow**'
     A                                    20'After: '
     A                                      SPACEB(1)
     A            #VAL2A        90A       30
     A  51        #VAL2B        90A       30SPACEB(1)
     A  52        #VAL2C        90A       30SPACEB(1)
     A  53        #VAL2D        90A       30SPACEB(1)
     A  54        #VAL2E        90A       30SPACEB(1)
     A  55        #VAL2F        90A       30SPACEB(1)
     A  56        #VAL2G        90A       30SPACEB(1)
     A  57        #VAL2H        90A       30SPACEB(1)
     A  73                                75'**Unknown record format**'
      *
     A          R DTLAX                     SPACEB(4)
     A                                    15'*** There are no amended +
     A                                      messages ***'
     A          R DTLAU
     A            CMMIDR         7A       10SPACEB(2)
     A                                    30'*** There are unspecified differen+
     A                                      ces in this message ***'
      *****************************************************************
      *
     A          R DTLD
     A                                      SPACEB(1)
     A                                    10'Messages inserted into base file'
     A                                    10'Messages inserted into base file'
     A            #LIB1         10A       43
     A            #LIB1O        10A       43
     A                                    53'/'
     A                                    53'/'
     A N80        CMFILE        10A       54
     A  80        #MSGFB        10A       54
     A N80        CMFIL1        10A       54
     A  80        #MSFBO        10A       54
      *
     A          R DTLD1
     A  49                                 8'Message ID'
     A  49                                  SPACEB(2)
     A            CMMIDR    R             10SPACEB(2)
     A            #VAL1A    R             30
     A  51        #VAL1B    R             30SPACEB(1)
      *
     A          R DTLDX                     SPACEB(2)
     A                                    15'*** There are no inserted +
     A                                      messages ***'
      *****************************************************************
     A          R DTLI
     A                                      SPACEB(1)
     A                                    10'Message deleted from base file'
     A                                    10'Message deleted from base file'
     A            #LIB1         10A       44
     A            #LIB1O        10A       44
     A                                    54'/'
     A                                    54'/'
     A N80        CMFILE        10A       55
     A  80        #MSGFC        10A       55
     A N80        CMFIL1        10A       55
     A  80        #MSFCO        10A       55
     A          R DTLI1
     A  59                                 8'Message ID'
     A  59                                  SPACEB(2)
     A            CMMIDR    R             10SPACEB(2)
     A            #VAL1A    R             30
     A  51        #VAL1B    R             30SPACEB(1)
      *
     A          R DTLIX                     SPACEB(2)
     A                                    15'*** There are no deleted +
     A                                      messages ***'
     A**************************************************************************
     A          R DTLSUM
     A                                      SKIPB(8)
     A                                    44'*****   Message Summary   *****'
     A                                    44'*****   Message Summary   *****'
     A                                      SPACEA(2)
     A                                    44'Base'
     A                                    71'Compare'
     A                                      SPACEA(1)
     A                                    10'Base file'
     A                                    21'Library'
     A                                    32'Amended'
     A                                    43'Total'
     A                                    50'Deleted'
     A                                    61'Inserted'
     A                                    73'Total'
     A                                    82'Comp. File'
     A                                    93'Library'
     A
     A          R DTLS                      SPACEB(2)
     A  61                                 7'***Base File not found***'
     A    N61     #NAME1        10        10
     A    N61     #LIBR1        10        21
     A N60N61N62  #AMEND         5  0     32EDTCDE(1)
     A    N61     #TOTL1         5  0     42EDTCDE(1)
     A N60N61N62  #INSER         5  0     52EDTCDE(1)
     A N60N61N62  #DELET         5  0     62EDTCDE(1)
     A N60        #TOTL2         5  0     72EDTCDE(1)
     A  60                                79'***Compare file not found***'
     A N60        #NAME2        10        82
     A N60        #LIBR2        10        93
     A  62                               102'***Files identical***'
      *****************************************************************
      *
     A          R DTLOFM
     A                                      SKIPB(8)
     A                                    44'***  Overflow Messages ***'
     A                                    44'***  Overflow Messages ***'
     A                                      SPACEA(2)
     A  86                                20'The following messages have not +
     A                                      been checked in full as the field +
     A                                      lengths exceed the preset parameter+
     A                                      s.'
     A                                      SPACEA(1)
     A  86                                20'Please check these messages indivi+
     A                                      dually.'
     A                                      SPACEA(2)
     A                                    10'File'
     A                                    21'Library'
     A                                    32'Message ID'
      *
     A          R DTLOM
     A            CMFILE        10A       10SPACEB(1)
     A  80        #LIB1         10A       21
     A  81        #LIB2         10A       21
     A            CMMIDR         7A       32
     A            #CMMSG        80A       45SPACEA(1)
     A                                    26'Overflow field(s):'
     A  82        #TEXT1        80A       45SPACEA(1)
     A  83        #TEXT2        80A       45SPACEA(1)
     A  84        #TEXT3        80A       45SPACEA(1)
     A  85        #TEXT4        80A       45SPACEA(1)
      *
     A          R DTLOFF
     A                                      SKIPB(8)
     A                                    44'*****   Overflow Summary  *****'
     A                                    44'*****   Overflow Summary  *****'
     A                                      SPACEA(2)
     A                                    35'First'
     A                                    45'Second'
     A                                    55'Default'
     A                                    63'Substition'
     A                                      SPACEA(1)
     A                                    10'File'
     A                                    21'Library'
     A                                    35'Level'
     A                                    46'Level'
     A                                    57'Reply'
     A                                    64'Variables'
     A                                    79'Total'
      *
     A          R DTLO                      SPACEB(2)
     A  60        #NAME1        10        10
     A  60        #LIBR1        10        21
     A  61        #NAME2        10        10
     A  61        #LIBR2        10        21
     A  60        #MSGOF         5  0     34EDTCDE(1)
     A  60        #HLPOF         5  0     45EDTCDE(1)
     A  60        #DFTOF         5  0     56EDTCDE(1)
     A  60        #SUBOF         5  0     67EDTCDE(1)
     A  60        #TOTOF         5  0     78EDTCDE(1)
     A  61        #MSGO2         5  0     34EDTCDE(1)
     A  61        #HLPO2         5  0     45EDTCDE(1)
     A  61        #DFTO2         5  0     56EDTCDE(1)
     A  61        #SUBO2         5  0     67EDTCDE(1)
     A  61        #TOTO2         5  0     78EDTCDE(1)
      *
     A          R DTLOX                     SPACEB(2)
     A                                    15'*** There are no overflow +
     A                                      messages ***'
     A**************************************************************************
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP
     A                                      SPACEB(2)
     A                                    51'***  END OF REPORT  ***'
