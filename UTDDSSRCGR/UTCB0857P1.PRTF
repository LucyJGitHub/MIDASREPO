      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas  CB MD-57673 Data Patch Printer File')           *
      *****************************************************************
      *                                                               *
      *  Midas - Close of Business                                    *
      *                                                               *
      *  PRTF/UTCB0857P1 -  CB MD-57673 Data Patch Printer File       *
      *                                                               *
      *  (c) Finastra International Limited 2021                      *
      *                                                               *
      *  Last Amend No. MD057673  *CREATE  Date 13Mar21               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD057673 - Unauthorised transactions screen error in         *
      *             CB001001DF                                        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R UTCB0857H1                TEXT('(c) Finastra Internatio +
     A                                      nal Limited 2021')
     A                                      SKIPB(1)
     A                                     6'REFERENCE UTCB0857P1'
     A N10                                27'- UPDATE'
     A  10                                27'- AUDIT'
     A            BJURPT    R   53        38REFFLD(URPT)
     A                                   101'DATE:'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                    19'Update Value from Non-numeric to +
     A                                       Zero (0)'
     A                                      SPACEB(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                    19'---------------------------------+
     A                                       --------'
     A                                      SPACEA(2)
      *
     A          R UTCB0857H2
     A                                    02'MIDAS'
     A                                    08'KEY'
     A                                      SPACEA(1)
     A                                    02'ZONE'
     A                                    08'DESCRIPTION'
     A                                    30'VALUE'
     A                                      SPACEA(1)
      *
     A          R UTCB0857D1
     A                                      SPACEB(1)
     A            PMSID          2A       02
     A            PKEYD         20A       08
     A            PVAL          25A       30
      *
     A          R UTCB0857T
     A                                      SPACEB(1)
     A  10                                10'NUMBER OF RECORDS TO BE UPDATED'
     A N10                                10'NUMBER OF RECORDS WERE UPDATED'
     A                                    42
     A                                      '='
     A            PCTR           6S 0O    45
     A                                      EDTCDE(3)
     A                                      SPACEA(1)
     A          R UTCB0857E
     A                                      SPACEB(1)
     A                                    54'*** END OF REPORT ***'

