      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('TI Exposure Report')                                   *
      *****************************************************************
      *                                                               *
      *  Midas - Security Module                                      *
      *                                                               *
      *  PRTF/UTGO0539P1 - TI Purging Report                          *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. AR318278 *CREATE   Date 04Oct10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR318278 - TI Purging audit reports.                         *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(UTGO0539L1)
     A          R HEADER
     A                                      TEXT('(c) Finastra International +
     A                                      Limited 2010')
     A                                  3  5
     A                                      'REFERENCE UTGO0539P1'
     A                                  3 99
     A                                      'DATE'
     A                                  3105
     A                                      DATE
     A                                      EDTWRD('  /  /  ')
     A                                  3118
     A                                      'PAGE'
     A            PAGE           5S 0   3123
     A                                      EDTCDE(4)
     A            REPNAME       50A     5 41
     A                                  6 41
     A                                      '-----------------------------+
     A                                       ---------------------'
     A                                  8  5
     A                                      'Risk Customer'
     A                                  8 20
     A                                      'Reference'
     A                                  8 65
     A                                      'Value Date'
     A                                  8 78
     A                                      'Maturity Date'
     A                                  8122
     A                                      'Amount'
     A                                  9  5
     A                                      '-------------'
     A                                  9 20
     A                                      '---------'
     A                                  9 65
     A                                      '----------'
     A                                  9 78
     A                                      '-------------'
     A                                  9122
     A                                      '------'
      *
     A          R DETAIL
     A                                      SPACEB(001)
     A            CUSTCODE  R              8
     A            XTREF         40A       20
     A            XTVALDATE     10A       65
     A            XTMATDATE     10A       79
     A            MTASSETAMTR             99
     A                                      EDTWRD('   ,   ,   ,   , 0 .       -
     A                                       -')
      *
     A          R TOTAL
     A                                      SPACEB(001)
     A                                    99
     A                                      '-----------------------------'
     A                                      SPACEA(001)
     A                                    85'Net Balance:'
     A            BALANCE       23S 8     99
     A                                      EDTWRD('   ,   ,   ,   , 0 .       -
     A                                       -')
     A                                      SPACEA(001)
     A          R AUDIT
     A
     A                                      SPACEB(002)
     A            NODET          5S 0     78EDTCDE(3)
     A                                    50'Number of records printed -'
     A                                      SPACEA(001)
     A
     A            NOREC          5S 0     78EDTCDE(3)
     A                                    50'Number of records read    -'
     A                                    54SPACEB(002)
     A                                      '**     END OF REPORT     **'
      *
     A          R NODETS
     A                                      SPACEB(002)
     A                                    55
     A                                      '** NO DETAILS TO REPORT **'
