      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas UT to Housekeep 24x7 DZ Objects - Report')       *
      *****************************************************************
      *                                                               *
      *  Midas - Utilities Module                                     *
      *                                                               *
      *  PRTF/UT000247P1 - UT to Housekeep 24x7 DZ Objects Report     *
      *                                                               *
      *  (c) Finastra International Limited 2022                      *
      *                                                               *
      *  Last Amend No. MD058904 *CREATE    Date 19Dec22              *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD058904 - 24x7 Housekeeping and Clearout of the zzDZLIB     *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
     A          R HEADER01                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2022')
     A                                      SKIPB(1)
     A                                     6'REFERENCE UT000247P1'
     A N10                                27'- AUDIT'
     A  10                                27'- UPDATE'
     A            BJURPT    R   53        40REFFLD(URPT)
     A                                   101'DATE:'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                    47'Housekeeping of 24x7 DZ Library'
     A                                      SPACEB(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                    45'--------------------------------
     A                                      ----'
     A                                      SPACEA(1)
      *
     A          R HEADER02
     A                                      SPACEB(1)
     A                                    15'OLD LIB'
     A                                    28'NEW LIB'
     A                                    41'OBJ NAME'
     A                                    54'OBJ TYPE'
     A                                    65'DESCRIPTION'
     A                                      SPACEA(1)
      *
     A          R DETAIL
     A                                      SPACEB(1)
     A            P1OLDL        10A  O    15
     A            P1NEWL        10A  O    28
     A            P1OBNM        10A  O    41
     A            P1OBTP         8A  O    54
     A            P1OBTX        50A  O    65
      *
     A          R AUDIT01
     A                                      SPACEB(2)
     A N10                                15'NO. OF OBJECTS TO MOVE TO DPLIB'
     A  10                                15'NO. OF OBJECTS MOVED TO DPLIB'
     A                                    48
     A                                      '='
     A            P1CNT1         6S 0O    50
     A                                      EDTCDE(3)
      *
     A          R AUDIT02
     A                                      SPACEB(1)
     A                                    15'REMAINING OBJECTS IN'
     A            P1LBNM        10A  O    36
     A N11                                60'All 24x7 DZ objects are successful+
     A                                       ly housekept.'
     A  11                                60'MOVE failure(s). Please check jobl+
     A                                       og and rerun.'
     A                                    48
     A                                      '='
     A            P1CNT2         6S 0O    50
     A                                      EDTCDE(3)
     A                                      SPACEA(1)
      *
     A          R ENDREP
     A                                      SPACEB(2)
     A                                    49'*** END OF REPORT ***'
      *
     A          R NODETL
     A                                      SPACEB(2)
     A                                    47'*** NO DETAILS TO REPORT ***'
