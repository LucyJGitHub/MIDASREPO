/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas UT Recreate T_DLAAQD Records Based on STRANF')   *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/UT019950P1 - Recreate T_DLAAQD Records Based on         *
      *                    STRANF file                                *
      *                                                               *
      *  (c) Finastra International Limited 2013                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD019950 *CREATE   Date 10Apr13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD019950 - Transaction Number already exists encountered     *
      *             in SOAP                                           *
      *                                                               *
      *****************************************************************
      *
      ** Header
      *
     A          R PAGHDG                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2013')
      *
     A                                      SKIPB(1)
     A                                     1'REFERENCE'
     A                                    11'UT019950'
     A                                   101'Date'
     A            RUNA           7A      106
     A                                   119'PAGE'
     A                                   124PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                    33'RECREATED T_DLAAQD RECORDS +
     A                                       BASED ON STRANF FILE'
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                    32'-------------------------+
     A                                       ------------------------'
     A                                      SPACEA(2)
      *
      ** Detail Sub-Headings
      *
     A          R DETAIL
      *
     A                                      SPACEA(2)
     A                                     5'Transaction Number'
     A                                      HIGHLIGHT
     A                                    30'Front of Office ID'
     A                                      HIGHLIGHT
     A                                      SPACEA(1)
      *
      ** Detail Record Format
      *
     A          R DET2
      *
     A                                      SPACEA(1)
     A            R0TRNN         6A        5
     A            R0FRNT        20A       30
      *
      ** Report Trailer
      *
     A          R TOTAL
     A                                      SPACEB(1)
     A                                    20'Number of records +
     A                                      written ='
     A            R0CNTR         7  0     50EDTWRD('     0 ')
     A                                      SPACEA(2)
      *
     A          R END
     A                                      SPACEB(1)
     A N04                                54'*** END OF REPORT ***'
     A  04                                50'*** NO DETAILS TO REPORT +
     A                                      ***'
      *
      ** Database Error
      *
     A          R ERRORAU                   TEXT('DATABASE ERROR MSG')
     A                                      SPACEB(3)
     A                                    47'DATABASE ERROR'
     A            DBASE          3  0     62
     A                                    66'IN PROGRAM'
     A            DBPGM         10        77
     A                                    47'FILE'
     A                                      SPACEA(1)
     A            DBFILE         8        52
     A                                    62'KEY'
     A            DBKEY         29        66
