      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas LE Facility History Update/Audit Mode Report')   *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/UT0840P1 - Facility History Update/Audit Mode Report    *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. MD053826 *CREATE   Date 27Jul20               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD053826 - Facility amount in credit agreement is incorrect  *
      *             after back-valued transaction. Printer file to    *
      *             show the facility history records with incorrect  *
      *             revolving credit indicator.                       *
      *                                                               *
      *****************************************************************
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A          R HEADER1
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2020')
     A                                      SPACEA(001)
     A                                      SKIPB(001)
     A                                     6
     A                                      'REFERENCE UT0840P1'
     A            BJURPT        53A  O    40
     A                                   105
     A                                      'DATE'
     A            BJMRDT         7A  O   110
     A                                   121
     A                                      'PAGE'
     A                                    +2
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A N16                                 6
     A                                      'AUDIT MODE'
     A                                      SPACEB(001)
     A  16                                 6
     A                                      'UPDATE MODE'
     A                                      SPACEB(001)
     A N16                                52
     A                                      'RECORDS IN FACILITY HISTORY'
     A                                      SPACEB(001)
     A N16                                45
     A                                      'WITH INCORRECT REVOLVING CREDIT IN-
     A                                      DICATOR'
     A                                      SPACEB(001)
     A  16                                52
     A                                      'RECORDS IN FACILITY HISTORY'
     A  16                                45
     A                                      'WITH UPDATED REVOLVING CREDIT INDI-
     A                                      CATOR'
     A                                      SPACEB(001)
     A                                    39
     A                                      '-----------------------------------
     A                                      -------------------'
     A                                      SPACEB(001)
     A                                    25
     A                                      'BRANCH'
     A                                      SPACEB(002)
     A                                    33
     A                                      'CUSTOMER'
     A                                    43
     A                                      'FACILITY'
     A                                    54
     A                                      'DATE'
     A                                    62
     A                                      'SEQ'
     A                                    67
     A                                      'ACTION'
     A                                    80
     A                                      'REVOLVING CREDIT INDICATOR'
      *
      *----------------*
      ** Report Detail *
      *----------------*
      *
     A          R DETAIL1
     A                                      SPACEA(001)
     A            PBRCA          3A  O    27
     A            PCNUM          6A  O    34
     A            PFACT          3S 0O    44
     A                                    47
     A                                      '/'
     A            PFCNO          2S 0O    48
     A            PDATE          7A  O    53
     A            PSEQ           3S 0O    62
     A            PACTN          2A  O    69
     A            PRCINB         5A  O    90
     A            PRCIN          1A  O    92
      *
      *-----------------*
      ** Report Summary *
      *-----------------*
      *
     A          R TRAILER1
     A N16                                35
     A                                      'Number of records to be updated:'
     A                                      SPACEB(001)
     A  16                                35
     A                                      'Number of records updated:'
     A                                      SPACEB(001)
     A                                    81
     A                                      '='
     A            WRECER         6S 0O    84
     A                                      EDTCDE(3)
     A N12                                55
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(003)
     A  12                                52
     A                                      '***  NO DETAILS TO REPORT  ***'
     A                                      SPACEB(003)
      *
      *-----------------*
      ** Database Error *
      *-----------------*
      *
     A          R DBERROR1
     A                                      SKIPB(017)
     A                                    34
     A                                      '**** DATABASE ERROR ****'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*************************'
     A                                    45
     A                                      '*************************'
     A                                    70
     A                                      '*****'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*    FILE NAME'
     A            DBFILE         8        39
     A                                    73
     A                                      '*'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*    KEY'
     A            DBKEY         29        39
     A                                    73
     A                                      '*'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73
     A                                      '*'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*    AT'
     A            DBASE          3  0     39
     A                                    73
     A                                      '*'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*************************'
     A                                    45
     A                                      '*************************'
     A                                    70
     A                                      '*****'
     A                                      SKIPA(043)
     A                                    35
     A                                      '*** END OF REPORT ***'
