      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas LE Facility History Tagged for Rework Report')   *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/UT0840P2 - Facility History Tagged for Rework Report    *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. MD053826 *CREATE   Date 27Jul20               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD053826 - Facility amount in credit agreement is incorrect  *
      *             after back-valued transaction. Printer file to    *
      *             show the facilities tagged for rework calculation.*
      *                                                               *
      *****************************************************************
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A          R HEADER2
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2020')
     A                                      SPACEA(002)
     A                                      SKIPB(001)
     A                                     6
     A                                      'REFERENCE UT0840P2'
     A            BJURPT        53A  O    40
     A                                   105
     A                                      'DATE'
     A            BJMRDT         7A  O   110
     A                                   121
     A                                      'PAGE'
     A                                    +2
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A N16                                 6
     A                                      'AUDIT MODE'
     A                                      SPACEB(001)
     A  16                                 6
     A                                      'UPDATE MODE'
     A                                      SPACEB(001)
     A                                    45
     A                                      'FACILITIES THAT ARE TAGGED FOR REC-
     A                                      ALCULATION'
     A                                      SPACEB(001)
     A                                    38
     A                                      '-----------------------------------
     A                                      ------------------------'
     A                                      SPACEB(001)
     A                                    38
     A                                      'BRANCH'
     A                                      SPACEB(002)
     A                                    49
     A                                      'CUSTOMER'
     A                                    62
     A                                      'FACILITY'
     A                                    75
     A                                      'CCY'
     A                                    86
     A                                      'PROCESSED?'
      *
      *----------------*
      ** Report Detail *
      *----------------*
      *
     A          R DETAIL2
     A                                      SPACEA(001)
     A            PBRCA          3A  O    39
     A            PCNUM          6A  O    50
     A            PFACT          3S 0O    63
     A                                    66
     A                                      '/'
     A            PFCNO          2S 0O    67
     A            PCCY           3A  O    75
     A  16                                90
     A                                      'Y'
      *
     A N16                                90
     A                                      'N'
      *
      *-----------------*
      ** Report Summary *
      *-----------------*
      *
     A          R TRAILER2
     A N16                                38
     A                                      'Number of Facilities for Rework'
     A                                      SPACEB(003)
     A  16                                38
     A                                      'Number of Triggered Facilities'
     A                                      SPACEB(001)
     A                                    82
     A                                      '='
     A            FACERR         6S 0O    85
     A                                      EDTCDE(3)
     A N12                                55
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(003)
     A  12                                52
     A                                      '***  NO DETAILS TO REPORT  ***'
     A                                      SPACEB(003)
      *
      *-----------------*
      ** Database Error *
      *-----------------*
      *
     A          R DBERROR2
     A                                      SKIPB(017)
     A                                    34
     A                                      '**** DATABASE ERROR ****'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*************************'
     A                                    45
     A                                      '*************************'
     A                                    70
     A                                      '*****'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*    FILE NAME'
     A            DBFILE         8        39
     A                                    73
     A                                      '*'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*    KEY'
     A            DBKEY         29        39
     A                                    73
     A                                      '*'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73
     A                                      '*'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*    AT'
     A            DBASE          3  0     39
     A                                    73
     A                                      '*'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*************************'
     A                                    45
     A                                      '*************************'
     A                                    70
     A                                      '*****'
     A                                      SKIPA(043)
     A                                    35
     A                                      '*** END OF REPORT ***'
