      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas UT Programs/Error Messages List Report')         *
      *****************************************************************
      *                                                               *
      *  Midas - Utilities Module                                     *
      *                                                               *
      *  PRTF/UT1004P1 - Midas UT Programs/Error Messages List Report *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2011            *
      *                                                               *
      *  Last Amend No. CBF011  *CREATE    Date 04Jul11               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CBF011 - BF Infrastructure: Error Message Handling           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
     A          R HEADPY
      *
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2011')
      *
     A                                      SKIPB(003)
     A                                     6
     A                                      'REFERENCE UT1004P1'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            BJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(002)
      *
     A                                    46
     A                                      'Error Messages/Message Files List -
     A                                      Report'
     A                                      SPACEA(001)
     A                                    46
     A                                      '-----------------------------------
     A                                      ------'
     A                                      SPACEA(002)
      *
      *--------------------------------*
      ***  Column Header Record Format *
      *--------------------------------*
     A          R COLHDG                    SPACEA(001)
     A                                     6
     A                                      'Program Name'
      *
     A                                    30
     A                                      'Program Description'
      *
     A                                   104
     A                                      'Error Code'
      *
     A                                   118
     A                                      'Msg File'
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
     A          R DETAIL
      *
     A            UTPERS       125         6
     A N21                                  SPACEB(01)
     A                                      TEXT('PROGRAM')
     A                                      SPACEA(01)
      *-------------------*
      ***  Report Trailer *
      *-------------------*
     A          R TRAILPY
      *
     A                                    51
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(003)
      *
