      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas UT Error Messages/Message Files List Report')    *
      *****************************************************************
      *                                                               *
      *  Midas - Utilities Module                                     *
      *                                                               *
      *  PRTF/UT1004P2 - Error Messages/Message Files List Report     *
      *                                                               *
      *  (c) Finastra International Limited 2011                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CBF011  *CREATE    Date 04Jul11               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CBF011 - BF Infrastructure: Error Message Handling           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2011')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE UT1004P2'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'Error Messages/Message F+
     A                                      iles List Report'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    46'------------------------+
     A                                      ----------------'
     A                                      SPACEA(2)
      *
      *------------------------------------*
      ***  Error Code Detail Record Format *
      *------------------------------------*
      *
     A          R ERRDET
      *
     A                                      SPACEA(2)
     A            RERRD1       120         6TEXT('Msg ID + Desc + Msg File')
     A                                      SPACEA(1)
     A  20        RERRD2       120         6TEXT('Msg Desc Continuation')
      *
      *-----------------------------*
      ***  Call Stack Record Format *
      *-----------------------------*
      *
     A          R CSTACK
     A                                      SPACEA(1)
     A            RSTACK       120         6TEXT('Call Stack Program')
     A  21                                  SPACEA(2)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILER
      *
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
