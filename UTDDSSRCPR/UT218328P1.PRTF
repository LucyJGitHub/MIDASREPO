      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Printer file for UT218328')                            *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/UT218328P1 - Printer file for UT218328                  *
      *                                                               *
      *  (c) Copyright Misys Banking Systems, Ltd. 2010               *
      *                                                               *
      *  Last Amend No. AR218328 *CREATE   Date 24Mar10               *
      *  Prev Amend No. xxxxxx             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR218328 - Populate DLDTSCPD COHO if date is a holiday.      *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R UT218328H                 TEXT('(C) Copyright Misys +
     A                                      Banking Systems, Ltd. 2010')
      *
     A                                      SKIPB(3)
     A                                     5'REFERENCE UT218328P1'
     A            BJURPT    R             45 REFFLD(URPT)
     A                                   113'DATE'
     A            BJMRDT         7A  O    +2
     A                                      SPACEA(2)
     A N11                                 5'AUDIT'
     A  11                                 5'UPDATE'
     A                                    12'MODE'
     A            TITLE         53A  O    42
     A                                   113'PAGE'
     A                                    +2PAGNBR
     A                                      EDTCDE(Z)
      *
      ** Header part 2
      *
     A                                    48'SCHEDULES THAT ARE N'
     A                                      SPACEB(1)
     A                                    68'OT TAGGED AS HOLIDAYS'
      *
     A                                    48'----------------------'
     A                                      SPACEB(1)
     A                                    68'---------------------'
      *
     A                                    10'DEAL NUMBER'
     A                                      SPACEB(2)
     A                                    30'OUR/THEIR'
     A                                    48'RATE/PAY'
     A                                    65'DATE'
      *
     A                                    10'------------'
     A                                      SPACEB(2)
     A                                    30'---------'
     A                                    48'--------'
     A                                    65'-----'
      *
     A                                      SPACEA(2)
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R UT218328D
      *
     A                                      SPACEA(1)
     A            @DLNO          6  0     15
     A            @OTIN          1A       34
     A            @RCIP          1A       52
     A            WPRDT          7A       65
      *
      ***  Report Trailer
      *
     A          R UT218328T
      *
     A                                    40'TOTAL NO. OF DATES'
     A                                      SPACEB(2)
     A                                    59'THAT SHOULD BE HOLIDAY: '
     A            WTOT           6S  O    84
     A                                    56'*** END OF REPORT ***'
     A                                      SPACEB(2)
