/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Check/update invalid PRTO and NDEC')                   *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/UT9008P1 - Check/update invalid PRTO and NDEC.          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2012            *
      *                                                               *
      *  Last Amend No. AR317764 *CREATE   Date 22Aug12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR317764 - Unable to extract data from some fields of RUDWH  *
      *             library tables - Securities. Initialize PRTO and  *
      *             NDEC fields to zero. (Child: AR317765)            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R PAGHDG                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2012')
      *
     A                                      SKIPB(1)
     A                                     1'REFERENCE'
     A                                    11'UT09008'
     A                                    38'INITIALIZE INVESTMENT +
     A                                       TYPES PRICE TOLERANCE (%)'
     A                                   101'Date'
     A            RUNA           7A      106
     A                                   119'PAGE'
     A                                   124PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A N01                                 1'AUDIT MODE'
     A  01                                 1'UPDATE MODE'
     A                                    33'AND/OR BLANK NUMBER OF +
     A                                       DECIMAL PLACES FOR PRICE +
     A                                       ACCURACY'
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                    32'-------------------------+
     A                                       -------------------------+
     A                                       --------'
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A          R DETAIL
      *
     A                                      SPACEA(2)
     A                                     5'Investment Name'
     A                                      HIGHLIGHT
     A                                    26'Investment Type'
     A                                      HIGHLIGHT
     A                                    44'Investment Currency'
     A                                      HIGHLIGHT
     A                                    66'Processing Type'
     A                                      HIGHLIGHT
     A                                    84'Price Tolerance (%)'
     A                                   106'Decimal Places'
     A                                      HIGHLIGHT
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DET2
      *
     A                                      SPACEA(1)
     A  33        SINAM         20A        3
     A            SINVT          3A       32
     A            SCCYI          3A       51
     A            SPROT          1A       72
     A            SPRTO          7A       89
     A            SNDEC          1A      112
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TOTAL
     A                                      SPACEB(1)
     A N01                                20'Number of records +
     A                                      to be updated ='
     A N01        CTR            7  0     50EDTWRD('     0 ')
      *
     A  01                                20'Number of records +
     A                                      updated ='
     A  01        CHNGE          7  0     50EDTWRD('     0 ')
     A                                      SPACEA(2)
      *
     A          R END
     A                                      SPACEB(1)
     A N04                                54'*** END OF REPORT ***'
     A  04                                50'*** NO DETAILS TO REPORT +
     A                                      ***'
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R ERRORAU                   TEXT('DATABASE ERROR MSG')
     A                                      SPACEB(3)
     A                                    47'DATABASE ERROR'
     A            DBASE          3  0     62
     A                                    66'IN PROGRAM'
     A            DBPGM         10        77
     A                                    47'FILE'
     A                                      SPACEA(1)
     A            DBFILE         8        52
     A                                    62'KEY'
     A            DBKEY         29        66
