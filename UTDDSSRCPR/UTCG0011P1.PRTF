      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas CG Utility Printer File for CGCORRPD')           *
      *****************************************************************
      *                                                               *
      *  Midas - User Defined Correspondence                          *
      *                                                               *
      *  PRTF/UTCG0011P1 - Midas CG Utility Printer File for CGCORRPD *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. MD056543  *CREATE  Date 15Sep20               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD056543 - EDWH SSRS Reports cannot be run.                  *
      *           - Translate graphic fields of CGCORRPD to readable  *
      *             unicode equivalent.                               *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      *** Report Header Record Format  *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Finastra International Li-
     A                                      mited 2020')
      *
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A                                     6
     A                                      'REFERENCE UTCG0011P1'
     A            BJURPT        53A  O    37
     A                                   101
     A                                      'DATE'
     A            RUNA           7A      106
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
      *
     A N90                                 6
     A                                      'AUDIT'
     A                                      SPACEB(001)
     A  90                                 6
     A                                      'UPDATE'
     A                                      SPACEB(001)
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
      *
      *------------------------*
      ***  Detail Sub-Headings *
      *------------------------*
      *
     A N90                                36
     A                                      'Records in CGCORRPD that will be u-
     A                                      pdated by the Utility'
     A                                      SPACEB(001)
     A  90                                37
     A                                      'Records in CGCORRPD that were upda-
     A                                      ted by the Utility'
     A                                      SPACEB(001)
     A N90                                36
     A                                      '-----------------------------------
     A                                      ---------------------'
     A                                      SPACEB(001)
     A  90                                37
     A                                      '-----------------------------------
     A                                      ------------------'
     A                                      SPACEB(001)
     A                                    31
     A                                      'Correspondent'
     A                                      SPACEB(002)
     A                                    51
     A                                      'Customer Number'
     A                                    72
     A                                      'Correspondent Name'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAILP1
      *
     A                                      SPACEB(001)
     A            CDCORRP       10A  O    31
     A            CDCUSTP        6A  O    51
     A            CDCORNP       30A  O    72
      *
      *-------------------*
      *** No Details      *
      *-------------------*
      *
     A          R NODETP1
     A                                    50
     A                                      '*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(003)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TOTALP1
     A                                      SPACEB(003)
      *
     A  90                                29
     A                                      'No. of Records Updated in CGCORRPD-
     A                                      :'
     A N90                                23
     A                                      'No. of Records to be Updated in CG-
     A                                      CORRPD:'
     A            TOTAL1        10  0     65
     A                                      EDTWRD('        0 ')
     A                                    55
     A                                      '*** END OF REPORT ***'
     A                                      SPACEB(001)
      *
      *-------------------*
      *** Database Error  *
      *-------------------*
      *
     A          R DBERROR
     A                                      SPACEB(001)
     A                                    47
     A                                      'DATABASE ERROR'
     A            DBASE          3  0     62
     A                                    66
     A                                      'IN PROGRAM'
     A            DBPGM         10        77
     A                                    47
     A                                      'FILE'
     A                                      SPACEB(001)
     A            DBFILE         8        52
     A                                    62
     A                                      'KEY'
     A            DBKEY         29        66
      *
