      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas UT Check corrupted FSRRAT in historic-detail')   *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/UTDL0502P1 - Check corrupted FSRRAT field - detail      *
      *                                                               *
      *  (c) Finastra International Limited 2012                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR317892 *CREATE   Date 26Jul12               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR317892 - Utility that will check corrupted FSRRAT fields   *
      *             in DLBHISPD, DLCHISPD and DLDHISPD.               *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *-------------------------------*
      *** Report Header Record Format *
      *-------------------------------*
      *
     A          R UTHEADP1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2012')
      *
     A                                      SKIPB(3)
      *
     A                                     6'REFERENCE UTDL0502P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    40'DEALS WITH CORRUPTED +
     A                                      FSRRAT FIELD IN HISTORIC'
     A                                      SPACEA(1)
      *
     A                                    40'---------------------+
     A                                      ------------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                    28'Historic file'
     A                                    55'Deal Number'
     A                                    80'Original Entry Date'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R UTDETLP1
     A            RFILE          8A       30
     A            RDLNO          6S 0     57EDTWRD('0      ')
     A            RORED          7A       84
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1                   SPACEB(3)
     A                                    51'*** END OF REPORT ***'
