      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL Interest History Purge Utility Report')       *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  UTDL0503P1 - Interest History Purge Utility Report           *
      *                                                               *
      *  (c) Finastra International Limited 2018                      *
      *                                                               *
      *  Last Amend No. MD051650*CREATE    Date 12Sep18               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD051650 - Utility program to purge old records in HISTSAA   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited 2018')
      *
     A                                      SKIPB(2)
     A                                     6'REFERENCE UTDL0503P1'
     A                                      SPACEB(1)
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A            RMODE         11         6
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    51'PURGED INTEREST HISTORY RECORDS'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    50'--------------------+
     A                                      -------------'
     A                                      SPACEA(3)
     A                                    15'Record Type'
     A                                    30'Customer Number'
     A                                    48'Deal Number'
     A                                    63'Deal Type'
     A                                    75'Deal Subtype'
     A                                    91'Branch'
     A                                   101'Currency'
     A                                      SPACEA(1)
     A                                    15'-----------'
     A                                    30'---------------'
     A                                    48'-----------'
     A                                    63'---------'
     A                                    75'------------'
     A                                    91'------'
     A                                   101'--------'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
     A            RRTYP          2        19
     A            RCNUM          6        34
     A            RDLNO          6  0     50
     A            RDTYP          2        66
     A            RDLST          2        79
     A            RBRCA          3        92
     A            RCCY           3       103
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
