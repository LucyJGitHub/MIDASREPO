      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas  DL MD-53068 Data Patch Printer File')           *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/UTFDC844P1 -  DL MD-53068 Data Patch Printer File       *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. MD053068  *CREATE  Date 18Feb20               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD053068 - FX deal not deleted after deletion in Kondor      *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R UTDL0844H1                TEXT('(c) Finastra Internatio +
     A                                      nal Limited 2020')
     A                                      SKIPB(1)
     A                                     6'REFERENCE UTDL0844P1'
     A N10                                27'- UPDATE'
     A  10                                27'- AUDIT'
     A            BJURPT    R   53        38REFFLD(URPT)
     A                                   101'DATE:'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                    45'List of missing FX Deals in extens-
     A                                      ion file'
     A                                      SPACEB(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                    45'-----------------------------------
     A                                      --------'
     A                                      SPACEA(2)
      *
     A          R UTDL0844H2
     A                                    02'DEAL NUMBER'
     A                                    15'FRONT OFFICE ID'
     A                                    53'REMARKS'
     A                                      SPACEA(1)
      *
     A          R UTDL0844D1
     A            PDLNO          6A       02
     A            PFRNT         20A       15
     A            PREM          50A       53
     A                                      SPACEA(1)
      *
     A          R UTDL0844T
     A                                      SPACEB(1)
     A  10                                10'NUMBER OF RECORDS TO BE INSERTED'
     A N10                                10'NUMBER OF RECORDS WERE INSERTED'
     A                                    44
     A                                      '='
     A            PCTR           6S 0O    46
     A                                      EDTCDE(3)
     A                                      SPACEA(1)
     A          R UTDL0844E
     A                                      SPACEB(1)
     A                                    54'*** END OF REPORT ***'

