      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Report Incorrect DAVD and DASN')                       *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  UTDL51P1 - Printer file to report deal records with incorrect*
      *             DAVD and DASN fields.                             *
      *                                                               *
      *  (c) Finastra International Limited 2022                      *
      *                                                               *
      *  Last Amend No. 255626 *CREATE     Date 25Jul22               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  255626 - Applied fix 242527                                  *
      *  242527 - DL deal amendment is not shown on projected account *
      *           movements. Applied fix 213349.                      *
      *           Report correct values of deal amendments NAAD and   *
      *           NASN fields.                                        *
      *                                                               *
      *****************************************************************
     A          R HEADER
     A                                  1  5'REF: UTDL51P1'
     A        30                        1 20'(AUDIT MODE)'
     A        31                        1 20'(UPDATE MODE)'
     A            TITLE         50A  O  1 38
     A                                  1100'DATE'
     A            AGMRDT         7A  O    +2
     A                                  1120'PAGE'
     A            PAGE           4S  O    +2EDTCDE(Z)
      *
     A          R HEAD2
     A                                  5 34'DEALS WITH INCORRECT'
     A                                  5 55'DEAL AMENDMENT VALUE DATE'
     A                                  5 81'AND SEQ. NUMBER.'
     A                                  6 34'---------------------'
     A                                  6 55'-------------------------'
     A                                  6 80'-----------------'
      *
     A          R DETHED
     A                                  9 49'   DEAL AMENDMENT   '
     A                                  9 76'   DEAL AMENDMENT   '
     A                                 10 34'DEAL NO.'
     A                                 10 49'VALUE DATE    SEQ. #'
     A                                 10 76'VALUE DATE    SEQ. #'
     A                                 11 49'   (B E F O R E)    '
     A                                 11 76'    (A F T E R)     '
     A                                 12 34'--------'
     A                                 12 49'--------------------'
     A                                 12 76'--------------------'
      *
     A          R DETINF
     A                                      SPACEB(001)
     A            DLNO           6S  O    35
     A            DAVDF          7   O    51
     A            DASNF          3  0O    65
     A            DAVDF2         7   O    78
     A            DMDASN         3  0O    92
      *
     A          R TRAIL
     A                                      SPACEB(003)
     A        30                          45'NO. OF RECORDS TO BE'
     A        31                          51'NO. OF RECORDS'
     A                                    66'CORRECTED :'
     A            CNTR           5  0O    81EDTCDE(3)
     A                                      SPACEA(001)
     A                                    45'TOTAL NO. OF RECORDS'
     A                                    66'PROCESSED :'
     A            TOTCTR         5  0O    81EDTCDE(3)
     A                                      SPACEA(002)
     A                                    56'*** END OF REPORT ***'
