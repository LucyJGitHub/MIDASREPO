      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas  SD MD-54408 Data Patch Printer File')           *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/UTFC0841P1 -  SD MD-54408 Data Patch Printer File       *
      *                                                               *
      *  (c) Finastra International Limited 2019                      *
      *                                                               *
      *  Last Amend No. MD054408  *CREATE  Date 05OCt19               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD054408 - TAIC013 - Periodic Report starts generating Daily *
      *                  if previous month does not have 31 days      *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R UTFC0841H1                TEXT('(c) Finastra Internatio +
     A                                      nal Limited 2019')
     A                                      SKIPB(1)
     A                                     6'REFERENCE UTFC0841P1'
     A N10                                27'- UPDATE'
     A  10                                27'- AUDIT'
     A            BJURPT    R   53        38REFFLD(URPT)
     A                                   101'DATE:'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                    25'Next Active Date Update Utility'
     A                                      SPACEB(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                    25'-------------------------------'
     A                                      SPACEA(2)
      *
     A          R UTFC0841H2
     A                                    02'REPORT'
     A                                    12'SEQUENCE'
     A                                    22'--------NEXT ACTIVE DATE-------'
     A                                      SPACEA(1)
     A                                    02'NAME'
     A                                    12'NUMBER'
     A                                    22'CALCULATED'
     A  10                                37'CURRENT'
     A N10                                37'PREVIOUS'
     A                                    53'REMARKS'
     A                                      SPACEA(1)
      *
     A          R UTFC0841D1
     A                                      SPACEB(1)
     A            PRNAM         10A       02
     A            PPSQN          2        12
     A            PPNDT_C        8A       22
     A            PPNDT_P        8A       37
     A            PREM          50A       53
     A                                      SPACEA(1)
     A            PPNDT_CN       5S 0     22
     A            PPNDT_PN       5S 0     37
      *
     A          R UTFC0841D2
     A                                      SPACEB(1)
     A            PREM2         50A       21
     A*                                     SPACEA(1)
      *
     A          R UTFC0841T
     A                                      SPACEB(1)
     A  10                                10'NUMBER OF RECORDS TO BE UPDATED'
     A N10                                10'NUMBER OF RECORDS WERE UPDATED'
     A                                    42
     A                                      '='
     A            PCTR           6S 0O    45
     A                                      EDTCDE(3)
     A                                      SPACEA(1)
     A          R UTFC0841E
     A                                      SPACEB(1)
     A                                    54'*** END OF REPORT ***'

