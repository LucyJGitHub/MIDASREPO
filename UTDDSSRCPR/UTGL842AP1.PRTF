      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Reporting/update utility for MD-54125')                *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/UTGL842AP1 - Printer File for UTGL0842A                 *
      *                                                               *
      *  (c) Finastra International Limited 2019                      *
      *                                                               *
      *  Last Amend No. MD054125 *Create   Date 10Dec19               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD054125 - Datapatch to blank out Associated Customer (ASOC) *
      *             if value of ASOC = '000000'                       *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *****************************************************************
     A          R UTPAGHDR
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2019')
     A                                      SKIPB(003)
     A                                    40
     A                                      '(c) Finastra International -
     A                                      Limited 2019'
     A          R UTTITLE
     A                                     2
     A                                      'Reference:'
     A                                      SPACEB(001)
     A                                    12'UTGL842AP1'
     A                                      TEXT('*PROGRAM')
     A N30 40                             27'APOSTPD POSTINGS with -
     A                                      ASSOCIATED CUSTOMER = 000000 -
     A                                      (AUDIT MODE)'
     A N30N40                             27'GLACPHPD POSTINGS with -
     A                                      ASSOCIATED CUSTOMER = 000000 -
     A                                      (AUDIT MODE)'
     A  30 40                             27'APOSTPD POSTINGS with -
     A                                      ASSOCIATED CUSTOMER = 000000 -
     A                                      (UPDATE MODE)'
     A  30N40                             27'GLACPHPD POSTINGS with -
     A                                      ASSOCIATED CUSTOMER = 000000 -
     A                                      (UPDATE MODE)'
     A                                    95
     A                                      'Date'
     A            UTMRDT         7       100
     A                                      TEXT('Midas Run Date')
     A                                   110
     A                                      'Page'
     A                                   116
     A                                      PAGNBR
     A                                      EDTCDE(3)
     A                                      SPACEA(001)
     A                                     5
     A                                      'Account Number'
     A                                      SPACEB(001)
     A                                    37
     A                                      'Posting Date'
     A                                    58
     A                                      'Posting Amount'
     A                                    74
     A                                      'Dr/Cr Indicator'
     A N31                                92
     A                                      'Associated Customer'
     A  31                                92
     A                                      'Associated Customer Cleared?'
      *****************************************************************
     A          R UTDTLRCD
     A                                      TEXT('Detail line.')
     A                                      SPACEB(001)
     A            UTBRCA         3A        5
     A                                      TEXT('Branch')
     A                                     8
     A                                      '-'
     A            UTCNUM         6A        9
     A                                      TEXT('Customer Number')
     A                                    15
     A                                      '-'
     A            UTCCY          3A       16
     A                                      TEXT('Currency')
     A                                    19
     A                                      '-'
     A            UTACOD        10  0     20
     A                                      TEXT('Account Code')
     A                                    30
     A                                      '-'
     A            UTACSQ         2  0     31
     A                                      TEXT('Account Sequence')
     A            UTPSTD         7A       40
     A                                      TEXT('Posting Date')
     A            UTPSTA        13  0     55EDTWRD('  ,   ,   , 0 .  ')
     A                                      TEXT('Posting Amount')
     A            UTDRCR         1  0     81
     A                                      TEXT('DR/CR Indicator')
     A N31        UTASOC         6A       98
     A                                      TEXT('Associated Customer')
     A  31        UTCLRD         1A      105
     A                                      TEXT('Associated Customer Cleared')
      *****************************************************************
     A          R UTFINTTL
      *****************************************************************
     A                                      TEXT('Total No. of affected -
     A                                      accounts')
     A                                      SPACEB(001)
      *****************************************************************
     A  67                                43
     A                                      '*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(001)
     A N67N30                              2
     A                                      'Total Number of Postings whose -
     A                                      Associated Customer is 000000:'
     A                                      SPACEB(002)
     A N67 30                              2
     A                                      'Total Number of Postings with -
     A                                      Associated Customer blank out:'
     A                                      SPACEB(002)
     A N67        UTCOUNT        6  0     63
     A                                      TEXT('Total No. of affected -
                                            postings')
     A                                      EDTCDE(4)
      *****************************************************************
     A          R UTENDRPT
      *****************************************************************
     A                                      TEXT('End of Report.           ')
     A                                      SPACEB(002)
      *****************************************************************
     A                                    45
     A                                      '*** END OF REPORT ***'
      *****************************************************************
