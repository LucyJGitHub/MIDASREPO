      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Reporting/update utility for MD-54125')                *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/UTGL0842P1 - Printer File for UTGL0842B                 *
      *                                                               *
      *  (c) Finastra International Limited 2019                      *
      *                                                               *
      *  Last Amend No. MD054125 *Create   Date 10Dec19               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD054125 - Datapatch to blank out Associated Customer (ASOC) *
      *             if value of ASOC = '000000'                       *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *****************************************************************
     A          R UTPAGHDR
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2019')
     A                                      SKIPB(003)
     A                                    40
     A                                      '(c) Finastra International -
     A                                      Limited 2019'
     A          R UTTITLE
     A                                     2
     A                                      'Reference:'
     A                                      SPACEB(001)
     A                                    12'UTGL842BP1'
     A                                      TEXT('*PROGRAM')
     A N30                                40'MC Historic Balances - GLHIBLPD -
     A                                      (AUDIT)'
     A  30                                40'MC Historic Balances - GLHIBLPD -
     A                                      (UPDATE)'
     A                                    95
     A                                      'Date'
     A            UTMRDT         7       100
     A                                      TEXT('Midas Run Date')
     A                                   110
     A                                      'Page'
     A                                   116
     A                                      PAGNBR
     A                                      EDTCDE(3)
     A                                      SPACEA(001)
     A                                    47
     A                                      'Control Group Code:'
     A            UTCGCD         2A       67
     A                                      TEXT('Control Group Code')
     A                                      SPACEA(001)
     A                                     4
     A                                      'Account Number'
     A                                      SPACEB(001)
     A                                    34
     A                                      'Profit'
     A                                    46
     A                                      'Associated'
     A                                    66
     A                                      'Period'
     A N30                                90
     A                                      'Action Needed'
     A  30                                90
     A                                      'Action Taken'
     A                                    34
     A                                      'Center'
     A                                      SPACEB(001)
     A                                    43
     A                                      'Customer Number'
     A                                    61
     A                                      '(No.in Year/Year)'
     A                                    85
     A                                      '(U)pdate/(D)elete Record?'
      *****************************************************************
     A          R UTDTLRCD
     A                                      TEXT('Detail line.')
     A                                      SPACEB(001)
     A            UTBRCA         3A        4
     A                                      TEXT('Branch')
     A                                     7
     A                                      '-'
     A            UTCNUM         6A        8
     A                                      TEXT('Customer Number')
     A                                    14
     A                                      '-'
     A            UTCCY          3A       15
     A                                      TEXT('Currency')
     A                                    18
     A                                      '-'
     A            UTACOD        10A       19
     A                                      TEXT('Account Code')
     A                                    29
     A                                      '-'
     A            UTACSQ         2A       30
     A                                      TEXT('Account Sequence')
     A            UTPCCD         4A       35
     A                                      TEXT('Profit Center Code')
     A            UTACNB         6A       48
     A                                      TEXT('Associated Customer Number')
     A            UTPDNY         2A       65
     A                                      TEXT('Period Number in Year')
     A                                    67
     A                                      '/'
     A            UTPDCT         2A       68
     A                                      TEXT('Period Century')
     A            UTPDYR         2A       70
     A                                      TEXT('Period Year')
     A            UTACTN         1A       95
     A                                      TEXT('U-Update/D-Delete')
      *****************************************************************
     A          R UTFINTTL
      *****************************************************************
     A                                      TEXT('Total No. of affected -
     A                                      accounts')
     A                                      SPACEB(001)
      *****************************************************************
     A  67                                43
     A                                      '*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(001)
     A N67N30                              2
     A                                      'Total Number of Postings whose -
     A                                      Associated Customer is 000000:'
     A                                      SPACEB(002)
     A N67 30                              2
     A                                      'Total Number of Postings with -
     A                                      Associated Customer blank out:'
     A                                      SPACEB(002)
     A N67        UTCOUNT        6  0     63
     A                                      TEXT('Total No. of affected -
                                            postings')
     A                                      EDTCDE(4)
      *****************************************************************
     A          R UTENDRPT
      *****************************************************************
     A                                      TEXT('End of Report.           ')
     A                                      SPACEB(002)
      *****************************************************************
     A                                    45
     A                                      '*** END OF REPORT ***'
      *****************************************************************
