      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Reporting/update utility for MD-54125')                *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/UTGL0842P1 - Printer File for UTGL0842B                 *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. MD054125A *Create  Date 17Aug20               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD054125A - Reports account with balance discrepancy between *
      *              GLHIBLPD (Period Closing Balance) and            *
      *              ACCNTAB (Cleared Balance/Ledger Balance).        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *****************************************************************
     A          R UTPAGHDR
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2020')
     A                                      SKIPB(003)
     A                                    40
     A                                      '(c) Finastra International -
     A                                      Limited 2020'
     A          R UTTITLE
     A                                     2
     A                                      'Reference:'
     A                                      SPACEB(001)
     A                                    12'UTGL842CP1'
     A                                      TEXT('*PROGRAM')
     A                                    40'Account(s) with Balance -
     A                                      Discrepancy'
     A                                    95
     A                                      'Date'
     A            UTMRDT         7       100
     A                                      TEXT('Midas Run Date')
     A                                   110
     A                                      'Page'
     A                                   116
     A                                      PAGNBR
     A                                      EDTCDE(3)
     A                                      SPACEA(001)
     A                                    47
     A                                      'Control Group Code:'
     A            UTCGCD         2A       67
     A                                      TEXT('Control Group Code')
     A                                      SPACEA(001)
     A                                     4
     A                                      'Account Number'
     A                                      SPACEB(001)
     A N50                                40
     A                                      'Cleared Balance'
     A  50                                40
     A                                      'Ledger Balance'
     A                                    70
     A                                      'Period Closing Balance'
     A                                    40
     A                                      '(ACCNTAB)'
     A                                      SPACEB(001)
     A                                    70
     A                                      '(GLHIBLPD)'
      *****************************************************************
     A          R UTDTLRCD
     A                                      TEXT('Detail line.')
     A                                      SPACEB(001)
     A            UTBRCA         3A        4
     A                                      TEXT('Branch')
     A                                     7
     A                                      '-'
     A            UTCNUM         6A        8
     A                                      TEXT('Customer Number')
     A                                    14
     A                                      '-'
     A            UTCCY          3A       15
     A                                      TEXT('Currency')
     A                                    18
     A                                      '-'
     A            UTACOD        10A       19
     A                                      TEXT('Account Code')
     A                                    29
     A                                      '-'
     A            UTACSQ         2A       30
     A                                      TEXT('Account Sequence')
     A            UTACBL        15  0     40EDTWRD('            0.  ')
     A                                      TEXT('Cleared Balance')
     A            UTPDCB        19  0     70EDTWRD('                0.  ')
     A                                      TEXT('Closing Period Balance')
      *****************************************************************
     A          R UTFINTTL
      *****************************************************************
     A                                      TEXT('Total No. of affected -
     A                                      accounts')
     A                                      SPACEB(001)
      *****************************************************************
     A  67                                43
     A                                      '*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(001)
     A N67 30                              2
     A                                      'Account(s) with Balance -
     A                                      Discrepancy in ACCNTAB and GLHIBLPD'
     A                                      SPACEB(002)
     A N67        UTCOUNT        6  0     63
     A                                      TEXT('Total No. of affected -
                                            accounts')
     A                                      EDTCDE(4)
      *****************************************************************
     A          R UTENDRPT
      *****************************************************************
     A                                      TEXT('End of Report.           ')
     A                                      SPACEB(002)
      *****************************************************************
     A                                    45
     A                                      '*** END OF REPORT ***'
      *****************************************************************
