      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Fee Rate Change Amendment Audit report - PRTF')        *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/UTL441P1  - Printer file for UTLE0441.                  *
      *                                                               *
      *  (c) Finastra International Limited 2022                      *
      *                                                               *
      *  Last Amend No. 263582  *CREATE    Date 29Jul22               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  263582 - Wrong 'Total Accrued to Date' in Fee Detail Enq.    *
      *           Correct the NoOfDays in computations and update     *
      *           value of %indicator field in LEFEED and LEFHST.     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R UTLE441H1                 TEXT('(c) Finastra International +
     A                                      Limited 2022')
      *
     A                                      SKIPB(3)
     A N09                                 2'UTLE0441 - AUDIT'
     A  09                                 2'UTLE0441 - UPDATE'
     A            BJURPT    R             27REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
      ** Main Heading
      *
     A            RHEAD         45        40
     A                                      SPACEA(1)
     A                                    40'----------------------------'
     A                                      SPACEA(2)
      *
      ** Sub Heading
      *
     A                                    05'Calculated Fees w/ blank +
     A                                       %INDICATOR'
     A                                    50'Corresponding Fee Rate Change +
     A                                       history w/ blank %INDICATOR'
     A                                       SPACEA(1)
     A                                    02'==============================+
     A                                       ===========|==================='
     A                                    63'==============================+
     A                                       ==============================+
     A                                       ========='
     A                                       SPACEA(1)
      *
     A                                    02'Facility'
     A                                    18'Seq'
     A                                    23'Cod'
     A                                    28'Ccy'
     A                                    33'Cal'
     A                                    38'%IND'
     A                                    43'|'
     A                                    46'Rate1 '
     A                                    56'Rate2 '
     A                                    66'Rate3 '
     A                                    76'Rate4 '
     A                                    86'Rate5 '
     A                                    96'Chg date'
     A                                   106'Chg type'
     A                                   116'Chg %IND'
     A                                       SPACEA(1)
      *
     A                                    02'==============================+
     A                                       ===========|==================='
     A                                    63'==============================+
     A                                       ==============================+
     A                                       ========='
     A                                       SPACEA(1)
      *
      ** Details
      *
     A          R UTLE441D1                  SPACEA(1)
      *
      ** Details from fee master file
      *
     A            RCUST          6        02
     A  20        RFATP          3  0     10
     A  20                                13'/'
     A  20        RFCNO          2  0     14
     A            RSEQ           2        19
     A            RCOD           2        23
     A            RCCY           3        28
     A            RCALT          2        34
     A            RPIND          1        40
     A                                    43'|'
      *
      ** Details from fee history file
      *
     A  21        RRTE1          8        46
     A  21        RRTE2          8        56
     A  21        RRTE3          8        66
     A  21        RRTE4          8        76
     A  21        RRTE5          8        86
     A  21        RRCVD          7        96
     A  21        RRCTP          2       110
     A  21        RRPIN          2       119
      *
      ** End of the Report
      *
     A          R UTLE441T1                 SPACEB(2)
     A                                    56'*** END OF REPORT ***'
      *
     A          R UTLE441T2                 SPACEB(2)
     A                                    48'Fee record(s) for update:'
     A            RTOTAL         5  0     75
     A                                    82'***'
