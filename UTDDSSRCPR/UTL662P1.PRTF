      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas UT Printer File for UTLE0662')                   *
      *****************************************************************
      *                                                               *
      *  Midas - Printer file for program UTLE0662                    *
      *                                                               *
      *  PRTF/UTL662P1 - Datapatch Printer File Program               *
      *                                                               *
      *  (c) Finastra International Limited 2022                      *
      *                                                               *
      *  Last Amend No. AR1019909 *CREATE  Date 15Jul22               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR1019909 - This printer file contains loans with incorrect  *
      *              YILN, YPLN and YPLC in PF/CLOANCK and PF/MCLONCK.*
      *              (Child: AR1019910)                               *
      *                                                               *
      *****************************************************************
      *
      ** Report Header Record Format
      *
     A          R HEADER1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited 2022')
     A                                      SPACEA(001)
     A                                      SKIPB(001)
     A                                     6
     A                                      'REFERENCE UTL662P1'
     A            BJURPT        53A  O    37
     A                                   101
     A                                      'DATE'
     A            BJMRDT         7A  O   106
     A                                   123
     A                                      'PAGE'
     A                                   125
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A N01                                 6
     A                                      'AUDIT MODE'
     A                                      SPACEB(001)
     A  01                                 6
     A                                      'UPDATE MODE'
     A                                      SPACEB(001)
     A                                    38
     A                                      'LOANS from PF/CLOANCK with incorre-
     A                                      ct YILN, YPLN and YPLC'
     A                                      SPACEB(002)
     A                                    36
     A                                      '-----------------------------------
     A                                      ----------------------------'
     A                                      SPACEB(001)
     A          R HEADER2
     A                                      TEXT('(c) Finastra International +
     A                                      Limited 2022')
     A                                      SPACEA(001)
     A                                      SKIPB(001)
     A                                     6
     A                                      'REFERENCE UTL662P1'
     A            BJURPT        53A  O    37
     A                                   101
     A                                      'DATE'
     A            BJMRDT         7A  O   106
     A                                   123
     A                                      'PAGE'
     A                                   125
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A N01                                 6
     A                                      'AUDIT MODE'
     A                                      SPACEB(001)
     A  01                                 6
     A                                      'UPDATE MODE'
     A                                      SPACEB(001)
     A                                    38
     A                                      'LOANS from PF/MCLONCK with incorre-
     A                                      ct YILN, YPLN and YPLC'
     A                                      SPACEB(002)
     A                                    36
     A                                      '-----------------------------------
     A                                      ----------------------------'
     A                                      SPACEB(001)
      *
      ** Report Detail
      *
     A          R DETAIL1
     A                                    10
     A                                      'Record ID:'
     A                                      SPACEB(002)
     A            WRECI          1A  O    25
     A                                   +60
     A                                      'BEFORE'
     A                                    +7
     A                                      '/'
     A                                    +6
     A                                      'AFTER'
     A                                    10
     A                                      'Loan Number:'
     A                                      SPACEB(001)
     A            WLNRF          6A  O    25
     A                                    50
     A                                      'Interest accrued ytd (YILN):'
     A  20        WYILN0        13S 0O    80
     A                                      EDTWRD(' ,   ,   ,   , 0 -')
     A  21        WYILN1        13S 1O    80
     A                                      EDTWRD('   ,   ,   , 0 . -')
     A  22        WYILN2        13S 2O    80
     A                                      EDTWRD('  ,   ,   , 0 .  -')
     A  23        WYILN3        13S 3O    80
     A                                      EDTWRD(' ,   ,   , 0 .   -')
     A                                    99
     A                                      '/'
     A  20        AYILN0        13S 0O   101
     A                                      EDTWRD(' ,   ,   ,   , 0 -')
     A  21        AYILN1        13S 1O   101
     A                                      EDTWRD('   ,   ,   , 0 . -')
     A  22        AYILN2        13S 2O   101
     A                                      EDTWRD('  ,   ,   , 0 .  -')
     A  23        AYILN3        13S 3O   101
     A                                      EDTWRD(' ,   ,   , 0 .   -')
     A  11 01                            120'***'
     A                                    10
     A                                      'Value Date:'
     A                                      SPACEB(001)
     A            WVDAT          7A  O    25
     A                                    44
     A                                      'Ytd interest pd - loan ccy (YPLN):'
     A  20        WYPLN0        13S 0O    80
     A                                      EDTWRD(' ,   ,   ,   , 0 -')
     A  21        WYPLN1        13S 1O    80
     A                                      EDTWRD('   ,   ,   , 0 . -')
     A  22        WYPLN2        13S 2O    80
     A                                      EDTWRD('  ,   ,   , 0 .  -')
     A  23        WYPLN3        13S 3O    80
     A                                      EDTWRD(' ,   ,   , 0 .   -')
     A                                    99
     A                                      '/'
     A  20        AYPLN0        13S 0O   101
     A                                      EDTWRD(' ,   ,   ,   , 0 -')
     A  21        AYPLN1        13S 1O   101
     A                                      EDTWRD('   ,   ,   , 0 . -')
     A  22        AYPLN2        13S 2O   101
     A                                      EDTWRD('  ,   ,   , 0 .  -')
     A  23        AYPLN3        13S 3O   101
     A                                      EDTWRD(' ,   ,   , 0 .   -')
     A  12 01                            120'***'
     A                                    10
     A                                      'Maturity Date:'
     A                                      SPACEB(001)
     A            WMDAT          7A  O    25
     A                                    42
     A                                      'Yr to date int.pd.-local ccy (YPLC-
     A                                      ):'
     A  30        WYPLC0        13S 0O    80
     A                                      EDTWRD(' ,   ,   ,   , 0 -')
     A  31        WYPLC1        13S 1O    80
     A                                      EDTWRD('   ,   ,   , 0 . -')
     A  32        WYPLC2        13S 2O    80
     A                                      EDTWRD('  ,   ,   , 0 .  -')
     A  33        WYPLC3        13S 3O    80
     A                                      EDTWRD(' ,   ,   , 0 .   -')
     A                                    99
     A                                      '/'
     A  30        AYPLC0        13S 0O   101
     A                                      EDTWRD(' ,   ,   ,   , 0 -')
     A  31        AYPLC1        13S 1O   101
     A                                      EDTWRD('   ,   ,   , 0 . -')
     A  32        AYPLC2        13S 2O   101
     A                                      EDTWRD('  ,   ,   , 0 .  -')
     A  33        AYPLC3        13S 3O   101
     A                                      EDTWRD(' ,   ,   , 0 .   -')
     A  13 01                            120'***'
     A                                    10
     A                                      'Currency:'
     A                                      SPACEB(001)
     A            WCCY           3A  O    25
     A                                    36
     A                                      '-----------------------------------
     A                                      ----------------------------'
     A                                      SPACEB(002)
     A                                      SPACEA(002)
      *
      ** Report Summary
      *
     A          R TRAILER1
     A                                    47
     A                                      SPACEB(003)
     A                                      'Number of loans with error :'
     A            RLON           5S 0O    +8
     A                                      EDTCDE(3)
     A N02                                51
     A                                      '   ***  END OF REPORT  ***'
     A                                      SPACEB(008)
     A  02                                51
     A                                      '***  NO DETAILS TO REPORT***'
     A                                      SPACEB(003)
      *
      ** Database Error
      *
     A          R DBERROR1
     A                                      SKIPB(017)
     A                                    34
     A                                      '**** DATABASE ERROR ****'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*************************'
     A                                    45
     A                                      '*************************'
     A                                    70
     A                                      '*****'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*    FILE NAME'
     A            DBFILE         8        39
     A                                    73
     A                                      '*'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*    KEY'
     A            DBKEY         29        39
     A                                    73
     A                                      '*'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73
     A                                      '*'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*    AT'
     A            DBASE          3  0     39
     A                                    73
     A                                      '*'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*************************'
     A                                    45
     A                                      '*************************'
     A                                    70
     A                                      '*****'
     A                                      SKIPA(043)
     A                                    35
     A                                      '*** END OF REPORT ***'
