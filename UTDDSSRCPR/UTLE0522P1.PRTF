      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Loans with incorrect CPAM in History file')            *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/UTLE0522P1 - Display loan w/ Incorrect Principal value  *
      *                    Used by UTLE0522 utility for MD037545.     *
      *                                                               *
      *  (C) Misys Banking Systems Ltd. 2016                          *
      *                                                               *
      *  Last Amend No. AR545781 *CREATE   Date 24Aug16               *
      *  Prev Amend No. Xnnnnnnn           Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR545781 - Missing/Multiple IH on history file.              *
      *             Provide datapatch utility to correct principal    *
      *             and interest values on history file.              *
      *           - Applied utility to MD-37545                       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER1                   TEXT('(C) Misys +
     A                                       Banking Systems +
     A                                      2016')
      *
     A                                      SKIPB(3)
     A                                     3'REFERENCE UTLE0522P1'
     A            MODNAM        11A       26
     A            BJURPT        53A       50
     A                                   105'DATE'
     A            BJMRDT         7A      110
     A                                   120'PAGE'
     A                                   125PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   105'TIME'
     A                                   110TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    40'RECALCULATION OF LOAN DUE +
     A                                      TO DUPLICATE/MISSING IH RECORD'
     A                                      SPACEA(1)
     A                                    04'=========================+
     A                                      ==========================+
     A                                      ==========================+
     A                                      ==========================+
     A                                      =========================='
     A                                      SPACEA(2)
     A                                    04'Loan '
     A                                    15'Id'
     A                                    25'Ccy'
     A                                    35'Master File - CLOANCL'
     A                                    67'History File - HISTSHP/M'
     A                                    99'Header File - HISTSHN'
     A                                      SPACEA(1)
     A                                    04'-------------------------+
     A                                      --------------------------+
     A                                      --------------------------+
     A                                      --------------------------+
     A                                      --------------------------'
     A                                      SPACEA(2)
      *
      ** Details of Loan Records - Body
      *
     A          R DETAIL1
     A                                      SPACEA(2)
     A            LNRF           6A  O    04
     A            WRECI          1A  O    15
     A            CCY            3A  O    25
     A  70        LCPAM0        13S 0O    35EDTWRD(' ,   ,   ,   , 0 -')
     A  71        LCPAM1        13S 1O    35EDTWRD('   ,   ,   , 0 . -')
     A  72        LCPAM2        13S 2O    35EDTWRD('  ,   ,   , 0 .  -')
     A  73        LCPAM3        13S 3O    35EDTWRD(' ,   ,   , 0 .   -')
     A  70        HCPAM0        13S 0O    63EDTWRD(' ,   ,   ,   , 0 -')
     A  71        HCPAM1        13S 1O    63EDTWRD('   ,   ,   , 0 . -')
     A  72        HCPAM2        13S 2O    63EDTWRD('  ,   ,   , 0 .  -')
     A  73        HCPAM3        13S 3O    63EDTWRD(' ,   ,   , 0 .   -')
     A  70        HIPRM0        13S 0O    93EDTWRD(' ,   ,   ,   , 0 -')
     A  71        HIPRM1        13S 1O    93EDTWRD('   ,   ,   , 0 . -')
     A  72        HIPRM2        13S 2O    93EDTWRD('  ,   ,   , 0 .  -')
     A  73        HIPRM3        13S 3O    93EDTWRD(' ,   ,   , 0 .   -')
     A                                      SPACEA(2)
      *
     A          R NODTLS1
     A                                    54'*** No Details to Report +
     A                                      ***'
     A                                      SPACEB(3)
     A          R TRAILER1
     A N11                                37'NO. OF LOANS RECORDS TO +
     A                                       BE UPDATED  :'
     A N11        LONCTR         5S  O    75EDTCDE(L)
     A                                      SPACEA(2)
     A  11                                37'NO. OF LOANS RECORDS U+
     A                                       PDATED   :'
     A  11        LONUPD         5S  O    75EDTCDE(L)
     A                                      SPACEA(2)
     A                                    57'***END OF REPORT***'
