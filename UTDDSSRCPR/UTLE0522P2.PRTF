      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Display loan with duplicate/missing IH')               *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/UTLE0522P2 - Display loan w/ duplicate Interest Header  *
      *                    Used by UTLE0522 utility for MD037545.     *
      *                                                               *
      *  (C) Finastra International Limited 2016                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR545781 *CREATE   Date 24Aug16               *
      *                 XNNNNNNN           Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR545781 - Missing/Multiple IH on history file.              *
      *             Provide datapatch utility to correct principal    *
      *             and interest values on history file.              *
      *           - Applied utility to MD-37545                       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER2
     A                                      TEXT('(c) Finastra International Limited -
     A                                      . 2016')
      *
     A                                      SKIPB(003)
     A                                      SPACEA(002)
     A                                     2
     A                                      'REFERENCE UTLE0522P2'
     A            BJURPT        53   O    40
     A                                   117
     A                                      'DATE'
     A            BJMRDT    R            122
     A                                      REFFLD(MRDT)
     A  11                                 2
     A                                      'UPDATE MODE'
     A                                      SPACEB(001)
     A N11                                 2
     A                                      'AUDIT MODE'
     A                                      SPACEB(001)
     A                                   117
     A                                      'PAGE'
     A                                   125
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                    28
     A                                      'Datapatch program to DELETE or INS-
     A                                      ERT Interest Header in LE Histo-
     A                                      ry Files'
     A                                      SPACEB(001)
     A                                     4
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ==============================='
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     4
     A                                      'Loan'
     A                                    12
     A                                      'Value Date'
     A                                    25
     A                                      'Ccy'
     A                                    32
     A                                      'Type'
     A                                    39
     A                                      'Record'
     A                                    51
     A                                      'Principal Amount'
     A                                    75
     A                                      'Interest Amount'
     A                                    97
     A                                      'Header Status'
     A                                      SPACEA(001)
     A                                     4'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------------'
     A                                      SPACEB(001)
      *
      *--------------------------------*
      ** Report Detail Record Format   *
      *--------------------------------*
      *
     A          R DETAIL2
     A                                      SPACEA(001)
      *
     A            WLNRF          6A  O     4
     A            EDATE          7   O    14
     A            HNCCY          3A  O    25
     A            HNPTYP         2S  O    33
     A            HNAMTP         2A  O    40
     A            PRAMOL        20A  O    50
     A            INTAH         20A  O    73
      *
     A  31                                97'BackValue IH  (delete)'
     A  32                                97'Duplicate IH  (delete)'
     A  33                                97'Missing - IH  (insert)'
     A N31N32N33                          97'                      '
      *
      *--------------------------------*
      ** Report Footer Record Format   *
      *--------------------------------*
      *
     A          R TRAILER2
     A                                      SPACEB(002)
     A N11                                20
     A                                      'Number of duplicates to be Deleted'
     A  11                                20
     A                                      'Number of Deleted duplicates '
     A                                    57
     A                                      ':'
     A            TOTALD         6  0O    60
     A                                      EDTCDE(2)
     A                                      SPACEA(001)
     A N11                                20
     A                                      'Number of missing to be Inserted'
     A  11                                20
     A                                      'Number of Inserted missing header'
     A                                    57
     A                                      ':'
     A            TOTALA         6  0O    60
     A                                      EDTCDE(2)
     A          R ENDOF2
     A                                      SPACEB(001)
     A                                    55
     A                                      '***'
     A                                      SPACEB(001)
     A                                    +2
     A                                      'END OF REPORT'
     A                                    +2
     A                                      '***'
     A          R NODTLS2
     A                                      SPACEB(001)
     A                                    53
     A                                      '*** NO DETAILS TO REPORT ***'
      *
