      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Display loans that needs AITC update')                 *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/UTLE0522P3 - Display loan that needs AITC update.       *
      *                    Used by UTLE0522 utility for MD037545.     *
      *                                                               *
      *  (C) Finastra International Limited 2016                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR545781 *CREATE   Date 24Aug16               *
      *                 XNNNNNNN           Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR545781 - Missing/Multiple IH on history file.              *
      *             Provide datapatch utility to correct principal    *
      *             and interest values on history file.              *
      *           - Applied utility to MD-37545                       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *-------------------------------*
      *** Report Header Record Format *
      *-------------------------------*
     A          R HEADER3                   TEXT('(C) Finastra International +
     A                                       Systems Ltd. 2016')
      *
     A                                      SKIPB(1)
     A                                     2'UTLE0522P3 -'
     A N11                                15'AUDIT MODE'
     A  11                                15'UPDATE MODE'
     A            BJURPT    R             37REFFLD(URPT)
     A                                    95'DATE'
     A            BJMRDT    R            100REFFLD(MRDT)
     A                                   108'TIME'
     A                                   113TIME
     A                                      EDTWRD('  :  :  ')
     A                                   122'PAGE'
     A            PAGE3          5S 0    127
     A                                      EDTCDE(Z)
      *
     A                                      SPACEA(1)
     A                                    35'Suggested loan events to +
     A                                       be inserted to trigger update of'
     A                                      SPACEA(1)
     A                                    34'Accrued Interest to Con+
     A                                      trol Date (AITC) field +
     A                                      in PF/CLOANCL'
     A                                      SPACEA(1)
     A                                    34'==========================+
     A                                      ================================='
      *
     A                                      SPACEA(2)
     A                                    15'Branch'
     A                                    23'Customer No.'
     A                                    38'Loan No.'
     A                                    49'Action'
     A                                    57'Spread-Rate or ManRep/PrinInc'
     A                                   106'Value Date'
      *
     A                                      SPACEA(1)
     A                                    15'------'
     A                                    23'------------'
     A                                    38'--------'
     A                                    49'------'
     A                                    57'-----------------------------'
     A                                   106'----------'
     A                                      SPACEA(1)
      *------------------------*
      *** Detail Record Format *
      *------------------------*
     A          R DETAIL3
     A                                      SPACEA(1)
     A            VBRCA          3A       16
     A            VCNUM          6A       26
     A            VLNRF          6A       39
     A N17        VACTN          2A       51
     A N17        VRATE         13A       57
     A            VDATE          7A      108
     A  17                                51'insert manual repayment +
     A                                       and principal increase of 1.00'
      *------------------*
      *** End of Report  *
      *------------------*
     A          R ENDOF3
     A                                      SPACEB(3)
     A                                    54'***  END OF REPORT  ***'
      *------------------*
      *** Database Error *
      *------------------*
     A          R DBERR3
     A                                      SPACEB(3)
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
