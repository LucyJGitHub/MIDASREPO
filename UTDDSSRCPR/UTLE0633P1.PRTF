      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Printer File for UTLE0633')                            *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/UTLE0633P1 - Datapatch Printer File Program             *
      *                    Used by UTLE0633 utility for AR806352      *
      *                                                               *
      *  (c) Finastra International Limited 2012                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR806352 *CREATE   Date 05Mar12               *
      *                 Xxxxxxxx           Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR806351 - PDCLs with type Y/Z and FAMU = N have records in  *
      *             FACHISA. Delete invalid records in file and tag   *
      *             facility for recalculation. (Child: AR806352)     *
      *                                                               *
      *****************************************************************
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A          R HEADER1
     A                                      TEXT('(c) Finastra International -
     A                                       2011')
     A                                      SPACEA(001)
     A                                      SKIPB(001)
     A                                     6
     A                                      'REFERENCE UTLE0633P1'
     A            BJURPT        53A  O    37
     A                                   101
     A                                      'DATE'
     A            BJMRDT         7A  O   106
     A                                   122
     A                                      'PAGE'
     A                                    +2
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A N16                                 6
     A                                      'AUDIT MODE'
     A                                      SPACEB(001)
     A  16                                 6
     A                                      'UPDATE MODE'
     A                                      SPACEB(001)
     A N16                                40
     A                                      'INTEREST AND FEE PDCL +
     A                                      EVENTS THAT EXIST IN +
     A                                      FACHISA'
     A                                      SPACEB(001)
     A  16                                39
     A                                      'DELETED RECORDS IN FACHISA +
     A                                      OF INTEREST AND FEE PDCLs'
     A                                      SPACEB(001)
     A                                    33
     A                                      '-------------------------+
     A                                      --------------------------+
     A                                      -------------'
     A                                      SPACEB(001)
     A                                    40
     A                                      'LOAN'
     A                                      SPACEB(002)
     A                                   107
     A                                      'FAC. AMOUNT'
     A                                     4
     A                                      'BRANCH'
     A                                      SPACEB(001)
     A                                    12
     A                                      'CUSTOMER'
     A                                    22
     A                                      'FACILITY'
     A                                    33
     A                                      'LOAN'
     A                                    40
     A                                      'CCY'
     A                                    46
     A                                      'DATE'
     A                                    53
     A                                      'ACTION'
     A                                    65
     A                                      'ACTION AMOUNT'
     A                                    84
     A                                      'LOAN TYPE-SUBTYPE'
     A                                   105
     A                                      'UTILISATION FLAG'
     A                                      SPACEA(001)
      *
      *----------------*
      ** Report Detail *
      *----------------*
      *
     A          R DETAIL1
     A                                      SPACEA(001)
     A            BRCA           3A  O     5
     A            CNUM           6A  O    13
     A            FACT           3S 0O    23
     A                                    26
     A                                      '/'
     A            FCNO           2S 0O    27
     A            FALOAN         6A  O    32
     A            VLCCY          3A  O    40
     A            VDATE          7A  O    45
     A            FAACTN         2A  O    55
      *
     A  60        VAAMT0        15S 0O    61EDTWRD('   ,   ,   ,   , 0 -')
     A  61        VAAMT1        15S 1O    61EDTWRD('  ,   ,   ,   , 0 . -')
     A  62        VAAMT2        15S 2O    61EDTWRD(' ,   ,   ,   , 0 .  -')
     A  63        VAAMT3        15S 3O    61EDTWRD('   ,   ,   , 0 .   -')
      *
     A            WKLTYP         2A  O    90
     A                                    92
     A                                      '-'
     A            WKSUTP         2A  O    93
     A            WFLAG          3A  O   111
      *
      *-----------------*
      ** Report Summary *
      *-----------------*
      *
     A          R TRAILER1
     A N16                                35
     A                                      'Number of records to be +
     A                                      deleted:'
     A                                      SPACEB(001)
     A  16                                35
     A                                      'Number of records deleted:'
     A                                      SPACEB(001)
     A                                    81'='
     A            WRECER         7S 0O    84
     A                                      EDTCDE(3)
     A N12                                51
     A                                      '   ***  END OF REPORT  ***'
     A                                      SPACEB(003)
     A  12                                51
     A                                      '***  NO DETAILS TO REPORT  +
     A                                      ***'
     A                                      SPACEB(003)
      *
      *-----------------*
      ** Database Error *
      *-----------------*
      *
     A          R DBERROR1
     A                                      SKIPB(17)
     A                                    34'**** DATABASE ERROR ****'
     A                                      SPACEA(1)
     A                                    20'*************************'
     A                                    45'*************************'
     A                                    70'*****'
     A                                      SPACEA(1)
     A                                    20'*    FILE NAME'
     A            DBFILE         8        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    KEY'
     A            DBKEY         29        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    AT'
     A            DBASE          3  0     39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*************************'
     A                                    45'*************************'
     A                                    70'*****'
     A                                      SKIPA(43)
     A                                    35'*** END OF REPORT ***'
