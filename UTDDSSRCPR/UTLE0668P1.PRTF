      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Printer File - Missing PDCL repayments')               *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/UTLE0668P1 - Printer file for ILE/UTLE0668.             *
      *                                                               *
      *  (c) Misys Banking Systems 2012                               *
      *                                                               *
      *  Last Amend No. AR1036834 *CREATE  Date 23Oct12               *
      *  Prev Amend No. Xxxxxxxxx          Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR1036834 - Report file which shows all PDCL repayments      *
      *              that are not existing in FACHISA.                *
      *              (Child: AR1036835)                               *
      *                                                               *
      *****************************************************************
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
     A          R HEADER1
     A                                      TEXT('(c) Misys Banking -
     A                                      Systems 2012')
     A                                      SPACEA(001)
     A                                      SKIPB(001)
     A                                     6
     A                                      'REFERENCE UTLE0668P1'
     A            BJURPT        53A  O    37
     A                                   101
     A                                      'DATE'
     A            BJMRDT         7A  O   106
     A                                   122
     A                                      'PAGE'
     A                                    +2
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A N30                                 6
     A                                      'AUDIT MODE'
     A                                      SPACEB(001)
     A  30                                 6
     A                                      'UPDATE MODE'
     A                                      SPACEB(001)
      *
     A                                    34
     A                                      'Datapatch program to correct the F-
     A                                      acility History File (PF/FACHISA)'
      *
     A                                      SPACEA(001)
     A                                    34
     A                                      '==================================-
     A                                      ================================='
      *
     A                                      SPACEA(003)
      *
     A                                     5
     A                                      'Branch'
     A                                    13
     A                                      'Customer No.'
     A                                    28
     A                                      'Facility No.'
     A                                    43
     A                                      'Loan No.'
     A                                    54
     A                                      'Value Date'
     A                                    66
     A                                      'Action'
     A                                    85
     A                                      'Amount'
     A                                    97
     A                                      'Fac. Ccy'
     A N30                               107
     A                                      'Action to be taken'
     A                                      SPACEA(001)
     A  30                               107
     A                                      'Action taken'
      *
     A                                      SPACEA(001)
     A                                     5
     A                                      '------'
     A                                    13
     A                                      '------------'
     A                                    28
     A                                      '------------'
     A                                    43
     A                                      '--------'
     A                                    54
     A                                      '----------'
     A                                    66
     A                                      '------'
     A                                    85
     A                                      '------'
     A                                    97
     A                                      '--------'
     A N30                               107
     A                                      '------------------'
     A                                      SPACEA(001)
     A  30                               107
     A                                      '------------'
     A                                      SPACEA(001)
      *-----------------------*
      ** Detail Record Format *
      *-----------------------*
     A          R DETAIL1
     A                                      SPACEA(001)
     A            VBRCA          3A        6
     A            VCNUM          6        16
     A            VFACT          3        29
     A                                    32 '-'
     A            VFCNO          2        33
     A            VLNRF          6        44
     A            VDATE          7A       55
     A            VACTN          2A       68
     A  60        VFAMT0        15S 0O    75EDTWRD('   ,   ,   ,   , 0 -')
     A  61        VFAMT1        15S 1O    75EDTWRD('  ,   ,   ,   , 0 . -')
     A  62        VFAMT2        15S 2O    75EDTWRD(' ,   ,   ,   , 0 .  -')
     A  63        VFAMT3        15S 3O    75EDTWRD('   ,   ,   , 0 .   -')
     A            VFCCY          3A      100
     A  30N32                            107'Added to FACHISA'
     A N30N32                            107'Add to FACHISA'
     A
      *---------*
      ** Totals *
      *---------*
     A          R PRECS
     A                                      SPACEB(002)
     A                                      SPACEA(002)
     A N30                                35
     A                                      'Number of missing PDCL-
     A                                       repayments in FACHISA :'
     A N30        NOREAD         7S 0     83
     A                                      EDTCDE(1)
     A  30                                35
     A                                      'Number of created PDCL-
     A                                       repayments in FACHISA :'
     A  30        NOADD          7S 0     83
     A                                      EDTCDE(1)
      *-----------------*
      ** Report Trailer *
      *-----------------*
     A          R NODTLS
     A                                      SPACEB(003)
     A                                    52
     A                                      '*** NO DETAILS TO REPORT ***'
      *
     A          R ENDOF
     A                                      SPACEB(003)
     A                                    54
     A                                      '***  END OF REPORT  ***'
      *-----------------*
      ** Database Error *
      *-----------------*
     A          R DBERROR1
     A                                      SPACEB(003)
     A                                    25
     A                                      '**** DATABASE ERROR ****'
     A                                      SPACEB(002)
     A                                    11
     A                                      '**********************************-
     A                                      ********************'
     A                                      SPACEB(002)
     A                                    11
     A                                      '*'
     A                                      SPACEB(001)
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*    FILE NAME    '''
     A                                      SPACEB(001)
     A            DBFILE         8A       30
     A                                    38
     A                                      ''''
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*    KEY          '''
     A                                      SPACEB(001)
     A            DBKEY         29        30
     A                                    59
     A                                      '''    *'
     A                                    11
     A                                      '*    PROGRAM NAME '''
     A                                      SPACEB(001)
     A            DBPGM         10A       30
     A                                    40
     A                                      ''''
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*    AT           '''
     A                                      SPACEB(001)
     A            DBASE          3  0     30
     A                                    33
     A                                      ''''
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*'
     A                                      SPACEB(001)
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '**********************************-
     A                                      ********************'
     A                                      SPACEB(001)
