      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Printer File for UTLE0793')                            *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/UTLE0793P1 - Datapatch Printer File Program             *
      *                    Used by UTLE0793 utility for MD022057      *
      *                                                               *
      *  (c) Finastra International Limited 2016                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD022057 *CREATE   Date 08Nov16               *
      *                 XXXXXXXX           Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD022057 - Recalculate facilities in LETFHAPD.               *
      *                                                               *
      *****************************************************************
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A          R HEADER1
     A                                      TEXT('(c) Finastra International -
     A                                      Systems 2016')
     A                                      SPACEA(001)
     A                                      SKIPB(001)
     A                                     6
     A                                      'REFERENCE UTLE0793P1'
     A            BJURPT        53A  O    37
     A                                   101
     A                                      'DATE'
     A            BJMRDT         7A  O   106
     A                                   122
     A                                      'PAGE'
     A                                    +2
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A N16                                 6
     A                                      'AUDIT MODE'
     A                                      SPACEB(001)
     A  16                                 6
     A                                      'UPDATE MODE'
     A                                      SPACEB(001)
     A                                    43
     A                                      'FACILITIES THAT ARE TAGGED +
     A                                      FOR RECALCULATION'
     A                                      SPACEB(001)
     A                                    35
     A                                      '-------------------------+
     A                                      --------------------------+
     A                                      ---------'
     A                                      SPACEB(001)
     A                                    25
     A                                      'BRANCH'
     A                                      SPACEB(001)
     A                                    36
     A                                      'CUSTOMER'
     A                                    49
     A                                      'FACILITY'
     A                                    61
     A                                      'CCY'
     A                                    68
     A                                      'MCCY'
     A                                    80
     A                                      'PROCESSED?'
     A                                      SPACEA(001)
      *
      *----------------*
      ** Report Detail *
      *----------------*
      *
     A          R DETAIL1
     A                                      SPACEA(001)
     A            DUPBRC         3A  O    26
     A            DUPNUM         6A  O    37
     A            WFACT          3S 0O    50
     A                                    53
     A                                      '/'
     A            WFCNO          2S 0O    54
     A            FM_FCCY        3A  O    61
     A            FM_MCCY        1A  O    69
      *
     A N16                                84
     A                                      'N'
     A  16                                84
     A                                      'Y'
      *
      *-----------------*
      ** Report Summary *
      *-----------------*
      *
     A          R TRAILER1
     A                                    35
     A                                      'Number of LIVE facilities +
     A                                      in Fcty Detail File'
     A                                      SPACEB(003)
     A                                    81'='
     A            FACAUD         7S 0O    84
     A                                      EDTCDE(3)
     A N16                                35
     A                                      'Number of Facilities in +
     A                                      Error'
     A                                      SPACEB(001)
     A  16                                35
     A                                      'Number of Triggered Facilities'
     A                                      SPACEB(001)
     A                                    81'='
     A            FACERR         7S 0O    84
     A                                      EDTCDE(3)
     A N12                                51
     A                                      '   ***  END OF REPORT  ***'
     A                                      SPACEB(003)
     A  12                                51
     A                                      '***  NO DETAILS TO REPORT   +
     A                                      ***'
     A                                      SPACEB(003)
      *
      *-----------------*
      ** Database Error *
      *-----------------*
      *
     A          R DBERROR1
     A                                      SKIPB(17)
     A                                    34'**** DATABASE ERROR ****'
     A                                      SPACEA(1)
     A                                    20'*************************'
     A                                    45'*************************'
     A                                    70'*****'
     A                                      SPACEA(1)
     A                                    20'*    FILE NAME'
     A            DBFILE         8        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    KEY'
     A            DBKEY         29        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    AT'
     A            DBASE          3  0     39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*************************'
     A                                    45'*************************'
     A                                    70'*****'
     A                                      SKIPA(43)
     A                                    35'*** END OF REPORT ***'
