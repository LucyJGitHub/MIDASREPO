      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas UT Patch for Facility Interest Calc Basis')      *
      *****************************************************************
      *                                                               *
      *  Midas - Lending Module                                       *
      *                                                               *
      *  PRTF/UTLE0809P1 - Midas UT Patch for Facility Interest       *
      *                    Calculation Basis                          *
      *                                                               *
      *  (c) Finastra International Limited 2018                      *
      *                                                               *
      *  Last Amend No. MD042752 *Create   Date 15Jan18               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD042752 - Facility currency being picked up to calculate    *
      *             interest rather than loan currency                *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      *** Report Header Record Format  *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2018')
      *
     A                                      SKIPB(002)
     A                                     6
     A                                      'REFERENCE UTLE0809P1'
     A            BJURPT        53A  O    37
     A                                   101
     A                                      'DATE'
     A            RUNA           7A      106
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A N10                                 6'AUDIT MODE '
     A  10                                 6'UPDATE MODE'
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
      *------------------------*
      ***  Detail Sub-Headings *
      *------------------------*
      *
     A                                    37
     A                                      'Facilities with Default Interest +
     A                                      Calculation Basis'
     A                                      SPACEA(001)
     A                                    37
     A                                      '-------------------------------+
     A                                      --------------------'
     A                                      SPACEA(001)
     A                                    14
     A                                      'Branch  '
     A                                    24
     A                                      'Customer '
     A                                    35
     A                                      'Facility '
     A                                    46
     A                                      'Facility '
     A                                    57
     A                                      'Currency '
     A                                    70
     A                                      'Multi-'
     A                                    81
     A                                      'Interest Calc. Basis'
     A                                      SPACEA(001)
     A                                    37
     A                                      'Type'
     A                                    47
     A                                      'Number'
     A                                    67
     A                                      'Currency Ind'
     A                                    84
     A                                      'Before'
     A                                    93
     A                                      'After'
     A                                      SPACEA(001)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAILP1
      *
     A                                      SPACEB(001)
     A            BRCA           3A       16
     A            CNUM           6A       25
     A            FACT           3S 0     38
     A            FCNO           2S 0     49
     A            FCCY           3A       60
     A            MCCY           1A       72
     A            ODICB          1S 0     87
     A            DICB           1S 0     95
      *
      *-------------------*
      *** No Details      *
      *-------------------*
      *
     A          R NODETP1
     A                                    52
     A                                      '*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(003)
      *
      *-------------------*
      *** Database Error  *
      *-------------------*
      *
     A          R DBERROR
     A                                      SPACEB(001)
     A                                    47
     A                                      'DATABASE ERROR'
     A            DBASE          3  0     62
     A                                    66
     A                                      'IN PROGRAM'
     A            DBPGM         10        77
     A            DBFILE         8        52SPACEB(1)
     A                                    62
     A                                      'KEY'
     A            DBKEY         29        66
      *
      *
      **-----------------*
      ** Report Trailer  *
      **-----------------*
      *
     A          R ENDRPT
     A                                    54
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(002)
