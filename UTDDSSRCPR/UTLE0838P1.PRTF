      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas LE Basel II Fields in LEFEED Report')            *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/UTLE0838P1 - Basel II Fields in LEFEED Report           *
      *                                                               *
      *  (c) Finastra International Limited 2019                      *
      *                                                               *
      *  Last Amend No. MD053837 *CREATE   Date 02Sep19               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD053837 - Inconsistency in Basel II initial date and        *
      *             classification for fees. Zeroise Basel II fields  *
      *             in LEFEED to trigger repair of incorrect values   *
      *             in COB.                                           *
      *                                                               *
      *****************************************************************
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A          R HEADER1
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2019')
     A                                      SPACEA(002)
     A                                      SKIPB(001)
     A                                     6
     A                                      'REFERENCE UTLE0838P1'
     A            BJURPT        53A  O    37
     A                                   101
     A                                      'DATE'
     A            BJMRDT         7A  O   106
     A                                   119
     A                                      'PAGE'
     A                                    +2
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A N16                                 6
     A                                      'AUDIT MODE'
     A                                      SPACEB(001)
     A  16                                 6
     A                                      'UPDATE MODE'
     A                                      SPACEB(001)
     A N16                                54
     A                                      'FEE RECORDS TO BE TRIGGERED'
     A                                      SPACEB(001)
     A N16                                49
     A                                      'FOR REPAIR OF BASEL II FIELDS'
     A                                      SPACEB(001)
     A N16                                79
     A                                      'IN COB'
     A  16                                54
     A                                      'FEE RECORDS TRIGGERED FOR'
     A                                      SPACEB(001)
     A  16                                51
     A                                      'REPAIR OF BASEL II FIELDS'
     A                                      SPACEB(001)
     A  16                                77
     A                                      'IN COB'
     A                                    40
     A                                      '-----------------------------------
     A                                      --------------------'
     A                                      SPACEB(001)
     A                                    71
     A                                      'BASEL II PAST DUE'
     A                                      SPACEB(002)
     A                                    26
     A                                      'CUSTOMER'
     A                                      SPACEB(001)
     A                                    35
     A                                      'FACILITY'
     A                                    44
     A                                      'LOAN NO.'
     A                                    53
     A                                      'SEQUENCE'
     A                                    62
     A                                      'PDCL NO.'
     A                                    71
     A                                      'INITIAL DATE'
     A                                    84
     A                                      'CLASSIFICATION'
     A                                    99
     A                                      'START DATE'
      *
      *----------------*
      ** Report Detail *
      *----------------*
      *
     A          R DETAIL1
     A                                      SPACEA(001)
     A            PCNUM          6A  O    27
     A            PFACT          3S 0O    36
     A                                    39
     A                                      '/'
     A            PFCNO          2S 0O    40
     A            PLOAN          6A  O    45
     A            PFSEQ          2S 0O    56
     A            PPLNR          6A  O    63
     A            PBIDT          7A  O    73
     A            PBCAT          1S  O    90
     A            PBCSD          7A  O   100
      *
      *-----------------*
      ** Report Summary *
      *-----------------*
      *
     A          R TRAILER1
     A N16                                38
     A                                      'Number of Records to be Triggered'
     A                                      SPACEB(003)
     A  16                                38
     A                                      'Number of Records Triggered'
     A                                      SPACEB(003)
     A                                    82
     A                                      '='
     A            WRECER         6S 0O    85
     A                                      EDTCDE(3)
     A N12                                55
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(003)
     A  12                                52
     A                                      '***  NO DETAILS TO REPORT  ***'
     A                                      SPACEB(003)
      *
      *-----------------*
      ** Database Error *
      *-----------------*
      *
     A          R DBERROR1
     A                                      SKIPB(017)
     A                                    34
     A                                      '**** DATABASE ERROR ****'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*************************'
     A                                    45
     A                                      '*************************'
     A                                    70
     A                                      '*****'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*    FILE NAME'
     A            DBFILE         8        39
     A                                    73
     A                                      '*'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*    KEY'
     A            DBKEY         29        39
     A                                    73
     A                                      '*'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73
     A                                      '*'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*    AT'
     A            DBASE          3  0     39
     A                                    73
     A                                      '*'
     A                                      SPACEA(001)
     A                                    20
     A                                      '*************************'
     A                                    45
     A                                      '*************************'
     A                                    70
     A                                      '*****'
     A                                      SKIPA(043)
     A                                    35
     A                                      '*** END OF REPORT ***'
