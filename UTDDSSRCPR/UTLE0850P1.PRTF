      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Printer File for UTLE0850A')                           *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/UTLE0850P1 - Utility Printer File Program               *
      *                    Used by UTLE0850A utility for MD056104     *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *                                                               *
      *  Last Amend No. MD056104 *Create   Date 07Aug20               *
      *  Prev Amend No. XXXXXXXX           Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD056104 - Numerous incorrect utilisation records found in   *
      *             FACHISA.                                          *
      *           - Reverse all required utilisation records and      *
      *             regenerate US records from LEMNFUPD.              *
      *                                                               *
      *****************************************************************
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A          R HEADER1
     A                                      TEXT('(c) Finastra International -
     A                                      Systems 2020')
     A                                      SPACEA(001)
     A                                      SKIPB(001)
     A                                     6
     A                                      'REFERENCE UTLE0850P1'
     A            BJURPT        53A  O    37
     A                                   101
     A                                      'DATE'
     A            BJMRDT         7A  O   106
     A                                   122
     A                                      'PAGE'
     A                                    +2
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A N16                                 6
     A                                      'AUDIT MODE'
     A                                      SPACEB(001)
     A  16                                 6
     A                                      'UPDATE MODE'
     A                                      SPACEB(001)
     A N16                                43
     A                                      'FACHISA UTILISATION RECORDS TO BE -
     A                                      REVERSED'
     A                                      SPACEB(001)
     A  16                                46
     A                                      'REVERSED FACHISA UTILISATION RECOR-
     A                                      DS'
     A                                      SPACEB(001)
     A                                    35
     A                                      '-------------------------+
     A                                      --------------------------+
     A                                      ---------'
     A                                      SPACEB(001)
     A                                    35
     A                                      'BRANCH'
     A                                      SPACEB(001)
     A                                    46
     A                                      'CUSTOMER'
     A                                    60
     A                                      'FACILITY'
     A                                    72
     A                                      'NUM. OF UTILISATIONS'
      *
      *----------------*
      ** Report Detail *
      *----------------*
      *
     A          R DETAIL1
     A                                      SPACEA(001)
     A            Z_BRCA         3A  O    36
     A            Z_FACNUM       6   O    47
     A            FCTY_TYPE      3   O    61
     A N12                                64
     A                                      '/'
     A            FCTY_SEQ       2   O    65
      *
     A            UTIL_COUNT     7S 0O    74EDTWRD(' ,   ,   ')
      *
      *-----------------*
      ** Report Summary *
      *-----------------*
      *
     A          R TRAILER1
     A                                      SPACEB(4)
     A                                    35'Number of Distinct Facilties'
     A                                    81'='
     A            FCTY_COUNT     7S 0O    84EDTWRD(' ,   ,   ')
     A*                                     EDTCDE(3)
     A                                    35
     A                                      'Total Number of Utilisation Record-
     A                                      s'
     A                                      SPACEB(001)
     A                                    81'='
     A            UTIL_TOTAL     7S 0O    84EDTWRD(' ,   ,   ')
     A*                                     EDTCDE(3)
     A N12                                51
     A                                      '   ***  END OF REPORT  ***'
     A                                      SPACEB(3)
     A  12                                51
     A                                      '***  NO DETAILS TO REPORT   +
     A                                      ***'
     A                                      SPACEB(003)
      *
      *-----------------*
      ** Database Error *
      *-----------------*
      *
     A          R DBERROR1
     A                                      SKIPB(17)
     A                                    34'**** DATABASE ERROR ****'
     A                                      SPACEA(1)
     A                                    20'*************************'
     A                                    45'*************************'
     A                                    70'*****'
     A                                      SPACEA(1)
     A                                    20'*    FILE NAME'
     A            DBFILE         8        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    KEY'
     A            DBKEY         29        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    AT'
     A            DBASE          3  0     39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*************************'
     A                                    45'*************************'
     A                                    70'*****'
     A                                      SKIPA(43)
     A                                    35'*** END OF REPORT ***'
