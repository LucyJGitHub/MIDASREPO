      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Printer File for UTLE0850B')                           *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/UTLE0850P2 - Utility Printer File Program               *
      *                    Used by UTLE0850B utility for MD056104     *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *                                                               *
      *  Last Amend No. MD056104 *Create   Date 07Aug20               *
      *  Prev Amend No. XXXXXXXX           Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD056104 - Numerous incorrect utilisation records found in   *
      *             FACHISA.                                          *
      *           - Reverse all required utilisation records and      *
      *             regenerate US records from LEMNFUPD.              *
      *                                                               *
      *****************************************************************
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A          R HEADER1
     A                                      TEXT('(c) Finastra International -
     A                                      Systems 2020')
     A                                      SPACEA(001)
     A                                      SKIPB(001)
     A                                     6
     A                                      'REFERENCE UTLE0850P2'
     A            BJURPT        53A  O    37
     A                                   101
     A                                      'DATE'
     A            BJMRDT         7A  O   106
     A                                   122
     A                                      'PAGE'
     A                                    +2
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A N16                                 6
     A                                      'AUDIT MODE'
     A                                      SPACEB(001)
     A  16                                 6
     A                                      'UPDATE MODE'
     A                                      SPACEB(1)
     A N16                                43
     A                                      'FACHISA US RECORDS TO BE GENERATED'
     A                                      SPACEB(001)
     A  16                                46
     A                                      'GENERATED FACHISA US RECORDS'
     A                                      SPACEB(001)
     A                                    35
     A                                      '-------------------------+
     A                                      --------------------------+
     A                                      ---------'
     A                                      SPACEB(001)
     A                                     6
     A                                      'BRANCH'
     A                                      SPACEB(001)
     A                                    14
     A                                      'CUSTOMER'
     A                                    25
     A                                      'FACILITY'
     A                                    35
     A                                      'SEQ. NUM.'
     A                                    46
     A                                      'FCTY. CLASS'
     A                                    60
     A                                      'UTIL. START DATE'
     A                                    79
     A                                      'UCCY'
     A                                    91
     A                                      'UTIL. AMT.'
     A                                   105
     A                                      'FCCY'
     A                                   112
     A                                      'UTIL. AMT. (FCTY)'
     A*                                     7'ABC'
     A*                                      SPACEB(1)
     A*                                    15'ABC123'
     A*                                    26'001/01'
     A*                                    37'9,999'
     A*                                    47'CR.AGRMT'
     A*                                    64'01JAN20'
     A*                                    79'USD'
     A*                                    85'XXX,XXX,XXX,XXX'
     A*                                   104'USD'
     A*                                   113'XXX,XXX,XXX,XXX'
      *
      *----------------*
      ** Report Detail *
      *----------------*
      *
     A          R DETAIL1
     A                                      SPACEA(001)
     A            DPBRCA         3A  O     7
     A            DPCNUM         6A  O    15
     A            DPFACT         3S 0O    26
     A N12                                29
     A                                      '/'
     A            DPFCNO         2S 0O    30
      *
     A            DPSQNO         4S 0O    37EDTWRD(' ,   ')
     A            DPTRCA         8A  O    47
     A            DPSTDT         7A  O    64
     A            DPUCCY         3A  O    79
      *
     A  60        DPUAMT0       15  0     82EDTCDE(K)
     A  61        DPUAMT1       15  1     82EDTCDE(K)
     A  62        DPUAMT2       15  2     82EDTCDE(K)
     A  63        DPUAMT3       15  3     82EDTCDE(K)
      *
     A            DPFCCY         3A      105
      *
     A  50        DPFAMT0       15  0    109EDTCDE(K)
     A  51        DPFAMT1       15  1    109EDTCDE(K)
     A  52        DPFAMT2       15  2    109EDTCDE(K)
     A  53        DPFAMT3       15  3    109EDTCDE(K)
      *
      *-----------------*
      ** Report Summary *
      *-----------------*
      *
     A          R TRAILER1
     A                                      SPACEB(4)
     A                                    35'Number of Utilisations'
     A                                    81'='
     A            MFU_COUNT      7S 0O    84EDTWRD(' ,   ,   ')
     A*                                     EDTCDE(3)
     A N12                                51
     A                                      '   ***  END OF REPORT  ***'
     A                                      SPACEB(3)
     A  12                                51
     A                                      '***  NO DETAILS TO REPORT   +
     A                                      ***'
     A                                      SPACEB(003)
      *
      *-----------------*
      ** Database Error *
      *-----------------*
      *
     A          R DBERROR1
     A                                      SKIPB(17)
     A                                    34'**** DATABASE ERROR ****'
     A                                      SPACEA(1)
     A                                    20'*************************'
     A                                    45'*************************'
     A                                    70'*****'
     A                                      SPACEA(1)
     A                                    20'*    FILE NAME'
     A            DBFILE         8        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    KEY'
     A            DBKEY         29        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    AT'
     A            DBASE          3  0     39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*************************'
     A                                    45'*************************'
     A                                    70'*****'
     A                                      SKIPA(43)
     A                                    35'*** END OF REPORT ***'
