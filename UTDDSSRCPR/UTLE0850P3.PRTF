      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Printer File for UTLE0850C')                           *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/UTLE0850P3 - Utility Printer File Program               *
      *                    Used by UTLE0850C utility for MD056104     *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *                                                               *
      *  Last Amend No. MD056104 *Create   Date 07Aug20               *
      *  Prev Amend No. XXXXXXXX           Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD056104 - Numerous incorrect utilisation records found in   *
      *             FACHISA.                                          *
      *           - Reverse all required utilisation records and      *
      *             regenerate US records from LEMNFUPD.              *
      *                                                               *
      *****************************************************************
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A          R HEADER1
     A                                      TEXT('(c) Finastra International -
     A                                      Systems 2020')
     A                                      SPACEA(001)
     A                                      SKIPB(001)
     A                                     6
     A                                      'REFERENCE UTLE0850P3'
     A            BJURPT        53A  O    37
     A                                   101
     A                                      'DATE'
     A            BJMRDT         7A  O   106
     A                                   122
     A                                      'PAGE'
     A                                    +2
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                    49
     A                                      'FACILITIES TO BE RECALCULATED'
     A                                      SPACEB(001)
     A                                    35
     A                                      '-------------------------+
     A                                      --------------------------+
     A                                      ---------'
     A                                      SPACEB(1)
     A                                    35
     A                                      'BRANCH'
     A                                      SPACEB(001)
     A                                    44
     A                                      'CUSTOMER'
     A                                    55
     A                                      'FACILITY'
     A                                    66
     A                                      'CCY'
     A                                    72
     A                                      'MULT.CCY?'
     A                                    84
     A                                      'FCTY. CLASS'
     A*                                    36'ABC'
     A*                                      SPACEB(1)
     A*                                    45'ABC123'
     A*                                    56'001/01'
     A*                                    66'PHP'
     A*                                    74'YES'
     A*                                    85'CR.AGRMT'
      *
      *----------------*
      ** Report Detail *
      *----------------*
      *
     A          R DETAIL1
     A                                      SPACEA(001)
     A            DPBRCA         3A  O    36
     A            DPCNUM         6A  O    45
     A            DPFACT         3A  O    56
     A N12                                59
     A                                      '/'
     A            DPFCNO         2A  O    60
     A            DPFCCY         3A  O    66
     A            DPMCCY         3A  O    74
     A            DPTRCA         8A  O    85
      *
      *-----------------*
      ** Report Summary *
      *-----------------*
      *
     A          R TRAILER1
     A                                      SPACEB(4)
     A                                    35'Total Number of Facilities to be R-
     A                                      ecalculated'
     A                                    81'='
     A            FCTY_COUNT     7S 0O    84EDTWRD(' ,   ,   ')
     A N12                                51
     A                                      '   ***  END OF REPORT  ***'
     A                                      SPACEB(3)
     A  12                                51
     A                                      '***  NO DETAILS TO REPORT   +
     A                                      ***'
     A                                      SPACEB(003)
      *
      *-----------------*
      ** Database Error *
      *-----------------*
      *
     A          R DBERROR1
     A                                      SKIPB(17)
     A                                    34'**** DATABASE ERROR ****'
     A                                      SPACEA(1)
     A                                    20'*************************'
     A                                    45'*************************'
     A                                    70'*****'
     A                                      SPACEA(1)
     A                                    20'*    FILE NAME'
     A            DBFILE         8        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    KEY'
     A            DBKEY         29        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    AT'
     A            DBASE          3  0     39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*************************'
     A                                    45'*************************'
     A                                    70'*****'
     A                                      SKIPA(43)
     A                                    35'*** END OF REPORT ***'
