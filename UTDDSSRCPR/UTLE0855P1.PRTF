      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas  SD MD-57165 Data Patch Printer File')           *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/UTLE0855P1 -  SD MD-57165 Data Patch Printer File       *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. MD057165  *CREATE  Date 18Nov20               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD057165 - SP23 Correspondence manager cob failure after     *
      *             input of new fee                                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R UTLE0855H1                TEXT('(c) Finastra Internatio +
     A                                      nal Limited 2020')
     A                                      SKIPB(1)
     A                                     6'REFERENCE UTLE0855P1'
     A N10                                27'- UPDATE'
     A  10                                27'- AUDIT'
     A            BJURPT    R   53        38REFFLD(URPT)
     A                                   101'DATE:'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                    23'Amend Lending Fees Extension Files'
     A                                      SPACEB(1)
     A                                   101'TIME:'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                    23'----------------------------------'
     A                                      SPACEA(2)
      *
     A          R UTLE0855H2
     A                                    02'LOAN'
     A                                    09'PURCHASE'
     A                                    18'ORIGINAL'
     A                                    27'LOAN'
     A                                    32 'FEE'
     A                                      SPACEA(1)
     A                                    02'NUMBER'
     A                                    09'FROM'
     A                                    18'BORROWER'
     A                                    27'TYPE'
     A                                    32 'SEQ'
     A                                    36 'REMARKS'
     A                                      SPACEA(1)
      *
     A          R UTLE0855D1
     A                                      SPACEB(1)
     A            PLNRF          6A       02
     A            PCNUM          6A       09
     A            POLNO          6A       18
     A            PPTYP          2S 0     27
     A            PFSEQ          2S 0     32
     A            PREM1         60A       36
      *
     A          R UTLE0855D2
     A                                      SPACEB(1)
     A            PREM2         60A       36
     A*                                     SPACEA(1)
      *
     A          R UTLE0855T
     A                                      SPACEB(1)
     A  10                                10'NUMBER OF RECORDS TO BE AMENDED'
     A N10                                10'NUMBER OF RECORDS WERE AMENDED'
     A                                    42
     A                                      '='
     A            PCTR           6S 0O    45
     A                                      EDTCDE(3)
     A                                      SPACEA(1)
     A          R UTLE0855E
     A                                      SPACEB(1)
     A                                    54'*** END OF REPORT ***'

