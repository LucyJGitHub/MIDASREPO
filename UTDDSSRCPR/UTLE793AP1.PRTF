      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Printer file - resequence facility history records')   *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/UTLE793AP1 - Datapatch Printer File Program             *
      *                    Used by UTLE0793A utility for MD022057     *
      *                                                               *
      *  (c) Misys Banking Systems 2016                               *
      *                                                               *
      *  Last Amend No. MD022057 *CREATE   Date 08Nov16               *
      *  Prev Amend No. Xxxxxxxx           Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD022057 - To correct FACHISA. This will report the records  *
      *             with incorrect sequence number.                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
     A          R HEADER                    TEXT('(c) Misys Ba+
     A                                      nking Systems 2016')
      *
     A                                      SKIPB(1)
     A                                     2'UTLE793AP1 -'
     A N30                                15'AUDIT MODE'
     A  30                                15'UPDATE MODE'
     A            BJURPT    R             37REFFLD(URPT)
     A                                    95'DATE'
     A            BJMRDT    R            100REFFLD(MRDT)
     A                                   108'TIME'
     A                                   113TIME
     A                                      EDTWRD('  :  :  ')
     A                                   122'PAGE'
     A            PAGENO         5S 0    127
     A                                      EDTCDE(Z)
      *
     A                                      SPACEA(1)
     A                                    34'Datapatch program to cor+
     A                                      rect the Facility History +
     A                                      File (PF/FACHISA)'
      *
     A                                      SPACEA(1)
     A                                    34'========================+
     A                                      =========================+
     A                                      =================='
      *
     A                                      SPACEA(2)
     A                                    34'---------------- Facility +
     A                                       with Incorrect Seq. No. +
     A                                       ----------------'
      *
     A                                      SPACEA(2)
     A                                    15'Branch'
     A                                    23'Customer No.'
     A                                    38'Facility No.'
     A                                    53'Value Date'
     A                                    66'Seq.'
     A                                    72'Action'
     A                                    80'Loan No.'
     A                                    95'Loan Amount'
     A                                   111'Fac. Currency'
      *
     A                                      SPACEA(1)
     A                                    15'------'
     A                                    23'------------'
     A                                    38'------------'
     A                                    53'----------'
     A                                    66'----'
     A                                    72'------'
     A                                    80'--------'
     A                                    95'-----------'
     A                                   111'-------------'
     A                                      SPACEA(1)
      *-----------------------*
      ** Detail Record Format *
      *-----------------------*
     A          R DETAIL
     A                                      SPACEA(1)
     A            VBRCA          3A       16
     A            VCNUM          6        26
     A            VFCTY          5        41
     A            VDATE          7A       54
     A            VFSEQ          3        67
     A            VACTN          2A       74
     A            VLOAN          6A       81
     A  60        VFAMT0        15S 0O    90EDTWRD('   ,   ,   ,   , 0 -')
     A  61        VFAMT1        15S 1O    90EDTWRD('  ,   ,   ,   , 0 . -')
     A  62        VFAMT2        15S 2O    90EDTWRD(' ,   ,   ,   , 0 .  -')
     A  63        VFAMT3        15S 3O    90EDTWRD('   ,   ,   , 0 .   -')
     A            VFCCY          3A      115
     A            VERR           3A      124
     A
     A          R PRECS
     A                                      SPACEB(2)
     A                                      SPACEA(2)
     A N30                                20'Number of facility records +
     A                                      with incorrect seq. no.:'
     A N30        NOREAD        10S 0     76
     A                                      EDTCDE(L)
     A                                      SPACEA(2)
     A  30                                20'Number of Updated Facili+
     A                                       ty History records :'
     A  30        NOUPDT        10S 0     76
     A                                      EDTCDE(L)
     A                                      SPACEA(2)
     A                                    20'Total number of Facilities +
     A                                      in PF/FCLTYFM :'
     A            NOTOTS        10S 0     76
     A                                      EDTCDE(L)
     A                                      SPACEA(2)
     A  30                                20'*** Records with corrected +
     A                                      Sequence No.'
     A N30                                20'*** Records with incorrect +
     A                                      Sequence No.'
      *-----------------*
      ** Report Trailer *
      *-----------------*
     A          R ENDOF
     A                                      SPACEB(3)
     A                                    54'***  END OF REPORT  ***'
      *-----------------*
      ** Database Error *
      *-----------------*
     A          R DBERR
     A                                      SPACEB(3)
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
