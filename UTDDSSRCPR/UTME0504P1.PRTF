      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas ME MT101 & MT102 Totals Utility Report')         *
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  UTME0504P1 - MT101 & MT102 Totals Utility Report             *
      *                                                               *
      *  (c) Finastra International Limited 2021                      *
      *                                                               *
      *  Last Amend No. MD057504*CREATE    Date 22Jan21               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD057504 - Utility program to houskeep records in MEIRFTPD   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited 2021')
      *
     A                                      SKIPB(2)
     A                                     6'REFERENCE UTME0504P1'
     A                                      SPACEB(1)
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A            RMODE         11         6
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A  60                                48'DELETED MT101 / MT102 TOTALS-
     A                                       RECORDS'
     A                                      SPACEA(1)
     A N60                                48'MT101 / MT102 TOTALS RECORDS-
     A                                       TO DELETE'
     A                                      SPACEA(1)
     A                                    47'-----------------------------------
     A                                      ------'
     A                                      SPACEA(3)
     A                                     6'SWIFT Reference'
     A                                    61'Msg Type'
     A                                      SPACEA(1)
     A                                     6'---------------'
     A                                    61'--------'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
     A            RSWFR         28         6
     A            RMTPY          3        63
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
