/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Utility to patch account keys')                        *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/UTNARRP1                                                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2009            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. BUG23884 *CREATE   Date 10Jun09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG23884 - Tax deduction on MM Deals shows customer name     *
      *             as narrative                                      *
      *                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2009')
      *
     A                                      SKIPB(003)
     A                                     6
     A                                      '            REFERENCE UTNARRP1'
     A            BJURPT        53        41
     A                                      TEXT('User Report Title')
     A                                   101
     A                                      'DATE'
     A            BJMRDT         7       106
     A                                      TEXT('Midas Run Date')
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
      *
     A  26                                 6
     A                                      'UPDATE MODE'
     A                                      SPACEB(001)
     A N26                                 6
     A                                      'AUDIT MODE'
     A                                      SPACEB(001)
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    55
     A                                      'Account Keys Narratives Report'
     A                                      SPACEB(001)
      *
     A                                    55
     A                                      '------------------------------'
     A                                      SPACEB(001)
      *
     A                                     3
     A                                      'Account Key'
     A                                      SPACEB(003)
     A N26                                22
     A                                      'Current Narrative Code'
     A N26                                55
     A                                      'Narrative Code - AFTER'
     A  26                                55
     A                                      'Current Narrative Code'
     A N26                                22
     A                                      '1    2    3    4    5    6'
     A                                      SPACEB(001)
     A                                    55
     A                                      '1    2    3    4    5    6'
     A  26                                  SPACEB(001)
     A                                     3
     A                                      '---------------'
     A                                      SPACEB(001)
     A N26                                22
     A                                      '--------------------------'
     A                                    55
     A                                      '--------------------------'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            AKEY          14         3
     A                                      TEXT('Account Key')
     A                                      SPACEB(001)
     A N26        NCD11          2        22
     A                                      TEXT('First Narrative Code')
     A N26        NCD22          2        27
     A                                      TEXT('2nd Narrative Code')
     A N26        NCD33          2        32
     A                                      TEXT('3rd Narrative Code')
     A N26        NCD44          2        37
     A                                      TEXT('4th Narrative Code')
     A N26        NCD55          2        42
     A                                      TEXT('5th Narrative Code')
     A N26        NCD66          2        47
     A                                      TEXT('6th Narrative Code')
     A            NCD1           2        55
     A                                      TEXT('First Narrative Code')
     A            NCD2           2        60
     A                                      TEXT('2nd Narrative Code')
     A            NCD3           2        65
     A                                      TEXT('3rd Narrative Code')
     A            NCD4           2        70
     A                                      TEXT('4th Narrative Code')
     A            NCD5           2        75
     A                                      TEXT('5th Narrative Code')
     A            NCD6           2        80
     A                                      TEXT('6th Narrative Code')
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILER
     A                                     3
     A                                      'Total count ->'
     A                                      SPACEB(003)
     A            AMENDS         5  0     18
     A                                    51
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(006)
