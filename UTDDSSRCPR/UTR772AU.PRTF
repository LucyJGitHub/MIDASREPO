      *****************************************************************
/*STD *  PRTFAU                                                       *
/*EXI *  TEXT('Midas Retail Discrepancy Audit')                       *
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/UTR772AU - Retail History Update Audit                  *
      *                                                               *
      *  (c) Finastra International Banking Sytems Ltd. 2019          *
      *                                                               *
      *  Last Amend No. MD035660 *CREATE   Date 15May19               *
      *  Prev Amend No. MD0nnnnn           Date ddMmmyy               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD035660 - No fee settlement posting in Retail history file. *
      *             Printer files to list no. of retail account with  *
      *             discrepancies.                                    *
      *           - Applied fix for MD-53040.                         *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *-----------------------------*
      * Report Header Record Format *
      *-----------------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      System LTD. 2019')
      *
     A                                      SKIPB(3)
     A                                     7'REFERENCE'
     A                                    17'UTR772AU'
     A            TITL      R             37
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A                                   126'   1'
     A                                      SPACEA(2)
      *
     A                                    38'ACCOUNT BALANCE AND '
     A                                    58'HISTORY BALANCE '
     A                                    74'DISCREPANCY AUDIT'
     A                                      SPACEA(1)
      *
     A                                    38'--------------------'
     A                                    58'----------------'
     A                                    74'-----------------'
     A                                      SPACEA(2)
      *
      *-----------------------------*
      * Report Detail Record Format *
      *-----------------------------*
      *
     A          R TOTALS                    SKIPB(10)
      *
     A                                    21'FILE CONTROLS REPORT'
     A                                      SPACEB(2)
      *
     A                                    23'NUMBER OF DISCREPANCIES'
     A                                      SPACEB(2)
     A            NBRREC         5S       47EDTCDE(J)
      *
      *------------------------------*
      * Report Database Error Format *
      *------------------------------*
      *
     A          R ERRAU
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE    R             30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY     R             30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM     R             30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3S 0     30
     A                                    33''''
     A                                    64'*'
     A  88                                11'*    STATUS       '''
     A  88                                  SPACEB(1)
     A  88        DBSTAT         5        30
     A  88                                35''''
     A  88                                64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
