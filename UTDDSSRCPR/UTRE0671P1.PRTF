      *****************************************************************
/*STD *  PRTFAU                                                       *
/*EXI *  TEXT('List of Accounts with incorrect type/subtype')         *
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/UTRE0671P1 - List of Accounts with incorrect Interest   *
      *                    Type/Stype.                                *
      *                                                               *
      *  (c) Finastra International Limited 2013                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR1055719 *CREATE  Date 26Nov13               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR1055719 - Wrong Retail Interest Rate. Created a utility    *
      *              that will update pf/REHPOSRn with the correct s  *
      *              interest rate details based on pf/REIACD.        *
      *              (Child: AR1055720)                               *
      *            - Applied for MD023799                             *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *-------------------------------*
      *  Report Header Record Format  *
      *-------------------------------*
      *
     A          R HEAD1
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2013')
      *
     A                                      SKIPB(003)
     A                                     5
     A                                      'REFERENCE UTRE0671P1'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            BJMRDT    R            106
     A                                      REFFLD(RUNA)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
     A                                    30
     A                                      'Accounts with incorrect Interest +
     A                                      Type/Stype'
     A N97                                84
     A                                      '** AUDIT  **'
     A  97                                84
     A                                      '** UPDATE **'
     A                                    30
     A                                      '---------------------------------
     A                                      ---------------------------------
     A                                      -'
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A                                     5
     A                                      'Retail Account'
     A                                    46
     A                                      'Dr/Cr'
     A                                    52
     A                                      'History'
     A                                    65
     A                                      'Interest Type/Subtype'
     A                                      SPACEA(001)
     A                                    52
     A                                      'Date'
     A                                    65
     A                                      'Current'
     A                                    76
     A                                      'New'
     A
      *
      *--------------*
      *  No Details  *
      *--------------*
      *
     A          R NODETAIL
     A                                    53
     A                                      '*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(003)
      *
      *---------------------------------------*
      *  Details of account tranfers postings *
      *---------------------------------------*
      *
     A          R DETAIL
     A                                      SPACEB(001)
     A            RPBRCA         3A        5
     A                                     8'-'
     A            RPCNUM         6A        9
     A                                    15'-'
     A            RPCCY          3A       16
     A                                    19'-'
     A            RPACOD        10S       20
     A                                    30'-'
     A            RPACSQ         2S       31
     A            RPACNO        10S       35
     A            RPDRCR         2A       48
     A            RPHISD         7A       52
     A            CUINTP         2S       65
     A                                    67'-'
     A            CUSTYP         5S       68
     A            NWINTP         2S       76
     A                                    78'-'
     A            NWSTYP         5S       79
      *
      *---------------*
      *  Report Total *
      *---------------*
      *
      *
     A          R TOTAL
     A                                      SPACEB(002)
     A                                    45
     A                                      '*** END OF REPORT ***'
     A                                      SPACEA(001)
     A N97                                 7
     A                                      'Number of records to be amended:'
     A  97                                 7
     A                                      'Number of records amended:'
     A            TRAIL          5  0     43EDTCDE(Z)
     A                                      SPACEA(001)
