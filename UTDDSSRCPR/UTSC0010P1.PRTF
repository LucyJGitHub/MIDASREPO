      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas SC Utility Printer File for SCFMBRTD')           *
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      *  PRTF/UTSC0010P1 - Midas SC Utility Printer File for SCFMBRTD *
      *                                                               *
      *  (c) Finastra International Limited 2021                      *
      *                                                               *
      *  Last Amend No. MD057248  *Create   Date 21Jan21              *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD057248 - Program CBC022001 failed to add member SDACAHL3   *
      *             to file SDACAHL3.                                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      *** Report Header Record Format  *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Finastra International Li-
     A                                      mited 2020')
      *
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A                                     6
     A                                      'REFERENCE UTSC0010P1'
     A            BJURPT        53A  O    37
     A                                   101
     A                                      'DATE'
     A            RUNA           7A      106
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
      *
     A N90                                 6
     A                                      'AUDIT'
     A                                      SPACEB(001)
     A  90                                 6
     A                                      'UPDATE'
     A                                      SPACEB(001)
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A N90                                34
     A                                      'The SDACAHL3 record that will be d-
     A                                      eleted in SCFMBRTD file'
     A                                      SPACEB(001)
     A N90                                34
     A                                      '-----------------------------------
     A                                      ------------------------'
     A                                      SPACEB(001)
      *
      *------------------------*
      ***  Detail Sub-Headings *
      *------------------------*
      *
     A  90                                41
     A                                      'The deleted SDACAHL3 record in SCF-
     A                                      MBRTD file'
     A                                      SPACEB(001)
     A  90                                41
     A                                      '-----------------------------------
     A                                      ----------'
     A                                      SPACEB(001)
     A                                     8
     A                                      'File Name'
     A                                      SPACEB(001)
     A                                    21
     A                                      'Member Name'
     A                                    35
     A                                      'Member Text'
     A                                    88
     A                                      'Physical File'
     A                                   104
     A                                      'Permanent'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAILP1
      *
     A                                      SPACEB(001)
     A            PRFILN        10A  O     8
     A            PRMBRN        10A  O    21
     A            PRMTXT        50A  O    35
     A            PRPFIL        10A  O    88
     A            PRPERM         1A  O   104
      *
      *-------------------*
      *** No Details      *
      *-------------------*
      *
     A          R NODETP1
     A                                    49
     A                                      '*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(003)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TOTALP1
     A                                      SPACEB(003)
      *
     A  90                                47
     A                                      'No. of deleted record in GPTRAPPD:'
     A N90                                41
     A                                      'No. of record to be deleted in SCF-
     A                                      MBRTD:'
     A            TOTAL1         3  0     82
     A                                      EDTWRD(' 0 ')
     A                                    56
     A                                      '*** END OF REPORT ***'
     A                                      SPACEB(001)
      *
      *-------------------*
      *** Database Error  *
      *-------------------*
      *
     A          R DBERROR
     A                                      SPACEB(001)
     A                                    47
     A                                      'DATABASE ERROR'
     A            DBASE          3  0     62
     A                                    66
     A                                      'IN PROGRAM'
     A            DBPGM         10        77
     A                                    47
     A                                      'FILE'
     A                                      SPACEB(001)
     A            DBFILE         8        52
     A                                    62
     A                                      'KEY'
     A            DBKEY         29        66
      *
