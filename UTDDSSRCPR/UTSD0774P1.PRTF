      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Printer file 1 for utility of MD-38430')               *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trades Module                             *
      *                                                               *
      *  PRTF/UTSD0774P1 - Printer File for UTSD0774                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2016            *
      *                                                               *
      *  Last Amend No. MD038430 *Create   Date 19May16               *
      *  Prev Amend No. MDnnnnnn           Date nnXxxnn               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD038430 - Unable to grant BFTC HKG user Company Codes.      *
      *             Reporting Utility for SDCOMPPD / GZSDCOMPPD /     *
      *             TABLET3.                                          *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *****************************************************************
     A          R UTPAGHDR
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2016')
     A                                      SKIPB(003)
     A                                    31
     A                                      '(c) Misys International Banking Sy-
     A                                      stems Ltd. 2016'
     A          R UTTITLE
     A                                     2
     A                                      'Reference:'
     A                                      SPACEB(001)
     A                                    12'UTSD0774P1'
     A                                      TEXT('*PROGRAM')
     A N30                                30'SD COMPANY CODES -
     A                                      FOR UPDATE (AUDIT MODE)'
     A  30                                30'SD COMPANY CODES UPDATED -
     A                                      (UPDATE MODE)'
     A                                    95
     A                                      'Date'
     A            UTMRDT         7       100
     A                                      TEXT('Midas Run Date')
     A                                   110
     A                                      'Page'
     A                                   116
     A                                      PAGNBR
     A                                      EDTCDE(3)
     A                                      SPACEA(001)
     A                                    31
     A                                      'Old Company Code'
     A                                      SPACEB(001)
     A                                    68
     A                                      'New Company Code'
      *****************************************************************
     A          R UTDTLRCD
     A                                      TEXT('Detail line.')
     A                                      SPACEB(001)
     A            UTOLDCC        3        36
     A                                      TEXT('Old Company Code')
     A            UTNEWCC        3        73
     A                                      TEXT('New Company Code')
      *
      *****************************************************************
     A          R UTFINTTL
      *****************************************************************
     A                                      TEXT('Total No. of Company Codes')
     A                                      SPACEB(001)
      *****************************************************************
     A  67                                43
     A                                      '*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(001)
     A N67N30                              2
     A                                      'Total Number of Company Codes -
     A                                      for update on SDCOMPPD/GZSDCOMPPD/-
     A                                      TABLET3: '
     A                                      SPACEB(002)
     A N67 30                              2
     A                                      'Total Number of Company Codes -
     A                                      updated on SDCOMPPD/GZSDCOMPPD/-
     A                                      TABLET3: '
     A                                      SPACEB(002)
     A N67        UTCOUNT        6  0     74
     A                                      TEXT('Total SDCOMPPD/GZSDCOMPPD/-
     A                                      TABLET3 Records')
     A                                      EDTCDE(4)
      *****************************************************************
     A          R UTENDRPT
      *****************************************************************
     A                                      TEXT('End of Report.           ')
     A                                      SPACEB(002)
      *****************************************************************
     A                                    45
     A                                      '*** END OF REPORT ***'
      *****************************************************************
