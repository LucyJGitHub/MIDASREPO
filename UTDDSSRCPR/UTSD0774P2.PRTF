      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Printer file 3 for utility of MD-38430')               *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trades Module                             *
      *                                                               *
      *  PRTF/UTSD0774P2 - Printer File 2 for UTSD0774                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2016            *
      *                                                               *
      *  Last Amend No. MD038430 *Create   Date 19May16               *
      *  Prev Amend No. MDnnnnnn           Date nnXxxnn               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD038430 - Unable to grant BFTC HKG user Company Codes.      *
      *             Reporting Utility for other files using company   *
      *             codes for update.                                 *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *****************************************************************
     A          R UTPAGHDR2
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2016')
     A                                      SKIPB(003)
     A                                    31
     A                                      '(c) Misys International Banking Sy-
     A                                      stems Ltd. 2016'
     A          R UTTITLE2
     A                                     2
     A                                      'Reference:'
     A                                      SPACEB(001)
     A                                    12'UTSD0774P2'
     A                                      TEXT('*PROGRAM')
     A N30                                30'MIDAS FILES USING COMPANY CODES -
     A                                      FOR UPDATE (AUDIT MODE)'
     A  30                                30'MIDAS FILES USING COMPANY CODES -
     A                                      UPDATED (UPDATE MODE)'
     A                                    95
     A                                      'Date'
     A            UTMRDT         7       100
     A                                      TEXT('Midas Run Date')
     A                                   110
     A                                      'Page'
     A                                   116
     A                                      PAGNBR
     A                                      EDTCDE(3)
     A                                      SPACEA(001)
     A                                    31
     A                                      'Midas File Name'
     A                                      SPACEB(001)
     A N30                                68
     A                                      'Number of Records for Update'
     A  30                                68
     A                                      'Number of Records Updated'
      *****************************************************************
     A          R UTDTLRCD2
     A                                      TEXT('Detail line.')
     A                                      SPACEB(001)
     A            UTMFILE       10        38
     A                                      TEXT('Midas File Name')
     A            UTFCOUNT       6  0     78
     A                                      TEXT('Number of Records')
     A                                      EDTCDE(4)
      *
      *****************************************************************
     A          R UTFINTTL2
      *****************************************************************
     A                                      TEXT('Total No. of Files using -
     A                                      Company Codes')
     A                                      SPACEB(001)
      *****************************************************************
     A  67                                43
     A                                      '*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(001)
     A N67N30                              2
     A                                      'Total Number of Midas Files using -
     A                                      Company Codes for update: '
     A                                      SPACEB(002)
     A N67 30                              2
     A                                      'Total Number of Midas Files using -
     A                                      Company Codes updated: '
     A                                      SPACEB(002)
     A N67        UTTFCOUNT      6  0     74
     A                                      TEXT('Total Midas Files')
     A                                      EDTCDE(4)
      *****************************************************************
     A          R UTENDRPT2
      *****************************************************************
     A                                      TEXT('End of Report.           ')
     A                                      SPACEB(002)
      *****************************************************************
     A                                    45
     A                                      '*** END OF REPORT ***'
      *****************************************************************
