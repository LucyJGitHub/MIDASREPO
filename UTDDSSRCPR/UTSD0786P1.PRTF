      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas  SD MD-39670 Utility program for SDFTCSPD')      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/UTSD0784P1 -  SD MD-39670 Utility program for SDFTCSPD  *
      *                                                               *
      *  (c) Finastra International Limited 2016                      *
      *                                                               *
      *  Last Amend No. MD039670  *CREATE  Date 26Jul16               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD039670 - FATCA Housekeeping program                        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R UTSD0786H                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2016')
     A                                      SKIPB(1)
     A                                     6'REFERENCE UTSD0786P1'
     A N10                                27'- UPDATE'
     A  10                                27'- AUDIT'
     A            BJURPT    R   53        38REFFLD(URPT)
     A                                   101'DATE:'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                    50'FATCA Customer Details'
     A                                      SPACEB(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                    40'-----------------------------------
     A                                      -'
     A                                      SPACEA(2)
      *
     A          R UTSD0786H2
     A                                    10'CUSTOMER NO'
     A                                    23'CLASS CODE'
     A                                    35'CLASS DATE'
     A                                    46'PREV CLASS CODE'
     A                                      SPACEA(1)
      *
     A          R UTSD0786D
     A                                      SPACEB(1)
     A            WCUST          6A       10
     A            WCLAC          7A  O    22
     A            WCLAD          7A  O    35
     A            WPCLA         10A  O    46
     A  10                                63'RECORD FOR DELETE'
     A N10                                63'RECORD WAS DELETED'
     A                                      SPACEA(1)
      *
     A          R UTSD0786T
     A                                      SPACEB(1)
     A  10                                10'NUMBER OF RECORDS TO BE DELETED'
     A N10                                10'NUMBER OF RECORDS WERE DELETED'
     A                                    42
     A                                      '='
     A            WCTR           6S 0O    45
     A                                      EDTCDE(3)
     A                                      SPACEA(1)
     A          R UTSD0786E
     A                                      SPACEB(1)
     A                                    54'*** END OF REPORT ***'

