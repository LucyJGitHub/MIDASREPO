      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas  SD MD-51784 Data Patch Printer File')           *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/UTSD0815P1 -  SD MD-51784 Data Patch Printer File       *
      *                                                               *
      *  (c) Finastra International Limited 2019                      *
      *                                                               *
      *  Last Amend No. MD051784  *CREATE  Date 20Feb19               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD051784 - FBM Live COB: SDC000776 Customers Status Update   *
      *             at Effective Date 2 FAILED                        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R UTSD0815H1                TEXT('(c) Finastra Internatio +
     A                                      nal Limited 2019')
     A                                      SKIPB(1)
     A                                     6'REFERENCE UTSD0815P1'
     A N10                                27'- UPDATE'
     A  10                                27'- AUDIT'
     A            BJURPT    R   53        38REFFLD(URPT)
     A                                   101'DATE:'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                    24'Deletion of Other Customer Files'
     A                                      SPACEB(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                    24'--------------------------------'
     A                                      SPACEA(2)
      *
     A          R UTSD0815H2
     A                                    02'CUSTOMER'
     A                                    12' COUNTRY'
     A                                      SPACEA(1)
     A                                    02'NUMBER'
     A                                    12'OF TAX'
     A                                    21'REMARKS'
     A                                      SPACEA(1)
      *
     A          R UTSD0815D1
     A                                      SPACEB(1)
     A            PCUST          6A       03
     A            PCTTX          8A       12
     A            PREM          50A       21
      *
     A          R UTSD0815D2
     A                                      SPACEB(1)
     A            PREM2         50A       21
     A*                                     SPACEA(1)
      *
     A          R UTSD0815T
     A                                      SPACEB(1)
     A  10                                10'NUMBER OF RECORDS TO BE DELETED'
     A N10                                10'NUMBER OF RECORDS WERE DELETED'
     A                                    42
     A                                      '='
     A            PCTR           6S 0O    45
     A                                      EDTCDE(3)
     A                                      SPACEA(1)
     A          R UTSD0815E
     A                                      SPACEB(1)
     A                                    54'*** END OF REPORT ***'

