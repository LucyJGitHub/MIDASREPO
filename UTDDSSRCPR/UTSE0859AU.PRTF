      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE SECTYDQD File Utility Audit')                 *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  UTSE0859AU - SE Details JMS Enabling Extn File Utility Audit *
      *                                                               *
      *  (c) Finastra International Limited 2021                      *
      *                                                               *
      *  Last Amend No. MD058313*CREATE    Date 17Apr23               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD058313 - Utility program to add records in SECTYDQD        *
      *             Applied for MD-61078.                             *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      **  Report Header Record Format
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited 2021')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE UTSE0859AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    43'SECURITY DETAILS EXTENSION FILE -
     A                                       - AUDIT'
     A                                      SPACEA(1)
      *
     A                                    40'-----------------------------------
     A                                      -------------'
     A                                      SPACEA(3)
      *
      ** File Control
      *
     A          R CONTROL
      *
     A N60                                20'Total no. of records to add'
     A  60                                20'Total no. of records added'
     A            RINSRT        10  0     70EDTCDE(3)
     A                                      SPACEA(1)
     A                                    20'Total no. of records on file'
     A            RTOTAL        10  0     70EDTCDE(3)
     A                                      SPACEA(1)
      *
      ** No Details
      *
     A          R NODTLS
     A                                    49'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      ** Database Error
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
