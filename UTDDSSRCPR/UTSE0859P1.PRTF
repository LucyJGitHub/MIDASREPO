      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE SECTYDQD File Utility Report')                *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  UTSE0859P1 - SE Details JMS Enabling Extn File Utility Report*
      *                                                               *
      *  (c) Finastra International Limited 2021                      *
      *                                                               *
      *  Last Amend No. MD058313*CREATE    Date 17Apr23               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD058313 - Utility program to add records in SECTYDQD        *
      *             Applied for MD-61078.                             *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited 2021')
      *
     A                                      SKIPB(2)
     A                                     6'REFERENCE UTSE0859P1'
     A                                      SPACEB(1)
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A            RMODE         11         6
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A  60                                47'SECURITY DETAILS ADDED IN-
     A                                       SECTYDQD FILE'
     A                                      SPACEA(1)
     A N60                                47'SECURITY DETAILS TO ADD IN-
     A                                       SECTYDQD FILE'
     A                                      SPACEA(1)
     A                                    47'-----------------------------------
     A                                      ------'
     A                                      SPACEA(3)
     A                                    48'Security Shortname'
     A                                    77'Status'
     A                                      SPACEA(1)
     A                                    48'------------------'
     A                                    77'------'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
     A            RSESN         10        52
     A            RSTAT          7        77
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
