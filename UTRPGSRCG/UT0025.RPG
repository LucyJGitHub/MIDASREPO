     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas UT Display message file')
      *****************************************************************
      *                                                               *
      *  Midas - UT Module                                            *
      *                                                               *
      *  UT0025 - Display message files                               *
      *                                                               *
      *  Called By: DSPMSGF                                           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CPK018             Date 25Apr04               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CPK015             Date 06Dec01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CPK009             Date 20Jul99               *
      *                 CUP002 *CREATE     Date 18Apr99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CPK018 - MidasPlus packaging.  Moved to global layer.        *
      *  CPK015 - Release 4 Alpha site: CPK009 doesn't work at V5R1.  *
      *  CPK009 - DBA R3 packaging:                                   *
      *         - fix IBM's fix re API offsets                        *
      *  CUP002 - Message files utilities                             *
      *                                                               *
      *****************************************************************
      *
      * Use of indicators
      * 71 - No Message files found
      *****************************************************************
     FUTDSPMPDO   E                    DISK                           UC
      /EJECT
      * Array containing Copyright statement
     E                    CPY@    1   1 80
      *
      * Parameters for retrieval of user space DSPMSGF
     IUSNAME      DS
     I                                        1  10 US
     I                                       11  20 USLIB
      *
      * Parameters for retrieval of user space header
     IUSHEAD      DS
     I                                    B 125 1280OFFLST
     I                                    B 133 1360NOENTS
     I                                    B 137 1400ENTSIZ
      *
      * Parameters for retrieval of user space list data
     IMSGFLL      DS
     I                                        1  10 DMFILE
     I                                       11  20 DMLIBR
      * Parameters for user space retrieval API
     I            DS
     I                                    B   1   40USSTRT
     I                                    B   5   80USLEN
     I                                        9 158 USDTA
      *
      * Values returned from Message Retrieval API
     IMSGINF      DS                           6605
     I                                    B   1   40XMBYTR
     I                                    B   5   80XMBYTA
     I                                    B   9  120XMMSEV
     I                                    B  13  160XMAIND
     I                                       17  25 DMAOPT
     I                                       26  26 DMLIND
     I                                       27  33 DMMIDR
     I                                    B  37  400XMSFMT
     I                                    B  41  440XMCCSS
     I                                    B  45  480XMCCSR
     I                                    B  49  520XMCCST
     I                                    B  53  560XMDFTO
     I                                    B  57  580XMDFTL
     I                                    B  61  640XMDFTA
     I                                    B  65  680XMMSGO
     I                                    B  69  720XMMSGL
     I                                    B  73  760XMMSGA
     I                                    B  77  800XMHLPO
     I                                    B  81  840XMHLPL
     I                                    B  85  880XMHLPA
     I                                    B  89  920XMSUBO
     I                                    B  93  960XMSUBL
     I                                    B  97 1000XMSUBA
     I                                    B 101 1040XMFMTL
      *
      * Field definitions for message texts (DS definition needed if
      * field length is greater than 256)
     IDMMSGT      DS                            150
     IDMHLPT      DS                            540
     IDMDFTT      DS                             60
      *
      * Formats for substitution variables returned - (some in
      * binary format)
     ISUBBIN      DS
     I                                    B   1   40DMSB1A
     I                                    B   5   80DMSB1B
     I                                        9  18 DMSB1C
     I                                    B  19  200DMSB1D
     I                                    B  21  240DMSB2A
     I                                    B  25  280DMSB2B
     I                                       29  38 DMSB2C
     I                                    B  39  400DMSB2D
     I                                    B  41  440DMSB3A
     I                                    B  45  480DMSB3B
     I                                       49  58 DMSB3C
     I                                    B  59  600DMSB3D
     I                                    B  61  640DMSB4A
     I                                    B  65  680DMSB4B
     I                                       69  78 DMSB4C
     I                                    B  79  800DMSB4D
     I                                    B  81  840DMSB5A
     I                                    B  85  880DMSB5B
     I                                       89  98 DMSB5C
     I                                    B  99 1000DMSB5D
      *
      * Formats for substitution variables decimalised
     ISUBDTA      DS
     I                                        1  24 DMSUB1
     I                                       25  48 DMSUB2
     I                                       49  72 DMSUB3
     I                                       73  96 DMSUB4
     I                                       97 120 DMSUB5
     I                                        1   3 DMSD1A
     I                                        4   6 DMSD1B
     I                                        7  16 DMSD1C
     I                                       17  21 DMSD1D
     I                                       22  24 DMSD1E
     I                                       25  27 DMSD2A
     I                                       28  30 DMSD2B
     I                                       31  40 DMSD2C
     I                                       41  46 DMSD2D
     I                                       47  48 DMSD2E
     I                                       49  51 DMSD3A
     I                                       52  54 DMSD3B
     I                                       55  64 DMSD3C
     I                                       65  69 DMSD3D
     I                                       70  72 DMSD3E
     I                                       73  75 DMSD4A
     I                                       76  78 DMSD4B
     I                                       79  88 DMSD4C
     I                                       89  93 DMSD4D
     I                                       94  96 DMSD4E
     I                                       97  99 DMSD5A
     I                                      100 102 DMSD5B
     I                                      103 112 DMSD5C
     I                                      113 117 DMSD5D
     I                                      118 120 DMSD5E
     I                                      121 123 DMST1A
     I                                      124 126 DMST1B
     I                                      127 129 DMST2A
     I                                      130 132 DMST2B
     I                                      133 135 DMST3A
     I                                      136 138 DMST3B
     I                                      139 141 DMST4A
     I                                      142 144 DMST4B
     I                                      145 147 DMST5A
     I                                      148 150 DMST5B
      *
      *   Setup PARM variables as Binary
     ICLRBIN      DS
     I                                    B   1   40REPLEN
     I                                    B   5   80CCSX
     I                                    B   9  120CCSY
     I                                    B  13  160INFLEN
     I                                    B  17  200RCVLEN                                    CPK009
      *
      *   Error DS for error messages from API
     IERROR       DS
     I                                    B   1   40BYTPRV
     I                                    B   5   80BYTAVA
     I                                        9  15 ERRID
     I                                       16  16 ERRXXX
     I                                       17 272 INSDTA
      *
      *   DS to extract release level                                                         CPK009
     IRCVVAR      DS                             88                                           CPK009
     I                                       21  210VERNO                                     CPK009
     I                                       23  230RELNO                                     CPK009
     I                                       25  250MNO                                       CPK009
      *                                                                                       CPK009
      *  Named constant to retrieve release no                                                CPK009
     I              '*OPSYS *CUR  0000*CO-C         CONST                                     CPK009
     I              'DE'                                                                      CPK009
      *****************************************************************
      *
      * Set up copyright parameter
     C                     MOVEACPY@      CPY2@  80
      *
      * Use API to get release level - QMHRTVM was altered at V4R3                            CPK009
     C                     CLEARCLRBIN                                                        CPK009
     C                     MOVELCONST     PRDINF                                              CPK009
     C                     CALL 'QSZRTVPR'                                                    CPK009
     C                     PARM           RCVVAR                                              CPK009
     C                     PARM 88        RCVLEN                                              CPK009
     C                     PARM 'PRDR0100'RCVFMT  8                                           CPK009
     C                     PARM           PRDINF 27                                           CPK009
     C                     PARM           ERROR                                               CPK009
      *                                                                                       CPK009
      *  If system is post-V4R3 offset values returned by QMHRTVM are one                     CPK009
      * less than earlier systems. (If API not found system is pre-V2R3.)                     CPK009
      * Therefore need to increase the offset for later systems.                              CPK009
     C           VERNO     IFGE 4                                                             CPK009
     C           RELNO     ANDGE3                                                             CPK009
     C           VERNO     ORGT 4                                                             CPK015
     C                     Z-ADD1         OFFSET  10                                          CPK009
     C                     ENDIF                                                              CPK009
      *
     C                     OPEN UTDSPMPD
      *
      * Use API to retrieve list data from user space; first call gets
      * user space header information (See I specs for DS)
     C                     MOVEL'DSPMSGF' US
     C                     MOVEL'QTEMP'   USLIB
     C                     CALL 'QUSRTVUS'
     C                     PARM           USNAME
     C                     PARM 1         USSTRT
     C                     PARM 140       USLEN
     C           USHEAD    PARM           USDTA
      *
      * If no entries then set on indicator and skip further processing
     C           NOENTS    IFEQ 0                          B01
     C                     MOVE *ON       *IN71
     C                     ENDIF                           E01
      *
      * Execute SR to loop through list data from user space if entries
     C           *IN71     IFEQ *OFF                       B01
     C                     EXSR GETDTA
     C                     ENDIF                           E01
      *
     C                     CLOSEUTDSPMPD
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
      *****************************************************************
      *
     C           GETDTA    BEGSR
      * Use API to retrieve list data from user space; set up control
     C                     Z-ADDENTSIZ    USLEN
     C                     Z-ADD0         DMFINO  50
     C           OFFLST    ADD  1         USSTRT
      *
     C           DMFINO    DOWLENOENTS                     D01
     C                     ADD  1         DMFINO
     C                     MOVE DMFILE    DMFILT 10
     C                     CLEARUSDTA
      *
     C                     CALL 'QUSRTVUS'
     C                     PARM           USNAME
     C                     PARM           USSTRT
     C                     PARM           USLEN
     C           MSGFLL    PARM           USDTA
      *
      * Use API to retrieve messages from files
      * Set initial PARMs & remove decimal data errors
     C                     CLEARERROR
     C                     CLEARCLRBIN
     C                     MOVEL'*FIRST'  RTVOPT
     C                     Z-ADD272       BYTPRV           Error setup
     C                     Z-ADD1         DMMGNO  50       Msg counter
     C                     Z-ADD0         DMMOFL  50       OVERFLOW
     C                     Z-ADD0         DMHOFL  50       OVERFLOW
     C                     Z-ADD0         DMSOFL  50       OVERFLOW
     C                     Z-ADD0         DMDOFL  50       OVERFLOW
     C                     Z-ADD0         DMTOTL  50       OVERFLOW
      *
      *  If DMFILE not already found in previous directory,
      *  Call API until error message received ( = no message found)
     C           DMFILE    IFNE DMFILT
     C           ERRID     DOWEQ*BLANK                     D02
      *
     C                     MOVE DMMIDR    DMMIDS  7
     C                     CLEARMSGINF
     C                     CLEARDMMSGT
     C                     CLEARDMHLPT
     C                     CLEARSUBBIN
     C                     CLEARSUBDTA
     C                     CALL 'QMHRTVM'
     C                     PARM           MSGINF
     C                     PARM 6605      INFLEN
     C                     PARM 'RTVM0300'RTVFMT  8
     C                     PARM           DMMIDS
     C                     PARM           MSGFLL
      *
      *  Facility to replace substitution variables
     C                     PARM           REPDTA 70
     C                     PARM           REPLEN
     C                     PARM '*NO'     REPVAL 10
      *
     C                     PARM '*YES'    FMTCTL 10
     C                     PARM           ERROR
     C                     PARM           RTVOPT 10
     C                     PARM           CCSX
     C                     PARM           CCSY
      *
      * Write message if MSGID found
     C           DMMIDR    IFNE *BLANKS                    B01
      *
      * Reformat binaries
     C                     Z-ADDXMBYTR    DMBYTR  40
     C                     Z-ADDXMBYTA    DMBYTA  40
     C                     Z-ADDXMMSEV    DMMSEV
     C                     Z-ADDXMAIND    DMAIND  40
     C                     Z-ADDXMSFMT    DMSFMT
     C                     Z-ADDXMCCSS    DMCCSS  50
     C                     Z-ADDXMCCSR    DMCCSR  50
     C                     Z-ADDXMCCST    DMCCST  50
     C                     Z-ADDXMDFTO    DMDFTO  40
     C                     Z-ADDXMDFTL    DMDFTL  40
     C                     Z-ADDXMDFTA    DMDFTA  40
     C                     Z-ADDXMMSGO    DMMSGO  40
     C                     Z-ADDXMMSGL    DMMSGL  40
     C                     Z-ADDXMMSGA    DMMSGA  40
     C                     Z-ADDXMHLPO    DMHLPO  40
     C                     Z-ADDXMHLPL    DMHLPL  40
     C                     Z-ADDXMHLPA    DMHLPA  40
     C                     Z-ADDXMSUBO    DMSUBO  40
     C                     Z-ADDXMSUBL    DMSUBL  40
     C                     Z-ADDXMSUBA    DMSUBA  40
     C                     Z-ADDXMFMTL    DMFMTL  40
      *
      *  Extract message details
     C           DMMSGL    IFGT 0                          B02
     C***********          Z-ADDDMMSGO    MO      40                                          CPK009
     C           OFFSET    ADD  DMMSGO    MO      40                                          CPK009
     C           DMMSGL    SUBSTMSGINF:MO DMMSGT
     C                     ENDIF                           E02
      *
     C           DMHLPL    IFGT 0                          B02
     C***********          Z-ADDDMHLPO    HO      40                                          CPK009
     C           OFFSET    ADD  DMHLPO    HO      40                                          CPK009
     C           DMHLPL    SUBSTMSGINF:HO DMHLPT
     C                     ENDIF                           E02
      *
     C           DMSUBL    IFGT 0                          B02
     C***********          Z-ADDDMSUBO    SO      40                                          CPK009
     C           OFFSET    ADD  DMSUBO    SO      40                                          CPK009
     C           DMSUBL    SUBSTMSGINF:SO SUBBIN
      *
      * Call SR to convert substitution variables to printable data
     C                     EXSR SUBVAR
     C                     ENDIF                           E02
      *
     C           DMDFTL    IFGT 0                          B02
     C***********          Z-ADDDMDFTO    DO      40                                          CPK009
     C           OFFSET    ADD  DMDFTO    DO      40                                          CPK009
     C           DMDFTL    SUBSTMSGINF:DO DMDFTT
     C                     ENDIF                           E02
      *
      *  Check whether all messages written to file
     C           DMMSGL    IFGT 150
     C                     ADD  1         DMMOFL
     C                     ADD  1         DMTOTL
     C                     ENDIF
     C           DMHLPL    IFGT 540
     C                     ADD  1         DMHOFL
     C                     ADD  1         DMTOTL
     C                     ENDIF
     C           DMSUBL    IFGT 100
     C                     ADD  1         DMSOFL
     C                     ADD  1         DMTOTL
     C                     ENDIF
     C           DMDFTL    IFGT 60
     C                     ADD  1         DMDOFL
     C                     ADD  1         DMTOTL
     C                     ENDIF
     C                     WRITEUTDSPMD0
     C                     ENDIF                           E01
      *
      *Reset parameters/fields
     C                     ADD  1         DMMGNO           MSG count
     C                     MOVEL'*NEXT '  RTVOPT
     C                     ENDDO                           ED02
     C                     ENDIF
      *
     C                     ADD  ENTSIZ    USSTRT
     C                     ENDDO                           ED01
      *
     C                     ENDSR
      *
      *****************************************************************
      *
      * SUBVAR Translate substitution variable formats from binary
      * to decimal - first 5 formats only
      *
      *****************************************************************
      *
     C           SUBVAR    BEGSR
      *
      * Convert binary data to decimal; if *VARY indicator is on
      * (-1 in Length field), reformat.
     C                     CLEARSUBDTA
      *
      * Move field types to output fields
     C                     MOVELDMSB1C    DMSD1C
     C                     MOVELDMSB2C    DMSD2C
     C                     MOVELDMSB3C    DMSD3C
     C                     MOVELDMSB4C    DMSD4C
     C                     MOVELDMSB5C    DMSD5C
      *
      * Move field length/*VARY indicators to temporary storage
     C                     Z-ADDDMSB1A    DMSD1T  30
     C                     Z-ADDDMSB2A    DMSD2T  30
     C                     Z-ADDDMSB3A    DMSD3T  30
     C                     Z-ADDDMSB4A    DMSD4T  30
     C                     Z-ADDDMSB5A    DMSD5T  30
      *
      * Move binary attribute fields to text & remove leading zeros
     C                     MOVE DMSB1A    DMST1A
     C           '0'       CHECKDMST1A    CHK     10
     C           CHK       IFGT 0
     C                     SUBSTDMST1A:CHKDMSD1A
     C                     ENDIF
      *
     C                     MOVE DMSB1B    DMST1B
     C           '0'       CHECKDMST1B    CHK
     C           CHK       IFGT 0
     C                     SUBSTDMST1B:CHKDMSD1B
     C                     ENDIF
      *
     C                     MOVE DMSB2A    DMST2A
     C           '0'       CHECKDMST2A    CHK
     C           CHK       IFGT 0
     C                     SUBSTDMST2A:CHKDMSD2A
     C                     ENDIF
      *
     C                     MOVE DMSB2B    DMST2B
     C           '0'       CHECKDMST2B    CHK
     C           CHK       IFGT 0
     C                     SUBSTDMST2B:CHKDMSD2B
     C                     ENDIF
      *
     C                     MOVE DMSB3A    DMST3A
     C           '0'       CHECKDMST3A    CHK
     C           CHK       IFGT 0
     C                     SUBSTDMST3A:CHKDMSD3A
     C                     ENDIF
      *
     C                     MOVE DMSB3B    DMST3B
     C           '0'       CHECKDMST3B    CHK
     C           CHK       IFGT 0
     C                     SUBSTDMST3B:CHKDMSD3B
     C                     ENDIF
      *
     C                     MOVE DMSB4A    DMST4A
     C           '0'       CHECKDMST4A    CHK
     C           CHK       IFGT 0
     C                     SUBSTDMST4A:CHKDMSD4A
     C                     ENDIF
      *
     C                     MOVE DMSB4B    DMST4B
     C           '0'       CHECKDMST4B    CHK
     C           CHK       IFGT 0
     C                     SUBSTDMST4B:CHKDMSD4B
     C                     ENDIF
      *
     C                     MOVE DMSB5A    DMST5A
     C           '0'       CHECKDMST5A    CHK
     C           CHK       IFGT 0
     C                     SUBSTDMST5A:CHKDMSD5A
     C                     ENDIF
      *
     C                     MOVE DMSB5B    DMST5B
     C           '0'       CHECKDMST5B    CHK
     C           CHK       IFGT 0
     C                     SUBSTDMST5B:CHKDMSD5B
     C                     ENDIF
      *
      * Extract field definitions, depending on *VARY indicators
     C           DMSD1T    IFEQ -1
     C                     MOVEL'*VARY'   DMSD1D
     C                     MOVE DMSD1B    DMSD1E
     C                     CLEARDMSD1A
     C                     CLEARDMSD1B
     C                     ENDIF
     C           DMSD1T    IFEQ 0
     C                     CLEARDMSD1A
     C                     CLEARDMSD1B
     C                     ENDIF
      *
     C           DMSD2T    IFEQ -1
     C                     MOVEL'*VARY'   DMSD1D
     C                     MOVE DMSD2B    DMSD1E
     C                     CLEARDMSD2A
     C                     CLEARDMSD2B
     C                     ENDIF
     C           DMSD2T    IFEQ 0
     C                     CLEARDMSD2A
     C                     CLEARDMSD2B
     C                     ENDIF
      *
     C           DMSD3T    IFEQ -1
     C                     MOVEL'*VARY'   DMSD3D
     C                     MOVE DMSD3B    DMSD3E
     C                     CLEARDMSD3A
     C                     CLEARDMSD3B
     C                     ENDIF
     C           DMSD3T    IFEQ 0
     C                     CLEARDMSD3A
     C                     CLEARDMSD3B
     C                     ENDIF
      *
     C           DMSD4T    IFEQ -1
     C                     MOVEL'*VARY'   DMSD4D
     C                     MOVE DMSD4B    DMSD4E
     C                     CLEARDMSD4A
     C                     CLEARDMSD4B
     C                     ENDIF
     C           DMSD4T    IFEQ 0
     C                     CLEARDMSD4A
     C                     CLEARDMSD4B
     C                     ENDIF
      *
     C           DMSD5T    IFEQ -1
     C                     MOVEL'*VARY'   DMSD5D
     C                     MOVE DMSD5B    DMSD5E
     C                     CLEARDMSD5A
     C                     CLEARDMSD5B
     C                     ENDIF
     C           DMSD5T    IFEQ 0
     C                     CLEARDMSD5A
     C                     CLEARDMSD5B
     C                     ENDIF
      *
      *
     C                     ENDSR
      *****************************************************************
**  CPY@
(c) Finastra International Limited 2001
