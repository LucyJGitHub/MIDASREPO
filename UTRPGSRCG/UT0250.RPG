     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas UT CPP for RTVUSRSPCI')
      *****************************************************************
      *                                                               *
      *  Midas - Utilities module                                     *
      *                                                               *
      *  UT0250 - CPP for RTVUSRSPCI (Retreive user space             *
      *           initialisation) commadn                             *
      *                                                               *
      *  Function:  This program returns the initial values from a    *
      *             user space.                                       *
      *                                                               *
      *  Called By: RTVUSRSPCI command.                               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CAA002 *CREATE     Date 31OCT95               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAA002 - Development of utility to save, edit and restore    *
      *           spooled files.                                      *
      *                                                               *
      *****************************************************************
      /EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      /SPACE 3
     I*
     IDSBGN       DS
     I                                    B   1   40BGNPOS
     IDSLEN       DS
     I                                    B   1   40DTALEN
     IDSRCV       DS
     I                                    B   1   40STRPS
     I                                        1   4 STRPOS
     I                                    B   9  120NBREN
     I                                    B  13  160ENTLN
     I                                       13  16 ENTLEN
     C*
     C** Set up copyright parameter
     C*
     C                     MOVEACPY@      ACT@   80
     C*
     C* Entry parameter list
     C           *ENTRY    PLIST                           Parm list
     C                     PARM           FULSPC 20        Full spc nam
     C                     PARM           STRP    4        Start pos
     C                     PARM           NBRENT  90       Nbr of entrs
     C                     PARM           ENTL    4        Entry len
     C*
     C** Initialize for the call to QUSRTVUS
     C                     Z-ADD125       BGNPOS           Begin pos
     C                     Z-ADD16        DTALEN           Data length
     C*
     C** Call QUSRTRVUS to get initial values for the list
     C** First three parms are passed to the pgm
     C** DSRCV data structure is passed back
     C                     CALL 'QUSRTVUS'                 Retrieve
     C                     PARM           FULSPC           Full spc nam
     C                     PARM           DSBGN            Begin pos
     C                     PARM           DSLEN            Data length
     C                     PARM           DSRCV            Receive DS
     C*
     C** Convert number of entries to decimal
     C                     Z-ADDNBREN     NBRENT           Make decimal
     C*
     C** Add 1 to start position for first actual position
     C                     ADD  1         STRPS            Bump
     C*
     C** Move DS character fields to PLIST parameters
     C                     MOVE STRPOS    STRP             List offset
     C                     MOVE ENTLEN    ENTL             Entry length
     C*
     C** Return with LR on
     C                     SETON                     LR    Set LR
     C                     RETRN                           Return
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
