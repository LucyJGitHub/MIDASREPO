     H        1
      *****************************************************************
/*OVR *  CRTPF FILE(QTEMP/HOOKS) RCDLEN(112) MBR(*FILE)               *
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas UT Produce list of hooks in source member')
      *****************************************************************
      *                                                               *
      *  Midas - UT Module                                            *
      *                                                               *
      *  UT0060 - Produce list of hooks and/or /COPYs in src. member  *
      *                                                               *
      *  Called By: UTC0060                                           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CPK008             Date 13Feb97               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CPK008 - DBA R2 packaging:                                   *
      *         - Changes for source being 112 long (recompile).      *
      *         - Changes to cope with ILE souce types                *
      *                                                               *
      *****************************************************************
      *
      * Use of indicators
      * 31 - RPG source
      * 32 - CL source
      * 33 - DDS source
      * 37 - Overflow
      * 38 - No details to report
      * 39 - Sub-total line
      * 40 - Error in file handling
      * 45 - Loop control for reading file
      * 51 - Loop control for finding library containing source file
      * 61 - Hooks only requested
      * 71 - Source file not found
      * 72 - 1st occurrence of source file or first detail on page
      * 73 - /COPY member found
      * 74 - /COPY member not found
      * 81 - Loop control for finding /COPY member
      *****************************************************************
     FHOOKS   IF  E                    DISK                           UC
     F            HOOKS                             KRENAMEHOOKSF
     FUT0060P1O   E             37     PRINTER                        UC
      /EJECT
      * Array containing Copyright statement
     E                    CPY@    1   1 80
      * Array to contain names of libraries containing source file
     E                    LIBA       25 10
      *
     IHOOKSF
     I              HOOKS                           HOOKS1                BHF020
      * DAta structure used to break down source file sequence number
     IHOOKS1      DS
     I                                        1   4 SQ1
     I                                        5   6 SQ2
      * Parameters for creation of user space LIBLUS
     I            DS
     I                                        1  20 NAME
     I                                        1  10 US
     I                                       11  20 USLIB
     I                                       21  30 ATTR
     I                                    B  31  340SIZE
     I                                       35  35 IVAL
     I                                       36  45 AUT
     I                                       46  95 DESC
     I                                       96 105 REPL
     I                                      106 130 ERRP
     I                                    B 106 1090ERR1
     I                                    B 110 1130ERR2
     I                                      114 120 ERR3
     I                                      121 121 ERR4
     I                                      122 131 ERR5
      * Parameters for List API
     I            DS
     I                                        1   8 FMT
     I                                        9  28 OBJNL
     I                                        9  18 CPYSRC
     I                                       19  28 OBJL
     I                                       29  38 OTYPE
      * Parameters for retrieval of user space header
     IUSHEAD      DS
     I                                        1 140 ALLDTA
     I                                    B 125 1280OFFLST
     I                                    B 133 1360NOENTS
     I                                    B 137 1400ENTSIZ
      * Parameters for retrieval of user space list data
     IUSLIST      DS
     I                                        1  10 FILEN
     I                                       11  20 FILEL
     I                                       21  30 FILET
      * Parameters for user space retrieval API
     I            DS
     I                                    B   1   40USSTRT
     I                                    B   5   80USLEN
     I                                        9 158 USDTA
      * Parameters for retrieval of member description
     I            DS
     I                                    B   1   40BRT
     I                                    B   5   80BAV
     I                                        9  18 MFILE
     I                                        1  18 MRCV
     I                                    B  19  220MLEN
     I                                       23  30 MFMT
     I                                       31  50 MFLIB
     I                                       31  40 MFIL
     I                                       41  50 MRLIB
     I                                       51  51 MOVR
      /SPACE 3
      * Set up copyright parameter
     C                     MOVEACPY@      CPY2@  80
      * Parameters passed to program
     C           *ENTRY    PLIST
     C***********          PARM           SRCTYP  7        CPK008 CL, CMD, RPG or DDS
     C                     PARM           SRCTYP 10        CPK008 CL, CLLE, CMD, RPG, RPGLE or DDS
     C                     PARM           HDRTXT 34        Header text
     C                     PARM           CPYTYP  6        Hooks or all /COPY
     C                     PARM           ERR     1        Error return
      *
      * Depending on source type set on indicator
     C           SRCTYP    IFEQ 'RPG'                      B01
     C           SRCTYP    OREQ 'RPGLE'                    CPK008
     C                     SETON                     31
     C                     ENDIF                           E01
     C           SRCTYP    IFEQ 'CLP'                      B01
     C           SRCTYP    OREQ 'CMD'
     C           SRCTYP    OREQ 'CLLE'                     CPK008
     C                     SETON                     32
     C                     ENDIF                           E01
     C           SRCTYP    IFEQ 'PRTF'                     B01
     C           SRCTYP    OREQ 'DSPF'
     C           SRCTYP    OREQ 'LF'
     C                     SETON                     33
     C                     ENDIF                           E01
      *
      * Process according to source type; find name of source file
     C           *IN31     IFEQ *ON                        B01  RPG RPGLE
     C                     Z-ADD19        Y       20
     C                     ENDIF                           E01
     C           *IN32     IFEQ *ON                        B01  CLP CLLE CMD
     C                     Z-ADD15        Y
     C                     ENDIF                           E01
     C           *IN33     IFEQ *ON                        B01  DDS
     C                     Z-ADD20        Y
     C                     ENDIF                           E01
      * If HOOKS only set on indicator
     C           CPYTYP    IFEQ '*HOOKS'                   B01
     C                     SETON                     61
     C                     ENDIF                           E01
      * Set up text for header
     C                     MOVELHDRTXT    SOURCE 34
      * Set on overflow to force first header
     C                     SETON                     37
      * Set record counter and subtotals to zero
     C                     Z-ADD0         COUNT
     C                     Z-ADD0         SBTTL
      * Open file for read
     C                     OPEN HOOKS                  40
     C                     OPEN UT0060P1               40
      *****************************************************************
      * Read until end of file
     C           *IN45     DOUEQ*ON                        D01
      *
      * Blank source file field for report
     C                     MOVE *BLANKS   CPYSRC           B01
     C           *IN37     IFEQ *ON
     C                     WRITEHEADER                 40
     C                     WRITEHEADL                  40
      * If error end program
     C           *IN40     IFEQ *ON                        B02
     C                     MOVE '1'       ERR
     C                     DUMP
     C                     SETON                     LR
     C                     RETRN
     C                     ENDIF                           E02
      *
      * Set on indicator to show that this is either the first detail
      * on the page or the first detail for a particular source file
     C                     SETON                     72
     C                     ENDIF                           E01
      *
     C                     READ HOOKSF                 4045
      * If error end program
     C           *IN40     IFEQ *ON                        B01
     C                     MOVE '1'       ERR
     C                     SETON                     LR
     C                     RETRN
     C                     ENDIF                           E01
      * If end of file and first read set on 'No details' ind.
     C           *IN45     IFEQ *ON                        B01
     C           COUNT     IFEQ 0                          B02
     C                     SETON                     38
     C                     WRITEDTLL1                  40
      * If error end program
     C           *IN40     IFEQ *ON                        B03
     C                     MOVE '1'       ERR
     C                     SETON                     LR
     C                     RETRN
     C                     ENDIF                           E03
     C                     ENDIF                           E02
     C                     ELSE                            X01  *IN45 not on
      *
      * Set up sequence number for report
     C                     MOVELSQ1       SQ3     5
     C                     MOVE '.'       SQ3
     C                     MOVELSQ3       LINENO
     C                     MOVE SQ2       LINENO
      * Look for comma to find end of CPYSRC file; add 1 for start of
      *     member name
     C           ','       SCAN HOOKS1:Y  CPOS    20
     C           CPOS      ADD  1         Z       20
      * Find length of string; move string to report field
     C           CPOS      SUB  Y         SLEN    20
     C           SLEN      SUBSTHOOKS1:Y  CPYSRC
     C                     MOVE CPYSRC    MFIL
      * Move source file name into storage field; compare and only
      *    print if different (or if at top of page)
     C           STORE1    IFEQ CPYSRC                     B02
     C           *IN37     IFNE *ON                        B03
     C                     MOVE *BLANKS   CPYSRC
     C                     SETOF                     72
     C                     ENDIF                           E03
     C                     ELSE                            X02
      * Start processing to identify first occurrence of member in
      *    library list
     C                     EXSR IDLIB
      * If source file has changed then print subtotal
     C           COUNT     IFNE 0                          B03
     C                     WRITEDTLL3                  40
      * If error end program
     C           *IN40     IFEQ *ON                        B04
     C                     MOVE '1'       ERR
     C                     SETON                     LR
     C                     RETRN
     C                     ENDIF                           E04
      *
      * Set subtotal field back to zero
     C                     Z-ADD0         SBTTL
     C                     ENDIF                           E03
      * Save source file name for comparison
     C                     MOVE CPYSRC    STORE1
     C                     ENDIF                           E02
      *
      * Set off overflow
     C                     SETOF                     37
      * Move hook or /COPY member name to report field
     C           10        SUBSTHOOKS1:Z  HOOK
      *
      * If source file was found in library list then look for hook
     C           *IN71     IFNE *ON                        B02
     C                     EXSR FNDMBR
     C                     ENDIF                           E02
      *
     C                     WRITEDTLL2                  40
     C                     SETOF                     7374
      * If error end program
     C           *IN40     IFEQ *ON                        B02
     C                     MOVE '1'       ERR
     C                     SETON                     LR
     C                     RETRN
     C                     ENDIF                           E02
      *
     C                     ADD  1         COUNT
     C                     ADD  1         SBTTL
      *
     C                     ENDIF                           E01
     C                     ENDDO                           ED01
      *****************************************************************
      * Write last Sub-total record (This uses the COUNT field)
     C                     WRITEDTLL3                  40
      * If error end program
     C           *IN40     IFEQ *ON                        B01
     C                     MOVE '1'       ERR
     C                     SETON                     LR
     C                     RETRN
     C                     ENDIF                           E01
      *
     C                     WRITETRAILL                 40
      * If error end program
     C           *IN40     IFEQ *ON                        B01
     C                     MOVE '1'       ERR
     C                     SETON                     LR
     C                     RETRN
     C                     ENDIF                           E01
      *
      * If *PLACE report requested then do not close print file
     C                     CLOSEHOOKS
      *
     C                     RETRN
      *****************************************************************
     C           IDLIB     BEGSR
      * Set off indicator for source file not found and set on
      *     for first print
     C                     SETOF                     71
     C                     SETON                       72
      * Create user space for use by list API (See I specs for DS)
     C                     MOVEL'LIBLUS'  US
     C                     MOVEL'QTEMP'   USLIB
     C                     MOVEL'APILIST' ATTR
     C                     Z-ADD2000      SIZE
     C                     MOVEL'*USE'    AUT
     C                     MOVEL'*YES'    REPL
     C                     Z-ADD8         ERR1
     C                     CALL 'QUSCRTUS'
     C                     PARM           NAME
     C                     PARM           ATTR
     C                     PARM           SIZE
     C                     PARM           IVAL
     C                     PARM           AUT
     C                     PARM           DESC
     C                     PARM           REPL
     C                     PARM           ERRP
      * Use API to build list of occurences of source file in library
      *    list (see I specs for DS)
     C                     MOVEL'OBJL0100'FMT
     C                     MOVEL'*FILE'   OTYPE
     C                     MOVEL'*LIBL'   OBJL
     C                     CALL 'QUSLOBJ'
     C                     PARM           NAME
     C                     PARM           FMT
     C                     PARM           OBJNL
     C                     PARM           OTYPE
      * Use API to retrieve list data from user space; first call gets
      *    user space header information (See I specs for DS)
     C                     CALL 'QUSRTVUS'
     C                     PARM           NAME
     C                     PARM 1         USSTRT
     C                     PARM 140       USLEN
     C           USHEAD    PARM           USDTA
      * If no entries then set on indicator and skip further processing
     C           NOENTS    IFEQ 0                          B01
     C                     SETON                     71
     C                     ENDIF                           E01
      * Execute SR to loop through list data from user space if entrie
     C  N71                EXSR GETDTA
     C                     ENDSR
      *****************************************************************
     C           GETDTA    BEGSR
      * Use API to retrieve list data from user space; set up control
     C                     Z-ADD1         A       20
     C                     Z-ADDENTSIZ    USLEN
     C           OFFLST    ADD  1         USSTRT
      *
     C           A         DOWLENOENTS                     D01
     C                     CLEARUSDTA
      *
     C                     CALL 'QUSRTVUS'
     C                     PARM           NAME
     C                     PARM           USSTRT
     C                     PARM           USLEN
     C           USLIST    PARM           USDTA
      * Move to array
     C                     MOVEAFILEL     LIBA,A
     C                     ADD  1         A
     C                     ADD  ENTSIZ    USSTRT
     C                     ENDDO                           ED01
      *
     C                     ENDSR
      *****************************************************************
     C           FNDMBR    BEGSR
      * Set up loop controls and constant parameters
     C                     SETOF                     817374
     C                     MOVE *BLANKS   MLIB
     C                     Z-ADD1         A
     C                     Z-ADD18        MLEN
     C                     MOVE 'MBRD0100'MFMT
     C                     MOVE '0'       MOVR
     C                     Z-ADD16        ERR1
      *
     C           *IN81     DOUEQ*ON                        D01
     C                     MOVELLIBA,A    MRLIB
     C           MRLIB     IFEQ *BLANKS                    B01
      * If no entry then member has not been found; end loop and output
      * to report
     C                     SETON                     7481
     C                     ELSE                            X01
     C                     MOVE *BLANKS   ERR3
     C                     CALL 'QUSRMBRD'
     C                     PARM           MRCV
     C                     PARM           MLEN
     C                     PARM           MFMT
     C                     PARM           MFLIB
     C                     PARM           HOOK
     C                     PARM           MOVR
     C                     PARM           ERRP
      * If member not found then try next library
     C           ERR2      IFEQ 0                          B02
     C                     MOVE MRLIB     MLIB
     C                     SETON                     7381
     C                     ENDIF                           E02
     C                     ADD  1         A
     C                     ENDIF                           E01
     C                     ENDDO                           ED01
     C                     ENDSR
      *****************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
