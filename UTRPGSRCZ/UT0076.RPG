     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas UT View file records')
      *****************************************************************
      *                                                               *
      *  Midas - Utilities Module                                     *
      *                                                               *
      *  UT0076 - Update Physical File                                *
      *                                                               *
      *  Function:  This program xxxxxxxxxxxxxxxxxxxxxxxxxxxx         *
      *  (phase(s))                                                   *
      *                                                               *
      *  Called By: UPDPFM - Update physical file                     *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CREATE             Date 05Mar96               *
      *                                    Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CREATE - Rewrite UPDPF                                       *
      *                                                               *
      *****************************************************************
      /EJECT
     FUT0076DFCF  E                    WORKSTN
     F                                        ##RR  KSFILE #SFLRCD
     F                                              KINFDS INFDS#
      * DSP: View File Records         Edit file
      *
     FCSCFILE IF  F    9950            DISK
     F                                              KINFDS INFDS1
      * RTV : File to dispaly
      *
     E/EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
     E                    MNM    12  12  3
      *
      ** Array of month names
      *
     E                    DAT@     9950  1
      *
      ** Array of 50 characters which represent the read data
      *
     E                    VAC@       10  1
      *
      ** Array of 10 characters which represent the actions available
      *
     E                    ACT@    1   6 10
      *
      ** Valid actions for command line
      *
     E                    CMD@        7 10
      *
      ** Array of 10 characters which represent the command line
      *
     E                    PS1@       50  1
     E                    PS2@       50  1
      *
      ** Array of 50 characters which represent the window position
      *
     I/EJECT
      *
      * Input file Specification
      *
     ICSCFILE NS  01
     I                                        1 250 FLD1
     I                                      251 500 FLD2
     I                                      501 750 FLD3
     I                                      7511000 FLD4
     I                                     10011250 FLD5
     I                                     12511500 FLD6
     I                                     15011750 FLD7
     I                                     17512000 FLD8
     I                                     20012250 FLD9
     I                                     22512500 FLD10
     I                                     25012750 FLD11
     I                                     27513000 FLD12
     I                                     30013250 FLD13
     I                                     32513500 FLD14
     I                                     35013750 FLD15
     I                                     37514000 FLD16
     I                                     40014250 FLD17
     I                                     42514500 FLD18
     I                                     45014750 FLD19
     I                                     47515000 FLD20
     I                                     50015250 FLD21
     I                                     52515500 FLD22
     I                                     55015750 FLD23
     I                                     57516000 FLD24
     I                                     60016250 FLD25
     I                                     62516500 FLD26
     I                                     65016750 FLD27
     I                                     67517000 FLD28
     I                                     70017250 FLD29
     I                                     72517500 FLD30
     I                                     75017750 FLD31
     I                                     77518000 FLD32
     I                                     80018250 FLD33
     I                                     82518500 FLD34
     I                                     85018750 FLD35
     I                                     87519000 FLD36
     I                                     90019250 FLD37
     I                                     92519500 FLD38
     I                                     95019750 FLD39
     I                                     97519950 FLD40
     I                                        19950 DAT@
      *
      *    Catchall for deleted records
     I        NS
     I/SPACE 3
      /EJECT
      * Data structures:
     IPGMDS     ESDSY2PGDSP
      * Program data structure
     IJBDTTM      DS
      * Job date/time
     I                                        1   60##JDT
     I                                        1   20##JYY
     I                                        3   40##JMM
     I                                        5   60##JDD
     I                                        7  120##JTM
     I                                        7   80##JHH
     I                                        9  100##JNN
     I                                       11  120##JSS
     IINFDS#    E DSY2I#DSP
      * Display file information data structure
      *
     IINFDS1    E DSY2I1DSP
     I                                    B 125 1260@FLSZ
     I                                      261 270 @RECFM
      * File information data structure
      *
      /EJECT
      * Command line
     I@CMD1       DS                             74
     I                                        1  70 CMD@
      * Position line 1
     I@POS1       DS                             73
     I                                        1   40OF1@
     I                                        4  53 PS1@
      * Parameter declarations
     IP1PARM      DS                             10
      * Action type
     I                                        1  10 P1VACT
     I                                        1  10 VAC@
      * Outward parameter declarations
     IPARB        DS                             10
      * Action type
     I                                        1  10 POACT
      * Parameter declarations
     IPARC        DS                              8
      * KEY: Relative record number
     I                                        1   80PORREL
      /EJECT
      *****************************************************************
      * Entry parameters
     C           *ENTRY    PLIST
     C                     PARM           P0RTN   7
     C                     PARM           P1PARM           Valid Actions
      *****************************************************************
      * Initialise
     C                     EXSR ZZINIT
      *
     C                     DO   *HIVAL
      * Initialise and load subfile page
     C                     EXSR BAIZSF
     C                     MOVEL'N'       W0RSF   1
      * Display screen until reload requested
     C           W0RSF     DOWEQ'N'
      * Display screen
     C                     EXSR CAEXFM
      * Process response
      * Cancel & exit program
     C   03                CAS            ZXEXPG
      * HOME: Request subfile reload
     C   30                CAS            FBRQRL
      * Display next SFL page
     C   27                CAS            BBLDSF
      * Process screen input
     C                     CAS            DAPR##
     C                     END
      *
     C                     END
     C                     END
      *****************************************************************
      /EJECT
     CSR         BAIZSF    BEGSR
      *================================================================
      * Initialise & load subfile page
      *================================================================
      * Clear subfile
     C                     SETON                     80
     C                     WRITE#SFLCTL
     C                     SETOF                     80
      * Reset no of records in subfile
     C                     Z-ADD*ZERO     ##RRMX  50 81     SETOF 81
      * 82=EOF
     C           @1RREL    SETLLCSCFILE              82    *
      * 82=EOF
     C  N82                READ CSCFILE                9182*
      *
      * Set up postion fields
      *
     C                     MOVEL*BLANKS   @POS1
     C                     MOVEL*BLANKS   PS2@
     C           @1ACT     IFLE @FLSZ
     C           @1ACT     ANDGT*ZEROS
     C                     Z-ADD@1ACT     OF1@
     C                     Z-ADD@1ACT     @WSZ    50
      *
      * Set up start 1 character fields
      *
     C           @1ACT     DIV  10        @O2     10
     C                     Z-ADD@1ACT     @O      10
      *
      * Fill up to 50 offset positions
      *
     C           1         DO   50        K       50
     C           @WSZ      IFLE @FLSZ
     C                     MOVE @O        PS2@,K
      *
      * If a factor of ten then place second sign fig above
      *
     C           @O        IFEQ 0
     C           K         ANDGT2
     C           @O2       ADD  1         @O2
     C                     MOVE @O2       PS1@,K
     C                     END
      *
      * Increment work fields
      *
     C           @O        ADD  1         @O
     C           @WSZ      ADD  1         @WSZ
     C                     END
     C                     END
     C                     END
      *
      * Move in screen fields for display
      *
     C                     MOVEL@POS1     @OFFS1
     C                     MOVEAPS2@      @OFFS2
      *
      * Load subfile page
     C                     EXSR BBLDSF
      *................................................................
      * If no records found, display error message
     C   82      ##RR      IFEQ *ZERO                      IF
      * Send message '*No data to display'
     C                     MOVEL'Y2U0008' ZAMSID
     C                     MOVEL'Y2USRMSG'ZAMSGF
     C                     EXSR ZASNMS
     C                     END
      *
      *================================================================
     CSR         BAEXIT    ENDSR
      /EJECT
     CSR         BBLDSF    BEGSR
      *================================================================
      * Load subfile page
      *================================================================
     C                     SETOF                     84    No SFLNXTCHG
      * Re-establish fields in read-ahead record
     C   27      *IN27     IFEQ '1'
     C  N82                READPCSCFILE                  90*
     C  N82                READ CSCFILE                  90*
     C                     END
      *
      * Setof record error indicators
     C                     MOVE *ALL'0'   WKIND0  4
     C                     MOVE *ALL'1'   WKIND1  4
      * Start at previous highest record in SFL
     C                     Z-ADD##RRMX    ##RR    50
     C                     Z-ADD*ZERO     ##RROK  50
      *................................................................
      * Load next subfile page
     C  N82      ##RROK    DOWLT##SFPG                     DO
     C                     MOVEAWKIND0    *IN,32
     C                     SETOF                     87    *
      * Clear subfile fields
     C                     EXSR MAIZ#1
      * Load subfile fields
     C                     EXSR MBFL#1
      * Output to subfile
     C                     ADD  1         ##RR       81    *
     C                     ADD  1         ##RROK
      * Set screen conditioning indicators
     C                     EXSR GADSA1
     C                     WRITE#SFLRCD
      * 82=EOF
     C                     READ CSCFILE                  82*
     C  N82                END
      *................................................................
      * Save highest SFL record load can continue at end point
     C           ##RR      IFGT ##RRMX
     C           ##RRMX    ADD  1         ##SFRC
     C                     Z-ADD##RR      ##RRMX
     C                     END
      *================================================================
     CSR         BBEXIT    ENDSR
      /EJECT
     CSR         CAEXFM    BEGSR
      *================================================================
      * Display screen and process HELP requests
      *================================================================
      * Set screen conditioning indicators
     C                     EXSR GBDSA2
      * Update screen time
     C                     TIME           ##TME
      * PUTOVR unless conditioned fields change
     C                     SETON                     86
     C           *IN81     IFNE CAIN81
     C                     SETOF                     86
     C                     END
     C                     MOVE *IN81     CAIN81  1
     C                     WRITE#MSGCTL
     C                     WRITE#CMDTXT1
     C                     EXFMT#SFLCTL
      * Update job time
     C                     TIME           ##JTM
      * Clear messages from program message queue
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
      * Reset first message only flag
     C                     MOVEL'Y'       ZAFSMS  1      99*
      *================================================================
     CSR         CAEXIT    ENDSR
      /EJECT
     CSR         DAPR##    BEGSR
      *================================================================
      * Process screen input
      *================================================================
      * Maintain subfile position where possible
     C           @#SFRC    IFGT *ZERO
     C                     Z-ADD@#SFRC    ##SFRC
     C                     END
      *
      * Change of position specified
     C           WZACT     CASNE@1ACT     FBRQRL           Action specified
     C           WZRREL    CASNE@1RREL    FBRQRL           Action specified
     C                     END
      * Quit if reload requested
     C           W0RSF     CABEQ'Y'       DAEXIT
      *
     C   81                DO
      * Confirm/update is not deferred
     C                     MOVEL'N'       W0DCF   1
      * Process subfile records
     C                     EXSR DBPRSF
      * If error, exit
     C           *IN99     CABEQ'1'       DAEXIT
      * Defer confirm/update requested
     C           W0DCF     CABEQ'Y'       DAEXIT
     C                     END
      *
      * Process Command key 6 request
      *
     C           *IN06     IFEQ '1'
     C                     MOVEL'A'       @ACTL            Valid Action
     C           @ACTL     LOKUPVAC@                     90Valid Action
     C           *IN90     IFEQ '0'
      * Send message 'Action not valid'
     C                     SETON                     9832
     C                     MOVEL'Y2MAA04' ZAMSID
     C                     EXSR ZASNMS
     C                     GOTO DAEXIT
     C                     END
      *
      * Add  Record
      *
     C                     MOVEL*BLANKS   POACT            Action
     C                     MOVEL'*ADD  '  POACT            Action
     C                     Z-ADD*ZEROS    PORREL           Action
     C                     CALL 'UT0077 '              90  Edit Record
     C                     PARM *BLANK    W0RTN   7
     C                     PARM           PARB             Action
     C                     PARM           PARC             Relative Record
      *
     C           *IN90     IFEQ '1'
      * Call to program ended in error
     C                     MOVEL'Y2U0021' W0RTN   7
     C                     END
      *
      *  F3 pressed - exit program
      *
     C           W0RTN     IFEQ 'Y2U9999'                  *IF
     C                     EXSR ZYEXPG
     C                     ENDIF
      *
      * Error detected?
     C           W0RTN     IFNE *BLANK
     C           W0RTN     ANDNE'USR0790'
     C                     SETON                     9833  *
     C                     END
     C                     MOVEL'Y'       W0RSF            *Reload subfile
     C                     MOVEL'Y'       W0DCF            *Defer confirm
     C                     END                             *FI
      *================================================================
     CSR         DAEXIT    ENDSR
      /EJECT
     CSR         DBPRSF    BEGSR
      *================================================================
      * Process modified subfile record
      *================================================================
     C                     READC#SFLRCD                  92*
     C           *IN92     DOWEQ'0'
     C                     EXSR DCPRSR
     C                     SETOF                     87    *
      * Set screen conditioning indicators
     C                     EXSR GADSA1
     C                     UPDAT#SFLRCD
     C                     READC#SFLRCD                  92*
     C                     END
      *================================================================
     CSR         DBEXIT    ENDSR
      /EJECT
     CSR         DCPRSR    BEGSR
      *================================================================
      * Process subfile record
      *================================================================
      * Set off error indicators
     C                     MOVEAWKIND0    *IN,32
      * No errors
     C                     SETOF                     98
      * SFLNXTCHG
     C                     SETON                     84
      * Validate subfile record
     C                     EXSR DEV1RC
      * If SFLRCD invalid, note the fact
     C   98N99             Z-ADD##RR      ##SFRC     99    *
      *================================================================
     CSR         DCEXIT    ENDSR
      /EJECT
     CSR         DEV1RC    BEGSR
      *================================================================
      * Validate subfile record
      *================================================================
      * USER: Validate subfile record fields
     C           #1SEL     LOKUPVAC@                     90Valid Action
     C           *IN90     IFEQ '0'
      * Send message 'Action not valid'
     C                     SETON                     9832
     C                     MOVEL'Y2MAA04' ZAMSID
     C                     EXSR ZASNMS
     C                     GOTO DEEXIT
     C                     END
      * CASE: RCD.*SFLSEL is Edit Record
     C           #1SEL     IFEQ '2'                        *IF
      * Edit Record
      *
     C                     MOVEL*BLANKS   POACT            Action
     C                     MOVEL'*AMEND'  POACT            Action
     C                     Z-ADD#1RREL    PORREL           Action
     C                     CALL 'UT0077 '              90  Edit Record
     C                     PARM *BLANK    W0RTN   7
     C                     PARM           PARB             Action
     C                     PARM           PARC             Relative Record
      *
     C           *IN90     IFEQ '1'
      * Call to program ended in error
     C                     MOVEL'Y2U0021' W0RTN   7
     C                     END
      *
      *  F3 pressed - exit program
      *
     C           W0RTN     IFEQ 'Y2U9999'                  *IF
     C                     EXSR ZYEXPG
     C                     ENDIF
      *
      * Error detected?
     C           W0RTN     IFNE *BLANK
     C           W0RTN     ANDNE'USR0790'
     C                     SETON                     9833  *
     C                     END
     C                     MOVEL'Y'       W0RSF            *Reload subfile
     C                     MOVEL'Y'       W0DCF            *Defer confirm
     C                     END                             *FI
      * CASE: RCD.*SFLSEL is Copy Record
     C           #1SEL     IFEQ '3'                        *IF
      * Edit Record
      *
     C                     MOVEL*BLANKS   POACT            Action
     C                     MOVEL'*COPY '  POACT            Action
     C                     Z-ADD#1RREL    PORREL           Action
     C                     CALL 'UT0077 '              90  Edit Record
     C                     PARM *BLANK    W0RTN   7
     C                     PARM           PARB             Action
     C                     PARM           PARC             Relative Record
      *
     C           *IN90     IFEQ '1'
      * Call to program ended in error
     C                     MOVEL'Y2U0021' W0RTN   7
     C                     END
      *
      *  F3 pressed - exit program
      *
     C           W0RTN     IFEQ 'Y2U9999'                  *IF
     C                     EXSR ZYEXPG
     C                     ENDIF
      *
      * Error detected?
     C           W0RTN     IFNE *BLANK
     C           W0RTN     ANDNE'USR0790'
     C                     SETON                     9833  *
     C                     END
     C                     MOVEL'Y'       W0RSF            *Reload subfile
     C                     MOVEL'Y'       W0DCF            *Defer confirm
     C                     END                             *FI
      * CASE: RCD.*SFLSEL is Delete Record
     C           #1SEL     IFEQ '4'                        *IF
      * Edit Record
      *
     C                     MOVEL*BLANKS   POACT            Action
     C                     MOVEL'*DELETE' POACT            Action
     C                     Z-ADD#1RREL    PORREL           Action
     C                     CALL 'UT0077 '              90  Edit Record
     C                     PARM *BLANK    W0RTN   7
     C                     PARM           PARB             Action
     C                     PARM           PARC             Relative Record
      *
     C           *IN90     IFEQ '1'
      * Call to program ended in error
     C                     MOVEL'Y2U0021' W0RTN   7
     C                     END
      *
      *  F3 pressed - exit program
      *
     C           W0RTN     IFEQ 'Y2U9999'                  *IF
     C                     EXSR ZYEXPG
     C                     ENDIF
      *
      * Error detected?
     C           W0RTN     IFNE *BLANK
     C           W0RTN     ANDNE'USR0790'
     C                     SETON                     9833  *
     C                     END
     C                     MOVEL'Y'       W0RSF            *Reload subfile
     C                     MOVEL'Y'       W0DCF            *Defer confirm
     C                     END                             *FI
      * CASE: RCD.*SFLSEL is Display Record
     C           #1SEL     IFEQ '5'                        *IF
      * Edit Record
      *
     C                     MOVEL*BLANKS   POACT            Action
     C                     MOVEL'*ENQUIRE'POACT            Action
     C                     Z-ADD#1RREL    PORREL           Action
     C                     CALL 'UT0077 '              90  Edit Record
     C                     PARM *BLANK    W0RTN   7
     C                     PARM           PARB             Action
     C                     PARM           PARC             Relative Record
      *
     C           *IN90     IFEQ '1'
      * Call to program ended in error
     C                     MOVEL'Y2U0021' W0RTN   7
     C                     END
      *
      *  F3 pressed - exit program
      *
     C           W0RTN     IFEQ 'Y2U9999'                  *IF
     C                     EXSR ZYEXPG
     C                     ENDIF
      *
      * Error detected?
     C           W0RTN     IFNE *BLANK
     C           W0RTN     ANDNE'USR0790'
     C                     SETON                     9833  *
     C                     END
     C                     MOVEL'Y'       W0RSF            *Reload subfile
     C                     MOVEL'Y'       W0DCF            *Defer confirm
     C                     END                             *FI
      *================================================================
     CSR         DEEXIT    ENDSR
      /EJECT
     CSR         FBRQRL    BEGSR
      *================================================================
      * Request subfile reload
      *================================================================
     C                     MOVEL'Y'       W0RSF
     C                     Z-ADD@1ACT     WZACT            Action specified
     C                     Z-ADD@1RREL    WZRREL  80       Action specified
      *================================================================
     CSR         FBEXIT    ENDSR
      /EJECT
     CSR         GADSA1    BEGSR
      *================================================================
      * Set display attributes for Subfile record
      *================================================================
      * Protect keys if change mode or updated record
     C                     SETON                     88    *
      *================================================================
     CSR         GAEXIT    ENDSR
      /EJECT
     CSR         GBDSA2    BEGSR
      *================================================================
      * Set display attributes for Subfile control
      *================================================================
      *================================================================
     CSR         GBEXIT    ENDSR
      /EJECT
     CSR         MAIZ#1    BEGSR
      *================================================================
      * Initialise subfile record
      *================================================================
      * Previous values
     C                     MOVEL*BLANK    #1SEL            *SFLSEL
      *================================================================
     CSR         MAEXIT    ENDSR
      /EJECT
     CSR         MBFL#1    BEGSR
      *================================================================
      * Move CSCFILE fields to subfile
      *================================================================
     C                     Z-ADD@FLSZ     @1RECL
     C                     MOVEL@RECFM    @1RECF
     C                     Z-ADD@1RCNO    #1RREL           Relative Record
      *
      * If out of range do not display
      *
     C           @1ACT     IFLT 1
     C           @1ACT     ORGT @FLSZ
     C                     MOVEL*BLANKS   #RDCHR
     C                     MOVEL*BLANKS   #RHEX1
     C                     MOVEL*BLANKS   #RHEX2
     C                     GOTO MBEXIT
     C                     END
      *
      * Decide what to display
      *
     C           @1ACT     IFGT 1
     C           @1ACT     ANDLE@FLSZ
     C                     Z-ADD@1ACT     J       50
     C                     MOVEADAT@,J    WUCHAR
     C                     ELSE
     C                     Z-ADD1         J
     C                     MOVEADAT@,1    WUCHAR 50
     C                     END
     C           @1ACT     ADD  50        J
     C           J         IFGT @FLSZ
     C           @FLSZ     SUB  @1ACT     J
     C                     ADD  1         J
     C                     ELSE
     C                     Z-ADD50        J
     C                     END
      *
      * Format characters for screen
      *
     C                     MOVEL*BLANKS   P0CHAR
     C                     MOVEL*BLANKS   #RDCHR
     C                     MOVEL*BLANKS   #RHEX1
     C                     MOVEL*BLANKS   #RHEX2
     C                     MOVELWUCHAR    P0CHAR
     C                     MOVEL*BLANKS   @PROG  10
     C                     MOVEL'UT0078  '@PROG
     C                     MOVE '  '      @PROG
     C                     CALL @PROG
     C                     PARM *BLANKS   P0RTN   7        Return Code
     C                     PARM '*C*H'    P0CVTT 10        Conv. Type
     C                     PARM           P0CHAR100        Chars
     C                     PARM           P0DSPC100        Display Char
     C                     PARM           P0HX1 100        Hex first
     C                     PARM           P0HX2 100        Hex Second
     C                     PARM           P0HEX 200        Hex both
     C                     PARM J         P0LEN   50       Length
      *
      * Load screen fields
      *
     C                     MOVELP0DSPC    #RDCHR
     C                     MOVELP0HX1     #RHEX1
     C                     MOVELP0HX2     #RHEX2
      *================================================================
     CSR         MBEXIT    ENDSR
      /EJECT
     CSR         MEIZ#2    BEGSR
      *================================================================
      * Initialise subfile control
      *================================================================
     C                     Z-ADD1         @1ACT            Window
     C                     Z-ADD1         @1RREL           Window
      *================================================================
     CSR         MEEXIT    ENDSR
      /EJECT
     CSR         ZASNMS    BEGSR
      *================================================================
      * Send message to program's message queue
      *================================================================
      * Send if first error message or not an error message
     C           ZAMSTP    IFNE *BLANK
     C           ZAFSMS    ORNE 'N'
     C           ZAMSTP    IFEQ *BLANK
      * Signal first error message sent
     C                     MOVEL'N'       ZAFSMS
     C                     END
     C           ZAPGM     IFEQ *BLANK
     C                     MOVEL##PGM     ZAPGM
     C                     END
      * If no message file specified, use default
     C           ZAMSGF    IFEQ *BLANK
     C                     MOVELZADFMF    ZAMSGF
     C                     END
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGM  10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message ID
     C                     PARM           ZAMSGF 10        Message file
     C                     PARM           ZAMSDA132        Message data
     C                     PARM           ZAMSTP  7        Message type
     C                     END
      * Clear all fields for default mechanism next time
     C                     MOVEL*BLANK    ZAPGM
     C                     MOVEL*BLANK    ZAPGRL
     C                     MOVEL*BLANK    ZAMSID
     C                     MOVEL*BLANK    ZAMSGF
     C                     MOVEL*BLANK    ZAMSDA
     C                     MOVEL*BLANK    ZAMSTP
      *================================================================
     CSR         ZAEXIT    ENDSR
      /EJECT
     CSR         ZXEXPG    BEGSR
      *================================================================
      * Exit program: Normal
      *================================================================
     C                     MOVEL*BLANK    P0RTN
     C                     EXSR ZYEXPG
      *================================================================
     CSR         ZXEXIT    ENDSR
      /EJECT
     CSR         ZYEXPG    BEGSR
      *================================================================
      * Exit program: Direct
      *================================================================
      * Reclaim resources of called programs
     C                     CALL 'Y2RCRSC'
      *
      * Terminate program
     C                     SETON                     LR
      *
      * Exit program
     C                     RETRN
      *
      *================================================================
     CSR         ZYEXIT    ENDSR
      /EJECT
     CSR         ZZINIT    BEGSR
      *================================================================
      * Initialisation
      *================================================================
     C                     MOVE *BLANK    P0RTN
     C                     MOVE *BLANK    W0RTN   7
     C                     MOVEL*BLANK    W0RSL   1
     C                     MOVEL*BLANK    W0RSF   1
     C                     Z-ADD1         WZACT   40
     C                     Z-ADD1         WZRREL  80       Action specified
      * Setup job date/time
      *
     C                     Z-ADDUDATE     ##JDT
     C                     TIME           ##JTM
      * Update screen time
     C                     TIME           ##TME   60
      * Obtain default message file
     C                     MOVEL'UTMSGF  'ZADFMF 10
      * Signal first error message outstanding
     C                     MOVEL'Y'       ZAFSMS  1
      *
     C                     Z-ADD12        ##SFPG  30       SFLPAG
     C                     Z-ADD1         ##SFRC
      * Maximum record number
     C                     Z-ADD*ZERO     ##RRMX
      *................................................................
      * USER: Initialise program
      * Retrieve Midas Date - R10 Standard Functions  *
     C*
     C** Set up copyright parameter
     C*
     C                     MOVEACPY@      CPY2@  80
      * Get System date and convert to todays date
     C                     TIME           ##012N 120       Get date
     C                     MOVE ##012N    ##006A  6        Move to char
     C                     MOVEL##006A    ##YY    2        Get year
     C                     MOVE ##006A    ##DD    2        Get day
     C           2         SUBST##006A:3  ##MM    2        Get Month
      * Get month name
     C                     MOVEL##MM      ##MMN   20
     C                     MOVEAMNM,##MMN ##MMNM  3
      * Set up date
     C                     MOVEL##YY      ##MRDT  7 P
     C           ##MMNM    CAT  ##MRDT:0  ##MRDT    P
     C           ##DD      CAT  ##MRDT:0  ##MRDT
      *
      * Clear messages from program message queue
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
      *
      * Set up command line of valid functions
      *
     C                     MOVEL*BLANKS   @CMD1            Command Line
     C                     MOVEL'A'       @ACTL   1        Valid Action
     C                     Z-ADD1         N       50
     C           @ACTL     LOKUPVAC@                     90Valid Action
     C           *IN90     IFEQ '1'
     C                     MOVELACT@,1    CMD@,N           Command Line
     C                     ADD  1         N
     C                     END
     C                     MOVEL'2'       @ACTL   1        Valid Action
     C           @ACTL     LOKUPVAC@                     90Valid Action
     C           *IN90     IFEQ '1'
     C                     MOVELACT@,2    CMD@,N           Command Line
     C                     ADD  1         N
     C                     END
     C                     MOVEL'3'       @ACTL   1        Valid Action
     C           @ACTL     LOKUPVAC@                     90Valid Action
     C           *IN90     IFEQ '1'
     C                     MOVELACT@,3    CMD@,N           Command Line
     C                     ADD  1         N
     C                     END
     C                     MOVEL'4'       @ACTL            Valid Action
     C           @ACTL     LOKUPVAC@                     90Valid Action
     C           *IN90     IFEQ '1'
     C                     MOVELACT@,4    CMD@,N           Command Line
     C                     ADD  1         N
     C                     END
     C                     MOVEL'5'       @ACTL            Valid Action
     C           @ACTL     LOKUPVAC@                     90Valid Action
     C           *IN90     IFEQ '1'
     C                     MOVELACT@,5    CMD@,N           Command Line
     C                     ADD  1         N
     C                     END
      ***********
     C***********          MOVEL'6'       @ACTL            Valid Action
     C***********@ACTL     LOKUPVAC@                     90Valid Action
     C************IN90     IFEQ '1'
     C***********          MOVELACT@,6    CMD@,N           Command Line
     C***********          ADD  1         N
     C***********          END
      * Initialise subfile control
     C                     EXSR MEIZ#2
      *================================================================
     CSR         ZZEXIT    ENDSR
**  CPY@
(c) Finastra International Limited 2001
**   MNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  ACT@
F6=Add
2=Edit
3=Copy
4=Delete
5=Display
6=Print
