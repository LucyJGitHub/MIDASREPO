      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Utility to be run for CSE031')                         *
      *****************************************************************
      *                                                               *
      *  Midas - Utility Module                                       *
      *                                                               *
      *  UT0100 - Utility to update fields Fixing Days, Auto Fix      *
      *           and Business Day convention on file SECTYD.         *
      *                                                               *
      *           NOTE: The values for the above fields should be     *
      *                 set according to the client's requirements.   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CRE073             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSE075             Date 17Nov05               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CSE031  *CREATE    Date 10Dec01               *
      *                 XXXXXX             Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRE073 - Interest Rate Rounding (Recompile)                  *
      *  CSE075 - US Enhanced Treasury Upgrade to MidasPlus           *
      *           (Recompile)                                         *
      *  CSE031 - Coupon fixing for floating rate notes.              *
      *                                                               *
      *****************************************************************
      *
     FSECTY   UF  E           K        DISK
      ** Midas SE Securities
      *
      *****************************************************************
      *
     IDSFDY     E DSDSFDY
      ** First DS for access programs, short data structure
      *
     ISDBANK    E DSSDBANKPD
      ** Data Structure to receive Bank Details
      *
     IINVTP     E DSINVTPD
     I              RECI                            INRECI
     I              LCD                             INLCD
     I              CHTP                            INCHTP
     I              TNLU                            INTNLU
      ** Data Structure to receive Investment Type Details
      *
      *****************************************************************
      /EJECT
      *
      ** Obtain Local CCY.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '       ' PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** Read records from SECTYD until the end of the file.
      *
     C           *LOVAL    SETLLSECTY
     C                     READ SECTY                    89
      *
     C           *IN89     DOWEQ*OFF
      *
      ** Access AOINVTR0 to get the processing type
     C                     CALL 'AOINVTR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM SITP      PINVT   3
     C                     PARM NMCY      PCCYI   3
     C           INVTP     PARM INVTP     DSFDY
      *
      ** If processing type is 3 or 8 and Value CCY is local CCY,
      ** default fixing days to 0. If any other CCY, set to 2.
      ** If any other security set to 0.
      *
     C           PROT      IFEQ '3'
     C           PROT      OREQ '8'
      *
     C           VLCY      IFEQ BJCYCD
     C                     Z-ADD0         FIXD
     C                     ELSE
     C                     Z-ADD2         FIXD
     C                     ENDIF
      *
     C                     ELSE
     C                     Z-ADD0         FIXD
     C                     ENDIF
      *
      ** If processing type is 3 or 8 set Auto Fix to be 'N'.
      ** and Business Day convention to 'N'.
      *
     C           PROT      IFEQ '3'
     C           PROT      OREQ '8'
      *
     C                     MOVE 'N'       BCNV
     C                     MOVE 'N'       AUFX
     C                     ELSE
     C                     MOVE 'N'       AUFX
     C                     ENDIF
      *
     C                     UPDATSECTYDF
      *
     C                     READ SECTY                    89
     C                     ENDDO
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
