     H
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas UT Midas day number to date conversion')
      *****************************************************************
      *                                                               *
      *  Midas - Utility Module                                       *
      *                                                               *
      *  UT0500 - Program to Convert Date to Midas Date               *
      *                                                               *
      *  Function:  This program converts date to Midas date and      *
      *             vice-versa                                        *
      *                                                               *
      *  Called By: DATCHK Command                                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      *  Last Amend No. 242163             Date 16Feb07               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CPK009  *CREATE    Date 07Jul99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  242163 - American date format.                               *
      *  CPK009 - Packaging for DBA3                                  *
      *           - Moved to main Midas libraries from SYSLIB400.     *
      *                                                               *
      *****************************************************************
      /EJECT
     FUT0500DFCF  E                    WORKSTN
     E                    CPY@    1   1 80
     E                    MSG     1   3 40
     E                    ZYDY    4   4  4 0             ZDATE1 YEAR
     E                    ZMDY   13  13  3 0             ZDATE1 MONTH
     E                    ZMNM   12  12  3               ZDATE2 SR. ARRAY
      *
      ** Array containing Copyright statement
      *
     IRUNDAT    E DSRUNDAT
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      * Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      * Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C*
      **  Access data area RUNDAT for Run Date                                                242163
     C           *NAMVAR   DEFN           RUNDAT                                              242163
     C                     IN   RUNDAT                                                        242163
     C                     SETON                     91
     C           AGDFF     IFEQ 'M'                                                           242163
     C                     SETON                     98                                       242163
     C                     ELSE                                                               242163
     C                     SETOF                     98
     C                     END                                                                242163
      *
     C           RDSPLY    TAG
      *
     C                     EXFMTUT0500F0
     C                     MOVE *BLANKS   MESS
      *
     C                     SETON                     91
     C                     SETOF                     99
      *
     C   KC                SETON                     LR
     C   KC                GOTO END
      *
     C           DATE      COMP 0                    11  10
     C           DAY       COMP 0                    13  12
      *
     C   10 12             MOVE MSG,2     MESS   40      91
     C   13 11             MOVE MSG,1     MESS           91
      *
     C  N91                GOTO RDSPLY
      *
     C   11                MOVE DATE      ZDATE
     C   11                EXSR ZDATE1
      *
     C   13                MOVE DAY       ZDAYNO
     C   13                EXSR ZDATE2
      *
     C   99                MOVE MSG,3     MESS           91
     C   99                GOTO RDSPLY
      *
     C   11                MOVE ZDAYNO    DAY
     C   13                MOVE ZDATE     DATE
      *
     C                     GOTO RDSPLY
      *
     C           END       TAG
      ********************************************************************
      *                                                                  *
      *   ZDATE1 SR. TO VALIDATE DATES SUBMITTED AND                     *
      *              CONVERT TO A NUMBER OF DAYS.                        *
      *                                                                  *
      *   THE YEAR 2000, BEING DIVISIBLE BY 400, IS A LEAP YEAR.         *
      *                                                                  *
      ********************************************************************
      *
     C           ZDATE1    BEGSR                           *** ZDATE1 ***
      *
      * Clear day number
      *
     C                     Z-ADD0         ZDAYNO  50
      *
      * Calculation to define input date field
      *
     C                     Z-ADDZDATE     ZDATE   60
      *
      * Get individual day, month and year fields
      *
     C                     MOVE ZDATE     ZYEAR   20
     C  N98                MOVELZDATE     ZDAY    20
     C   98                MOVELZDATE     ZMTH    20
     C                     MOVE ZDATE     ZWRK4   40
     C  N98                MOVELZWRK4     ZMTH
     C   98                MOVELZWRK4     ZDAY
      *
      * Ensure month is valid
      *
     C           ZMTH      COMP 0                      9999
     C  N99      ZMTH      COMP 12                   99
     C   99                GOTO ZDEND1                     BYPASS IF ERROR
      *
      * Check for 30 day months
      *
     C           ZMTH      COMP 4                        90IF 90 ON, 30
     C  N90      ZMTH      COMP 6                        90DAY MONTH.
     C  N90      ZMTH      COMP 9                        90
     C  N90      ZMTH      COMP 11                       90
      *
      * Ensure day is valid
      *
     C           ZDAY      COMP 0                      9999
     C   90N99   ZDAY      COMP 30                   99
     C  N90N99   ZDAY      COMP 31                   99
     C   99                GOTO ZDEND1                     BYPASS IF ERROR
      *
      * Check for leap year and February
      *
     C           ZYEAR     ADD  28        ZYEAR
     C           ZYEAR     DIV  4         ZLYR    20
     C                     MVR            ZLY     10     91LEAP YR, 91 ON
     C           ZMTH      COMP 2                    93  92FEB., 92 ON
      *                                                    PAST FEB, 93 ON
      * If February further validate day
      *                                                    PAST FEB, 93 ON
     C  N91 92   ZDAY      COMP 28                   99
     C   91 92   ZDAY      COMP 29                   99
     C   99                GOTO ZDEND1                     BYPASS IF ERROR
      *
      * Determine number of days from 31 Dec 1971          NO.OF 4 YR. SPANS
      *
     C           ZLYR      MULT 1461      ZDAYNO           X DAYS IN SPAN.
     C  N91      ZDAYNO    ADD  ZYDY,ZLY  ZDAYNO           SINCE LAST L.YR
     C   91 93   ZDAYNO    ADD  1         ZDAYNO           L.YR.,PAST FEB.
     C           ZDAYNO    ADD  ZMDY,ZMTH ZDAYNO           DAYS THIS YEAR
     C           ZDAYNO    ADD  ZDAY      ZDAYNO           DAYS THIS MONTH
      *
     C           ZDEND1    ENDSR                           * ZDEND1 ENDSR*
      *
      ********************************************************************
      *                                                                  *
      *   ZDATE2 SR. TO CONVERT A DAY NUMBER TO DATE FORMATS.            *
      *                                                                  *
      *   THE YEAR 2000, BEING DIVISIBLE BY 400, IS A LEAP YEAR.         *
      *                                                                  *
      ********************************************************************
      *
     C           ZDATE2    BEGSR                           *** ZDATE2 ***
      *
      * Clear date fields
      *
     C                     Z-ADD0         ZDATE   60
     C                     MOVEL'       ' ZADATE  7
      *
      * Calculation to define input day number
      *
     C                     Z-ADDZDAYNO    ZDAYNO  50
      *
      * Determine year number
      *
      * Adjust day number in case last day of year
      *
     C           ZDAYNO    SUB  1         ZDAYN1  50   99
     C   99                GOTO ZDEND2
      *
      * Calculate number of 4 year spans since 31 Dec 1971
      *
     C           ZDAYN1    DIV  1461      ZLYR    20
     C                     MVR            ZDAYN1           SAVE REMAINING
      *                                                    DAYS
      * Calculate number of remaining years
      *                                                    DAYS
     C                     Z-ADD1         ZWRK2   20
     C           ZDTAG1    TAG                             ** ZDTAG1 TAG *
     C           ZDAYN1    COMP ZYDY,ZWRK2             90
     C  N90      ZWRK2     ADD  1         ZWRK2
     C  N90                GOTO ZDTAG1
     C           ZWRK2     SUB  1         ZWRK2          91LEAP YR, 91 ON
      *
      * Decrease day number by days in remaining years
      *
     C  N91      ZDAYN1    SUB  ZYDY,ZWRK2ZDAYN1
      *
      * Calculate actual year number
      *
     C           ZLYR      MULT 4         ZWRK3   30
     C           ZWRK3     ADD  72        ZWRK3            BASE IS 1972
     C                     Z-ADDZWRK3     ZYEAR   20
     C           ZYEAR     ADD  ZWRK2     ZYEAR            YEAR
      *
      * Determine month number
      *
      * Adjust day number case last day of leap year February
      *
     C                     SETOF                     9293
     C   91      ZDAYN1    COMP 59                     9293
     C   91N92   ZDAYN1    SUB  1         ZDAYN1
      *
      * Calculate month number
      *
     C                     Z-ADD2         ZWRK2
     C           ZDTAG2    TAG                             ** ZDTAG2 TAG *
     C           ZDAYN1    COMP ZMDY,ZWRK2             94
     C  N94      ZWRK2     ADD  1         ZWRK2
     C  N94                GOTO ZDTAG2
     C           ZWRK2     SUB  1         ZWRK2
      *
     C                     Z-ADDZWRK2     ZMTH    20       MONTH
      *
      * Determine day of month
      *
      * Add back last day of year adjustment
      *
     C           ZDAYN1    ADD  1         ZDAYN1
      *
      * Calculate day of month
      *
     C           ZDAYN1    SUB  ZMDY,ZWRK2ZDAY    20       DAY
      *
      * Add back leap year 29th February adjustment,if required
      *
     C   93      ZDAY      ADD  1         ZDAY
      *
      * Format date, DDMMYY or MMDDYY
      *
     C  N98                MOVELZDAY      ZWRK4   40
     C   98                MOVE ZDAY      ZWRK4
     C  N98                MOVE ZMTH      ZWRK4
     C   98                MOVELZMTH      ZWRK4
     C                     MOVELZWRK4     ZDATE
     C                     MOVE ZYEAR     ZDATE
      *
      * Format alpha date, DDMMMYY.
      *
     C           ZDAY      COMP 10                     95
     C                     MOVELZDAY      ZWRK5   5
     C   95                MOVEL' '       ZWRK5
     C                     MOVE ZMNM,ZMTH ZWRK5
     C                     MOVELZWRK5     ZADATE
     C                     MOVE ZYEAR     ZADATE
      *
     C           ZDEND2    ENDSR                           * ZDEND2 ENDSR*
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
** MSG
Only one field may be entered
No fields entered
Invalid entry
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
