      *****************************************************************
      *                                                               *
      *  Last Amend No. DUG502 *CREATE     Date 29Nov17               *
      *                                                               *
      *------------------------------------------------------------------
      *                                                               *
      *  DUG502 - Security Diary Enhancements                         *
      *                                                               *
      *****************************************************************
      *
     A          R XECDETLS                  TEXT('(c) Finastra Technology, +
     A                                      Inc. 2017')
     A                                      RTNDTA
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
      *
     A            DELT          10A  O  6 70DSPATR(HI)
     A                                      DSPATR(BL)
     A                                      OVRDTA
     A                                  6  2'Nominal Position    '
     A            SPNMP         17A  B  6 25DSPATR(HI)
     A N06N53                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A                                      OVRDTA
     A  53                                  DSPATR(RI)
     A  53
     AO 29
     AO 30                                  DSPATR(PC)
      *
     A                                  6 44'Nominal Ccy'
     A            NMCY           3A  O  6 58DSPATR(HI)
     A                                      OVRDTA
     A  19                              7  2'Payment/Value Date'
     A  19        SPSVL          6A  B  7 25DSPATR(HI)
     A N06N29                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  29
     AO 30                                  DSPATR(PC)
     A  29                                  DSPATR(RI)
     A                                      OVRDTA
      *
     A            SPMTRV         8A  O  7 72DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(BL)
     A N32                                  DSPATR(ND)
      *
     A N86                              8  2'Book Code           '
     A N86        SPBCD          2A  B  8 25DSPATR(HI)
     A N06N35N86                            DSPATR(UL)
     A  06N86                               DSPATR(PR)
     A  35N86                               DSPATR(RI)
     A  35N86
     AO 30N86                               DSPATR(PC)
     A N86                                  OVRDTA
      *
     A                                  8 52'Profit Centre '
     A  84
     AON82                                  DSPATR(ND)
     A                                      OVRATR
      *
     A            SPPFC          4A  B  8 67DSPATR(HI)
     A  84
     AON82                                  DSPATR(ND)
     A  83N54N06                            DSPATR(UL)
     A  06
     AO 84
      *
     AO 68
     AON83                                  DSPATR(PR)
     A  54                                  DSPATR(RI)
     A  54
     AO 30                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A                                  9  2'Amount Due          '
     A            SPAMD         14A  B  9 25DSPATR(HI)
     A N06N50                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A                                      OVRDTA
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
      *
     A  18                              9 44'Due currency'
     A  18        SPNCY          3A  O  9 58DSPATR(HI)
     A                                      OVRDTA
      *
     A                                 10  2'Pay/Receive         '
     A            SPPRE          1A  B 10 25DSPATR(HI)
     A N06N36                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A                                 11  2'Settlement Currency '
     A            SPSCU          3A  B 11 25DSPATR(HI)
     A N06N37                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A  13N14                          10 52'Margin  -'
     A                                 12  2'Exchange Rate/ MD Ind'
     A            SPMDI          1A  B 12 41DSPATR(HI)
     A N06N51                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A  13N14                          11 53'Points'
     A  13N14     SFXMP          9A  B 11 64DSPATR(HI)
     A N56N06                               DSPATR(UL)
     A  56                                  DSPATR(RI PC)
     A  06                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 13  2'Base Ccy Rate       '
     A            SPBRX         15A  B 13 25DSPATR(HI)
     A N06N38                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A                                      OVRDTA
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
      *
     A  13N14                          12 53'Amount'
     A  13N14     SMAMT         15A  O 12 64DSPATR(HI)
     A                                      OVRDTA
      *
     A                                 14  2'Settlement Method   '
     A            SPSMT          2A  B 14 25DSPATR(HI)
     A N06N39                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A                                 15  2'Settlement Account  '
     A            SPSEA         18A  B 15 25DSPATR(HI)
     A N06N40                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A                                 13 52'Tax amount deducted  -'
     A  09                                  DSPATR(ND)
      *
     A  81                             16  2'Settlement Branch   '
     A            SPSEB          3A  B 16 25DSPATR(HI)
     A N06N41                               DSPATR(UL)
     A N81                                  DSPATR(ND)
     A N81
     AO 06                                  DSPATR(PR)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A                                 14 53'at source'
     A  09                                  DSPATR(ND)
     A            STASO         14A  B 14 64DSPATR(HI)
     A  09                                  DSPATR(ND)
     A N70N06N66                            DSPATR(UL)
     A  06
     AO 66
     AO 09                                  DSPATR(PR)
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A                                 17  2'Counterparty Bank   '
     A            SPCPN         10A  B 17 25DSPATR(HI)
     A N06N42                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A                                 15 53'refundable'
     A  09                                  DSPATR(ND)
     A            STASR         14A  B 15 64DSPATR(HI)
     A  09                                  DSPATR(ND)
     A N71N06N66                            DSPATR(UL)
     A  06
     AO 66
     AO 09                                  DSPATR(PR)
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A                                 18  2'For A/c of          '
     A            SPFAC         10A  B 18 25DSPATR(HI)
     A N06N43                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A                                 16 53'by user'
     A  09                                  DSPATR(ND)
     A            STAUS         14A  B 16 64DSPATR(HI)
     A  09                                  DSPATR(ND)
     A N72N06N66                            DSPATR(UL)
     A  06
     AO 66
     AO 09                                  DSPATR(PR)
     A  72                                  DSPATR(RI)
     A  72                                  DSPATR(PC)
     A                                      OVRDTA
      *                                              End   /CAC002/
     A                                 19  2'Special Instructions'
     A            SPSPI         35A  B 19 25DSPATR(HI)
     A N06N44                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A                                      OVRDTA
     A                                 20  2'Charge code/Amount'
     A                                 21  2'Commission Code/Amount'
     A                                 22  2'Settlement Amount'
     A            SPCHC          2A  B 20 25DSPATR(HI)
     A  45                                  DSPATR(RI)
     A N06N45                               DSPATR(UL)
     A  45                                  DSPATR(PC)
     A  06                                  DSPATR(PR)
     A                                      OVRDTA
     A            SPCHA         15A  B 20 28DSPATR(HI)
     A  46                                  DSPATR(RI)
     A N06N46                               DSPATR(UL)
     A  46                                  DSPATR(PC)
     A  06                                  DSPATR(PR)
     A                                      OVRDTA
     A            SPCCD          2A  B 21 25DSPATR(HI)
     A  47                                  DSPATR(RI)
     A N06N47                               DSPATR(UL)
     A  47                                  DSPATR(PC)
     A  06                                  DSPATR(PR)
     A                                      OVRDTA
     A            SPCAM         15A  B 21 28DSPATR(HI)
     A  48                                  DSPATR(RI)
     A N06N48                               DSPATR(UL)
     A  48                                  DSPATR(PC)
     A  06                                  DSPATR(PR)
     A                                      OVRDTA
     A            SPSAT         15A  B 22 25DSPATR(HI)
     A  49                                  DSPATR(RI)
     A N06N49                               DSPATR(UL)
     A  49                                  DSPATR(PC)
     A  19
     AO 06                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 17 52'EU Tax amount deducted  -'
     A N78                                  DSPATR(ND)
     A                                      OVRATR
     A                                 18 53'at source'
     A N78                                  DSPATR(ND)
     A                                      OVRATR
     A            STXNM         16A  B 18 64DSPATR(HI)
     A N76N06                               DSPATR(UL)
     A  76                                  DSPATR(RI PC)
     A  06
     AO 66
     AON78                                  DSPATR(PR)
     A N78                                  DSPATR(ND)
     A                                      OVRDTA
     A            SPSXR         15A  B 12 25DSPATR(HI)
     A  52
     AO 34                                  DSPATR(RI)
     A N06N52N34                            DSPATR(UL)
     A  52
     AO 34                                  DSPATR(PC)
     A  06                                  DSPATR(PR)
     A                                      OVRDTA
     A            SPTAXL         1A  B 21 62DSPATR(HI)
     A  80                                  DSPATR(PC RI)
     A N06N80                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A N07                                  DSPATR(ND)
     A                                      OVRDTA
     A            SPCHCD         2A  O 22 61DSPATR(HI)
     A N07                                  DSPATR(ND)
     A                                      OVRDTA
     A            SPTAXA        15A  O 21 64DSPATR(HI)
     A N07                                  DSPATR(ND)
     A                                      OVRDTA
     A            SPCHAM        15A  O 22 64DSPATR(HI)
     A N07                                  DSPATR(ND)
     A                                      OVRDTA
     A  07                             20 44'Stamp Tax -'
     A  07                             21 45'Liable/Amount'
     A  07                             22 44'Charge code/Amt'
      *
     A          R XEYINPUT
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                  1  2'POSITION SETTLEMENTS'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 25'SE0170'
     A            RUNA           7A  O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78
     A                                  2  2'Action:'
     A            ACTN           1A  B  2 10DSPATR(HI)
     A N05N21                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A  81                              2 12'Booking Branch'
     A            SPBCA          3A  B  2 27DSPATR(HI)
     A N05N22                               DSPATR(UL)
     A N81                                  DSPATR(ND)
     A  05
     AON81                                  DSPATR(PR)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A                                  2 31'Security'
     A            SPSSH         10A  B  2 40DSPATR(HI)
     A N05N23                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A                                  2 51'Cust'
     A            SPCPY         10A  B  2 56
     A  24                                  DSPATR(RI)
     A N05N24                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
     A  20                              2 67'Tax Basket'
     A N20                                  DSPATR(ND)
     A                                      OVRATR
     A            STXBS          2A  B  2 78
     A  58                                  DSPATR(RI PC)
     A N05N58                               DSPATR(UL)
     A  05
     AON20                                  DSPATR(PR)
     A  20                                  DSPATR(HI)
     A N20                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A  20                              3  2'Benf.Cust/Owner no.'
     A N20                                  DSPATR(ND)
     A                                      OVRATR
     A            SBNFO         20A  B  3 22
     A  59                                  DSPATR(RI PC)
     A N05N59                               DSPATR(UL)
     A  05
     AON20                                  DSPATR(PR)
     A  20                                  DSPATR(HI)
     A N20                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A                                  3 43'Review From'
     A                                      OVRATR
     A  05                                  DSPATR(ND)
      *
     A            PPBCA          3A  B  3 55DSPATR(HI)
     A                                      DSPATR(UL)
     A  05
     AON81                                  DSPATR(PR)
     A  05
     AON81                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A            PPSSH         10A  B  3 59DSPATR(HI)
     A                                      DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  05                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A            PPCPY         10A  B  3 70DSPATR(UL)
     A  05                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A  05                                  DSPATR(ND)
     A            PPDUD          6A  B  4 53DSPATR(HI)
     A                                      DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  05                                  DSPATR(ND)
     A                                      OVRDTA
     A            PPEVT          2A  B  4 61DSPATR(HI)
     A                                      DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  05                                  DSPATR(ND)
     A                                      OVRDTA
      ** DUG502 changes *IN86 is ON
     A     86     PPBCD          2A  B  4 64DSPATR(HI)
     A     86                               DSPATR(UL)
     A  05 86                               DSPATR(PR)
     A  05 86                               DSPATR(ND)
     A                                      OVRDTA
      *
     A            PPTFR          4A  B  4 67DSPATR(HI)
     A                                      DSPATR(UL)
     A  05
     AON28                                  DSPATR(PR ND)
     A                                      OVRDTA
     A
     A N05                              4 71'Live/All'
     A N73                                  DSPATR(ND)
     A            PPALL          1A  B  4 80DSPATR(HI)
     A N05N31                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A N73
     AO 05                                  DSPATR(ND)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A                                      OVRDTA
     A  20                              5  2'Exemption Code'
     A N20                                  DSPATR(ND)
     A                                      OVRATR
     A            SEXCD          2A  B  5 17
     A  57                                  DSPATR(RI PC)
     A N05N57                               DSPATR(UL)
     A  05
     AON20                                  DSPATR(PR)
     A  20                                  DSPATR(HI)
     A N20                                  DSPATR(ND)
     A                                      OVRDTA
     A N20                              5  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                  4  2'Due Date'
     A            SPDUD          6A  B  4 11DSPATR(HI)
     A N05N25                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  25                                  DSPATR(PC)
     A                                      OVRDTA
     A  25                                  DSPATR(RI)
     A                                  4 23'Event Type'
     A            SPEVT          2A  B  4 34DSPATR(HI)
     A N05N26                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  26                                  DSPATR(RI)
     A  26                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A  86                              4 38'Book'
     A  86        SPBCD          2A  B  4 43DSPATR(HI)
     A N05N80 86                            DSPATR(UL)
     A  05 86                               DSPATR(PR)
     A  80 86                               DSPATR(RI)
     A  80 86                               DSPATR(PC)
     A  86                                  OVRDTA
      *
     A
     A  28                              4 46'Portfolio'
     A            SPPTFR         4A  B  4 48DSPATR(HI)
     A N05N27                               DSPATR(UL)
     A  05
     AON28                                  DSPATR(PR)
     A N28                                  DSPATR(ND)
     A  27                                  DSPATR(RI)
     A  27                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A          R XUBFLREC                  SFL
      *
     A  61                                  SFLNXTCHG
     A            SWARN          1A  H
     A            SEQ            3  0H
     A            SPTNLU         5  0H
     A            ACTN           1A  B  9  2DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI PC)
      *
     A            STAT           1A  O  9  4DSPATR(BL)
     A                                      DSPATR(HI)
     A            SPBCA          3A  O  9  6DSPATR(HI)
     A N81                                  DSPATR(ND)
     A            SPSSH         10A  O  9 10DSPATR(HI)
     A            SPCPY         10A  O  9 21DSPATR(HI)
     A            SPDUD          6A  O  9 32DSPATR(HI)
      * DUG502 changes *IN86 is ON
     A            SPBCD          2A  O  9 39
     A N07 86                               DSPATR(HI)
     A  07 86                               DSPATR(ND)
      *
     A            SPEVT          2A  O  9 44DSPATR(HI)
     A            SPAUI          1A  O  9 48DSPATR(HI)
     A            SFPSVL         6A  O  9 50DSPATR(HI)
     A N19                                  DSPATR(ND)
     A            SPPTFR         4A  O  9 57DSPATR(HI)
     A N28                                  DSPATR(ND)
     A            STXBS          2A  O  9 62DSPATR(HI)
     A N20                                  DSPATR(ND)
     A            SBNFO         20A  O  9 66DSPATR(HI)
     A N20                                  DSPATR(ND)
      *
     A            SRNME         41A  O 10 14TEXT('Security Description')
     A                                      DSPATR(HI)
      *
     A          R XUBFLCTL                  SFLCTL(XUBFLREC)
     A                                      BLINK
     A N03                                  ROLLUP(55)
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A                                      OVERLAY
     A  03 55                               SFLMSGID(CPF5203 QCPFMSG)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A            RECNO          4S 0H      SFLRCDNBR
      *
     A                                  1  2'POSITION SETTLEMENTS'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 25'SE0170'
     A            RUNA           7A  O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78
     A            PPBCA          3A  B  3 56DSPATR(HI)
     A N81                                  DSPATR(PR ND)
      *
     A                                      DSPATR(UL)
     A            PPSSH         10A  B  3 60DSPATR(HI)
     A                                      DSPATR(UL)
     A            PPCPY         10A  B  3 71DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A                                  6  2'X'
     A                                      DSPATR(UL)
      *
     A                                  6  4'S'
     A                                      DSPATR(UL)
     A N81                              6  6'   '
     A  81                              6  6'Brc'
     A                                      DSPATR(UL)
     A                                  6 10'Security'
     A                                      DSPATR(UL)
     A                                  6 21'Client'
     A                                      DSPATR(UL)
     A                                  6 32'Due'
     A                                      DSPATR(UL)
     A                                  7 32'Date'
     A                                      DSPATR(UL)
     A  86                              6 39'Book'
     A  86                                  DSPATR(UL)
     A                                  6 44'Evt'
     A                                      DSPATR(UL)
     A                                  6 48'A'
     A                                      DSPATR(UL)
     A N19                              6 50'     '
     A  19                              6 50'Value'
     A                                      DSPATR(UL)
     A N19                              7 50'    '
     A  19                              7 50'Date'
     A                                      DSPATR(UL)
     A N28                              6 57'    '
     A  28                              6 57'Port'
     A                                      DSPATR(UL)
     A N20                              6 62'   '
     A  20                              6 62'Tax'
     A                                      DSPATR(UL)
     A N20                              7 62'   '
     A  20                              7 62'Bkt'
     A                                      DSPATR(UL)
     A N20                              6 66'          '
     A  20                              6 66'Beneficial'
     A                                      DSPATR(UL)
     A N20                              7 64'              '
     A  20                              7 64'Customer Owner'
     A                                      DSPATR(UL)
     A            PPDUD          6A  B  4 60DSPATR(HI)
     A                                      DSPATR(UL)
     A            PPEVT          2A  B  4 68DSPATR(HI)
     A                                      DSPATR(UL)
     A  86        PPBCD          2A  B  4 72DSPATR(HI)
     A  86                                  DSPATR(UL)
     A            PPTFR          4A  B  4 76DSPATR(HI)
     A                                      DSPATR(UL)
     A N28                                  DSPATR(PR ND)
     A                                  3 44'Review from'
      *