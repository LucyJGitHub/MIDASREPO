      *****************************************************************
      *                                                               *
      *  Last Amend No. DUG506  *CREATE    Date 24Nov17               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  DUG506 - Report changes to SE0800 and SE1130                 *
      *                                                               *
      *****************************************************************
     A*/OMITEND
     A          R HEADL3                    SKIPB(3)
     A                                     6'REFERENCE SE0800P1'
     A            TITL      R             39
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE         004S 0    125EDTCDE(Z) SPACEA(2)
      *
     A N38                                36'SECURITIES TRADING'
     A  38                                48'SECURITY POSITIONS SHEET'
     A N38                                55'SECURITY POSITIONS SHEET FOR'
     A N38        NCRY      R             84
     A  88                                90'BRANCH -'
     A  88        BHBA      R             98
     A  88                               101'-'
     A  88        BRNM      R            102
     A                                   132' '
     A                                      SPACEA(1)
     A N38                                36'-------------------'
     A  38                                48'------------------------'
     A N38                                55'--------------------------------'
      *
     A          R HEADL1                    SPACEB(2)
     A                                     1'BOOK -'
     A            BHBK      R              8
     A            BOKD      R             11
     A N38                                52'INVESTMENT TYPE -'
     A N38        INVT      R             70
     A N38        INAM      R             74
     A N38                                97'MARKET -'
     A N38        TABMNM         9       106
     A  38                               118'CURRENCY -'
     A  38        NCRY      R            129SPACEA(2)
     A  01N38                              4'PROFIT CENTRE - '
     A    N38                               SPACEB(1)
     A  01N38     PRFC           4        20
     A  01N38     PRFN          30        26
     A    N38                            131' '
     A    N38                               SPACEA(2)
     A  38                                 1'INV.'
     A N38                                 1'SECURITY    SECURITY DESCRIPTION'
     A  38                                11'SECURITY'
     A  38                                70'LONG               SHORT'
     A N38                                71'LONG               SHORT'
     A  38                               111'REPO    AVERAGE PRICE'  SPACEA(1)
     A N38                               111'REPO   AVERAGE PRICE'   SPACEA(2)
     A  38                                 1'TYPE' SPACEA(2)
      *
     A          R DETAIL
     A  38        INVT      R              1
     A  38        SRPN      R             11
     A N38        BHSC      R              1
     A N38        ZRNME         41        12
     A            LONG          19        56
     A            SHORT         19        76
     A            REPO          20        96
     A            AVPR          16       117SPACEA(2)
     A          R MKTTOT                    SPACEB(2)
     A N38                                28'TOTALS FOR MARKET'
     A N38        TMKL          19        56
     A N38        TMKS          19        76
     A N38        TMKR          20        96SPACEA(1)
     A          R ITPTOT                    SPACEB(2)
     A N38                                28'TOTALS FOR INVESTMENT TYPE'
     A N38        TINL          19        56
     A N38        TINS          19        76
     A N38        TINR          20        96SPACEA(1)
      *                                              Begin /E29170/
      **   *****************
      **   TRAILER TO REPORT
      **   *****************
      *
     A          R TRAIL                     SPACEB(2)
     A N88N38                             50'*** END OF REPORT ***'
     A  88N38                             50'*** END OF REPORT FOR'
     A  88N38                             72'BRANCH ***'
