      *****************************************************************                       EML102
      *                                                               *                       EML102
      *  Midas - Copy member for GL001416                             *                       EML102
      *                                                               *                       EML102
      *  EML102_018 - Midas GL Retrieve Shadow Postings               *                       EML102
      *                                                               *                       EML102
      *  Last Amend No. EML104             Date 02Mar20               *                       EML102
      *  Prev Amend No. EML102  *CREATE    Date 28Nov19               *                       EML102
      * FBMidas 2.1 ---- Base ----------------------------------------*                       EML102
      *                                                               *                       EML102
      *---------------------------------------------------------------*                       EML102
      *                                                               *                       EML102
      *  EML104 - Upgrade of EML010 to FB2.1 SP21                     *                       EML104
      *           EML010 - Structured field :86:                      *                       EML104
      *  EML102 - Upgrade of EML004 to FB2.1 SP21                     *                       EML102
      *           EML004 - Extended Narrative                         *                       EML102
      *                                                               *                       EML102
      *****************************************************************                       EML102
      *========================================================================*              EML102
      * $LoadNarr : Load Varacity Extended Narrative                           *              EML102
      *------------------------------------------------------------------------*              EML102
                                                                                              EML102
     C     $LoadNarr     BEGSR                                                                EML102
      *    --------      -----                                                                EML102
                                                                                              EML102
      *  If Switchable EML102 inactive, exit                                                  EML102
     C                   If        EML102='N'                                                 EML102
     C                   Leavesr                                                              EML102
     C                   Endif                                                                EML102
      *                                                                                       EML104
      ***  EML104 - check Network MultiCash Newtork Flag                                      EML104
      *                                                                                       EML104
     C                   Move      ' '           EDMCNW                                       EML104
     C                   If        EML104 = 'Y'                                               EML104
     C                   CALLB     'AONWRKR1'                                                 EML104
     C                   PARM      *BLANKS       PRtcd             7            Return Code   EML104
     C                   PARM      '*KEY'        POptn             7            Option        EML104
     C                   PARM      MRNWRK        PNwrk             6            Network       EML104
     C     SDNWRK        PARM      SDNWRK        DSLDY                                        EML104
     C                   Endif                                                  EML104        EML104
      *                                                                                       EML102
      ** Get narrative from GLJENPY0                                                          EML102
      *                                                                                       EML102
     C     KeyGLJEN      Chain     GLJENPY0                                                   EML102
     C                   If        %Found                                                     EML102
     C                   Eval      arExtNarr(1) =FNA02                                        EML102
     C                   Eval      arExtNarr(2) =FNA03                                        EML102
     C                   Eval      arExtNarr(3) =FNA04                                        EML102
     C                   Eval      arExtNarr(4) =FNA05                                        EML102
     C                   Eval      arExtNarr(5) =FNA06                                        EML102
     C                   Eval      arExtNarr(6) =FNA07                                        EML102
     C                   Eval      arExtNarr(7) =FNA08                                        EML102
     C                   Eval      arExtNarr(8) =FNA09                                        EML102
     C                   Eval      arExtNarr(9) =FNA10                                        EML102
     C                   Eval      arExtNarr(10)=FNA11                                        EML102
     C                   Eval      arExtNarr(11)=FNA12                                        EML102
     C                   Eval      arExtNarr(12)=FNA13                                        EML102
     C                   Eval      arExtNarr(13)=FNA14                                        EML102
     C                   Eval      arExtNarr(14)=FNA15                                        EML102
     C                   Eval      arExtNarr(15)=FNA16                                        EML102
     C                   Eval      arExtNarr(16)=FNA17                                        EML102
                                                                                              EML102
     C                   Eval      ArAddInfo=*Blanks                                          EML102
     C                   Eval      ReturnCode=*Blanks                                         EML102
                                                                                              EML102
      *  Call API to format entended narrative array                                          EML102
                                                                                              EML104
     C                   If        EDMCNW <> 'F' OR                                           EML104
     C                             EML104 <> 'Y'                                              EML104
                                                                                              EML104
     C                   Call      'MGUF86'                                                   EML102
     C                   PARM                    ReturnCode                     Return Code   EML102
     C                   PARM                    ArExtNarr                      Extended NarraEML102
     C                   PARM                    ArAddInfo                      Additional InfEML102
                                                                                              EML104
     C                   Else                                                                 EML104
      *                                                                                       EML104
      ***  EML104 specific Formatting of Field 86:                                            EML104
     C                   Call      'MGUF86SP'                                                 EML104
     C                   PARM                    ReturnCode                     Return Code   EML104
     C                   PARM                    ArExtNarr                      Extended NarraEML104
     C                   PARM                    ArAddInfo                      Additional InfEML104
                                                                                              EML104
     C                   EndIf                                                                EML104
                                                                                              EML102
     C                   If        ReturnCode=*Blanks                                         EML102
     C                   Eval      GLMGADI1 = arAddInfo(1)                                    EML102
     C                   Eval      GLMGADI2 = arAddInfo(2)                                    EML102
     C                   Eval      GLMGADI3 = arAddInfo(3)                                    EML102
     C                   Eval      GLMGADI4 = arAddInfo(4)                                    EML102
     C                   Eval      GLMGADI5 = arAddInfo(5)                                    EML102
     C                   Eval      GLMGADI6 = arAddInfo(6)                                    EML102
     C                   Endif                                                                EML102
                                                                                              EML102
     C                   Endif                                                                EML102
                                                                                              EML102
     C     @LoadNarr     ENDSR                                                                EML102
      *    --------      -----                                                                EML102
                                                                                              EML102
      *****************************************************************                       EML102
      * End of /COPY EML102_018                                       *                       EML102
      *****************************************************************                       EML102
