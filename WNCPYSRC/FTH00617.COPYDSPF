      *****************************************************************                       ERZ123
      *                                                               *                       ERZ123
      *  Midas - /COPY Member (FTOPAYBDF)                             *                       ERZ123
      *                                                               *                       ERZ123
      *  FTH00617 - Midas FT Outgoing payments browse                 *                       ERZ123
      *                                                               *                       ERZ123
      *  (c) Misys International Banking Systems Ltd. 2013            *                       ERZ123
      *                                                               *                       ERZ123
      *  Last Amend No. 259456             Date 29Jan13               *                       259456
      *  Prev Amend No. ERZ123   *CREATE   Date 29Jan13               *                       ERZ123
      *                                                               *                       ERZ123
      *---------------------------------------------------------------*                       ERZ123
      *                                                               *                       ERZ123
      *  259456 - ERZ123 : Add dummy record format                    *                       259456
      *  ERZ123 - FT Select Payment on Beneficiary Line 1             *                       ERZ123
      *                                                               *                       ERZ123
      *****************************************************************                       ERZ123
      *                                                                                       ERZ123
      **Start ERZ123                                                                          ERZ123
      *                                                                                       ERZ123
      *                                                                                       259456
      **Start 259456                                                                          259456
      *                                                                                       259456
      * -------------------------------------------------------------------*                  259456
      ** DUMMY RECORD                                                      *                  259456
      * -------------------------------------------------------------------*                  259456
     A          R DUMMY                     TEXT('DUMMY FILE')                                259456
     A                                  7  1'                                  '              259456
     A                                  7 35'                                  '              259456
     A                                  7 69'            '                                    259456
      *                                                                                       259456
      **End   259456                                                                          259456
      *                                                                                       259456
      * -------------------------------------------------------------------*                  ERZ123
      ** SELECT SCREEN                                                     *                  ERZ123
      * -------------------------------------------------------------------*                  ERZ123
     A          R FTOPAYS4                  TEXT('SELECT SCREEN SUBFILE')                     ERZ123
     A                                      SFL                                               ERZ123
     A            DDOPT          1   B  8  3TEXT('SELECT OPTION')                             ERZ123
     A                                      DSPATR(UL)                                        ERZ123
     A            DDPREF        15A  O  8  8TEXT('Payment reference')                         ERZ123
     A                                      DSPATR(HI)                                        ERZ123
     A            DDBNC1        35A  O  8 26TEXT('Beneficiary Line 1')                        ERZ123
     A            DDCCY          3A  O  8 68TEXT('Currency')                                  ERZ123
      * -------------------------------------------------------------------*                  ERZ123
      ** SELECT SCREEN CONTROL                                             *                  ERZ123
      * -------------------------------------------------------------------*                  ERZ123
     A          R FTOPAYS5                  TEXT('SELECT SCREEN CONTROL')                     ERZ123
     A                                      CF03                                              ERZ123
     A                                      CF12                                              ERZ123
     A                                      CF11                                              ERZ123
     A                                      SFLCTL(FTOPAYS4)                                  ERZ123
     A                                      SFLDSP                                            ERZ123
     A                                      SFLDSPCTL                                         ERZ123
     A  96                                  SFLEND                                            ERZ123
     A  97                                  SFLCLR                                            ERZ123
     A                                      SFLSIZ(29)                                        ERZ123
     A                                      SFLPAG(14)                                        ERZ123
     A N96                                  ROLLUP(98)                                        ERZ123
     A                                      OVERLAY                                           ERZ123
     A                                      PUTOVR                                            ERZ123
     A                                      BLINK                                             ERZ123
     A            DDSFRN         4  0H      SFLRCDNBR                                         ERZ123
     A                                  1  2'FTOPAYB4'                                        ERZ123
     A                                  1 26'            Midas            '                   ERZ123
     A                                      DSPATR(HI)                                        ERZ123
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')                                ERZ123
     A                                      OVRDTA                                            ERZ123
     A                                      TEXT('TIME')                                      ERZ123
     A                                  1 72'WID:'                                            ERZ123
     A            DDWID          3      1 77TEXT('WORKSTATION ID')                            ERZ123
     A                                  3 28'FT - OUTGOING PAYMENTS'                          ERZ123
     A                                  4 37'ENQUIRY'                                         ERZ123
     A                                  6  2'Sel'                                             ERZ123
     A                                  6  8'Payment ref'                                     ERZ123
     A                                  6 26'Beneficiary A/c Line 1'                          ERZ123
     A                                  6 68'Ccy'                                             ERZ123
     A            S1PREF        15A  B  7  8TEXT('Payment reference')                         ERZ123
     A                                      DSPATR(UL)                                        ERZ123
     A            S1BNC1        35A  B  7 26TEXT('Beneficiary Line 1')                        ERZ123
     A                                      DSPATR(UL)                                        ERZ123
     A            S1CCY          3A  B  7 68TEXT('Currency')                                  ERZ123
     A                                      DSPATR(UL)                                        ERZ123
      **End   ERZ123                                                                          ERZ123
      *****************************************************************                       ERZ123
      * End of /COPY FTH00617                                         *                       ERZ123
      *****************************************************************                       ERZ123
