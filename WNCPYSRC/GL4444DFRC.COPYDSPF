      *****************************************************************                       EML102
      *                                                               *                       EML102
      *  Midas - /COPY Member GL4444DF                                *                       EML102
      *                                                               *                       EML102
      *  GL4444DFRC - Midas Allow enquiry on Extend narrative         *                       EML102
      *                                                               *                       EML102
      *  Last Amend No. EML102  *CREATE    Date 28Nov19               *                       EML102
      *                                                               *                       EML102
      *---------------------------------------------------------------*                       EML102
      *                                                               *                       EML102
      *  EML102 - Upgrade of UNL007, EML004 to FB2.1 SP21             *                       EML102
      *           EML004 - Extended Narrative                         *                       EML102
      *           UNL007 - Extended Narratives                        *                       EML102
      *           BUG24535 - Application error in Account Movements   *                       EML102
      *                      and Account  Master Enquiry (Recompile)  *                       EML102
      *                                                               *                       EML102
      *****************************************************************                       EML102
      *
      *  Format: subfile for posting records
      *
     A          R SFLRD2                    SFL
     A            SELEC          1A  B  9  2TEXT('SELECTION')
     A                                      CHECK(ER)
     A            SFLDS4        93A  O  9  4TEXT('SUBFILE REC.')
     A                                      DSPATR(HI)
     A            UFPTDT         5S 0H      TEXT('Posting date')
     A            UFBTNB         3A  H      TEXT('Batch number')
     A            UFBINB         3S 0H      TEXT('Batch item number')
      *
      *  Format: subfile Control for selection criteria
      *
     A          R SFLCTL2                   SFLCTL(SFLRD2)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A  02                                  SFLDSP
     A  03                                  SFLDSPCTL
     A N03N02
     AO 50                                  SFLCLR
     A  04                                  SFLEND
     A  05                                  SFLMSGID(CPF5203 QCPFMSG)
     A N04 02                               ROLLUP(25)
     A  50N83 01
     AON50    01                            CF05
     A                                      OVERLAY
     A                                      PUTOVR
     A  50                                  ERASE(SFLRD2)
     A            DSPREC         4S 0H      SFLRCDNBR
     A                                 21  2'F3=Exit'
     A                                 21 11'F5=Refresh'
     A                                 21 23'F13=Acct Details'
     A                                 21 41'F14=Acct Mvmts'
     A                                 21 57'F15=Proj Acct Mvmts'
     A                                 22 57'N=Extended narrative'
     A                                 22  2'Enter Account'
     A            CNUMR         10A  B 22 16
     A                                      OVRDTA
     A N64                                  DSPATR(HI)
     A  64                                  DSPATR(RI PC)
     A            CURRY          3A  B 22 27
     A                                      OVRDTA
     A N64                                  DSPATR(HI)
     A  64                                  DSPATR(RI)
     A            ACCOD         10A  B 22 31
     A                                      OVRDTA
     A N64                                  DSPATR(HI)
     A  64                                  DSPATR(RI)
     A            ACSEQ          2A  B 22 42
     A                                      OVRDTA
     A N64                                  DSPATR(HI)
     A  64                                  DSPATR(RI)
     A            BRNCH          3A  B 22 45
     A                                      OVRDTA
     A N64                                  DSPATR(HI)
     A  52
     AO 64                                  DSPATR(RI)
     A  21                                  DSPATR(ND)
     A  21                                  DSPATR(PR)
     A                                 23  2'Enter Value Date'
     A            VALDA          6A  B 23 21
     A                                      OVRDTA
     A N65                                  DSPATR(HI)
     A  65                                  DSPATR(RI PC)
     A                                 23 31'Amount'
     A            PORM           1A  B 23 38
     A                                      OVRDTA
     A N66                                  DSPATR(HI)
     A  66                                  DSPATR(RI)
     A            AMONT         14A  B 23 40
     A                                      OVRDTA
     A N67                                  DSPATR(HI)
     A  67                                  DSPATR(RI PC)
     A                                 23 59'Cheque No.'
     A            CQNMR          8A  B 23 70
     A                                      OVRDTA
     A N68                                  DSPATR(HI)
     A  68                                  DSPATR(RI PC)
     A            ERRDSP        76A  O 24 01DSPATR(HI)
     A  50                                  DSPATR(RI)
     A                                      OVRDTA
     A N50N48                               DSPATR(ND)
     A          R STATIC2
     A                                      PUTOVR
     A                                      OVERLAY
     A                                  8  4'Posting'
     A                                      OVRATR
     A
     A  50
     AON83                                  DSPATR(ND)
     A                                  8 14'Value'
     A                                      OVRATR
     A  50
     AON83                                  DSPATR(ND)
     A                                  8 21'Cheque'
     A                                      OVRATR
     A  50
     AON83                                  DSPATR(ND)
     A                                  8 30'Movement'
     A                                      OVRATR
     A                                  8 61'Tr. #'
     A                                      OVRATR
     A  50
     AON83                                  DSPATR(ND)
     A  82                              8 68'Batch'
     A                                      OVRATR
     A  50
     AON83                                  DSPATR(ND)
     A                                  8 90'Amount'
     A                                      OVRATR
     A  50
     AON83                                  DSPATR(ND)
      *****************************************************************                       EML102
      * End of /COPY GL4444DFRC                                       *                       EML102
      *****************************************************************                       EML102
