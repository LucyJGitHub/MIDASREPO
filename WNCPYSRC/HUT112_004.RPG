      *****************************************************************                       HUT112
      *                                                               *                       HUT112
      *  Midas - /COPY Member FT0647                                  *                       HUT112
      *                                                               *                       HUT112
      *  HUT112_004 - Suppression of Tag 72 from Cover MT202 Message  *                       HUT112
      *                                                               *                       HUT112
      *  Last Amend No. HUT112  *CREATE    Date 30Mar20               *                       HUT112
      *                                                               *                       HUT112
      *---------------------------------------------------------------*                       HUT112
      *                                                               *                       HUT112
      *  HUT112 - Upgrade HUT025 to FBM 2.1 SP22                      *                       HUT112
      *           Suppression of Tag 72 from Cover MT202 Message      *                       HUT112
      *                                                               *                       HUT112
      *****************************************************************                       HUT112
      *                                                                                       HUT112
      ** If HUT112 is switched on and generated together with MT103,                          HUT112
      ** check if Bank to Bank Info line 1 matches any codeword                               HUT112
      ** in XXC72SPD                                                                          HUT112
      *                                                                                       HUT112
      *                                                                                       HUT112
     C                     MOVE 'N'       SUPRES  1                                           HUT112
      *                                                                                       HUT112
     C           HUT112    IFEQ 'Y'                                                           HUT112
     C           RMTPY     ANDEQ'103'                                                         HUT112
     C           OPCBB1    ANDNE*BLANKS                                                       HUT112
      *                                                                                       HUT112
      ** Read tag 72 codewords suppression file                                               HUT112
      *                                                                                       HUT112
     C           *LOVAL    SETLLXXC72SD0                                                      HUT112
     C                     READ XXC72SD0                 14                                   HUT112
      *                                                                                       HUT112
     C           *IN14     DOWEQ'0'                                                           HUT112
      *                                                                                       HUT112
      ** Translate all to uppercase                                                           HUT112
      *                                                                                       HUT112
     C           LO:UP     XLATECDEWRD    XDEWRD 12                                           HUT112
     C           LO:UP     XLATEOPCBB1    XPCBB1 35                                           HUT112
      *                                                                                       HUT112
      ** Check the length for proper scanning                                                 HUT112
      *                                                                                       HUT112
     C           ' '       CHEKRXDEWRD    LEN     30                                          HUT112
      *                                                                                       HUT112
      ** Scan according to length of codeword                                                 HUT112
      *                                                                                       HUT112
     C                     SELEC                                                              HUT112
     C           LEN       WHEQ 3                                                             HUT112
     C                     MOVELXDEWRD    CDEW03  3                                           HUT112
     C           CDEW03    SCAN XPCBB1                   76                                   HUT112
      *                                                                                       HUT112
     C           LEN       WHEQ 4                                                             HUT112
     C                     MOVELXDEWRD    CDEW04  4                                           HUT112
     C           CDEW04    SCAN XPCBB1                   76                                   HUT112
      *                                                                                       HUT112
     C           LEN       WHEQ 5                                                             HUT112
     C                     MOVELXDEWRD    CDEW05  5                                           HUT112
     C           CDEW05    SCAN XPCBB1                   76                                   HUT112
      *                                                                                       HUT112
     C           LEN       WHEQ 6                                                             HUT112
     C                     MOVELXDEWRD    CDEW06  6                                           HUT112
     C           CDEW06    SCAN XPCBB1                   76                                   HUT112
      *                                                                                       HUT112
     C           LEN       WHEQ 7                                                             HUT112
     C                     MOVELXDEWRD    CDEW07  7                                           HUT112
     C           CDEW07    SCAN XPCBB1                   76                                   HUT112
      *                                                                                       HUT112
     C           LEN       WHEQ 8                                                             HUT112
     C                     MOVELXDEWRD    CDEW08  8                                           HUT112
     C           CDEW08    SCAN XPCBB1                   76                                   HUT112
      *                                                                                       HUT112
     C           LEN       WHEQ 9                                                             HUT112
     C                     MOVELXDEWRD    CDEW09  9                                           HUT112
     C           CDEW09    SCAN XPCBB1                   76                                   HUT112
      *                                                                                       HUT112
     C           LEN       WHEQ 10                                                            HUT112
     C                     MOVELXDEWRD    CDEW10 10                                           HUT112
     C           CDEW10    SCAN XPCBB1                   76                                   HUT112
      *                                                                                       HUT112
     C           LEN       WHEQ 11                                                            HUT112
     C                     MOVELXDEWRD    CDEW11 11                                           HUT112
     C           CDEW11    SCAN XPCBB1                   76                                   HUT112
      *                                                                                       HUT112
     C           LEN       WHEQ 12                                                            HUT112
     C                     MOVELXDEWRD    CDEW12 12                                           HUT112
     C           CDEW12    SCAN XPCBB1                   76                                   HUT112
      *                                                                                       HUT112
     C                     ENDSL                                                              HUT112
      *                                                                                       HUT112
      ** Suppress if Bank to Bank Info Line 1 matches any codewords defined                   HUT112
      *                                                                                       HUT112
     C           *IN76     IFEQ '1'                                                           HUT112
     C                     MOVE '1'       *IN14                                               HUT112
     C                     MOVE 'Y'       SUPRES                                              HUT112
     C                     ENDIF                                                              HUT112
      *                                                                                       HUT112
     C                     READ XXC72SD0                 14                                   HUT112
      *                                                                                       HUT112
     C                     ENDDO                                                              HUT112
      *                                                                                       HUT112
     C                     ENDIF                                                              HUT112
      *                                                                                       HUT112
      ** Do not suppress if no match found                                                    HUT112
      *                                                                                       HUT112
     C           SUPRES    IFNE 'Y'                                                           HUT112
      *                                                                                       HUT112
      *****************************************************************                       HUT112
      * End of /COPY HUT112_004                                       *                       HUT112
      *****************************************************************                       HUT112
