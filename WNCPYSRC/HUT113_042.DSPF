      *****************************************************************
      *  Start of /COPY HUT113_042                                    *
      *****************************************************************
      *                                                               *
      *  PMPFDM3DDF - Portfolio Definition Details                    *
      *                                                               *
      *  Last Amend No. HUT113    *Create  Date 01Apr20               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  HUT113 - Full Private Banking Functionality in Midas         *
      *           (Upgrade HUT026 - MD-54322 - to FM2.1 SP22)         *
      *   MD054322 - Portfolio Management - SE - Incorrect holdings   *
      *              for custody fee calculation report.              *
      *              Allow portfolio definition for PB customer.      *
      *****************************************************************
      ** Start of change MD054322
     A          R PMPFDMD3                  TEXT('Rcd fmt used when cust. +
     A                                      is not a PB Customer')
      *
     A                                      CF03
     A                                      CF12
     A  72                                  CF07
     A  73                                  CF08
     A  74                                  CF15
      *
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A            PSUSER        10A  O  1  2TEXT('User Name')
     A                                  1 13'PMPFDMD'
     A                                  1 28'Portfolio Definition Maintenance'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73TEXT('Midas rundate')
     A            PSJOBNAME     10A  O  2  2TEXT('Workstation ID')
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  3  2'Action'
     A            DDACTN         1A  O  3 12TEXT('Action code')
     A N10                                  DSPATR(HI)
     A  10                                  DSPATR(RI)
     A  68
     AO 10                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            DDDELT        16   O  3 62TEXT('Closed?')
     A                                      DSPATR(HI BL)
     A                                      OVRDTA
      *
     A                                  4 02'Customer'
     A            DDCSNM        10A  O  4 12
     A N11                                  DSPATR(HI)
     A  11                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                  4 25'Portfolio'
     A            DDPREF         4A  O  4 36TEXT('Portfolio Reference')
     A N12                                  DSPATR(HI)
     A  12                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                  5  2'                                  -
     A                                                                         -
     A                                                '
     A                                      DSPATR(UL)
      *
     A                                  7  5'PM Port. Ccy'
     A            DDPTCY    R        B  7 25TEXT('Portfolio Currency')
     A N13                                  DSPATR(HI)
     A N81                                  DSPATR(ND)
     A N69                                  DSPATR(UL)
     A N81
     AO 69                                  DSPATR(PR)
     A  13                                  DSPATR(RI)
     A  13                                  DSPATR(PC)
     A                                      REFFLD(PTCY)
     A                                      OVRDTA
      *
     A                                  7 40'Narrative'
     A            DDPTFN    R        B  7 60TEXT('Portfolio Narrative')
     A N14                                  DSPATR(HI)
     A N81                                  DSPATR(ND)
     A N70                                  DSPATR(UL)
     A N81
     AO 70                                  DSPATR(PR)
     A  14                                  DSPATR(RI)
     A  14                                  DSPATR(PC)
     A                                      REFFLD(PTFN)
     A                                      OVRDTA
      *
     A                                  9  5'Date Opened'
     A            DDDPOP         6A  O  9 25TEXT('Date Opened')
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
     A  81                              9 40'Custody Group Code'
     A            DDCFGC    R        B  9 60TEXT('CF Group Code')
     A                                      REFFLD(CFGC)
     A N16                                  DSPATR(HI)
     A N81                                  DSPATR(ND)
     A N70                                  DSPATR(UL)
     A N81
     AO 70                                  DSPATR(PR)
     A  16                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 11  5'Date Closed'
     A            DDDPCL         6A  O 11 25TEXT('Date Closed')
     A                                      OVRDTA
      *
     A            DDMGTP         3A  H      TEXT('Management Type')
     A            DDINTP         3A  H      TEXT('Investment Profile Code')
      *
     A                                 11 40'Managed Ind.(M/P/U)'
     A            DDMNGI    R        B 11 60
     A                                      REFFLD(MNGI)
     A N17                                  DSPATR(HI)
     A N81                                  DSPATR(ND)
     A N70                                  DSPATR(UL)
     A N81
     AO 70                                  DSPATR(PR)
     A  17                                  DSPATR(RI)
     A  17                                  DSPATR(PC)
     A                                      OVRDTA
      ** End of change MD054322
      *****************************************************************
      * End of /COPY PMPFDM3D01                                       *
      *****************************************************************
