      *****************************************************************                       ERZ210
      *                                                               *                       ERZ210
      *  Midas - /COPY Member (LE0470)                                *                       ERZ210
      *                                                               *                       ERZ210
      *  LE0470CCP3 - Midas LE Generate Account Postings              *                       ERZ210
      *                                                               *                       ERZ210
      *  Last Amend No. AR317401A          Date 16Jan13               *                       ERZ210
      *  Prev Amend No. ERZ210   *CREATE   Date 16Jan13               *                       ERZ210
      *                                                               *                       ERZ210
      *****************************************************************                       ERZ210
      *                                                               *                       ERZ210
      *  AR317401A - No '*' in the end of narrative for reversal      *                       ERZ210
      *              posting. Add checking of *IN19 after ERZ210      *                       ERZ210
      *              setup the fee narrative.                         *                       ERZ210
      *  ERZ210 - Lending Fees Posting Narratives                     *                       ERZ210
      *                                                               *                       ERZ210
      *****************************************************************                       ERZ210
      *                                                               *                       ERZ210
      * SUBROUTINE : FEENAR                                           *                       ERZ210
      *                                                               *                       ERZ210
      * This Subroutine sets up the Fee Posting Narrative using the   *                       ERZ210
      * Loan or Facility Narrative as appropriate                     *                       ERZ210
      *                                                               *                       ERZ210
      *****************************************************************                       ERZ210
     C           FEENAR    BEGSR                                                              ERZ210
      *                                                                                       ERZ210
     C           KLFEE     KLIST                                                              ERZ210
     C                     KFLD           K@FECD  2                                           ERZ210
      *                                                                                       ERZ210
      ** Obtain Fee Code Narrative                                                            ERZ210
      *                                                                                       ERZ210
     C                     MOVEL*BLANKS   O#PNAR                                              ERZ210
     C                     MOVELWKFECD    K@FECD                                              ERZ210
     C           KLFEE     CHAINSDFEEL1              89                                       ERZ210
      *                                                                                       ERZ210
     C           *IN89     IFEQ *OFF                                                          ERZ210
     C                     MOVELAUFENV    FEENR1                                              ERZ210
      *                                                                                       ERZ210
      ** Obtain Facility / Loan Narrative as appropriate                                      ERZ210
      *                                                                                       ERZ210
     C           WKFTYP    IFGE '000'                                                         ERZ210
     C           WKFTYP    ANDLE'999'                                                         ERZ210
     C                     MOVELCNUM      K@CNUM                                              ERZ210
     C                     MOVELWKFTYP    K@FACT                                              ERZ210
     C                     MOVELFACSB     K@FCNO                                              ERZ210
     C                     MOVEL'A'       K@RCDT                                              ERZ210
     C           KLFCLT    CHAINFCLTY                89                                       ERZ210
      *                                                                                       ERZ210
     C           *IN89     IFEQ *OFF                                                          ERZ210
     C           FCNAR3    IFNE *BLANKS                                                       ERZ210
     C                     MOVELFCNAR3    FEENR2                                              ERZ210
     C                     ELSE                                                               ERZ210
     C                     MOVELWKFTYP    FEEN21                                              ERZ210
     C                     MOVEL'/'       FEEN22                                              ERZ210
     C                     MOVELFACSB     FEEN23                                              ERZ210
     C                     MOVEL'/'       FEEN24                                              ERZ210
     C                     MOVELCNUM      FEEN25                                              ERZ210
     C                     ENDIF                                                              ERZ210
     C                     ENDIF                                                              ERZ210
      *                                                                                       ERZ210
     C                     ELSE                                                               ERZ210
     C                     MOVELLNRF      K@LNRF                                              ERZ210
     C                     MOVEL'B'       K@RCD2                                              ERZ210
     C           KLCLON    CHAINCLOAN                89                                       ERZ210
     C           *IN89     IFEQ *OFF                                                          ERZ210
     C                     MOVELCLNAR1    FEENR2                                              ERZ210
     C                     ENDIF                                                              ERZ210
     C                     ENDIF                                                              ERZ210
      *                                                                                       ERZ210
     C                     MOVELFEENRR    O#PNAR                                              ERZ210
      *                                                                                    AR317401A
      ** Ensure that for reversal posting (*IN19 = 0),                                     AR317401A
      ** place '*' in the end of the narrative.                                            AR317401A
      *                                                                                    AR317401A
     C           *IN19     IFEQ '0'                                                        AR317401A
     C                     MOVE '*'       O#PNAR                                           AR317401A
     C                     ENDIF                                                           AR317401A
     C                     ENDIF                                                              ERZ210
      *                                                                                       ERZ210
     C                     ENDSR                                                              ERZ210
      *                                                                                       ERZ210
      *****************************************************************                       ERZ210
      * End of /COPY LE0470CCP3                                       *                       ERZ210
      *****************************************************************                       ERZ210
