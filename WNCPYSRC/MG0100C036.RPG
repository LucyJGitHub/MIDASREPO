      *****************************************************************
      *  Start of /COPY MG0100C036                                    *
      *****************************************************************
      *                                                               *
      *  Last Amend No. EML0101 *Create     Date 21Nov19              *
      *  Prev Amend No. nnnnnn              Date ddmmmyy              *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  EML101 - Output account in field 50K on MT103 for FX/MM deal.*
      *           Upgrade of UMA007 to FB 2.1                         *
      *                                                               *
      *****************************************************************
     C           ACC50     BEGSR                                          EML101
      *                                                                   EML101
     C                     MOVELMGBRCA    @BRCA                           EML101
     C                     MOVEL'EUR'     @CCY                            EML101
     C                     MOVELMGPOCN    @CNUM                           EML101
     C                     Z-ADD0         @ACSQ                           EML101
     C                     MOVE *BLANK    ACC5   34                       EML101
     C                     MOVE *BLANK    ACC35  35                       EML101
      *                                                                   EML101
      *  Access account details                                           EML101
     C           MGACC     SETLLMGACCNL0                                  EML101
     C                     READ MGACCNL0                 90               EML101
      *                                                                   EML101
     C           *IN90     DOWNE'1'                                       EML101
     C           MABRCA    ANDEQMGBRCA                                    EML101
     C           MACNUM    ANDEQMGPOCN                                    EML101
     C           MACCY     ANDEQ'EUR'                                     EML101
      *                                                                   EML101
     C           MAIBAN    IFNE *BLANK                                    EML101
     C                     MOVE *BLANK    ACC5                            EML101
     C                     MOVELMAIBAN    ACC5                            EML101
     C                     SETON                     90                   EML101
     C                     ENDIF                                          EML101
      *                                                                   EML101
     C           *IN90     IFNE '1'                                       EML101
     C           MAACNO    ANDNE0                                         EML101
     C           ACC5      ANDEQ*BLANK                                    EML101
     C                     MOVE *BLANK    ACC5                            EML101
     C                     MOVELMAACNO    ACC5                            EML101
     C                     ENDIF                                          EML101
      *                                                                   EML101
     C  N90                READ MGACCNL0                 90               EML101
      *                                                                   EML101
     C                     ENDDO                                          EML101
      *                                                                   EML101
     C                     MOVE *BLANK    ACC35  35                       EML101
     C           ACC5      IFNE *BLANK                                    EML101
     C                     MOVE *BLANK    ACC35  35                       EML101
     C                     MOVEL'/'       ACC35                           EML101
     C                     MOVE ACC5      ACC35                           EML101
     C                     ELSE                                           EML101
     C                     MOVEL'/'       ACC7    7                       EML101
     C                     MOVE MGPOCN    ACC7                            EML101
     C                     MOVELACC7      ACC35                           EML101
     C                     ENDIF                                          EML101
      *                                                                   EML101
     C                     EXSR FSWAD2                                    EML101
      *                                                                   EML101
     C                     ENDSR                                          EML101
      ********************************************************************EML101
      *                                                                   EML101
     C           FSWAD2    BEGSR                                          EML101
      *                                                                   EML101
     C           ACC35     IFNE *BLANK                                    EML101
     C                     MOVELACC35     MFLD                            EML101
     C                     ENDIF                                          EML101
      *                                                                   EML101
      **  Address 1                                                       EML101
     C           ZSADD1    IFNE *BLANKS                                   EML101
     C                     ADD  1         X                               EML101
     C           X         OCUR MULT                                      EML101
     C                     MOVELZSADD1    MFLD                            EML101
     C                     END                             Address 1      EML101
      *                                                                   EML101
      **  Address 2                                                       EML101
     C           ZSADD2    IFNE *BLANKS                                   EML101
     C                     ADD  1         X                               EML101
     C           X         OCUR MULT                                      EML101
     C                     MOVELZSADD2    MFLD                            EML101
     C                     END                             Address 2      EML101
      **  Address 3                                                       EML101
     C           ZSADD3    IFNE *BLANKS                                   EML101
     C                     ADD  1         X                               EML101
     C           X         OCUR MULT                                      EML101
     C                     MOVELZSADD3    MFLD                            EML101
     C                     END                             Address 3      EML101
      **  Address 4                                                       EML101
     C           ZSADD4    IFNE *BLANKS                                   EML101
     C                     ADD  1         X                               EML101
     C           X         OCUR MULT                                      EML101
     C                     MOVELZSADD4    MFLD                            EML101
     C                     END                             Address 4      EML101
     C                     ENDSR                                          EML101
      *****************************************************************
      *  End of /COPY MG0100C036                                      *
      *****************************************************************
