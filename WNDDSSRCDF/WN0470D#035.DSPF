      *****************************************************************
/*STD *  DSPFDFRWN                                                    *
/*EXI *  TEXT('Midas WN Display window layout dat screen')
     A*****************************************************************
     A*                                                               *
     A*  Midas - STANDARD WINDOW FACILITY
     A*                                                               *
     A*   DSPF/WN0470D#  -   WINDOW DISPLAY                           *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. E39162             Date 18Jun92               *
      *                                    Date                       *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      INDARA
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT(QSYSPRT)
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Condition SFLNXTCHG.')
     A                                      INDTXT(35 'Invalid #1SEL     ')
     A  84                                  SFLNXTCHG
     A            #1FDAT         3   H      TEXT('Field Attribute')
     A            #1TXAT         1   H      TEXT('Text Attribute')
     A            #1DSFL       100   H      TEXT('Data Stream for Line')
     A            #1SEL          1   B  9 10
     A                                      VALUES('E' +
     A                                             'A' +
     A                                             'D')
     A                                      CHECK(AB)
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A            #1WDCN        10      9 16TEXT('Window Controller Name(K)')
     A                                      DSPATR(HI)
     A            #1LNNO         3      9 29TEXT('Line No.')
     A                                      DSPATR(HI)
     A            #1NOCF        10      9 35TEXT('Name of Character Field')
     A                                      DSPATR(HI)
     A            #1NFTC        10      9 48TEXT('Name of File to Change')
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(13) SFLSIZ(14)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Condition SFLCLR.')
     A                                      INDTXT(81 'Condition SFLDSP.')
     A                                      INDTXT(82 'Condition SFLEND.')
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A*.COMMAND.KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF09(09 'Go to ''Add'' mode.')
     A*.SETOFFS.................................................................
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #2EGCD')
     A                                      SETOF(32 'Invalid: #2EFCD')
     A                                      SETOF(33 'Invalid: #2ANNA')
     A                                      SETOF(34 'Invalid: #2AONA')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1  2'WN0470D1'
     A            ##JOB         10      1 49TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##MRDT         7      1 62TEXT('midas rundate')
     A N25                                  OVRDTA
     A            ##TME          6  0   1 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  2 28'Window Layout Maintenance'
     A                                      DSPATR(HI)
     A            #2FDAT         3   H      TEXT('Field Attribute')
     A            #2TXAT         1   H      TEXT('Text Attribute')
     A            #2DSFL       100   H      TEXT('Data Stream for Line')
     A                                  4 16'Window'
     A                                  4 29'Line'
     A                                  4 35'Field Name'
     A                                  4 48'Name of File'
     A                                  5 16'Controller'
     A                                  5 29'No.'
     A                                  5 35'Input from'
     A                                  5 48'to Change'
     A                                  6 16'Name'
     A                                  6 35'Screen'
     A            #2WDCN        10   B  7 16TEXT('Window Controller Name(K)')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2LNNO         3   B  7 29TEXT('Line No.')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2NOCF        10   B  7 35TEXT('Name of Character Field')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2NFTC        10   B  7 48TEXT('Name of File to Change')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
