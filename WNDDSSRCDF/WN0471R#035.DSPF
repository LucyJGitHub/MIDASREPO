      *****************************************************************
/*STD *  DSPFDFRWN                                                    *
/*EXI *  TEXT('Midas WN Edit Window Layout screen')
     A*****************************************************************
     A*                                                               *
     A*  Midas - STANDARD WINDOW FACILITY
     A*                                                               *
     A*   DSPF/WN0471R# - EDIT WINDOW LAYOUT CREATION                 *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. E39162             DATE 18JUN92               *
     A*  PREV AMEND NO. E00000                  DDMMMYY               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      INDARA
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT(QSYSPRT)
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDKEY
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  86                                  PUTOVR
     A                                      INDTXT(89 'ADD mode.')
     A*.COMMAND.KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A                                      CF09(09 'Change mode.')
     A                                      CF05(05 'Reset.')
     A*.SETOFFS.................................................................
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #1EGCD')
     A                                      SETOF(32 'Invalid: #1EFCD')
     A*.........................................................................
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1  2'WN0471R1'
     A            ##JOB         10      1 49TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##MRDT         7      1 62TEXT('midas rundate')
     A N25                                  OVRDTA
     A            ##TME          6  0   1 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  2 32'New Window Layout'
     A                                      DSPATR(HI)
     A                                  6  2'Window Controller Name...:'
     A            #1WDCN        10   B  6 29TEXT('Window Controller Name(K)')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  7  2'Line No..................:'
     A            #1LNNO         3   B  7 29TEXT('Line No.')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('Details.')
     A                                      RTNDTA
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  86                                  PUTOVR
     A                                      INDTXT(89 'ADD mode.')
     A                                      INDTXT(88 'Protect key fields.')
     A                                      INDTXT(87 'Enable CMD2.')
     A*.COMMAND.KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A                                      CF05(05 'Reset.')
     A                                      CF10(10 'Delete.')
     A*.SETOFFS.................................................................
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #DM8ST')
     A                                      SETOF(32 'Invalid: #DM3ST')
     A                                      SETOF(33 'Invalid: #DM6ST')
     A                                      SETOF(34 'Invalid: #DM4ST')
     A                                      SETOF(35 'Invalid: #DM7ST')
     A                                      SETOF(36 'Invalid: #DM5ST')
     A                                      SETOF(37 'Invalid: #DARNA')
     A                                      SETOF(38 'Invalid: #DASNA')
     A                                      SETOF(39 'Invalid: #1ANNA')
     A                                      SETOF(40 'Invalid: #1AONA')
     A*.........................................................................
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1  2'WN0471R2'
     A            ##JOB         10      1 49TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##MRDT         7      1 62TEXT('midas rundate')
     A N25                                  OVRDTA
     A            ##TME          6  0   1 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  2 34'Window Layout'
     A                                      DSPATR(HI)
     A            #1FDAT         3   H      TEXT('Field Attribute')
     A            #1TXAT         1   H      TEXT('Text Attribute')
     A            #1DSFL       100   H      TEXT('Data Stream for Line')
     A                                  4  2'Window Controller Name(K):'
     A            #1WDCN        10      4 29TEXT('Window Controller Name(K)')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  5  2'Line No..................:'
     A            #1LNNO         3      5 29TEXT('Line No.')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'Field Attributes'
     A                                      DSPATR(UL)
     A                                  9  2'I/O Field Type...........:'
     A            #DIOFT         2   B  9 29TEXT('I/O Field Type')
     A* Condition: Valid Codes
     A                                      VALUES('AL' +
     A                                             'AU' +
     A                                             ' ' +
     A                                             'NU' '?')
     A                                      CHECK(AB)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 11  2'Normal Display...........:'
     A            #DATND         1   B 11 29TEXT('Attributes Normal Display')
     A* Condition: Valid Codes
     A                                      VALUES(' ' +
     A                                             'X' '?')
     A                                      CHECK(AB)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 11 38'Reverse Image...........:'
     A            #DATRI         1   B 11 64TEXT('Attributes Reverse Image')
     A* Condition: Valid Codes
     A                                      VALUES(' ' +
     A                                             'X' '?')
     A                                      CHECK(AB)
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 12  2'High Intensity...........:'
     A            #DATHI         1   B 12 29TEXT('Attributes High Intensity')
     A* Condition: Valid Codes
     A                                      VALUES(' ' +
     A                                             'X' '?')
     A                                      CHECK(AB)
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 12 38'Underscore..............:'
     A            #DATUN         1   B 12 64TEXT('Attributes Underscore')
     A* Condition: Valid Codes
     A                                      VALUES(' ' +
     A                                             'X' '?')
     A                                      CHECK(AB)
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 13  2'Blink....................:'
     A            #DATBL         1   B 13 29TEXT('Attributes Blink')
     A* Condition: Valid Codes
     A                                      VALUES(' ' +
     A                                             'X' '?')
     A                                      CHECK(AB)
     A N25                                  OVRDTA
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 15  2'Data Stream Number 1.....:'
     A            #DDSN1        50   B 15 29TEXT('Data Stream Number 1')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 16  2'Data Stream Number 2.....:'
     A            #DDSN2        50   B 16 29TEXT('Data Stream Number 2')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 18  2'Name of Character Field..:'
     A            #1NOCF        10   B 18 29TEXT('Name of Character Field')
     A N25                                  OVRDTA
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 19  2'Name of File to Change...:'
     A            #1NFTC        10   B 19 29TEXT('Name of File to Change')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys.')
     A                                      RTNDTA
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
