     H
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas WN Window displayer')
     F********************************************************************
     F*                                                                  *
     F*  Midas - Windows
     F*                                                                  *
     F*   WN0020 - Window Displayer                                      *
     F*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     F*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01304             Date 14Feb91               *
      *                 S01251             Date 03Feb90               *
     F*                                                                  *
     F********************************************************************
     F*                                                                  *
      *  MD046248 - Finastra Rebranding                               *
     F*      S01304 - CHANGES MADE DURING PTF INCORPORATION TO R10       *
     F*               (*ENTRY PARAMETER SPECIFIED AS INCORRECT LENGTH    *
     F*                PASSED FROM RPG/WN0010 AS 256 LONG)               *
     F*      S01251 - SYNON NAME CHANGES                                 *S01251
     F*                                                                  *
     F********************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F********************************************************************
     FWN0020DFCF  E                    WORKSTN                        UC
     E*
     E                    DUMMYE      1  1
     E*
     E*           DATA STREAM BY 1 CHARACTER
     E*
     E                    S        1900  1
     E*
     E*           TEXT STRING COMING BY PARAMETER
     E*
     E                    C         100  1
     E*
     E*           OVERRIDE FOR DSPF
     E*
     E                    OVRDSP  1   1 60
     I            DS
     I                                        1   1 DUMMYD
     I*****************************************************************
     I*
     I*           STREAM DATA STRUCTURE
     I*
     I*****************************************************************
     ISTREAM      DS                           1900
     I                                        11900 S
     I*
     I* LENGTH OF DATA STREAM TO SEND TO DSP - BIN
     I*
     I                                    B   1   20LSND
     I*
     I* LENGTH OF DATA STREAM TO RECEIVE
     I*
     I                                    B   3   40LREC
     I*
     I* REQUESTED FUNCTION   X'71'  SEND
     I*                      X'73'  SEND/RECEIVE
     I*
     I                                        5   5 OP
     I*****************************************************************
     ICFTCTL      DS                              2
     I                                        1   1 ESC1
     I                                        2   2 CFT
     IRUCTL       DS
     I                                        1   1 ESC
     I                                        2   2 WTD
     I                                        3   3 CC1
     I                                        4   4 CC2
     ISBA         DS                              3
     I                                        1   1 SB
     I                                        2   2 ROW
     I                                        3   3 COL
      /EJECT
      ***************************************************************
      *           FIELD DEFINITION                                  *
      *                                                             *
      *           SF          START OF FIELD                        *
      *                                                             *
      *           FFW1        FIELD FORMAT 1                        *
      *                       - BIT 0      = '0'                    *
      *                       - BIT 1      = '1'   DEFAULT          *
      *                       - BIT 2      = '1'   PROTECT          *
      *                       - BIT 2      = '0'   NOT PROTECT      *
      *                       - BIT 3      = '0'                    *
      *                       - BIT 4      = '1'   SET MDT ON       *
      *                       - BIT 5-6-7  = '000' ALPHA FIELD      *
      *                       - BIT 5-6-7  = '111' NUM.  FIELD      *
      *                       - BIT 5-6-7  = '110' LIGHT PEN        *
      *                                                             *
      *           FFW2        FIELD FORMAT 2                        *
      *                       - ALL BITS   = '0'   NUMERIC FIELD    *
      *                       - BIT 0      = '1'   ENTER WHEN FIELD *
      *                                              EXIT PRESSES   *
      *                       - BIT 2      = '1'   ALPHA FIELD      *
      *                                            UPPER CASE FIELD *
      *                       - BIT 56     = '1'   FILL WHITH BLANK *
      *                                            WHEN FIELD EXIT  *
      *                       - BIT 57     = '1'   FILL WHITH ZERO  *
      *                                            WHEN FIELD EXIT  *
      *                                                             *
      *           ATR         ATTRIBUTE                             *
      *                                                             *
      *                       ALL BITS OF EXCEPT                    *
      *                                                             *
      *                       - BIT 2      = X'20' NORMAL DISPLAY   *
      *                       - BIT 27     = X'21' REVERSE IMAGE    *
      *                       - BIT 26     = X'22' HIGH INTENSITY   *
      *                       - BIT 267    = X'23' HIGH IN + RIV.IM.*
      *                       - BIT 25     = X'24' UNDERSCORE       *
      *                       - BIT 257    = X'25' UNDERSC + REV.IM.*
      *                       - BIT 256    = X'26' HIGH IN + UNDERS.*
      *                       - BIT 2567   = X'27' NON DISPLAY      *
      *                       - BIT 24     = X'28' BLINK            *
      *                       - BIT 247    = X'29' BLINK + REV IMAG *
      *                       - BIT 23     = X'30' COLUMN SEPARATOR *
      *                                                             *
      *                                                             *
      *           FL          FILED LENGTH - BINARY                 *
      *                                                             *
      ***************************************************************
     IRUDTA       DS
     I                                        1   1 SF
     I                                        2   2 FFW1
     I                                        3   3 FFW2
     I                                        4   4 ATR
     I                                        2   4 WATR
     I                                    B   5   60FL
     I*
     I* (WATR)    ATTRIBUTES OF FIELDS ARE COMING WITH PARAMETER
     I*
      /EJECT
     I            DS
     I                                    B   1   20BIN2
     I                                        2   2 BINLWB
     C*
     C           *ENTRY    PLIST
     C                     PARM           FUNC    1
     C*********************PARM           CHAR  100                       S01304
     C                     PARM           CHAR  256                       S01304
     C                     PARM           LEN     30
     C                     PARM           ROWI    30
     C                     PARM           COLI    30
     C                     PARM           KEY     1
     C                     PARM           ERR     1
     C                     PARM           FATR    3
     C*
     C                     MOVELFATR      WATR
     C*
     C*          ONE TIME SUBROUTINE, INIT CONSTANT DATA
     C*
     C           *IN01     CASEQ'0'       $FIRST
     C                     END
     C*
     C*
     C*          ALLOW AT LEAST 198 BYTES FOR THIS STRING
     C*          UP TO 18 POSSIBLE 1 BYTE INPUT FIELDS
     C*          6 BYTES REQUIRED FOR THE DEFINITION = 108
     C*          PLUS 18 BYTES OF DEFAULT INPUT DATA = 126
     C*          PLUS 18 SPACES BETWEEN FIELS AND THE DEFINITION
     C*
     C*          18 * 4 = 72  ---> 126 + 72 = 198 POSSIBLE BYTES ...
     C*
     C*
     C           #IX       CASGT1702      $WRITE
     C                     END
     C*
     C*          CHECK ROW AND COLUMN
     C*
     C                     MOVE 'N'       ERR
     C* ROW
     C           FUNC      IFEQ 'P'
     C           ROWI      IFLT 1
     C           ROWI      ORGT 24
     C                     MOVE 'Y'       ERR
     C                     END
     C* COLUMN
     C           COLI      IFLT 1
     C           COLI      ORGT 80
     C                     MOVE 'Y'       ERR
     C                     END
     C* STRING LENGTH
     C           LEN       IFLT 1
     C           LEN       ORGT 100
     C                     MOVE 'Y'       ERR
     C                     END
     C                     END
     C*
     C*          INSURE VALID FUNCTION IS REQUESTED
     C*
     C           FUNC      IFNE 'P'
     C           FUNC      ANDNE'R'
     C           FUNC      ANDNE'W'
     C           FUNC      ANDNE'Q'
     C                     MOVE 'Y'       ERR
     C                     END
     C*
     C*          PROCESS INPUT FUNCTION
     C*
     C           ERR       CABEQ'Y'       @EXIT
     C           FUNC      CASEQ'P'       $PUT
     C           FUNC      CASEQ'R'       $READ
     C           FUNC      CASEQ'W'       $WRITE
     C                     END
     C*
     C           FUNC      IFEQ 'Q'
     C                     SETON                     LR
     C                     END
     C*
     C           @EXIT     TAG
     C                     RETRN
     C/EJECT
     C*****************************************************************
     C           $FIRST    BEGSR
     C*
     C                     MOVE '1'       *IN01
     C                     MOVEAOVRDSP    CMDTXT 60
     C                     Z-ADD60        CMDLEN 155
     C*
     C                     CALL 'QCAEXEC'              LR
     C                     PARM           CMDTXT
     C                     PARM           CMDLEN
     C   LR                MOVE 'Y'       ERR
     C   LR                RETRN
     C*
     C                     OPEN WN0020DF
     C                     EXSR $INZ
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C           $INZ      BEGSR
     C                     Z-ADD0         RCOL    30
     C*
     C*          INIT TO ALL ZEROS
     C*
     C                     BITOF'01234567'ESC1
     C                     BITOF'01234567'CFT
     C                     BITOF'01234567'ESC
     C                     BITOF'01234567'WTD
     C                     BITOF'01234567'CC1
     C                     BITOF'01234567'CC2
     C                     BITOF'01234567'SB
     C                     BITOF'01234567'SF
     C                     BITOF'01234567'NM      1
     C*
     C*          INIT CONTROL CARACTERS
     C*
     C                     BITON'5'       ESC1
     C                     BITON'13'      CFT
     C                     BITON'5'       ESC
     C                     BITON'37'      SB
     C                     BITON'3457'    SF
     C                     BITON'2'       NM
     C                     BITON'37'      WTD
     C                     BITON'234'     CC2
     C*
     C                     MOVEACFTCTL    S,6
     C                     MOVEARUCTL     S,8
     C                     Z-ADD12        #IX     50
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C           $PUT      BEGSR
     C*
     C                     MOVEACHAR      C,1
     C*
     C*          INIT C ARRAY, CONVERT ROW, COLUMN, LEN ...
     C*
     C                     EXSR $CVTRC
     C                     Z-ADDLEN       WLEN    30
     C*
     C*          ADJUST TEXT STRING IF WON'T FIT ON WINDOW
     C*
     C                     EXSR $ADJST
     C*
     C*          GET STARTING & ENDING COLUMN
     C*
     C                     Z-ADD1         #B
     C*
     C           *IN51     DOUEQ'0'
     C           '~'       LOKUPC,#B                     51
     C*
     C           *IN51     IFEQ '1'
     C                     Z-ADD#B        SCOL    30
     C                     ADD  1         #B
     C           '~'       LOKUPC,#B                     51
     C*
     C           *IN51     IFEQ '1'
     C                     Z-ADD#B        ECOL    30
     C           ECOL      SUB  SCOL      FL
     C                     SUB  1         FL
     C*
     C*          CALCULATE FIELD LENGTH
     C*
     C           COLI      ADD  SCOL      BIN2
     C                     SUB  1         BIN2
     C                     MOVE BINLWB    COL
     C*
     C*          INCLUDE FIELD POSITION ON WINDOW
     C*
     C                     MOVEASBA       S,#IX
     C                     ADD  3         #IX
     C*
     C*          INCLUDE FIELD ATTRIBUTES
     C*
     C                     MOVEARUDTA     S,#IX
     C                     ADD  6         #IX
     C*
     C*          INCLUDE DEFAULT DATA
     C**
     C           SCOL      DO   ECOL      #B1     50
     C*
     C           C,#B1     IFNE '~'
     C           C,#B1     ANDNE'.'
     C           C,#B1     IFNE '-'
     C                     MOVE C,#B1     S,#IX
     C                     ADD  1         #IX
     C                     ELSE
     C                     MOVEL*BLANKS   S,#IX
     C                     ADD  1         #IX
     C                     END
     C                     END
     C*
     C                     END
     C**
     C                     ADD  1         #B
     C                     END
     C                     END
     C                     END
     C*
     C*          CONVERT LOCATION TO BINARY
     C*
     C                     EXSR $CVTRC
     C*
     C                     MOVEASBA       S,#IX
     C                     ADD  3         #IX
     C*
     C*          FIND TEXT ATTRIBUTES
     C*
     C                     EXSR $ATR
     C*
     C*          WRITE TEXT TO SCREEN INGNORING INPUT DEFINITIONS
     C*
     C                     DO   WLEN      #B      50
     C           C,#B      IFEQ '~'
     C                     ADD  1         #B
     C           C,#B      DOUEQ'~'
     C                     ADD  1         #B
     C                     END
     C*
     C*          SEND NEW BUFFER ADDRESS FOR REST OF TEXT
     C*
     C           #B        ADD  COLI      BIN2
     C                     SUB  1         BIN2
     C                     MOVE BINLWB    COL
     C                     MOVEASBA       S,#IX
     C                     ADD  3         #IX
     C                     MOVE HLDATR    S,#IX
     C                     ADD  1         #IX
     C                     ELSE
     C*
     C*          BUILD TEXT STRING
     C*
     C                     MOVELC,#B      S,#IX
     C                     ADD  1         #IX
     C                     END
     C                     END
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C           $READ     BEGSR
     C*
     C*          SET CONTROL CARACTERS FOR SEND/RECEIVE
     C*
     C                     BITOF'01234567'OP
     C                     BITON'12367'   OP               HEX 73
     C*
     C*          SET REQUEST UNIT CONTROL TO READ SCREEN
     C*
     C                     BITOF'01234567'WTD
     C                     BITON'16'      WTD              HEX 42
     C*
     C*          ADD READ REQUEST TO DATA STREAM
     C*
     C                     MOVEARUCTL     S,#IX
     C                     ADD  4         #IX
     C*
     C           #IX       SUB  6         #IX
     C                     Z-ADD#IX       LSND
     C                     Z-ADD1900      LREC
     C*
     C*          REQUEST I/O TO DISPLAY DEVICE
     C*
     C*                    EXFMTUSERFMT                                   S01251
     C                     EXFMTUSRFMT                                    S01251
     C*
     C                     MOVEL*BLANKS   CHAR
     C*
     C                     Z-ADD0         BIN2
     C                     MOVE S,1       BINLWB
     C                     Z-ADDBIN2      ROWI
     C                     Z-ADD0         BIN2
     C                     MOVE S,2       BINLWB
     C                     Z-ADDBIN2      COLI
     C                     MOVELS,3       KEY
     C                     MOVEAS,4       CHAR
     C                     MOVEA*BLANKS   S,1
     C*
     C                     EXSR $INZ
     C                     MOVELCHAR      OCHAR 256
     C                     EXSR $WRITE
     C                     MOVELOCHAR     CHAR
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C           $WRITE    BEGSR
     C*
     C*          SET CONTROL CARACTERS TO SEND ONLY
     C*
     C                     BITOF'01234567'OP
     C                     BITON'1237'    OP
     C*
     C           #IX       SUB  6         #IX
     C                     Z-ADD#IX       LSND
     C                     Z-ADD0         LREC
     C*
     C******************** WRITEUSERFMT                                   S01251
     C                     WRITEUSRFMT                                    S01251
     C*
     C                     MOVEA*BLANKS   S,1
     C                     EXSR $INZ
     C*
     C                     ENDSR
     C*****************************************************************
     C           $ATR      BEGSR
     C*
     C                     MOVELNM        HLDATR  1
     C           WLEN      DIV  2         HALF    30
     C           HALF      IFGT 1
     C                     DO   HALF      #A      50
     C                     TESTB'01'      C,#A       51
     C   51                TESTB'2'       C,#A           51
     C   51                MOVE C,#A      HLDATR
     C                     END
     C                     END
     C*
     C                     ENDSR
     C*****************************************************************
     C           $ADJST    BEGSR
     C           COLI      ADD  WLEN      RCOL    30
     C                     SUB  1         RCOL
     C           RCOL      IFGT 80
     C           RCOL      SUB  80        #OVER   50
     C                     SUB  #OVER     WLEN
     C                     END
     C           WLEN      ADD  1         #NL     50
     C                     MOVEA*BLANKS   C,#NL
     C                     Z-ADD0         #CNT
     C                     DO   WLEN      #AD     30
     C           C,#AD     IFEQ '~'
     C                     ADD  1         #CNT    50
     C                     END
     C                     END
     C           #CNT      IFNE 0
     C           #CNT      DIV  2         #WKX    30
     C                     MVR            #WKR    30
     C           #WKR      IFNE 0
     C                     Z-ADDWLEN      #AD
     C                     DO   WLEN      #ADCTL  30
     C           C,#AD     IFEQ '~'
     C                     MOVEL*BLANK    C,#AD
     C                     Z-ADDWLEN      #ADCTL
     C                     ELSE
     C                     MOVEL*BLANK    C,#AD
     C                     END
     C                     SUB  1         #AD
     C                     END
     C                     END
     C                     END
     C                     ENDSR
     C*****************************************************************
     C           $CVTRC    BEGSR
     C                     Z-ADDROWI      BIN2
     C                     MOVE BINLWB    ROW
     C                     Z-ADDCOLI      BIN2
     C                     MOVE BINLWB    COL
     C                     ENDSR
**
OVRDSPF WN0020DF WN0020DX LVLCHK(*NO)
