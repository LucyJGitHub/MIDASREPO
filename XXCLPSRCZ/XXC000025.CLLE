/*********************************************************************/
/*S*D****CLPBASE******************************************************/             /*LUC109*/
/*STD    CLPBASEBND                                                  */             /*LUC109*/
/*********************************************************************/
/*                                                                   */
/*       Midas - Interface to Head Office Reporting                  */
/*                                                                   */
/********GLCM025*-*FECCD*Extract**************************************/             /*LUC109*/
/*       XXC000025 - Midas XX FECCD Extract                          */             /*LUC109*/
/*                                                                   */
/********(C) COPYRIGHT MKI LIMITED 1998                              */             /*LUC109*/
/*       (C) Copyright Finastra International Limited 2016           */             /*LUC109*/
/*                                                                   */
/*       THIS SRC MUST BE COMPILED WITH xxINTFLIB at BOTTOM of LIBL  */             /*LUC109*/
/*                                                                   */
/*       Last Amend No. LUC109           Date 13Sep16                */
/*       Prev Amend No. 197893           Date 30Oct01                */
/*                      166140           Date 05JUL99                */
/*                      MMI043           Date 21Apr99                */
/*                      MMI043 *AMEND*   Date 01Apr99                */
/*                             *CREATE   Date 15Dec98                */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       LUC109 - HO Reporting (Upgrade to FBM 2.1)                  */
/*       197893 - Do not share access path of PSOLD.                 */
/*       166140 - Specify *MAP *DROP on the CPYF.                    */
/*                                                                   */
/*********************************************************************/
             PGM
/**/
/*   Declare variables                                            */
/**/
             DCL        VAR(&MIDDAYC) TYPE(*CHAR) LEN(5)
/************DCL VAR(&CPY) TYPE(*CHAR) LEN(64) VALUE('(C) +
                 COPYRIGHT MKI LIMITED 1998')                     */                /*LUC109*/
             DCL VAR(&CPY) TYPE(*CHAR) LEN(64) VALUE('(C) +
                 Copyright Finastra International Limited 2016')                    /*LUC109*/
             DCL VAR(&LDA) TYPE(*CHAR) LEN(256)
             DCL        VAR(&DATJOB) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MMDD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&LIBCCD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FILENAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&LIBHIS) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SLCT) TYPE(*CHAR) LEN(100)
             DCL        VAR(&MPEMA) TYPE(*CHAR) LEN(5)
/**/
             DCLF       FILE(SDGELRPP)


/**/
/*   Create Local Data Area                                       */
/**/
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256) +
                          TEXT('Midas SD Local Data Area')
             MONMSG     MSGID(CPF0000)
/*                                                                   */
/* Retrieve 'Last Acct date' (month and day) from SDGELRPP           */
/*                                                                   */
/************CALL*******GLCM049*PARM(&MMDD)***************************/             /*LUC109*/
             CALL       XXC000049 PARM(&MMDD)                                       /*LUC109*/

/**/
/*  Get Midas Run Day Number from SDBANKPD                           */
/**/
/************CALL*******GLCM052*PARM(&MIDDAYC)************************/             /*LUC109*/
             CALL       XXC000052 PARM(&MIDDAYC)                                    /*LUC109*/

/*                                                                   */
/* Clear working file FECCDDPP for new extraction                    */
/*                                                                   */
             CLRPFM     FILE(FECCDDPP)
/*                                                                   */
             RCVF

             CHGVAR     VAR(&MPEMA) VALUE(&MPEMB)
             CHGVAR     VAR(&SLCT) VALUE('((MDAT *LE' *BCAT +
                          &MIDDAYC *CAT ') *AND (MDAT *GT' *BCAT +
                          &MPEMA *CAT '))')
/**/
/**/
/*   Copy DLCHISPD todays records only to work file.             */
/**/
             RTVOBJD    OBJ(WKDLCHIS) OBJTYPE(*FILE) RTNLIB(&LIBHIS)
             CLRPFM     FILE(WKDLCHIS)
             OPNQRYF    FILE((*LIBL/DLCHISPD)) OPTION(*INP) +
                          FORMAT(*FILE) QRYSLT(&SLCT)

             CPYFRMQRYF FROMOPNID(DLCHISPD) TOFILE(&LIBHIS/WKDLCHIS) +
                          MBROPT(*REPLACE) CRTFILE(*NO)
             CLOF       OPNID(DLCHISPD)
/**/
/*   Copy DLDHISPD todays records only to work file.             */
/**/
             RTVOBJD    OBJ(WKDLDHIS) OBJTYPE(*FILE) RTNLIB(&LIBHIS)
             CLRPFM     FILE(WKDLDHIS)
             OPNQRYF    FILE((*LIBL/DLDHISPD)) OPTION(*INP) +
                          FORMAT(*FILE) QRYSLT(&SLCT)

             CPYFRMQRYF FROMOPNID(DLDHISPD) TOFILE(&LIBHIS/WKDLDHIS) +
                          MBROPT(*REPLACE) CRTFILE(*NO)
             CLOF       OPNID(DLDHISPD)
/*                                                                   */
/* Loans/Deposits Dealing Extraction                                 */
/*                                                                   */
/************CALL*******PGM(GLM025)***********************************/             /*LUC109*/
             CALL       PGM(XX000025)                                               /*LUC109*/

/*                                                                   */
/* Negotiable Assets Purchased Dealing Extraction                    */
/*                                                                   */
/************CALL*******PGM(GLM026)***********************************/             /*LUC109*/
             CALL       PGM(XX000026)                                               /*LUC109*/

/*                                                                   */
/* Customer Lending Extraction                                       */
/*                                                                   */
             RTVOBJD    OBJ(WKMCLONL) OBJTYPE(*FILE) RTNLIB(&LIBHIS)
             CLRPFM     FILE(WKMCLONL)
             OPNQRYF    FILE((*LIBL/MCLONCL)) OPTION(*INP) +
                          FORMAT(*FILE) QRYSLT(&SLCT)

             CPYFRMQRYF FROMOPNID(MCLONCL) TOFILE(&LIBHIS/WKMCLONL) +
                          MBROPT(*REPLACE) CRTFILE(*NO) FMTOPT(*NOCHK)
             CLOF       OPNID(MCLONCL)
/*                                                                   */
             RTVOBJD    OBJ(WKMCLONK) OBJTYPE(*FILE) RTNLIB(&LIBHIS)
             CLRPFM     FILE(WKMCLONK)
             CPYF       FROMFILE(MCLONCK) TOFILE(&LIBHIS/WKMCLONK) +
                          MBROPT(*REPLACE) CRTFILE(*NO) FMTOPT(*MAP +
                          *DROP)                                      /*166140*/
             MONMSG     MSGID(CPF2869 CPF2817)
/************CPYF       FROMFILE(MCLONCK) TOFILE(&LIBHIS/WKMCLONK) +
/************             MBROPT(*REPLACE) CRTFILE(*NO)                *166140*/
/*                                                                   */
             OVRDBF     FILE(PSOLD) SHARE(*NO)                        /*197893*/
/************CALL*******PGM(GLM027)***********************************/             /*LUC109*/
             CALL       PGM(XX000027)                                               /*LUC109*/

/*                                                                   */
/* Retail/Nostro Account Extraction                                  */
/*                                                                   */
/************CALL*******PGM(GLM028)***********************************/             /*LUC109*/
             CALL       PGM(XX000028)                                               /*LUC109*/

/*                                                                   */
/* Creating file name CCDxxyy (where xx=month, yy=day)               */
/* CCDxxyy - File Formatting from FECCDDPP                           */
/*                                                                   */
             CHGVAR     VAR(&FILENAME) VALUE('CCD' *TCAT &MMDD)
             RTVOBJD    OBJ(CCD) OBJTYPE(*FILE) RTNLIB(&LIBCCD)
             DLTF       FILE(&LIBCCD/&FILENAME)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(CCD) FROMLIB(&LIBCCD) OBJTYPE(*FILE) +
                          TOLIB(*FROMLIB) NEWOBJ(&FILENAME) DATA(*NO)

             OVRDBF     FILE(SDGELRPP) SHARE(*NO)
             OVRDBF     FILE(CCD) TOFILE(&LIBCCD/&FILENAME)
/************CALL*******PGM(GLM029)***********************************/             /*LUC109*/
             CALL       PGM(XX000029)                                               /*LUC109*/
             DLTOVR     FILE(CCD)
             DLTOVR     FILE(SDGELRPP)

             ENDPGM

/************CHGVAR*&CPY*'(C)*COPYRIGHT*MKI*LIMITED*1998'*************/             /*LUC109*/
             CHGVAR &CPY '(C) Copyright Finastra International Limited 2016')       /*LUC109*/
