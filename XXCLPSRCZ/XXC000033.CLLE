/*********************************************************************/
/*S*D****CLPBASE******************************************************/             /*LUC109*/
/*STD    CLPBASEBND                                                  */             /*LUC109*/
/*********************************************************************/
/*                                                                   */
/*       Midas - Interface to Head Office Reporting                  */
/*                                                                   */
/********GLCM033*-*GL*Bills*Extract***********************************/             /*LUC109*/
/*       XXC000033 - Midas HOR GL Bills Extract                      */             /*LUC109*/
/*                                                                   */
/********(C)*COPYRIGHT*MKI*LIMITED*1998*******************************/             /*LUC109*/
/*       (C) Copyright Finastra International Limited 2016           */             /*LUC109*/
/*                                                                   */
/*       THIS SRC MUST BE COMPILED WITH xxINTFLIB at BOTTOM of LIBL  */             /*LUC109*/
/*                                                                   */
/*       Last Amend No. LUC109             Date 14Sep16              */
/*       Prev Amend No. 159964             Date 22Aug99              */
/*                      MMI043             Date 21Apr99              */
/*                      MMI043  *CREATE    Date 09Dec98              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       LUC109 - HO Reporting (Upgrade to FBM 2.1)                  */
/*       159964 - PGM/GLM032A cannot allocate LF/SDCUSTL0 due to     */
/*                SCC0406.                                           */
/*              - Seton job switches U7/U8 for error processing.     */
/*       MMI043 - Interface to Head Office Reporting                 */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       C R E A T I O N     P A R A M E T E R S                     */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&RERUN)
/**/
/*   Declare variables                                            */
/**/
/************DCL VAR(&CPY) TYPE(*CHAR) LEN(64) VALUE('(C) +
                 COPYRIGHT MKI LIMITED 1998')                     */                /*LUC109*/
             DCL VAR(&CPY) TYPE(*CHAR) LEN(64) VALUE('(C) +
                 Copyright Finastra International Limited 2016')                    /*LUC109*/
             DCL VAR(&LDA) TYPE(*CHAR) LEN(256)
             DCL        VAR(&DATJOB) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MMDD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FILENAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&RERUN) TYPE(*CHAR) LEN(1)
             DCL        VAR(&MIDDAYC) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LIBHIS) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SLCT) TYPE(*CHAR) LEN(100)
             DCL        VAR(&MPEMA) TYPE(*CHAR) LEN(5)
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)                 /*159964*/
/**/
             DCLF       FILE(SDGELRPP)

             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))                              /*159964*/
/**/
/*   Create Local Data Area                                       */
/**/
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256) +
                          TEXT('Midas SD Local Data Area')
             MONMSG     MSGID(CPF0000)

/*                                                                   */
/* Retrieve 'Last Acct date' (month and day) from SDGELRPP           */
/*                                                                   */
/************CALL*******GLCM049*PARM(&MMDD)***************************/             /*LUC109*/
             CALL       XXC000049 PARM(&MMDD)                                       /*LUC109*/

/**/
/*  Get Midas Run Day Number from SDBANKPD                           */
/**/
/************CALL       GLCM052 PARM(&MIDDAYC)                       */             /*LUC109*/
             CALL       XXC000052 PARM(&MIDDAYC)                                    /*LUC109*/
/*                                                                   */
/* Clear working file FEPTF and WKFEPTF for extraction               */
/*                                                                   */
             CLRPFM     FILE(FEPTF)
             CLRPFM     FILE(WKFEPTF)
             CLRPFM     FILE(FEPTFTI)

/*                                                                   */
/* Creating file name PTFxxyy (where xx=month, yy=day)               */
/* PTFxxyy                                                           */
/*                                                                   */
             CHGVAR     VAR(&FILENAME) VALUE('PTF' *TCAT &MMDD)
             RTVOBJD    OBJ(FEPTF) OBJTYPE(*FILE) RTNLIB(&LIB)
             DLTF       FILE(&LIB/&FILENAME)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(FEPTF) FROMLIB(&LIB) OBJTYPE(*FILE) +
                          TOLIB(*FROMLIB) NEWOBJ(&FILENAME) DATA(*NO)

/*                                                                   */
/* In Rerun Mode, restore FEPTFTIPSV to FEPTFTIPC                    */
/*                                                                   */
             IF         COND(&RERUN *EQ 'Y') THEN(DO)
             CLRPFM     FILE(&LIB/FEPTFTIPC)
             CPYF       FROMFILE(FEPTFTIPSV) TOFILE(&LIB/FEPTFTIPC) +
                          MBROPT(*REPLACE)
             MONMSG     MSGID(CPF0000)
             ENDDO

/*                                                                   */
/* Copy in TI File                                                   */
/*                                                                   */
             CPYF       FROMFILE(FEPTFTIPC) TOFILE(FEPTFTI) +
                          MBROPT(*REPLACE) FMTOPT(*CVTSRC)
             MONMSG     MSGID(CPF0000)

/*                                                                   */
/* Run Extract                                                       */
/*                                                                   */
             RCVF

             CHGVAR     VAR(&MPEMA) VALUE(&MPEMB)
             CHGVAR     VAR(&SLCT) VALUE('((MDAT *LE' *BCAT +
                          &MIDDAYC *CAT ') *AND (MDAT *GT' *BCAT +
                          &MPEMA *CAT '))')
             RTVOBJD    OBJ(WKMCLONL) OBJTYPE(*FILE) RTNLIB(&LIBHIS)
             CLRPFM     FILE(WKMCLONL)
             OPNQRYF    FILE((*LIBL/MCLONCL)) OPTION(*INP) +
                          FORMAT(*FILE) QRYSLT(&SLCT)

             CPYFRMQRYF FROMOPNID(MCLONCL) TOFILE(&LIBHIS/WKMCLONL) +
                          MBROPT(*REPLACE) CRTFILE(*NO) FMTOPT(*NOCHK)
             CLOF       OPNID(MCLONCL)

             OVRDBF     FILE(FEPTF) TOFILE(&LIB/&FILENAME)

             OVRDBF     FILE(SDGELRPP) SHARE(*NO)

/************CALL       PGM(GLM033A)*********************************/              /*LUC109*/
             CALL       PGM(XX00033A)                                               /*LUC109*/

/************CALL       PGM(GLM033B)*********************************/              /*LUC109*/
             CALL       PGM(XX00033B)                                               /*LUC109*/

/************CALL       PGM(GLM033C)*********************************/              /*LUC109*/
             CALL       PGM(XX00033C)                                               /*LUC109*/

             DLTOVR     FILE(FEPTF)


             DLTOVR     FILE(SDGELRPP)
                                                                      /*159964*/
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)              /*159964*/
               RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)           /*159964*/
               SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                          TOMSGQ(MOPERQ MRUNQ)                        /*159964*/
               MONMSG     MSGID(CPF0000 RPG0000 MCH0000)              /*159964*/
               GOTO       CMDLBL(ABNOR)                               /*159964*/
             ENDDO                                                    /*159964*/
/*                                                                   */
/* Copy FEPTFTIPC to FEPTFTIPSV                                      */
/*                                                                   */
             DLTF       FILE(&LIB/FEPTFTIPSV)
             MONMSG     MSGID(CPF0000)
             CPYF       FROMFILE(FEPTFTIPC) TOFILE(&LIB/FEPTFTIPSV) +
                          MBROPT(*ADD) CRTFILE(*YES)

             CLRPFM     FILE(FEPTFTIPC)
             MONMSG     MSGID(CPF0000)
                                                                      /*159964*/
             GOTO       CMDLBL(ENDPGM)                                /*159964*/

 ABNOR:                                                               /*159964*/
             CHGJOB     SWS(XXXXXX11)                                 /*159964*/
/************SNDPGMMSG**MSG('Program*GLCM033*ended*abnormally')*+****/
/*************************TOMSGQ(MOPERQ*MRUNQ)*******************/    /*159964*/    /*LUC109*/
             SNDPGMMSG  MSG('Program XXC000033 ended abnormally') +
                          TOMSGQ(MOPERQ MRUNQ)                                      /*LUC109*/
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)                /*159964*/
 ENDPGM:                                                              /*159964*/
/************CHGVAR &CPY '(C) COPYRIGHT MKI LIMITED 1998'*********/   /*159964*/    /*LUC109*/
             CHGVAR &CPY '(C) Copyright Finastra International Limited 2016'        /*LUC109*/
             ENDPGM

/**          CHGVAR &CPY '(C) COPYRIGHT MKI LIMITED 1998'             /*159964*/
