/*********************************************************************/
/*S*D****CLPBASE******************************************************/             /*LUC109*/
/*STD    CLPBASEBND                                                  */             /*LUC109*/
/*********************************************************************/
/*                                                                   */
/*       Midas - Interface to Head Office Reporting                  */
/*                                                                   */             /*LUC109*/
/********GLCM034*-*GL*Foreign*Exchange*Transcation*Extract************/             /*LUC109*/
/*       XXC000034 - Midas HOR FX Transaction Extract                */
/*                                                                   */
/********(C)*COPYRIGHT*MKI*LIMITED*1998*******************************/             /*LUC109*/
/*       (C) Copyright Finastra International Limited 2016           */             /*LUC109*/
/*                                                                   */
/*       THIS SRC MUST BE COMPILED WITH xxINTFLIB at BOTTOM of LIBL  */             /*LUC109*/
/*                                                                   */
/*       Last Amend No. LUC109             Date 14Sep16              */
/*       Prev Amend No. 159964             Date 22Aug99              */
/*                      MMI043             Date 21Apr99              */
/*                      MMI043  *CREATE    Date 09Dec98              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       LUC109 - HO Reporting (Upgrade to FBM 2.1)                  */
/*       159964 - PGM/GLM032A cannot allocate LF/SDCUSTL0 due to     */
/*                SCC0406.                                           */
/*              - Seton job switches U7/U8 for error processing.     */
/*       MMI043 - Interface to Head Office Reporting                 */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       C R E A T I O N     P A R A M E T E R S                     */
/*                                                                   */
/*********************************************************************/
             PGM
/**/
/*   Declare variables                                            */
/**/
/************DCL VAR(&CPY) TYPE(*CHAR) LEN(64) VALUE('(C) +
                 COPYRIGHT MKI LIMITED 1998')                     */                /*LUC109*/
             DCL VAR(&CPY) TYPE(*CHAR) LEN(64) VALUE('(C) +
                 Copyright Finastra International Limited 2016')                    /*LUC109*/
             DCL VAR(&LDA) TYPE(*CHAR) LEN(256)
             DCL        VAR(&DATJOB) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MMDD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FILENAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&LIBHIS) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MIDDAYC) TYPE(*CHAR) LEN(5)
             DCL        VAR(&SLCT) TYPE(*CHAR) LEN(100)
             DCL        VAR(&MPEMA) TYPE(*CHAR) LEN(5)
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)                 /*159964*/
/**/
             DCLF       FILE(SDGELRPP)

             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))                              /*159964*/

/**/
/*   Create Local Data Area                                       */
/**/
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256) +
                          TEXT('Midas SD Local Data Area')
             MONMSG     MSGID(CPF0000)

/*                                                                   */
/* Retrieve 'Last Acct date' (month and day) from SDGELRPP           */
/*                                                                   */
/************CALL*******GLCM049*PARM(&MMDD)***************************/             /*LUC109*/
             CALL       XXC000049 PARM(&MMDD)                                       /*LUC109*/

/**/
/*  Get Midas Run Day Number from SDBANKPD                           */
/**/
/************CALL*******GLCM052*PARM(&MIDDAYC)************************/             /*LUC109*/
             CALL       XXC000052 PARM(&MIDDAYC)                                    /*LUC109*/
/*                                                                   */
/* Clear working file FEIMC and WKFEIMC for extraction               */
/*                                                                   */
             CLRPFM     FILE(FEIMC)

             RCVF

             CHGVAR     VAR(&MPEMA) VALUE(&MPEMB)
             CHGVAR     VAR(&SLCT) VALUE('((VDAT *LE' *BCAT +
                          &MIDDAYC *CAT ') *AND (VDAT *GT' *BCAT +
                          &MPEMA *CAT '))')
             RTVOBJD    OBJ(WKDLBHIS) OBJTYPE(*FILE) RTNLIB(&LIBHIS)
             CLRPFM     FILE(WKDLBHIS)
             OPNQRYF    FILE((*LIBL/DLBHISPD)) OPTION(*INP) +
                          FORMAT(*FILE) QRYSLT(&SLCT)

             CPYFRMQRYF FROMOPNID(DLBHISPD) TOFILE(&LIBHIS/WKDLBHIS) +
                          MBROPT(*REPLACE) CRTFILE(*NO) FMTOPT(*NOCHK)
             CLOF       OPNID(DLBHISPD)
/*                                                                   */
/* Creating file name IMCxxyy (where xx=month, yy=day)               */
/* IMCxxyy                                                           */
/*                                                                   */
             CHGVAR     VAR(&FILENAME) VALUE('IMC' *TCAT &MMDD)
             RTVOBJD    OBJ(FEIMC) OBJTYPE(*FILE) RTNLIB(&LIB)
             DLTF       FILE(&LIB/&FILENAME)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(FEIMC) FROMLIB(&LIB) OBJTYPE(*FILE) +
                          TOLIB(*FROMLIB) NEWOBJ(&FILENAME) DATA(*NO)

/*                                                                   */
/* Run Extract                                                       */
/*                                                                   */
             OVRDBF     FILE(FEIMC) TOFILE(&LIB/&FILENAME)
             OVRDBF     FILE(SDGELRPP) SHARE(*NO)

/************CALL*******PGM(GLM034)***********************************/             /*LUC109*/
             CALL       PGM(XX000034)                                               /*LUC109*/

             DLTOVR     FILE(FEIMC)
             DLTOVR     FILE(SDGELRPP)

             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)              /*159964*/
               RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)           /*159964*/
               SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                          TOMSGQ(MOPERQ MRUNQ)                        /*159964*/
               MONMSG     MSGID(CPF0000 RPG0000 MCH0000)              /*159964*/
               GOTO       CMDLBL(ABNOR)                               /*159964*/
             ENDDO                                                    /*159964*/
                                                                      /*159964*/
             GOTO       CMDLBL(ENDPGM)                                /*159964*/
                                                                      /*159964*/
 ABNOR:                                                               /*159964*/
             CHGJOB     SWS(XXXXXX11)                                 /*159964*/
/************SNDPGMMSG  MSG('Program GLCM034 ended abnormally') +
                          TOMSGQ(MOPERQ MRUNQ)                  */    /*159964*/    /*LUC109*/
             SNDPGMMSG  MSG('Program XXC000034 ended abnormally') +
                          TOMSGQ(MOPERQ MRUNQ)                        /*159964*/    /*LUC109*/
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)                /*159964*/
 ENDPGM:                                                              /*159964*/
 /***********CHGVAR &CPY '(C) COPYRIGHT MKI LIMITED 1998'     */      /*159964*/    /*LUC109*/
             CHGVAR &CPY '(C) Copyright Finastra International Limited 2016'        /*LUC109*/
             ENDPGM

 /**         CHGVAR &CPY '(C) COPYRIGHT MKI LIMITED 1998'             /*159964*/
 /**         PGM                                                      /*159964*/
