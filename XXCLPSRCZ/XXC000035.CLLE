/*********************************************************************/
/*S*D****CLPBASE******************************************************/             /*LUC109*/
/*STD    CLPBASEBND                                                  */             /*LUC109*/
/*********************************************************************/
/*                                                                   */
/*       Midas - Interface to Head Office Reporting                  */
/*                                                                   */
/********GLCM035*-*GL*Daily Movements Extract*************************/             /*LUC109*/
/*       XXC000035 - Midas HOR GL Daily Movements Extract            */             /*LUC109*/
/*                                                                   */
/********(C)*COPYRIGHT*MKI*LIMITED*1998*******************************/             /*LUC109*/
/*       (C) Copyright Finastra International Limited 2016           */             /*LUC109*/
/*                                                                   */
/*       THIS SRC MUST BE COMPILED WITH xxINTFLIB at BOTTOM of LIBL  */             /*LUC109*/
/*                                                                   */
/*       Last Amend No. LUC109             Date 14Sep16              */
/*       Prev Amend No. 159964             Date 22Aug99              */
/*                      MMI043             Date 07Jul99              */
/*                      MMI043             Date 21Apr99              */
/*                      MMI043  *CREATE    Date 09Dec98              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       LUC109 - HO Reporting (Upgrade to FBM 2.1)                  */
/*       159964 - PGM/GLM032A cannot allocate LF/SDCUSTL0 due to     */
/*                SCC0406.                                           */
/*              - Seton job switches U7/U8 for error processing.     */
/*       MMI043 - Call UTIL06 to setup PF SDACODX1.                  */
/*       MMI043 - Interface to Head Office Reporting                 */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       C R E A T I O N     P A R A M E T E R S                     */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&RERUN)
/**/
/*   Declare variables                                            */
/**/
/************DCL VAR(&CPY) TYPE(*CHAR) LEN(64) VALUE('(C) +
                 COPYRIGHT MKI LIMITED 1998')                     */                /*LUC109*/
             DCL VAR(&CPY) TYPE(*CHAR) LEN(64) VALUE('(C) +
                 Copyright Finastra International Limited 2016')                    /*LUC109*/
             DCL VAR(&LDA) TYPE(*CHAR) LEN(256)
             DCL        VAR(&DATJOB) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MMDD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FILENAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&RERUN) TYPE(*CHAR) LEN(1)
             DCL        VAR(&MIDDAYC) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LIBHIS) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SLCT) TYPE(*CHAR) LEN(100)
             DCL        VAR(&MPEMA) TYPE(*CHAR) LEN(5)
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)                 /*159964*/
/**/
             DCLF       FILE(SDGELRPP)

             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))                              /*159964*/
/**/
/*   Create Local Data Area                                       */
/**/
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256) +
                          TEXT('Midas SD Local Data Area')
             MONMSG     MSGID(CPF0000)

/*                                                                   */
/* Retrieve 'Last Acct date' (month and day) from SDGELRPP           */
/*                                                                   */
/************CALL       GLCM049 PARM(&MMDD)                          */             /*LUC109*/
             CALL       XXC000049 PARM(&MMDD)                                       /*LUC109*/
/**/
/*  Get Midas Run Day Number from SDBANKPD                           */
/**/
/************CALL       GLCM052 PARM(&MIDDAYC)                       */             /*LUC109*/
             CALL       XXC000052 PARM(&MIDDAYC)                                    /*LUC109*/

/*                                                                      MMI043*/
/* Clear and Setup SDACODX1                                             MMI043*/
/*                                                                      MMI043*/
             CLRPFM     FILE(SDACODX1)                                /*MMI043*/
/************CALL*******UTIL06*******************************/        /*MMI043*/    /*LUC109*/
             CALL       XXUT0006                                      /*MMI043*/    /*LUC109*/
/*                                                                   */
/* Clear working files FEGIB AND EODCLWK                             */
/*                                                                   */
             CLRPFM     FILE(FEGIB)
             CLRPFM     FILE(EODCLWK)

/*                                                                   */
/* Creating file name GIBxxyy (where xx=month, yy=day)               */
/* GIBxxyy                                                           */
/*                                                                   */
             CHGVAR     VAR(&FILENAME) VALUE('GIB' *TCAT &MMDD)
             RTVOBJD    OBJ(FEGIB) OBJTYPE(*FILE) RTNLIB(&LIB)
             DLTF       FILE(&LIB/&FILENAME)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(FEGIB) FROMLIB(&LIB) OBJTYPE(*FILE) +
                          TOLIB(*FROMLIB) NEWOBJ(&FILENAME) DATA(*NO)

/*                                                                   */
/* In Rerun Mode, restore ACCOLD AND EODCLPD                         */
/*                                                                   */
             IF         COND(&RERUN *EQ 'Y') THEN(DO)
             CPYF       FROMFILE(ACCOLDSV) TOFILE(&LIB/ACCOLD) +
                          MBROPT(*REPLACE)
             CLRPFM     FILE(EODCLPD)
             CPYF       FROMFILE(EODCLPDSV) TOFILE(&LIB/EODCLPD) +
                          MBROPT(*REPLACE)
             MONMSG     MSGID(CPF0000)
             ENDDO

/*                                                                   */
/* Run Extract                                                       */
/*                                                                   */
             RCVF

             CHGVAR     VAR(&MPEMA) VALUE(&MPEMB)
             CHGVAR     VAR(&SLCT) VALUE('((MDAT *LE' *BCAT +
                          &MIDDAYC *CAT ') *AND (MDAT *GT' *BCAT +
                          &MPEMA *CAT '))')
             RTVOBJD    OBJ(WKMCLONL) OBJTYPE(*FILE) RTNLIB(&LIBHIS)
             CLRPFM     FILE(WKMCLONL)
             OPNQRYF    FILE((*LIBL/MCLONCL)) OPTION(*INP) +
                          FORMAT(*FILE) QRYSLT(&SLCT)

             CPYFRMQRYF FROMOPNID(MCLONCL) TOFILE(&LIBHIS/WKMCLONL) +
                          MBROPT(*REPLACE) CRTFILE(*NO) FMTOPT(*NOCHK)
             CLOF       OPNID(MCLONCL)

             OVRDBF     FILE(FEGIB) TOFILE(&LIB/&FILENAME)

             OVRDBF     FILE(SDGELRPP) SHARE(*NO)

/************CALL*******PGM(GLM035)**********************************/              /*LUC109*/
             CALL       PGM(XX000035)                                               /*LUC109*/

/************CALL*******PGM(GLM035A)*********************************/              /*LUC109*/
             CALL       PGM(XX00035A)                                               /*LUC109*/

             DLTOVR     FILE(FEGIB)

             DLTOVR     FILE(SDGELRPP)

             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)              /*159964*/
               RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)           /*159964*/
               SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                          TOMSGQ(MOPERQ MRUNQ)                        /*159964*/
               MONMSG     MSGID(CPF0000 RPG0000 MCH0000)              /*159964*/
               GOTO       CMDLBL(ABNOR)                               /*159964*/
             ENDDO                                                    /*159964*/
/*                                                                   */
/* Copy ACCNTAB to ACCOLD                                            */
/*                                                                   */
             DLTF       FILE(&LIB/ACCOLDSV)
             MONMSG     MSGID(CPF0000)
             CPYF       FROMFILE(ACCOLD) TOFILE(&LIB/ACCOLDSV) +
                          MBROPT(*ADD) CRTFILE(*YES)

             CPYF       FROMFILE(ACCNTAB) TOFILE(&LIB/ACCOLD) +
                          MBROPT(*REPLACE)

/*                                                                   */
/* Copy EODCLWK to EODCLPD for next days processing.                 */
/*                                                                   */
             DLTF       FILE(&LIB/EODCLPDSV)
             MONMSG     MSGID(CPF0000)

             CPYF       FROMFILE(EODCLPD) TOFILE(&LIB/EODCLPDSV) +
                          MBROPT(*ADD) CRTFILE(*YES)
             MONMSG     MSGID(CPF0000)

             CLRPFM     FILE(EODCLPD)
             CPYF       FROMFILE(EODCLWK) TOFILE(&LIB/EODCLPD) +
                          MBROPT(*REPLACE)
             MONMSG     MSGID(CPF0000)
                                                                      /*159964*/
             GOTO       CMDLBL(ENDPGM)                                /*159964*/
                                                                      /*159964*/
 ABNOR:                                                               /*159964*/
             CHGJOB     SWS(XXXXXX11)                                 /*159964*/
/************SNDPGMMSG  MSG('Program GLCM035 ended abnormally') +
                          TOMSGQ(MOPERQ MRUNQ)                        /*159964*/    /*LUC109*/
             SNDPGMMSG  MSG('Program XXC000035 ended abnormally') +
                          TOMSGQ(MOPERQ MRUNQ)                                      /*LUC109*/
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)                /*159964*/
 ENDPGM:                                                              /*159964*/
/************CHGVAR*&CPY*'(C)*COPYRIGHT*MKI*LIMITED*1998'             /*159964*/    /*LUC109*/
             CHGVAR &CPY '(C) Copyright Finastra International Limited 2016'        /*LUC109*/

/*                                                                   */
/* End Program                                                       */
/*                                                                   */
             ENDPGM

/**          CHGVAR &CPY '(C) COPYRIGHT MKI LIMITED 1998'             /*159964*/
