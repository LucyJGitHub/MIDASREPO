      *****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('RTGS Codewords Enquiry')                               *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  XX000020DF - RTGS Codewords Enquiry                          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. HUT110   *Create   Date 17Apr20               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  HUT110 - Upgrade of HUT002 to FBM 2.1 SP22.                  *
      *           RTGS Via SWIFT                                      *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
      ** Subfile record.
      *
     A          R #SFLRCD                   SFL
     A                                      TEXT('(c) Finastra International Li-
     A                                      mited 2001          ')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(35 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1CDES        30A  H      TEXT('RTGS Codeword Description')
     A            #1SEL          1A  B  8  2VALUES('E' 'A' 'D')
     A                                      CHECK(AB)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N35                                  DSPATR(UL)
     A N35                                  DSPATR(HI)
     A            #1CCY          3A  O  8 30TEXT('RTGS Currency Code')
     A                                      DSPATR(HI)
     A            #1COD2        20A  O  9  5DSPATR(HI)
     A            #1COD1        20A  O  8  5DSPATR(HI)
      *
      ** Subfile control.
      *
     A          R #SFLCTL                   SFLCTL(#SFLRCD)
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A                                      TEXT('Subfile control.')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF09(09 'Add.')
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2COD1')
     A                                      SETOF(32 'Invalid: #2CCY')
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A N25N80 81                            SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A N80 81 82                            SFLEND
     A            ##SFRC         4S 0H      SFLRCDNBR
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A            ##MRDT         7A  O  1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##URPT        53A  O  2 13TEXT('User Report Title')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  5'RTGS'
     A                                  4 30'Pay'
     A                                  5  5'Codeword'
     A                                  5 28'Currency'
     A            #2COD1        20A  B  6  5TEXT('RTGS Codeword')
     A                                      CHECK(LC)
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A N25                                  OVRATR
     A            #2CCY          3A  B  6 30TEXT('RTGS Currency Code')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A N25                                  OVRATR
     A            ##USR         10A  O  1  2TEXT('*USER')
     A                                  1 13'XX0020D1'
      *
      ** Function keys record format.
      *
     A          R #CMDTXT1
     A                                      TEXT('Command keys')
     A                                      OVERLAY
     A                                      PUTOVR
     A            ##CTX1        78A  O 22  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A            ##CTX2        78A  O 23  2TEXT('Command Text 2')
     A N25                                  OVRDTA
      *
      ** Subfile message.
      *
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ(10)
      *
      ** Subfile control message.
      *
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A  86                                  OVERLAY
     A  86                                  PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            ##PGM                     SFLPGMQ(10)
