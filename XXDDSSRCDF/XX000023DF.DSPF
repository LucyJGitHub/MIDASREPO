      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('CNAPS Code Maintenance')                               *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  XX000023DF - CNAPS Code Maintenance                          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. HUT110   *Create   Date 17Apr20               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  HUT110 - Upgrade HUT022 to FBM 2.1 SP22.                     *
      *           RTGS Via SWIFT                                      *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
      ** Prompt 1 record format.
      *
     A          R #RCDKEY
     A                                      TEXT('(c) Finastra International Li-
     A                                      mited 2001          ')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF09(09 'Change mode.')
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1SWCD')
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'XX0023C1'
     A            ##MRDT         7A  O  1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##URPT        53A  O  2 13TEXT('User Report Title')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #PANCD         1A  H      TEXT('Action Code')
     A                                  4  2'CNAPS Code.....:'
     A            #1CODE        12   B  4 19
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PC)
     A N25                                  OVRDTA
     A N25                                  OVRATR
      *
      ** Function Key 1 record format.
      *
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*
     A** Prompt 2 record format.
     A*
     A          R #RCDDTL1
     A                                      TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A N89                                  CF10(10 'Delete.')
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1COD1')
     A                                      SETOF(32 'Invalid: #1COD2')
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'XX0023C2'
     A            ##MRDT         7A  O  1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##URPT        53A  O  2 13TEXT('User Report Title')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #PANCD         1A  H      TEXT('Action Code')
     A                                  4  2'CNAPS Code....:'
     A                                  5  2'Bank Name.....:'
     A            #1BKNM        35A  B  5 18CHECK(FE)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A N79N42                               DSPATR(UL)
     A N42                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                      CHECK(LC)
     A                                  8  2'City..........:'
     A            #1CITY        20A  B  8 18TEXT('City')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N25                                  OVRATR
     A N32                                  DSPATR(HI)
     A            #1CODE        12   O  4 18
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI)
     A N25                                  OVRDTA
     A                                  6  2'Branch Name...:'
     A            #1BRNM        30A  B  6 18CHECK(FE)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A N79N43                               DSPATR(UL)
     A N43                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                      CHECK(LC)
     A                                  7  2'Customer No...:'
     A            #1CUST         6A  B  7 18CHECK(FE)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A N79N44                               DSPATR(UL)
     A N44                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  9  2'Address.......:'
     A            #1ADR1        35A  B  9 18TEXT('Address line 1')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N25                                  OVRATR
     A N32                                  DSPATR(HI)
     A            #1ADR2        35A  B 10 18TEXT('Address line 2')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N25                                  OVRATR
     A N32                                  DSPATR(HI)
     A            #1ADR3        35A  B 11 18TEXT('Address line 3')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  80                                  DSPATR(PR)
     A N80                                  DSPATR(UL)
     A N25                                  OVRATR
     A N32                                  DSPATR(HI)
      *
      ** Function Key 2 record format.
      *
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
      *
      ** Subfile message.
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      ** Subfile message control.
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
