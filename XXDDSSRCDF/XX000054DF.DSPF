      *****************************************************************
/*STD *  DSPFBASE                                                     *
      *****************************************************************
      *                                                               *
      *  Midas - Interface File Copy Definition Screens.              *
      *                                                               *
      ***DSPF/GLM054DF*-*Interface File Copy Definition.              *                       LUC109
      *  DSPF/XX000054DF - Interface File Copy Definition.            *                       LUC109
      *  (Copy of IBM003DF - CAR ICM003)                              *
      *                                                               *
      ***(C)*Copyright*Midas-Kapiti*International*Ltd.*1998************                       LUC109
      *  (c) Misys International Banking Systems Ltd. 2001            *                       LUC109
      *                                                               *
      *  Last Amend No. LUC109             Date 23Aug16               *
      *  Prev Amend No. MUI009             Date 10Feb04               *
      *                 MMI043  *CREATE    Date 01Dec98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC109 - HO Reporting (Upgrade to FB Midas 2.1)              *
      *  MUI009 - Upgrade to R4.01: remove Help.                      *
      *  MMI043 - Bank of Italy Head Office Reporting                 *
      *                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A**********                            REF(*LIBL/GLINCPPP)                               LUC109
     A                                      REF(*LIBL/XXINCPPD)                               LUC109
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A*** MUI009 begin
     A**********                            HELP
     A**********                            HLPDOC('H905@000' 'BBN20500' 'ABS-
     A**********                            ENG/DBAR2')
     A*** MUI009 end
     A*=========================================================================
     A**********R GLM054F1                  TEXT('(c) Copyright Midas-Kapiti In-              LUC109
     A          R XX000054F1                TEXT('(c) Copyright Midas-Kapiti In-              LUC109
     A                                      ternational Ltd 1998')
     A                                      INDTXT(41 'Invalid: I1MDFN')
     A                                      INDTXT(42 'Invalid: I1TGFN')
     A                                      INDTXT(43 'Invalid: I1SLCT')
     A                                      INDTXT(21 'Protect fields')
     A                                      CA03(03 'Exit.')
     A                                      CA12(12 'Prev.')
     A                                      CA10(10 'Delete')
     A                                      OVERLAY
     A*** MUI009 begin
      ***Help*Documentation.
      **********
     A**********H                           HLPDOC('H905@010' 'BBN20500' 'ABS-
     A**********                            ENG/DBAR2')
     A**********                            HLPARA(06 03 06 34)
      **********
     A**********H                           HLPDOC('H905@015' 'BBN20500' 'ABS-
     A**********                            ENG/DBAR2')
     A**********                            HLPARA(08 03 08 34)
      **********
     A**********H                           HLPDOC('H905@020' 'BBN20500' 'ABS-
     A**********                            ENG/DBAR2')
     A**********                            HLPARA(10 03 10 25)
      **********
     A**********H                           HLPDOC('H905@025' 'BBN20500' 'ABS-
     A**********                            ENG/DBAR2')
     A**********                            HLPARA(12 03 18 80)
     A*** MUI009 end
     A*                                                                =========
     A** Header details.                                               =========
     A            USER          10A  O  1  2TEXT('*USER')
     A            ##PGM         10A  O  1 13TEXT('*PROGRAM')
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            JOB           10A  O  2  2TEXT('*JOB')
     A                                  2 30'FILE COPY DEFINITION'
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A*                                                                =========
     A** Fields details.                                               =========
     A                                  6  3'Midas File Name ....:'
     A                                  8  3'Target File Name....:'
     A                                 10  3'Intra-Day Update?...:'
     A                                 12  3'Selection Criteria..:'
     A************I1MDFN    R        B  6 25REFFLD(IMDFN)                                     LUC109
     A            I1MDFN        10A  B  6 25                                                  LUC109
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(PR)
     A************I1TGFN    R        B  8 25REFFLD(ITGFN)                                     LUC109
     A            I1TGFN        10A  B  8 25                                                  LUC109
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(PR)
     A************I1INDY    R        O 10 25REFFLD(IINDY)                                     LUC109
     A            I1INDY         1A  O 10 25                                                  LUC109
     A            I1SLC1        55A  B 12 25
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(PR)
     A            I1SLC2        77A  B 13  3
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(PR)
     A            I1SLC3        77A  B 14  3
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(PR)
     A            I1SLC4        77A  B 15  3
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(PR)
     A            I1SLC5        77A  B 16  3
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(PR)
     A            I1SLC6        77A  B 17  3
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(PR)
     A            I1SLC7        72A  B 18  3
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(PR)
     A*=========================================================================
      * * Subfile record details format.
     A          R #SFLRCD                   SFL
     A                                      TEXT('(c) Copyright Midas Kapiti In-
     A                                      ternational Ltd 1998')
     A  74                                  SFLNXTCHG
     A            I1SEL          1A  B  8  3TEXT('Opt')
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            I1MDFN        10A  O  8  6TEXT('Midas Filename')
     A            I1TGFN        10A  O  8 20TEXT('Target Filename')
     A            I1INDY         1A  O  8 37TEXT('Intra-Day Update')
     A*=========================================================================
      * * Subfile control format.
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(25 'Disable OVRDTA')
     A                                      INDTXT(51 'Subfile message ID')
     A                                      INDTXT(60 'Clear subfile')
     A                                      INDTXT(61 'Display subfile')
     A                                      INDTXT(62 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  60                                  SFLDSP
     A  61                                  SFLDSPCTL
     A N60N61                               SFLCLR
     A  62                                  SFLEND(*MORE)
     A  51                                  SFLMSGID(CPF5203 *LIBL/QCPFMSG)
     A                                      CA03(03 'Exit.')
     A                                      CA05(05 'Refresh')
     A                                      ROLLUP(27 'Next page.')
     A                                      CF09(09 'Add mode.')
     A*** MUI009 begin
      ***Help*Documentation.
     A**********H                           HLPDOC('H905@005' 'BBN20500' 'ABS-
     A**********                            ENG/DBAR2')
     A**********                            HLPARA(08 03 19 03)
     A*** MUI009 end
     A            UISFRN         4S 0H      SFLRCDNBR(CURSOR)
     A*                                                                =========
     A** Header details.                                               =========
     A            USER          10A  O  1  2TEXT('*USER')
     A            ##PGM         10A  O  1 13TEXT('*PROGRAM')
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            JOB           10A  O  2  2TEXT('*JOB')
     A                                  2 30'FILE COPY DEFINITION'
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A*                                                                =========
     A** Subfile details.                                              =========
     A                                  5 07'Midas'
     A                                  5 21'Target'
     A                                  5 33'Intra-day'
     A                                  6 06'Filename'
     A                                  6 20'Filename'
     A                                  6 34'Update'
     A                                  7  6'--------'
     A                                  7 20'--------'
     A                                  7 33'---------'
      *
     A*                                                                =========
     A*=========================================================================
     A          R FOOTER                    TEXT('Command keys.')
     A N23                             22  1'A=Amend  E=Enquire'
     A N23                             22 21'D=Delete  C=Copy File'
     A                                 23  1'F3=Exit'
     A  23                             23 10'F12=Previous'
     A N23                             23 10'F5=Refresh'
     A N23                             23 24'F9=Add new definition'
     A  24                             23 24'F10=Confirm delete'
     A*                                                                =========
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
