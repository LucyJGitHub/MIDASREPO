      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Income and Expense Mapping')                  *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/XX000101DF - Income and Expense Accounts Mapping        *
      *                                                               *
      *  Function:  Allow entry of a single or a range of Income and  *
      *  (I/C)      Expense accounts to be mapped to a Profit/Loss    *
      *             account and Spot Trade Account.                   *
      *                                                               *
      *  (c) Finastra International Limited 2017                      *
      *                                                               *
      *  Last Amend No. FTV102  *CREATE     Date 08May17              *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  FTV102 - Posting of Profit and Loss to Multiple Retained     *
      *           Earnings Accounts.                                  *
      *                                                               *
      *****************************************************************
      *
     A                                      CA03(03 'Exit')
     A                                      CA05(05 'Reset')
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
      * -------------------------------------------------------------------*
      ** Subfile Record Format
      * -------------------------------------------------------------------*
      *
     A          R XX000101S0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2017')
      *
     A                                      SFL
     A                                      SETOF(83 'Force input format')
     A  84                                  SFLNXTCHG
     A            ##DBRC        45   H      TEXT('Existing record fields')
     A            ##AINC1       10   H      TEXT('Income and Expense 1')
     A            #RECI          1   H      TEXT('Record Identifier')
     A            #CHTP          1   H      TEXT('Type of Last Change')
     A            #LCDT          6  0H      TEXT('Last Change Date')
      *
     A            #SELC          1A  B  8  2TEXT('Action Code')
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  79N32                               DSPATR(ND)
     A N79N32                               DSPATR(HI UL)
     A            #AINC1        10A  B  8  5TEXT('Income and Expense 1')
     A  78                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N78N33                               DSPATR(HI UL)
     A            #AINC2        10A  B  8 19TEXT('Income and Expense 2')
     A  87                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N87N34                               DSPATR(HI UL)
      *
     A            #APLA1        10A  B  8 39TEXT('Profit and Loss')
     A  88                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N88N35                               DSPATR(HI UL)
      *
     A            #ASRVL        10A  B  8 59TEXT('Spot Trade')
     A  88                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N88N36                               DSPATR(HI UL)
      *
      * -------------------------------------------------------------------*
      ** Subfile Control
      * -------------------------------------------------------------------*
      *
     A          R XX000101C0                TEXT('Subfile Control')
     A                                      SFLCTL(XX000101S0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      SETOF(83 'Force input format')
     A                                      OVERLAY
     A  86                                  PUTOVR
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N80 81                               SFLDSP
     A N80 81 82                            SFLEND
     A N82                                  ROLLUP(27 'Next page')
     A N31                                  CF09(09 'Change mode')
     A                                      SETOF(40 'Global error flag')
      *
     A            RRN            4  0H      SFLRCDNBR
     A            #USER         10   O  1  2
     A                                  1 13'XX000101'
     A N31N37                           1 29'Mapping Matrix Maintenance'
     A                                      DSPATR(HI)
     A  31N37                           1 31'Mapping Matrix Enquiry'
     A                                      DSPATR(HI)
     A                                  2 27'for Income and Expense Accounts'
     A                                      DSPATR(HI)
     A            #RUNA         10   O  1 72
     A            #WSID         10   O  2  2
     A            #TIME          6  0O  2 71
     A                                      EDTWRD('  :  :  ')
     A                                      OVRATR
     A                                      OVRDTA
     A                                  4  4'Income & Expense A/c codes'
     A                                  4 37'Profit & Loss'
     A                                  4 59'Spot Trade'
     A                                  5  7'From'
     A                                  5 23'To'
     A                                  5 38'Account Code'
     A                                  5 58'Account Code'
     A N89        #POSINC1      10A  B  6  5TEXT('Position Inc & Exp 1')
     A                                      DSPATR(HI UL PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A* -------------------------------------------------------------------*
      ** Command Line
     A* -------------------------------------------------------------------*
      *
     A          R XX000101F0                TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A N89N31N37                       23  3'D=Delete   F3=Main menu   F5=Refr-
     A                                      esh   F9=Go to ''ADD'' mode'
     A  89N31N37                       23  3'F3=Main menu   F5=Refr-
     A                                      esh   F9=Go to CHANGE mode'
     A  31                             23  3'F3=Main menu  F5=Refresh'
      *
     A* -------------------------------------------------------------------*
      ** Message Record Format
     A* -------------------------------------------------------------------*
      *
     A          R XX000101S1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            ##MSGK                    SFLMSGKEY
     A            ##PGMQ                    SFLPGMQ
      *
     A* -------------------------------------------------------------------*
      ** Message Control
     A* -------------------------------------------------------------------*
      *
     A          R XX000101C1                TEXT('Program messages')
     A                                      SFLCTL(XX000101S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGMQ                    SFLPGMQ
      *****************************************************************
