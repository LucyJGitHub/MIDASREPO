      *****************************************************************
/*STD *  DSPFBASE                                                     *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/XX000102DF - Spot Trade Accounts Mapping                *
      *                                                               *
      *  Function:  Allow the user to map a Spot Trade P/L            *
      *  (I/C)      account code to a Spot Trade Equivalent           *
      *             account code and a Spot Revaluation account code  *
      *             per Foreign Currency.                             *
      *                                                               *
      *  (c) Finastra International Limited 2017                      *
      *                                                               *
      *  Last Amend No. FVT102CC1           Date 09Jun17              *
      *  Prev Amend No. FVT102  *CREATE     Date 08May17              *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  FVT102CC1 - Posting of P/L to Mult. Retained Earnings CC1    *
      *  FVT102 - Posting of Profit and Loss to Multiple Retained     *
      *           Earnings Accounts.                                  *
      *                                                               *
      *****************************************************************
      *
     A                                      CA03(03 'Exit')
     A                                      CA05(05 'Reset')
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
      * -------------------------------------------------------------------*
      ** Subfile Record Format
      * -------------------------------------------------------------------*
      *
     A          R XX000102S0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2017')
      *
     A                                      SFL
     A                                      SETOF(83 'Force input format')
     A  84                                  SFLNXTCHG
     A            ##DBRC        38   H      TEXT('Existing record fields')
     A            ##BSRVL       10   H      TEXT('Spot Trade a/c code')
      ** Start of FVT102CC1
     A**********  ##BFCCY        3   H      TEXT('Foreign Currency Code')
     A            ##BFCCY        3   H      TEXT('Currency Code')
      ** End of FVT102CC1
     A            #RECI          1   H      TEXT('Record Identifier')
     A            #CHTP          1   H      TEXT('Type of Last Change')
     A            #LCDT          6  0H      TEXT('Last Change Date')
      *
     A            #SELC          1A  B  8  2TEXT('Action Code')
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  79N32                               DSPATR(ND)
     A N79N32                               DSPATR(HI UL)
     A            #BSRVL        10A  B  8  7TEXT('Spot Trade a/c code')
     A  78                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N78N33                               DSPATR(HI UL)
      ** Start of FVT102CC1
     A**********  #BFCCY         3A  B  8 27TEXT('Foreign Currency Code')
     A            #BFCCY         3A  B  8 27TEXT('Currency Code')
      ** End of FVT102CC1
     A  87                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N87N34                               DSPATR(HI UL)
      *
     A            #BSPAE        10A  B  8 42TEXT('Spot Trade Equiv a/c code')
     A  88                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N88N35                               DSPATR(HI UL)
      *
     A            #BSPTA        10A  B  8 62TEXT('Spot Revaluation a/c code')
     A  88                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N88N36                               DSPATR(HI UL)
      *
      * -------------------------------------------------------------------*
      ** Subfile Control
      * -------------------------------------------------------------------*
      *
     A          R XX000102C0                TEXT('Subfile Control')
     A                                      SFLCTL(XX000102S0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      SETOF(83 'Force input format')
     A                                      OVERLAY
     A  86                                  PUTOVR
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N80 81                               SFLDSP
     A N80 81 82                            SFLEND
     A N82                                  ROLLUP(27 'Next page')
     A N31                                  CF09(09 'Change mode')
     A                                      SETOF(40 'Global error flag')
      *
     A            RRN            4  0H      SFLRCDNBR
     A            #USER         10   O  1  2
     A                                  1 13'XX000102'
     A N31N37                           1 29'Mapping Matrix Maintenance'
     A                                      DSPATR(HI)
     A  31N37                           1 31'Mapping Matrix Enquiry'
     A                                      DSPATR(HI)
     A                                  2 30'for Spot Trade Accounts'
     A                                      DSPATR(HI)
     A            #RUNA         10   O  1 72
     A            #WSID         10   O  2  2
     A            #TIME          6  0O  2 71
     A                                      EDTWRD('  :  :  ')
     A                                      OVRATR
     A                                      OVRDTA
     A                                  4  7'Spot Trade'
      ** Start of FVT102CC1
     A**********                        4 25'Foreign'
     A                                  4 25'Currency'
      ** End of FVT102CC1
     A                                  4 37'Spot Trade Equivalent'
     A                                  4 59'Spot Revaluation'
     A                                  5  6'Account Code'
      ** Start of FVT102CC1
     A**********                        5 25'Currency'
      ** End of FVT102CC1
     A                                  5 41'Account Code'
     A                                  5 61'Account Code'
     A N89        #POSTRAD      10A  B  6  7TEXT('Spot Trade')
     A                                      DSPATR(HI UL PC)
      ** Start of FVT102CC1
     A*N89******  #POSFCCY       3A  B  6 27TEXT('Position Foreign currency')
     A N89        #POSFCCY       3A  B  6 27TEXT('Position Currency')
      ** End of FVT102CC1
     A                                      DSPATR(HI UL PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
     A* -------------------------------------------------------------------*
      ** Command Line
     A* -------------------------------------------------------------------*
      *
     A          R XX000102F0                TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A N89N31N37                       23  3'D=Delete   F3=Main menu   F5=Refr-
     A                                      esh   F9=Go to ''ADD'' mode'
     A  89N31N37                       23  3'F3=Main menu   F5=Refr-
     A                                      esh   F9=Go to CHANGE mode'
     A  31                             23  3'F3=Main menu  F5=Refresh'
      *
     A* -------------------------------------------------------------------*
      ** Message Record Format
     A* -------------------------------------------------------------------*
      *
     A          R XX000102S1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            ##MSGK                    SFLMSGKEY
     A            ##PGMQ                    SFLPGMQ
      *
     A* -------------------------------------------------------------------*
      ** Message Control
     A* -------------------------------------------------------------------*
      *
     A          R XX000102C1                TEXT('Program messages')
     A                                      SFLCTL(XX000102S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGMQ                    SFLPGMQ
