      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas XX SE Month End Exchange Rates Maintenance')     *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  XX0002DF - XX SE Month End Exchange Rates Maintenance        *
      *                                                               *
      *  (c) Finastra Technology, Inc. 2017                           *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Last Amend No. MD052141           Date 17Oct18               *
      *  Prev Amend No. DUG504             Date 07Sep17               *
      *                 nnnnnn             Date ddmmmyy               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD052141 - Recompile with multi-language system *NO          *
      *  DUG504 - Cost of Funds                                       *
      *                                                               *
      *---------------------------------------------------------------*
      *
     A                                      PRINT(MPRINT)
     A                                      CF03
     A                                      CF07
     A                                      CF10
     A                                      CF12
     A          R SCREEN
     A                                      RTNDTA
     A                                      PUTOVR
     A                                      BLINK
     A                                      ROLLDOWN(05)
     A                                      ROLLUP(06)
     A            WSID          10A  O  1  2
     A                                  1 27'MONTH END EXCHANGE RATES'
     A                                      DSPATR(HI UL)
     A            RUNA           7A  O  1 55
     A                                  1 64TIME
     A            DMEYR        002A  B  3 17DSPATR(HI)
     A N11                                  DSPATR(UL CS)
     A  11                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A                                      OVRDTA
     A            DMECY        003A  B  4 17DSPATR(HI)
     A N12                                  DSPATR(UL CS)
     A  12                                  DSPATR(RI PC)
     A                                      OVRDTA
     A            DMEMN        002A  B  5 17DSPATR(HI)
     A N13                                  DSPATR(UL CS)
     A  13                                  DSPATR(RI PC)
     A                                      OVRDTA
     A            DEXRT        014A  B  7 17DSPATR(HI)
     A N14N70                               DSPATR(UL   )
     A  14N70                               DSPATR(RI)
     A     24                               DSPATR(PC)
     A     70                               DSPATR(PR)
     A                                      OVRDTA
     A            DSPOT         14A  O  8 17DSPATR(HI)
     A                                      OVRDTA
     A            DMDIN          1A  O  8 33DSPATR(HI)
     A                                      OVRDTA
     A                                  3  2'Year.........:'
     A                                  4  2'Currency.....:'
     A                                  5  2'Month Number.:'
     A                                  7  2'Exchange Rate:'
     A                                  8  2'Spot Rate....:'
     A                                 15  2' '
     A          R SUBFLREC                  SFL
     A            ACTN           1A  I  5  2DSPATR(HI UL)
     A                                      AUTO(RA)
     A            DMEYR          2A  O  5  4DSPATR(HI)
     A            DMECY          3A  O  5  7DSPATR(HI)
     A            DMEMN          2A  O  5 11DSPATR(HI)
     A            DEXRT         14A  O  5 15DSPATR(HI)
     A            DSPOT         14A  O  5 32DSPATR(HI)
     A            DMDIN          1A  O  5 33DSPATR(HI)
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0018)
     A                                      BLINK
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A                                      ROLLDOWN(05)
     A                                      ROLLUP(06)
     A            WSID          10A  O  1  2
     A                                  1 27'MONTH END EXCHANGE RATES'
     A                                      DSPATR(HI UL)
     A            RUNA           7A  O  1 55
     A                                  1 64TIME
     A                                  2  4'Yr'
     A                                  3  4'  '
     A                                  2  7'Ccy'
     A                                  2 11'Mn'
     A                                  3 11'No'
     A                                  3 10'   '
     A                                  2 15'   Exchange   '
     A                                  3 15'     Rate     '
     A                                  2 32'     Spot     '
     A                                  3 32'     Rate     '
     A            DMEYR        002A  B  4  4DSPATR(HI UL)
     A            DMECY        003A  B  4  7DSPATR(HI UL)
     A            DMEMN        002A  B  4 11DSPATR(HI UL)
     A*
     A          R SCRMESS
     A                                      PUTOVR
     A                                 23  2'F3=Exit, F7=Review, F10=Delete, -
     A                                      F12=Previous, Rollup, Rolldown   -
     A                                              '
     A            DDERR        076A  O 24  2OVRDTA
     A                                      DSPATR(HI)
     A          R SUBMESS
     A                                      PUTOVR
     A                                 23  2'F3=Exit, F12=Previous, Rollup, -
     A                                      Rolldown'
     A            DDERR        076A  O 24  2OVRDTA
     A                                      DSPATR(HI)