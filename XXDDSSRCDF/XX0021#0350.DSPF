      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Currencies window')                           *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      ***DSPF/SD0021#0*-*Currencies*window*****************************         EML110
      *  DSPF/XX0021#0 - Currencies window                            *         EML110
      *                                                               *
      ***(c)*Misys*International*Banking*Systems*Ltd.*2005*************         EML110
      *  (c) Finastra International Limited 2018                      *         EML110
      *                                                               *
      *  Last Amend No. EML110             Date 20Jan20               *
      *  Prev Amend No. TFJ011  *CREATE    Date 19Jan07               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  EML110 - Account Balance Pooling. Upgrade to FBM 2.1         *
      *  TFJ011 - Account Pooling                                     *
      *                                                               *
      *****************************************************************
     A                                      PRINT
     A                                      CA03(03 'Exit')
     A  45                                  CF05(05 'Reset')
     A                                      CF12(12 'Previous')
      ** Start of EML110
     A********* R SD0021F0                  TEXT('(c) Misys International +
     A*********                             Banking Systems Ltd. +
     A*********                             2005')
     A          R XX0021F0                  TEXT('(c) Finastra Internation+
     A                                      al Limited 2018')
      ** End of EML110
     A                                      WINDOW(&SWROW &SWCOL 8 35 *NOMSGLIN)
     A                                      USRRSTDSP
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
     A            STITL         30A  O  1  1OVRDTA
      *----------------------------------------------------------------
     A                                  3  1'Account code     '
     A                                      OVRATR
     A            SACOD         10A  B  3 20OVRDTA
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N50N31                               DSPATR(UL)
     A  50                                  DSPATR(PR)
     A                                  4  1'Account seq. No. '
     A                                      OVRATR
     A            SACSQ          2A  B  4 20OVRDTA
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N50N32                               DSPATR(UL)
     A  50                                  DSPATR(PR)
     A                                  5  1'Branch code      '
     A                                      OVRATR
     A            SBRCA          3A  B  5 20OVRDTA
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N50N33                               DSPATR(UL)
     A  50                                  DSPATR(PR)
      *----------------------------------------------------------------
      *    To reduce the window size you may wish to split the command
      *    key display over two lines.
     A                                  7  1'F3=End'
     A  45                              7  8'F5=Refresh'
     A                                  7 20'F12=Previous'
     A*=========================================================================
      *    Dummy record to prevent full-screen display being cleared
     A          R ASSUMEF
     A                                      KEEP
     A                                      ASSUME
     A                                      OVERLAY
     A                                      PUTOVR
     A                                  1  3' '
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(8)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
      ** Start of EML110
     A************************************  WINDOW(SD0021F0)
     A                                      WINDOW(XX0021F0)
      ** End of EML110
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
