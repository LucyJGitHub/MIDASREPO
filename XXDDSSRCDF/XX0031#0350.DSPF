      *****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Account Code Extension')                      *
      *****************************************************************
      *                                                               *
      *  Midas - SD Module                                            *
      *                                                               *
      *  DSPF/XX0031#0 - Account Code Extension                       *
      *                  (window added to SD0031R and SD0031E)        *
      *                                                               *
      *  (C) Copyright Finastra International Limited 2019            *
      *                                                               *
      *  Last Amend No. LUC117  *Create    Date 13Feb19               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC117 - Journal Entry changes (Upgrade S21049 to FBM 2.1)   *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(*DS4)
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A N55N56                               CF05(05 'Reset.')
     A  55                                  CF10(10 'Delete.')
     A                                      CF12(12 'Previous.')
     A*.........................................................................
     A          R WINDOWF
     A                                      WINDOW(&SWROW &SWCOL 7 72 *NOMSGLIN-
     A                                       *NORSTCSR)
     A                                      TEXT('(c) Copyright Finastra Intern-
     A                                      ational Limited 2019')
     A                                      USRRSTDSP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      KEEP
     A            ##PGM         10A  H
     A            #1LCD          5Y 0H
     A            #1CHTP         1A  H
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
     A                                  1  1'XX0031'
     A            STITL         40A  O  1 15OVRDTA
     A            #DATE          7A  O  1 62OVRDTA
     A            ##TME          6Y 0O  2 61TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A            SFOOT         31A  O  7  1OVRDTA
     A                                  4  9'Book Code Mandatory?'
     A            #1BKYN         1   B  4 38
     A N32N56N55
     AO 55
     AO 56                                  DSPATR(HI)
     A N32N56N55                            DSPATR(UL)
     A  32N56N55                            DSPATR(RI PC)
     A  55
     AO 56                                  DSPATR(PR)
     A N55N56                               OVRDTA
     A                                  4 42'(Y/N)'
     A*=========================================================================
      *    Dummy record to prevent full-screen display being cleared
     A          R ASSUMEF
     A                                      ASSUME
     A                                  1  3' '
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
