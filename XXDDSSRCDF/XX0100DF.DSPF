      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD TI Transaction Types Maintenance')            *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Dat Module                                  *
      *                                                               *
      ***DSPF/SDT010DF*-*TI*Transaction*Types*Maintenance**************                       DAI102
      *  DSPF/XX0100DF - TI Transaction Types Maintenance             *                       DAI102
      *                                                               *
      ***(C)*Copyright*Misys*International*Banking*Systems*Ltd*2002****                       DAI102
      *  (c) Finastra International Limited 2019                      *                       DAI102
      *                                                               *
      *  Last Amend No. DAI102             Date 25Jun19               *
      *  Prev Amend No. DTA001 *Create     Date 03Nov02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  DAI102 - TI Posting Narrative Enhancement. Upgrade to        *
      *           FBM 2.1                                             *
      *  DTA001 - TI Posting Narrative Enhancement                    *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      VLDCMDKEY(85)
     A                                      DSPSIZ(24 80 *DS3)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
      ** Start of DAI102
     A**********R SDT010F0                  TEXT('(C) Misys International +
     A**********                            Banking Systems Ltd. 2002')
     A          R SDT010F0                  TEXT('(C) Finastra Internatio +
     A                                      nal Limited 2019')
      ** End of DAI102
     A                                      CF03(03 'to exit')
     A N52                                  CF05(05 'to refresh')
     A  53                                  CF10(10 'to delete')
     A                                      CF12(12 'to previous scrn')
     A                                      OVERLAY
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'XX000100'
     A                                  1 27'TI TRANSACTION TYPE +
     A                                       MAINTENANCE'
     A                                      DSPATR(HI UL)
     A                                  1 67'DATE'
     A            SRUNA     R        O  1 72REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 34'STANDING DATA'
     A                                      DSPATR(HI UL)
     A                                  2 67'TIME'
     A                                  2 72TIME
      *
     A                                  6  3'Transaction Type.....:'
     A            STTYP          3A  B  6 26
     A N50                                  DSPATR(UL)
     A N20                                  DSPATR(HI)
     A N50 20                               DSPATR(RI PC)
     A  50                                  DSPATR(PR)
      *
     A                                  8  3'Description..........:'
     A            STTDS         30A  B  8 26
     A N50                                  DSPATR(UL)
     A N21                                  DSPATR(HI)
     A N50 21                               DSPATR(RI PC)
     A  50                                  DSPATR(PR)
     A                                      CHECK(LC)
      *
     A                                 10  3'Enhanced Posting Narr:'
     A            SEPNI          1A  B 10 26
     A N50                                  DSPATR(UL)
     A N22                                  DSPATR(HI)
     A N50 22                               DSPATR(RI PC)
     A  50                                  DSPATR(PR)
      *
     A                                 22  3'F3=Exit'
     A N52                             22 12'F5=Refresh'
     A                                 22 24'F12=Key Screen'
     A  53                             22 48'F10=Confirm Delete'
      *
      ** Subfile Processing
      *
     A          R SDT010S0                  SFL
     A  54                                  SFLNXTCHG
      *
     A            SACTN          1A  B  7  3
     A  32                                  DSPATR(ND)
     A  32                                  DSPATR(PR)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI PC)
      *
     A            STTYP          3A  O  7  9DSPATR(HI)
     A            STTDS         30A  O  7 17DSPATR(HI)
     A            SEPNI          1A  O  7 54DSPATR(HI)
      *
     A          R SDT010C0                  SFLCTL(SDT010S0)
     A                                      CF03(03 'to exit')
     A                                      CF05(05 'to refresh')
     A                                      CF09(09 'to add')
     A                                      OVERLAY
     A N10                                  SFLDSP
     A N10                                  SFLDSPCTL
     A  10                                  SFLCLR
     A  65                                  SFLEND
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'XX000100'
     A                                  1 27'TI TRANSACTION TYPE +
     A                                       MAINTENANCE'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE'
     A            SRUNA     R        O  1 72REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME'
     A                                  2 72TIME
      *
     A                                  4  7'TI Tran'
     A                                  4 17'TI Transaction Type'
     A                                  4 50'Enh.Post.'
      *
     A                                  5  3'?'
     A                                  5  7'Type'
     A                                  5 17'Description'
     A                                  5 50'Narrative'
      *
     A          R SDT010F1                  OVERLAY
     A                                 22  3'A=Amend  D=Delete  E=Enquire'
     A                                 22 33'F3=Main Menu  F5=Refresh'
     A                                 22 59'F9=Go to ''Add'' Mode'
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
