      *****************************************************************
/*STD *  DSPFBASE                                                     *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      ***DSPF/SD0181#0 - Country Codes Maintenance Window             *                       LUC109
      *  DSPF/XX0181#0 - Country Codes Maintenance Window             *                       LUC109
      *                                                               *
      ***(C)*Copyright*Midas-Kapiti*International*Ltd.*1998************                       LUC109
      *  (c) Misys International Banking Systems Ltd. 2001            *                       LUC109
      *                                                               *
      *  Last Amend No. LUC109             Date 23Aug16               *
      *  Prev Amend No. MUI009             Date 13Feb04               *
      *                 MMI043  *CREATE    Date 24Nov98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC109 - Head Office Reporting (Upgrade to FB Midas 2.1)     *
      *  MUI009 - Upgrade to R4.01: remove Help.                      *
      *  MMI043 - Interface to Head Office Reporting                  *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A*** MUI009 begin
     A**********                            HELP
     A**********                            HLPDOC(H020@000 SDINS ABSENG/DBAR2)
     A*** MUI009 end
     A                                      CF03(03 'Exit')
     A                                      CF12(12 'Previous')
     A*.........................................................................
     A**********R SD0181F0                  TEXT('(C) Copyright MKI +                         LUC109
     A          R XX0181F0                  TEXT('(C) Copyright MKI +                         LUC109
     A                                      Limited 1988, 1997')
     A                                      WINDOW(&SWROW &SWCOL 10 70)
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A*** MUI009 begin
     A**********H                           HLPDOC(H020@005 SDINS ABSENG/DBAR2)
     A**********                            HLPARA(04 01 04 17)
     A*** MUI009 end
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
     A            STITL         38   O  1  2DSPATR(HI)
      *
     A                                  3  2'H.O.Geographical Area-
     A                                      .........'
     A                                  3 45'(1=America, 2=Europe,'
     A                                  4 45' 3=Pacific,'
     A                                  5 45' 4=Middle East & Africa)'
     A            SGEOA          1   B  3 35OVRDTA
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(HI)
     A N39                                  DSPATR(UL)
     A  39                                  DSPATR(PR)
      *
     A                                  6  2'Risk Category Code -
     A                                      ...........'
     A                                  6 45'(1, 2, 3, 4, 5, X)'
     A            SRCAT          1   B  6 35OVRDTA
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(HI)
     A N39                                  DSPATR(UL)
     A  39                                  DSPATR(PR)
      *
     A                                  8  2'F3=Exit  F12=Previous'
     A*.........................................................................
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program Messages')
     A                                      SFLMSGRCD(09)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*.........................................................................
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program Messages')
     A************                          WINDOW(SD0181F0)                                  LUC109
     A                                      WINDOW(XX0181F0)                                  LUC109
     A                                      SFLSIZ(03)
     A                                      SFLPAG(01)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  55
     AON55                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*.........................................................................
     A          R DUMMY
     A                                      TEXT('Dummy Format')
     A                                      ASSUME
     A                                  1  3' '
     A*.........................................................................
