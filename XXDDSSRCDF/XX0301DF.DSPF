      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Midas/OPICS i/face ICD maint')                *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      ***DSPF/SD9481R#*-*Midas-OPICS*Interface*ICD*Maintenance*********                       JMI111
      *  DSPF/XX0301DF - Midas-OPICS Interface ICD Maintenance        *                       JMI111
      *                                                               *
      ***(c)*Misys*International*Banking*Systems*Ltd.*2007*************                       JMI111
      *  (c) Finastra International Limited 2018                      *                       JMI111
      *                                                               *
      *  Last Amend No. JMI111             Date 16May18               *
      *  Prev Amend No. CGL019  *CREATE    Date 20Apr07               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  JMI111 - Midas-OPICS Interface. Upgrade to FBM 2.1           *
      *  CGL019 - Midas-OPICS Interface                               *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
      *
      ** Start of JMI111
     A**********R #RCDKEY                   TEXT('(c) Misys International +
     A**********                            Banking Systems Ltd. +
     A**********                            2007')
      *
     A          R #RCDKEY                   TEXT('(c) Finastra Internation +
     A                                      al Limited 2018')
      ** End of JMI111
      *
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1INKY')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
      *
      ** Start of JMI111
     A**********                        1 13'SD9481R1'
     A                                  1 13'XX0301R1'
      ** End of JMI111
      *
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 24'Edit Midas/OPICS I/face ICD KEY S-
     A                                      CREEN'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #PWRTC         5   H      TEXT('Work Return code')
     A                                  4  2'Midas/OPICS Interface Key...:'
     A            #1INKY        10   B  4 32TEXT('Midas/OP Interface Key')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Exit'
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(32 'Invalid: #1DEPT')
     A                                      SETOF(33 'Invalid: #1TRTP')
     A                                      SETOF(34 'Invalid: #1PRFC')
     A                                      SETOF(35 'Invalid: #1BOOK')
     A                                      SETOF(38 'Invalid: #DLRBS')
     A                                      SETOF(39 'Invalid: #DURBS')
     A                                      SETOF(47 'Invalid: #1BALA')
      *
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
      *
      ** Start of JMI111
     A**********                        1 13'SD9481R2'
     A                                  1 13'XX0301R2'
      ** End of JMI111
      *
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 24'Midas-OPICS Interface ICD Maintena+
     A                                      nce'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #PWRTC         5   H      TEXT('Work Return code')
     A            #1INKY        10   H      TEXT('Midas/OP Interface Key')
     A            #1LRBN         3  0H      TEXT('Lower Range Batch Number')
     A            #1URBN         3  0H      TEXT('Upper Range Batch Number')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1LCD          5  0H      TEXT('Last Change Date')
      *
     A                                  5 22'OPICS Department...............:'
     A            #1DEPT         3   B  5 55TEXT('Department')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A                                  7 22'OPICS Profit Centre............:'
     A  79                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1PRFC         4   B  7 55TEXT('Profit centre code  Midas')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A  79N34                               DSPATR(ND)
     A N79N34                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  9 22'OPICS Book.....................:'
     A            #1BOOK         2   B  9 55TEXT('Book')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 11 22'Lower Range Batch Number.......:'
     A            #DLRBS         3   B 11 55TEXT('Lower Rng Batch Nbr (scr)')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 12 22'Upper Range Batch Number.......:'
     A            #DURBS         3   B 12 55TEXT('Upper Rng Batch Nbr (scr)')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A                                 14 22'OPICS Transaction Type.........:'
     A N78                                  DSPATR(ND)
     A            #1TRTP         5   B 14 55TEXT('Transaction Type (OP)')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A N78                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N78N33                               DSPATR(ND)
     A  78N33                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 15 22'Balancing Account Code.........:'
     A            #1BALA        10   B 15 55TEXT('Balancing Account Code')
     A N25                                  OVRDTA
     A  47                                  DSPATR(RI PC)
     A N47                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Exit'
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
