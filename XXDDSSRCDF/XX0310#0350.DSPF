      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas XX SD Book Code Window Display File')            *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  XX0310#0 - Additional Book Code Details Display File         *
      *                                                               *
      *  (c) Finastra International Banking Systems Ltd. 2017         *
      *                                                               *
      *  Last Amend No. MD052141           Date 17Oct18               *
      *  Prev Amend No. DUG501  *CREATE    Date 23Aug17               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD052141 - Recompile with multi-language system *NO          *
      *  DUG501 - Accounts split for Realised P&L and Account Seq     *
      *           by Book                                             *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CA03(03 'Exit')
     A  35                                  CF05(05 'Reset')
     A  40                                  CF10(10 'Delete')
     A                                      CF12(12 'Previous')
     A          R XX0310F0                  TEXT('(C) Finastra Interna+
     A                                      tional Baking Systems Ltd. 2017')
     A                                      WINDOW(&SWROW &SWCOL 7 58)
     A                                      WDWBORDER((*COLOR WHT) (*DSPATR HI)-
     A                                       (*CHAR '...:::.:'))
     A                                      KEEP
     A                                      OVERLAY
     A            PGMNAM        10A  H
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
     A            STITL         35A  O  1  1DSPATR(HI)
      *----------------------------------------------------------------
      * Replace fields below with required fields for new window
     A                                  3  2'Default Account Sequence Number +
                                             ..................:'
     A            SADACS         2   B  3 55
     A N20                                  DSPATR(UL)
     A  20
     AON50N20                               DSPATR(HI)
     A  50N20                               DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A                                      CHECK(FE)
      *----------------------------------------------------------------
      *    To reduce the window size you may wish to split the command
      *    key display over two lines.
     A                                  5  2'F3=Exit'
     A  35                              5 11'F5=Refresh'
     A                                  5 23'F12=Previous'
     A*=========================================================================
      *    Dummy record to prevent full-screen display being cleared
     A          R XX0310F1
     A                                      KEEP
     A                                      ASSUME
     A                                      OVERLAY
     A                                      PUTOVR
     A            DUMMYF         1A  O  1 80DSPATR(ND)
     A*=========================================================================
     A          R XX0310F2                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(6)
     A            MSGKEY                    SFLMSGKEY
     A            PGMNAM                    SFLPGMQ
     A*=========================================================================
     A          R XX0310F3                  TEXT('Program messages')
     A                                      WINDOW(XX0310F0)
     A                                      SFLCTL(XX0310F2)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            PGMNAM                    SFLPGMQ