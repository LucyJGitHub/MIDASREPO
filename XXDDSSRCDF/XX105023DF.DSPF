      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Bulk Amend/Auth of Position Settlements')     *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      ***DSPF/SEH023DF*-*Bulk*Amendment/Authorisation*of*Position******                  HUT105
      *  DSPF/XX105023DF -  Bulk Amendment/Authorisation of Position  *                  HUT105
      *                     Settlements                               *
      *                                                               *
      ***(c)*Misys*International*Banking*Systems*Ltd.*2008*************                  HUT105
      *  (c) Finastra International Systems Ltd. 2020                 *                  HUT105
      *                                                               *
      *  Last Amend No. HUT105             Date 06Apr20               *
      *  Prev Amend No. HUT011  *CREATE    Date 12May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  HUT105 - Bulk Position Settlement Authorisation              *
      *           (Upgrade HUT011 to FM2.1 SP22)                      *
      *  HUT011 - Bulk Position Settlement Authorisation              *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      CF03(03 'Exit')
     A                                      CF12(12 'Cancel')
     A                                      CF08(08 'Unauth Rpt')
     A                                      CF09(09 'Auth Today Rpt')
     A                                      CF10(10 'Authorise')
      *
      * -------------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
      *
      ** Start of HUT105
     A**********R SEH023F1
     A**********                            TEXT('(c) Misys International Banki-
     A**********                            ng Systems Ltd. 2008')
     A          R XX105023F1
     A                                      TEXT('(c) Finastra International Sy-
     A                                      stems Ltd. 2020')
      ** End of HUT105
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            ##USR         10A  O  1  2TEXT('User ID')
     A                                      OVRDTA
      ** Start of HUT105
     A**********                        1 13'SEH023'
     A                                  1 13'XX105023'
      ** End of HUT105
     A                                  2 16'Bulk Amendment/Authorisation of Po-
     A                                      sition Settlements'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            ##MRDT         7A  O  1 74TEXT('Midas Run Date')
     A                                      OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A                                      OVRDTA
     A                                  2 67'TIME'
     A                                  2 73TIME
     A                                      EDTWRD('  :  :  ')
     A            SESTAT        12A  O  5 55OVRDTA
     A N40                                  DSPATR(ND)
     A                                  5  5'Action....:'
     A            SEACTN         1A  B  5 20OVRDTA
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33                                  DSPATR(HI)
     A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                  7  5'Security..:'
     A            SESESN        10A  B  7 20OVRDTA
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N34                                  DSPATR(HI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                  8  5'Due Date..:'
     A                                  8 28'(DDMMYY)'
     A            SEDUDT         6A  B  8 20OVRDTA
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N35                                  DSPATR(HI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                  9  5'Event Type:'
     A            SEEVTP         2A  B  9 20OVRDTA
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A N36                                  DSPATR(HI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A            SEVLTX        11   O 11  5OVRDTA
     A N38                                  DSPATR(ND)
     A            SEFRMT         8   O 11 28OVRDTA
     A N38                                  DSPATR(ND)
     A            SEVLDT         6A  B 11 20OVRDTA
     A N38                                  DSPATR(ND)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A N37                                  DSPATR(HI)
     A N32                                  DSPATR(UL)
     A N38
     AO 32                                  DSPATR(PR)
     A            SEAUTH        60   O 16  5OVRDTA
     A N39                                  DSPATR(ND)
     A                                 23  2'F3=Exit  F8=Unauthorised Pos Sett -
     A                                      Rpt'
     A                                 23 41'F9=Authorised Pos Sett Rpt'
     A                                 23 69'F12=Cancel'
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
      ** Start of HUT105
     A**********R SEH023F2                  TEXT('Program messages')
     A          R XX105023F2                TEXT('Program messages')
      ** End of HUT105
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
      ** Start of HUT105
     A**********R SEH023F3                  TEXT('Program messages')
     A**********                            SFLCTL(SEH023F2)
     A          R XX105023F3                TEXT('Program messages')
     A                                      SFLCTL(XX105023F2)
      ** End of HUT105
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
