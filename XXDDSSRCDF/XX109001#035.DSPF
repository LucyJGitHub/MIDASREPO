     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('G-AML ICD Maintenance Display File')                   *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Standing Data Module                                 *
     A*                                                               *
     A***DSPF/SDJ107#*-*G-AML ICD Maintenance Display File*************
     A*  DSPF/XX109001# - G-AML ICD Maintenance Display File          *
     A*                                                               *
     A*  (c) Misys International Banking Systems Ltd. 2008            *
     A*                                                               *
     A*  Last Amend No. JMI109             Date 21May18               *
     A*  Prev Amend No. JMI026I02          Date 15Oct08               *
     A*                 JMI026  *CREATE    Date 30Apr08               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  JMI109 - G-AML PreProcessor (Upgrade to FB Midas)            *
     A*  JMI026I02 - Increased length of SDIR, SUSER and SPWORD       *
     A*  JMI026 - G-AML PreProcessor                                  *
     A*                                                               *
     A*****************************************************************
     A*
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A*=========================================================================
     A*
     A          R #RCDKEY
     A*
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2008')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10A  O  1  2TEXT('*USER')
     A                                      OVRDTA
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A************                      1 13'SDJ107'                                          JMI109
     A                                  1 13'XX109001'                                        JMI109
     A            ##MRDT         7A  O  1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A                                      OVRDTA
     A                                  2 23'         G-AML ICD '
     A                                      DSPATR(HI)
     A                                      OVRATR
     A N87                              2 43'Maintenance'
     A                                      DSPATR(HI)
     A                                      OVRATR
     A  87                              2 43'Enquiry'
     A                                      DSPATR(HI)
     A                                      OVRATR
     A            ##TME          6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('Details Account Pooling ICD')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(87 'Protect fields')
     A                                      VLDCMDKEY(88 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N87                                  CA05(05 'Refresh.')
     A                                      CA12(12 'Key screen.')
     A                                      SETOF(29 'Invalid: SPDIR')
     A                                      SETOF(31 'Invalid: SPSIP')
     A                                      SETOF(32 'Invalid: SPSUS')
     A                                      SETOF(33 'Invalid: SPSPS')
     A*-------------------------------------------------------------------------
     A                                  4  5'G-AML server'
     A            SIPS          15A  B  4 20TEXT('G-AML server IP address')
     A                                      OVRDTA
     A  87                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31N87                               DSPATR(UL)
     A N31                                  DSPATR(HI)
     A**********                        4 37'G-AML directory'                              JMI026I02
     A                                  5  5'G-AML directory'                              JMI026I02
     A**********  SDIR          20A  B  4 55TEXT('G-AML directory')                        JMI026I02
     A            SDIR          70A  B  6  5TEXT('G-AML directory')                        JMI026I02
     A                                      OVRDTA
     A  87                                  DSPATR(PR)
     A  29                                  DSPATR(RI)
     A  29                                  DSPATR(PC)
     A N29N87                               DSPATR(UL)
     A N29                                  DSPATR(HI)
     A**********                        5  9'User ID'                                      JMI026I02
     A                                  7  5'User ID'                                      JMI026I02
     A**********  SUSER          7   B  5 18TEXT('User ID')                                JMI026I02
     A            SUSER         30   B  7 14TEXT('User ID')                                JMI026I02
     A                                      OVRDTA
     A  87                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32N87                               DSPATR(UL)
     A N32                                  DSPATR(HI)
     A**********                        5 29'Password'                                     JMI026I02
     A                                  8  5'Password'                                     JMI026I02
     A**********  SPWORD         8   B  5 40TEXT('G-AML Server Password')                  JMI026I02
     A            SPWORD        30   B  8 15TEXT('G-AML Server Password')                  JMI026I02
     A                                      OVRDTA
     A  87                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33N87                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A**********                        6  9'Operating Dept.'                              JMI026I02
     A                                  9  5'Operating Dept.'                              JMI026I02
     A**********  SOPDPT        10   B  6 25TEXT('Operating Dept.')
     A            SOPDPT        10   B  9 22TEXT('Operating Dept.')                        JMI026I02
     A                                      OVRDTA
     A  87                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N34N87                               DSPATR(UL)
     A N34                                  DSPATR(HI)
      *
      *=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 24  2'F3=Main Menu'
     A N87                             24 17'F5=Refresh'
     A                                 24 30'F12=Previous'
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(23)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(55)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
