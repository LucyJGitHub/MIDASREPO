     A*%%TS  SD  20080502  095909  JAVIERJ2    REL-V5R3M0  5722-WDS
     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Customer Types Display File')                 *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Standing Data Module                                 *
     A*                                                               *
     A***DSPF/SDJ106M#*-*Midas SD Customer Types Display File**********
     A*  DSPF/XX109002M# - Midas SD Customer Types Display File       *
     A*                                                               *
     A*  (c) Misys International Banking Systems Ltd. 2008            *
     A*                                                               *
     A*  Last Amend No. JMI109             Date 21May18               *
     A*  Prev Amend No. JMI026  *CREATE    Date 30Apr08               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  JMI109 - G-AML Pre-Processor (Upgrade to FB Midas)           *
     A*  JMI026 - G-AML Pre-Processor                                 *
     A*                                                               *
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A*=========================================================================
     A          R #SFLRCD                   SFL
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2008')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(32 'Invalid: #1SEL')
     A                                      INDTXT(33 'Invalid: S1MTYP')
     A                                      INDTXT(34 'Invalid: S1GTYP')
     A                                      INDTXT(35 'Invalid: S1CTDE')
     A  84                                  SFLNXTCHG
     A            #1DBRC        46A  H      TEXT('Existing record fields')
     A            #1LCD          5S 0H      TEXT('Last Change Date')
     A            #1TYLC         1A  H      TEXT('Type of Last Change')
     A            #1SEL          1A  B  8 11VALUES('D')
     A                                      CHECK(AB)
     A  78                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A  78N32                               DSPATR(ND)
     A N78N32                               DSPATR(UL)
     A N78N32                               DSPATR(HI)
     A            S1MTYP         1A  B  8 20TEXT('Midas Customer Type')
     A  88                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N88N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A            S1GTYP        11A  B  8 27TEXT('G-AML Customer Type')
     A                                      CHECK(LC)
     A  87                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N87N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
     A            S1CTDE        30A  B  8 43TEXT('Customer Type Description')
     A  87                                  DSPATR(PR)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N87N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL                   SFLCTL(#SFLRCD)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('Subfile control.')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25N80 81                            SFLDSP
     A N80 81 82                            SFLEND
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
     A                                      CF09(09 'Change mode.')
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: S2MTYP')
     A*.........................................................................
     A            ##SFRC         4S 0H      SFLRCDNBR(CURSOR)
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A************                      1 13'SDJ106M#'                                        JMI109
     A                                  1 13'XX109002M#'                                      JMI109
     A            ##MRDT         7A  O  1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 24'G-AML Customer Types Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4 18'Midas'
     A                                  4 27'G-AML'
     A                                  5 18'Type'
     A                                  5 27'Type'
     A                                  5 43'Description'
     A N89        S2MTYP         1A  B  6 20TEXT('Local Institution Code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
