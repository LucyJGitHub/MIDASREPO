     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD G-AML Activity Type Display File')            *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Standing Data Module                                 *
     A*                                                               *
     A***DSPF/SDJ108#*-*Midas SD G-AML Activity Type Display File******
     A*  DSPF/XX109003# - Midas SD G-AML Activity Type Display File   *
     A*                                                               *
     A*  (c) Misys International Banking Systems Ltd. 2008            *
     A*                                                               *
     A*  Last Amend No. JMI109             Date 21May18               *
     A*  Prev Amend No. JMI026  *CREATE    Date 30Apr08               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  JMI109 - G-AML Pre-Processor (Upgrade to FB Midas)           *
     A*  JMI026 - G-AML Pre-Processor                                 *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A*=========================================================================
     A          R #SFLRCD                   SFL
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2008')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(32 'Invalid: #1SEL')
     A                                      INDTXT(33 'Invalid: #GMOD')
     A                                      INDTXT(34 'Invalid: #GTYP')
     A                                      INDTXT(35 'Invalid: #GSTYP')
     A                                      INDTXT(36 'Invalid: #GSRNG')
     A                                      INDTXT(37 'Invalid: #GERNG')
     A                                      INDTXT(38 'Invalid: #GASEQ')
     A                                      INDTXT(39 'Invalid: #GATYP')
     A  84                                  SFLNXTCHG
     A            #1DBRC        50A  H      TEXT('Existing record fields')
     A            #1LCD          5S 0H      TEXT('Last Change Date')
     A            #1TYLC         1A  H      TEXT('Type of Last Change')
     A            #1SEL          1A  B  8  3VALUES('D')
     A                                      CHECK(AB)
     A  78                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A  78N32                               DSPATR(ND)
     A N78N32                               DSPATR(UL)
     A N78N32                               DSPATR(HI)
     A            #GMOD          2A  B  8  7TEXT('Midas Module')
     A  88                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N88                                  DSPATR(UL)
     A N33                                  DSPATR(HI)
     A            #GTYP          2A  B  8 13TEXT('Midas Type')
     A  88                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N88N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
     A            #GSTYP         2A  B  8 19TEXT('Midas SubType')
     A  88                                  DSPATR(PR)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N88N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
     A            #GSRNG         3A  B  8 23TEXT('Start Range')
     A  88                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A N88N36                               DSPATR(UL)
     A N36                                  DSPATR(HI)
     A            #GERNG         3A  B  8 29TEXT('End Range')
     A  88                                  DSPATR(PR)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A N88N37                               DSPATR(UL)
     A N37                                  DSPATR(HI)
     A            #GASEQ         3A  B  8 36TEXT('G-AML Activity Seq')
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A N38                                  DSPATR(UL)
     A N38                                  DSPATR(HI)
     A            #GATYP         4A  B  8 43TEXT('G-AML Activity Type')
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A N39                                  DSPATR(UL)
     A N39                                  DSPATR(HI)
     A            #GADSC        30A  B  8 49TEXT('Activity Description')
     A                                      CHECK(LC)
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL                   SFLCTL(#SFLRCD)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('Subfile control.')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25N80 81                            SFLDSP
     A N80 81 82                            SFLEND
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
     A                                      CF09(09 'Change mode.')
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##SFRC         4S 0H      SFLRCDNBR(CURSOR)
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A************                      1 13'SDJ108#'                                         JMI109
     A                                  1 13'XX109003#'                                       JMI109
     A            ##MRDT         7A  O  1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 24'G-AML Activity Types Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A N89        @GMOD          2A  B  6  7TEXT('Module')
     A N25                                  OVRDTA
     A                                      DSPATR(HI UL)
     A N25                                  OVRATR
     A N89        @GTYP          2A  B  6 13TEXT('Midas Type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI UL)
     A N25                                  OVRATR
     A N89        @GSTYP         2A  B  6 19TEXT('Midas SubType')
     A N25                                  OVRDTA
     A                                      DSPATR(HI UL)
     A N25                                  OVRATR
     A N89        @GSRNG         3  0B  6 23TEXT('Start Range')
     A N25                                  OVRDTA
     A                                      DSPATR(HI UL)
     A N25                                  OVRATR
     A N89        @GERNG         3  0B  6 29TEXT('End Range')
     A N25                                  OVRDTA
     A                                      DSPATR(HI UL)
     A N25                                  OVRATR
     A                                  5  5'Module'
     A                                  4 12'Midas'
     A                                  5 12'Type'
     A                                  4 18'Sub-'
     A                                  5 18'Type'
     A                                  4 23'Batch Range'
     A                                  5 23'From  To'
     A                                  5 36'Seq    Type'
     A                                  4 36'G-AML  Act'
     A                                  5 49'Description'
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
