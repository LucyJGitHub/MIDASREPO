     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas XX Ad-hoc Date Rerun Display Prompt')            *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Standing Data Module                                 *
     A*                                                               *
     A***DSPF/SCJ001#*-*AD-HOC*DATE*RE-RUN*PROMPT**********************                       JMI109
     A*  DSPF/XX90101# - AD-HOC DATE RE-RUN PROMPT                    *                       JMI109
     A*                                                               *
     A*  (c) Misys International Banking Systems Ltd. 2008            *
     A*                                                               *
     A*  Last Amend No. JMI109             Date 05Jun18               *
     A*  Prev Amend No. JMI026  *CREATE    Date 31Aug08               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  JMI109 - G-AML PreProcessor (Upgrade to FB Midas)            *
     A*  JMI026 - G-AML Pre-Processor                                 *
     A*                                                               *
     A*****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      KEEP
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* COMMAND KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #DFDTE')
     A                                      SETOF(32 'Invalid: #DTDTE')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
      **Start of change JMI109
     A**********                        1 13'SCJ009#'                                         JMI109
     A                                  1 13'XX10901#'
      **End of  change JMI109
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  2 31'Ad-hoc Date Re-run Pre-processor'
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  8 26'Extract Date..'
     A                                      DSPATR(UL)
     A            #DFDTE         6   B  8 42TEXT('From date')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
