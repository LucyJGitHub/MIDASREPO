      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD GCMS interface ICD enquiry')                  *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *                  JMI113
      ***DSPF/SDJ002E# - GCMS Interface ICD Enquiry.                  *                  JMI113
      *  DSPF/XX1131E# - GCMS Interface ICD Enquiry.                  *
      *                                                               *
      ***(c)*Misys*International*Banking*Systems*Ltd.*2007*************                  JMI113
      *  (c) Finastra International Limited 2018                      *                  JMI113
      *                                                               *
      *                                                               *
      *  Last Amend No. JMI113             Date 16May18               *
      *  Prev Amend No. JMI019             Date 16Feb07               *
      *                 JMI019B            Date 28Apr05               *
      *                 JMI019  *CREATE    Date 03Jun03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  JMI113 - Midas GCMS Interface. Upgrade to FBM 2.1            *
      *  JMI019 - Phase III MT999 Message Processing                  *
      *  JMI019B - GCMS. File Name and Confirmation File              *
      *  JMI019 - Midas GCMS Interface                                *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
      ** Start of JMI113
     A**********R #RCDKEY                   TEXT('(c) Misys International +
     A**********                            Banking Systems Ltd. +
     A**********                            2007')
     A          R #RCDKEY                   TEXT('(c) Finastra Internation+
     A                                      al Limited 2018')
      ** End of JMI113
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
      * Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
      * SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1GKEY')
      *.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
      ** Start of JMI113
     A**********                        1 13'SDJ002E1'
     A                                  1 13'XX1131E1'
      ** End of JMI113
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 29'GCMS Interface ICD Enqy'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  2'GCMS Interface ICD Key...:'
     A            #1GKEY        10   B  4 31TEXT('GCMS Interface ICD Key')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Exit'
      *=========================================================================
     A          R #RCDDTL1                  TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
      * Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
      * SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
      *.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
      ** Start of JMI113
     A**********                        1 13'SDJ002E2'
     A                                  1 13'XX1131E2'
      ** End of JMI113
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 21'GCMS Customers Interface ICD -
     A                                      Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1GKEY        10   H      TEXT('GCMS Interface ICD Key')
     A            S0LCD          5  0H      TEXT('Last Change Date')
     A            S0TYLC         1   H      TEXT('Type of Last Change')
     A                                  4  2'Incoming Server Host name:'
     A            S0101H        30      4 29TEXT('Host name for GCMS Incoming +
     A                                      Server')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  5  2'Incoming Server User Name:'
     A            S0101U        20      5 29TEXT('Midas User name on GCMS +
     A                                      Incoming Server')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Incoming Server Password.:'
     A            S0101P        20      6 29TEXT('Password for Midas User +
     A                                      on GCMS Incoming server')
     A N25                                  OVRDTA
     A                                      DSPATR(ND)
     A                                  7  2'Incoming Datafile Path...:'
     A            S0101T        77      8  2TEXT('Incoming Server Datafile +
     A                                      Path')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      **Start of Change JMI019
     A                                  9  2'MT940/MT942 FTP Instructions:'
     A                                      DSPATR(UL)
      **End of Change JMI019
     A                                 10  2'Outgoing Server Host name:'
     A            S09NNH        30     10 29TEXT('Host name for GCMS Outgoing +
     A                                      Server')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 11  2'Outgoing Server User Name:'
     A            S09NNU        20     11 29TEXT('Midas User name on GCMS +
     A                                      Outgoing Server')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 12  2'Outgoing Server Password.:'
     A            S09NNP        20     12 29TEXT('Password for Midas User +
     A                                      on GCMS Outgoing Server')
     A N25                                  OVRDTA
     A                                      DSPATR(ND)
     A                                 13  2'Outgoing Datafile Path...:'
     A            S09NNT        77     14  2TEXT('Outgoing Server Datafile +
     A                                      Path')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #RCDDTL2                  TEXT('Details IAT/TPP')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
      *---------------------------------------------------------------------------------------
      ** Start of JMI019B change
     A                                 15  2'Outgoing Confirmation File Path..:'
     A            S09NNC        77     16  2TEXT('Outgoing Confirmation +
     A                                      File Path')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A**********                       15  2'Maximum No. of Retries..:'                      JMI019B
     A**********  S0MAXT         1  0  15 28TEXT('Maximum no. of re-tries')                  JMI019B
     A                                 17  2'Maximum No. of Re-tries.:'
     A            S0MAXT         1  0  17 28TEXT('Maximum no. of re-tries')
      ** End of JMI019B change
     A                                      EDTCDE(3)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      ** Start of JMI019B change
     A**********                       16  2'History Retention Period:'                      JMI019B
     A**********  S0HREP         2  0  16 28TEXT('History Retention Period')                 JMI019B
     A                                 17 62'History Reten:'
     A            S0HREP         2  0  17 77TEXT('History Retention Period')
      ** End of JMI019B change
     A                                      EDTCDE(3)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      ** Start of JMI019B change
     A**********                       17 36'MT942 Processing Frequency:'                    JMI019B
     A**********  S0942F         3Y 0  17 64EDTCDE(4)                                        JMI019B
     A                                 17 33'MT942 Processing Freq:'
     A            S0942F         3Y 0  17 56EDTCDE(3)
      ** End of JMI019B change
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      ** Start of JMI019B change
     A**********                       18 36'MT999 Processing Frequency:'                    JMI019B
     A**********  S0999F         3Y 0  18 64EDTCDE(4)                                        JMI019B
     A*N25******                            OVRDTA                                           JMI019B
     A**********                            DSPATR(HI)                                       JMI019B
      ** End of JMI019B change
      *
     A                                 18  2'MT101 Payments:'
     A                                      DSPATR(UL)
     A                                 19  4'Processing Frequency..:'
     A            S0PYPF         3Y 0  19 28EDTCDE(4)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 20  4'Message Retention Days:'
     A            S0PYMR         2Y 0  20 28EDTCDE(4)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 19 36'Outgoing Payment Sub-type.:'
     A            S0PYOP         2A    19 64
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 20 36'Incoming Payment Sub-type.:'
     A            S0PYIP         2A    20 64
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 21  4'Settlement Method.....:'
     A            S0PYSM         2A    21 28
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 21 36'Book code.................:'
     A            S0PYBC         2A    21 64
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A  52                             22  4'Profit Centre.........:'
     A            S0PYPC         4A    22 28
     A N52                                  DSPATR(ND)
     A N25 52                               OVRDTA
     A     52                               DSPATR(HI)
      *=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
      **Start of change JMI019
     A**********                       24  2'F3=Main Menu'                                    JMI019
     A                                 23  2'F3=Main Menu'
      **End of Change JMI019
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *=========================================================================
