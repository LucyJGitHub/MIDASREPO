      *****************************************************************
/*S*D ***DSPF**********************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD GCMS interface ICD maintenance')              *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      ***DSPF/SDJ002E#*-*GCMS*Interface*ICD*Maintenance.***************                  JMI113
      *  DSPF/XX1131R# - GCMS Interface ICD Maintenance.              *                  JMI113
      *                                                               *
      ***(c)*Misys*International*Banking*Systems*Ltd.*2007*************                  JMI113
      *  (c) Finastra International Limited 2018                      *                  JMI113
      *                                                               *
      *  Last Amend No. JMI113             Date 16May18               *
      *  Prev Amend No. JMI019             Date 16Feb07               *
      *                 JMI019B            Date 28Apr05               *
      *                 JMI019  *CREATE    Date 03Jun03               *
      *                                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  JMI113 - Midas GCMS Interface. Upgrade to FBM 2.1            *
      *  JMI019 - Phase III MT999 Message Processing                  *
      *  JMI019B - GCMS. File Name and Confirmation File              *
      *  JMI019 - Midas GCMS Interface                                *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *=========================================================================
      ** Start of JMI113
     A**********R #RCDKEY                   TEXT('(c) Misys International +
     A**********                            Banking Systems Ltd. +
     A**********                            2007')
     A          R #RCDKEY                   TEXT('(c) Finastra Internation+
     A                                      al 2018')
      ** End of JMI113
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
      * Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
      * SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #PWRTC')
     A                                      SETOF(32 'Invalid: #1GKEY')
      *.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
      ** Start of JMI113
     A**********                        1 13'SDJ002R1'
     A                                  1 13'XX1131R1'
      ** End of JMI113
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 29'GCMS Interface ICD Mnt'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  2'Work Return code......:'
     A            #PWRTC         5   B  4 26TEXT('Work Return code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  5  2'GCMS Interface ICD Key:'
     A            #1GKEY        10   B  5 26TEXT('GCMS Interface ICD Key')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
      **Start of Change JMI019
     A**********                       24  2'F3=Exit'                                         JMI019
     A                                 23  2'F3=Exit'
      **End of Change JMI019
     A            SERRM         65   H      TEXT('Error Message')
      *=========================================================================
     A          R #RCDDTL1                  TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
      * Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
      * SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(34 'Invalid: S0101H')
     A                                      SETOF(35 'Invalid: S0101U')
     A                                      SETOF(36 'Invalid: S0101P')
     A                                      SETOF(37 'Invalid: S0101T')
     A                                      SETOF(41 'Invalid: S09NNU')
     A                                      SETOF(42 'Invalid: S09NNT')
     A                                      SETOF(44 'Invalid: S09NNH')
     A                                      SETOF(45 'Invalid: S09NNP')
      *.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
      ** Start of JMI113
     A**********                        1 13'SDJ002R2'
     A                                  1 13'XX1131R2'
      ** End of JMI113
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 24'GCMS Customers Interface ICD -
     A                                      Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #PWRTC         5   H      TEXT('Work Return code')
     A            #1GKEY        10   H      TEXT('GCMS Interface ICD Key')
     A            S0LCD          5  0H      TEXT('Last Change Date')
     A            S0TYLC         1   H      TEXT('Type of Last Change')
     A                                  4  2'Incoming Server Host name:'
     A            S0101H        30   B  4 29TEXT('Host name for GCMS Incoming +
     A                                      Server')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  5  2'Incoming Server User Name:'
     A            S0101U        20   B  5 29TEXT('Midas User name on GCMS +
     A                                      Incoming Server')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  6  2'Incoming Server Password.:'
     A            S0101P        20   B  6 29TEXT('Password for Midas User +
     A                                      on GCMS Incoming server')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  36                                  DSPATR(PC)
     A                                      DSPATR(ND)
     A N25                                  OVRATR
     A                                  7  2'Incoming Datafile Path...:'
     A            S0101T        77   B  8  2TEXT('Incoming Server Datafile +
     A                                      Path')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(UL HI)
     A N25                                  OVRATR
      **Start of Change JMI019
     A                                  9  2'MT940/MT942 FTP Instructions:'
     A                                      DSPATR(UL)
      **End of Change JMI019
     A                                 10  2'Outgoing Server Host name:'
     A            S09NNH        30   B 10 29TEXT('Host name for GCMS Outgoing +
     A                                      Server')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  44                                  DSPATR(RI PC)
     A N44                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 11  2'Outgoing Server User Name:'
     A            S09NNU        20   B 11 29TEXT('Midas User name on GCMS +
     A                                      Outgoing Server')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 12  2'Outgoing Server Password.:'
     A            S09NNP        20   B 12 29TEXT('Password for Midas User +
     A                                      on GCMS Outgoing Server')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  45                                  DSPATR(PC)
     A                                      DSPATR(ND)
     A N25                                  OVRATR
     A                                 13  2'Outgoing Datafile Path...:'
     A            S09NNT        77   B 14  2TEXT('Outgoing Server Datafile +
     A                                      Path:')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 15  2'Outgoing Confirmation File Path:'
     A            S09NNC        77   B 16  2TEXT('Outgoing Confirmation File+
     A                                      Path:')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *=========================================================================
     A          R #RCDDTL2                  TEXT('Details IAT/TPP')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
      *-SETOFFS-------------------------------------------------------------------------------
     A                                      SETOF(32 'Invalid: S0MAXT')
     A                                      SETOF(33 'Invalid: S0HREP')
     A                                      SETOF(38 'Invalid: S0101F')
     A                                      SETOF(39 'Invalid: S0942F')
     A                                      SETOF(40 'Invalid: S0999F')
     A                                      SETOF(46 'Invalid: S0PYPF')
     A                                      SETOF(47 'Invalid: S0PYMR')
     A                                      SETOF(48 'Invalid: S0PYIP')
     A                                      SETOF(49 'Invalid: S0PYSM')
     A                                      SETOF(50 'Invalid: S0PYBC')
     A                                      SETOF(51 'Invalid: S0PYPC')
     A                                      SETOF(54 'Invalid: S0101R')
     A                                      SETOF(55 'Invalid: S0PYOP')
      *---------------------------------------------------------------------------------------
     A**********                       15  2'Maximum No. of Re-tries.:'                      JMI019B
     A**********  S0MAXT         1  0B 15 28TEXT('Maximum no. of re-tries')                  JMI019B
     A                                 17  2'Maximum No. of Re-tries.:'                      JMI019B
     A            S0MAXT         1  0B 17 28TEXT('Maximum no. of re-tries')                  JMI019B
     A                                      EDTCDE(3)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A**********                       16  2'History Retention Period:'                      JMI019B
     A**********  S0HREP         2  0B 16 28TEXT('History Retention Period')                 JMI019B
     A                                 17 62'History Reten:'                                 JMI019B
     A            S0HREP         2  0B 17 77TEXT('History Retention Period')                 JMI019B
     A                                      EDTCDE(3)
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A**********                       15 36'MT942 Processing Frequency:'                    JMI019B
     A**********  S0942F         3Y 0B 15 64EDTCDE(3)                                        JMI019B
     A                                 17 33'MT942 Processing Freq:'                         JMI019B
     A            S0942F         3Y 0B 17 56EDTCDE(3)                                        JMI019B
     A N25                                  OVRDTA
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A**********                       16 36'MT999 Processing Frequency:'                    JMI019B
     A**********  S0999F         3Y 0B 16 64EDTCDE(4)                                        JMI019B
     A*N25******                            OVRDTA                                           JMI019B
     A**40******                            DSPATR(RI PC)                                    JMI019B
     A*N40******                            DSPATR(UL HI)                                    JMI019B
     A*N25******                            OVRATR                                           JMI019B
     A                                 18  2'MT101 Payments:'
     A                                      DSPATR(UL)
     A                                 19  4'Processing Frequency..:'
     A            S0PYPF         3Y 0B 19 28EDTCDE(3)
     A N25                                  OVRDTA
     A  46                                  DSPATR(RI PC)
     A N46                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 20  4'Message Retention Days:'
     A            S0PYMR         2Y 0B 20 28EDTCDE(3)
     A N25                                  OVRDTA
     A  47                                  DSPATR(RI PC)
     A N47                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 19 36'Outgoing Payment Sub-type.:'
     A            S0PYOP         2A  B 19 64
     A N25                                  OVRDTA
     A  55                                  DSPATR(RI PC)
     A N55                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 20 36'Incoming Payment Sub-type.:'
     A            S0PYIP         2A  B 20 64
     A N25                                  OVRDTA
     A  48                                  DSPATR(RI PC)
     A N48                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 21  4'Settlement Method.....:'
     A            S0PYSM         2A  B 21 28
     A N25                                  OVRDTA
     A  49                                  DSPATR(RI PC)
     A N49                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 21 36'Book code.................:'
     A            S0PYBC         2A  B 21 64
     A N25                                  OVRDTA
     A  50                                  DSPATR(RI PC)
     A N50                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A  52                             22  4'Profit Centre.........:'
     A            S0PYPC         4A  B 22 28
     A N52                                  DSPATR(PR ND)
     A N25 52                               OVRDTA
     A  51 52                               DSPATR(RI PC)
     A N51 52                               DSPATR(UL HI)
     A N25 52                               OVRATR
      *=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
      **Start of Change JMI019
     A**********                       24  2'F3=Exit'                                         JMI019
     A                                 23  2'F3=Exit'
      **End of Change JMI019
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *=========================================================================
