      *****************************************************************
/*S*D ***DSPF**********************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD GCMS Interface ICD Enquiry - MT999')          *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      ***DSPF/SDJ004E#*-*GCMS*Interface*ICD*Enquiry.*******************                  JMI113
      *  DSPF/XX1132E# - GCMS Interface ICD Enquiry.                  *                  JMI113
      *                                                               *
      ***(c)*Misys*International*Banking*Systems*Ltd.*2007*************                  JMI113
      *  (c) Finastra International Limited 2018                      *                  JMI113
      *                                                               *
      *  Last Amend No. JMI113             Date 16May18               *
      *  Prev Amend No. JMI019  *Create    Date 16Feb07               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  JMI113 - Midas GCMS Interface. Upgrade to FBM 2.1            *
      *  JMI019 - Phase III MT999 Message Processing                  *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
      ** Start of JMI113
     A**********R #RCDKEY                   TEXT('(c) Misys International +
     A**********                            Banking Systems Ltd. +
     A**********                            2007')
     A          R #RCDKEY                   TEXT('(c) Finastra Internation+
     A                                      al Limited 2018')
      ** End of JMI113
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1GKEY')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
      ** Start of JMI113
     A**********                        1 13'SDJ004E1'
     A                                  1 13'XX1132E1'
      ** End of JMI113
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 29'GCMS Customers Interface +
     A                                       ICD Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  2'GCMS Interface ICD Key...:'
     A            #1GKEY        10   B  4 31TEXT('GCMS Interface ICD Key')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Exit    F12=Previous Screen'
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
      ** Start of JMI113
     A**********                        1 13'SDJ004E2'
     A                                  1 13'XX1132E2'
      ** End of JMI113
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 21'GCMS Customers Interface ICD-
     A                                       Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1GKEY        10   H      TEXT('GCMS Interface ICD Key')
     A            S0LCD          5  0H      TEXT('Last Change Date')
     A            S0TYLC         1   H      TEXT('Type of Last Change')
     A                                  4  2'MT999 FTP Instructions:'
     A                                      DSPATR(UL)
     A                                  5  2'Outgoing Server Host name:'
     A            S0999H        30      5 29TEXT('Host name for GCMS Outgoing +
     A                                      Server for MT999')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Outgoing Server User name:'
     A            S0999U        20      6 29TEXT('User name for GCMS Outgoing +
     A                                      Server for MT999')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'Outgoing Server Password :'
     A            S0999P        20      7 29TEXT('Password for GCMS Outgoing +
     A                                      Server for MT999')
     A N25                                  OVRDTA
     A                                      DSPATR(ND)
     A                                  8  2'Outgoing Datafile Path...:'
     A            S0999T        77      9  2TEXT('Datafile Path for GCMS +
     A                                      Outgoing Server for MT999')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 10  2'Outgoing Confirmation File +
     A                                       Path...:'
     A            S0999C        77     11  2TEXT('Outgoing Confirmation +
     A                                      File Path for MT999')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 14  2'Maximum No. of Re-tries.:'
     A            S0MAX9         1     14 29TEXT('Maximum No. of Re-tries+
     A                                       for MT999')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 15  2'History Retention Period:'
     A            S0REP9         2  0  15 29TEXT('History Retention Period+
     A                                       for MT999')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Main Menu   F12=Previous Screen'
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
