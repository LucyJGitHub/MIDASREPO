      *****************************************************************
/*S*D ***DSPF**********************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD GCMS Interface ICD Maintenance- MT999')       *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      ***DSPF/SDJ004R#*-*Maintain*the*MT999*FTP*Instructions.**********                  JMI113
      *  DSPF/XX1132R# - Maintain the MT999 FTP Instructions.         *                  JMI113
      *                                                               *
      ***(c)*Misys*International*Banking*Systems*Ltd.*2007*************                  JMI113
      *  (c) Finastra International Limited 2018                      *                  JMI113
      *                                                               *
      *  Last Amend No. JMI113             Date 16May18               *
      *  Prev Amend No. JMI019  *Create    Date 16Feb07               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  JMI113 - Midas GCMS Interface. Upgrade to FBM 2.1            *
      *  JMI019 - Phase III MT999 Message Processing                  *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
      ** Start of JMI113
     A**********R #RCDKEY                   TEXT('(C) Misys International +
     A**********                            Banking Systems Ltd. 2007')
     A          R #RCDKEY                   TEXT('(C) Finastra Internation+
     A                                      al 2018')
      ** End of JMI113
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
      ** Start of JMI113
     A**********                        1 13'SDJ004R1'
     A                                  1 13'XX1132R1'
      ** End of JMI113
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 23'GCMS Customers Interface ICD Main-
     A                                      tenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  2'Duplicate Return Code...:'
     A                                  5  2'GCMS Interface ICD Key...:'
     A            #1GKEY        10   B  5 23TEXT('GCMS Interface ICD Key')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Exit'
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('Details')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
      ** Start of JMI113
     A**********                        1 13'SDJ004R2'
     A                                  1 13'XX1132R2'
      ** End of JMI113
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 25'GCMS Customers Interface ICD +
     A                                       Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  2'MT999 FTP Instructions:'
     A                                      DSPATR(UL)
     A                                  5  2'Outgoing Server Host name:'
     A            S0999H        30   B  5 29TEXT('Host name for GCMS Outgoing +
     A                                      Server for MT999')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  44                                  DSPATR(RI PC)
     A N44                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  6  2'Outgoing Server User Name:'
     A            S0999U        20   B  6 29TEXT('User name for GCMS Outgoing +
     A                                      Server for MT999')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  7  2'Outgoing Server Password :'
     A            S0999P        20   B  7 29TEXT('Password for GCMS Outgoing +
     A                                      Server for MT999')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  45                                  DSPATR(PC)
     A                                      DSPATR(ND)
     A N25                                  OVRATR
     A                                  8  2'Outgoing Datafile Path...:'
     A            S0999T        77   B  9  2TEXT('Datafile Path for GCMS +
     A                                      Outgoing Server for MT999')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 10  2'Outgoing Confirmation File +
     A                                       Path...:'
     A            S0999C        77   B 11  2TEXT('Outgoing Confirmation +
     A                                      File Path for MT999')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 14  2'Maximum No. of Re-tries.:'
     A            S0MAX9         1   B 14 29TEXT('Maximum No. of Re-tries+
     A                                       for MT999')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 15  2'History Retention Period:'
     A            S0REP9         2  0B 15 29TEXT('History Retention Period+
     A                                       for MT999')
     A                                      EDTCDE(3)
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A*=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Main Menu   F12=Previous'
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
