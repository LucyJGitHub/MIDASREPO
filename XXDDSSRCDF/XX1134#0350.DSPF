      *****************************************************************
/*STD *  DSPFBASE
/*EXI *  TEXT('Midas GL GCMS MT999 file re-send function')            *
      *****************************************************************
      *                                                               *
      *  Midas - Midas-GCMS Interface                                 *
      *                                                               *
      ***DSPF/GLJ045#0*-*GCMS*MT999*File*Re-send*Function**************                   JMI113
      *  DSPF/XX1134#0 - GCMS MT999 File Re-send Function             *                   JMI113
      *                                                               *
      ***(c)*Misys*International*Banking*Systems*Ltd.*2007*************                   JMI113
      *  (c) Finastra International Limited 2018                      *                   JMI113
      *                                                               *
      *  Last Amend No. JMI113             Date 23May18               *
      *  Prev Amend No. JMI019  *CREATE    Date 16Feb07               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  JMI113 - Midas GCMS Interface. Upgrade to FBM 2.1            *
      *  JMI019 - Phase III MT999 Message Processing                  *
      *                                                               *
      *****************************************************************
      ** Start of JMI113
     A**********                            REF(MFRFL)
      ** End of JMI113
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*================================================================
      ** Start of JMI113
     A**********R #SFLRCD                   TEXT('(c) Misys International +
     A**********                            Banking Systems Ltd. +
     A**********                            2007')
     A          R #SFLRCD                   TEXT('(c) Finastra Internation+
     A                                      al Limited 2018.')
      ** End of JMI113
      *
     A                                      SFL
     A  84                                  SFLNXTCHG
      *
     A            #RSET          1   B  6  2
     A                                      DSPATR(HI UL)
      *
      ** Start of JMI113
     A**********  #MBRN     R        O  6 14REFFLD(USRP)
     A            #MBRN         10A  O  6 14
      ** End of JMI113
     A                                      DSPATR(HI)
      *
      ** Start of JMI113
     A**********  #DNLS     R        O  6 27REFFLD(MSTS)
     A            #DNLS          1A  O  6 27
      ** End of JMI113
     A                                      DSPATR(HI)
      *
     A            #DLDL          6A  O  6 35
     A                                      DSPATR(HI)
      *
      ** Start of JMI113
     A**********  #TDNL     R        O  6 42REFFLD(LATM)
     A            #TDNL          6S 0O  6 42
      ** End of JMI113
     A                                      DSPATR(HI)
      *
     A            #RSTN          1N 0O  6 54
     A                                      DSPATR(HI)
      *
     A*================================================================
      *
     A          R #SFLCTL                   TEXT('Subfile Control')
     A                                      SFLCTL(#SFLRCD)
     A                                      SFLPAG(10)
     A                                      SFLSIZ(50)
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A  80                                  SFLCLR
     A  81                                  SFLDSPCTL
     A  82                                  SFLDSP
     A N80 81 82                            SFLEND
      *.COMMAND.KEYS............................................................
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27)
     A                                      CF05(05 'Refresh.')
      *
     A                                 23  2'F3=Main Menu '
     A                                 23 16'F5=Refresh '
      *
     A*=========================================================================
      *
     A          R #HDR                      TEXT('HEADER')
     A                                      OVERLAY
     A                                      PUTOVR
     A            ##USR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A                                      OVRDTA
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
     A                                  2 30'GCMS MT999 File Resend'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  4  2'Reset'
     A                                  4 12'Generation'
     A                                  4 26'Status'
     A                                  4 35'Last Download'
     A                                  4 51'No. of'
      *
     A                                  5  2'Status'
     A                                  5 15'Date'
     A                                  5 51'Tries'
      *
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
