      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Reserved Batch Numbers')                      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  XX116000DF - Midas SD Reserved Batch Numbers Enquiry         *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. JMI116   *Create   Date 08Jul20               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  JMI116 - Reserve Journal Entry Batch Numbers                 *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
      ** Subfile record.
      *
     A          R #SFLRCD                   SFL
     A                                      TEXT('(c) Finastra International Li-
     A                                      mited 2001          ')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(35 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1SEL          1A  B  7  2VALUES('E' 'A' 'D')
     A                                      CHECK(AB)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N35                                  DSPATR(UL)
     A N35                                  DSPATR(HI)
     A            #1SBTN         3A  O  7  6DSPATR(HI)
     A            #1EBTN         3A  O  7 12DSPATR(HI)
     A            #1FOID        20A  O  7 21DSPATR(HI)
     A            #1DEPT         4A  O  7 47DSPATR(HI)
     A            #1ENAB         8A  O  7 57DSPATR(HI)
      *
      ** Subfile control.
      *
     A          R #SFLCTL                   SFLCTL(#SFLRCD)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      TEXT('Subfile control.')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF09(09 'Add.')
     A                                      SETOF(99 'Global error flag')
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A N25N80 81                            SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A N80 81 82                            SFLEND
     A            ##SFRC         4S 0H      SFLRCDNBR
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A            ##MRDT         7A  O  1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##URPT        53A  O  2 13TEXT('User Report Title')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  5'Batch Number'
     A                                  5  5'Start  End  '
     A            #2SBTN         3A  B  6  6
     A N70                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  70                                  DSPATR(RI)
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A N25                                  OVRATR
     A            #2EBTN         3A  B  6 12
     A N70                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  70                                  DSPATR(RI)
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A N25                                  OVRATR
     A                                  4 21'Front'
     A                                  5 21'Office ID'
     A                                  4 44'Department'
     A                                  5 44'   Code  '
     A                                  4 57'Enabled/'
     A                                  5 57'Disabled'
     A            ##USR         10A  O  1  2TEXT('*USER')
     A                                  1 13'XX116000'
      *
      ** Function keys record format.
      *
     A          R #CMDTXT1
     A                                      TEXT('Command keys')
     A                                      OVERLAY
     A                                      PUTOVR
     A            ##CTX1        78A  O 22  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A            ##CTX2        78A  O 23  2TEXT('Command Text 2')
     A N25                                  OVRDTA
      *
      ** Subfile message.
      *
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ(10)
      *
      ** Subfile control message.
      *
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A  86                                  OVERLAY
     A  86                                  PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            ##PGM                     SFLPGMQ(10)
