      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Reserved Batch Numbers')                      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  XX116001DF - Midas SD Reserved Batch Numbers                 *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. JMI116   *Create   Date 08Jul20               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  JMI116 - Reserve Journal Entry Batch Numbers                 *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
      ** Prompt 1 record format.
      *
     A          R #RCDKEY
     A                                      TEXT('(c) Finastra International Li-
     A                                      mited 2020          ')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF09(09 'Change mode.')
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1SWCD')
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'XX116001DF'
     A            ##MRDT         7A  O  1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##URPT        53A  O  2 13TEXT('User Report Title')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #PANCD         1A  H      TEXT('Action Code')
     A                                  4  2'Start Batch Number:'
     A            #1SBTN         3   B  4 23
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PC)
     A N25                                  OVRDTA
     A N25                                  OVRATR
      *
     A                                  5  2'End Batch Number..:'
     A            #1EBTN         3   B  5 23
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(UL)
     A  32                                  DSPATR(PC)
     A N25                                  OVRDTA
     A N25                                  OVRATR
      *
      ** Function Key 1 record format.
      *
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*
     A** Prompt 2 record format.
     A*
     A          R #RCDDTL1
     A                                      TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A N89                                  CF10(10 'Delete.')
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1SBTN')
     A                                      SETOF(32 'Invalid: #1EBTN')
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'XX116001'
     A            ##MRDT         7A  O  1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##URPT        53A  O  2 13TEXT('User Report Title')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #PANCD         1A  H      TEXT('Action Code')
     A                                  4  2'Start Batch Number:'
     A            #1SBTN         3A  O  4 23
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI)
     A N25                                  OVRDTA
     A                                  5  2'End Batch Number..:'
     A            #1EBTN         3A  O  5 23
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI)
     A N25                                  OVRDTA
      *
     A                                  7  2'Front Office ID...:'
     A            #1FOID        20A  B  7 23TEXT('Front Office ID')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A N79N42                               DSPATR(UL)
     A N42                                  DSPATR(HI)
     A*N25                                  OVRATR
     A                                  8  2'Department........:'
     A            #1DEPT         4A  B  8 23
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A N79N43                               DSPATR(UL)
     A N43                                  DSPATR(HI)
     A*N25                                  OVRATR
     A                                  9  2'Enabled/Disabled..:'
     A            #1ENAB         8A  B  9 23
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A N79N44                               DSPATR(UL)
     A N44                                  DSPATR(HI)
     A*N25                                  OVRATR
      *
      ** Function Key 2 record format.
      *
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
      *
      ** Subfile message.
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      ** Subfile message control.
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
