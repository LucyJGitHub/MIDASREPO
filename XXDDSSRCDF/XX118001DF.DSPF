      *****************************************************************
/*STD *  DSPFBASE                                                     *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/XX118001DF - On-Line Generation of Position Settlements *
      *                                                               *
      *  (c) Finastra International Limited 2022                      *
      *                                                               *
      *  Last Amend No. HUT118  *CREATE    Date 03Nov22               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  HUT118 - On-line Generation of Position Settlements          *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R XX118001F0                TEXT('(c) Finastra Ltd 2020')
     A                                      FRCDTA
     A                                      OVERLAY BLINK PUTOVR
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'XX118001'
     A                                  1 25'On-Line Positions Generation'
     A                                      DSPATR(UL HI)
     A            BJMRDT         7A  O  1 73TEXT('Midas Rundate')
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
      *
     A            STIME          6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  5  5'Security        :'
     A            SSECU         10A  B  5 23
     A  09                                  DSPATR(RI PC)
     A N09N15                               DSPATR(UL)
     A  15                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                  7  5'Event Date      :'
     A            SEDTE          6A  B  7 23
     A  11                                  DSPATR(RI PC)
     A N11N15                               DSPATR(UL)
     A  15                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                  9  5'Payment Date    :'
     A            SPDTE          6A  B  9 23
     A  12                                  DSPATR(RI PC)
     A N12N15                               DSPATR(UL)
     A  15                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 11  5'Event Type      :'
     A            SEVTP          2A  B 11 23
     A  14                                  DSPATR(RI PC)
     A N14N15                               DSPATR(UL)
     A  15                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A          R XX118001F1                  TEXT('Screen Footer')
     A                                      CF03(03 'Exit')
     A  30                                  CF08(08 'Generation Rpt')
     A  13                                  CF10(10 'Generate')
     A  20                                  CF12(12 'Cancel')
     A                                      OVERLAY
     A                                 23  3'F3=End'
     A N30 20N13                       23 12'F12=Cancel'
     A N30 13                          23 12'F10=Generate   F12=Cancel'
     A  30                             23 12'F8=Generated Position -
     A                                      Settlements   F12=Cancel'
      *
     A          R XX118001S1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            SMSGKY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
     A          R XX118001C1                TEXT('Program messages')
     A                                      SFLCTL(XX118001S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
      *
