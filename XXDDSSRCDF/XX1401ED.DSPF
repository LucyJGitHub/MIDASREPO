     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Stamp Duty Code Enquiry/Delete Display')      *
      *****************************************************************
      *                                                               *
      *  Midas    - Retail Module                                     *
      *                                                               *
      ***REM001ED*-*Stamp*Duty*Code*Enquiry/Delete Display*************
      *  XX1401ED - Stamp Duty Code Enquiry/Delete Display            *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1998           *
      *                                                               *
      *  Last Amend No. LUC140             Date 24Feb21               *
      *  Prev Amend No. MMI013 *CREATE     Date 18Mar98               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  LUC140 - Upgrade MMI013 to FM2.1                             *
      *  MMI013 - Retail Stamp Duty and Account Changes               *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03(03 'Exit')
     A  39                                  CA10(10 'Delete')
     A                                      CA12(12 'Previous')
     A                                      HELP
     A                                      HLPDOC('H030@000' 'ITINS' 'ABSENG/I-
     A                                      TL')
      *================================================================
     A          R REM001F1
      *================================================================
     A                                      TEXT('(C) COPYRIGHT MKI INTL.LTD 19-
     A                                      98')
     A                                      OVERLAY
      * Help tag for 'Stamp Duty Code'
     A          H                           HLPARA(04 03 04 32)
     A                                      HLPDOC(H030@015 ITINS ABSENG/ITL)
      * Help tag for 'Descrition'
     A          H                           HLPARA(05 03 05 60)
     A                                      HLPDOC(H030@060 ITINS ABSENG/ITL)
      * Help tag for 'Currency'
     A          H                           HLPARA(06 03 06 33)
     A                                      HLPDOC(H030@020 ITINS ABSENG/ITL)
      * Help tag for 'Yearly Amount'
     A          H                           HLPARA(07 03 07 46)
     A                                      HLPDOC(H030@025 ITINS ABSENG/ITL)
      * Help tag for 'Narrative Code-DR'
     A          H                           HLPARA(08 03 08 32)
     A                                      HLPDOC(H030@030 ITINS ABSENG/ITL)
      * Help tag for 'Narrative Type-DR'
     A          H                           HLPARA(09 03 09 36)
     A                                      HLPDOC(H030@040 ITINS ABSENG/ITL)
      * Help tag for 'Narrative Code-CR'
     A          H                           HLPARA(10 03 10 32)
     A                                      HLPDOC(H030@035 ITINS ABSENG/ITL)
      * Help tag for 'Customer Code-CR'
     A          H                           HLPARA(11 03 11 36)
     A                                      HLPDOC(H030@045 ITINS ABSENG/ITL)
      * Help tag for 'A/c Code-CR'
     A          H                           HLPARA(12 03 12 33)
     A                                      HLPDOC(H030@050 ITINS ABSENG/ITL)
      * Help tag for 'A/c sequence'
     A          H                           HLPARA(13 03 13 32)
     A                                      HLPDOC(H030@055 ITINS ABSENG/ITL)
     A            USER          10A  O  1  2
     A            ##PGM         10A  O  1 13
     A            RUNA           7A  O  1 73
     A            JOB           10A  O  2  2
     A N38                              2 30'Stamp Duty Code Enquiry'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A  38                              2 30'Stamp Duty Code Deletion'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                 23  2'F3=Main Menu'
     A                                 23 16'F12=Previous'
     A  39                             23 30'F10=Delete'
     A                                  4  3'Stamp Duty Code...........:'
     A            SSDC           2A  O  4 31DSPATR(HI)
     A                                  5  3'Description...............:'
     A            SDESC         30A  O  5 31DSPATR(HI)
     A                                  6  3'Currency..................:'
     A            SCYCD          3A  O  6 31DSPATR(HI)
     A                                  7  3'Yearly Amount.............:'
     A            SYAMT         15A  O  7 31DSPATR(HI)
     A                                  8  3'Narrative Code-DR.........:'
     A            SNADR          2A  O  8 31DSPATR(HI)
     A                                  9  3'Transaction Type-DR.......:'
     A            STTYP          5A  O  9 31DSPATR(HI)
     A                                 10  3'Narrative Code-CR.........:'
     A            SNACR          2A  O 10 31DSPATR(HI)
     A                                 11  3'Customer Code-CR..........:'
     A            SCUST          6A  O 11 31DSPATR(HI)
     A                                 12  3'A/c Code-CR...............:'
      ** Start of LUC140
     A**********  SACOD          4A  O 12 31DSPATR(HI)
     A            SACOD         10A  O 12 31DSPATR(HI)
      ** End of LUC140
     A                                 13  3'A/c Sequence..............:'
     A            SACSQ          2A  O 13 31DSPATR(HI)
      *================================================================
     A          R REM001S0                  TEXT('Program messages')
      *================================================================
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *================================================================
     A          R REM001S1                  TEXT('Program messages')
      *================================================================
     A                                      SFLCTL(REM001S0)
     A                                      SFLPAG(01) SFLSIZ(6)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
      *================================================================
