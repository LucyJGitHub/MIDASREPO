     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Stamp Duty Code Maintenance Display')         *
      *****************************************************************
      *                                                               *
      *  Midas    - Retail Module                                     *
      *                                                               *
      ***REM001RD - Stamp Duty Code Maintenance Display               *
      *  XX1401RD - Stamp Duty Code Maintenance Display               *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1998           *
      *                                                               *
      *  Last Amend No. LUC140             Date 24Feb21               *
      *  Prev Amend No. MMI013 *CREATE     Date 20Mar98               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  LUC140 - Upgrade MMI013 to FM2.1                             *
      *  MMI013 - Retail Stamp Duty and Account Changes               *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      HELP
     A                                      HLPDOC(H030@000 ITINS ABSENG/ITL)
     A                                      CA03(03 'Exit')
     A                                      CA12(12 'Previous')
      *================================================================
     A          R REM001F1
      *================================================================
     A                                      TEXT('(C) COPYRIGHT MKI INTL.LTD 19-
     A                                      98')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SETOF(21 'Invalid Stamp Duty Code')
     A          H                           HLPARA(04 19 4 23)
     A                                      HLPDOC(H030@010 ITINS ABSENG/ITL)
     A            USER          10A  O  1  2
     A            ##PGM         10A  O  1 13
     A            RUNA           7A  O  1 73
     A            JOB           10A  O  2  2
     A                                  2 30'New Stamp Duty Code'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                 23  2'F3=Main Menu'
     A                                 23 16'F12=Previous'
     A                                  4  3'Stamp Duty Code:'
     A            SSDC           2A  B  4 20OVRDTA
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
      *================================================================
     A          R REM001F2
      *================================================================
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SETOF(22)
     A                                      SETOF(23 'Invalid Currency')
     A                                      SETOF(24 'Invalid Yearly Amount')
     A                                      SETOF(25 'Invalid Narrative Code')
     A                                      SETOF(26 'Invalid Transaction Type')
     A                                      SETOF(27 'Invalid Narrative Code')
     A                                      SETOF(28 'Invalid Customer Code')
     A                                      SETOF(29 'Invalid A/c Code')
     A                                      SETOF(30 'Invalid Sequence')
      * Help tag for 'Stamp Duty Code'
     A          H                           HLPARA(04 03 04 32)
     A                                      HLPDOC(H030@015 ITINS ABSENG/ITL)
      * Help tag for 'Descrition'
     A          H                           HLPARA(05 03 05 60)
     A                                      HLPDOC(H030@060 ITINS ABSENG/ITL)
      * Help tag for 'Currency'
     A          H                           HLPARA(06 03 06 33)
     A                                      HLPDOC(H030@020 ITINS ABSENG/ITL)
      * Help tag for 'Yearly Amount'
     A          H                           HLPARA(07 03 07 46)
     A                                      HLPDOC(H030@025 ITINS ABSENG/ITL)
      * Help tag for 'Narrative Code-DR'
     A          H                           HLPARA(08 03 08 32)
     A                                      HLPDOC(H030@030 ITINS ABSENG/ITL)
      * Help tag for 'Narrative Type-DR'
     A          H                           HLPARA(09 03 09 36)
     A                                      HLPDOC(H030@040 ITINS ABSENG/ITL)
      * Help tag for 'Narrative Code-CR'
     A          H                           HLPARA(10 03 10 32)
     A                                      HLPDOC(H030@035 ITINS ABSENG/ITL)
      * Help tag for 'Customer Code-CR'
     A          H                           HLPARA(11 03 11 36)
     A                                      HLPDOC(H030@045 ITINS ABSENG/ITL)
      * Help tag for 'A/c Code-CR'
     A          H                           HLPARA(12 03 12 33)
     A                                      HLPDOC(H030@050 ITINS ABSENG/ITL)
      * Help tag for 'A/c sequence'
     A          H                           HLPARA(13 03 13 32)
     A                                      HLPDOC(H030@055 ITINS ABSENG/ITL)
     A            USER          10A  O  1  2
     A            ##PGM         10A  O  1 13
     A            RUNA           7A  O  1 73
     A            JOB           10A  O  2  2
     A                                  2 30'Stamp Duty Code Maintenance'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                 23  2'F3=Main Menu'
     A                                 23 16'F12=Previous'
     A                                  4  3'Stamp Duty Code...........:'
     A            SSDC           2A  O  4 31OVRDTA
     A                                  5  3'Description...............:'
     A            SDESC         30A  B  5 31OVRDTA
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A                                      CHECK(LC)
     A                                  6  3'Currency..................:'
     A            SCYCD          3A  B  6 31OVRDTA
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A                                  7  3'Yearly Amount.............:'
     A            SYAMT         15A  B  7 31OVRDTA
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A  24                                  DSPATR(PC)
     A                                  8  3'Narrative Code-DR.........:'
     A            SNADR          2A  B  8 31OVRDTA
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A  25                                  DSPATR(PC)
     A                                  9  3'Transaction Type-DR.......:'
     A            STTYP          5A  B  9 31OVRDTA
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A  26                                  DSPATR(PC)
     A                                 10  3'Narrative Code-CR.........:'
     A            SNACR          2A  B 10 31OVRDTA
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(RI)
     A  27                                  DSPATR(PC)
     A                                 11  3'Customer Code-CR..........:'
     A            SCUST          6A  B 11 31OVRDTA
     A N28                                  DSPATR(HI)
     A  28                                  DSPATR(RI)
     A  28                                  DSPATR(PC)
     A                                 12  3'A/c Code-CR...............:'
      ** Start of LUC140
     A**********  SACOD          4A  B 12 31OVRDTA
     A            SACOD         10A  B 12 31OVRDTA
      ** End of LUC140
     A N29                                  DSPATR(HI)
     A  29                                  DSPATR(RI)
     A  29                                  DSPATR(PC)
     A                                 13  3'A/c Sequence..............:'
     A            SACSQ          2A  B 13 31OVRDTA
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
      *================================================================
     A          R REM001S0                  TEXT('Program messages')
      *================================================================
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *================================================================
     A          R REM001S1                  TEXT('Program messages')
      *================================================================
     A                                      SFLCTL(REM001S0)
     A                                      SFLPAG(01) SFLSIZ(6)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
      *================================================================
