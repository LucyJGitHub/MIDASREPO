     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Stamp Duty Codes Subfile Selection Dis')      *
      *****************************************************************
      *                                                               *
      *  Midas    - Retail Module                                     *
      *                                                               *
      ***REM001SD - Stamp Duty Codes Subfile Selection Display        *
      *  XX1401SD - Stamp Duty Codes Subfile Selection Display        *
      *             (Enquiry Only)                                    *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1998           *
      *                                                               *
      *  Last Amend No. LUC140             Date 24Feb21               *
      *  Prev Amend No. MMI013 *CREATE     Date 23Mar98               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  LUC140 - Upgrade MMI013 to FM2.1                             *
      *  MMI013 - Retail Stamp Duty and Account Changes               *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A                                      HELP
     A                                      HLPDOC('H128@000' 'ITINS' 'ABSENG/I-
     A                                      TL')
     A*=========================================================================
     A          R REM001S2                  SFL
     A                                      TEXT('(C) Copyright MKI Internation-
     A                                      al Ltd 1998')
     A  84                                  SFLNXTCHG
     A            SSEL1          1A  B  7  2VALUES('E')
     A                                      CHECK(AB)
     A  40                                  DSPATR(ND)
     A            SMSDC1         2A  O  7  5TEXT('SDC Code')
     A                                      DSPATR(HI)
     A  40                                  DSPATR(ND)
     A            SDESC1        30A  O  7 11TEXT('Description')
     A                                      DSPATR(HI)
     A  40                                  DSPATR(ND)
     A            SCYCD1         3A  O  7 45TEXT('Currency')
     A                                      DSPATR(HI)
     A  40                                  DSPATR(ND)
     A            SYAMT1        15A  O  7 56TEXT('Yearly Amount')
     A                                      DSPATR(HI)
     A  40                                  DSPATR(ND)
     A*=========================================================================
     A          R REM001C2                  SFLCTL(REM001S2)
     A                                      SFLSIZ(1024)
     A                                      SFLPAG(0014)
     A                                      TEXT('Subfile control.')
     A                                      BLINK
     A                                      OVERLAY
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'End Subfile')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N80                                  SFLDSP
     A  81                                  SFLEND(*MORE)
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A                                      CA12(12 'Previous')
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(40 'No data')
     A          H                           HLPDOC('H001@SCD' 'ITINS' 'ABSENG/I-
     A                                      TL')
     A                                      HLPARA(05 004 05 008)
     A          H                           HLPDOC('H001@SEL' 'ITINS' 'ABSENG/I-
     A                                      TL')
     A                                      HLPARA(07 001 22 008)
     A            RRN            4S 0H      SFLRCDNBR
     A            USER          10A  O  1  2
     A            ##PGM         10A  O  1 13
     A            RUNA           7A  O  1 73
     A            JOB           10A  O  2  2
     A                                  2 25'Stamp Duty Codes Maintenance'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A            SMSDC2         2A  B  5  5TEXT('SDC Code')
     A                                  4  5'SDC'
     A                                  4 11'Description'
     A                                  4 45'Currency'
     A                                  4 56'Yearly Amount'
     A*=========================================================================
     A          R REM001F1
     A                                      TEXT('Command keys')
     A                                 23  2'E=Enquiry   F3=Main Menu   F5=Refr-
     A                                      esh                           '
      *================================================================
     A          R REM001S0                  TEXT('Program messages')
      *================================================================
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *================================================================
     A          R REM001S1                  TEXT('Program messages')
      *================================================================
     A                                      SFLCTL(REM001S0)
     A                                      SFLPAG(01) SFLSIZ(6)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
      *================================================================
