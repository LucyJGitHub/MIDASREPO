      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Stamp Duty Report Prompt Display')            *
      *****************************************************************
      *                                                               *
      *  Midas    - Retail Module                                     *
      *                                                               *
      ***REM003DF - Stamp Duty Report Prompt Display                  *
      *  XX1403DF - Stamp Duty Report Prompt Display                  *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1998           *
      *                                                               *
      *  Last Amend No. LUC140             Date 24Feb21               *
      *  Prev Amend No. MMI013 *CREATE     Date 12Mar98               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  LUC140 - Upgrade MMI013 to FM2.1                             *
      *  MMI013 - Retail Stamp Duty and Account Changes               *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      HELP
     A                                      HLPDOC(HXXX@000 ITINS ABSENG/ITL)
     A                                      CA03(03 'Exit')
     A                                      CA12(12 'Previous')
      *================================================================
     A          R REM003F1
      *================================================================
     A                                      TEXT('(C) COPYRIGHT MKI INTL.LTD 19-
     A                                      98')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SETOF(30 'Invalid Reporting Period')
     A                                      SETOF(31 'Invalid Selection')
     A          H                           HLPARA(09 48 09 52)
     A                                      HLPDOC(HXXX@010 ITINS ABSENG/ITL)
     A          H                           HLPARA(13 48 13 51)
     A                                      HLPDOC(HXXX@010 ITINS ABSENG/ITL)
     A            USER          10A  O  1  2
     A            ##PGM         10A  O  1 13
     A            RUNA           7A  O  1 73
     A            JOB           10A  O  2  2
     A                                  2 25'RETAIL STAMP DUTY REPORT'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A            SREPR          2Y 0B  9 49OVRDTA
     A                                      EDTCDE(4)
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(UL HI)
     A            SDLSM          1A  B 13 49OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A                                 23  2'F3=Exit F12=Previous'
     A                                 13 24'Detail/Summary (D/S)...'
     A                                  9 24'Reporting Period.......'
      *
      *================================================================
     A          R REM003S0                  TEXT('Program messages')
      *================================================================
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *================================================================
     A          R REM003S1                  TEXT('Program messages')
      *================================================================
     A                                      SFLCTL(REM003S0)
     A                                      SFLPAG(01) SFLSIZ(6)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
      *================================================================
