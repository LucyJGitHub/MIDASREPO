     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas xx I/O Account Code Table Maintenance')          *
     A*****************************************************************
     A*                                                               *
     A*   Midas - I/O RECONCILE                                       *
     A*                                                               *
     A*   XX1407DF - I/O ACCOUNT CODE TABLE MAINTENANCE               *
     A*                                                               *
     A*  (C) Finastra International Limited 2021                      *
     A*                                                               *
     A*  Last Amend No. EML114   *CREATE   Date 23Sep21               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  EML114 - Upgrade TFJ004 to FM2.1                             *
     A*  TFJ004 - Inter-office Reconcile                              *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A*================================================================
     A*LIST SUBFILE
     A          R XX1407S1                  TEXT('(C) Finastra +
     A                                      International Limited +
     A                                      2021')
     A                                      SFL
     A  45                                  SFLNXTCHG
     A*
     A            WSACTN         1A  B  7  2TEXT('ACTION CODE')
     A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  20                                  DSPATR(RI PC)
     A            WSACOD        10A  B  7  5TEXT('ACCOUNT CODE')
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A  21                                  DSPATR(RI PC)
     A            WSACSC         2A  O  7 17TEXT('ACCOUNT SECTION')

     A            WSACCN        25A  O  7 21TEXT('ACCOUNT NAME')
     A            WSRCTP         1A  B  7 48TEXT('RECORD TYPE')
     A                                      DSPATR(UL)
     A  22                                  DSPATR(RI PC)
     A            WSALTN        10A  B  7 51TEXT('ALTERNATE A/C')
     A                                      DSPATR(UL)
     A  23                                  DSPATR(RI PC)
     A            WSHCST         3A  B  7 63TEXT('H/O CUSTOMER')
     A                                      DSPATR(UL)
     A  24                                  DSPATR(RI PC)
     A            WSIOCS         1A  B  7 68TEXT('I/O CUSTOMER')
     A                                      DSPATR(UL)
     A  25                                  DSPATR(RI PC)
     A*SAVES
     A            WSSAVE        53A  H      TEXT('RECORD SAVE')
     A*================================================================
     A*LIST SUBFILE-CTL
     A          R XX1407C1                  TEXT('(C) Finastra +
     A                                      International Limited +
     A                                      2021')
     A                                      BLINK OVERLAY
     A  36                                  ERASEINP
     A*
     A                                      SFLCTL(XX1407S1)
     A                                      SFLSIZ(0016) SFLPAG(0015)
     A  40                                  SFLCLR
     A N40                                  SFLDSPCTL
     A N40 41                               SFLDSP
     A N40 41N42                            SFLEND
     A*
     A                                      CA03
     A                                      CA05
     A                                      CA09
     A  42                                  ROLLUP(14 'NEXT PAGE')
     A                                      VLDCMDKEY(16 'ENTER KEY')
     A*
     A            WSSFRC         4S 0H      SFLRCDNBR(CURSOR)
     A*
     A            WSPGM         10A  O  1  2
     A            WSDMY          7A  O  1 72
     A            WSID          10A  O  2  2
     A            WSTIME         6Y 0O  2 71EDTWRD('  :  :  ')
     A                                  2 23'I/O ACCOUNT CODES +
     A                                      MAINTENANCE'
     A                                      DSPATR(UL HI)
     A*
     A                                  4  5'A/C'
     A                                  4 21'Account Name'
     A                                  4 47'Rec.'
     A                                  4 53'Alt.'
     A                                  4 62'H/O'
     A                                  4 67'I/O'
     A                                  5  5'Code'
     A                                  5 47'Type'
     A                                  5 53'A/C'
     A                                  5 62'Cust'
     A                                  5 67'Cust'
     A*
     A N30        WSIND1    R        B  6  5REFFLD(WSACOD) CHANGE(35)
     A                                      DSPATR(UL)
     A*================================================================
     A*GUIDANCE
     A          R XX1407F1                  TEXT('(C) Finastra +
     A                                      International Limited +
     A                                      2021')
     A                                      BLINK
     A*
     A            WSMSG         80A  O 23  1
     A N79                                  DSPATR(HI)
     A  79                                  DSPATR(RI)
     A*
     A            WSCMD         78A  O 24  2
