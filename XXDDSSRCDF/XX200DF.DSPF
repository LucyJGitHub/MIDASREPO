      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD - Maintain COCUNUT Type')                     *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  XX200DF - Midas SD COCUNUT Type Maintenance                  *
      *                                                               *
      *  (c) Midas International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Last Amend No. MD054957           Date 11Dec19               *
      *  Prev Amend No. ERZ032   *CREATE   Date 24Jan13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD054957 - RBRU SP21 Compatibility Patch (Recompile)         *
      *  ERZ032 - Customer Details Additional Information             *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CA03(03 'Exit.')
      *================================================================
     A          R XX200F1                   TEXT('(c) Misys International -
     A                                      Banking Systems Ltd. 2013')
     A                                      INDTXT(41 'Invalid: S1COCT')
     A                                      INDTXT(42 'Invalid: S1DESC')
     A                                      INDTXT(43 'Invalid: S1MAND')
     A                                      INDTXT(44 'Invalid: S1SEL' )
     A                                      CA12(12 'Prev.')
     A N70                                  CF10(10 'Delete')
     A                                      OVERLAY
      *
      ** Header details.
      *
     A            USER          10A  O  1  2TEXT('*USER')
     A            ##PGM         10A  O  1 13TEXT('*PROGRAM')
     A            ##RUN          7      1 72TEXT('Midas Run Date')
     A            JOB           10A  O  2  2TEXT('*JOB')
     A                                  2 25'COCUNUT Type Maintenance'
     A                                      DSPATR(HI)
     A                                  2 72TIME
      *
      ** Fields details.
      *
     A                                  5  7'COCUNUT Type ........:'
     A                                  6  7'Description .........:'
     A                                  7  7'COCUNUT Mandatory? ..:'
     A            S1COCT         2A  B  5 30TEXT('COCUNUT Type')
     A                                      DSPATR(HI)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A N71N41                               DSPATR(UL)
     A  71                                  DSPATR(PR)
     A            S1DESC        30A  B  6 30TEXT('Description')
     A                                      DSPATR(HI)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A N72N42                               DSPATR(UL)
     A  72                                  DSPATR(PR)
     A            S1MAND         1A  B  7 30TEXT('COCUNUT Mandatory')
     A                                      DSPATR(HI)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A N72N43                               DSPATR(UL)
     A  72                                  DSPATR(PR)
      *================================================================
      ** Subfile record details format.
     A          R #SFLRCD                   SFL
     A  74                                  SFLNXTCHG
     A            S1SEL          1A  B  9  2TEXT('Opt')
     A                                      DSPATR(HI)
     A N44                                  DSPATR(UL)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A            S1COCT         2A  O  9  7TEXT('COCUNUT Type')
     A            S1DESC        30A  O  9 18TEXT('Description')
     A            S1MAND         1A  O  9 55TEXT('COCUNUT Mandatory?')
      *================================================================
      ** Subfile control format.
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(25 'Disable OVRDTA')
     A                                      INDTXT(51 'Subfile message ID')
     A                                      INDTXT(60 'Clear subfile')
     A                                      INDTXT(61 'Display subfile')
     A                                      INDTXT(62 'End of subfile')
     A                                      INDTXT(70 'Enquiry Mode')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  60                                  SFLDSP
     A  61                                  SFLDSPCTL
     A N60N61                               SFLCLR
     A  62                                  SFLEND(*MORE)
     A  51                                  SFLMSGID(CPF5203 *LIBL/QCPFMSG)
     A N70                                  CA05(05 'Refresh')
     A                                      ROLLUP(27 'Next page.')
     A N70                                  CF09(09 'Add mode.')
     A                                      SETOF(10)
     A                                      SETOF(12)
     A            UISFRN         4S 0H      SFLRCDNBR(CURSOR)
      *
      ** Header details.
      *
     A            USER          10A  O  1  2TEXT('*USER')
     A            ##PGM         10A  O  1 13TEXT('*PROGRAM')
     A            ##RUN          7      1 72TEXT('Midas Run Date')
     A            JOB           10A  O  2  2TEXT('*JOB')
     A N70                              2 25'COCUNUT Type Maintenance'
     A                                      DSPATR(HI)
     A  70                              2 25'COCUNUT Type Enquiry'
     A                                      DSPATR(HI)
     A                                  2 72TIME
      *
      ** Fields details.
      *
     A                                  5  7'COCUNUT'
     A                                  6  7'Type'
     A                                  5 18'Description'
     A                                  5 55'COCUNUT'
     A                                  6 55'Mandatory?'
     A            S2COCT         2A  B  7  7TEXT('COCUNUT Type')
     A N25                                  OVRDTA
     A N73                                  DSPATR(UL HI)
     A  73                                  DSPATR(PR)
     A            S2DESC        30A  B  7 18TEXT('Description')
     A N25                                  OVRDTA
     A N73                                  DSPATR(UL HI)
     A  73                                  DSPATR(PR)
     A            S2MAND         1A  B  7 55TEXT('COCUNUT Mandatory?')
     A N25                                  OVRDTA
     A N73                                  DSPATR(UL HI)
     A  73                                  DSPATR(PR)
     A  45                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
      *================================================================
      *
      ** First screen footer
      ** *IN70 = enquiry
      *
     A          R FOOTER1                   TEXT('Command keys.')
     A     70                          23 01'F3=Main Menu'
     A    N70                          23 01'A=Amend'
     A    N70                          23 10'D=Delete'
     A    N70                          23 20'F3=Main Menu'
     A    N70                          23 34'F5=Refresh'
     A    N70                          23 46'F9=Go to Add Mode'
      *
      *================================================================
      *
      ** Add/amend/delete/enquire screen
      ** *IN75 = confirm delete screen
      ** *IN76 = confirm change screen
      ** *IN70 = enquiry
      *
     A          R FOOTER2                   TEXT('Command keys.')
     A                                 23 01'F3=Main Menu'
     A  75N70                          23 15'F10=Confirm deletion'
     A  75N70                          23 37'F12=Previous'
     A N75N70N76                       23 15'F10=Confirm'
     A N75N70N76                       23 28'F12=Previous'
     A  70                             23 15'F12=Previous'
     A  76N70                          23 15'F10=Confirm Change'
     A  76N70                          23 35'F12=Previous'
      *
      *================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
