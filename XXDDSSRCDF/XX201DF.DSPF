      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD - Maintain COCUNUT')                          *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  XX201DF - Midas SD COCUNUT Maintenance                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Last Amend No. MD054957           Date 11Dec19               *
      *  Prev Amend No. ERZ032   *CREATE   Date 24Jan13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD054957 - RBRU SP21 Compatibility Patch (Recompile)         *
      *  ERZ032 - Customer Details Additional Information             *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A                                      HELP
     A                                      CA03(03 'Exit.')
      *================================================================
     A          R XX201F1                   TEXT('(c) Misys International -
     A                                      Banking Systems Ltd. 2013')
     A                                      INDTXT(41 'Invalid: S1COCO')
     A                                      INDTXT(44 'Invalid: S1SEL' )
     A                                      INDTXT(21 'Protect fields')
     A                                      CA12(12 'Prev.')
     A N70                                  CF10(10 'Delete')
     A                                      OVERLAY
      *
      ** Header details.
      *
     A            USER          10A  O  1  2TEXT('*USER')
     A            ##PGM         10A  O  1 13TEXT('*PROGRAM')
     A            ##RUN          7      1 72TEXT('Midas Run Date')
     A            JOB           10A  O  2  2TEXT('*JOB')
     A N70                              2 30'COCUNUT Maintenance'
     A                                      DSPATR(HI)
     A  70                              2 30'COCUNUT Enquiry'
     A                                      DSPATR(HI)
     A                                  2 72TIME
      *
      ** Fields details.
      *
     A                                  5  7'COCUNUT .......:'
     A            S1COCO        20A  B  5 25TEXT('COCUNUT')
     A                                      DSPATR(HI)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A N30N41                               DSPATR(UL)
     A  30                                  DSPATR(PR)
     A*================================================================
     A** Subfile record details format.
     A          R #SFLRCD                   SFL
     A  74                                  SFLNXTCHG
     A            S1SEL          1A  B  9  2TEXT('Opt')
     A N44                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A            S1COCO        20A  O  9  7TEXT('COCUNUT')
      *================================================================
      ** Subfile control format.
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(25 'Disable OVRDTA')
     A                                      INDTXT(51 'Subfile message ID')
     A                                      INDTXT(60 'Clear subfile')
     A                                      INDTXT(61 'Display subfile')
     A                                      INDTXT(62 'End of subfile')
     A                                      INDTXT(70 'Enquiry Mode')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  60                                  SFLDSP
     A  61                                  SFLDSPCTL
     A N60N61                               SFLCLR
     A  62                                  SFLEND(*MORE)
     A  51                                  SFLMSGID(CPF5203 *LIBL/QCPFMSG)
     A N70                                  CA05(05 'Refresh')
     A                                      ROLLUP(27 'Next page.')
     A N70                                  CF09(09 'Add mode.')
     A                                      SETOF(10)
     A                                      SETOF(12)
     A            UISFRN         4S 0H      SFLRCDNBR(CURSOR)
      *
      ** Header details.
      *
     A            USER          10A  O  1  2TEXT('*USER')
     A            ##PGM         10A  O  1 13TEXT('*PROGRAM')
     A            ##RUN          7A  O  1 72TEXT('Midas Run Date')
     A            JOB           10A  O  2  2TEXT('*JOB')
     A N70                              2 30'COCUNUT Maintenance'
     A                                      DSPATR(HI)
     A  70                              2 30'COCUNUT Enquiry'
     A                                      DSPATR(HI)
     A                                  2 72TIME
      *
      ** Fields details.
      *
     A                                  5  7'COCUNUT'
     A            S2COCO        20A  B  7  7TEXT('COCUNUT')
     A N25                                  OVRDTA
     A N78                                  DSPATR(UL HI)
     A  78                                  DSPATR(PR)
     A*================================================================
     A*
     A          R FOOTER
     A                                      TEXT('Command keys.')
     A N70N81N82                       23 01'D=Delete  +
     A                                      F3=Main Menu  +
     A                                      F5=Refresh  +
     A                                      F9=Go to Add mode'
     A N70 81                          23 01'F3=Main Menu  +
     A                                      F10=Confirm  +
     A                                      F12=Previous'
     A N70 82                          23 01'F3=Main Menu  +
     A                                      F10=Confirm Delete  +
     A                                      F12=Previous'
     A  70N81N82                       23 01'F3=Main Menu'
      *
      *================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
