      *****************************************************************
/*STD *  DSPFBASE                                                     *
      *****************************************************************
      *                                                               *
      *  Midas - LE  Module                                           *
      *                                                               *
      ***DSPF/LE2020#0-*Facilities Maintenance Window                *                        LUC109
      *  DSPF/XX2020#0- Facilities Maintenance Window                *                        LUC109
      *                                                               *
      ***(C)*COPYRIGHT*MIDAS-KAPITI*INTERNATIONAL*LTD.*1998,*1997******                       LUC109
      *  (C) MISYS INTERNATIONAL BANKING SYSTEMS LTD. 2001            *                       LUC109
      *                                                               *
      *  Last Amend No. LUC109             Date 24Aug16               *
      *  Prev Amend No. MUI010             Date 10Mar04               *
      *                 MUI009             Date 10Feb04               *
      *                 LUC007                  20Dec00               *
      *                 MMI043  *CREATE         24Nov98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC109 - HO Reporting (Upgrade to FB Midas)                  *
      *  MUI010 - HOR Changes - Add HO Facility Link                  *
      *  MUI009 - Upgrade to R4.01: apply LUC007 and remove Help.     *
      *  LUC007 - Amendments to Facilities and Loan Input Functions.  *
      *  MMI043 - Interface to Head Office Reporting                  *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A*** MUI009 begin
     A*********                             HELP
     A*********                             HLPDOC(H020@000 SDINS ABSENG/DBAR2)
     A*** MUI009 end
     A                                      CF03(03 'Exit')
     A                                      CF12(12 'Previous')
     A*.........................................................................
     A**********R LE2020F0                  TEXT('(C) Copyright MKI +                         LUC109
     A          R XX2020F0                  TEXT('(C) Copyright MKI +                         LUC109
     A                                      Limited 1988, 1997')
     A*** LUC007 begin
     A*********                             WINDOW(&SWROW &SWCOL 09 75)
     A*** MUI010 begin
     A*********                             WINDOW(&SWROW &SWCOL 13 67)
     A                                      WINDOW(&SWROW &SWCOL 14 67)
     A*** MUI010 end
     A*** LUC007 end
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A*** MUI009 begin
     A********* H                           HLPDOC(H020@005 SDINS ABSENG/DBAR2)
     A*********                             HLPARA(04 01 04 17)
     A*** MUI009 end
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
     A            STITL         38   O  1 17DSPATR(HI)
      *
     A*** LUC007 begin
     A*********                         3  2'Risk on Italian Branc-
     A*********                             h........'
     A*********                         3 45'(If the risk is on an-'
     A*********                         4 45' Italian branch,enter-'
     A*********                         5 45' the Italian branch code)'
     A*********   SRKBR          3   B  3 35OVRDTA
     A**41*****                             DSPATR(RI PC)
     A*N41*****                             DSPATR(HI)
     A*N39*****                             DSPATR(UL)
     A**39*****                             DSPATR(PR)
     A*********
     A*********                         7  2'F3=Exit  F12=Previous'
      *
     A                                  3  2'Maximum Tenor            :'
     A                                  3 38'(nnM/nnY  No of Months/Years)'
     A            SMAXT          3   B  3 29OVRDTA
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(HI)
     A N39                                  DSPATR(UL)
     A  39                                  DSPATR(PR)
      *
     A                                  4  2'Maximum End Date of Risk :'
     A            SMEDR          6   O  4 29OVRDTA
     A N41                                  DSPATR(HI)
      *
     A                                  5  2'Multiyear Transaction    :'
     A                                  5 38'(Y/N)'
     A            SMYTR          1   B  5 29OVRATR
     A  43                                  DSPATR(RI PC)
     A N43                                  DSPATR(HI)
     A N39                                  DSPATR(UL)
     A  39                                  DSPATR(PR)
     A*
     A                                  6  2'Type of Facility         :'
     A                                  6 38'(C=Ordinary  T=Temporary)'
     A            SFACT          1   B  6 29OVRATR
     A  44                                  DSPATR(RI PC)
     A N44                                  DSPATR(HI)
     A N39                                  DSPATR(UL)
     A  39                                  DSPATR(PR)
     A*
     A                                  7  2'Blocked Facility         :'
     A                                  7 38'(Y/N)'
     A            SBFAC          1   B  7 29OVRATR
     A  45                                  DSPATR(RI PC)
     A N45                                  DSPATR(HI)
     A N39                                  DSPATR(UL)
     A  39                                  DSPATR(PR)
     A*
     A                                  8  2'Proposal Sent to H.O.    :'
     A                                  8 38'(Y/N)'
     A            SPROP          1   B  8 29OVRATR
     A  46                                  DSPATR(RI PC)
     A N46                                  DSPATR(HI)
     A N39                                  DSPATR(UL)
     A  39                                  DSPATR(PR)
     A*
     A                                  9  2'Review Notes             :'
     A            SNOTE         30   B  9 29OVRATR
     A  47                                  DSPATR(RI PC)
     A N47                                  DSPATR(HI)
     A N39                                  DSPATR(UL)
     A  39                                  DSPATR(PR)
     A*
     A*** MUI010 begin
     A**********                       11  2'F3=Exit  F12=Previous'
     A*** LUC007 end
     A*
     A                                 10  2'H.O. Facility Link       :'
     A            SLINK         21   B 10 29OVRATR
     A  48                                  DSPATR(RI PC)
     A N48                                  DSPATR(HI)
     A N39                                  DSPATR(UL)
     A  39                                  DSPATR(PR)
      *
     A                                 12  2'F3=Exit  F12=Previous'
      *
     A*** MUI010 end
     A*.........................................................................
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program Messages')
     A*** LUC007 begin
     A*********                             SFLMSGRCD(8)
     A*** MUI010 begin
     A*********                             SFLMSGRCD(12)
     A                                      SFLMSGRCD(13)
     A*** MUI010 end
     A*** LUC007 end
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*.........................................................................
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program Messages')
     A**********                            WINDOW(LE2020F0)                                  LUC109
     A                                      WINDOW(XX2020F0)                                  LUC109
     A                                      SFLSIZ(03)
     A                                      SFLPAG(01)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  55
     AON55                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*.........................................................................
     A          R DUMMY
     A                                      TEXT('Dummy Format')
     A                                      ASSUME
     A                                  1  3' '
     A*.........................................................................
