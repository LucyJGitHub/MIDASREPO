      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD - Maintain URS Code')                         *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  XX203DF - URS Code Maintenance                               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Last Amend No. ERZ032   *CREATE   Date 24Jan13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  ERZ032 - Customer Details Additional Information             *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CA03(03 'Exit.')
      *================================================================
     A          R XX203F1                   TEXT('(c) Misys International -
     A                                      Banking Systems Ltd. 2013')
     A                                      INDTXT(41 'Invalid: S1URST')
     A                                      INDTXT(42 'Invalid: S1DESC')
     A                                      INDTXT(44 'Invalid: S1SEL' )
     A                                      CA12(12 'Prev.')
     A N70                                  CF10(10 'Delete')
     A                                      OVERLAY
      *
      ** Header details.
     A            USER          10A  O  1  2TEXT('*USER')
     A            ##PGM         10A  O  1 13TEXT('*PROGRAM')
     A            ##RUN          7      1 72TEXT('Midas Run Date')
     A            JOB           10A  O  2  2TEXT('*JOB')
     A                                  2 25'URS Code Maintenance'
     A                                      DSPATR(HI)
     A                                  2 72TIME
      *
      ** Fields details.
     A                                  5  7'URS Code............:'
     A                                  6  7'Description.........:'
     A            S1URST         2A  B  5 30TEXT('URS Code')
     A                                      DSPATR(HI)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A N71N41                               DSPATR(UL)
     A  71                                  DSPATR(PR)
     A            S1DESC        20A  B  6 30TEXT('Description')
     A                                      DSPATR(HI)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A N72N42                               DSPATR(UL)
     A  72                                  DSPATR(PR)
      *================================================================
      ** Subfile record details format.
     A          R #SFLRCD                   SFL
     A  74                                  SFLNXTCHG
     A            S1SEL          1A  B  9  2TEXT('Opt')
     A N44                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A            S1URST         2A  O  9  7TEXT('URS Code')
     A            S1DESC        20A  O  9 18TEXT('Description')
      *================================================================
      ** Subfile control format.
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(25 'Disable OVRDTA')
     A                                      INDTXT(51 'Subfile message ID')
     A                                      INDTXT(60 'Clear subfile')
     A                                      INDTXT(61 'Display subfile')
     A                                      INDTXT(62 'End of subfile')
     A                                      INDTXT(70 'Enquiry Mode')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  60                                  SFLDSP
     A  61                                  SFLDSPCTL
     A N60N61                               SFLCLR
     A  62                                  SFLEND(*MORE)
     A  51                                  SFLMSGID(CPF5203 QCPFMSG)
     A N70                                  CA05(05 'Refresh')
     A                                      ROLLUP(27 'Next page.')
     A N70                                  CF09(09 'Add mode.')
     A                                      SETOF(10)
     A                                      SETOF(12)
     A            UISFRN         4S 0H      SFLRCDNBR(CURSOR)
      *
      ** Header details.
     A            USER          10A  O  1  2TEXT('*USER')
     A            ##PGM         10A  O  1 13TEXT('*PROGRAM')
     A            ##RUN          7      1 72TEXT('Midas Run Date')
     A            JOB           10A  O  2  2TEXT('*JOB')
     A N70                              2 25'URS Code Maintenance'
     A                                      DSPATR(HI)
     A  70                              2 25'URS Code Enquiry'
     A                                      DSPATR(HI)
     A                                  2 72TIME
      *
      ** Fields details.
     A                                  5  7'URS'
     A                                  6  7'Code'
     A                                  5 18'Description'
     A            S2URST         2A  B  7  7TEXT('URS Code')
     A N25                                  OVRDTA
     A N73                                  DSPATR(UL HI)
     A  73                                  DSPATR(PR)
     A            S2DESC        20A  B  7 18TEXT('Description')
     A N25                                  OVRDTA
     A N73                                  DSPATR(UL HI)
     A  73                                  DSPATR(PR)
      *================================================================
      *
      ** First screen footer
      * *IN70 = enquiry
     A          R FOOTER1                   TEXT('Command keys.')
     A     70                          23 01'F3=Main Menu'
     A    N70                          23 01'D=Delete'
     A    N70                          23 11'F3=Main Menu'
     A    N70                          23 25'F5=Refresh'
     A    N70                          23 37'F9=Go to Add Mode'
      *
      *================================================================
      *
      * Add/amend/delete/enquire screen
      * *IN75 = confirm delete screen
      * *IN70 = enquiry
     A          R FOOTER2                   TEXT('Command keys.')
     A                                 23 01'F3=Main Menu'
     A  75                             23 15'F10=Confirm deletion'
     A  75N70                          23 37'F12=Previous'
     A N75                             23 15'F10=Confirm'
     A N75                             23 28'F12=Previous'
      *
      *================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
