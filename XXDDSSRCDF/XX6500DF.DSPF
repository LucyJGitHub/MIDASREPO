      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('MIDAS RE Manual Turnover Commission Update')           *
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  DSPF/XX6500DF - Midas RE Manual Turnover Commission Update   *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. EML105  *Create    Date 05Nov09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  EML105 - Upgrade CFT043 to FBM 2.1 SP22                      *
      *           Exclude FT Transactions from Turnover Commission    *
      *                                                               *
      *****************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CA03(03 'Exit.')
     A                                      CA12(12 'Previous.')
     A*=========================================================================
     A          R #SFLNUM                   SFL
     A                                      TEXT('Subfile record.')
     A            #1TCOM         1A  B  8  3TEXT('Turnover Commission Apply')
     A                                      VALUES('Y' ' ')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            #1TRAT         5S 0O  8  9TEXT('Transaction Type')
     A                                      DSPATR(HI)
     A            #1PSTD         6Y 0O  8 15TEXT('Posting date    ')
     A                                      EDTWRD('0 /  /  ')
     A                                      DSPATR(HI)
     A            #1VALD         6Y 0O  8 24TEXT('Posting date    ')
     A                                      EDTWRD('0 /  /  ')
     A                                      DSPATR(HI)
     A            #1PSTA        15A  O  8 33TEXT('Posting Amount')
     A                                      DSPATR(HI)
     A            #1PNAR        30A  O  8 50TEXT('Posting Narrative')
     A                                      DSPATR(HI)
     A            #1RCNO         9S 0H      TEXT('PF Record No.')
     A            #1TURN         1A  H      TEXT('Turnover Commission')
     A*=========================================================================
     A          R #CTLNUM                   SFLCTL(#SFLNUM)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      TEXT('Subfile control.')
     A                                      OVERLAY
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N80 81                               SFLDSP
     A N80 81 30                            SFLEND
     A N30                                  ROLLUP(27 'Next page.')
     A            #1RRN          4S 0H      SFLRCDNBR
     A            ##PGM         10A  O  1  3TEXT('*PROGRAM')
     A                                  1 27'TURNOVER COMMISSION'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            ##MRDT         7A  O  1 63TEXT('Midas rundate')
     A            ##TME          6Y 0O  1 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                  3  7'Retail Number :'
     A            #RNUM         10Y 0O  3 23DSPATR(HI)
     A                                  4  7'Start Date    :'
     A            #SDAT          6Y 0O  4 25DSPATR(HI)
     A                                      EDTWRD('0 /  /  ')
     A                                  3 40'Last debit capital. date  :'
     A            #LDID          6Y 0O  3 69DSPATR(HI)
     A                                      EDTWRD('0 /  /  ')
     A                                  4 40'Last credit capital. date :'
     A            #LCID          6Y 0O  4 69DSPATR(HI)
     A                                      EDTWRD('0 /  /  ')
     A                                  5  1'Turnover'
     A                                  6  9'Trans'
     A                                  6 16'Posting'
     A                                  6 26'Value  '
     A                                  6 39'Posting'
     A                                  6 53'Posting'
     A                                  6  1'Comm.'
     A                                  7  9'Type'
     A                                  7 17'Date'
     A                                  7 26'Date'
     A                                  7 39'Amount'
     A                                  7 52'Narrative'
     A                                  7  1'Apply'
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A                                 22 51'F12=Previous'
     A                                 22 66'F3=Exit'
     A*=========================================================================
     A          R XX6500F1                  OVERLAY
     A            ##PGM         10A  O  1  3TEXT('*PROGRAM')
     A                                  1 27'TURNOVER COMMISSION'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            ##MRDT         7A  O  1 63TEXT('Midas rundate')
     A            ##TME          6Y 0O  1 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                 08 20'Retail Number :'
     A            #RNUM         10Y 0B 08 36
     A N50                                  DSPATR(UL)
     A N50                                  DSPATR(HI)
     A  50                                  DSPATR(RI)
     A                                 13 20'Start Date    :'
     A            #SDAT          6Y 0B 13 36
     A N51                                  DSPATR(UL)
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(RI)
     A                                 22 66'F3=Exit'
