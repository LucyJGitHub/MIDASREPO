      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Pooling Accounts Maintenance')                *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      ***DSPF/SDJ101D#*-*Pooling*Accounts Maintenance******************         EML110
      *  DSPF/XXJ101D# - Pooling Accounts Maintenance                 *         EML110
      *                                                               *
      ***(c)*Misys*International*Banking*Systems*Ltd.*2005*************         EML110
      *  (c) Finastra International Limited 2018                      *         EML110
      *                                                               *
      *  Last Amend No. EML110             Date 20Jan20               *
      *  Prev Amend No. TFJ011  *CREATE    Date 19Jan07               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  EML110 - Account Balance Pooling. Upgrade to FBM 2.1         *
      *  TFJ011 - Account Balance Pooling                             *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *=========================================================================
      *
      ** Start of EML110
     A********* R #SFLRCD                   TEXT('(c) Misys International +
     A*********                             Banking Systems Ltd. +
     A*********                             2005')
     A          R #SFLRCD                   TEXT('(c) Finastra Internation+
     A                                      al Limited 2018')
      ** End of EML110
      *
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(44 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1RECI         1   H      TEXT('RECI')
     A            #1BRCA         3   H      TEXT('Branch')
     A            #1CNUM         6   H      TEXT('Customer')
     A            #1CCY          3   H      TEXT('Currency')
     A            #1ACOD        10   H      TEXT('Account Code')
     A            #1ACSQ         2   H      TEXT('Account Seq')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Last Change Type')
     A            #1LTME         4  0H      TEXT('Last Change Time')
     A            #1SEL          1   B  8  2
     A                                      VALUES('E' +
     A                                             'A' +
     A                                             'D' +
     A                                                 )
     A                                      CHECK(AB)
     A  44                                  DSPATR(RI PC)
     A N44                                  DSPATR(UL HI)
     A            #1ACNT        28      8  4TEXT('Account')
     A                                      DSPATR(HI)
     A            #1ACNO        10      8 33TEXT('Account Number')
     A                                      DSPATR(HI)
     A            #1ANAM        16      8 44TEXT('Account Name')
     A                                      DSPATR(HI)
     A            #1CHMA         1      8 61TEXT('Schema Actual')
     A                                      DSPATR(HI)
     A            #1CHMS         1      8 63TEXT('Schema Sweep')
     A                                      DSPATR(HI)
     A            #1CHMT         1      8 65TEXT('Schema Transfer')
     A                                      DSPATR(HI)
     A            #1CHMI         1      8 67TEXT('Schema Inter-Office')
     A                                      DSPATR(HI)
     A            #1BRCH         3      8 69TEXT('Internal Branch')
     A                                      DSPATR(HI)
     A            #1LAMD         7      8 73TEXT('Last Amend Date')
     A                                      DSPATR(HI)
      *=========================================================================
      *
     A          R #SFLCTL                   TEXT('Subfile control.')
      *
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF09(09 'Add.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2BRCA')
     A                                      SETOF(32 'Invalid: #2CNUM')
     A                                      SETOF(33 'Invalid: #2CCY')
     A                                      SETOF(34 'Invalid: #2ACOD')
     A                                      SETOF(35 'Invalid: #2ACSQ')
     A                                      SETOF(36 'Invalid: #2ACNO')
     A                                      SETOF(37 'Invalid: #2ANAM')
     A                                      SETOF(38 'Invalid: #2CHMA')
     A                                      SETOF(39 'Invalid: #2CHMS')
     A                                      SETOF(40 'Invalid: #2CHMT')
     A                                      SETOF(41 'Invalid: #2CHMI')
     A                                      SETOF(42 'Invalid: #2BRCH')
     A                                      SETOF(43 'Invalid: #2LAMD')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
      ** Start of EML110
     A***********                       1 13'SDJ101D1'
     A                                  1 13'XXJ101D1'
      ** End of EML110
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##URPT        53      2 13TEXT('User Report Title')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #2RECI         1   H      TEXT('RECI')
     A            #2LCD          5  0H      TEXT('Last Change Date')
     A            #2TYLC         1   H      TEXT('Last Change Type')
     A            #2LTME         4  0H      TEXT('Last Change Time')
     A                                  4  4'Account'
     A                                  4 33'Retail'
     A                                  4 44'Account Name'
     A                                  4 61'Scheme'
     A                                  4 69'Int'
     A                                  4 74'Last'
      *
     A                                  5 33'A/C Number'
     A                                  5 69'Brc'
     A                                  5 74'Amend'
     A            #2BRCA         3   B  6  4TEXT('Branch')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2CNUM         6   B  6  8TEXT('Customer')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2CCY          3   B  6 15TEXT('Currency')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2ACOD        10   B  6 19TEXT('Account Code')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2ACSQ         2   B  6 30TEXT('Account Seq')
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2ACNO        10   B  6 33TEXT('Account Number')
     A N25                                  OVRDTA
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2ANAM        16   B  6 44TEXT('Account Name')
     A N25                                  OVRDTA
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2CHMA         1   B  6 61TEXT('Schema Actual')
     A N25                                  OVRDTA
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2CHMS         1   B  6 63TEXT('Schema Sweep')
     A N25                                  OVRDTA
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2CHMT         1   B  6 65TEXT('Schema Transfer')
     A N25                                  OVRDTA
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2CHMI         1   B  6 67TEXT('Schema Inter-Office')
     A N25                                  OVRDTA
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2BRCH         3   B  6 69TEXT('Internal Branch')
     A N25                                  OVRDTA
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2LAMD         7   B  6 73TEXT('Last Amend Date')
     A N25                                  OVRDTA
     A  43                                  DSPATR(RI PC)
     A N43                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *=========================================================================
      *
     A          R #CMDTXT1                  TEXT('Command keys')
      *
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
      *=========================================================================
      *
     A          R #MSGRCD                   TEXT('Program messages')
      *
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
      *
     A          R #MSGCTL                   TEXT('Program messages')
      *
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *=========================================================================
      *
