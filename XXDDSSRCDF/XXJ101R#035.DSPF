      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Pooling Accounts Maintenance')                *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      ***DSPF/SDJ101R#*-*Pooling*Accounts Maintenance******************         JMI108
      *  DSPF/XXJ101R# - Pooling Accounts Maintenance                 *         JMI108
      *                                                               *
      ***(c)*Misys*International*Banking*Systems*Ltd.*2005*************         JMI108
      *  (c) Finastra International Limited 2018                      *         JMI108
      *                                                               *
      *  Last Amend No. JMI108             Date 31May18               *
      *  Prev Amend No. TFJ011  *CREATE    Date 19Jan07               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  JMI108 - Account Balance Pooling. Upgrade to FBM 2.1         *
      *  TFJ011 - Account Balance Pooling                             *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *=========================================================================
      *
      ** Start of JMI108
     A********* R #RCDKEY                   TEXT('(c) Misys International +
     A*********                             Banking Systems Ltd. +
     A*********                             2005')
     A          R #RCDKEY                   TEXT('(c) Finastra Internation+
     A                                      al Limited 2018')
      ** End of JMI108
      *
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
      * Command keys............................................................
     A                                      CA12(12 'Key screen.')
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CF09(09 'Change mode.')
      * SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1ACCD')
      *.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
      ** Start of JMI108
     A************                      1 13'SDJ101R1'
     A                                  1 13'XXJ101R1'
      ** End of JMI108
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 29'New Pooling Accounts'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  2'Account'
     A            #1ACCD        24   B  4 10TEXT('Account')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *=========================================================================
      *
     A          R #CMDTXT1                  TEXT('Command keys')
      *
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
      *=========================================================================
      *
     A          R #RCDDTL1                  TEXT('Details')
      *
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
      * Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A  85                                  CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A N89                                  CF10(10 'Delete.')
      * SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1CHMA')
     A                                      SETOF(32 'Invalid: #1CHMS')
     A                                      SETOF(33 'Invalid: #1CHMT')
     A                                      SETOF(34 'Invalid: #1CHMI')
     A                                      SETOF(35 'Invalid: #1BRCH')
      *.........................................................................
      *
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
      ** Start of JMI108
     A*************                     1 13'SDJ101R1'
     A                                  1 13'XXJ101R1'
      ** End of JMI108
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A N70                              2 29'Pooling Accounts Maintenance'
     A                                      DSPATR(HI)
     A  70N71                           2 29'Pooling Accounts Enquiry'
     A                                      DSPATR(HI)
     A  70 71                           2 29'Pooling Accounts Maintenance  '
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1RECI         1   H      TEXT('RECI')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Last Change Type')
     A            #1LTME         4  0H      TEXT('Last Change Time')
     A                                  4  2'Account'
     A            #1ACNT        28      4 10TEXT('Account')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4 49'Retail Account No.'
     A            #1ACCN        10      4 68TEXT('Retail Account No.')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1ANAM        20      6 10TEXT('Account Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *
     A                                  8  2'Scheme'
     A                                  8 10'Actual Pooling............:'
     A                                  8 40'(A)'
     A            #1CHMA         1   B  8 38TEXT('Schema Actual')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31N70                               DSPATR(UL)
     A N31                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N25                                  OVRATR
      *
     A                                  9 10'Sweep.....................:'
     A                                  9 40'(S)'
     A            #1CHMS         1   B  9 38TEXT('Schema Sweep')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32N70                               DSPATR(UL)
     A N32                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N25                                  OVRATR
      *
     A                                 10 10'Transfer Account..........:'
     A                                 10 40'(T)'
     A            #1CHMT         1   B 10 38TEXT('Schema Transfer')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33N70                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N25                                  OVRATR
      *
     A                                 11 10'Inter-office..............:'
     A                                 11 40'(I)'
     A            #1CHMI         1   B 11 38TEXT('Schema Inter-Office')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34N70                               DSPATR(UL)
     A N34                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N25                                  OVRATR
      *
     A                                 13  2'Internal Branch Code.............+
     A                                      .:'
     A            #1BRCH         3   B 13 38TEXT('Internal Branch')
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35N70                               DSPATR(UL)
     A N35                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N25                                  OVRATR
      *
      *
      *=========================================================================
      *
     A          R #CMDTXT2                  TEXT('Command keys')
      *
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
      *=========================================================================
      *
     A          R #MSGRCD                   TEXT('Program messages')
      *
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
      *
     A          R #MSGCTL                   TEXT('Program messages')
      *
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *=========================================================================
      *
