      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Pooling Account job status')                  *
/*S*D****DSPF**********************************************************
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      ***DSPF/SDJ104DF*-*Midas*SD*Pooling Account Job Status***********         JMI108
      *  DSPF/XXJ104DF - Midas SD Pooling Account Job Status          *         JMI108
      *                                                               *
      ***(c)*Misys*International*Banking*Systems*Ltd.*2005*************
      *  (c) Finastra International Limited 2018                      *         JMI108
      *                                                               *         JMI108
      *  Last Amend No. JMI108             Date 31May18               *
      *  Prev Amend No. TFJ011  *CREATE    Date 22Jan07               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  JMI108 - Account Balance Pooling. Upgrade to FBM 2.1         *
      *  TFJ011 - Account Pooling                                     *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *=========================================================================
      ** Start of JMI108
     A********* R #RCDKEY                   TEXT('(c) Misys International +
     A*********                             Banking Systems Ltd. +
     A*********                             2005')
     A          R #RCDKEY                   TEXT('(c) Finastra Internation+
     A                                      al Limited 2018')
      ** End of JMI108
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
      * SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
      *.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
      ** Start of JMI108
     A************                      1 13'SDJ104'
     A                                  1 13'XXJ104'
      ** End of JMI108
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
     A                                  2 18'Account Pooling Activation/+
     A                                      Deactivation'
     A                                      DSPATR(HI)
     A                                      OVRATR
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('Details Account Pooling ICD')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(87 'Protect fields')
     A                                      VLDCMDKEY(88 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CA05(05 'Refresh.')
     A*-------------------------------------------------------------------------
     A                                  6  2'Action'
     A                                  6 15'Processing'
     A                                  6 39'Status'
     A                                  7 15'Task'
     A            SACTNR         1   B 10  4TEXT('Action')
     A                                      OVRDTA
     A  87                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31N87                               DSPATR(UL)
     A N31                                  DSPATR(HI)
     A                                 10 15'A/C Pool Receive'
     A                                      DSPATR(HI)
     A            SREC          20   O 10 39TEXT('Receive job status')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            SACTNS         1   B 11  4TEXT('Action')
     A                                      OVRDTA
     A  87                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32N87                               DSPATR(UL)
     A N32                                  DSPATR(HI)
     A                                 11 15'A/C Pool Send'
     A                                      DSPATR(HI)
     A            SSEND         20   O 11 39TEXT('Send job status')
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 24  2'F3=Main Menu'
     A                                 24 16'F5=Refresh'
     A N87                             24 28'S=Start job'
     A N87                             24 41'T=Terminate job'
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(23)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(55)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
