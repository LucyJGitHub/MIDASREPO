      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_NM                                                        *
/*EXI *  TEXT('Midas SE Position Settlements - Client/Market')        *
/*ADD *  ADDPFM FILE(POSETD) MBR(CPOSN1) SHARE(*YES)                  *
/*ADD *  ADDPFM FILE(POSETD) MBR(CPOSN2) SHARE(*YES)                  *
/*ADD *  ADDPFM FILE(POSETD) MBR(DPOSN1) SHARE(*YES)                  *
/*ADD *  ADDPFM FILE(POSETD) MBR(DPOSN2) SHARE(*YES)                  *
/*ADD *  ADDPFM FILE(POSETD) MBR(REDEMP) SHARE(*YES)                  *
/*ADD *  ADDPFM FILE(POSETD) MBR(CORPAC) SHARE(*YES)                  *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      ***PF/POSETDX4**-**POSITION*SETTLEMENTS*-*CLIENT/MARKET**********                  HUT105
      *  PF/XXPOSEX4  -  POSITION SETTLEMENTS - CLIENT/MARKET         *                  HUT105
      *                                                               *
      ***(c)*Misys*International*Banking*Systems*Ltd.*2008*************                  HUT105
      *  (c) Finastra International Systems Ltd. 2020                 *                  HUT105
      *                                                               *
      *  Last Amend No. HUT105             Date 07Apr20               *
      *  Prev Amend No. HUT011   *CREATE   Date 12May08               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  HUT105 - Bulk Position Settlement Authorisation              *
      *           (Upgrade HUT011/015 to FM2.1 SP22)                  *
      *  HUT011 - Bulk Position Settlement Authorisation              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      ** Start of HUT105
     A**********R POSETDD4                  TEXT('(c) Misys International +
     A**********                            Banking Systems Ltd. +
     A**********                            2008')
     A          R POSETDD4                  TEXT('(c) Finastra Internation+
     A                                      al Systems Ltd. 2020')
      ** End of HUT105
     A            RECI      R
     A            PBCA      R
     A            PSSH      R
      ** Start of HUT105
     A**********  PCPY      R
     A            PCPY      R    6A
      ** End of HUT105
     A            PMAR      R
     A            PDUD      R
     A            PEVT      R
     A            PAUI      R
     A            PNMP      R
     A            PNCY      R
     A            PBCD      R
     A            PAMD      R
     A            PPRE      R
     A            PSCU      R
     A            PSXR      R
     A            PMDI      R
     A            PBRX      R
     A            PSMT      R
     A            PSEAQQ        12A
     A            PSEB      R
     A            PCPN      R
     A            PFAC      R
     A            PSPI      R
     A            PCHC      R
     A            PCHA      R
     A            PCCD      R
     A            PCAM      R
     A            PSAT      R
     A            PSMI      R
     A            PDTG      R
     A            LCD       R
     A            CHTP      R
     A            TNLU      R
     A            PPFC      R
     A            TASO      R               REFFLD(TAXA)
     A                                      TEXT('Tax ded. at source')
     A            TASR      R               REFFLD(TAXA)
     A                                      TEXT('Tax ded.at srce ref.')
     A            TAUS      R               REFFLD(TAXA)
     A                                      TEXT('Tax ded. by user')
     A            PSVL      R               REFFLD(PDUD)
     A                                      TEXT('Value Date')
     A                                      COLHDG('Value +
     A                                      Date')
     A            PTFR      R
     A            SRPG      R               REFFLD(IND)
     A                                      TEXT('Settle Reversal Postings Gen')
     A                                      COLHDG('Reversal' 'Pos Sttls' 'Gen')
     A            SABC      R               REFFLD(NOMN)
     A                                      TEXT('Sett Amount in Base Ccy')
     A                                      COLHDG('Settl Amount' 'in Base Ccy')
     A            BAUS      R               REFFLD(BCEQ)
     A                                      TEXT('Base currency equivalent of +
     A                                            TAUS')
     A                                      COLHDG('B/C equiv'+
     A                                             'Of'+
     A                                             'TAUS')
     A            PSCF      R               TEXT('Posn Sett.Conf. Produced')
     A                                      REFFLD(RECI)
     A            FXMP      R               REFFLD(FWPP)
     A                                      TEXT('FX Margin Points')
     A                                      COLHDG('FX' +
     A                                             'Margin' +
     A                                             'Points')
      *
     A            FXMR      R               REFFLD(TDER)
     A                                      TEXT('FX Margin Rate')
     A                                      COLHDG('FX' +
     A                                             'Margin' +
     A                                             'Rate')
      *
     A            MAMT      R               REFFLD(AABC)
     A                                      TEXT('Margin Amount')
     A                                      COLHDG('Margin' +
     A                                             'Amount')
      *                                              End   /CAC002/
     A            TXBS      R               REFFLD(ZZ002)
     A                                      TEXT('Tax basket')
     A                                      COLHDG('Tax' 'basket')
      *
     A            TXBN      R               REFFLD(ZZ015)
     A                                      TEXT('Tax basket narrative')
     A                                      COLHDG('Tax basket nar-' +
     A                                             'rative')
      *
     A            EXCD      R               REFFLD(ZZ002)
     A                                      TEXT('Exemption code')
     A                                      COLHDG('Exempt' 'code')
      *
     A            CRTX      R               REFFLD(CCOD)
     A                                      TEXT('Country of tax')
     A                                      COLHDG('Country' +
     A                                             'of tax')
      *
     A            RPCD      R               REFFLD(ZZ002)
     A                                      TEXT('Recipient code')
     A                                      COLHDG('Recipient' 'Code')
      *
     A            ICTP      R               REFFLD(ZZ002)
     A                                      TEXT('Income type')
     A                                      COLHDG('Income' +
     A                                             'type')
      *
     A            BNFO      R               REFFLD(ZZ026)
     A                                      TEXT('Beneficial owner number')
     A                                      COLHDG('Beneficial owner' +
     A                                             'number')
      *
     A            MT5R      R               REFFLD(ZZ001)
     A                                      TEXT('MT565 message required Y/N')
     A                                      COLHDG('MT565' 'Req' 'Y/N')
      *
     A            MT5G      R               REFFLD(ZZ001)
     A                                      TEXT('MT565 generated Y/N/R')
     A                                      COLHDG('MT565' 'Gen' 'Y/N/R')
      *
     A            CTYP      R               REFFLD(ZZ001)
     A                                      TEXT('Customer type')
     A                                      COLHDG('Cust' 'type')
      *
     A            PREF      R               REFFLD(ZZ006)
     A                                      TEXT('Position settlement ref no.')
     A                                      COLHDG('Position' 'Sett.' 'ref no.')
      *
     A            PSEQ      R               REFFLD(DTSEQN)
     A                                      TEXT('Sequence number')
     A                                      COLHDG('Seq' ' No.')
      *
     A            CTIN      R               REFFLD(ZZ020)
     A                                      TEXT('Customer Tax Id number')
     A                                      COLHDG('Customer tax' 'Id number')
      *
     A            DAMT      R               REFFLD(AMNT)
     A                                      TEXT('Total discounted amount')
     A                                      COLHDG('Total discount' ' amount')
      *
     A            DEPT      R               REFFLD(CNUMA)
     A                                      TEXT('Depot')
     A                                      COLHDG('Depot')
     A            PSEA          18A
     A            TXNM      R               REFFLD(TAXA)
     A                                      TEXT('EU Tax Amount in Nom Ccy')
     A                                      COLHDG('EU Tax Amount in ' +
     A                                      'Nom Ccy')
     A            TXST      R               REFFLD(TAXA)
     A                                      TEXT('EU Tax Amount in Set Ccy')
     A                                      COLHDG('EU Tax Amount in ' +
     A                                      'Set Ccy')
     A            TAXA      R               REFFLD(TAXA)
     A                                      TEXT('EU Tax Amount in Tax Ccy')
     A                                      COLHDG('EU Tax Amount in ' +
     A                                      'Tax Ccy')
     A            TXCY      R               REFFLD(CCYD)
     A                                      TEXT('EU Tax Ccy')
     A                                      COLHDG('EU Tax Ccy')
     A            PRBA           1A         TEXT('Private Banking Customer')
     A                                      COLHDG('Private' 'Banking' 'Cust')
