      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_RB                                                        *
/*EXI *  TEXT('Midas XX SE Delete Reversed Book Posn Actions')        *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  JLF/XXBKPHDJ - XX Delete Reversed Book Posn Actions          *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. DUG504 *CREATE      Date 07Sep17              *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  DUG504 - Cost of Funds                                       *
      *                                                               *
      *****************************************************************
      *
     A                                      DYNSLT
     A          R BKPHDJFF                  JFILE(BKPHDD INVTPD SECTYD)
     A                                      TEXT('(c) Finastra International  +
     A                                      Limited +
     A                                      2020')
      *  Join Book Positions Header to Investment Types
     A          J                           JOIN(1 2)
     A                                      JFLD(NCRY CCYI)
     A                                      JFLD(INVT INVT)
      *  Join Book Positions Header to Security Details
     A          J                           JOIN(1 3)
     A                                      JFLD(BHSC SESN)
      *  Book Position Header Fields
     A            RECI                      JREF(1)
     A            BHBA
     A            BHBK
     A            NCRY
     A            BHSC
     A            BHMK
     A            BHTV
     A            LPSD
      *  Investment Type Fields
     A            PROT
      *  Security Fields
     A            NMDP
     A            MATY
     A            VLCY
     A            MLTI
     A            PPDI
     A            SPBS
     A            SINP
     A            SFPP
     A            CFCT
     A            SEXR
     A            SMDI
      *
     A          K BHBA
     A          K BHBK
     A          K BHSC
     A          K BHMK
     A          S BHTV                      COMP(EQ 'V')
