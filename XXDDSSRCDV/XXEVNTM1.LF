      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_DL                                                        *
/*EXI *  TEXT('Midas XX Merged events for GL0554')                    *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *     LF/XXEVNTM1 - MERGED EVENTS FILE FOR GL0554               *
      *                                                               *
      *         This file is based on EVNTM1 but without the Omit of  *
      *         AUTO SETTLE <> 'Y'                                    *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. LUC137  *CREATE    Date 28Aug20               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC137 - Include Non-Auto Settle Loans Events on GL0580      *
      *                                                               *
     A*****************************************************************
     A          R EVNTXECF                  PFILE(EVNTXEC)
     A                                      TEXT('(c) Finastra Internation+
     A                                      al Limited 2020')
     A          K RECI
     A          R EVNTXEDF                  PFILE(EVNTXED)
     A            RECI
     A            DLNO
     A            CUNR
     A            ETYP
     A            EDAT
     A            EAMT
     A            ECCY
     A            ECSS
     A            DTYP
     A            DLST
     A            EBRC
     A            EXRT
     A            NGII
     A            NOTD
     A            OTHD
     A            OTHA
     A            OTHC
     A            DBCE
     A            BCEQ
     A            DBCR
     A            LCEQ
     A            SETP
     A            OSAC                      CONCAT(OSAC1 OSAC2)
     A            TSEN
     A            ZZ003
     A            FIPI
     A            INOI
     A            FCTI
     A            RLVI
     A            PTNU
     A            PARI
     A            CRNM
     A            CTWN
     A            CASK
     A            PRNM
     A            PTWN
     A            PASK
     A            CCTZ
     A            CLOC
     A            REGC
     A            DASN
     A            FFVD
     A            FFIN
     A            FACT
     A            FCNO
     A            FACO
     A            SPI
     A            CPCN
     A            FSLI
     A            ICIR
     A            ICAM
     A            RBDA
     A            OCFF
     A            ORBR
     A            COOB
     A            ECPY
     A            IBRE
     A            ICYE
     A            EXEI
     A            ELVB
     A            ELVC
     A            ELVS
     A            OSBR
     A            TAXI
     A            FUID
     A            RBDN
     A            IPSI
     A            IFXS
     A            SIAC
     A            ETAX
     A            CLAS
     A          K *NONE
     A          K ECCY
     A          K EDAT
     A          K ETYP
     A          S IBRE                      VALUES('N')
     A            DTYP                      COMP(NE 'IL')
     A            DTYP                      COMP(NE 'ID')
     A          R CEVNXCCF                  PFILE(CEVNXCC)
     A          K *NONE
     A          K ECCY
     A          K EDAT
     A          K ETYP
     A          R LVENTELF                  PFILE(LVENTEL)
     A            RECI
     A            LNRF
     A            CUNR
     A            ETYP
     A            EDAT
     A            EAMT
     A            ECCY
     A            ECSS
     A            LTYP
     A            SUTP
     A            EBRC
     A            INTR
     A            NGII
     A            OTHD
     A            OTHA
     A            BCEQ
     A            DFCE
     A            SETP
     A            OSAC
     A            TSEN
     A            ECDP
     A            CRNM
     A            CTWN
     A            CASK
     A            PRNM
     A            PTWN
     A            PASK
     A            INOI
     A            CRSK
     A            CDOM
     A            REGC
     A            PARI
     A            PTNU
     A            RCSI
     A            FACT
     A            FCNO
     A            RDOM
     A            LASN
     A            FREO
     A            WTIN
     A            WTRT
     A            CPAM
     A            AUTO
     A            MRIN
     A            ACOF
     A            OSBR
     A            ECPY
     A            IBRE
     A            ICYE
     A            EXEI
     A            ELVB
     A            ELVC
     A            ELVS
     A            BRCA
     A            COBB
     A            OLNO
     A            IFXC
     A            SIAC
     A            CLAS
     A          K *NONE
     A          K ECCY
     A          K EDAT
     A          K ETYP
     A*/COPY WNCPYSRC,EVNTM1001
     A          S IBRE                      VALUES('N')
     A          O                           ALL
     A          R BVNTXEDF                  PFILE(BVNTXED)
     A            RECI
     A            LCNO           6
     A            CNUM
     A            ETYP
     A            EDAT
     A            EAMT
     A            ECCY
     A            ECSS
     A            DTYP
     A            DLST
     A            BRCA
     A            OTHD
     A            OTHA
     A            OTHC
     A            DBCE
     A            BCEQ
     A            DBCR
     A            SETP
     A            OSAC                      CONCAT(OSAC1 OSAC2)
     A            TSEN
     A            FACO
     A            SPI
     A            FIPI
     A            INOI
     A            CPCN
     A            CPAR
     A            PARI
     A            CRNM
     A            CTWN
     A            CASK
     A            PRNM
     A            PTWN
     A            PASK
     A            CCTZ
     A            CLOC
     A            REGC
     A            FFIN
     A            LCEQ
     A            OSBR
     A            ZZ070
     A            TIRF
     A            TICM
     A          K *NONE
     A          K ECCY
     A          K EDAT
     A          K ETYP
     A          R FPVNTDDF
     A                                      PFILE(FPVNTDD)
     A          K *NONE
     A          K ECCY
     A          K EDAT
     A          K ETYP
     A          S IBRE                      VALUES('N')
     A          R FOEVEDF                   PFILE(FOEVED)
     A                                      TEXT('FO EVENTS DETAIL')
     A          K *NONE
     A          K ECCY
     A          K EDAT
     A          K ETYP
     A          R TREVEDF                   PFILE(TREVED)
     A          K *NONE
     A          K TRSC
     A          K TRVD
     A          K TREC
     A          O COAF                      COMP(EQ 'Y')
     A            TRPI                      COMP(EQ 'Y')
     A          S IBRE                      VALUES('N')
     A          R CPEVEDF                   PFILE(CPEVED)
     A          K *NONE
     A          K CCEC
     A          K CPYD
     A          K CCCE
     A          O CCCE                      VALUES('    ')
     A          R DPEVEDF                   PFILE(DPEVED)
     A          K *NONE
     A          K CCEC
     A          K CCPD
     A          K CCCE
     A          O CCCE                      VALUES('    ')
     A          S IBRE                      VALUES('N')
     A          R BPEVEDF                   PFILE(BPEVED)
     A          K *NONE
     A          K CCEC
     A          K CCPD
     A          K CCCE
     A          O CCCE                      VALUES('    ')
     A          R GLEVNTDF                  PFILE(GLEVNTPD)
     A          K *NONE
     A          K ECCY
     A          K EDAT
     A          K ETYP
     A          S IBRE                      VALUES('N')
     A          R EVNTXZZF                  PFILE(EVNTXZZ)
     A          K RECI
     A          R CEVNXZZF                  PFILE(CEVNXZZ)
     A          K RECI
     A          R LVENTZZF                  PFILE(LVENTZZ)
     A          K RECI
     A          R BVNTXEZF
     A                                      PFILE(BVNTXEZ)
     A          K RECI
     A          R FPVNTZZF                  PFILE(FPVNTZZ)
     A          K RECI
     A          R FOEVEZZF                  PFILE(FOEVEZZ)
     A          K RECI
     A          R GLEVNTZF                  PFILE(GLEVNTZZ)
     A          K RECI

