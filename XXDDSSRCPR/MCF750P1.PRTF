      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas MC Management Accounting Balance Reporting')     *
      *****************************************************************
      *                                                               *
      *  Midas Management Accounts Module                             *
      *                                                               *
      *  MCF750P1 - Management Accounting Balance Reporting           *
      *                                                               *
      *  (c) Copyright Finastra Systems Technology, Inc. 2020         *
      *                                                               *
      *  Last Amend No. EML108             Date 13Jan20               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  EML108 - Upgrade FMD003 to FBM2.1SP21                        *
      *  FMD003 - Management Accounts Automatic Balance Update        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      **
     A          R WX01                      SKIPB(3)
     A                                      TEXT('(C) Copyright +
     A                                      Finastra Systems +
     A                                      2020')
     A                                     1'REFERENCE MCF750P1'
     A            BJURPT        53A       39
     A                                   100'DATE'
     A            BJMRDT         7A      105
     A                                   122'PAGE'
     A                                   127PAGNBR
     A                                      EDTCDE(J) SPACEA(1)
      *
     A                                   100'TIME'
     A                                   105TIME EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    40'MANAGEMENT ACCOUNTS -'
     A            TRMTYP        25A       62SPACEA(1)
      *
     A            G5RSNM        30A       48SPACEA(1)
      *
     A            PRTDT         50A       51SPACEA(1)
      *
      *
      *
     A                                     1'CURRENCY - '
     A            RPCCY         32A       12
      *
     A  73                                93'BRANCH - '
     A  73        A8BRNM        30       102
      *
     A  74                                92'COMPANY - '
     A  74        APCMNM        30       102
      *
     A  75                                80'CONSOLIDATED'
      *
     A                                   132' ' SPACEA(1)
      *
     A                                    80'MANAGEMENT ACCOUNTS -'
     A            PRCGDS        30A      102SPACEA(1)
      *
     A  88        FMDTX         15A       94
     A                                   132' ' SPACEA(1)
     A            FMDT1X        15A       33
     A            FMDT2X        15A       59
     A            FMDT3X        15A       79
     A            FMDT4X        15A       94
     A            FMDT5X        15A      114
     A                                   132' ' SPACEA(2)
      *
     A          R WX02                      SPACEA(1)
      *
     A  71N81 25                          32'------------------'
     A  71N81 25                          54'-----------------'
     A  71N81 25                          73'-----------------'
     A  71N81 25                          92'------------------'
     A  88 71N81
     AA 25                               113'------------------'
     A    N81 25                         132' ' SPACEA(1)
      *
     A  61        NMX1          30         1
      *
     A  62        NMX2          30         1
      *
     A  63        NMX3          30         1
      *
     A  64        NMX4          30         1
      *
     A  65        NMX5          30         1
      *
     A  66        NMX6          30         1
      *
     A  67        NMX7          30         1
      *
     A  68        NMX8          30         1
      *
     A  69        NMX9          30         1
      *
     A  71        RAMT1X        21        31
     A  71        RAMT2X        19        54
     A  71        RAMT3X        19        73
     A  71        RAMT4X        21        91
     A  71        RAMT5X        21       112
     A  71 81                             32SPACEB(1) +
     A                                      '=================='
     A  71 81                             54'================='
     A  71 81                             73'================='
     A  71 81                             92'=================='
     A  71 81 88                         113'=================='
      *
     A*
      *
     A          R DX01                      SPACEA(1)
     A            DESC          30         1
     A*
     A  52        XBCURR         3         2
     A*
     A* Detail value
     A            DAMT1X        21        31
     A            DAMT2X        19        54
     A            DAMT3X        19        73
     A            DAMT4X        21        91
     A            DAMT5X        21       112
      *
      *
      ** End of Report Format
      *
     A          R WX03                      TEXT('End of Report Format')
     A                                      SKIPB(58)
     A                                    56'*** End of'
     A  73                                67'Branch Report ***'
     A  74                                67'Company Report ***'
     A N73N74                             67'Report ***'
