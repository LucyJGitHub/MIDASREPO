      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('CNAPS Code - Details Report')                          *
     ******************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/XX000024P1 - CNAPS Code Details Report                  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. HUT110   *Create   Date 17Apr20               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  HUT110 - Upgrade of HUT011 to FBM 2.1 SP22.                  *
      *           RTGS Via SWIFT                                      *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      **  Report Main Heading
      *
     A          R XX0024H1
     A                                      SKIPB(003)
     A                                      TEXT('(c) Finastra International Li-
     A                                      mited 2001          ')
     A                                     3
     A                                      'Reference'
     A                                    +1
     A                                      'XX000024P1'
     A            BJURPT        53        39
     A                                   +10
     A                                      'DATE:'
     A            BJMRDT         7       108
     A                                   121
     A                                      'Page'
     A                                   126
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                   102
     A                                      'TIME:'
     A                                      SPACEB(001)
     A                                    +1
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    54
     A                                      'CNAPS Code - Detail Report'
     A                                      SPACEB(001)
     A                                    54
     A                                      '--------------------------'
     A                                      SPACEB(001)
      *
      **  Report Sub-Heading
      *
     A          R XX0024H2
     A                                      SPACEB(002)
     A                                     6
     A                                      'CNAPS Code'
     A                                    +4
     A                                      'Bank'
     A                                   +33
     A                                      'Branch'
     A                                    89
     A                                      'Customer'
     A                                    +6
     A                                      'City'
      *
      **  Report details
      *
     A          R XX0024D1
     A                                      SPACEB(001)
      *
     A            PRCODE        12A  O     6
     A            PRBKNM        35A  O    +1
     A            PRBRNM        30A  O    +2
     A            PRCUST         6A  O    +2
     A            PRCITY        20A  O    +8
     A                                    10
     A                                      'Address:'
     A                                      SPACEB(001)
     A            PRADR1        35A  O    +2
     A            PRADR2        35A  O    +2
     A            PRADR3        35A  O    +2
      *
      ** Report Trailer - No Details
      *
     A          R XX0024T1
     A                                      SPACEB(002)
     A                                    52
     A                                      '**** NO DETAILS TO REPORT ****'
      *
      ** Report Trailer - End of Report
      *
     A          R XX0024T2
     A                                      SPACEB(003)
     A                                    56
     A                                      '*** END OF REPORT ***'
      *
