      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Daily Movements Extract P1')                           *                       LUC109
      *****************************************************************
      *                                                               *
      *  Midas - Interface to Head office                             *
      *                                                               *
      ***GLM035P1*-*Daily*Movements*Extract****************************                       LUC109
      *  XX000035P1 - Daily Movements Extract                         *                       LUC109
      *                                                               *
      ***(C)*COPYRIGHT*MKI*LIMITED*1993,*1994**************************                       LUC109
      *  (c) Misys International Banking Systems Ltd. 2016            *                       LUC109
      *                                                               *
      *  Last Amend No. LUC109             Date 16Sep16               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas DBA 2.08 -------------- Base ---------------------------*
      *  Prev Amend No. MMI043             Date 14Dec98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC109 - HO Reporting (Upgrade to FB Midas 2.1)              *
      *  MMI043 - Balance Sheet Extract                               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  C R E A T I O N     P A R A M E T E R S                      *
      *                                                               *
      *****************************************************************
      *
      ** Start of LUC109
     A**********R P1HEAD                    TEXT('(C) MKI +                                   LUC109
     A**********                            SYSTEMS INTERNATIONAL +                           LUC109
     A**********                            LTD.1998')                                        LUC109
     A          R P1HEAD                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2016')
      ** End of LUC109
      *
     A                                      SKIPB(1)
     A                                     2'BRANCH'
     A            P1BRNM        30A       11
     A            P1RUND        10A      106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    43'FILE "GIOBOL" RECONCILIATION AS OF'
     A            P1RPTD        10A       78
     A                                      SPACEA(1)
      *
     A          R P1SUB1
     A                                      SPACEB(1)
     A                                     2'Currency.....'
     A            P1CYCD         3A       15
     A                                      SPACEA(2)
      *
     A          R P1SUB2
     A                                     2'DB POSTINGS REPORTED'
     A            P1DBIT        22A       24
     A                                    48'CR POSTINGS REPORTED'
     A            P1CRDT        22A       69
     A                                    94'DIFFERENCE'
     A            P1NETT        22A      105
      *
     A          R P1COLM                    SPACEB(2)
     A                                     7'Account'
     A                                    27'Postings Reported'
     A                                    55'Previous Day Balance'
     A                                    83'Current Balance'
     A                                   109'Difference'
     A                                      SPACEA(1)
      *
     A                                    34'(A)'
     A                                    63'(B)'
     A                                    88'(C)'
     A                                   111'(A+B-C)'
     A                                      SPACEA(1)
      *
     A            P1UL01       125A        2
     A                                      SPACEA(1)
      *
     A          R P1DTL1
     A            P1ACNO        18A        2
     A            P1PSTA        22A       26
     A                                      SPACEA(1)
      *
     A          R P1DTL2
     A            P1PSTA        22A       26
     A                                      SPACEA(1)
      *
     A          R P1DTL3
     A            P1UL02        22A       26
     A                                      SPACEA(1)
      *
     A          R P1DTL4
     A            P1TOTP        22A       26
     A            P1OPEN        22A       55
     A            P1CLOS        22A       78
     A            P1DIFF        22A      105
     A                                      SPACEA(2)
      *
     A          R P1FOOT
     A                                      SPACEB(1)
     A            P1UL03        53A        2
     A                                    55'*** END OF REPORT ***'
     A            P1UL04        52A       76
      *
