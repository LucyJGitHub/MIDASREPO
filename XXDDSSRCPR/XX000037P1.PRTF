      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Non Reconciled Items Report')                          *                       LUC109
      *****************************************************************
      *                                                               *
      *  Midas - Interface to Head office                             *
      *                                                               *
      ***GLM037P1*-*non*Reconciled*Items*report************************                       LUC109
      *  XX000037P1 - Non Reconciled Items Report                     *                       LUC109
      *                                                               *
      ***(C)*COPYRIGHT*MKI*LIMITED*1993,*1994**************************                       LUC109
      *  (c) Misys International Banking Systems Ltd. 2016            *                       LUC109
      *                                                               *
      *  Last Amend No. LUC109             Date 16Sep16               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas DBA 2.08 -------------- Base ---------------------------*
      *  Prev Amend No. MMI043             Date 22Dec98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC109 - HO Reporting (Upgrade to FB Midas 2.1)              *
      *  MMI043 - Balance Sheet Extract                               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  C R E A T I O N     P A R A M E T E R S                      *
      *                                                               *
      *****************************************************************
      *
      *
      *------------------*
      ***  Report Header *
      *------------------*
      *
      ** Start of LUC109
     A**********R RECHDR                    TEXT('(C) MKI +                                   LUC109
     A**********                            SYSTEMS INTERNATIONAL +                           LUC109
     A**********                            LTD.1998')                                        LUC109
     A          R RECHDR                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2016')
      ** End of LUC109
      *
     A                                      SKIPB(3)
      ** Start of LUC109
     A**********                           6'REFERENCE GLM037P1'                              LUC109
     A                                     6'REFERENCE XX000037P1'
      ** End of LUC109
     A            BJURPT        53A       37
     A                                   101'DATE'
     A            BJMRDT         7A      106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'RECONCILIATION IMBALANCE REPORT'
     A                                      SPACEA(1)
     A                                    46'-------------------------------'
     A                                      SPACEA(3)
     A                                     3'________________Account_+
     A                                       _____________'
     A                                    45'______Transaction___________'
     A                                    73'Principal_____'
     A                                    93'______Balance_______'
     A                                      SPACEA(2)
     A                                     3'Branch'
     A                                    12'Customer'
     A                                    21'CCY'
     A                                    27'A/C Code'
     A                                    36'Seq'
     A                                      SPACEA(2)
     A          R RECSUB
     A            RECBRC         3A        5
     A            RECCUS         6A       13
     A            RECCCY         3A       21
     A            RECACC         4A       29
     A            RECACS         2A       36
     A            RECBAL        22A       93
     A                                      SPACEA(2)
     A          R RECTTL
     A                                      SPACEB(1)
     A                                    48'TOTAL'
     A            RECBLT        22A       68
     A            RECBAL        22A       93
     A                                   116'** Difference **'
     A                                      SPACEA(2)
     A          R RECDET
     A            RECTRN        15A       48
     A            RECDTL        22A       68
     A                                      SPACEA(1)
     A          R RECEOR
     A                                      SPACEB(2)
     A                                    60'***END OF REPORT***'
     A          R RECNOD
     A                                      SPACEB(2)
     A                                    60'***NO DETAILS TO REPORT***'
