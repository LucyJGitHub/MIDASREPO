/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL Interest Statement of Deposit')               *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/XX000080P1 - Interest Statement of Deposit              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2015            *
      *                                                               *
      *  Last Amend No. LUC106  *CREATE    Date 21May15               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC106 - Dealing interest statement                          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A*
     A          R DETAIL                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2015')
     A                                      SKIPB(3)
     A                                    52'INTEREST STATEMENT OF DEPOSIT'
     A                                    52'-----------------------------'
     A                                      SPACEB(1)
     A            DPTY          14        60SPACEB(1)
     A*
     A            ADDR1         35         2SPACEB(3)
     A                                    46'PAID DATE     :'
     A            PDATE          7        63
     A*
     A            ADDR2         35         2SPACEB(1)
     A                                    46'CUSTOMER NO   :'
     A            CNUM           6        63
     A*
     A            ADDR3         35         2SPACEB(1)
     A                                    46'DEAL NO       :'
     A            DLNO           6  0     63
     A*
     A            ADDR4         35         2SPACEB(1)
     A                                    46'PRINCIPAL AMT :'
     A            CCY            3        63
     A     40     PCPL          13  0     67EDTWRD(' ,   ,   ,   , * ')
     A     41     PCPL1         13  1     67EDTWRD('   ,   ,   , * . ')
     A     42     PCPL2         13  2     67EDTWRD('  ,   ,   , * .  ')
     A     43     PCPL3         13  3     67EDTWRD(' ,   ,   , * .   ')
     A*
     A                                     2'START DATE' SPACEB(4)
     A                                    17'END DATE'
     A                                    32'RATE(%)'
     A                                    44'TERM'
     A                                    51'CCY'
     A                                    58'INTEREST AMOUNT'
     A*
     A                                    43'(DAYS)' SPACEB(1)
     A*
     A            SDATE          7         4SPACEB(2)
     A            MDATE          7        18
     A            INTR          11  7     28EDTWRD('  0 .       ')
     A            TERM           5  0     42EDTCDE(M)
     A            CCY0           3        51
     A     40     IDUE          13  0     57EDTWRD(' ,   ,   ,   , * ')
     A     41     IDUE1         13  1     57EDTWRD('   ,   ,   , * . ')
     A     42     IDUE2         13  2     57EDTWRD('  ,   ,   , * .  ')
     A     43     IDUE3         13  3     57EDTWRD(' ,   ,   , * .   ')
     A
     A*
     A                                     3'NATIONAL TAX' SPACEB(4)
     A            NTRT          11  7     15EDTWRD('  0 .       ')
     A                                    27'%:'
     A            CCY1           3        30
     A     40     NTXA          13  0     34EDTWRD(' ,   ,   ,   , * ')
     A     41     NTXA1         13  1     34EDTWRD('   ,   ,   , * . ')
     A     42     NTXA2         13  2     34EDTWRD('  ,   ,   , * .  ')
     A     43     NTXA3         13  3     34EDTWRD(' ,   ,   , * .   ')
     A*
     A                                     3'LOCAL    TAX' SPACEB(1)
     A            LTRT          11  7     15EDTWRD('  0 .       ')
     A                                    27'%:'
     A            CCY2           3        30
     A     40     LTXA          13  0     34EDTWRD(' ,   ,   ,   , * ')
     A     41     LTXA1         13  1     34EDTWRD('   ,   ,   , * . ')
     A     42     LTXA2         13  2     34EDTWRD('  ,   ,   , * .  ')
     A     43     LTXA3         13  3     34EDTWRD(' ,   ,   , * .   ')
     A*
     A                                     3'NET INTEREST:' SPACEB(2)
     A            CCY3           3        30
     A     40     IPAD          13  0     34EDTWRD(' ,   ,   ,   , * ')
     A     41     IPAD1         13  1     34EDTWRD('   ,   ,   , * . ')
     A     42     IPAD2         13  2     34EDTWRD('  ,   ,   , * .  ')
     A     43     IPAD3         13  3     34EDTWRD(' ,   ,   , * .   ')
     A*
     A                                     3'PAID AMOUNT :' SPACEB(1)
     A            CCY4           3        30
     A     40     PDAM          13  0     34EDTWRD(' ,   ,   ,   , * ')
     A     41     PDAM1         13  1     34EDTWRD('   ,   ,   , * . ')
     A     42     PDAM2         13  2     34EDTWRD('  ,   ,   , * .  ')
     A     43     PDAM3         13  3     34EDTWRD(' ,   ,   , * .   ')
     A*
     A            BADR1         35        46SKIPB(31)
     A            BADR2         35        46SPACEB(1)
     A            BADR3         35        46SPACEB(1)
     A            BADR4         35        46SPACEB(1)
     A*
     A                                    46'----------------------'
     A                                      SKIPB(39)
     A                                    46'AUTHORIZED   SIGNATURE'
     A                                      SPACEB(1)
     A*
     A*****************************************************************
     A          R DBERRP
     A                                      TEXT('DATABASE ERROR')
     A                                      SPACEB(003)
      *
     A                                     7
     A                                      DFT('DATABASE ERROR IN ')
     A                                    26
     A                                      DFT('FILE ')
     A            DBFILE         8   O    31
     A                                    39
     A                                      DFT(',')
     A                                    41
     A                                      DFT('KEY ')
     A            DBKEY         29   O    45
     A                                    71
     A                                      DFT(',')
     A                                    73
     A                                      DFT('IN ')
     A            DBPGM         10   O    76
     A                                    84
     A                                      DFT(',')
     A                                    86
     A                                      DFT('AT ')
     A            DBASE          3  0O    89
