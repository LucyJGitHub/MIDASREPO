      *****************************************************************
/*STD *  PRTFAU                                                       *
/*EXI *  TEXT('Midas SD Income & Expense a/c mapping Audit')          *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/XX000105AU - Income & Expense a/c Mapping Report -Audit *
      *                                                               *
      *  (c) Finastra International Limited 2017                      *
      *                                                               *
      *  Last Amend No. FVT102  *CREATE     Date 08May17              *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  FVT102 - Posting of Profit and Loss to Multiple Retained     *
      *           Earnings Accounts.                                  *
      *                                                               *
      *****************************************************************
      *  Indicators                                                   *
      *                                                               *
      *    54  -  AT LEAST ONE SET OF DETAILS OUTPUT - USED FOR       *
      *           PRINTER FILE CONDITIONING.                          *
      *    80  -  DATA BASE ERROR INDICATOR                           *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Format for Whole Report
      *
     A          R CONTROL
     A                                      TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2017')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE XX000105AU'
     A            RRTITL    R             36REFFLD(TITL)
     A                                   100'DATE'
     A            RRRUNA    R            105REFFLD(RUNA)
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      SPACEA(1)
     A                                      EDTWRD('0 :  :  ')
      *
     A                                    37'INCOME AND EXPENSE ACCOUNTS MAPPI+
     A                                      NG MATRIX REPORT - AUDIT'
     A                                      SPACEA(1)
      *
     A                                    37'---------------------------------+
     A                                      ------------------------'
     A                                      SPACEA(3)
      *
     A                                    65'CALCULATED'
     A                                      SPACEA(1)
     A                                    65'----------'
     A                                      SPACEA(2)
      *
     A                                     6'FILE - Income and Expense Account+
     A                                      s - LF/XXINEXL1'
     A                                     6'---------------------------------+
     A                                      ---------------'
     A                                      SPACEB(1)
     A                                      SPACEA(3)
     A                                    10'NUMBER OF RECORDS REPORTED:'
     A            RRCALC         4  0     68TEXT('CALC NO. OF RECORDS')
     A                                      EDTWRD('  0 ')
      *
     A N54N80                             48'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      ** Database Error Message
      *
     A          R DBERROR                   TEXT('DATABASE ERROR MSG')
     A                                      SPACEB(3)
     A                                    48 '***   PRINT TERMINATED   ***'
     A                                      SPACEA(2)
     A                                    45 'DATABASE ERROR'
     A            DBASE          3  0     60
     A                                    65 'FILE'
     A            DBFILE         8        70
     A                                      SPACEA(1)
     A                                    45 'KEY'
     A            DBKEY         29        49
      *****************************************************************
