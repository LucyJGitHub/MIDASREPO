      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas SD Income & Expense a/c mapping Rpt')            *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/XX000105P1 - Income & Expense a/c Mapping Report        *
      *                                                               *
      *  (c) Finastra International Limited 2017                      *
      *                                                               *
      *  Last Amend No. FVT102  *CREATE     Date 08May17              *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  FVT102 - Posting of Profit and Loss to Multiple Retained     *
      *           Earnings Accounts.                                  *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R XX0105R0
     A                                      TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2017')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE XX000105P1'
     A            RRTITL    R             36REFFLD(TITL)
     A                                   100'DATE'
     A            RRRUNA         7       105
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(1)
     A                                    41'INCOME AND EXPENSE ACCOUNTS MAPPI+
     A                                      NG MATRIX REPORT'
     A                                      SPACEA(1)
     A                                    41'---------------------------------+
     A                                      ----------------'
     A                                      SPACEA(3)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     3'Income and Expense A/c Codes'
     A                                    46'Profit and Loss'
     A                                    76'Spot Trade'
     A                                   100'Last Change'
     A                                   120'Last Change'
     A                                      SPACEA(1)
      *
     A                                     7'From'
     A                                    24'To'
     A                                    47'Account Code'
     A                                    75'Account Code'
     A                                   103'Date'
     A                                   123'Type'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R XX0105R1                  TEXT('DETAIL OUTPUT FMT')
      *
     A            XRINC1        10         4TEXT('From Income & Expense a/c')
     A            XRINC2        10        20TEXT('To Income & Expense a/c')
     A            XRPLA1        10        48TEXT('Profit & Loss a/c code')
     A            XRSRVL        10        76TEXT('Spot Trade a/c code')
     A            XRLCDT         7       102TEXT('Last Change Date')
     A            XRLCTP         6       122TEXT('Last Change Type')
     A                                      SPACEA(1)
      *
      *-----------------------*
      ** End of Report Format *
      *-----------------------*
      *
     A          R XX0105R2                  TEXT('END OF REPORT FORMAT')
      *
     A                                      SPACEB(2)
     A                                    52'*** END OF REPORT ***'
      *
      *****************************************************************
