      *****************************************************************
/*STD *  PRTFBASE                                                     *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/XX000106P1 - Spot Trade A/C Mapping Report              *
      *                                                               *
      *  (c) Finastra International Limited 2017                      *
      *                                                               *
      *  Last Amend No. FVT102CC1           Date 09Jun17              *
      *  Prev Amend No. FVT102  *CREATE     Date 08May17              *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  FVT102CC1 - Posting of P/L to Mult. Retained Earnings CC1    *
      *  FVT102 - Posting of Profit and Loss to Multiple Retained     *
      *           Earnings Accounts.                                  *
      *                                                               *
      *****************************************************************
      *
      ** File Level Keywords
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R XX0106R0
     A                                      TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2017')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE XX000106P1'
     A            RRTITL    R             36REFFLD(TITL)
     A                                   100'DATE'
     A            RRRUNA         7       105
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(1)
     A                                    43'SPOT TRADE ACCOUNTS MAPPI+
     A                                      NG MATRIX REPORT'
     A                                      SPACEA(1)
     A                                    41'-------------------------------+
     A                                      ----------------'
     A                                      SPACEA(3)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                    13'Spot Trade'
      ** Start of FVT102CC1
     A**********                          30'Foreign'
     A                                    30'Currency'
      ** End of FVT102CC1
     A                                    48'Spot Trade Equivalent'
     A                                    76'Spot Revaluation'
     A                                   100'Last Change'
     A                                   120'Last Change'
     A                                      SPACEA(1)
      *
     A                                    12'Account Code'
      ** Start of FVT102CC1
     A**********                          30'Currency'
      ** End of FVT102CC1
     A                                    53'Account Code'
     A                                    78'Account Code'
     A                                   103'Date'
     A                                   123'Type'
     A                                      SPACEA(2)
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R XX0106R1                  TEXT('DETAIL OUTPUT FMT')
      *
     A  15                                  SPACEB(1)
     A            XRSRVL        10        13TEXT('Spot Trade a/c code')
      ** Start of FVT102CC1
     A**********  XRFCCY         3        30TEXT('Foreign Currency')
     A            XRFCCY         3        30TEXT('Currency')
      ** End of FVT102CC1
     A  15        XRCCNM        14        36TEXT('Currency Name')
     A            XRSPAE        10        54TEXT('Spot Trade Equiv a/c code')
     A            XRSPTA        10        79TEXT('Spot Revaluation a/c code')
     A            XRLCDT         7       102TEXT('Last Change Date')
     A            XRLCTP         6       122TEXT('Last Change Type')
     A                                      SPACEA(1)
      *
      *-----------------------*
      ** End of Report Format *
      *-----------------------*
      *
     A          R XX0106R2                  TEXT('END OF REPORT FORMAT')
      *
     A                                      SPACEB(2)
     A                                    52'*** END OF REPORT ***'
      *
