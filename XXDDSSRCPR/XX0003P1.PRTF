/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas XX SE Month End Exchange Rates Report')          *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  XX0003P1 - XX  SE Month End Exchange Rates Report            *
      *                                                               *
      *  (c) Finastra International Limited 2017                      *
      *                                                               *
      *  Last Amend No. DUG504 *CREATE     Date 07Sep17               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  DUG504 - Cost of Funds                                       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER                    TEXT('(c) Finastra Technology, +
     A                                      Inc. 2017')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE XX0003P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'Month End Exchange Rates +
     A                                       Report'
      *
     A                                      SPACEA(1)
     A                                    46'------------------------+
     A                                      -------'
     A                                      SPACEA(2)
      *
      ***  Sub-Headings:
      ***  ------------
      *
     A                                     6'YEAR'
     A                                    15'CURRENCY'
     A                                    26'MONTH'
     A                                    38'EXCHANGE RATE'
     A                                      SPACEA(1)
     A                                     6'----'
     A                                    15'--------'
     A                                    26'-----'
     A                                    38'-------------'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            DMEYR          2         7
     A            DMECY          3        17
     A            DMEMN          2        27
     A            EXCRAT        14        37
     A                                      SPACEA(1)
      *
      *---------------*
      ***  No details *
      *---------------*
      *
     A          R NODETS
     A                                    47'***  NO DETAILS TO REPORT  ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILER
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
