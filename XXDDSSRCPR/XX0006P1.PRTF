     A*****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas XX SE Cost of Funds Report')                     *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  XX0006P1 - XX SE Cost of Funds Report                        *
      *                                                               *
      *  (c) Finastra International Limited 2017                      *
      *                                                               *
      *  Last Amend No. DUG504              Date 06Sep17              *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  DUG504 - Cost of Funds                                       *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *-------------------------*
      ***  Report Header Format *
      *-------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra +
     A                                      Technology, Inc. 2017')
      *
      ***  Main Header:
      ***  ------------
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE XX0006P1'
     A            BJURPT        53A       39
     A                                   101'DATE'
     A            BJMRDT         7A      106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A            REPTYP        30         6
      *
     A                                    46'SECURITIES TRADING COST +
     A                                      OF FUNDS REPORT'
      *
     A                                    89'BRANCH -'
     A            RBHBR     R             98REFFLD(BRCA)
     A            BRNM      R            102
      *
     A                                    46'------------------------'
     A                                      SPACEB(1)
     A                                    70'---------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                     6'CURRENCY'
     A            RCCY      R             16REFFLD(CCY)
     A            RCCNM     R             20REFFLD(CCNM)
      *
     A                                    85'BOOK'
     A            RBHBK     R             91REFFLD(BHBK)
     A            BDBKD         30A       94
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     2'SECURITY'
     A                                    13'DATE'
     A                                    30'POSITION'
     A                                    52'EXCHANGE RATE'
     A                                    66'COST/EARNINGS RATE'
     A                                    86 'DAYS'
     A                                    96'COST/EARNINGS'
     A                                   117'BASE CURRENCY'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RBHSC         10         1
     A            RDATE          7        12
     A            RPOSN         20        20
     A            RNARR          5        41
     A            REXRT         14        51
     A            RRATE         12        70
     A            RDAYS          3        86
     A            RCOST         20        90
     A            RCOSTB        20       111
     A                                      SPACEA(1)
      *
      *--------------------------------*
      ***  Monthly Total Record Format *
      *--------------------------------*
      *
     A          R MTHTOT
     A            RBHSC         10         1
     A            RMONTH         3        86
     A            RMTOT         20        90
     A            RMTOTB        20       111
     A                                      SPACEA(1)
      *
      *---------------------------------*
      ***  Security Total Record Format *
      *---------------------------------*
      *
     A          R SECTOT
     A                                      SPACEB(1)
     A            RBHSC         10         1
     A                                    12'**********************************'
     A                                    46'**********************************'
     A                                    84'TOTAL'
     A            RSTOT         20        90
     A            RSTOTB        20       111
     A                                      SPACEA(2)
      *
      *---------------------------------*
      ***  Currency Total Record Format *
      *---------------------------------*
      *
     A          R CCYTOT
     A                                      SPACEB(1)
     A**S20240***                          1 'BOOK'
     A**S20240*** WOBHBK         2         6
     A                                    12 'CURRENCY'
     A            WOCCY          3        21
     A                                    84'TOTAL'
     A            RCTOT         20        90
     A            RCTOTB        20       111
      ********************************S20240************************************
      *-----------------------------*
      ***  Book Total Record Format *
      *-----------------------------*
      *
     A          R BOKTOT
     A                                      SPACEB(2)
     A                                    12 'BOOK'
     A            WOBHBK         2        17
     A                                    84'TOTAL'
     A            RBTOTB        20       111
      ********************************S20240************************************
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(2)
