      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas SE SSI Unauthorised Report - Audit')             *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/XX0109AU - Midas SE SSI Unauthorised Report - Audit     *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. HUT109  *CREATE    Date 14Apr20               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  HUT109 - Partial upgrade of HUT020 to FBM 2.1 SP22.          *
      *           Additinal Authorisation Steps in SE Module          *
      *                                                               *
      *****************************************************************
      *  C R E A T I O N     P A R A M E T E R S                      *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      **
      **   *******************
      **   PAGE HEADINGS
      **   *******************
      **
     A          R HEADAU                    SKIPB(3)
     A                                      TEXT('(c) Finastra Inter+
     A                                      national Limited +
     A                                      2001')
     A                                     6'REFERENCE XX0109AU'
     A            TITL      R             39
     A                                   100'DATE'
     A            RUNA      R            105
     A                                   119'PAGE'
     A            PAGE1        004S 0    125EDTCDE(Z) SPACEA(2)
      *
     A                                     8'SECURITIES TRADING'
     A                                    27'SSI AUTHORISATION CHECK -'
     A  40                                53'AUDIT - EXCEPTION'
     A  40                                81TIME
     A  41                                63'AUDIT'
     A  42                                63'FULL LIST'
     A  40                                93'RUN CONTROLS'     SPACEA(1)
     A  41                                72'RUN CONTROLS'     SPACEA(1)
     A  42                                76'RUN CONTROLS'     SPACEA(1)
     A                                     8'-------------------'
     A                                    27'--------------------------------'
     A                                      SPACEA(3)
     A                                    60'FILE CONTROL' SPACEB(3)
     A                                    91'CALCULATED' SPACEA(3)
     A                                     6'FILE - STD SETTLEMENT- PF/XXXSEDPS'
     A                                      SPACEA(1)
     A                                     6'--------------------------------'
     A                                      SPACEA(3)
      **
      **   *******************
      **   CONTROL REPORT FOR 'I' MODE
      **   *******************
      **
     A          R CONTROLI
     A                                    10'NO. OF LIVE RECORDS BEFORE INPUT'
     A            SLRB      R             67EDTCDE(3) SPACEA(2)
     A                                    10'INSERTIONS'
     A            SINS      R             67EDTCDE(3) SPACEA(1)
     A                                    10'AMENDMENTS'
     A            SAMD      R             67EDTCDE(3) SPACEA(1)
     A                                    10'DELETIONS'
     A            SDEL      R             67EDTCDE(3) SPACEA(2)
     A                                    10'NO. OF RECORDS CHANGED TODAY'
     A            ZCHG      R             96EDTCDE(3) REFFLD(NORE) SPACEA(2)
     A                                    10'NO. OF LIVE RECORDS ON FILE'
     A            NORE      R             67EDTCDE(3)  SPACEA(2)
      **
      **   *******************
      **   CONTROL REPORT FOR 'A' MODE
      **   *******************
      **
     A          R CONTROLA
     A                                    10'NO. OF LIVE RECORDS BEFORE INPUT'
     A            SLRB      R             67EDTCDE(3) SPACEA(2)
     A                                    10'INSERTIONS'
     A            SINS      R             67EDTCDE(3) SPACEA(1)
     A                                    10'AMENDMENTS'
     A            SAMD      R             67EDTCDE(3) SPACEA(1)
     A                                    10'DELETIONS'
     A            SDEL      R             67EDTCDE(3) SPACEA(2)
     A                                    10'NO. OF LIVE RECORDS AFTER INPUT'
     A            ZWORK     R             67EDTCDE(3)    REFFLD(NORE)
     A            ZLIVE     R             96EDTCDE(3) REFFLD(NORE)
     A  81                               112'*** DIFFERENCE ***'
     A                                      SPACEA(3)
      **
      **   *******************
      **   NO RECORDS
      **   *******************
      **
     A          R NONE
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      **
      **   *******************
      **   CONTROL REPORT FOR 'L' MODE
      **   *******************
      **
     A          R CONTROLL
     A                                    10'NO. OF LIVE RECORDS ON FILE'
     A            NORE      R             67EDTCDE(3)
     A            ZLIVE     R             96EDTCDE(3) REFFLD(NORE)
     A  81                               112'*** DIFFERENCE ***'
     A                                      SPACEA(3)
      **
      **   *******************
      **   REPORT TRAILER
      **   *******************
      **
      **
      **   *******************
      **   REPORT DATABASE ERROR
      **   *******************
      **
     A          R ERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
