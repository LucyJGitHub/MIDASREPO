      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE SSI Unauthorised Report - Detail')            *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/XX0109P1 - Midas SE SSI Unauthorised Report - Detail    *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. HUT109  *CREATE    Date 14Apr20               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  HUT109 - Partial upgrade of HUT020 to FBM 2.1 SP22.          *
      *           Additinal Authorisation Steps in SE Module          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      **
      **   *******************
      **   PAGE HEADINGS
      **   *******************
      **
     A          R HEADP1                    SKIPB(3)
     A                                      TEXT('(c) Finastra Inter+     +
     A                                      national Limited +
     A                                      2001')
     A                                     6'REFERENCE XX0109P1'
     A            TITL      R             39
     A                                   100'DATE'
     A            RUNA      R            105
     A                                   119'PAGE'
     A            PAGE         004S 0    125EDTCDE(Z) SPACEA(2)
     A                                     8'SECURITIES TRADING'
     A                                    27'SSI UNAUTHORISED REPORT -'
     A  40                                53'DETAIL - EXCEPTION'
     A  40                                81TIME
     A  41                                63'AUDIT'
     A  42                                63'FULL LIST'
     A  35                                93'BRANCH '
     A  35        HSBRA     R            100REFFLD(HSBRA *LIBL/XXXSEDPS)
     A  35                               103'-'
     A  35        ZBRNM     R            104REFFLD(BRNM)
     A                                     8'-------------------' SPACEB(1)
     A                                    27'--------------------------------'
     A                                      SPACEA(1)
     A                                     1'CUSTOMER'
     A  76                                35'BOOK'
     A  76                                97'PORTFOLIO'
     A  76                               117'LAST ACTION'
     A                                      SPACEA(1)
     A N76                                47'CASH'
     A N76                                68'CASH'
     A N76                                89'STOCK'
     A N76                               106'STOCK'
     A N76                               121'LAST ACTION'
     A                                      SPACEA(1)
     A                                     1'NUMBER/NAME'
     A N76                                22'CCY'
     A N76                                26'INVESTMENT TYPE'
     A  76                                35'CODE'
     A  76                                50'CCY'
     A  76                                66'INVESTMENT TYPE'
     A  76                                97'REFERENCE'
     A  76                               117'DATE'
     A                                       SPACEA(2)
     A N76                                47'PAYMENT'
     A N76                                68'RECEIPT'
     A N76                                89'DELIVERY'
     A N76                               106'RECEIPT'
     A N76                               121'DATE'
     A                                       SPACEA(2)
      **
      **   *******************
      **   DETAIL PART OF REPORT
      **   *******************
      **
     A          R DETAIL
     A
     A            HCSTM     R              1REFFLD(HCSTM *LIBL/XXXSEDPS)
     A                                      SPACEA(1)
     A            ZCRNM     R              1REFFLD(CRNM)
     A            HCYSI     R             22REFFLD(HCYSI *LIBL/XXXSEDPS)
     A            HITSI     R             26REFFLD(HITSI *LIBL/XXXSEDPS)
     A            HSPSM     R             47REFFLD(HSPSM *LIBL/XXXSEDPS)
     A            LSPSM         14        50
     A            HSRSM     R             68REFFLD(HSRSM *LIBL/XXXSEDPS)
     A            LSRSM         14        71
     A                                    89'CLEAR'
     A            HSCLS     R             97REFFLD(HSCLS *LIBL/XXXSEDPS)
     A                                   106'CLEAR'
     A            HSCLP     R            114REFFLD(HSCLP *LIBL/XXXSEDPS)
     A            ZALCD     R            121REFFLD(RUNA)
     A  51                               129'AMD'
     A                                      SPACEA(1)
     A  52                               129'INS'
     A                                      SPACEA(1)
     A  53                               129'DEL'
     A                                      SPACEA(1)
     A            ZCTWN     R              1REFFLD(CTWN)
     A            ZINAM     R             26REFFLD(INAM)
     A            ZSPA14        20        47
     A            ZSRA14        20        68
     A                                    89'FROM'
     A            HSDFO     R             97REFFLD(HSDFO *LIBL/XXXSEDPS)
     A                                   106'FROM'
     A            HSDFT     R            114REFFLD(HSDFT *LIBL/XXXSEDPS)
     A                                      SPACEA(1)
     A                                    47'BRANCH'
     A            HSPBA     R             55REFFLD(HSPBA *LIBL/XXXSEDPS)
     A                                    68'BRANCH'
     A            HSRAB     R             76REFFLD(HSRAB *LIBL/XXXSEDPS)
     A                                    89'TO'
     A            HSDTT     R             97REFFLD(HSDTT *LIBL/XXXSEDPS)
     A                                   106'TO'
     A            HSDTO     R            114REFFLD(HSDTO *LIBL/XXXSEDPS)
     A                                      SPACEA(1)
     A  65                                 1'SETTLEMENT CURRENCY'
     A  65        RSCCY     R             23REFFLD(SCCY)
     A                                      TEXT('SETTLEMENT CURRENCY')
     A                                    47'TO'
     A            HSPCB     R             55REFFLD(HSPCB *LIBL/XXXSEDPS)
     A                                    68'FROM'
     A            HSRCB     R             76REFFLD(HSRCB *LIBL/XXXSEDPS)
      *
      ** S01175 euclid incorporation
      *
     A                                    89'FOR A/C'
     A            HSFAT     R             97REFFLD(HSFAT *LIBL/XXXSEDPS)
     A                                   106'FOR A/C'
     A            HSFAF     R            114REFFLD(HSFAF *LIBL/XXXSEDPS)
     A                                      SPACEA(1)
     A  65                                 1'ERI'
     A  65        RERII     R              5REFFLD(SEEV)
     A                                      TEXT('INCLUDE ERI IN FIELD 72')
     A                                    47'FOR A/C OF'
     A            HSPFA     R             58REFFLD(HSPFA *LIBL/XXXSEDPS)
     A                                    89'PAY CODE'
     A            HSPCS     R             98REFFLD(HSPCS *LIBL/XXXSEDPS)
     A                                   106'PAY CODE'
     A            HSPCP     R            115REFFLD(HSPCP *LIBL/XXXSEDPS)
     A                                      SPACEA(1)
     A                                    47'SPEC.INSTR.'
     A            HSPSI     R             60REFFLD(HSPSI *LIBL/XXXSEDPS)
     A                                      SPACEA(1)
     A  69                                 1'SAFEKEEPING A/C'
     A  69        PSKAC         20A       20
     A  77                                  SPACEA(1)
     A  77                                 1'AUTO SETTLE IND.'
     A  77        SASIN          1A       19
     A                                      SPACEA(3)
     A          R DETAIL2
      *
     A            HCSTM     R              1REFFLD(HCSTM *LIBL/XXXSEDPS)
     A                                      SPACEA(1)
      *
     A            ZCRNM     R              1REFFLD(CRNM)
     A            RSIBK          2A       36
     A            HCYSI     R             50REFFLD(HCYSI *LIBL/XXXSEDPS)
     A            HITSI     R             66REFFLD(HITSI *LIBL/XXXSEDPS)
     A            RPTFR          4A      100
     A            ZALCD     R            117REFFLD(RUNA)
     A  51                               125'AMD'
     A                                      SPACEA(1)
     A  52                               125'INS'
     A                                      SPACEA(1)
     A  53                               125'DEL'
     A                                      SPACEA(1)
      *
     A            ZCTWN     R              1REFFLD(CTWN)
     A            ZINAM     R             66REFFLD(INAM)
     A                                      SPACEA(2)
      *
     A                                     1'CASH PAYMENT:'
     A            HSPSM     R             20REFFLD(HSPSM *LIBL/XXXSEDPS)
     A            LSPSM         14        23
     A            ZSPA14        20        39
     A                                    61'BRANCH'
     A            HSPBA     R             70REFFLD(HSPBA *LIBL/XXXSEDPS)
     A                                    85'TO'
     A            HSPCB     R             95REFFLD(HSPCB *LIBL/XXXSEDPS)
     A                                   110'FOR A/C OF'
     A            HSPFA     R            125REFFLD(HSPFA *LIBL/XXXSEDPS)
     A                                      SPACEA(1)
      *
     A                                    85'SPEC.INSTR.'
     A            HSPSI     R             98REFFLD(HSPSI *LIBL/XXXSEDPS)
     A                                      SPACEA(1)
      *
     A                                     1'CASH RECEIPT:'
     A            HSRSM     R             20REFFLD(HSRSM *LIBL/XXXSEDPS)
     A            LSRSM         14        23
     A            ZSRA14        20        39
     A                                    61'BRANCH'
     A            HSRAB     R             70REFFLD(HSRAB *LIBL/XXXSEDPS)
     A                                    85'FROM'
     A            HSRCB     R             95REFFLD(HSRCB *LIBL/XXXSEDPS)
     A                                      SPACEA(1)
      *
     A                                     1'STOCK DELIVERY:'
     A                                    20'CLEAR'
     A            HSCLS     R             30REFFLD(HSCLS *LIBL/XXXSEDPS)
     A                                    38'FROM'
     A            HSDFO     R             45REFFLD(HSDFO *LIBL/XXXSEDPS)
     A                                    58'TO'
     A            HSDTT     R             65REFFLD(HSDTT *LIBL/XXXSEDPS)
     A                                    78'FOR A/C'
     A            HSFAT     R             90REFFLD(HSFAT *LIBL/XXXSEDPS)
     A                                   105'PAY CODE'
     A            HSPCS     R            120REFFLD(HSPCS *LIBL/XXXSEDPS)
     A                                      SPACEA(1)
      *
     A                                     1'STOCK RECEIPT:'
     A                                    20'CLEAR'
     A            HSCLP     R             30REFFLD(HSCLP *LIBL/XXXSEDPS)
     A                                    38'FROM'
     A            HSDFT     R             45REFFLD(HSDFT *LIBL/XXXSEDPS)
     A                                    58'TO'
     A            HSDTO     R             65REFFLD(HSDTO *LIBL/XXXSEDPS)
     A                                    78'FOR A/C'
     A            HSFAF     R             90REFFLD(HSFAF *LIBL/XXXSEDPS)
     A                                   105'PAY CODE'
     A            HSPCP     R            120REFFLD(HSPCP *LIBL/XXXSEDPS)
     A                                      SPACEA(1)
      *
     A  65                                 1'SETTLEMENT CURRENCY'
     A  65        RSCCY     R             30REFFLD(SCCY)
     A                                      TEXT('SETTLEMENT CURRENCY')
     A  65                                58'ERI'
     A  65        RERII     R             65REFFLD(SEEV)
     A                                      TEXT('INCLUDE ERI IN FIELD 72')
     A  69                                78'SAFEKEEPING A/C'
     A  69        PSKAC         20A       98
     A                                      SPACEA(1)
      *
     A                                     1'TRADE SETTLEMENT TO BE USED +
     A                                      FOR DEPOT MOVEMENT'
     A            RTFDM          1A       50
     A  77                                  SPACEA(1)
     A  77                                 1'AUTO SETTLE IND.'
     A  77        SASIN          1A       19
     A  25                                65'ISO15022 MESSAGE'
     A  25        RSNEW          1A       85
     A                                   132' '
     A                                      SPACEA(3)
      **
      **   *******************
      **   TOTAL LINE
      **   *******************
      **
     A          R TOTAL
     A                                      SPACEB(1)
     A                                    19'TOTAL NUMBER OF STD SETTLEMENTS'
     A                                    51'LISTED-'
     A  40        ZCHGP1    R             59REFFLD(NORE)
     A                                      SPACEA(2)
     A  41        ZCOUNT    R             59REFFLD(NORE)
     A                                      SPACEA(2)
     A  42        ZLIVP1    R             59REFFLD(NORE)
     A                                      SPACEA(2)
      **
      **   *******************
      **   REPORT TRAILER
      **   *******************
      **
     A          R TRAILP1                   SPACEB(3)
     A  35                                46'***  END OF REPORT FOR BRANCH '
     A  35        ZSBRA          3A       76TEXT('BRANCH CODE')
     A  35                                81'***'
     A N35                                56'*** END OF REPORT ***'
      **
      **   *******************
      **   REPORT DATABASE ERROR
      **   *******************
      **
     A          R ERRORP1
     A                                    25'**** DATABASE ERROR ****'
