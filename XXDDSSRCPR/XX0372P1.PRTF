      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_CONF42                                                    *
/*EXI *  TEXT('Midas LE Loan Advice with Fwd ARR Rates Report')       *
/*ADD *  CHGPRTF FILE(XX0372P1) PAGESIZE(48 90) OVRFLW(48)            *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/XX0372P1 - Loan Advice with Fwd ARR rates               *
      *                                                               *
      *  (c) Finastra International Limited 2023                      *
      *                                                               *
      *  Last Amend No. MD061164 *CREATE   Date 03May23               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD061164 - LE ARR - Forward rates and new report changes.    *
      *             Formalise codes introduced by ARR_CC10, EF010822, *
      *             EF270622, MD061164, and ARRLOR.                   *
      *           - ARR_CC10 - Add page with breakdown of interest    *
      *           - EF010822 - Shift Date, Cust and CNA* fields to    *
      *             the left                                          *
      *           - EF270622 - Shift text to fit in green area        *
      *           - AM070622 - Amend page width to 90 and fit btn     *
      *             posns 9-81                                        *
      *           - ARRLOR - LE Loan Advice with Fwd ARR rates        *
      *                                                               *
      *****************************************************************
      *
     A          R DETLP1
     A                                    11'Date :'
     A                                      SPACEB(1)
     A            P1DATE         9        18
     A                                    11'Cust.'
     A                                      SPACEB(2)
     A            P1CUST         6        18HIGHLIGHT
     A            P1CNA1        35        18HIGHLIGHT
     A                                      SPACEB(1)
     A            P1CNA2        35        18HIGHLIGHT
     A                                      SPACEB(1)
     A            P1CNA3        35        18HIGHLIGHT
     A                                      SPACEB(1)
     A            P1CNA4        35        18HIGHLIGHT
     A                                      SPACEB(1)

     A                                    11'Subject:'
     A                                      SPACEB(4)
     A                                    27'Notification of Loan Confirmation'
     A                                    11'We are pleased to advise you on +
     A                                       the Loan Details following the '
     A                                      SPACEB(2)
     A                                    11'determination of the Risk Free +
     A                                       Rate :'
     A                                      SPACEB(1)

     A                                    11'Loan Details'
     A                                      SPACEB(2)
     A                                      HIGHLIGHT

     A                                    11'Our Ref'
     A                                      SPACEB(2)
     A            P1LNRF         6        54
     A                                    11'Principal'
     A                                      SPACEB(1)
     A            P1CCY          3        54
     A            P1CPAM        21        58
     A                                    11'Interest Start Date'
     A                                      SPACEB(1)
     A            P1SLID         9        54
     A                                    11'Interest End Date'
     A                                      SPACEB(1)
     A            P1NIDT         9        54
     A            P1NRDY        12        64
     A                                    11'Interest Rate'
     A                                      SPACEB(1)
     A            P1INTR        28        54
     A                                    11'Gross Interest'
     A                                      SPACEB(1)
     A            P1ICCY         3        54
     A            P1INTA        21        58
     A                                      SPACEA(1)

      ** Start of change MD061164
     A          R HEADP2
     A                                      SKIPB(2)
     A                                    11'LE LOAN ADVICE - BREAKDOWN'
     A                                      SPACEA(1)
     A                                    11'--------------------------'
     A                                      SPACEA(2)
      *
     A                                    11'Int. Date'
     A                                    23'Days'
     A                                    31'Interest Amount'
     A                                    52'Publ. Risk Free Rate'
     A                                    75'Rate Applied'
     A                                      SPACEA(1)
     A                                    11'---------'
     A                                    22'-----'
     A                                    29'---------------------'
     A                                    52'--------------------'
     A                                    74'--------------'
     A                                      SPACEA(2)
     A          R DETLP2
     A            P2INTD         9        11
     A            P2INDY         5        23
     A            P2INTA        21        29
     A            P2BRFR        15        54
     A            P2RTAP        15        74
     A                                      SPACEA(1)
      ** End of change MD061164

     A          R TRAILP1
     A                                      SPACEB(1)
      *
     A                                    11'This is a computer generated +
     A                                       document. No signature is +
     A                                       required.'
     A                                      SPACEA(2)
     A                                    11'This Notification is intended +
     A                                       for use of the individual or +
     A                                       entity to'
     A                                      SPACEA(1)
     A                                    11'whom it is addressed and contains +
     A                                       information that is privileged +
     A                                       and'
     A                                      SPACEA(1)
     A                                    11'confidential. If you are not the +
     A                                       intended recipient, please DO NOT'
     A                                      SPACEA(1)
     A                                    11'disseminate, distribute or make +
     A                                       copies of this notification. +
     A                                       Without'
     A                                      SPACEA(1)
     A                                    11'delay, we appreciate your +
     A                                       cooperation to destroy this +
     A                                       notification'
     A                                      SPACEA(1)
     A                                    11'completely from your computer +
     A                                       system and for hard copies to +
     A                                       shred to'
     A                                      SPACEA(1)
     A                                    11'unrecognised form.'
     A                                      SPACEA(1)
