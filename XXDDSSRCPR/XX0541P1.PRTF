      *****************************************************************
      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas MC Spot Reval by Alternate Spot Trade Report')   *
      *****************************************************************
      *                                                               *
      *  Midas - Management Accounting Module                         *
      *                                                               *
      *  PRTF/XX0541P1 - Spot Revaluation Report by                   *
      *                  Foreign Currency Spot Trade Account          *
      *                                                               *
      *  (c) Finastra International Limited 2017                      *
      *                                                               *
      *  This printer file is based on a copy of PRTF/MC0540P1.       *
      *  Any changes for MC0540* should be considered for XX0541* also*
      *                                                               *
      *  Last Amend No. FVT102  *Create    Date 09May17               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  FVT102 - Posting of P/L to Multiple Retained Earnings        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra +
     A                                      International Limited 2017')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE XX0541P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A* Start of change FVT102
     A**********                          48'REVALUATION OF SPOT TRADING +
     A**********                            POSITION'
     A**********                            SPACEA(1)
      *
     A**********                          48'----------------------------+
     A**********                            --------'
     A                                    20'REVALUATION OF SPOT TRADING +
     A                                      POSITION BY ALTERNATE SPOT TRADE +
     A                                      ACCOUNT -'
     A            RACCD         10A       91
     A                                      SPACEA(1)
      *
     A                                    20'--------------------------+
     A                                      --------------------------------+
     A                                      ----------'
     A* End of change FVT102
     A  37 38                             91'CONSOLIDATED'
     A  37N38                             91'BRANCH -'
     A  37N38     RBRCH     R            100REFFLD(BRCA)
     A  37N38     RBRNM     R            104REFFLD(BRNM)
     A                                   134' '
     A                                      SPACEA(2)
      *
      ***  Sub-Headings:
      ***  -------------
      *
     A                                     5'PRFT'
     A                                    24'SUB-'
     A                                    36'ASSOC.'
     A                                      SPACEA(1)
     A                                     5'CNTR'
     A                                    10'BK'
     A                                    13'DEAL TYPE'
     A                                    24'SUBTYPE'
     A                                    35'CUSTOMER'
     A                                    47'SPOT POSITION'
     A                                    70'EQUIVALENT'
     A                                    97'SPOT VALUE'
     A                                   118'PROFIT/LOSS'
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A          R SUBHDG                    SPACEB(2)
     A                                     5'CURRENCY'
     A            RCYCD     R             14REFFLD(CCY)
     A                                    18'-'
     A            RCYNM     R             20REFFLD(CYNM)
     A  25                                35'(CONTINUED)'
     A                                    54'RATE:'
     A            RSPRT     R             60REFFLD(ZZ014)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL                    SPACEB(1)
      *
     A            RPRFC     R              5REFFLD(PRFC)
     A            RBKCD     R             10REFFLD(BOKC)
     A            ROTTP     R             14REFFLD(OTTP)
     A            RTSTY     R             25REFFLD(OTST)
     A            RASNB     R             36REFFLD(CNUMA)
      *
     A            RSPOT     R             43REFFLD(ZZ022)
     A            RBCEQ     R             65REFFLD(ZZ022)
     A            RSPVL     R             87REFFLD(ZZ022)
     A            RPRLO     R            109REFFLD(ZZ022)
      *
      *-----------------------------*
      ***  Sub-Totals Record Format *
      *-----------------------------*
      *
     A          R SUBTTL                    SPACEB(1)
     A                                    43'---------------------'
     A                                    65'---------------------'
     A                                    87'---------------------'
     A                                   109'---------------------'
      *
     A                                    36'TOTALS'
     A                                      SPACEB(1)
     A            RTSPT     R             43REFFLD(ZZ022)
     A            RTBCE     R             65REFFLD(ZZ022)
     A            RTSPV     R             87REFFLD(ZZ022)
     A            RTPLO     R            109REFFLD(ZZ022)
      *
      *-----------------------------*
      ***  Database Error Reference *
      *-----------------------------*
      *
     A          R DBERRF                    SPACEB(3)
     A                                    32'*** REFERENCE XX0541'
     A                                    53'DATABASE ERROR ***'
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37N38                             46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37
     AO 38                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
