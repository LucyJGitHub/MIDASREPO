      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SE Unauthorised Position Settlemens Report')     *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      ***PRTF/SEH024P1*-*Midas*SE*Unauthorised*Position*Settlements****                  HUT105
      *  PRTF/XX105024P1 - Midas SE Unauthorised Position Settlements *                  HUT105
      *                  Report                                       *
      *                                                               *
      ***(c)*Misys*International*Banking*Systems*Ltd.*2008*************                  HUT105
      *  (c) Finastra International Systems Ltd. 2020                 *                  HUT105
      *                                                               *
      *  Last Amend No. HUT105             Date 07Apr20               *
      *  Prev Amend No. HUT015             Date 01Jul10               *
      *                 HUT011I09          Date 11Sep08               *
      *                 HUT011  *CREATE    Date 12May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  HUT105 - Bulk Position Settlement Authorisation              *
      *           (Upgrade HUT011/015 to FM2.1 SP22)                  *
      *  HUT015 - 4eyes on Bulk Position Settlement Function          *
      *  HUT011I09 - Amount decimal place formatting error            *
      *  HUT011 - Bulk Position Settlement Authorisation              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      ** Start of HUT105
     A**********R SEH241D1
     A**********                            TEXT('(c) Misys International Banki-
     A**********                            ng Systems Ltd. 2008')
     A          R XX105241D1
     A                                      TEXT('(c) Finastra International Sy-
     A                                      stems Ltd. 2020')
      ** End of HUT105
      *
     A                                      SKIPB(3)
      ** Start of HUT105
     A**********                           6'REFERENCE SEH024P1'
     A                                     6'REFERENCE XX105024P1'
      ** End of HUT105
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    47'UNAUTHORISED POSITION SETTLEMENTS'
     A                                      SPACEA(1)
     A                                    47'---------------------------------'
     A                                      SPACEA(1)
      *
      ** Start of HUT105
     A**********R SEH241D2                                                               HUT105
     A          R XX105241D2                                                             HUT105
      ** End of HUT105
     A                                     1'Security:'
     A            RSESN         10A       11TEXT('Security')
     A                                    25'Event Type:'
     A            REVTP          2A       37TEXT('Event Type')
     A                                    44'Due Date:'
     A            RDUDT          7A       54TEXT('Due Date')
     A                                      SPACEA(1)
      *
     A                                     1'Banking'
     A                                     9'Customer'
     A                                    18'Cust'
     A                                    25'Portfolio'
     A                                    35'Value'
     A                                    43'Amount Due'
     A                                    61'Sett'
     A                                    66'Tax Amount'
     A                                    84'Settlement'
     A                                   102'Settlement'
      *
     A                                      SPACEA(1)
      *
     A                                     1'Branch'
     A                                    18'Type'
     A                                    35'Date'
     A                                    61'Curr'
     A                                    66'Deducted'
     A                                    84'Amount'
     A                                   102'Account'
      *
     A                                      SPACEA(1)
     A                                     1'-------'
     A                                     9'--------'
     A                                    18'------'
     A                                    25'---------'
     A                                    35'-------'
     A                                    43'-----------------'
     A                                    61'----'
     A                                    66'-----------------'
     A                                    84'-----------------'
     A                                   102'-----------------------'
     A                                      SPACEA(1)
      *
      ** Start of HUT105
     A**********R SEH241D3                                                               HUT105
     A          R XX105241D3                                                             HUT105
      ** End of HUT105
      *
     A            RBRCH          3A        1TEXT('Branch')
     A            RCUSN          6A        9TEXT('Customer')
     A            RCTYP          6A       18TEXT('Customer Type')
     A            RPTFR          4A       25TEXT('Portfolio')
     A            RVLDT          7A       35TEXT('Value Date')
      * Start of change HUT011I09
     A**********  RAMTD         13  2     43TEXT('Amount Due')                             HUT011I09
     A**********                            EDTWRD('  ,   ,   , 0 .  ')                    HUT011I09
     A            RAMTD         17A       43TEXT('Amount Due')
      * End of change HUT011I09
     A            RPSCU          3A       61TEXT('Settlement Currency')
      * Start of change HUT011I09
     A**********  RTXAD         13  2     66TEXT('Tax Amount Deducted')                    HUT011I09
     A**********                            EDTWRD('  ,   ,   , 0 .  ')                    HUT011I09
     A**********  RPSAT         13  2     84TEXT('Settlement Amount')                      HUT011I09
     A**********                            EDTWRD('  ,   ,   , 0 .  ')                    HUT011I09
     A            RTXAD         17A       66TEXT('Tax Amount Deducted')
     A            RPSAT         17A       84TEXT('Settlement Amount')
      * End of change HUT011I09
     A            RPSEA         24A      102TEXT('Settlement Account')
     A                                      SPACEA(1)
      *                                                                                       HUT015
      ** Start of HUT105
     A**********R SEH241DB                                                               HUT105
     A          R XX105241DB                                                             HUT105
      ** End of HUT105
      *                                                                                       HUT015
     A                                     1'Last Action User:'                               HUT015
     A            RLUSR         10A       19TEXT('Last Action User')                          HUT015
     A                                      SPACEA(1)                                         HUT015
      *                                                                                       HUT015
      ** Start of HUT105
     A**********R SEH241D4                                                               HUT105
     A          R XX105241D4                                                             HUT105
      ** End of HUT105
     A                                      SPACEB(2)
     A                                    36'Total'
     A            RCTYPA         6A       42TEXT('Customer Type')
     A                                    49'Settlement Amount for Branch'
     A            RBRCHA         3A       78TEXT('Branch')
     A                                    81':'
      * Start of change HUT011I09
     A**********  RPSATA        13  2     84TEXT('Settlement Amount')                      HUT011I09
     A**********                            EDTWRD('  ,   ,   , 0 .  ')                    HUT011I09
     A            RPSATA        17A       84TEXT('Settlement Amount')
      * End of change HUT011I09
     A                                      SPACEA(1)
      *
      ** Start of HUT105
     A**********R SEH241D5                                                               HUT105
     A          R XX105241D5                                                             HUT105
      ** End of HUT105
     A                                      SPACEB(2)
     A                                     1'Total Unauthorised Position-
     A                                       Settlements for'
     A            RSESNA        10A       45TEXT('Security')
     A            REVTPA         2A       56TEXT('Event Type')
     A            RDUDTA         7A       59TEXT('Due Date')
     A                                    66':'
     A            RTOTAL         4  0     69TEXT('Total for each Security')
     A                                      SPACEA(1)
      *
      ** Start of HUT105
     A**********R SEH241D6                                                               HUT105
     A          R XX105241D6                                                             HUT105
      ** End of HUT105
     A                                      SPACEB(2)
     A                                     1
     A                                      ' '
      *
      ** Start of HUT105
     A**********R SEH241D7                                                               HUT105
     A          R XX105241D7                                                             HUT105
      ** End of HUT105
     A                                    52
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
      ** Start of HUT105
     A**********R SEH241D8                                                               HUT105
     A          R XX105241D8                                                             HUT105
      ** End of HUT105
     A                                     1'No details to print.'
     A                                      SPACEA(1)
      *
