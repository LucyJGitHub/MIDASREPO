      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('G-AML Customer Details Audit report')                  *
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      ***PRTF/SCJ0005P1*-*G-AML FTP Audit Report***********************
      *  PRTF/XX109006P1 - G-AML Customer Details Audit Report        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2008            *
      *                                                               *
      *  Last Amend No. JMI109             Date 22May18               *
      *  Prev Amend No. JMI026  *Create    Date 30Apr08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  JMI109 - G-AML PreProcessor (Upgrade to FB Midas)            *
      *  JMI026 - G-AML PreProcessor                                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2008')
      *
     A                                      SKIPB(3)
     A************                         3'REFERENCE SCJ00005P1'
     A                                     3'REFERENCE XX109006P1'
     A            BJURPT    R             27REFFLD(URPT)
     A                                   114'DATE'
     A            BJMRDT    R            119REFFLD(MRDT)
     A                                   127'PAGE'
     A                                   129PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   114'TIME'
     A                                   119TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    54'EXTRACTED G-AML CUSTOMER DETAILS'
     A                                      SPACEA(1)
      *
     A                                    54'--------------------------------'
     A                                      SPACEA(2)
      *
     A                                     5'Branch:'
     A            RBRCD          3   O    13
     A                                    17'-'
     A            RBRCH         30   O    19
     A                                      SPACEA(2)
     A                                     6'No.'
     A                                    14'Customer'
     A                                    26'Report'
     A                                    51'Risk'
     A                                    58'Cust'
     A                                    75'Customer'
     A                                   112'Country'
     A                                      SPACEA(1)
     A                                    14'ID'
     A                                    26'Name'
     A                                    51'Class'
     A                                    58'Type'
     A                                    75'Address'
     A*                                     SPACEA(2)
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A                                      SPACEB(1)
     A            RNUM           6   O     6
     A            RCUSID         6   O    14
     A            RRPTNM        20        26
     A            RRSKCL         4        51
     A            RCUSTY        11        58
     A            RCNTRY         2       112
     A            RCUSAD1       35        75
     A                                      SPACEA(1)
     A            RCUSAD2       35        75
     A                                      SPACEA(1)
     A            RCUSAD3       35        75
     A                                      SPACEA(1)
     A            RCUSAD4       35        75
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Trailer 1            *
      *-------------------------*
      *
     A          R TRAIL1
     A
     A                                    47'Total Customers Read and +
                                            Extracted to File for branch'
     A            RBRCD1         3   O   101
     A                                   104':'
     A            RBRCNT         6  0O   106EDTCDE(Z)
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Trailer 2            *
      *-------------------------*
      *
     A          R TRAIL2
     A
     A                                    47'Total Customers Read and +
     A                                      Extracted to File:'
     A            RTOTAL         6  0O    91EDTCDE(Z)
     A                                      SPACEA(2)
