      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('G-AML Account Extract Audit report')                   *
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      ***PRTF/SCJ0006P1*-*G-AML Account Extract Audit Report***********
      *  PRTF/XX109007P1 - G-AML Account Extract Audit Report         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2008            *
      *                                                               *
      *  Last Amend No. JMI109             Date 22May18               *
      *  Prev Amend No. JMI026  *Create    Date 30Apr08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  JMI109 - G-AML PreProcessor (Upgrade to FB Midas)            *
      *  JMI026 - G-AML PreProcessor                                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2008')
      *
     A                                      SKIPB(3)
     A************                         6'REFERENCE SCJ00006P1'
     A                                     6'REFERENCE XX109007P1'
     A            BJURPT    R             27REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   116'PAGE'
     A                                   121PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    48'Extracted G-AML Account Details'
     A                                      SPACEA(1)
      *
     A                                    48'--------------------------------'
     A                                      SPACEA(2)
      *
     A                                    14'Branch:'
     A            RBRCD          3   O    22
     A                                    26'-'
     A            RBRCH         30   O    28
     A                                      SPACEA(2)
     A                                    19'No.'
     A                                    27'Account'
     A                                    44'Account'
     A                                    70'Closed'
     A                                    79'Type'
     A                                    86'Account'
     A                                    96'Account'
     A                                      SPACEA(1)
     A                                    27'ID'
     A                                    44'Name'
     A                                    86'Rel'
     A                                    96'Party'
     A*                                     SPACEA(2)
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A                                      SPACEB(1)
     A            RNUM           6   O    19
     A            RACCID        10   O    27
     A            RACCNM        20        44
     A            RCLSED         1        72
     A            RTYPE          1        80
     A            RACCREL        5        86
     A            RACCPRT        6        96
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Trailer 1            *
      *-------------------------*
      *
     A          R TRAIL1
     A
     A                                    35'Total Customers Read and +
                                            Extracted to File for branch'
     A            RBRCD1         3   O    89
     A                                    92':'
     A            RBRCNT         6  0O    94EDTCDE(Z)
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Trailer 2            *
      *-------------------------*
      *
     A          R TRAIL2
     A
     A                                    35'Total Customers Read and +
     A                                      Extracted to File:'
     A            RTOTAL         6  0O    79EDTCDE(Z)
     A                                      SPACEA(2)
