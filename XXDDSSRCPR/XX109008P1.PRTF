      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('G-AML Activity Details Not Extracted Audit Report')    *
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      ***PRTF/SCJ0007P1*-*Activity Details Not Extracted Audit Report**
      *  PRTF/XX109008P1 - Activity Details Not Extracted Audit Report*
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2008            *
      *                                                               *
      *  Last Amend No. JMI109             Date 22May18               *
      *  Prev Amend No. JMI026  *Create    Date 16Sep08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  JMI109 - G-AML PreProcessor (Upgrade to FB Midas)            *
      *  JMI026 - G-AML PreProcessor                                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2008')
      *
     A                                      SKIPB(3)
     A************                         3'REFERENCE SCJ00007P1'
     A                                     3'REFERENCE XX109008P1'
     A            BJURPT    R             27REFFLD(URPT)
     A                                   105'DATE'
     A            BJMRDT    R            110REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      SPACEA(1)
      *
     A                                   105'TIME'
     A                                   110TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    54' Activity Details Not Extracted '
     A                                      SPACEA(1)
      *
     A                                    54' ------------------------------ '
     A                                      SPACEA(2)
      *
     A                                     7'No'
     A                                    14'Mod'
     A                                    18'Type'
     A                                    26'Sub'
     A                                    32'Cust'
     A                                    40'Retail'
     A                                    52'Posting'
     A                                    62'Value'
     A                                    72'Ccy'
     A                                    78'Posting'
     A                                    97'Narrative'
     A                                      SPACEA(1)
      *
     A                                    26'type'
     A                                    32'No'
     A                                    40'Number'
     A                                    52'Date'
     A                                    62'Date'
     A                                    78'Amount'
     A                                    97'Source & Reference'
     A                                      SPACEA(2)
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A                                      SPACEB(1)
     A            RNUM           6         7TEXT('Number')
     A            RGMOD          2        15TEXT('Module')
     A            RGTYP          5        18TEXT('Type')
     A            RGSTYP         2        28TEXT('Subtype')
     A            RCNUM          6        32TEXT('Customer Number')
     A            RACNO         10        40TEXT('Retail Number')
     A            RPDAT          8        52TEXT('Posting Date')
     A            RVDAT          8        62TEXT('Value Date')
     A            RCCY           3        72TEXT('Currency')
     A            RPAMT         13        78TEXT('Posting Amount')
     A            RNARR         30        97TEXT('Narrative')
     A                                      SPACEA(1)
     A            RSRC           7        97TEXT('Source')
     A            RREF          15       107TEXT('Reference')
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Trailer 1            *
      *-------------------------*
      *
     A          R TRAIL1
     A
     A                                    50'Total Postings Read But Not Extrac-
     A                                      ted to File:'
     A            RTNPOS         6  0     97TEXT('Total Postings Not Extracted')
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
