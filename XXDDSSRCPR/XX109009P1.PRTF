      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('G-AML Activity Details Not Extracted Audit Report')    *
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      ***PRTF/SCJ0008P1*-*G-AML Activity Extract Audit Report**********
      *  PRTF/XX109009P1 - G-AML Activity Extract Audit Report        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2008            *
      *                                                               *
      *  Last Amend No. JMI109             Date 22May18               *
      *  Prev Amend No. JMI026  *Create    Date 30Apr08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  JMI109 - G-AML PreProcessor (Upgrade to FB Midas)            *
      *  JMI026 - G-AML PreProcessor                                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *
      *-------------------------*
      ***  Report Header 1      *
      *-------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2008')
      *
     A                                      SKIPB(3)
     A************                         3'REFERENCE SCJ00008P1'
     A                                     3'REFERENCE XX109009P1'
     A            BJURPT    R             27REFFLD(URPT)
     A                                   105'DATE'
     A            BJMRDT    R            110REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      SPACEA(1)
      *
     A                                   105'TIME'
     A                                   110TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    51'Extracted G-AML Activity Details'
     A                                      SPACEA(1)
      *
     A                                    51'--------------------------------'
     A                                      SPACEA(2)
      *
     A                                     7'No'
     A                                    14'Type'
     A                                    20'Cust'
     A                                    28'Retail'
     A                                    40'Cash'
     A                                    46'Base'
     A                                    71'Rec/'
     A                                    76'Ccy'
     A                                    86'Orig Amount'
     A                                   110'Value'
     A                                   120'Post'
     A                                      SPACEA(1)
      *
     A                                    20'ID'
     A                                    28'Number'
     A                                    40'Tran'
     A                                    46'Amount'
     A                                    71'Pay'
     A                                    86'Reference'
     A                                   110'Date'
     A                                   120'Date'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail 1             *
      *-------------------------*
      *
     A          R DETAIL1
      *
     A                                      SPACEB(1)
     A            RNUM           6         7TEXT('Number')
     A            RTYPE          4        14TEXT('Type')
     A            RCNUM          6        20TEXT('Cust ID')
     A            RACNO         10        28TEXT('Retail Number')
     A            RCASH          1        42TEXT('Cash Tran')
     A            RBAMT         18        46TEXT('Base Amount')
     A            RRCPY          1        71TEXT('RecvPay')
     A            RCURR          3        76TEXT('Currency')
     A            RFXAM         18        86TEXT('Original Amount')
     A            RVDAT          8       110TEXT('Value Date')
     A            RBDAT          8       120TEXT('Book Date')
     A                                      SPACEA(1)
      *
     A            RREF          35        86TEXT('Reference')
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Trailer 1            *
      *-------------------------*
      *
     A          R TRAIL1
     A
     A                                    45'Total Postings Read and Extracted -
     A                                      to File:'
     A            RTNPOS         6  0     88TEXT('Total Postings Extracted')
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
      *
      *-------------------------*
      ***  Report Header 2      *
      *-------------------------*
      *
     A          R HEADP2
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2008')
      *
     A                                      SKIPB(3)
     A************                         3'REFERENCE SCJ0008P1'
     A                                     3'REFERENCE XX109009P1'
     A            BJURPT    R             27REFFLD(URPT)
     A                                   105'DATE'
     A            BJMRDT    R            110REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      SPACEA(1)
      *
     A                                   105'TIME'
     A                                   110TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    43'Extracted G-AML Activity Currency-
     A                                       Spot Rate Details'
     A                                      SPACEA(1)
      *
     A                                    43'-----------------------------------
     A                                       -----------------'
     A                                      SPACEA(2)
      *-------------------------*
      ***  Detail 2             *
      *-------------------------*
      *
     A          R DETAIL2
      *
     A                                      SPACEB(1)
     A            RCCY1          5         7TEXT('Currency')
     A  30        RRATE1        13  8     13TEXT('Spot Rate')
     A                                      EDTWRD('  , 0 .        ')
     A            RCCY2          5        33TEXT('Currency')
     A  31        RRATE2        13  8     39TEXT('Spot Rate')
     A                                      EDTWRD('  , 0 .        ')
     A            RCCY3          5        59TEXT('Currency')
     A  32        RRATE3        13  8     65TEXT('Spot Rate')
     A                                      EDTWRD('  , 0 .        ')
     A            RCCY4          5        85TEXT('Currency')
     A  33        RRATE4        13  8     91TEXT('Spot Rate')
     A                                      EDTWRD('  , 0 .        ')
      *
      *-------------------------*
      ***  Trailer 2            *
      *-------------------------*
      *
     A          R TRAIL2
     A
     A                                      SPACEB(2)
     A                                    54'***  END OF REPORT  ***'
     A                                      SPACEA(1)
