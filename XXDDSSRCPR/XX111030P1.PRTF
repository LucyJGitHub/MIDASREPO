      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*E*S****PR_STD60******************************************************                     MD058450
/*EXS *  PR_STD66                                                     *                     MD058450
/*EXI *  TEXT('Midas SE Pos Sett Authorised Over Blk CR A/C Rep')     *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      ***PRTF/SEH030P1*-*SE*Positions*Settlements*Authorised*Over******                  HUT111
      *  PRTF/XX111030P1 - SE Positions Settlements Authorised Over   *                  HUT111
      *                  Blocked Credit A/C Report                    *
      *                                                               *
      ***(c)*Misys*International*Banking*Systems*Ltd.*2016*************                  HUT111
      *  (c) Finastra International Systems Ltd. 2020                 *                  HUT111
      *                                                               *
      *  Last Amend No. MD058450           Date 10Aug21               *
      *  Prev Amend No. HUT111             Date 08Apr20               *
      *                 HUT024  *CREATE    Date 17Oct16               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD058450 - Component XXC111030 failed due to I/O error       *
      *             CPF5004 file XX111030P1. Change printerfile       *
      *             overflow to 66.                                   *
      *  HUT111 - Corporate Actions Blocked Credit Account            *
      *           (Upgrade HUT024 to FM2.1 SP22)                      *
      *  HUT024  Corporate Actions Blocked Credit Account            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Start of HUT111
     A**********R SEH030D1
     A**********                            TEXT('(c) Misys International Banki-
     A**********                            ng Systems Ltd. 2016')
     A          R XX111030D1
     A                                      TEXT('(c) Finastra International Sy-
     A                                      stems Ltd. 2020')
      ** End of HUT111
      *
     A                                      SKIPB(2)
      ** Start of HUT111
     A**********                           6'REFERENCE SEH030P1'
     A                                     6'REFERENCE XX111030P1'
      ** End of HUT111
     A                                      SPACEB(1)
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    44'POSITION SETTLEMENTS AUTHORISED TO-
     A                                      DAY'
     A                                      SPACEA(1)
     A*
     A                                    46'INVOLVING BLOCKED CREDITS ACCOUNTS'
     A                                      SPACEA(1)
     A                                    46'----------------------------------'
     A                                      SPACEA(1)
      *
     A                                     6'Booking'
     A                                      SPACEB(1)
     A                                    17'Security'
     A                                    33'Customer'
     A                                    46'Event'
     A                                    57'Value'
     A                                    70'Amount Due'
     A                                    90'Sett'
     A                                   100'Settlement'
     A                                   114'Settlement'
      *
     A                                      SPACEA(1)
      *
     A                                     6'Branch'
     A                                    46'Type'
     A                                    57'Date'
     A                                    90'Curr'
     A                                   100'Method'
     A                                   114'Account'
      *
     A                                      SPACEA(1)
     A                                     6'-------'
     A                                    17'-----------'
     A                                    33'--------'
     A                                    46'------'
     A                                    56'------'
     A                                    67'-----------------'
     A                                    90'----'
     A                                   100'-----------'
     A                                   114'-------------'
     A                                      SPACEA(2)
      *
      ** Start of HUT111
     A**********R SEH030D3
     A          R XX111030D3
      ** End of HUT111
      *
     A            RPBCA          3A        8TEXT('Booking Branch')
     A            RPSSH         10A       17TEXT('Security')
     A            RPCPY          6A       34TEXT('Customer Number')
     A            RPEVT          2A       48TEXT('Event Type')
     A            RPSVL          7A       56TEXT('Value Date')
     A            RPAMD         19A       67TEXT('Amount')
     A*
     A            RPSCU          3A       90TEXT('Settlement Currency')
     A*
     A            RPSMT          2A      102TEXT('Settlement Method')
     A*
     A            RACNO         10A      114TEXT('Settlement Account')
     A                                      SPACEA(1)
      *
      ** Start of HUT111
     A**********R SEH030D4
     A          R XX111030D4
      ** End of HUT111
     A                                      SPACEB(2)
     A                                    23'Total Authorised Position Settleme-
     A                                      nts settled to blocked credit accou-
     A                                      nt today:           '
     A            RPTOT          4  0    102TEXT('Total Audit')
     a                                      EDTWRD('  0 ')
     A                                      SPACEA(1)
     A                                    50
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
      ** Start of HUT111
     A**********R SEH030D5
     A          R XX111030D5
      ** End of HUT111
     A                                    50
     A                                      '***  NO DETAILS TO REPORT  ***'
     A                                      SPACEA(1)
      *
