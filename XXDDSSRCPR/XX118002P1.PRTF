      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('On-line generation of Position Settlemens Report')     *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/XX118002P1 - Midas SE On-line generation of Position    *
      *                  Settlements Report                           *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. HUT118  *CREATE    Date 10Dec20               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  HUT118 - On-line generation of Position Settlements          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R XX118002D1
     A                                      TEXT('(c) Finastra International Sy-
     A                                      stems Ltd. 2020')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE XX118002P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    48'GENERATED POSITION SETTLEMENTS'
     A                                      SPACEA(1)
     A                                    48'------------------------------'
     A                                      SPACEA(1)
      *
     A          R XX118002D2
     A                                     1'Security:'
     A            RSESN         10A       11TEXT('Security')
     A                                    25'Event Type:'
     A            REVTP          2A       37TEXT('Event Type')
     A                                    44'Due Date:'
     A            RDUDT          7A       54TEXT('Due Date')
     A                                      SPACEA(1)
      *
     A                                     1'Banking'
     A                                     9'Customer'
     A                                    18'Cust'
     A                                    25'Portfolio'
     A                                    35'Value'
     A                                    43'Amount Due'
     A                                    61'Sett'
     A                                    66'Tax Amount'
     A                                    84'Settlement'
     A                                   102'Settlement'
      *
     A                                      SPACEA(1)
      *
     A                                     1'Branch'
     A                                    18'Type'
     A                                    35'Date'
     A                                    61'Curr'
     A                                    66'Deducted'
     A                                    84'Amount'
     A                                   102'Account'
      *
     A                                      SPACEA(1)
     A                                     1'-------'
     A                                     9'--------'
     A                                    18'------'
     A                                    25'---------'
     A                                    35'-------'
     A                                    43'-----------------'
     A                                    61'----'
     A                                    66'-----------------'
     A                                    84'-----------------'
     A                                   102'-----------------------'
     A                                      SPACEA(1)
      *
     A          R XX118002D3
      *
     A            RBRCH          3A        1TEXT('Branch')
     A            RCUSN          6A        9TEXT('Customer')
     A            RCTYP          6A       18TEXT('Customer Type')
     A            RPTFR          4A       25TEXT('Portfolio')
     A            RVLDT          7A       35TEXT('Value Date')
     A            RAMTD         17A       43TEXT('Amount Due')
     A            RPSCU          3A       61TEXT('Settlement Currency')
     A            RTXAD         17A       66TEXT('Tax Amount Deducted')
     A            RPSAT         17A       84TEXT('Settlement Amount')
     A            RPSEA         24A      102TEXT('Settlement Account')
     A                                      SPACEA(1)
      *
     A          R XX118002D4
     A                                      SPACEB(2)
     A                                    36'Total'
     A            RCTYPA         6A       42TEXT('Customer Type')
     A                                    49'Settlement Amount for Branch'
     A            RBRCHA         3A       78TEXT('Branch')
     A                                    81':'
     A            RPSATA        17A       84TEXT('Settlement Amount')
     A                                      SPACEA(1)
      *
     A          R XX118002D5
     A                                      SPACEB(2)
     A                                     1'Total Generated Position-
     A                                       Settlements for'
     A            RSESNA        10A       45TEXT('Security')
     A            REVTPA         2A       56TEXT('Event Type')
     A            RDUDTA         7A       59TEXT('Due Date')
     A                                    66':'
     A            RTOTAL         4  0     69TEXT('Total for each Security')
     A                                      SPACEA(1)
      *
     A          R XX118002D6
     A                                      SPACEB(2)
     A                                     1
     A                                      ' '
      *
     A          R XX118002D7
     A                                    52
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
     A          R XX118002D8
     A                                     1'No details to print.'
     A                                      SPACEA(1)
      *
