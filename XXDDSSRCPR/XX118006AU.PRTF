      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Traiding Module                           *
      *                                                               *
      *  PRTF/XX118006AU - Position Settlement check/delete           *
      *                    Run Control                                *
      *                                                               *
      *  (c) Finastra International Limited 2022                      *
      *                                                               *
      *  Last Amend No. HUT118  *Create    Date 03Nov22               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  HUT118 - Position Settlements Online Generation              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra Internation+
     A                                      al Limited 2020')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
     A                                    16'XX118006AU'
     A            BJURPT        53        37
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      SPACEA(1)
      *
     A                                    36'Position Settlements +
     A                                      check/delete - Run Controls'
     A                                      SPACEA(1)
     A                                    36'---------------------+
     A                                      ---------------------------'
     A                                      SPACEA(3)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
      *
     A                                    78'CALCULATED' SPACEA(2)
     A                                    84'NO.'
     A                                      SPACEA(3)
     A                                     6'FILE - POSITION SETTLEMENTS'
     A                                    34'- PF/POSETD'
     A                                      SPACEA(1)
     A                                     6'---------------------------'
     A                                    33'------------'
     A                                      SPACEA(3)
     A                                    10'NO. OF RECORDS DELETED'
     A            DEL_RCDS       5  0     82EDTCDE(3)
     A                                      SPACEA(3)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
