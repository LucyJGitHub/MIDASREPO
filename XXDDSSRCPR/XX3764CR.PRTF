      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE Int. capitalisation report cr')               *
/*ADD *  CHGPRTF FILE(XX3764CR) PAGRTT(0) SAVE(*YES) CPI(16.7)        *
/*ADD *  CHGPRTF FILE(XX3764CR) FONT(*CPI)                            *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module
     A*                                                               *
     A*  PRTF/XX3764CR - INTEREST CAPITALISATION REPORT               *
     A*                  CREDIT                                       *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      *  Last Amend No. LUC114   *CREATE   Date 23Jan19               *
      *  Prev Amend No. MD024700           Date 03Apr14               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 BUG26838           Date 18Dec09               *
      *                 CER048             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 30Nov05               *
      *                 CGL035             Date 01Mar05               *
      *                 CGL031             Date 05Jul04               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 182452             Date 20Jun01               *
      * Midas DBA 3.05 -----------------------------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CFT004             Date 19Jul99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 130468             Date 27Apr99               *
      *                 CRE003             Date 26Apr96               *
     A*                 094657             DATE 25NOV95               *
     A*                 094674             DATE 22NOV95               *
     A*                 086087             DATE 22AUG95               *
     A*                 057841             DATE 07MAR94               *
     A*                 040447             DATE 28SEP93               *
     A*                 S01413 *CREATE     DATE 13APR93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  LUC114 - Change Layout of Interest Capitalization Report     *
     A*           Layout of RE3764CR cloned as base to XX3764CR to    *
     A*           Change for LUC114 requirements.                     *
     A*           /Copy and comments removed to create the XX3764CR   *
      *  MD024700 - Move IBAN number to start 3 positions to the right*
      *             of retail account number.                         *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG26838 - RE3764CR did not show 3rd tax details             *
      *  CER048 - German Features - Taxes                             *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL035 - EUSD Upgrade to Midas Plus                          *
      *  CGL031 - Taxation of Savings Income                          *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  182452 - Reformat the subtotal for interest types 03 and 04  *
      *  CFT004 - Straight Through Processing Phase 2                 *
      *           International Bank Account Numbers (IBAN)           *
     A*  130468 - BRT not deducted from CR interest adjustments.      *
     A*  CRE003 - Ledger Balance Accruals                             *
     A*  094657 - Suppress customer address details if specified.     *
     A*  094674 - Amend to display negative interest rates.           *
     A*         - Alternate account. If not multi ccy, don't ouput    *
     A*           rate.                                               *
     A*         - CR interest amount reverse sign so suppress 'CR'.   *
     A*  086087 - Change format of tiered/threshold interest to same  *
     A*           as in RE3364E and display 'tiered'/'threshold'.     *
     A*           Change position of no. of days.                     *
     A*         - Amounts nor displayed consistently. xCR x- etc.     *
     A*  057841 - WITHHOLDING TAX is a debit not credit amount.       *
     A*  040447 - Print the correct opening rate                      *
     A*  S01413 - Retail 3 Incorporation                              *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A**   *************
     A**   REPORT HEADER
     A**   *************
     A**
     A          R HEADCR                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                      SPACEB(9)
     A                                    43'RETAIL INTEREST '
     A                                    59'CAPITALISATION REPORT'
     A                                   101'DATE'
     A            RUNA      R            106
     A                                      SPACEA(1)
     A*
     A                                    43'----------------'
     A                                    59'---------------------'
     A                                      SPACEA(2)
     A  23        CNA1      R             15
     A  70                                46'*** MANAGEMENT '
     A  70                                61'CAPITALISATION ***'
     A                                   101'CREDIT INTEREST'
     A                                      SPACEA(1)
     A*
     A  23        CNA2      R             15
     A                                      SPACEA(1)
     A*
     A  23        CNA3      R             15
     A                                      SPACEA(1)
     A  23        CNA4      R             15
     A                                      SPACEA(1)
     A*
     A            CBRCA2    R             89REFFLD(BRCA)
     A                                    92'-'
     A            CCNUM          6A       93TEXT('Customer number')
     A                                    99'-'
     A            CCCY      R            100
     A                                   103'-'
     A            CACOD     R            104REFFLD(ACOD)
     A                                   114'-'
     A            CACSQ     R            115REFFLD(ACSQ)
     A     65     ACNO      R            120
     A N04 65                               SPACEA(3)
     A N65N04     BLK10         10        72
     A N65N04                               SPACEA(4)
     A  04        RIBAN         47        89SPACEB(1)
     A                                      SPACEA(3)
     A*
     A N69                                10'VALUE'
     A  69                                10'EFFECT'
     A                                    17'NO.'
     A                                    62'POSTING'
     A                                      SPACEA(1)
     A*
     A                                    11'DATE'
     A                                    17'DAYS'
     A                                    23'NARRATIVE'
     A                                    63'AMOUNT'
     A                                    82'BALANCE'
     A                                   113'INTEREST'
     A                                      SPACEA(1)
     A*
     A**
     A**   *************
     A**   REPORT DETAIL
     A**   *************
     A**
     A          R POSTCR                    SPACEB(1)
     A*
     A            PSTDA     R              1REFFLD(RUNA)
     A            VALDA     R              9REFFLD(RUNA)
     A            NARR      R             21REFFLD(PNAR)
     A  80        PSTD0         15  0     52EDTCDE(C)
     A  81        PSTD1         15  1     52EDTCDE(C)
     A  82        PSTD2         15  2     52EDTCDE(C)
     A  83        PSTD3         15  3     52EDTCDE(C)
     A*
     A          R RATECR                    SPACEB(1)
     A*
     A            PSTDA     R              1REFFLD(RUNA)
     A            VALDA     R              9REFFLD(RUNA)
     A            RNARR     R             21REFFLD(PNAR)
     A            RATE          11  7     52EDTWRD('   0.       -%')
     A*
     A          R TITLECR                   SPACEB(1)
     A*
     A            VALDA     R              9REFFLD(RUNA)
     A            TNARR     R             21REFFLD(PNAR)
     A  80        HISB0         15  0     71EDTCDE(C)
     A  81        HISB1         15  1     71EDTCDE(C)
     A  82        HISB2         15  2     71EDTCDE(C)
     A  83        HISB3         15  3     71EDTCDE(C)
     A*
     A          R STOTCR                    SPACEB(1)
     A*
     A  26        WMNTA     R              9REFFLD(RUNA)
     A N26        DAYS           4  0     16EDTCDE(C)
     A  80N25     NUMB0         15  0     33EDTCDE(C)
     A  81N25     NUMB1         15  1     33EDTCDE(C)
     A  82N25     NUMB2         15  2     33EDTCDE(C)
     A  83N25     NUMB3         15  3     33EDTCDE(C)
     A  26                                54'/Min. Balance'
     A  80        HISB0         15  0     71EDTCDE(C)
     A  81        HISB1         15  1     71EDTCDE(C)
     A  82        HISB2         15  2     71EDTCDE(C)
     A  83        HISB3         15  3     71EDTCDE(C)
     A  80N25     INTC0         15  0    106EDTCDE(C)
     A  81N25     INTC1         15  1    106EDTCDE(C)
     A  82N25     INTC2         15  2    106EDTCDE(C)
     A  83N25     INTC3         15  3    106EDTCDE(C)
     A*
     A          R TOTALCR                   SPACEB(1)
     A*
     A            DAYS           4  0     16EDTCDE(C)
     A    N22     XNARR     R             21REFFLD(PNAR)
     A  80N22     TINTC0        15  0    106EDTCDE(C)
     A  81N22     TINTC1        15  1    106EDTCDE(C)
     A  82N22     TINTC2        15  2    106EDTCDE(C)
     A  83N22     TINTC3        15  3    106EDTCDE(C)
     A     22                             21'TOTAL GROSS INTEREST'
     A  80 22     TGINT0        15  0    106EDTCDE(C)
     A  81 22     TGINT1        15  1    106EDTCDE(C)
     A  82 22     TGINT2        15  2    106EDTCDE(C)
     A  83 22     TGINT3        15  3    106EDTCDE(C)
     A  41 22                            122'CR'
     A          R TOTBRT                    SPACEB(1)
     A     22                             21'LESS BRT AT'
     A     22     WKBRT          7  5     33EDTWRD(' 0.     %')
     A  80 22     BRTOT0        15  0    106EDTCDE(C)
     A  81 22     BRTOT1        15  1    106EDTCDE(C)
     A  82 22     BRTOT2        15  2    106EDTCDE(C)
     A  83 22     BRTOT3        15  3    106EDTCDE(C)
     A*
      ** Start of change CGL031
     A          R EUWTAX                    SPACEB(1)
     A     21                             21'LESS EUROPEAN WITHHOLDING TAX'
     A  80 21     EUTAX0        15  0    106EDTCDE(C)
     A  81 21     EUTAX1        15  1    106EDTCDE(C)
     A  82 21     EUTAX2        15  2    106EDTCDE(C)
     A  83 21     EUTAX3        15  3    106EDTCDE(C)
      ** End of change CGL031
      **Start of change CER048
     A          R SECTAX                    SPACEB(1)
     A                                    21'LESS SECONDARY TAX'
     A  80        STAX0         15  0    106EDTCDE(C)
     A  81        STAX1         15  1    106EDTCDE(C)
     A  82        STAX2         15  2    106EDTCDE(C)
     A  83        STAX3         15  3    106EDTCDE(C)
      **End of change CER048
      **Start of change BUG26838
     A          R THRTAX                    SPACEB(1)
     A                                    21'LESS THIRD TAX'
     A  80        TTAX0         15  0    106EDTCDE(C)
     A  81        TTAX1         15  1    106EDTCDE(C)
     A  82        TTAX2         15  2    106EDTCDE(C)
     A  83        TTAX3         15  3    106EDTCDE(C)
      **End of change BUG26838
     A          R TOTDCR                    SPACEB(1)
     A*
     A            DNARR     R             21REFFLD(PNAR)
     A  80        TINTC0        15  0    106EDTCDE(C)
     A  81        TINTC1        15  1    106EDTCDE(C)
     A  82        TINTC2        15  2    106EDTCDE(C)
     A  83        TINTC3        15  3    106EDTCDE(C)
     A  41                               122'DR'
     A*
     A          R RAT1CR                    SPACEB(1)
     A*
     A            PSTDA     R              1REFFLD(RUNA)
     A            VALDA     R              9REFFLD(RUNA)
     A            RNARR     R             21REFFLD(PNAR)
     A*                                            /*Begin 086087*/
     A  84                                77'   Tiered'
     A  85                                77'Threshold'
     A*                                            /*End   086087*/
     A*
     A          R RAT2CR                    SPACEB(1)
     A*
     A            BNARR     R             21REFFLD(PNAR)
     A  80        HISB0         15  0     71EDTCDE(C)
     A  81        HISB1         15  1     71EDTCDE(C)
     A  82        HISB2         15  2     71EDTCDE(C)
     A  83        HISB3         15  3     71EDTCDE(C)
     A*
     A          R RAT3CR                    SPACEB(1)
     A*
     A*                                            /*Begin 086087*/
     A                                    27'****'
     A  80        OBAL01        15  0     32EDTCDE(C)
     A  81        OBAL11        15  1     32EDTCDE(C)
     A  82        OBAL21        15  2     32EDTCDE(C)
     A  83        OBAL31        15  3     32EDTCDE(C)
     A                                    51'****'
     A*                                            /*Begin 094674*/
     A            ORAT1         11  7     56EDTWRD('   0.       -%')
     A*                                            /*End   094674*/
     A                                    70'****'
     A*
     A*                                            /*End   086087*/
     A*
     A          R ALTNCR                    SPACEB(1)
     A*
     A            NARR      R             20REFFLD(PNAR)
     A            MIDK          15        51
     A*                                            /*Begin 094674*/
     A N55        RATE          11  7     75EDTWRD('   0.       -%')
     A*                                            /*End   094674*/
     A*
     A          R TOTMCR                    SPACEB(2)
     A                                    42'*** MANAGEMENT INFO'
     A                                    61'RMATION ONLY - ENTRIES'
     A                                    84'NOT GENERATED ***'
     A*
     A          R MADJCR                    SPACEB(1)
     A*
     A            PSTDA     R              1REFFLD(RUNA)
     A            VALDA     R              9REFFLD(RUNA)
     A            NARR      R             21REFFLD(PNAR)
     A  80        INTC0         15  0    106EDTCDE(C)
     A  81        INTC1         15  1    106EDTCDE(C)
     A  82        INTC2         15  2    106EDTCDE(C)
     A  83        INTC3         15  3    106EDTCDE(C)
     A*
     A          R ALTNDC                    SPACEB(1)
     A*
     A            NARR      R             20REFFLD(PNAR)
     A            MIDK          15        51
     A*                                            /*Begin 094674*/
     A            RATE          11  7     75EDTWRD('   0.       -%')
     A*                                            /*End   094674*/
