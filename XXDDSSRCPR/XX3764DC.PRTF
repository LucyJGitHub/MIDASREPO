      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE Int capitalisation report Dr and Cr')         *
/*ADD *  CHGPRTF FILE(XX3764DC) PAGRTT(0) SAVE(*YES) CPI(16.7)        *
/*ADD *  CHGPRTF FILE(XX3764DC) FONT(*CPI)                            *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                        *
     A*                                                               *
     A*  PRTF/XX3764DC - INTEREST CAPITALISATION REPORT DR & CR       *
     A*                PRODUCE AT CHANGE OF ACCOUNT OR CHANGE OF PAGE *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      *  Last Amend No. LUC114   *CREATE   Date 23Jan19               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. BUG24136           Date 03Jun09               *
      *                 CER048             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      *                 241340             Date 21Dec06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 30Nov05               *
      *                 CGL035             Date 01Mar05               *
      *                 CGL031             Date 05Jul04               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 182452             Date 20Jun01               *
      * Midas DBA 3.05 -----------------------------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 160301             Date 05Jul99               *
      *                 CFT004             Date 19Jul99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 130468             Date 27Apr99               *
      *                 125176             Date 10Feb98               *
     A*                 CRE001             Date 18Feb97               *
     A*                 CRE003             Date 26Apr96               *
     A*                 094657             Date 25Nov95               *
     A*                 S01408             DATE 01APR96               *
     A*                 S01413 *CREATE     DATE 13APR93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  LUC114 - Change Layout of Interest Capitalization Report     *
     A*           Layout of RE3764DC cloned as base to XX3764DC to    *
     A*           Change for LUC114 requirements.                     *
     A*           /Copy and comments removed to create XX3764DC       *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG24136 - Problem with data not showing on Retail Reports   *
      *  CER048 - German Features - Taxes                             *
      *  241340 - Changed the starting position of ACNO and BLK10     *
      *           from 72 to 73                                       *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL035 - EUSD Upgrade to MidasPlus                           *
      *  CGL031 - Taxation of Savings Income                          *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  182452 - Reformat the subtotal for interest types 03 and 04  *
      *  160301 - Spacing error when management capitalisation.       *
      *  CFT004 - Straight Through Processing Phase 2                 *
      *           International Bank Account Numbers (IBAN)           *
     A*  130468 - BRT not deducted from CR interest adjustments.      *
     A*  125176 - Amend as part of general improvements to RE3763     *
     A*  CRE001 - Overdraft Interest Calculations                     *
     A*         - Apply fix 115460 - Headings OverPrinting            *
     A*  CRE003 - Ledger Balance Accruals                             *
     A*  094657 - Suppress customer address details if specified.     *
     A*  S01408 - Addition of core hook RE3764DC01                    *
     A*           Addition of core hook RE3764DC02                    *
     A*  S01413 - Retail 3 Incorporation                              *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R HEADDC                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A*
     A                                      SPACEB(9)
     A                                    43'RETAIL INTEREST CAPITALIS+
     A                                      ATION REPORT'
     A                                      SPACEA(1)
     A                                    43'-------------------------+
     A                                      ------------'
     A                                      SPACEA(2)
     A  23        CNA1      R             15
     A  23N70                               SPACEA(1)
     A  70                                47'*** MANAGEMENT CAPITALISA+
     A                                      TION ***'
     A                                      SPACEA(1)
     A  23        CNA2      R             15
     A                                      SPACEA(1)
     A  23        CNA3      R             15
     A                                      SPACEA(1)
     A  23        CNA4      R             15
     A                                      SPACEA(1)
     A            CBRCA2    R             89REFFLD(BRCA)
     A                                    92'-'
     A            CCNUM          6A       93TEXT('Customer number')
     A                                    99'-'
     A            CCCY      R            100
     A                                   103'-'
     A            CACOD     R            104REFFLD(ACOD)
     A                                   114'-'
     A            CACSQ     R            115REFFLD(ACSQ)
     A  65        ACNO      R            120
     A N04 65                               SPACEA(4)
     A N65N04     BLK10         10        70SPACEA(4)
     A*
     A  04        RIBAN         47        89SPACEB(1)
     A                                      SPACEA(3)
     A*
     A N69                                10'VALUE'
     A  69                                10'EFFECT'
     A                                    17'NO.'
     A    N03                             62'POSTING'
     A     01                             98'INTEREST'
     A    N02                               SPACEA(1)
     A     02                            119'INTEREST'
     A                                      SPACEA(1)
     A*
     A                                    11'DATE'
     A                                    16'DAYS'
     A                                    23'NARRATIVE'
     A     03                             44'NUMBER'
     A     03                             56'RATE'
     A                                    63'AMOUNT'
     A                                    82'BALANCE'
     A     01                            101'DEBIT'
     A    N02                               SPACEA(2)
     A     02                            121'CREDIT'
     A                                      SPACEA(2)
     A*
     A          R POSTDC                    SPACEA(1)
     A
     A            PSTDA     R              1REFFLD(RUNA)
     A            VALDA     R              9REFFLD(RUNA)
     A            NARR      R             21REFFLD(PNAR)
     A  80        PSTD0         15  0     52EDTCDE(C)
     A  81        PSTD1         15  1     52EDTCDE(C)
     A  82        PSTD2         15  2     52EDTCDE(C)
     A  83        PSTD3         15  3     52EDTCDE(C)
     A*
     A          R RATEDC                    SPACEA(1)
     A            PSTDA     R              1REFFLD(RUNA)
     A            VALDA     R              9REFFLD(RUNA)
     A            RNARR     R             21REFFLD(PNAR)
     A            RATE          11  7     52EDTWRD('   0.       -%')
     A*
     A          R ODLNDC                    SPACEA(1)
     A            ONARR         30        21
     A            OODRL         11  0     51EDTCDE(C)
      *
     A          R AODRDC                    SPACEA(1)
     A            ONARR         30        21
     A            OAODR         11  7     51EDTWRD('   0.       -%')
     A          R STOTDC                    SPACEA(1)
     A     25
     AO    03     VALDA     R              9REFFLD(RUNA)
     A    N25     DAYS           4  0     16EDTCDE(C)
     A  80        NUMB0         15  0     33EDTCDE(C)
     A  81        NUMB1         15  1     33EDTCDE(C)
     A  82        NUMB2         15  2     33EDTCDE(C)
     A  83        NUMB3         15  3     33EDTCDE(C)
     A  25                                54'/Min. Balance'
     A*
     A  80        HISB0         15  0     71EDTCDE(C)
     A  81        HISB1         15  1     71EDTCDE(C)
     A  82        HISB2         15  2     71EDTCDE(C)
     A  83        HISB3         15  3     71EDTCDE(C)
     A*
     A  80 01     INTD0         15  0     92EDTCDE(C)
     A  81 01     INTD1         15  1     92EDTCDE(C)
     A  82 01     INTD2         15  2     92EDTCDE(C)
     A  83 01     INTD3         15  3     92EDTCDE(C)
     A*
     A  80 02     INTC0         15  0    112EDTCDE(C)
     A  81 02     INTC1         15  1    112EDTCDE(C)
     A  82 02     INTC2         15  2    112EDTCDE(C)
     A  83 02     INTC3         15  3    112EDTCDE(C)
     A*
     A          R TITLEDC                   SPACEA(1)
     A            TNARR     R             21REFFLD(PNAR)
     A            VALDA     R              9REFFLD(RUNA)
     A  80        HISB0         15  0     71EDTCDE(C)
     A  81        HISB1         15  1     71EDTCDE(C)
     A  82        HISB2         15  2     71EDTCDE(C)
     A  83        HISB3         15  3     71EDTCDE(C)
     A*
     A          R TOTALDC                   SPACEA(1)
     A            DAYS           4  0     16EDTCDE(C)
     A N22        XNARR     R             21REFFLD(PNAR)
     A     22                             21'TOTAL GROSS INTEREST'
     A*
     A  80 01     TINTD0        15  0     92EDTCDE(C)
     A  81 01     TINTD1        15  1     92EDTCDE(C)
     A  82 01     TINTD2        15  2     92EDTCDE(C)
     A  83 01     TINTD3        15  3     92EDTCDE(C)
      *                                            /*End   130468*/
     A*
     A  80 02N22  TINTC0        15  0    112EDTCDE(C)
     A  81 02N22  TINTC1        15  1    112EDTCDE(C)
     A  82 02N22  TINTC2        15  2    112EDTCDE(C)
     A  83 02N22  TINTC3        15  3    112EDTCDE(C)
     A*
     A  80 02 22  TGINT0        15  0    112EDTCDE(C)
     A  81 02 22  TGINT1        15  1    112EDTCDE(C)
     A  82 02 22  TGINT2        15  2    112EDTCDE(C)
     A  83 02 22  TGINT3        15  3    112EDTCDE(C)
      *
     A          R TOTBRT                    SPACEA(1)
     A     22                             21'LESS BRT AT'
     A     22     WKBRT          7  5     33EDTWRD(' 0.     %')
     A  80 22     BRTOT0        15  0    112EDTCDE(C)
     A  81 22     BRTOT1        15  1    112EDTCDE(C)
     A  82 22     BRTOT2        15  2    112EDTCDE(C)
     A  83 22     BRTOT3        15  3    112EDTCDE(C)
      ** Start of Change CGL031
     A          R EUWTAX                    SPACEA(1)
     A  83 21     EUTAX3        15S 3O   112
     A                                      EDTCDE(C)
     A  82 21     EUTAX2        15S 2O   112
     A                                      EDTCDE(C)
     A  81 21     EUTAX1        15S 1O   112
     A                                      EDTCDE(C)
     A  21                                21
     A                                      'LESS MAIN TAX'
     A  80 21     EUTAX0        15S 0O   112
     A                                      EDTCDE(C)
      ** End of Change CGL031
     A*
      **Start of change CER048
     A          R SECTAX                    SPACEA(1)
     A                                    21'LESS SECONDARY TAX'
     A  80        STAX0         15  0    112EDTCDE(C)
     A  81        STAX1         15  1    112EDTCDE(C)
     A  82        STAX2         15  2    112EDTCDE(C)
     A  83        STAX3         15  3    112EDTCDE(C)
      **End of change CER048
     A          R TOTDDC                    SPACEA(1)
     A            DNARR     R             21REFFLD(PNAR)
      *                                            /*Begin 125176*/
     A  80 01     TINTD0        15  0     92EDTCDE(D)
     A  81 01     TINTD1        15  1     92EDTCDE(D)
     A  82 01     TINTD2        15  2     92EDTCDE(D)
     A  83 01     TINTD3        15  3     92EDTCDE(D)
     A  80 02     TINTC0        15  0    112EDTCDE(D)
     A  81 02     TINTC1        15  1    112EDTCDE(D)
     A  82 02     TINTC2        15  2    112EDTCDE(D)
     A  83 02     TINTC3        15  3    112EDTCDE(D)
     A  41                               129'CR'
     A**
     A          R RAT1DC                    SPACEA(1)
     A*
     A            PSTDA     R              1REFFLD(RUNA)
     A            VALDA     R              9REFFLD(RUNA)
     A            RNARR     R             21REFFLD(PNAR)
     A  84                                77'   Tiered'
     A  85                                77'Threshold'
     A*
     A          R RAT2DC                    SPACEA(1)
     A*
     A            BNARR     R             21REFFLD(PNAR)
     A  80        HISB0         15  0     71EDTCDE(C)
     A  81        HISB1         15  1     71EDTCDE(C)
     A  82        HISB2         15  2     71EDTCDE(C)
     A  83        HISB3         15  3     71EDTCDE(C)
     A*
     A          R RAT3DC                    SPACEA(1)
     A*
     A                                    27'****'
     A  80        OBAL01        15  0     32EDTCDE(C)
     A  81        OBAL11        15  1     32EDTCDE(C)
     A  82        OBAL21        15  2     32EDTCDE(C)
     A  83        OBAL31        15  3     32EDTCDE(C)
     A                                    51'****'
     A            ORAT1         11  7     56EDTWRD('   0.       -%')
     A                                    70'****'
     A*
     A*
     A*
     A          R ODAYDC                    SPACEA(1)
     A  03        VALDA     R              9REFFLD(RUNA)
     A  80        HISB0         15  0     71EDTCDE(C)
     A  81        HISB1         15  1     71EDTCDE(C)
     A  82        HISB2         15  2     71EDTCDE(C)
     A  83        HISB3         15  3     71EDTCDE(C)
     A*
     A          R STODDC                    SPACEA(1)
     A            VALDA          7         9
     A N25        DAYS           4  0     17EDTCDE(C)
     A*********** NUMB0         15  0     33EDTCDE(C)
     A  80        NUMB0         15  0     33EDTCDE(C)
     A  81        NUMB1         15  1     33EDTCDE(C)
     A  82        NUMB2         15  2     33EDTCDE(C)
     A  83        NUMB3         15  3     33EDTCDE(C)
     A  25                                54'/Min. Balance'
     A  80 25     HISB0         15  0     71EDTCDE(C)
     A  81 25     HISB1         15  1     71EDTCDE(C)
     A  82 25     HISB2         15  2     71EDTCDE(C)
     A  83 25     HISB3         15  3     71EDTCDE(C)
     A*
     A  80 01     INTD0         15  0     92EDTCDE(C)
     A  81 01     INTD1         15  1     92EDTCDE(C)
     A  82 01     INTD2         15  2     92EDTCDE(C)
     A  83 01     INTD3         15  3     92EDTCDE(C)
     A*
     A          R STOCDC                    SPACEA(1)
     A            VALDA     R              9REFFLD(RUNA)
     A N25        DAYS           4  0     17EDTCDE(C)
     A  80        NUMB0         15  0     33EDTCDE(C)
     A  81        NUMB1         15  1     33EDTCDE(C)
     A  82        NUMB2         15  2     33EDTCDE(C)
     A  83        NUMB3         15  3     33EDTCDE(C)
     A  25                                54'/Min. Balance'
     A  80 25     HISB0         15  0     71EDTCDE(C)
     A  81 25     HISB1         15  1     71EDTCDE(C)
     A  82 25     HISB2         15  2     71EDTCDE(C)
     A  83 25     HISB3         15  3     71EDTCDE(C)
     A*
     A  80 02     INTC0         15  0    112EDTCDE(C)
     A  81 02     INTC1         15  1    112EDTCDE(C)
     A  82 02     INTC2         15  2    112EDTCDE(C)
     A  83 02     INTC3         15  3    112EDTCDE(C)
     A*
     A          R TOTMAN                    SPACEB(2)
     A                                    42'*** MANAGEMENT INFO'
     A                                    61'RMATION ONLY - ENTRIES'
     A                                    84'NOT GENERATED ***'
     A*
     A          R MADJDC                    SPACEA(1)
     A            PSTDA     R              1REFFLD(RUNA)
     A            VALDA     R              9REFFLD(RUNA)
     A            NARR      R             21REFFLD(PNAR)
     A*
     A  80 01     INTD0         15  0     92EDTCDE(D)
     A  81 01     INTD1         15  1     92EDTCDE(D)
     A  82 01     INTD2         15  2     92EDTCDE(D)
     A  83 01     INTD3         15  3     92EDTCDE(D)
     A  80 02     INTC0         15  0    112EDTCDE(D)
     A  81 02     INTC1         15  1    112EDTCDE(D)
     A  82 02     INTC2         15  2    112EDTCDE(D)
     A  83 02     INTC3         15  3    112EDTCDE(D)
     A*
     A          R ALTNDC                    SPACEA(1)
     A            NARR      R             21REFFLD(PNAR)
     A            MIDK          15        51
     A N55        RATE          11  7     75EDTWRD('   0.       -%')
     A*
     A          R TOTND                     SPACEA(1)
     A            NARRD         21        11
     A            TNUMD         15  0     32EDTCDE(L)
     A*
     A          R TOTNC                     SPACEA(1)
     A            NARRC         21        11
     A            TNUMC         15  0     32EDTCDE(L)
     A*
     A          R TOTNB                     SPACEA(1)
     A            BLAN           1        10
     A*
