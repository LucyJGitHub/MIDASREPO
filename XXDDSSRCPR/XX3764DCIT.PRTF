      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE Int capitalisation report Dr and Cr')         *
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/XX3764DCIT - RE Interest Capitalisation Rpt DR & CR     *
      *                PRODUCE AT CHANGE OF ACCOUNT OR CHANGE OF PAGE *
      *                                                               *
      *  NOTE: This PRTF is based on PRTF/RE3764DC                    *
      *        Changes done to RE3764DC need to be reviewed for       *
      *        inclusion in this PRTF.                                *
      *                                                               *
      *  (c) Finastra International Systems Ltd. 2021                 *
      *                                                               *
      *  Last Amend No. LUC141  *Create    Date 23Feb21               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  LUC141 - Midas Italy - Scalare                               *
      *           Used hook RE3764DC03                                *
      *           RE3764DCIT is a copy of RE3764DC, with text transla-*
      *           ted into Italian.                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R HEADDC                    TEXT('(C) Finastra +
     A                                      International Systems +
     A                                      Ltd 2021')
     A                                      SKIPB(1)
     A                                     1'Conto Scalare al:'
     A                                      SPACEB(2)
     A            RUNA      R             30
     A  65                                 1'Conto No.:'
     A  65                                  SPACEB(1)
     A  65        ACNO      R             30
      *
     A                                     1'Divisa:'
     A                                      SPACEB(1)
     A            CCY       R             30
      *
     A                                     1'Pagina:'
     A                                      SPACEB(1)
     A            PAGA           4S 0     30EDTCDE(3)
      *
     A  23N70                               SPACEA(1)
     A  70                                47'*** MANAGEMENT CAPITALISA+
     A                                      TION ***'
     A                                      SPACEA(1)
     A                                     1'Ns. Rif.:'
     A                                      SPACEB(1)
     A            CBRCA2    R             30REFFLD(BRCA)
     A                                    +1'-'
     A            CCNUM     R             +1REFFLD(CNUM)
     A                                    +1'-'
     A            CCCY      R             +1
     A                                    +1'-'
     A            CACOD     R             +1REFFLD(ACOD)
     A                                    +1'-'
     A            CACSQ     R             +1REFFLD(ACSQ)
     A*                                                                   S01408
     A*/COPY WNCPYSRC,RE3764DC01                                          S01408
     A*                                                                   S01408
     A*                                                                   S01408
     A*/COPY WNCPYSRC,RE3764DC02                                          S01408
     A*                                                                   S01408
     A*
     A                                     1' DATA  '
     A                                      SPACEB(3)
     A N69                                10'DATA '
     A  69                                10'EFFECT'
     A                                    17'NUM'
     A    N03                             62'POSTING'
     A     01                             98'INTERESSI'
     A    N02                               SPACEA(1)
     A     02                            119'INTERESSI'
     A                                      SPACEA(1)
     A*
     A                                     1' CONT. '
     A                                     9'VALUTA '
     A                                    16'GIORNI'
     A                                    23'DESCRIZIONE'
     A     03                             44'NUMERI'
     A     03                             56'TASSO'
     A                                    63'IMPORTO'
     A                                    82'SALDO'
     A     01                             98'DEBITORI'
     A    N02                               SPACEA(2)
     A     02                            119'CREDITORI'
     A                                      SPACEA(2)
     A*
     A          R POSTDC                    SPACEA(1)
     A
     A            PSTDA     R              1REFFLD(RUNA)
     A            VALDA     R              9REFFLD(RUNA)
     A            NARR      R             21REFFLD(PNAR)
     A  80        PSTD0         15  0     52EDTCDE(C)
     A  81        PSTD1         15  1     52EDTCDE(C)
     A  82        PSTD2         15  2     52EDTCDE(C)
     A  83        PSTD3         15  3     52EDTCDE(C)
     A*
     A          R RATEDC                    SPACEA(1)
     A            PSTDA     R              1REFFLD(RUNA)
     A            VALDA     R              9REFFLD(RUNA)
     A            RNARR     R             21REFFLD(PNAR)
     A            RATE          11  7     52EDTWRD('   0.       -%')
     A*/COPY WNCPYSRC,RE3764DC21
     A*
     A          R ODLNDC                    SPACEA(1)
     A            ONARR         30        21
     A            OODRL         11  0     51EDTCDE(C)
      *
     A          R AODRDC                    SPACEA(1)
     A            ONARR         30        21
     A            OAODR         11  7     51EDTWRD('   0.       -%')
     A          R STOTDC                    SPACEA(1)
     A     03     VALDA     R              9REFFLD(RUNA)
     A            DAYS           4  0     16EDTCDE(C)
     A  80        NUMB0         15  0     33EDTCDE(C)
     A  81        NUMB1         15  1     33EDTCDE(C)
     A  82        NUMB2         15  2     33EDTCDE(C)
     A  83        NUMB3         15  3     33EDTCDE(C)
     A*
     A  80        HISB0         15  0     71EDTCDE(C)
     A  81        HISB1         15  1     71EDTCDE(C)
     A  82        HISB2         15  2     71EDTCDE(C)
     A  83        HISB3         15  3     71EDTCDE(C)
     A*
     A*/COPY WNCPYSRC,RE3764DC05
     A  80 01     INTD0         15  0     92EDTCDE(C)
     A  81 01     INTD1         15  1     92EDTCDE(C)
     A  82 01     INTD2         15  2     92EDTCDE(C)
     A  83 01     INTD3         15  3     92EDTCDE(C)
     A*
     A  80 02     INTC0         15  0    112EDTCDE(C)
     A  81 02     INTC1         15  1    112EDTCDE(C)
     A  82 02     INTC2         15  2    112EDTCDE(C)
     A  83 02     INTC3         15  3    112EDTCDE(C)
     A*/COPY WNCPYSRC,RE3764DC06
     A*
     A          R TITLEDC                   SPACEA(1)
     A            TNARR     R             21REFFLD(PNAR)
     A            VALDA     R              9REFFLD(RUNA)
     A  80        HISB0         15  0     71EDTCDE(C)
     A  81        HISB1         15  1     71EDTCDE(C)
     A  82        HISB2         15  2     71EDTCDE(C)
     A  83        HISB3         15  3     71EDTCDE(C)
     A*
     A          R TOTALDC                   SPACEA(1)
     A            DAYS           4  0     16EDTCDE(C)
     A N22        XNARR     R             21REFFLD(PNAR)
     A*/COPY WNCPYSRC,RE3764DC07
     A     22                             21'TOTAL GROSS INTEREST'
     A*
     A  80 01     TINTD0        15  0     92EDTCDE(C)
     A  81 01     TINTD1        15  1     92EDTCDE(C)
     A  82 01     TINTD2        15  2     92EDTCDE(C)
     A  83 01     TINTD3        15  3     92EDTCDE(C)
     A*
     A  80 02N22  TINTC0        15  0    112EDTCDE(C)
     A  81 02N22  TINTC1        15  1    112EDTCDE(C)
     A  82 02N22  TINTC2        15  2    112EDTCDE(C)
     A  83 02N22  TINTC3        15  3    112EDTCDE(C)
     A*/COPY WNCPYSRC,RE3764DC08
     A*
     A  80 02 22  TGINT0        15  0    112EDTCDE(C)
     A  81 02 22  TGINT1        15  1    112EDTCDE(C)
     A  82 02 22  TGINT2        15  2    112EDTCDE(C)
     A  83 02 22  TGINT3        15  3    112EDTCDE(C)
      *
     A          R TOTBRT                    SPACEA(1)
     A     22                             21'LESS BRT AT'
     A     22     WKBRT          7  5     33EDTWRD(' 0.     %')
     A  80 22     BRTOT0        15  0    112EDTCDE(C)
     A  81 22     BRTOT1        15  1    112EDTCDE(C)
     A  82 22     BRTOT2        15  2    112EDTCDE(C)
     A  83 22     BRTOT3        15  3    112EDTCDE(C)
     A*/COPY WNCPYSRC,RE3764DC23
     A*
     A          R EUWTAX                    SPACEA(1)
     A  83 21     EUTAX3        15S 3O   112
     A                                      EDTCDE(C)
     A  82 21     EUTAX2        15S 2O   112
     A                                      EDTCDE(C)
     A  81 21     EUTAX1        15S 1O   112
     A                                      EDTCDE(C)
     A  21                                21
     A                                      'LESS MAIN TAX'
     A  80 21     EUTAX0        15S 0O   112
     A                                      EDTCDE(C)
     A          R SECTAX                    SPACEA(1)
     A                                    21'LESS SECONDARY TAX'
     A  80        STAX0         15  0    112EDTCDE(C)
     A  81        STAX1         15  1    112EDTCDE(C)
     A  82        STAX2         15  2    112EDTCDE(C)
     A  83        STAX3         15  3    112EDTCDE(C)
      *
     A          R TOTDDC                    SPACEA(1)
     A            DNARR     R             21REFFLD(PNAR)
     A*/COPY WNCPYSRC,RE3764DC09
     A  80 01     TINTD0        15  0     92EDTCDE(D)
     A  81 01     TINTD1        15  1     92EDTCDE(D)
     A  82 01     TINTD2        15  2     92EDTCDE(D)
     A  83 01     TINTD3        15  3     92EDTCDE(D)
     A*/COPY WNCPYSRC,RE3764DC10
     A*
     A*/COPY WNCPYSRC,RE3764DC11
     A  80 02     TINTC0        15  0    112EDTCDE(D)
     A  81 02     TINTC1        15  1    112EDTCDE(D)
     A  82 02     TINTC2        15  2    112EDTCDE(D)
     A  83 02     TINTC3        15  3    112EDTCDE(D)
     A*/COPY WNCPYSRC,RE3764DC12
     A  41                               129'CR'
     A**
     A          R RAT1DC                    SPACEA(1)
     A*
     A            PSTDA     R              1REFFLD(RUNA)
     A            VALDA     R              9REFFLD(RUNA)
     A            RNARR     R             21REFFLD(PNAR)
     A  84                                77'   Tiered'
     A  85                                77'Threshold'
     A*
     A          R RAT2DC                    SPACEA(1)
     A*
     A            BNARR     R             21REFFLD(PNAR)
     A  80        HISB0         15  0     71EDTCDE(C)
     A  81        HISB1         15  1     71EDTCDE(C)
     A  82        HISB2         15  2     71EDTCDE(C)
     A  83        HISB3         15  3     71EDTCDE(C)
     A*
     A          R RAT3DC                    SPACEA(1)
     A*
     A                                    27'****'
     A  80        OBAL01        15  0     32EDTCDE(C)
     A  81        OBAL11        15  1     32EDTCDE(C)
     A  82        OBAL21        15  2     32EDTCDE(C)
     A  83        OBAL31        15  3     32EDTCDE(C)
     A                                    51'****'
     A            ORAT1         11  7     56EDTWRD('   0.       -%')
     A                                    70'****'
     A*/COPY WNCPYSRC,RE3764DC22
     A*
     A*
     A*
     A          R ODAYDC                    SPACEA(1)
     A  03        VALDA     R              9REFFLD(RUNA)
     A  80        HISB0         15  0     71EDTCDE(C)
     A  81        HISB1         15  1     71EDTCDE(C)
     A  82        HISB2         15  2     71EDTCDE(C)
     A  83        HISB3         15  3     71EDTCDE(C)
     A*
     A          R STODDC                    SPACEA(1)
     A            VALDA          7         9
     A            DAYS           4  0     17EDTCDE(C)
     A  80        NUMB0         15  0     33EDTCDE(C)
     A  81        NUMB1         15  1     33EDTCDE(C)
     A  82        NUMB2         15  2     33EDTCDE(C)
     A  83        NUMB3         15  3     33EDTCDE(C)
     A*
     A*/COPY WNCPYSRC,RE3764DC13
     A  80 01     INTD0         15  0     92EDTCDE(C)
     A  81 01     INTD1         15  1     92EDTCDE(C)
     A  82 01     INTD2         15  2     92EDTCDE(C)
     A  83 01     INTD3         15  3     92EDTCDE(C)
     A*/COPY WNCPYSRC,RE3764DC14
     A*
     A          R STOCDC                    SPACEA(1)
     A            VALDA     R              9REFFLD(RUNA)
     A            DAYS           4  0     17EDTCDE(C)
     A  80        NUMB0         15  0     33EDTCDE(C)
     A  81        NUMB1         15  1     33EDTCDE(C)
     A  82        NUMB2         15  2     33EDTCDE(C)
     A  83        NUMB3         15  3     33EDTCDE(C)
     A*
     A*/COPY WNCPYSRC,RE3764DC15
     A  80 02     INTC0         15  0    112EDTCDE(C)
     A  81 02     INTC1         15  1    112EDTCDE(C)
     A  82 02     INTC2         15  2    112EDTCDE(C)
     A  83 02     INTC3         15  3    112EDTCDE(C)
     A*/COPY WNCPYSRC,RE3764DC16
     A*
     A          R TOTMAN                    SPACEB(2)
     A                                    42'*** MANAGEMENT INFO'
     A                                    61'RMATION ONLY - ENTRIES'
     A                                    84'NOT GENERATED ***'
     A*
     A          R MADJDC                    SPACEA(1)
     A            PSTDA     R              1REFFLD(RUNA)
     A            VALDA     R              9REFFLD(RUNA)
     A            NARR      R             21REFFLD(PNAR)
     A*
     A*
     A*/COPY WNCPYSRC,RE3764DC17
     A  80 01     INTD0         15  0     92EDTCDE(D)
     A  81 01     INTD1         15  1     92EDTCDE(D)
     A  82 01     INTD2         15  2     92EDTCDE(D)
     A  83 01     INTD3         15  3     92EDTCDE(D)
     A*/COPY WNCPYSRC,RE3764DC18
     A*
     A*/COPY WNCPYSRC,RE3764DC19
     A  80 02     INTC0         15  0    112EDTCDE(D)
     A  81 02     INTC1         15  1    112EDTCDE(D)
     A  82 02     INTC2         15  2    112EDTCDE(D)
     A  83 02     INTC3         15  3    112EDTCDE(D)
     A*/COPY WNCPYSRC,RE3764DC20
     A*
     A          R ALTNDC                    SPACEA(1)
     A            NARR      R             21REFFLD(PNAR)
     A            MIDK          15        51
     A N55        RATE          11  7     75EDTWRD('   0.       -%')
     A*
     A          R TOTND                     SPACEA(1)
     A            NARRD         21        11
     A            TNUMD         15  0     32EDTCDE(L)
     A*
     A          R TOTNC                     SPACEA(1)
     A            NARRC         21        11
     A            TNUMC         15  0     32EDTCDE(L)
     A*
     A          R TOTNB                     SPACEA(1)
     A            BLAN           1        10
     A*
     A          R ADDRDC                    SKIPB(53)
     A            ADDL1         35        79
     A            ADDL2         35        79SPACEB(1)
     A            ADDL3         35        79SPACEB(1)
     A            ADDL4         35        79SPACEB(1)
     A            ADDL5         35        79SPACEB(1)
     A            ADDL6         35        79SPACEB(1)
     A*
