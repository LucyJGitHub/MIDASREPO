      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE Int capitalisation report dr')                *
/*ADD *  CHGPRTF FILE(XX3764DR) PAGRTT(0) SAVE(*YES) CPI(16.7)        *
/*ADD *  CHGPRTF FILE(XX3764DR) FONT(*CPI)                            *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module
     A*                                                               *
     A*  PRTF/RE3764DR - INTEREST CAPITALISATION REPORT               *
     A*                  DEBIT                                        *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      *  Last Amend No. LUC114   *CREATE   Date 23Jan19               *
      *  Last Amend No. MD024700           Date 03Apr14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 30Nov05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 182452             Date 20Jun01               *
      * Midas DBA 3.05 -----------------------------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CFT004             Date 19Jul99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CRE001             Date 18Feb97               *
      *                 CRE003             Date 26Apr96               *
     A*                 094657             Date 25Nov95               *
     A*                 094674             DATE 22NOV95               *
     A*                 086087             DATE 22AUG95               *
     A*                 040447             DATE 28SEP93               *
     A*                 S01413 *CREATE     DATE 13APR93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  LUC114 - Change Layout of Interest Capitalization Report     *
     A*           Layout of RE3764DR cloned as base to XX3764DR to    *
     A*           Change for LUC114 requirements.                     *
     A*           /Copy and comments removed to create XX3764DR       *
      *  MD024700 - Move IBAN number to start 3 positions to the right*
      *             of retail account number.                         *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  182452 - Reformat the subtotal for interest types 03 and 04  *
      *  CFT004 - Straight Through Processing Phase 2                 *
      *           International Bank Account Numbers (IBAN)           *
     A*  CRE001 - Overdraft Interest Calculations                     *
     A*  CRE003 - Ledger Balance Accruals                             *
     A*  094657 - Suppress customer address details if specified.     *
     A*  094674 - Amend to display negative interest rates.           *
     A*          - Alternate account. If not multi ccy, don't ouput   *
     A*            rate.                                              *
     A*  086087 - Change format of tiered/threshold interest to same  *
     A*           as in RE3364E and display 'tiered'/'threshold'.     *
     A*           Change position of no. of days.                     *
     A*  040447 - Print the correct opening rate                      *
     A*  S01413 - Retail 3 Incorporation                              *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A**
     A**   *************
     A**   REPORT HEADER
     A**   *************
     A**
     A          R HEADDR                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                      SPACEB(9)
     A*
     A                                    43'RETAIL INTEREST '
     A                                    59'CAPITALISATION REPORT'
     A                                   101'DATE'
     A            RUNA      R            106
     A                                      SPACEA(1)
     A*
     A                                    43'----------------'
     A                                    59'---------------------'
     A                                      SPACEA(2)
     A  23        CNA1      R             15
     A  70                                46'*** MANAGEMENT '
     A  70                                61'CAPITALISATION ***'
     A                                   101'DEBIT INTEREST'
     A                                      SPACEA(1)
     A  23        CNA2      R             15
     A                                      SPACEA(1)
     A  23        CNA3      R             15
     A                                      SPACEA(1)
     A  23        CNA4      R             15
     A                                      SPACEA(1)
     A*
     A            CBRCA2    R             89REFFLD(BRCA)
     A                                    92'-'
     A            CCNUM          6A       93TEXT('Customer number')
     A                                    99'-'
     A            CCCY      R            100
     A                                   103'-'
     A            CACOD     R            104REFFLD(ACOD)
     A                                   114'-'
     A            CACSQ     R            115REFFLD(ACSQ)
     A     65     ACNO      R            120
     A N04 65                               SPACEA(3)
     A N65N04     BLK10         10        70
     A N65N04                               SPACEA(4)
     A  04        RIBAN         47        89SPACEB(1)
     A                                      SPACEA(3)
     A N69                                10'VALUE'
     A  69                                10'EFFECT'
     A                                    17'NO.'
     A                                    62'POSTING'
     A                                      SPACEA(1)
     A*
     A                                    11'DATE'
     A                                    17'DAYS'
     A                                    23'NARRATIVE'
     A                                    63'AMOUNT'
     A                                    82'BALANCE'
     A                                   113'INTEREST'
     A                                      SPACEA(1)
     A*
     A**
     A**   *************
     A**   REPORT DETAIL
     A**   *************
     A**
     A          R POSTDR                    SPACEB(1)
     A*
     A            PSTDA     R              1REFFLD(RUNA)
     A            VALDA     R              9REFFLD(RUNA)
     A            NARR      R             21REFFLD(PNAR)
     A  80        PSTD0         15  0     52EDTCDE(C)
     A  81        PSTD1         15  1     52EDTCDE(C)
     A  82        PSTD2         15  2     52EDTCDE(C)
     A  83        PSTD3         15  3     52EDTCDE(C)
     A*
     A          R RATEDR                    SPACEB(1)
     A*
     A            PSTDA     R              1REFFLD(RUNA)
     A            VALDA     R              9REFFLD(RUNA)
     A            RNARR     R             21REFFLD(PNAR)
     A            RATE          11  7     52EDTWRD('   0.       -%')
     A          R ODLNDC                    SPACEB(1)
     A            ONARR         30        21
     A            OODRL         11  0     51EDTCDE(C)
      *
     A          R AODRDC                    SPACEB(1)
     A            ONARR         30        21
     A            OAODR         11  7     51EDTWRD('   0.       -%')
     A*
     A          R TITLEDR                   SPACEB(1)
     A*
     A            VALDA     R              9REFFLD(RUNA)
     A            TNARR     R             21REFFLD(PNAR)
     A  80        HISB0         15  0     71EDTCDE(C)
     A  81        HISB1         15  1     71EDTCDE(C)
     A  82        HISB2         15  2     71EDTCDE(C)
     A  83        HISB3         15  3     71EDTCDE(C)
     A*
     A          R STOTDR                    SPACEB(1)
     A*
     A  26        WMNTA     R              9REFFLD(RUNA)
     A N26        DAYS           4  0     16EDTCDE(C)
     A  80N25     NUMB0         15  0     33EDTCDE(C)
     A  81N25     NUMB1         15  1     33EDTCDE(C)
     A  82N25     NUMB2         15  2     33EDTCDE(C)
     A  83N25     NUMB3         15  3     33EDTCDE(C)
     A  26                                54'/Min. Balance'
     A  80        HISB0         15  0     71EDTCDE(C)
     A  81        HISB1         15  1     71EDTCDE(C)
     A  82        HISB2         15  2     71EDTCDE(C)
     A  83        HISB3         15  3     71EDTCDE(C)
     A  80N25     INTD0         15  0    106EDTCDE(C)
     A  81N25     INTD1         15  1    106EDTCDE(C)
     A  82N25     INTD2         15  2    106EDTCDE(C)
     A  83N25     INTD3         15  3    106EDTCDE(C)
     A*
     A          R TOTALDR                   SPACEB(1)
     A*
     A            DAYS           4  0     16EDTCDE(C)
     A*                                            /*End   086087*/
     A            XNARR     R             21REFFLD(PNAR)
     A  80        TINTD0        15  0    106EDTCDE(C)
     A  81        TINTD1        15  1    106EDTCDE(C)
     A  82        TINTD2        15  2    106EDTCDE(C)
     A  83        TINTD3        15  3    106EDTCDE(C)
     A*
     A          R TOTDDR                    SPACEB(1)
     A*
     A            DNARR     R             21REFFLD(PNAR)
     A  80        TINTD0        15  0    106EDTCDE(C)
     A  81        TINTD1        15  1    106EDTCDE(C)
     A  82        TINTD2        15  2    106EDTCDE(C)
     A  83        TINTD3        15  3    106EDTCDE(C)
     A*
     A          R RAT1DR                    SPACEB(1)
     A*
     A            PSTDA     R              1REFFLD(RUNA)
     A            VALDA     R              9REFFLD(RUNA)
     A            RNARR     R             21REFFLD(PNAR)
     A  84                                77'   Tiered'
     A  85                                77'Threshold'
     A*
     A          R RAT2DR                    SPACEB(1)
     A*
     A            BNARR     R             21REFFLD(PNAR)
     A  80        HISB0         15  0     71EDTCDE(C)
     A  81        HISB1         15  1     71EDTCDE(C)
     A  82        HISB2         15  2     71EDTCDE(C)
     A  83        HISB3         15  3     71EDTCDE(C)
     A*
     A          R RAT3DR                    SPACEB(1)
     A                                    27'****'
     A  80        OBAL01        15  0     32EDTCDE(C)
     A  81        OBAL11        15  1     32EDTCDE(C)
     A  82        OBAL21        15  2     32EDTCDE(C)
     A  83        OBAL31        15  3     32EDTCDE(C)
     A                                    51'****'
     A            ORAT1         11  7     56EDTWRD('   0.       -%')
     A                                    70'****'
     A*
     A          R ALTNDR                    SPACEB(1)
     A            NARR      R             20REFFLD(PNAR)
     A            MIDK          15        51
     A N55        RATE          11  7     75EDTWRD('   0.       -%')
     A*
     A          R TOTMDR                    SPACEB(2)
     A                                    42'*** MANAGEMENT INFO'
     A                                    61'RMATION ONLY - ENTRIES'
     A                                    84'NOT GENERATED ***'
     A*
     A          R MADJDR                    SPACEB(1)
     A            PSTDA     R              1REFFLD(RUNA)
     A            VALDA     R              9REFFLD(RUNA)
     A            NARR      R             21REFFLD(PNAR)
     A*
     A  80        INTD0         15  0    106EDTCDE(C)
     A  81        INTD1         15  1    106EDTCDE(C)
     A  82        INTD2         15  2    106EDTCDE(C)
     A  83        INTD3         15  3    106EDTCDE(C)
