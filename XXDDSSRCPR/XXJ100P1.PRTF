      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE Next Capitalisation Date Update Audit')       *
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      ***PRTF/REJ100P1*-*Midas*RE*Next Capitalisation Date Update******         EML110
      *  PRTF/XXJ100P1 - Midas RE Next Capitalisation Date Update     *         EML110
      *                  Audit Report                                 *
      *                                                               *
      ***(c)*Misys*International*Banking*Systems*Ltd.*2005*************         EML110
      *  (c) Finastra International Limited 2018                      *         EML110
      *                                                               *
      *  Last Amend No. EML110             Date 20Jan20               *
      *  Prev Amend No. TFJ11B  *CREATE    Date 17Jan07               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  EML110 - Account Balance Pooling. Upgrade to FBM 2.1         *
      *  TFJ11B - Retail interest Capitalisation Date Change Utility  *
      *                                                               *
      *****************************************************************
      ** Start of EML110
     A*********                             REF(MFRFL)
      ** End of EML110
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
      ** Start of EML110
     A********* R HEADPY                    TEXT('(c) Misys International +
     A*********                             Banking Systems Ltd. +
     A*********                             2005')
     A          R HEADPY                    TEXT('(c) Finastra Internation+
     A                                      al Limited 2018')
      ** End of EML110
      *
     A                                      SKIPB(3)
      ** Start of EML110
     A*********                            6'REFERENCE REJ100P1'
     A                                     6'REFERENCE XXJ100P1'
      ** End of EML110
      ** Start of EML110
     A*********** BJURPT    R             37REFFLD(URPT)
     A            BJURPT        53A       37
      ** End of EML110
     A                                   101'DATE'
      ** Start of EML110
     A*********** BJMRDT    R            106REFFLD(MRDT)
     A            BJMRDT         7A      106
      ** End of EML110
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'NEXT CAPITALISATION DATE +
     A                                      UPDATE AUDIT REPORT'
      *
     A  37                                89'BRANCH -'
      ** Start of EML110
     A**37********A8BRCD    R             98REFFLD(BRCA)
     A**37********A8BRNM    R            102REFFLD(BRNM)
     A  37        A8BRCD         3A       98
     A  37        A8BRNM        30A      102
      ** End of EML110
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    46'-------------------------+
     A                                      -------------------'
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     6'ACCOUNT NUMBER'
     A                                    31'RETAIL'
     A                                    44'ACCOUNT NAME'
     A                                    70'NEXT DEBIT'
     A                                    92'NEXT CREDIT'
     A                                      SPACEA(1)
     A                                    31'A/C NUMBER'
     A                                    66'CAPITALISATION DATE'
     A                                    88'CAPITALISATION DATE'
     A                                      SPACEA(1)
     A                                    66'-------------------'
     A                                    88'-------------------'
     A                                      SPACEA(1)
     A                                    68'BEFORE'
     A                                    78'AFTER'
     A                                    91'BEFORE'
     A                                   101'AFTER'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RJBRCA         3         3
     A                                     6'-'
     A            RJCNUM         6         7
     A                                    13'-'
     A            RJCCY          3        14
     A                                    17'-'
     A            RJACOD        10        18
     A                                    28'-'
     A            RJACSQ         2        29
     A            RJACNO        10        32
     A            RJANAM        20        44
     A            RNDIDB         7        67
     A            RNDIDA         7        77
     A            RNCIDB         7        90
     A            RNCIDA         7       100
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILPY
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
