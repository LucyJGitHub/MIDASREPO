      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas SD Pooling Accounts List - Detail')              *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      ***PRTF/SDJ101P1*-*Midas*SD*Pooling Accounts List - Details******         JMI108
      *  PRTF/XXJ101P1 - Midas SD Pooling Accounts List - Details     *         JMI108
      *                                                               *
      ***(c)*Misys*International*Banking*Systems*Ltd.*2007*************         JMI108
      *  (c) Finastra International Limited 2018                      *         JMI108
      *                                                               *
      *  Last Amend No. JMI108             Date 31May18               *
      *  Prev Amend No. TFJ011  *CREATE    Date 22Jan07               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  JMI108 - Account Balance Pooling. Upgrade to FBM 2.1         *
      *  TFJ011 - Account Balance Pooling                             *
      *                                                               *
      *****************************************************************
      ** Start of JMI108
     A***********                           REF(MFRFL)
      ** End of JMI108
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
      ** Start of JMI108
     A********* R HEADP1                    TEXT('(c) Misys International +
     A*********                             Banking Systems Ltd. +
     A*********                             2007')
     A          R HEADP1                    TEXT('(c) Finastra Internation+
     A                                      al Limited 2018')
      ** End of JMI108
      *
     A                                      SKIPB(3)
      ** Start of JMI108
     A************************************ 6'REFERENCE SDJ101P1'
     A                                     6'REFERENCE XXJ101P1'
      ** End of JMI108
      ** Start of JMI108
     A*********** BJURPT    R             37REFFLD(URPT)
     A            BJURPT        53A       37
      ** End of JMI108
     A                                   101'DATE'
      ** Start of JMI108
     A*********** BJMRDT    R            106REFFLD(MRDT)
     A            BJMRDT         7A      106
      ** End of JMI108
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    52'POOLING ACCOUNTS LIST'
     A                                      SPACEA(1)
     A                                    52'---------------------'
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     4'ACCOUNT'
     A                                    35'RETAIL'
     A                                    48'ACCOUNT NAME'
     A                                    69'--------------'
     A                                    83'SCHEME'
     A                                    89'---------------'
     A                                   106'INTERNAL'
     A                                   116'---LAST AMEND---'
     A                                       SPACEA(1)
     A                                    35'A/C NO.'
     A                                    69'ACTUAL   SWEEP   TRANSFER'
     A                                    97'INTER'
     A                                   106'BRANCH'
     A                                   116'TYPE'
     A                                   122'DATE'
     A                                   128'TIME'
     A                                       SPACEA(1)
     A                                    69'POOLING'
     A                                    86'ACCOUNT'
     A                                    97'-OFFICE'
     A                                   106'CODE'
     A                                       SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RACNT         28         4TEXT('Account')
     A            RACNO         10        35TEXT('Account Number')
     A            RANAM         20        48TEXT('Account Name')
     A            RCHMA          1        71TEXT('Scheme Actual')
     A            RCHMS          1        80TEXT('Scheme Sweep')
     A            RCHMT          1        89TEXT('Scheme Transfer')
     A            RCHMI          1        99TEXT('Scheme Inter-Office')
     A            RBRCH          3       107TEXT('Internal Branch')
     A            RTYLC          1       117TEXT('Last Change Type')
     A            RLCD           7       120TEXT('Last Change Date')
     A            RLTME          4  0    128TEXT('Last Change Time')
     A                                      EDTWRD('  :  ')
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
