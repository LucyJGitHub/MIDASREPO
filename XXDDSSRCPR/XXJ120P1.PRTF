      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Pooling Instructions Proof list(JE)-Detl')    *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      ***PRTF/GLJ110P1*-*Midas*GL*Pooling*Instructions*Proof*List -****         JMI108
      *  PRTF/XXJ120P1 - Midas GL Pooling Instructions Proof List -   *         JMI108
      *                  (Journal Entries) - Detail                   *
      *                                                               *
      ***(c)*Misys*International*Banking*Systems*Ltd.*2005*************         JMI108
      *  (c) Finastra International Limited 2018                      *         JMI108
      *                                                               *
      *  Last Amend No. JMI108             Date 31May18               *
      *  Prev Amend No. TFJ011  *CREATE    Date 22Jan07               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  JMI108 - Account Balance Pooling. Upgrade to FBM 2.1         *
      *  TFJ011 - Account Balance Pooling                             *
      *                                                               *
      *****************************************************************
      ** Start of JMI108
     A******************                    REF(MFRFL)
      ** End of JMI108
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
      **************************************************************************
      ** Start of JMI108
     A*************                         TEXT('(c) Misys International Banki-
     A*************                         ng Systems Ltd. 2005')
     A                                      TEXT('(c) Finastra International Li-
     A                                      mited 2018')
      ** End of JMI108
      *
     A                                      SKIPB(3)
      *
      ** Start of JMI108
     A*************                        6'REFERENCE GLJ110P1'
     A                                     6'REFERENCE XXJ120P1'
      ** End of JMI108
      ** Start of JMI108
     A*********** BJURPT    R             37REFFLD(URPT)
     A            BJURPT        53A       37
      ** End of JMI108
     A                                   101'DATE'
      ** Start of JMI108
     A*********** BJMRDT    R            106REFFLD(MRDT)
     A            BJMRDT         7A      106
      ** End of JMI108
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A  40                                38'POOLING INSTRUCTIONS PROOF LIST - -
     A                                      JOURNAL ENTRIES - ALL'
     A                                      SPACEA(1)
      *
     A  40                                38'-----------------------------------
     A                                      ---------------------'
     A                                      SPACEA(2)
      *
     A N40                                38'POOLING INSTRUCTIONS PROOF LIST - -
     A                                      JOURNAL ENTRIES - SINGLE'
     A                                      SPACEA(1)
      *
     A N40                                38'-----------------------------------
     A                                      ------------------------'
     A                                      SPACEA(2)
      *
      ***************************************************************************
      *---------------------*
      ***  Detail Headings  *
      *---------------------*
      *
     A          R HEADP2
      *
     A                                     2'FILE SEQ:'
     A            RSEQ           3        12TEXT('File Sequence')
     A                                    17'INSTRUCTION TYPE:'
     A            RTYPE          3        35TEXT('Transaction type')
     A            RTYPDESC      19        39TEXT('Actual Pooling Description')
     A                                    59'INTERFACE BATCH NO.'
     A            RIBTCH         3        79TEXT('Interface Batch No.')
     A                                    84'RECEIVER''S INTERNAL BRANCH:'
     A            RIBRCH         3       112TEXT('Internal Branch')
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A                                     2 'SCHEME DATE'
     A            RSCHDAT        8  0     19TEXT('Scheme Date')
     A                                      EDTWRD('    /  /  ')
     A                                    30'SCHEME NO.'
     A            RSCHEM        10        41TEXT('Scheme Number')
     A            RSCHNAM       40        52TEXT('Scheme Name')
     A                                      SPACEA(1)
      *
     A                                     2'EXECUTION TIME'
     A            RHHMM          5        19TEXT('Execution time')
     A                                    30'VALUE DATE'
     A            RVALDAT        8  0     41TEXT('Value date')
     A                                      EDTWRD('    /  /  ')
     A                                      SPACEA(1)
      *
     A                                     2'INSTRUCTION SEQ.'
     A            RISEQ          5        19TEXT('Instruction Seq.')
     A                                      SPACEA(1)
      *
     A                                     2'CONCENTRATION'
     A                                    71'----------- DEBIT -----------'
     A                                   103'---------- CREDIT -----------'
     A                                      SPACEA(1)
     A                                     3'/DISTRIBUTION'
     A            RCONDIS        1        19TEXT('Distribution')
     A                                    50'ACCOUNTING CODE'
     A            RDACOD         1        71TEXT('Debit Accounting Code')
     A            RCACOD         1       103TEXT('Credit Accounting Code')
     A                                      SPACEA(1)
      *
     A                                     2'REVERSAL ID'
     A            RREV           1        19TEXT('Reversal ID')
     A                                    50'INTERNAL BRANCH CODE'
     A            RDBRCH         3        71TEXT('Debit Branch')
     A            RCBRCH         3       103TEXT('Credit Branch')
     A                                      SPACEA(1)
      *
     A                                     2'CREATION DATE'
     A            RCREADT        8  0     19TEXT('Creation Date')
     A                                      EDTWRD('    /  /  ')
     A                                    50'CURRENCY CODE'
     A            RDCCY          7        71TEXT('Debit Currency')
     A            RCCCY          7       103TEXT('Credit Currency')
     A                                      SPACEA(1)
      *
     A                                     2'STATUS CODE'
     A            RSTAT          1        19TEXT('Status Code')
     A                                    50'ACCOUNT NO.'
     A            RDACNO        30        71TEXT('Debit Account No.')
     A            RCACNO        30       103TEXT('Credit Account No.')
     A                                      SPACEA(1)
      *
     A                                     2'REMARK'
     A            RREMK         30        19TEXT('Remark')
     A                                    50'AMOUNT'
     A            RDAMT         21        71TEXT('Debit Amount')
     A            RDAME          3        94TEXT('Debit Amount Er')
     A            RCAMT         21       103TEXT('Credit Amount')
     A            RCAME          3       126TEXT('Credit Amount Er')
     A                                      SPACEA(1)
      *
     A                                     2'RESULT CODE'
     A            RREST          3        19TEXT('Result Code')
     A                                      SPACEA(2)
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(5)
      *
      **************************************************************************
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
      *
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
      **************************************************************************
