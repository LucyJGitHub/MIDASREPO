      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas GL Pooling Instructions Proof list(MM)-Detl')    *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      ***PRTF/GLJ111P1*-*Midas*GL*Pooling Instructions Proof List -****         EML110
      *  PRTF/XXJ121P1 - Midas GL Pooling Instructions Proof List -   *         EML110
      *                  (MM) - Detail                                *
      *                                                               *
      ***(c)*Misys*International*Banking*Systems*Ltd.*2005*************         EML110
      *  (c) Finastra International Limited 2018                      *         EML110
      *                                                               *
      *  Last Amend No. EML110             Date 20Jan20               *
      *  Prev Amend No. TFJ011  *CREATE    Date 22Jan07               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  EML110 - Account Balance Pooling. Upgrade to FBM 2.1         *
      *  TFJ011 - Account Balance Pooling                             *
      *                                                               *
      *****************************************************************
      ** Start of EML110
     A************                          REF(MFRFL)
      ** End of EML110
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
      **************************************************************************
      ** Start of EML110
     A*********                             TEXT('(c) Misys International Banki-
     A*********                             ng Systems Ltd. 2005')
     A                                      TEXT('(c) Finastra International Li-
     A                                      mited 2018')
      ** End of EML110
      *
     A                                      SKIPB(3)
      *
      ** Start of EML110
     A*********                            6'REFERENCE GLJ111P1'
     A                                     6'REFERENCE XXJ121P1'
      ** End of EML110
      ** Start of EML110
     A*********** BJURPT    R             37REFFLD(URPT)
     A            BJURPT        53A       37
      ** End of EML110
     A                                   101'DATE'
      ** Start of EML110
     A*********** BJMRDT    R            106REFFLD(MRDT)
     A            BJMRDT         7A      106
      ** End of EML110
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A  40                                40'POOLING INSTRUCTIONS PROOF LIST - -
     A                                      MONEY MARKET - ALL'
     A                                      SPACEA(1)
      *
     A  40                                40'-----------------------------------
     A                                      ------------------'
     A                                      SPACEA(2)
      *
     A N40                                40'POOLING INSTRUCTIONS PROOF LIST - -
     A                                      MONEY MARKET - SINGLE'
     A                                      SPACEA(1)
      *
     A N40                                40'-----------------------------------
     A                                      ---------------------'
     A                                      SPACEA(2)
      *
      ***************************************************************************
      *---------------------*
      ***  Detail Headings  *
      *---------------------*
      *
     A          R HEADP2
      *
     A                                     2'FILE SEQ:'
     A            RSEQ           3        12TEXT('File Sequence')
     A                                    17'INSTRUCTION TYPE:'
     A            RTYPE          3        35TEXT('Transaction type')
     A            RTYPDESC      18        39TEXT('Trans Type Description')
     A                                    59'RECEIVER''S INTERNAL BRANCH:'
     A            RIBRCH         3        87TEXT('Receiver''s Internal Branch')
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A                                     2 'SCHEME DATE'
     A            RSCHDAT        8  0     19TEXT('Scheme Date')
     A                                      EDTWRD('    /  /  ')
     A                                    30'SCHEME NO.'
     A            RSCHEM        10        41TEXT('Scheme Number')
     A            RSCHNAM       40        52TEXT('Scheme Name')
     A                                      SPACEA(1)
      *
     A                                     2'EXECUTION TIME'
     A            RHHMM          5        19TEXT('Execution time')
     A                                    30'VALUE DATE'
     A            RVALDAT        8  0     41TEXT('Value date')
     A                                      EDTWRD('    /  /  ')
     A                                    52'DUE DATE'
     A            RDUEDAT        8  0     61TEXT('Due Date')
     A                                      EDTWRD('    /  /  ')
     A                                      SPACEA(1)
      *
     A                                     2'INSTRUCTION SEQ.'
     A            RISEQ          5        19TEXT('Instruction Seq.')
     A                                      SPACEA(1)
      *
     A                                     2'CREATION DATE'
     A            RCREADT        8  0     19TEXT('Creation Date')
     A                                      EDTWRD('    /  /  ')
     A                                      SPACEA(1)
      *
     A                                     2'STATUS CODE'
     A            RSTAT          1        19TEXT('Status Code')
     A                                    73'------- RECEIPT --------'
     A                                    99'------- PAYMENT --------'
     A                                      SPACEA(1)
      *
     A                                     2'BASE RATE CODE'
     A            RRATE          2        19TEXT('Base Rate Code')
     A                                    52'ACCOUNTING CODE'
     A            RDACOD         1        73TEXT('Debit Accounting Code')
     A            RCACOD         1        99TEXT('Credit Accounting Code')
     A                                      SPACEA(1)
      *
     A                                     2'SPREAD'
     A            RSPRD          9        19TEXT('Base Rate Spread')
     A                                    52'INTERNAL BRANCH CODE'
     A            RDBRCH         3        73TEXT('Debit Branch')
     A            RCBRCH         3        99TEXT('Credit Branch')
     A                                      SPACEA(1)
      *
     A                                     2'CALC METHOD'
     A            RCALC          1        19TEXT('Calculation Method')
     A                                    52'CURRENCY CODE'
     A            RDCCY          7        73TEXT('Debit Currency')
     A            RCCCY          7        99TEXT('Credit Currency')
     A                                      SPACEA(1)
      *
     A                                     2'REMARK'
     A            RREMK         30        19TEXT('Remarks')
     A                                    52'ACCOUNT NO.'
     A            RDACNO        24        73TEXT('Debit Account No.')
     A            RCACNO        24        99TEXT('Credit Account No.')
     A                                      SPACEA(1)
      *
     A                                     2'RESULT CODE'
     A            RREST          3        19TEXT('Result Code')
     A                                    52'AMOUNT'
     A            RDAMT         21        73TEXT('Debit Amount')
     A            RDAME          3        94TEXT('Debit Amount Er')
     A            RCAMT         21        99TEXT('Credit Amount')
     A            RCAME          3       120TEXT('Credit Amount Er')
     A                                      SPACEA(2)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(5)
      *
      **************************************************************************
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
      *
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
      **************************************************************************
