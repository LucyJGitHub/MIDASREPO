     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas XX Retrieve FT Account number')                  *
      *****************************************************************
      *                                                               *
      *  Midas - FT MODULE                                            *
      *                                                               *
      *  XX00002 - Midas XX Retrieve FT Account number                *
      *                                                               *
      *  Called By : FT0060                                           *
      *                                                               *
      *  (C) Copyright Finastra Ltd 2020                              *
      *                                                               *
      *  Last Amend No. UUC101  *CREATE    Date 07Oct20               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  UUC101 - Cover FT Payments                                   *
      *           Upgrade UUC041 to FBM 2.1                           *
      *                                                               *
      *****************************************************************
     FMEINFTL0IF  E           K        DISK                           UC
     F                                              KINFSR *PSSR
      **  Midas Incoming Message FT Data
      *****************************************************************
     ISDACOD    E DSSDACODPD
      **  Account code details
     IDSSDY     E DSDSSDY
      **  Second DS for access programs, Long Data Structure
     IO#DRAC      DS
     I                                        1   3 CFBRCA
     I                                        4  24 CFREST
     ILDA       E DSLDA
      ** Local data structure used for error handling
     I/EJECT
      *****************************************************************
      *
      *                  M A I N  P R O C E S S I N G
      *
      *****************************************************************
      *
      **  Parameters List.
     C           *ENTRY    PLIST
     C                     PARM           O#RETC  7        B:Return Code
     C                     PARM           A#MSGR  8        I:FT payment OPMSGR
     C                     PARM           I#PREF 15        I:FT payment ref PREF
     C                     PARM           O#DRAC 24        O:Gl A/c to DR
      **  Set up pgm variables
     C           *NAMVAR   DEFN           LDA   256                       CSD005
     C                     MOVE *BLANK    O#RETC           No Errors
     C                     MOVE A#MSGR    I#MSGR  80       Key to access MEINFTL0
      *   Retrieve the DR a/c using the unique message reference from FT payment
      **  Open Incoming Message FT Data
     C                     OPEN MEINFTL0
     C           I#MSGR    CHAINMEINFTL0             85
      **  If found
     C           *IN85     IFEQ *OFF
      **  Field FTFRGL represents the DR A/c in CNUM-CCY-ACOD-ACSQ-BRCA order
      ** Is this a retail a/c code
     C                     EXSR SRACOD
      ** If non-retail exit. Return code set in SR/SRACOD
     C           O#RETC    IFEQ *BLANKS
      **  Convert to BRCA-CNUM-CCY-ACOD-ACSQ order
     C           3         SUBSTFTFRGL:22 CFBRCA
     C           21        SUBSTFTFRGL:1  CFREST
     C                     ENDIF
     C                     ENDIF
      **  Close Incoming Message FT Data
     C                     CLOSEMEINFTL0
      **  Return to caller.
     C           @RETRN    TAG
     C                     RETRN
      *****************************************************************
      /EJECT
      *****************************************************************
     C           SRACOD    BEGSR
      *
      ** Obtain a/c code details
      *
     C           10        SUBSTFTFRGL:10 PKEY4
     C                     CALL 'AOACODR0'
     C                     PARM '*MSG   ' PRTCD   7
     C                     PARM '*KEY'    POPTN   7
     C                     PARM           PKEY4  10
     C           SDACOD    PARM SDACOD    DSSDY
      ** DB error if not found
      *
     C           PRTCD     IFNE *BLANK
     C                     MOVEL'SDACODPD'DBFILE
     C                     Z-ADD001       DBASE
     C                     MOVELPKEY4     DBKEY
     C                     MOVEL'XX00002 'DBPGM
     C                     MOVEL'NO RETL' O#RETC
     C                     DUMP
     C                     RETRN
     C                     END
      *
      ** Exclude non-retail a/cs
     C           A5ACTY    IFNE 'R'
     C                     MOVEL'NO RETL' O#RETC
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      *****************************************************************
      *                                                               *
      * *PSSR   - Program exception error routine                     *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *  Called by :                                                  *
      *                                                               *
      *  Calls     : #AUDIT                                           *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           WU@RUN    IFEQ *BLANK                     Begin WU@RUN
     C                     MOVE 'Y'       WU@RUN  1
     C                     DUMP
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
     C                     ENDSR
      *****************************************************************
      /EJECT
