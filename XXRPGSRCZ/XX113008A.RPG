     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas GL GCMS i/f - intra-day proj. mvmts. gen.EX1')   *
      *****************************************************************
      *                                                               *
      *  Midas - Midas-GCMS Interface                                 *
      *                                                               *
      ***GLJ008A*-*Intra-day Proj. Mvmts Generation (extract 1)********                   JMI113
      *  XX113008A - Intra-day proj mvmts generation (Extract 1)      *                   JMI113
      *                                                               *
      *  Function:  Extract 1, to extract record which                *
      *             To process the real time posting file for all     *
      *             active GCMS accounts and generate a MT942         *
      *             message for each active account.                  *
      *                                                               *
      ***(c)*Misys*International*Banking*Systems*Ltd.*2005*************                   JMI113
      *  (c) Finastra International Limited 2018                      *                   JMI113
      *                                                               *
      *  Last Amend No. JMI113             Date 22May18               *
      *  Prev Amend No. JMI019             Date 23Feb07               *
      *                 JMI019(C01)*CREATE Date 21Jun05               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  JMI113 - Midas GCMS Interface. Upgrade to FBM 2.1            *
      *  JMI019 - Upgrade as is except for the following              *
      *           K#ACOD to 10,0 and adjust DS G1FULA                 *
      *           K@ACDE to 10,0 and adjust DS K@ACCT                 *
      *           KLIST K#ACMT change K#ACOD from 4,0 to 10,0         *
      *  JMI019 (C01) - Midas-GCMS Interface Change Control 01        *
      *                 Non-accumulation of MT942                     *
      *                                                               *
      *****************************************************************
     F*********GLGCACL0IF  E           K        DISK                                      JMI113
     FXXGCACL0IF  E           K        DISK                                               JMI113
     F                                              KINFSR *PSSR
     FRSACMTL1IF  E           K        DISK
     F            APOSTPDF                          KIGNORE
     F*********GLGCAMPPO   E           K        DISK                      A               JMI113
     FXXGCAMPPO   E           K        DISK                      A                        JMI113
      /EJECT
      *****************************************************************
      *                                                               *
      *   Indicator Usage                                             *
      *   ---------------                                             *
      *                                                               *
      ****22********No Record Found/Last record on GLGCACL0           *                   JMI113
      *   22        No Record Found/Last record on XXGCACL0           *                   JMI113
      *   25        Acc not found on today's posting file RSACMTL1    *
      *                                                               *
      *   U7 & U8   Database error                                    *
      *   nn        X                                                 *
      *                                                               *
      *****************************************************************
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
     I              'XX113008A'           C         PGMNAM                                JMI113
      *
     ILDA       E DSLDA                         256
      *
      ** LDA for database error reporting
      *
     IRUNDAT    E DSRUNDAT
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
     I                                      244 253 @JOB
     I                                      254 263 @USER
     I                                      264 269 @NUM
      *
     I            DS
     I                                        1  24 G1FULA                                    JMI019
     I**********                              1  18 G1FULA                                    JMI019
     I                                        1   3 K#BRCA
     I*********                               4   90K#CNUM                                    JMI113
     I                                        4   9 K#CNUM                                    JMI113
     I                                       10  12 K#CCY
     I                                       13  220K#ACOD                                    JMI019
     I                                       23  240K#ACSQ                                    JMI019
     I**********                             13  160K#ACOD                                    JMI019
     I**********                             17  180K#ACSQ                                    JMI019
      *
     I            DS
     I                                        1  24 K@ACCT                                    JMI019
     I**********                              1  18 K@ACCT                                    JMI019
     I                                        1   3 K@BRCA
     I*********                               4   90K@CUSN                                    JMI113
     I                                        4   9 K@CUSN                                    JMI113
     I                                       10  12 K@CCYD
     I                                       13  22 K@ACDE                                    JMI019
     I                                       23  240K@ASNC                                    JMI019
     I**********                             13  16 K@ACDE                                    JMI019
     I**********                             17  180K@ASNC                                    JMI019
      *
      ** Program Status Data Structure - gives Job name, user and
      ** number
      *
      /EJECT
      *
      *****************************************************************
      *                                                               *
      *   Index to subroutines                                        *
      *   --------------------                                        *
      *                                                               *
      *   INIT      Initial process                                   *
      *   MAIN      Main detail process                               *
      *   CREATE    Create ISO MT942                                  *
      *   *PSSR     Process errors                                    *
      *                                                               *
      *****************************************************************
      *
      /EJECT
      *
      *================================================================
      *  P R O G R A M    S T A R T                                   *
      *================================================================
      *
      ** Receive Parameter List
      *
      ** Execute initial process
      *
     C                     EXSR INIT
      *
      ** Execute main detail process
      *
     C                     EXSR MAIN
      *
      ** Terminate RPG program
     C                     SETON                     LR
      /EJECT
      *
      *****************************************************************
      * Subroutine  :  INIT                                           *
      * Function    :  Perform initial processing                     *
      *                                                               *
      * Called by   :  Main process                                   *
      * Calls       :  AOBANKR0                                       *
      *****************************************************************
      *
     C           INIT      BEGSR
      *
      ** Initialise object copyright statement
      *
     C                     MOVEACPY@      MKI@   80
      *
      ** Initialise LDA
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE *BLANKS   DBASE
     C                     MOVE *BLANKS   DBKEY
     C                     MOVE *BLANKS   DBFILE
     C*********            MOVEL'GLJ008A' DBPGM                                           JMI113
     C                     MOVELPGMNAM    DBPGM                                           JMI113
     C                     OUT  LDA
      *
     C           K#ACMT    KLIST
     C                     KFLD           K#BRCA  3
     C*********            KFLD           K#CNUM  60                                      JMI113
     C                     KFLD           K#CNUM  6                                       JMI113
     C                     KFLD           K#CCY   3
     C                     KFLD           K#ACOD 100                                          JMI019
     C**********           KFLD           K#ACOD  40                                          JMI019
     C                     KFLD           K#ACSQ  20
      *
     C                     ENDSR                           INIT
      /EJECT
      *
      *****************************************************************
      * Subroutine  :  MAIN                                           *
      * Function    :  Perform main detail processing                 *
      *                                                               *
      * Called by   :  Main process                                   *
      * Calls       :  CREATE                                         *
      *****************************************************************
      *
     C           MAIN      BEGSR
      *
     C*********  *LOVAL    SETLLGLGCACL0                                                  JMI113
     C*********            READ GLGCACL0                 22                               JMI113
     C           *LOVAL    SETLLXXGCACL0                                                  JMI113
     C                     READ XXGCACL0                 22                               JMI113
      *
      ** Do while not EOF
      *
     C           *IN22     DOWEQ'0'
      *
     C           G1GN42    IFEQ 'Y'
      ** Generate MT942 message
      *
     C                     EXSR CREATE
     C                     ENDIF
      *
     C**********           READ GLGCACL0                 22                               JMI113
     C                     READ XXGCACL0                 22                               JMI113
      *
     C                     ENDDO
      *
     C                     ENDSR
      /EJECT
      *
      *****************************************************************
      * Subroutine  :  CREATE                                         *
      * Function    :  Create ISO MT942 message for transmission      *
      *                to GCMS                                        *
      *                                                               *
      * Called by   :  MAIN                                           *
      *****************************************************************
      *
     C           CREATE    BEGSR
      *
      ** Access first posting for this account
      *
     C                     SETOF                     25
     C           K#ACMT    SETLLRSACMTPO                 80
     C                     READ RSACMTPO                 25
      *
     C           *IN25     DOWEQ*OFF
      *
     C                     MOVE BRCA      K@BRCA
     C                     MOVE CUSN      K@CUSN
     C                     MOVE CCYD      K@CCYD
     C                     MOVE ACDE      K@ACDE
     C                     MOVE ASNC      K@ASNC
      *
     C           K@ACCT    IFEQ G1FULA
      *
     C**********           WRITEGLGCAMPO                                                  JMI113
     C                     WRITEXXGCAMPO                                                  JMI113
      *
     C                     ENDIF
     C                     READ RSACMTPO                 25
     C                     ENDDO
      *
     C                     ENDSR
      /EJECT
      *
      *****************************************************************
      * Subroutine  :  *PSSR                                          *
      * Function    :  Called autmatically if a program error occurs  *
      *                or directly in case of database errors.        *
      *                This program DUMPs the program just once.      *
      *                                                               *
      * Called by   :  Automatically                                  *
      *                INIT                                           *
      *                                                               *
      * Calls       :  -                                              *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
      ** Check to see if *PSSR has been already called
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
      ** DUMP and write database error report
      *
     C                     SETON                     U7U8LR
     C                     DUMP
      *
     C                     ENDIF
      *
      ** If *PSSR already run, end program.
      *
     C                     RETRN
      *
     C                     ENDSR                           *PSSR
      *
      ********************************************************************
** CPY@
(c) Finastra International Limited 2018
