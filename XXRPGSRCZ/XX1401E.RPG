     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RE Stamp Duty Code Enquiry/Delete')              *
      *****************************************************************
      *                                                               *
      *  Midas    - Retail Module                                     *
      *                                                               *
      ***REM001E**-*Stamp*Duty*Code*Enquiry/Delete*********************   LUC140
      *  XX1401E  - Stamp Duty Code Enquiry/Delete                    *   LUC140
      *                                                               *
      *  Function:  To display or to delete the Stamp Duty Code       *
      *   (I/C)     depending on the action code passed to the        *
      *             program.                                          *
      *                                                               *
      ***Called*By:*REM001D - Stamp Duty Code Subfile Maintenance     *   LUC140
      *  Called By: XX1401D - Stamp Duty Code Subfile Maintenance     *   LUC140
      *                                                               *
      *  Calls    : AOCURRR0 - Currency Codes Access Object           *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1998           *
      *                                                               *
      *  Last Amend No. LUC140             Date 24Feb21               *
      *  Prev Amend No. MMI013 *CREATE     Date 18Mar98               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  LUC140 - Upgrade MMI013 to FM2.1                             *
      *  MMI013 - Retail Stamp Duty and Account Changes               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   01  -  First-time loop.                                     *
      *   03  -  F3  KEY - Exit program.                              *
      *   10  -  F10 KEY - Delete.                                    *
      *   12  -  F12 KEY - Previous screen.                           *
      *   38  -  To display Stamp Duty Code Delection header.         *
      *   39  -  To enable F10 to enter the delete mode.              *
      *   40  -  General error field indicator.                       *
      * U7+U8 -  Database Error                                       *
      *   LR  -  Last record                                          *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  #MINIT - Initialise program.                                 *
      *  #MMAIN - Main Processing.                                    *
      *  #MVALD - Validate Stamp Duty Code exists.                    *
      *  #MEXIT - End processing .                                    *
      *  #MCLER - Clear the message file.                             *
      *  ZASNMS - Send message to the program message queue.          *
      *  ZTNLU1 - Get next transaction number from data area          *
      *  *PSSR  - Error processing subroutine                         *
      *  *INZSR - Auto program initialisation                         *
      *                                                               *
      *****************************************************************
     F**********REM001EDCF  E                    WORKSTN                  LUC140
     FXX1401EDCF  E                    WORKSTN                            LUC140
      ** Stamp Duty Code Enquiry Display
      *
     F**********RESDCDV0IF  E           K        DISK                     LUC140
     FXXSDCDV0IF  E           K        DISK                               LUC140
      ** Stamp Duty Codes File - Retrieval
     F**********RESDCDV1UF  E           K        DISK         KCOMIT      LUC140
     FXXSDCDV1UF  E           K        DISK         KCOMIT                LUC140
     F                                              KINFSR *PSSR
     F            RESDCDPP                          KRENAMERESDCDPX
      ** Stamp Duty Codes File - Update
     F**********SDMSTDY2IF  E           K        DISK                     LUC140
     FXXMSTDY2IF  E           K        DISK                               LUC140
      ** Stamp Duty Extension File
      *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *
      ** Copyright Array
      *
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
     E/COPY ZSRSRC,ZALIGNZ1
      *
      ** Array for CMTTXT commitment text
      *
     E                    DET       256  1
      *
      *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *
     IA@CPY       DS
     I                                        1  80 CPY@
      ** Copyright array
      *
     IDSFDY     E DSDSFDY
      ** Short data structure for Access Programs
      *
     IDSSDY     E DSDSSDY
      ** Long data structure for Access Programs
      *
     IRUNDAT    E DSRUNDAT
      ** Data Area giving Installation Control Details
      *
     ILDA       E DSLDA                         256
     I              SPARE                           W24
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
     IPSDS       SDS
      *
      ** Program Status Data Structure - gives Job/Workstation name and
      ** User ID
     I                                     *PROGRAM ##PGM
     I                                      244 253 JOB
     I                                      244 246 WSID
     I                                      254 263 USER
     IRUNDT       DS
      ** Data Area Rundat
      *
     I                                        1   7 RUNA
     I                                        3   5 UMONH
     I                                    P   8  100RUND
     I                                       11  11 SSF
     I                                       12  12 DATF
     I                                       13  13 MBIN
     I/COPY ZSRSRC,ZTNLU1Z1
     ICMTTXT      DS
      ** Data Structure to setup of commit text
      *
     I                                        1   50NATN
     I                                        6   7 MDID
     I                                        8  15 PGMN
     I                                       16  18 WSIDX
     I                                       19  240MTIME
     I                                       25  25 ACTNX
     I                                       26  35 USIDX
     I                                       51 306 DET
      ** Key fields for CMTTXT
      *
     I                                        1  14 SCRCTL
     I                                        1   2 USDC
     I                                        3   3 UACTD
      ** Screen fields for CMTTXT
      *
     IDETS        DS
     I                                        1   2 SSDC
     I                                        3  32 SDESC
     I                                       33  35 SCYCD
     I                                       36  50 SYAMT
     I                                       51  52 SNADR
     I                                       53  57 STTYP
     I                                       58  59 SNACR
     I                                       60  65 SCUST
     I**********                             66  69 SACOD                 LUC140
     I**********                             70  71 SACSQ                 LUC140
     I                                       66  75 SACOD                 LUC140
     I                                       76  77 SACSQ                 LUC140
     ISDCURR    E DSSDCURRPD
      **  External data structures for Currency Details
      *
     I/EJECT
     C/TITLE Main Processing
      *================================================================
      *  M A I N L I N E  -  P R O G R A M   S T A R T                *
      *================================================================
      *
      ** Program Initialisation
      *
     C                     EXSR #MINIT
      *
      ** Mainline Processing
      *
     C                     EXSR #MMAIN
      *
      ** Exit Program
      *
     C                     EXSR #MEXIT
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
     C/TITLE SR/#MINIT
      *****************************************************************
      *                                                               *
      *  #MINIT - Initialise program.                                 *
      *                                                               *
      *  Called By: Mainline Processing                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           #MINIT    BEGSR
      *
      ** Only do first time through
      *
     C           *IN01     IFEQ *OFF
      *
      ** Move the copyright array.
      *
     C                     MOVELCPY@,1    COPY   80
      *
      ** Define the LDA for error handling
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C**********           MOVEL'REM001E' DBPGM                           LUC140
     C                     MOVEL'XX1401E' DBPGM                           LUC140
     C                     OUT  LDA
      *
      ** Get run date from data area
      *
     C           *NAMVAR   DEFN RUNDAT    RUNDT
     C                     IN   RUNDT
      *
      *
      ** Prepare comit text
      *
     C                     MOVEL'RE'      MDID
     C**********           MOVEL'REM001E' PGMN                            LUC140
     C                     MOVEL'XX1401E' PGMN                            LUC140
     C                     MOVELWSID      WSIDX
     C                     MOVELUSER      USIDX
      *
      *
      ** If action code is 'D' , set on delete mode
      *
     C           @ACTD     IFEQ 'D'
     C                     MOVE *ON       *IN39
     C                     MOVE *ON       *IN38
     C                     ENDIF
      *
      ** Define message file name
      *
     C                     MOVEL'ITUSRMSG'ZAMSGF
      *
      ** Get Stamp Code Details
      *
     C********** @SDC      CHAINRESDCDV0             97                   076177 LUC140
     C           @SDC      CHAINXXSDCDV0             97                          LUC140
     C           *IN97     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL@SDC      DBKEY            ***************
     C                     Z-ADD001       DBASE            * DB ERROR 01 *
     C**********           MOVEL'RESDCDV0'DBFILE           ***************       LUC140
     C                     MOVEL'XXSDCDV0'DBFILE           ***************       LUC140
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Get the number of decimal places for the yearly amount
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*KEY   ' POPTN   7
     C                     PARM MCYCD     PTCCY   3
     C           SDCURR    PARM SDCURR    DSSDY
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVELMCYCD     DBKEY            ***************
     C                     Z-ADD002       DBASE            * DB ERROR 02 *
     C                     MOVEL'SDCURRPD'DBFILE           ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Format Yearly Amount
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE MYAMT     ZFIELD
     C                     Z-ADDA6NBDP    ZADEC
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    SYAMT
      *
      ** Format Transaction Type-DR
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE MTTYP     STTYP
      *
      ** Format Customer Code-CR
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE MCUST     SCUST
      *
      ** Move A/c Code-CR to screen
      *
     C                     MOVE MACOD     SACOD
      *
      ** Move A/c Sequence-CR to screen
      *
     C                     MOVE MACSQ     SACSQ
      *
      ** Move apha fields from file to screen
      *
     C                     MOVE MSDC      SSDC
     C                     MOVE MCYCD     SCYCD
     C                     MOVE MNADR     SNADR
     C                     MOVE MNACR     SNACR
     C                     MOVE MDESC     SDESC
      *
     C                     MOVE *ON       *IN01
      *
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/#MMAIN
      *****************************************************************
      *                                                               *
      *  #MMAIN - Main screen processing                              *
      *                                                               *
      *  Called By: Mainline Processing                               *
      *  Calls: #MCLER , #MVALD                                       *
      *                                                               *
      *****************************************************************
      *
     C           #MMAIN    BEGSR
      *
      *
      ** Loop until either a command key hit or
      *
     C           *IN12     DOUEQ'1'
     C           *IN03     OREQ '1'
      *
      ** Display prompt screen
      *
     C                     EXFMTREM001F1
      *
      ** If F3 is pressed , return to calling program
      *
     C           *IN03     IFEQ '1'
     C                     MOVE 'E'       @RCMD
     C                     ENDIF
      *
      ** If F12 is pressed , return to calling program
      *
     C           *IN12     IFEQ '1'
     C                     MOVE 'F'       @RCMD
     C                     ENDIF
      *
      ** Clear message queue
     C*
     C           *IN40     IFEQ '1'
     C                     EXSR #MCLER
     C                     END
      *
      ** Validate input fields
      *
     C                     EXSR #MVALD
      *
      ** If error , display error message
     C*
     C           *IN40     IFEQ '1'
     C                     WRITEREM001S1
     C                     ENDIF
      *
     C                     ENDDO
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/#MVALD
      *****************************************************************
      *                                                               *
      *  #MVALD - Check Stamp Duty Code exists                        *
      *                                                               *
      *  Called By: MAIN                                              *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           #MVALD    BEGSR
      *
      ** Check to see if Stamp Duty Code exists in Stamp Duty Extension
      ** File if in delete mode
      *
     C           *INKJ     IFEQ *ON
      *
     C********** @SDC      CHAINSDMSTDY2             97                   LUC140
     C           @SDC      CHAINXXMSTDY2             97                   LUC140
     C           *IN97     IFEQ *OFF
     C                     MOVE *ON       *IN40
     C                     MOVE *OFF      *IN39
     C                     MOVEL'XXX0014' ZAMSID
     C                     EXSR ZASNMS
     C                     ELSE
     C********** @SDC      CHAINRESDCDV1             97                   LUC140
     C           @SDC      CHAINXXSDCDV1             97                   LUC140
     C                     DELETRESDCDPX               99
      *
     C           *IN99     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL@SDC      DBKEY            ***************
     C                     Z-ADD003       DBASE            * DB ERROR 03 *
     C**********           MOVEL'RESDCDV1'DBFILE           ***************LUC140
     C                     MOVEL'XXSDCDV1'DBFILE           ***************LUC140
     C                     OUT  LDA
     C                     ROLBK
     C                     EXSR *PSSR
      *
     C                     ELSE
      *
     C                     EXSR ZTNLU1
     C                     TIME           MTIME
     C                     MOVEL@ACTD     ACTNX
     C                     MOVEASCRCTL    DET,01
     C                     MOVEADETS      DET,09
     C           CMTTXT    COMIT
      *
     C                     MOVE 'F'       @RCMD
     C                     MOVE *ON       *IN12
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/ZASNMS
      *****************************************************************
      *                                                               *
      *  ZASNMS - Send message to the program message queue.          *
      *                                                               *
      *  Called By: #MVALD                                            *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           ZASNMS    BEGSR
      *
     C           ZAPGMQ    IFEQ *BLANK
     C                     MOVEL##PGM     ZAPGMQ
     C                     ENDIF
      *
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGMQ 10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message Id.
     C                     PARM           ZAMSGF 10        Message file.
     C                     PARM           ZAMSDA132        Message data.
     C                     PARM           ZAMSTP  7        Message type.
      *
     C           ZAEXIT    ENDSR
      *
      *****************************************************************
      /TITLE SR/#MCLER
      *****************************************************************
      *                                                               *
      *  #MCLER - Clear the message file.                             *
      *                                                               *
      *  Called By: MAIN                                              *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           #MCLER    BEGSR
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGMQ 10
     C                     PARM '*SAME'   ZAPGRL  5
      *
     C                     SETOF                     40
      *
     C                     ENDSR
      *
      *
      *****************************************************************
      /TITLE SR/#MEXIT
      *****************************************************************
      *                                                               *
      *  #MEXIT - End processing .                                    *
      *                                                               *
      *  Called By: MAIN                                              *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           #MEXIT    BEGSR
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
      *
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     ENDIF
      *
     C                     ENDSR
      *
      ****************************************************************
      /TITLE SR/*INZSR
      *****************************************************************
      *                                                               *
      *  *INZSR - Auto Program Initialisation                         *
      *                                                               *
      *  Called By: Program                                           *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           *INZSR    BEGSR
      *
      ** Receive parameters from Calling Program
      *
     C           *ENTRY    PLIST
     C                     PARM           @SDC    2
     C                     PARM           @ACTD   1
     C                     PARM           @RCMD   1
      *
      ** Move Stamp Duty Code and ACtion Code to CMTTXT
      *
     C                     MOVE @SDC      USDC
     C                     MOVE @ACTD     UACTD
      *
     C                     ENDSR
      *
      *****************************************************************
     C/COPY ZSRSRC,ZALIGNZ2
      ** Right-align the amount
      *
     C/COPY ZSRSRC,ZEDITZ2
      ** Format the amount based on the number of decimal places
      *
      /EJECT
      ****************************************************************
     C/COPY ZSRSRC,ZTNLU1Z2
** CPY@   **      OBJECT COPYRIGHT
(C) Copyright Midas-Kapiti International Ltd. 1998
