     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RE Stamp Duty Report Prompt')                    *
      *****************************************************************
      *                                                               *
      *  Midas    - Retail Module                                     *
      *                                                               *
      ***REM003   - Stamp Duty Report Prompt                          *   LUC140
      *  XX1403   - Stamp Duty Report Prompt                          *   LUC140
      *                                                               *
      *  Function:  This is a prompt display to select detail or      *
      *   (I/C)     summary report by reporting period.               *
      *             If F3 or F12 is pressed , return no value.        *
      *                                                               *
      *  Called By: Main menu                                         *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1998           *
      *                                                               *
      *  Last Amend No. LUC140             Date 24Feb21               *
      *  Prev Amend No. MMI013 *CREATE     Date 13Mar98               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  LUC140 - Upgrade MMI013 to FM2.1                             *
      *  MMI013 - Retail Stamp Duty and Account Changes               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   01  -  First-time loop.                                     *
      *   03  -  F3  KEY - Exit program.                              *
      *   12  -  F12 KEY - Previous screen.                           *
      *   30  -  Invalid Reporting Period.                            *
      *   31  -  Invalid Detail/Summary selection.                    *
      *   40  -  General error field indicator.                       *
      * U7+U8 -  Database Error                                       *
      *   LR  -  Last record                                          *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  #MAIN  - Main Processing                                     *
      *  #MINIT - Initialise program.                                 *
      *  #MVALD - Validate Reporting Period and D/S selection.        *
      *  #MEXIT - End processing .                                    *
      *  ZASNMS - Send message to the program message queue.          *
      *  #MCLER - Clear the message file.                             *
      *  *PSSR  - Error processing subroutine                         *
      *  *INZSR - Auto program initialisation                         *
      *                                                               *
      *****************************************************************
     F**********REM003DFCF  E                    WORKSTN                  LUC140
     FXX1403DFCF  E                    WORKSTN                            LUC140
      ** Stamp Duty Report Prompt Display
      *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *
      ** Copyright Array
      *
     E                    CPY@    1   1 80
      *
      ** Month/Day Table
      *
     E                    TABMN   1  12  3   TABDY   2 0
      *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *
     IA@CPY       DS
     I                                        1  80 CPY@
      ** Copyright array
      *
     IDSFDY     E DSDSFDY
      ** Short data structure for Access Programs
      *
     IDSSDY     E DSDSSDY
      ** Long data structure for Access Programs
      *
     IRUNDAT    E DSRUNDAT
      ** Data Area giving Installation Control Details
      *
     ILDA       E DSLDA                         256
     I              SPARE                           W24
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
     IPSDS       SDS
      *
      ** Program Status Data Structure - gives Job/Workstation name and
      ** User ID
     I                                     *PROGRAM ##PGM
     I                                      244 253 JOB
     I                                      244 246 WSID
     I                                      254 263 USER
     IRUNDT       DS
      ** Data Area Rundat
      *
     I                                        1   7 RUNA
     I                                        3   5 UMONH
     I                                    P   8  100RUND
     I                                       11  11 SSF
     I                                       12  12 DATF
     I                                       13  13 MBIN
     I@PARM       DS
      ** Returning parameter containing Reporting Period
      ** and Detail/Summary selection
      *
     I                                        1 100 SPARM
     I                                        1   2 @REPR
     I                                        3   3 @DLSM
     I/EJECT
     C/TITLE Main Processing
      *================================================================
      *  M A I N L I N E  -  P R O G R A M   S T A R T                *
      *================================================================
      *
      ** Program Initialisation
      *
     C                     EXSR #MINIT
      *
      ** Mainline Processing
      *
     C                     EXSR #MMAIN
      *
      ** Exit program
      *
     C                     EXSR #MEXIT
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
     C/TITLE SR/#MINIT
      *****************************************************************
      *                                                               *
      *  #MINIT - Initialise program.                                 *
      *                                                               *
      *  Called By: Mainline Processing                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           #MINIT    BEGSR
      *
      ** Only do first time through
      *
     C           *IN01     IFEQ *OFF
      *
      ** Move the copyright array.
      *
     C                     MOVELCPY@,1    COPY   80
      *
      ** Define the LDA for error handling
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C**********           MOVEL'REM003'  DBPGM                           LUC140
     C                     MOVEL'XX1403'  DBPGM                           LUC140
     C                     OUT  LDA
      *
      ** Get run date from data area
      *
     C           *NAMVAR   DEFN RUNDAT    RUNDT
     C                     IN   RUNDT
      *
      ** Define message file name
      *
     C                     MOVEL'ITUSRMSG'ZAMSGF
      *
      ** Move rundate month to reporting period
      *
     C           UMONH     LOKUPTABMN     TABDY          32
     C           *IN32     IFEQ *ON
     C                     MOVE TABDY     SREPR
     C                     ENDIF
      *
      ** Move default value to detail/summary screen field
      *
     C                     MOVE 'D'       SDLSM
      *
     C                     MOVE *ON       *IN01
      *
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/#MMAIN
      *****************************************************************
      *                                                               *
      *  #MMAIN - Main screen processing                              *
      *                                                               *
      *  Called By: Mainline Processing                               *
      *  Calls: #MCLER , #MVALD                                       *
      *                                                               *
      *****************************************************************
      *
     C           #MMAIN    BEGSR
      *
      ** Loop until either a command key hit or valid screen fields
      ** entered
      *
     C           *IN12     DOUEQ*ON
     C           *IN03     OREQ *ON
     C           *IN40     OREQ *OFF
      *
      ** Display prompt screen
      *
     C                     EXFMTREM003F1
      *
      ** If F3 is pressed , return to calling program
      *
     C           *IN03     IFEQ '1'
     C                     MOVE 'E'       @RCMD
     C                     ENDIF
      *
      ** If F12 is pressed , return to calling program
      *
     C           *IN12     IFEQ '1'
     C                     MOVE 'F'       @RCMD
     C                     ENDIF
      *
      ** Clear message queue
      *
     C           *IN40     IFEQ '1'
     C                     EXSR #MCLER
     C                     WRITEREM003S1
     C                     ENDIF
      *
      ** Validate input fields
      *
     C                     EXSR #MVALD
      *
      ** If error , display error message
      *
     C           *IN40     IFEQ '1'
     C                     WRITEREM003S1
     C                     ENDIF
      *
     C                     ENDDO
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/#MCLER
      *****************************************************************
      *                                                               *
      *  #MCLER - Clear the message file.                             *
      *                                                               *
      *  Called By: MAIN                                              *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           #MCLER    BEGSR
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGMQ 10
     C                     PARM '*SAME'   ZAPGRL  5
      *
     C                     SETOF                     40
      *
     C                     ENDSR
      *
      *
      *****************************************************************
      /TITLE SR/#MVALD
      *****************************************************************
      *                                                               *
      *  #MVALD - Validate reporting period and detail/summary field  *
      *                                                               *
      *  Called By: MAIN                                              *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           #MVALD    BEGSR
      *
      ** If F3 or F12 is pressed , return no value
      *
     C           *IN03     IFEQ *OFF
     C           *IN12     ANDEQ*OFF
      *
      ** Validate reporting Month
      *
     C           SREPR     IFGT 0
     C           SREPR     IFLT TABDY
     C           SREPR     ORGT 12
     C                     MOVE *ON       *IN30
     C                     MOVE *ON       *IN40
     C                     MOVEL'XXX0010' ZAMSID
     C                     EXSR ZASNMS
      *
      ** Else no error - move reporting period to paramter list
      *
     C                     ELSE
     C                     MOVE SREPR     @REPR
     C                     ENDIF
     C                     ENDIF
      *
      ** Validate Detail/Summary Selection
      *
     C           SDLSM     IFNE *BLANK
     C           SDLSM     IFNE 'D'
     C           SDLSM     ANDNE'S'
     C                     MOVE *ON       *IN31
     C                     MOVE *ON       *IN40
     C                     MOVEL'XXX0011' ZAMSID
     C                     EXSR ZASNMS
      *
      ** Else no error - move D/S selection to paramter list
      *
     C                     ELSE
     C                     MOVE SDLSM     @DLSM
     C                     ENDIF
     C                     ENDIF
      *
      ** If no values entered on screen , move default values
      ** to parameter list
      *
     C           SREPR     IFEQ 0
     C                     MOVE TABDY     @REPR
     C                     MOVE TABDY     SREPR
     C                     ENDIF
      *
     C           SDLSM     IFEQ *BLANK
     C                     MOVE 'D'       @DLSM
     C                     MOVE 'D'       SDLSM
     C                     ENDIF
      *
      ** move screen fields to paramter list
      *
     C***********          MOVE SPARM     @PARM
     C                     ELSE
      *
      ** return no value
      *
     C**********           MOVE *BLANKS   @PARM
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/ZASNMS
      *****************************************************************
      *                                                               *
      *  ZASNMS - Send message to the program message queue.          *
      *                                                               *
      *  Called By: #MVALD                                            *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           ZASNMS    BEGSR
      *
     C           ZAPGMQ    IFEQ *BLANK
     C                     MOVEL##PGM     ZAPGMQ
     C                     ENDIF
      *
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGMQ 10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message Id.
     C                     PARM           ZAMSGF 10        Message file.
     C                     PARM           ZAMSDA132        Message data.
     C                     PARM           ZAMSTP  7        Message type.
      *
     C           ZAEXIT    ENDSR
      *
      *****************************************************************
      /TITLE SR/#MEXIT
      *****************************************************************
      *                                                               *
      *  #MEXIT - End processing .                                    *
      *                                                               *
      *  Called By: #MMAIN                                            *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           #MEXIT    BEGSR
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
      *
     C                     CALL 'DBERRCTL'
     C                     DUMP
     C                     ENDIF
      *
     C                     ENDSR
      *
      ****************************************************************
      /TITLE SR/*INZSR
      *****************************************************************
      *                                                               *
      *  *INZSR - Auto Program Initialisation                         *
      *                                                               *
      *  Called By: Program                                           *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           *INZSR    BEGSR
      *
      ** Receive parameters from Calling Program
      *
     C           *ENTRY    PLIST
     C                     PARM           @RSEQ   5
     C                     PARM           @RLEV   1
     C                     PARM           @RENT   1
     C                     PARM           @PARM 100
     C                     PARM           @ATCD   1
     C                     PARM           @RCMD   1
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
** CPY@   **      OBJECT COPYRIGHT
(C) Copyright Midas-Kapiti International Ltd. 1998
** MONTH/DAY TABLE
JAN01
FEB02
MAR03
APR04
MAY05
JUN06
JUL07
AUG08
SEP09
OCT10
NOV11
DEC12
