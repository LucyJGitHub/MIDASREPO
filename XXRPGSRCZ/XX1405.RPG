     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RE Retail Stamp Duty Postings')                  *
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      ***REM005 - Retail Stamp Duty Postings                          *   LUC140
      *  XX1405 - Retail Stamp Duty Postings                          *   LUC140
      *                                                               *
      *  Function:  This program calculates the stamp duty tax for    *
      *             each retail account and creates a debit and       *
      *             credit posting before updating the Midas Retail   *
      **********    Account Details (By Account) file, LF/SDMSTDY0.   *   LUC140
      *             Account Details (By Account) file, LF/XXMSTDY0.   *   LUC140
      *                                                               *
      *  This program is run during the Close of Business phase at    *
      *  the end of each month.                                       *
      *                                                               *
      ***Called By: RECM005 - Retail Stamp Duty Postings Component    *   LUC140
      *  Called By: XXC1405 - Retail Stamp Duty Postings Component    *   LUC140
      *                                                               *
      *  Calls:     AONARRR0 - Narrative Codes Access Object          *
      *             AOACODR0 - Account Codes Access Object            *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1998           *
      *                                                               *
      *  Last Amend No. LUC140             Date 24Feb21               *
      *  Prev Amend No. 171296             Date 24NOV99               *
      *                 146751             Date 03NOV98               *
      *                 MMI013  *CREATE    Date 29APR98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC140 - Upgrade MMI013 to FM2.1                             *
      *  171296 - Stamp duty amount not corredt if current month is   *
      *           'October'. ''BM' fied must be defined as (2,0)      *
      *  146751 - Credit postings output to wrong account - should be *
      *           the account defined on LF/RESDCDV0                  *
      *  MMI013 - Midas Italy Retail Stamp Duty and Account Charges   *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  F U N C T I O N  O F  I N D I C A T O R S                    *
      *                                                               *
      *  30 End of File condition for ACCNT                           *
      *  31 Error reading ACCNT                                       *
      *  32 Error writing to PF/GESDREPP                              *
      *  33 Error writing to PF/GESDGLPP                              *
      ***34*Error*updating*LF/SDMSTDY0                                *         LUC140
      *  34 Error updating LF/XXMSTDY0                                *         LUC140
      *  35 Error during dummy read of PF/GESDREPP or PF/GESDGLPP     *
      *  36 Equal value found in array ZMNM (outside Z subroutines)   *
      *  37 Equal value found in array ZMNM (outside Z subroutines)   *
      ***38*Error*during*CHAIN*to*LF/RESDCDV0                         *         LUC140
      *  38 Error during CHAIN to LF/XXSDCDV0                         *         LUC140
      ***39*Error*writing*to*LF/SDMSTDY0                              *         LUC140
      *  39 Error writing to LF/XXMSTDY0                              *         LUC140
      *  98 Date Format Indicator                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  I N D E X  O F  S U B R O U T I N E S                        *
      *                                                               *
      *  #MINIT - Initialisation subroutine                           *
      *  #MMAIN - Main processing subroutine                          *
      *  #MEXIT - Return to calling program subroutine                *
      *  #MFREQ - Retrieve Stamp Duty frequency                       *
      *  #MDATE - Determine if Stamp Duty is due                      *
      *  #MPROC - Calculate Stamp Duty Tax amount (Overall procedure) *
      *  #MDVAL - Determine whether opened in current charging period *
      *  #MCALO - Calculate Stamp Duty for accounts opened before     *
      *           the beginning of the current charging period        *
      *  #MMCAL - Calculate how long an account has been open         *
      *  #MCALN - Calculate Stamp Duty for accounts opened after      *
      *           the beginning of the current charging period        *
      *  #MDPST - Set up Debit (RE) posting fields                    *
      *  #MPNAR - Get posting narrative                               *
      *  #MDUPD - Update Debit file                                   *
      *  #MCPST - Set up Credit (GL) posting fields                   *
      *  #MACOD - Retrieve Account Section from PF/SDACODPD           *
      *  #MCUPD - Update Credit file                                  *
      *  #MSTPR - Calculate fields for Stamp Duty Extension File      *
      *  #MSUPD - Update Stamp Duty Extension File                    *
      *  ZDATE2 - Converts Midas Day No. to date                      *
      *   *PSSR - Error Control Subroutine                            *
      *                                                               *
      *****************************************************************
     FACCNT   IF  E           K        DISK
     F            ACCNTAAF                          KIGNORE
     F            ACCNTACF                          KIGNORE
      *  Account Master File
      *
     F**********RESDCDV0IF  E           K        DISK                           LUC140
     FXXSDCDV0IF  E           K        DISK                                     LUC140
      *  Stamp Duty Codes File (Retrieval)
      *
     F**********SDMSTDY0UF  E           K        DISK
     FXXMSTDY0UF  E           K        DISK                                     LUC140
      *  Stamp Duty Extension File (by Account)                                 LUC140
     F                                              KCOMIT                      LUC140
      *
     FGESDREPPUF  E                    DISK                      A
     F            APOSTPDF                          KRENAMEGESDRF
      *  Stamp Duty RE Generated Entries File
      *
     FGESDGLPPUF  E                    DISK                      A
     F            APOSTPDF                          KRENAMEGESDGF
      *  Stamp Duty GL Generated Entries File
      *
     FGESDREZZUF  E                    DISK                      A
     F            APOSTZZF                          KRENAMEAPOSTZF1
      *  Stamp Duty RE Trailer File
      *
     FGESDGLZZUF  E                    DISK                      A
     F            APOSTZZF                          KRENAMEAPOSTZF2
      *  Stamp Duty GL Trailer File
      *
     E/EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
     E/COPY ZSRSRC,ZDATE2Z1
     I/SPACE 3
      *
     IACCNTABF
      *
      *  Data Structure containing record format ACCNTABF (LF/ACCNT)
      *
     I              RECI                            MMRECI
     I              CNUM                            MMCNUM
     I              CCY                             MMCCY
     I              ACOD                            MMACOD
     I              ACSQ                            MMACSQ
     I              BRCA                            MMBRCA
     I              ATYP                            MMATYP
     I              STYP                            MMSTYP
     I              PRFC                            MMPRFC
     I              ACNO                            MMACNO
      *
     IRESDCDPP
      *
      *  Rename fields in Retail Stamp Duty Codes File
      *
     I              MSDC                            IMSDC
     I              MCYCD                           IMCYCD
     I              MCUST                           IMCUST
     I              MACOD                           IMACOD
     I              MACSQ                           IMACSQ
      *
     IACDETL      DS
      *
      *  Data Structure containing Account number structure
      *
     I                                        1   3 U1BRCA
     I**********                              4   90U1CNUM                LUC140
     I                                        4   9 U1CNUM                LUC140
     I                                       10  12 U1CCY
     I**********                             13  160U1ACOD                LUC140
     I**********                             17  180U1ACSQ                LUC140
     I                                       13  220U1ACOD                LUC140
     I                                       23  240U1ACSQ                LUC140
      *
     IKEYS        DS
      *
      ***Data*Structure containing LF/SDMSTDY0 key structure              LUC140
      *  Data Structure containing LF/XXMSTDY0 key structure              LUC140
      *
     I**********                              1   60KCNUM                 LUC140
     I                                        1   6 KCNUM                 LUC140
     I                                        7   9 KCCY
     I**********                             10  130KACOD                 LUC140
     I**********                             14  150KACSQ                 LUC140
     I**********                             16  18 KBRCA                 LUC140
     I                                       10  190KACOD                 LUC140
     I                                       20  210KACSQ                 LUC140
     I                                       22  24 KBRCA                 LUC140
      *
     I            DS
      *  Data Structure containing full Midas Account
     I**********                              1  18 MACCNT                LUC140
     I**********                              1   60MMCNUM                LUC140
     I                                        1  124MACCNT                LUC140
     I                                        1   6 MMCNUM                LUC140
     I                                        7   9 MMCCY
     I**********                             10  130MMACOD                LUC140
     I**********                             14  150MMACSQ                LUC140
     I**********                             16  18 MMBRCA                LUC140
     I                                       10  190MMACOD                LUC140
     I                                       20  210MMACSQ                LUC140
     I                                       22  24 MMBRCA                LUC140
      *
     I**********SDMSTD    E DSSDMSTDX0                                    LUC140
     ISDMSTD    E DSXXMSTDX0                                              LUC140
      *
      *  Data Structure containing Stamp Duty Extension file details
      *
     ISDACOD    E DSSDACODPD
      *
      *  Data structure containing PF/SDACODPD
      *
     ISDNARR    E DSSDNARRPD
      *
      *  Data structure containing PF/SDNARRPD
      *
     ISDBANK    E DSSDBANKPD
      *
      *  Data Structure containing Installation Control Details
      *
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
     IDSFDY     E DSDSFDY
      *
      *   Short data structure for access objects
      *
     C/EJECT
      *****************************************************************
      ***                                                           ***
      ***               CONTROL MODULE                              ***
      ***                                                           ***
      *****************************************************************
      *
     C                     EXSR #MINIT
     C                     EXSR #MMAIN
     C                     EXSR #MEXIT
      *
     C/EJECT
      *****************************************************************
      *                                                               *
      *      SR/#MINIT - Initial Subroutine                           *
      *                  Initialises fields                           *
      *                  Accesses Standing Data                       *
      *                                                               *
      *      Calls: *PSSR (if a database error takes place)           *
      *                                                               *
      *****************************************************************
      *
     C           #MINIT    BEGSR
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Read in Installation Data
      *
     C           *NAMVAR   DEFN           LDA
     C**********           MOVEL'REM005'  DBPGM                           LUC140
     C                     MOVEL'XX1405'  DBPGM                           LUC140
      *
      * Initialize work field for GESDREZZ
     C                     Z-ADD0         WTOTRE 130
     C                     Z-ADD0         WHRDCR  40       Hash decimal
     C           *LIKE     DEFN HRWN      WHRWNR           Hash integer
     C           *LIKE     DEFN NORE1     WCPTRE           COUNTER
      *
      * Initialize work field for GESDGLZZ
     C                     Z-ADD0         WTOTGL 130
     C                     Z-ADD0         WHRDCG  40       Hash decimal
     C           *LIKE     DEFN HRWN      WHRWNG           Hash integer
     C           *LIKE     DEFN NORE1     WCPTGL           COUNTER
      *
      *  Extract bank details
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*DBERR'  @RTCD   7
     C                     PARM '*FIRST'  @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           @RTCD     IFNE *BLANKS
      *
      ** Data base error in file SDBANKPD
      *
     C           *LOCK     IN   LDA
     C                     Z-ADD006       DBASE            ***************
     C                     MOVEL'SDBANKPD'DBFILE           *DB ERROR 006 *
     C                     MOVEL@OPTN     DBKEY            ***************
     C**********           MOVEL'REM005'  DBPGM                           LUC140
     C                     MOVEL'XX1405'  DBPGM                           LUC140
     C                     OUT  LDA
     C                     EXSR *PSSR
      *
     C                     ENDIF
      *
      *
      *  Set Date Format Indicator *IN98  on if date format is MMDDYY
      *
     C           BJDFIN    IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ENDIF
      *
      *  Perform dummy reads
      *
     C                     Z-ADD0         DUMM    10
     C           DUMM      IFEQ 1
     C                     READ GESDREPP                 35
     C                     READ GESDGLPP                 35
     C                     ENDIF
      *
      ***Set*up*key for LF/SDMSTDY0                                       LUC140
      *  Set up key for LF/XXMSTDY0                                       LUC140
      *
     C           KACNM     KLIST
     C                     KFLD           KCNUM
     C                     KFLD           KCCY
     C                     KFLD           KACOD
     C                     KFLD           KACSQ
     C                     KFLD           KBRCA
      *
      *  Set up workfields
      *
     C           *LIKE     DEFN MSDPM     UMSDPM
     C           *LIKE     DEFN MSDPY     UMSDPY
     C           *LIKE     DEFN MSDPD     UMSDPD
      *
     C                     ENDSR
      *
     C/EJECT
      *****************************************************************
      *                                                               *
      *       #MMAIN - Main Processing Subroutine                     *
      *                                                               *
      *       Calls: *PSSR                                            *
      *              #MFREQ                                           *
      *              #MPROC                                           *
      *              #MDPST                                           *
      *              #MDUPD                                           *
      *              #MCPST                                           *
      *              #MCUPD                                           *
      *              #MSTDR                                           *
      *              #MSUPD                                           *
      *                                                               *
      *****************************************************************
      *
     C           #MMAIN    BEGSR
      *
     C                     READ ACCNT                  3130
     C           *IN31     IFEQ *ON
      *
      *  Data base error in file ACCNT
      *
     C           *LOCK     IN   LDA
     C                     SETON                       U7U8
     C                     Z-ADD001       DBASE            ***************
     C                     MOVEL'ACCNT'   DBFILE           *DB ERROR 001 *
     C**********           MOVEL'REM005'  DBPGM            ***************LUC140
     C                     MOVEL'XX1405'  DBPGM            ***************LUC140
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           *IN30     DOWEQ*OFF
      *
     C           MMRECI    IFEQ 'D'
     C           MMATYP    ANDEQ'R'
      *
     C                     EXSR #MFREQ
     C                     EXSR #MDATE
      *
     C           DOPROC    IFEQ 'Y'
      *
     C                     EXSR #MPROC
     C                     EXSR #MDPST
     C                     EXSR #MDUPD
     C                     EXSR #MCPST
     C                     EXSR #MCUPD
     C                     EXSR #MSTDR
     C                     EXSR #MSUPD
     C                     MOVEL*BLANK    DOPROC  1
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     READ ACCNT                    30
     C                     ENDDO
      *
      * Update RE Trailer file
     C                     EXSR #MUPRE
      *
      * Update GL Trailer file
     C                     EXSR #MUPGL
      *
     C                     ENDSR
      *
     C/EJECT
      *****************************************************************
      *                                                               *
      *       #MUPRE - Update the Trailer File GESDREZZ               *
      *                                                               *
      *****************************************************************
      *
     C           #MUPRE    BEGSR
      *
     C                     MOVE 'T'       RECI
      *
      **   Calculate Hash-total of rcds whole numbers and decimals
     C           WTOTRE    DIV  1000      WHRWNR
     C                     MVR            WHRDCR
      *
      **   Read PF/GESDREZZ
     C           1         SETLLGESDREZZ
     C                     READ GESDREZZ                 80
      *
      **   If trailer already exists then update
     C           *IN80     IFEQ *OFF                       - B1
     C                     ADD  WCPTRE    NORE1
     C                     ADD  WHRWNR    HRWN
     C           HRDC      ADD  WHRDCR    WHRDCR
     C           WHRDCR    IFGE 1000                       - B2
     C           WHRDCR    SUB  1000      HRDC
     C                     ADD  1         HRWN
     C                     ELSE                            - X2
     C                     Z-ADDWHRDCR    HRDC
     C                     END                             - E2
     C                     UPDATAPOSTZF1
      *
      **   otherwise write
     C                     ELSE                            - X1
     C                     Z-ADDWCPTRE    NORE1
     C           NORE1     ADD  2         NORE1
     C                     Z-ADDWHRWNR    HRWN
     C                     Z-ADDWHRDCR    HRDC
     C                     WRITEAPOSTZF1
     C                     END                             - E1
      *
     C                     ENDSR
      *
     C/EJECT
      *****************************************************************
      *                                                               *
      *       #MUPGL - Update the Trailer File GESDGLZZ               *
      *                                                               *
      *****************************************************************
      *
     C           #MUPGL    BEGSR
      *
     C                     MOVE 'T'       RECI
      *
      **   Calculate Hash-total of rcds whole numbers and decimals
     C           WTOTGL    DIV  1000      WHRWNG
     C                     MVR            WHRDCG
      *
      **   Read PF/GESDGLZZ
     C           1         SETLLGESDGLZZ
     C                     READ GESDGLZZ                 80
      *
      **   If trailer already exists then update
     C           *IN80     IFEQ *OFF                       - B1
     C                     ADD  WCPTGL    NORE1
     C                     ADD  WHRWNG    HRWN
     C           HRDC      ADD  WHRDCG    WHRDCG
     C           WHRDCG    IFGE 1000                       - B2
     C           WHRDCG    SUB  1000      HRDC
     C                     ADD  1         HRWN
     C                     ELSE                            - X2
     C                     Z-ADDWHRDCG    HRDC
     C                     END                             - E2
     C                     UPDATAPOSTZF2
      *
      **   otherwise write
     C                     ELSE                            - X1
     C                     Z-ADDWCPTGL    NORE1
     C           NORE1     ADD  2         NORE1
     C                     Z-ADDWHRWNG    HRWN
     C                     Z-ADDWHRDCG    HRDC
     C                     WRITEAPOSTZF2
     C                     END                             - E1
      *
     C                     ENDSR
      *
     C/EJECT
      *****************************************************************
      *                                                               *
      *       #MEXIT - Exit Subroutine                                *
      *                Returns to calling program                     *
      *                                                               *
      *****************************************************************
      *
     C           #MEXIT    BEGSR
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
     C/EJECT
      *****************************************************************
      *                                                               *
      *       #MFREQ - Retrieves Frequency of Stamp Duty              *
      *                                                               *
      *       Called by: #MMAIN                                       *
      *       Calls:     AONARRR0                                     *
      *                  *PSSR                                        *
      *                                                               *
      *****************************************************************
      *
     C           #MFREQ    BEGSR
      *
     C**********           Z-ADDMMCNUM    KCNUM                           LUC140
     C                     MOVE MMCNUM    KCNUM                           LUC140
     C                     MOVELMMCCY     KCCY
     C                     Z-ADDMMACOD    KACOD
     C                     Z-ADDMMACSQ    KACSQ
     C                     MOVELMMBRCA    KBRCA
      *
     C********** *LOVAL    SETLLSDMSTDY0                                  LUC140
     C********** KACNM     CHAINSDMSTDY0             35                   LUC140
     C           *LOVAL    SETLLXXMSTDY0                                  LUC140
     C           KACNM     CHAINXXMSTDY0             35                   LUC140
      *
     C           *IN35     IFEQ *ON
      *
      ***Data*base*error in file SDMSTDY0                                 LUC140
      *  Data base error in file SDMSTDY0                                 LUC140
      *
     C           *LOCK     IN   LDA
     C                     SETON                       U7U8
     C                     Z-ADD005       DBASE            ***************
     C*********            MOVEL'SDMSTDY0'DBFILE           *DB ERROR 005 *LUC140
     C                     MOVEL'XXMSTDY0'DBFILE           *DB ERROR 005 *LUC140
     C                     MOVELKEYS      DBKEY            ***************
     C**********           MOVEL'REM005'  DBPGM                           LUC140
     C                     MOVEL'XX1405'  DBPGM                           LUC140
     C                     OUT  LDA
     C                     EXSR *PSSR
      *
     C                     ENDIF
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *       #MDATE - Determine if Stamp Duty is Due                 *
      *                If due sets flag DOPROC to 'Y'                 *
      *                                                               *
      *       Called by: #MMAIN                                       *
      *                                                               *
      *****************************************************************
      *
     C           #MDATE    BEGSR
      *
      *   Initialise flag
      *
     C                     MOVE 'N'       DOPROC  1
      *
      *   When the frequency indicator is blank...
      *
     C           MSDF      IFEQ *BLANKS
     C                     Z-ADD*ZEROS    UMSDPM
     C                     Z-ADDMSDPY     UMSDPY
     C                     MOVELMSDPD     UMSDPD
     C                     EXSR #MSUPD
     C                     ELSE
      *
      *   Determine month
      *
     C           3         SUBSTBJMRDT:3  MONTH   3
      *
     C                     SELEC
      *
      *   For monthly frequencies...
      *
     C           MSDF      WHEQ 'M'
     C                     MOVE 'Y'       DOPROC
      *
      *   For quarterly frequencies...
      *
     C           MSDF      WHEQ 'Q'
     C           MONTH     ANDEQ'MAR'
     C           MSDF      OREQ 'Q'
     C           MONTH     ANDEQ'JUN'
     C           MSDF      OREQ 'Q'
     C           MONTH     ANDEQ'SEP'
     C           MSDF      OREQ 'Q'
     C           MONTH     ANDEQ'DEC'
     C                     MOVE 'Y'       DOPROC
      *
      *   For six-monthly frequencies...
      *
     C           MSDF      WHEQ 'X'
     C           MONTH     ANDEQ'JUN'
     C           MSDF      OREQ 'X'
     C           MONTH     ANDEQ'DEC'
     C                     MOVE 'Y'       DOPROC
      *
      *   For annual frequencies...
      *
     C           MSDF      WHEQ 'Y'
     C           MONTH     ANDEQ'DEC'
     C                     MOVE 'Y'       DOPROC
     C                     ENDSL
     C                     ENDIF
      *
     C                     ENDSR
      *
     C/EJECT
      *****************************************************************
      *                                                               *
      *       #MPROC - Calculates amount of stamp duty according to   *
      *                the frequency at which the duty is due and the *
      *                yearly amount which is to be paid              *
      *                                                               *
      *       Called by: #MMAIN                                       *
      *       Calls:     *PSSR                                        *
      *                                                               *
      *****************************************************************
      *
     C           #MPROC    BEGSR
      *
      *   Determine whether account has been opened during charging
      *   period.
      *
     C                     EXSR #MDVAL
      *
      *   Initialise Stamp Duty for this month.
      *
     C                     Z-ADD*ZEROS    POST   150
     C                     Z-ADD*ZEROS    DFACT
      *
      *   Get Stamp Duty Yearly Amount from LF/XXSDCDV0
      *
     C                     MOVE MSDC      KMSDC   2
     C********** KMSDC     CHAINRESDCDV0             38                         LUC140
     C           KMSDC     CHAINXXSDCDV0             38                         LUC140
      *
     C           *IN38     IFEQ *ON
      *
      *  Data base error in file XXSDCDV0
      *
     C           *LOCK     IN   LDA
     C                     Z-ADD009       DBASE            ***************
     C**********           MOVEL'RESDCDV0'DBFILE           *DB ERROR 009 *      LUC140
     C                     MOVEL'XXSDCDV0'DBFILE           *DB ERROR 009 *      LUC140
     C                     MOVE KMSDC     DBKEY            ***************
     C**********           MOVEL'REM005'  DBPGM                           LUC140
     C                     MOVEL'XX1405'  DBPGM                           LUC140
     C                     OUT  LDA
     C                     EXSR *PSSR
      *
     C                     ENDIF
      *
     C                     SELEC
      *
      *   Calculate Duty for accounts opened before the charging period
      *
     C           NEW       WHEQ 'N'
     C                     EXSR #MCALO
      *
      *   Calculate Duty for accounts opened during the charging period
      *
     C           NEW       WHEQ 'Y'
     C                     EXSR #MMCAL
     C                     EXSR #MCALN
     C                     ENDSL
     C                     ENDSR
      *
     C/EJECT
      *****************************************************************
      *                                                               *
      *       #MDVAL - Checks to see if account has been opened       *
      *                within the Stamp Duty charging period.         *
      *                                                               *
      *       Called by: #MPROC                                       *
      *       Calls:     *PSSR                                        *
      *                                                               *
      *****************************************************************
      *
     C           #MDVAL    BEGSR
      *
     C                     Z-ADD1         MN      20
     C                     Z-ADD1         OM      20
     C***                  Z-ADD*ZERO     BM      10                      171296
     C                     Z-ADD*ZERO     BM      20                      171296
     C                     Z-ADD*ZEROS    PRD     20
     C                     MOVE 'N'       NEW     1
      *
      *  Initialise value of period of liability to duty.
      *
     C                     SELEC
     C           MSDF      WHEQ 'Y'
     C                     Z-ADD11        PRD
     C           MSDF      WHEQ 'X'
     C                     Z-ADD5         PRD
     C           MSDF      WHEQ 'Q'
     C                     Z-ADD2         PRD
     C                     ENDSL
      *
      *   Determine month account opened
      *
     C                     MOVELDACO      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZADATE    OPDATE  7
      *
     C           5         SUBSTOPDATE:3  OPMTYR  5
     C                     MOVE OPMTYR    OYR     2
     C                     MOVELOPMTYR    OMTH    3
     C                     MOVE BJMRDT    CURYR   2
      *
      *  If the year of opening is the same as the current year,
      *  the account may not be liable for full stamp duty.
      *  If the month of opening is greater (i.e. later) than the
      *  first month of the period, then partial duty should be
      *  charged.
      *
     C           OYR       IFEQ CURYR
     C           MONTH     LOKUPZMNM,MN                  36
     C           OMTH      LOKUPZMNM,OM                  37
     C           MN        SUB  PRD       BM
     C           OM        IFGT BM
     C           OM        ANDLEMN
     C                     MOVE 'Y'       NEW
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDSR
      *
     C/EJECT
      *****************************************************************
      *                                                               *
      *       #MMCAL - Calculates months of Stamp Duty to be levied   *
      *                on accounts which have opened during the       *
      *                charging period.                               *
      *                                                               *
      *       Called by: #MDVAL                                       *
      *                                                               *
      *****************************************************************
      *
     C           #MMCAL    BEGSR
      *
     C           MN        SUB  OM        TEMP    20
     C           TEMP      ADD  1         MTSOPN  20
      *
     C                     ENDSR
      *
     C/EJECT
      *****************************************************************
      *                                                               *
      *       #MCALO - Calculates amount of stamp duty according to   *
      *                the frequency at which the duty is due and the *
      *                yearly amount which is to be paid              *
      *                                                               *
      *       Called by: #MPROC                                       *
      *                                                               *
      *****************************************************************
      *
     C           #MCALO    BEGSR
      *
     C                     SELEC
     C           MSDF      WHEQ 'Y'
     C                     Z-ADD1         DFACT   20
     C           MSDF      WHEQ 'X'
     C                     Z-ADD2         DFACT
     C           MSDF      WHEQ 'Q'
     C                     Z-ADD4         DFACT
     C           MSDF      WHEQ 'M'
     C                     Z-ADD12        DFACT
     C                     ENDSL
      *
      *   Calculate amount to be posted
      *
     C           MYAMT     DIV  DFACT     POST
      *
     C                     ENDSR
      *
     C/EJECT
      *****************************************************************
      *                                                               *
      *       #MCALN - Calculates amount of stamp duty according to   *
      *                the frequency at which the duty is due and the *
      *                yearly amount which is to be paid              *
      *                                                               *
      *       Called by: #MPROC                                       *
      *                                                               *
      *****************************************************************
      *
     C           #MCALN    BEGSR
      *
      *   Calculate amount to be posted
      *
     C           12        DIV  MTSOPN    DFACT
     C           MYAMT     DIV  DFACT     POST
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *       #MDPST - Initialise and fill fields for debit (RE)      *
      *                posting to PF/GESDREPP                         *
      *                                                               *
      *       Called by: #MMAIN                                       *
      *       Calls:     *PSSR                                        *
      *                                                               *
      *****************************************************************
      *
     C           #MDPST    BEGSR
      *
      *  Initialise data structure for update file
      *
     C                     MOVE 'D'       ACTION
      *
     C                     MOVE 'P'       RECI
      *
      ***If*'Account*to be debited' exists in SDMSTDY0, use that          LUC140
      *  If 'Account to be debited' exists in XXMSTDY0, use that          LUC140
      *  account (stored in MACDR) for account details, otherwise
      *  use the details from ACCNT
      *
     C           MACDR     IFEQ *BLANKS
     C**********           Z-ADDMMCNUM    CNUM                            LUC140
     C                     MOVE MMCNUM    CNUM                            LUC140
     C                     MOVELMMCCY     CCY
     C                     Z-ADDMMACOD    ACOD
     C                     MOVELMMACOD    ACODQQ                          LUC140
     C                     Z-ADDMMACSQ    ACSQ
     C                     MOVELMMBRCA    BRCA
     C                     ELSE
     C                     MOVELMACDR     ACDETL
     C**********           Z-ADDU1CNUM    CNUM                            LUC140
     C                     MOVE U1CNUM    CNUM                            LUC140
     C                     MOVELU1CCY     CCY
     C                     Z-ADDU1ACOD    ACOD
     C                     MOVELU1ACOD    ACODQQ                          LUC140
     C                     Z-ADDU1ACSQ    ACSQ
     C                     MOVELU1BRCA    BRCA
     C                     ENDIF
     C                     MOVEL*BLANKS   IPDN
     C                     Z-ADDMMACNO    ACNO
     C                     Z-ADDBJRDNB    PSTD
     C                     Z-ADDBJRDNB    VALD
     C                     MOVELMTTYP     TRAT
      *
      *  Retrieve Posting Narrative
      *
     C                     EXSR #MPNAR
      *
     C                     MOVELALDON     PNAR
     C                     Z-ADDPOST      PSTA
     C                     Z-ADD*ZERO     DRCR
     C**********           Z-ADD*ZEROS    ASOC                            LUC140
     C                     MOVE *BLANKS   ASOC                            LUC140
     C                     Z-ADD*ZEROS    CHQN
     C************         MOVEL*BLANKS   SPOS
     C                     MOVE 'GE-S9'   SPOS
     C                     Z-ADD1         RIND
     C                     Z-ADD*ZEROS    REJC
     C                     MOVEL*BLANKS   DPMT
     C                     Z-ADD*ZEROS    RRNM
     C                     MOVELMMATYP    ATYP
     C                     MOVE *BLANK    EXIN
     C                     BITOF'01234567'PRIN
     C***********          MOVE *BLANK    GETP
     C                     MOVE '9'       GETP
     C                     MOVELMMPRFC    PRFC
     C                     Z-ADD235959    PTIM
     C                     Z-ADD*ZERO     VOIN
     C                     MOVEL*BLANKS   MGTT
      *
      *  Retrieve Account Section
      *
     C                     EXSR #MACOD
     C                     MOVELA5ACSC    ACSC
     C                     Z-ADD*ZERO     LTAI
     C                     MOVEL*BLANKS   RINO
     C                     MOVEL*BLANKS   SWCR
     C                     MOVEL*BLANKS   DLREF
     C                     MOVEL*BLANKS   FACO
     C**********           Z-ADD*ZEROS    DBCNUM                          LUC140
     C                     MOVE *BLANKS   DBCNUM                          LUC140
     C                     MOVEL*BLANKS   PREF
     C                     MOVEL*BLANKS   OTRF
     C                     MOVEL*BLANKS   OTST
     C                     MOVEL*BLANKS   OTTP
     C                     Z-ADD*ZEROS    SDCB
     C                     Z-ADD*ZEROS    SDLB
     C                     MOVEL*BLANKS   BOKC
     C                     Z-ADD*ZEROS    NITMS
     C                     MOVEL*BLANK    REBI
     C                     Z-ADD*ZEROS    CHGA
     C                     MOVEL*BLANK    TLIN
     C                     MOVEL*BLANKS   TRCCY
     C                     MOVEL*BLANK    STYP
     C                     MOVEL*BLANKS   PBTT
     C                     MOVEL*BLANKS   ORBR
     C                     Z-ADD*ZEROS    ORAMT
      *
     C                     ENDSR
      *
     C/EJECT
      *****************************************************************
      *                                                               *
      *       #MDUPD - Write new values to PF/GESDREPP                *
      *                                                               *
      *       Called by: #MMAIN                                       *
      *       Calls:     *PSSR                                        *
      *                                                               *
      *****************************************************************
      *
     C           #MDUPD    BEGSR
      *
     C                     WRITEGESDRF                 32
      *
     C           *IN32     IFEQ *ON
      *
      ** Data base error in file GESDREPP
      *
     C           *LOCK     IN   LDA
     C                     Z-ADD002       DBASE            ***************
     C                     MOVEL'GESDREPP'DBFILE           *DB ERROR 002 *
     C**********           MOVEL'REM005'  DBPGM            ***************LUC140
     C                     MOVEL'XX1405'  DBPGM            ***************LUC140
     C                     OUT  LDA
     C                     EXSR *PSSR
      *
     C*********************ENDIF
     C                     ELSE
      * Hash total setup
      * and update the Trailer record.
     C                     ADD  1         WCPTRE
     C                     ADD  PSTA      WTOTRE
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
     C/EJECT
      *****************************************************************
      *                                                               *
      *       #MCPST - Initialise and fill fields for credit (GL)     *
      *                posting to PF/GESDGLPP                         *
      *                                                               *
      *       Called by: #MMAIN                                       *
      *       Calls:     *PSSR                                        *
      *                                                               *
      *****************************************************************
      *
     C           #MCPST    BEGSR
      *
      *  Set up record format for update file
      *
     C                     MOVE 'C'       ACTION  1
      *
     C                     MOVE 'P'       RECI
     C**                   Z-ADDMCUST     CNUM                            146751
     C**                   MOVELMCYCD     CCY                             146751
     C**                   Z-ADDMACOD     ACOD                            146751
     C**                   Z-ADDMACSQ     ACSQ                            146751
     C**********           Z-ADDIMCUST    CNUM                     146751 LUC140
     C                     MOVE IMCUST    CNUM                            LUC140
     C                     MOVELIMCYCD    CCY                             146751
     C                     Z-ADDIMACOD    ACOD                            146751
     C                     MOVELIMACOD    ACODQQ                          LUC140
     C                     Z-ADDIMACSQ    ACSQ                            146751
     C                     Z-ADD*ZEROS    ACNO
     C                     Z-ADD*ZEROS    TRAT
     C                     MOVE 'GE-S7'   SPOS
     C                     MOVE '7'       GETP
      *
      *  Retrieve Posting Narrative
      *
     C                     EXSR #MPNAR
      *
     C                     MOVELUMPNAR    PNAR
     C                     Z-ADD1         DRCR
     C                     MOVE MBRCA     BRCA
     C                     Z-ADD*ZERO     RIND
     C                     MOVEL'G'       ATYP
      *
     C                     ENDSR
      *
     C/EJECT
      *****************************************************************
      *                                                               *
      *       #MACOD - Retrieves Account Section from PF/SDACODPD     *
      *                                                               *
      *       Called by: #MDPST                                       *
      *                  #MCPST                                       *
      *       Calls:     *PSSR                                        *
      *                                                               *
      *****************************************************************
      *
     C           #MACOD    BEGSR
      *
      *  Set up account code key
      *
     C                     SELEC
     C           ACTION    WHEQ 'D'
     C           MACDR     IFEQ *BLANKS
     C**********           MOVELMMACOD    T@ACOD  4                       LUC140
     C                     MOVELMMACOD    T@ACOD 10                       LUC140
     C                     ELSE
     C                     MOVELU1ACOD    T@ACOD
     C                     ENDIF
     C           ACTION    WHEQ 'C'
     C                     MOVELMACOD     T@ACOD
     C                     ENDSL
      *
     C                     CALL 'AOACODR0'
     C                     PARM *BLANKS   P@RTCD  7
     C                     PARM '*KEY   ' P@OPTN  7
     C**********           PARM T@ACOD    P@ACOD  4                       LUC140
     C                     PARM T@ACOD    P@ACOD 10                       LUC140
     C           SDACOD    PARM SDACOD    DSFDY
      *
      *  Check if an error occurred
      *
     C           P@RTCD    IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'SDACODPD'DBFILE
     C                     Z-ADD8         DBASE            **********
     C                     MOVELP@OPTN    DBOPTN  7        *  008   *
     C                     MOVELP@ACOD    DBKEY            **********
     C                     MOVEL'AOACODR0'DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      *
     C/EJECT
      *****************************************************************
      *                                                               *
      *       #MPNAR - Retrieves Posting Narrative from PF/SDNARRPD   *
      *                and performs additional processing for         *
      *                credit postings.                               *
      *                                                               *
      *       Called by: #MDPST                                       *
      *                  #MCPST                                       *
      *       Calls:     *PSSR                                        *
      *                                                               *
      *****************************************************************
      *
     C           #MPNAR    BEGSR
      *
      *   Insert correct key according to whether posting is credit or
      *   debit
      *
     C                     MOVE *BLANKS   @CBNB
     C           ACTION    IFEQ 'C'
     C                     MOVELMNACR     @CBNB
     C                     ELSE
     C                     MOVELMNADR     @CBNB
     C                     ENDIF
      *
     C                     CALL 'AONARRR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM           @CBNB   2
     C           SDNARR    PARM SDNARR    DSFDY
      *
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVE 'SDNARRPD'DBFILE
     C                     Z-ADD7         DBASE            **********
     C                     MOVEL@OPTN     DBOPTN  7        *  007   *
     C                     MOVEL@CBNB     DBKEY            **********
     C                     MOVEL'AONARRR0'DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      *   Additional processing for credit postings
      *
     C           ACTION    IFEQ 'C'
     C                     MOVELALDON     UMPNAR 30
     C                     MOVE MACCNT    UMPNAR
     C                     ENDIF
      *
     C                     ENDSR
      *
     C/EJECT
      *****************************************************************
      *                                                               *
      *       #MCUPD - Write new values to PF/GESDGLPP                *
      *                                                               *
      *       Called by: #MMAIN                                       *
      *       Calls:     *PSSR                                        *
      *                                                               *
      *****************************************************************
      *
     C           #MCUPD    BEGSR
      *
     C                     WRITEGESDGF                 33
      *
     C           *IN33     IFEQ *ON
      *
      *  Data base error in file GESDGLPP
      *
     C           *LOCK     IN   LDA
     C                     SETON                       U7U8
     C                     Z-ADD003       DBASE            ***************
     C                     MOVEL'GESDGLPP'DBFILE           *DB ERROR 003 *
     C**********           MOVEL'REM005'  DBPGM            ***************LUC140
     C                     MOVEL'XX1405'  DBPGM            ***************LUC140
     C                     OUT  LDA
     C                     EXSR *PSSR
      *
     C*********************ENDIF
     C                     ELSE
      * Hash total setup
      * and update the Trailer record.
     C                     ADD  1         WCPTGL
     C                     ADD  PSTA      WTOTGL
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
     C/EJECT
      *****************************************************************
      *                                                               *
      *       #MSTDR - Calculate fields for Stamp Duty Extension      *
      **********       File LF/SDMSTDY0                               *   LUC140
      *                File LF/XXMSTDY0                               *   LUC140
      *                                                               *
      *       Called by: #MMAIN                                       *
      *       Calls:     *PSSR                                        *
      *                                                               *
      *****************************************************************
      *
     C           #MSTDR    BEGSR
      *
      *  Z-add posting amount to Stamp Duty Posted This Month
      *
     C                     Z-ADDPOST      UMSDPM
      *
      *  Add posting amount to Stamp Duty Posted Year to Date from file
      *
     C           MSDPY     ADD  POST      UMSDPY
      *
      *  Set Last Stamp Duty Posting Date to today's date
      *
     C                     MOVELBJMRDT    UMSDPD
      *
     C                     ENDSR
      *
     C/EJECT
      *****************************************************************
      *                                                               *
      ********#MSUPD - Write new values to LF/SDMSTDY0                *   LUC140
      *       #MSUPD - Write new values to LF/XXMSTDY0                *   LUC140
      *                                                               *
      *       Called by: #MMAIN                                       *
      *       Calls:     *PSSR                                        *
      *                                                               *
      *****************************************************************
      *
     C           #MSUPD    BEGSR
      *
     C********** KACNM     CHAINSDMSTDY0             35                   LUC140
     C           KACNM     CHAINXXMSTDY0             35                   LUC140
     C           *IN35     IFEQ *ON
      *
      ***Data base error in file SDMSTDY0                                 LUC140
      *  Data base error in file XXMSTDY0                                 LUC140
      *
     C           *LOCK     IN   LDA
     C                     SETON                       U7U8
     C                     Z-ADD010       DBASE            ***************
     C**********           MOVEL'SDMSTDY0'DBFILE           *DB ERROR 010 *LUC140
     C                     MOVEL'XXMSTDY0'DBFILE           *DB ERROR 010 *LUC140
     C                     MOVELKEYS      DBKEY            ***************
     C**********           MOVEL'REM005'  DBPGM                           LUC140
     C                     MOVEL'XX1405'  DBPGM                           LUC140
     C                     OUT  LDA
     C                     EXSR *PSSR
      *
     C                     ELSE
      *
     C                     Z-ADDUMSDPM    MSDPM
     C                     Z-ADDUMSDPY    MSDPY
     C                     MOVELUMSDPD    MSDPD
     C                     UPDATSDMSTDD0               34
      *
     C           *IN34     IFEQ *ON
      *
      ***Data*base error in file SDMSTDY0                                 LUC140
      *  Data base error in file XXMSTDY0                                 LUC140
      *
     C           *LOCK     IN   LDA
     C                     SETON                       U7U8
     C                     Z-ADD004       DBASE            ***************
     C**********           MOVEL'SDMSTDY0'DBFILE           *DB ERROR 004 *LUC140
     C                     MOVEL'XXMSTDY0'DBFILE           *DB ERROR 004 *LUC140
     C                     MOVELKEYS      DBKEY            ***************
     C**********           MOVEL'REM005'  DBPGM                           LUC140
     C                     MOVEL'XX1405'  DBPGM                           LUC140
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
     C/EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      ********************************************************************
      *
     C                     ENDSR
      *
      ********************************************************************
     C/COPY ZSRSRC,ZDATE2Z4
**  CPY@
(C) Copyright Midas-Kapiti International Ltd. 1998
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
