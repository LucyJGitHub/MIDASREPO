     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas xx Extract I/O Data 2')                          *
     F*****************************************************************
     F*                                                               *
     F*  Midas - I/O RECONCILE                                        *
     F*                                                               *
     F*  XX1410 - EXTRACT I/O DATA 2                                  *
     F*                                                               *
     F*  (C) Finastra International Limited 2021                      *
     F*                                                               *
     F*  Last Amend No. EML114   *CREATE   Date 23Sep21               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
     F*  EML114 - Upgrade TFJ004 to FM2.1                             *
     F*  TFJ004 - Inter-office Reconcile                              *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     FACPO5   IP  F     620  1AI     1 DISK
     FXXACODPPIF  E                    DISK
     FXXCUSTPPIF  E                    DISK
      *
     FXXACNTL1UF  E           K        DISK                      A
     F*
      *****************************************************************
      *
     E                    ACD      1000 10 0
     E                    REC      1000  1
     E                    ALT      1000 10
     E                    HCS      1000  3
     E                    IOC      1000  1
      *
     E********************CST      1000  6 0                                                 EML114
     E                    CST      1000  6                                                   EML114
     E                    HCO      1000  3
      *
      *****************************************************************
     I*
     IACPO5   NS  01   1 CP
     I****************************************2   70CNUM1                                    EML114
     I                                        2   7 CNUM1                                    EML114
     I                                        8  10 CCY1
     I                                       15  160ACSQ1
     I                                    P  74  800PSTA
     I                                       81  810DRCR
     I                                      103 105 BRCA1
     I                                      590 5990ACOD1
     I        NS  09
     I*
     I@BANK     E DSSDBANKPD
     I** DUMMY RECORD FORMAT FOR BANK DETAILS
     IDSFDY     E DSDSFDY
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE
     I*
     C*****************************************************************
     C*
     C  N10                EXSR #INIT
     C  N10                SETON                     10
     C**
     C   09                GOTO #END
     C**
     C           #ACKEY    KLIST
     C                     KFLD           BRCA1
     C                     KFLD           CNUM1
     C                     KFLD           CCY1
     C                     KFLD           ACOD1
     C                     KFLD           ACSQ1
     C*
     C                     Z-ADD1         X
     C           ACOD1     LOKUPACD,X                    80
     C  N80                GOTO #END
     C*
     C           IOC,X     COMP 'Y'                      85
     C   85                Z-ADD1         Y       40
     C   85      CNUM1     LOKUPCST,Y                    86
     C   85N86             GOTO #END
     C*
     C           #ACKEY    CHAINXXACNTL1             88
     C   88                Z-ADD0         YTDBAL
     C*
     C                     MOVE 'D'       RECI
     C                     MOVE BRCA1     BRCA
     C                     MOVE CNUM1     CNUM
     C                     MOVE CCY1      CCY
     C                     MOVE ACOD1     ACOD
     C                     MOVE ACSQ1     ACSQ
     C                     MOVE REC,X     RECTYP
     C                     MOVE ALT,X     ALTAC
     C  N85                MOVE HCS,X     HOCNUM
     C   85                MOVE HCO,Y     HOCNUM
     C                     Z-ADDBJRDNB    LUPDD
     C*
     C           DRCR      IFEQ 0
     C                     ADD  PSTA      YTDBAL
     C                     ELSE
     C                     SUB  PSTA      YTDBAL
     C                     END
     C*
     C  N88                UPDATXXACNTP0
     C   88                WRITEXXACNTP0
     C*
     C           #END      TAG
     C*
     C********************************************************************
     C           #INIT     BEGSR
     C*
     C** CALL ACCESS PROGRAM FOR BANK DETAILS
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG    '@RTCD   7
     C                     PARM '*FIRST  '@OPTN   7
     C           @BANK     PARM @BANK     DSFDY
     C*
     C           @RTCD     IFNE *BLANK
     C                     MOVEL'SDBANKPD'DBFILE 10         *************
     C                     MOVEL'01'      DBASE   3         *DBERROR 001*
     C                     MOVEL@OPTN     DBOPTN  7         *************
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     END
     C*
     C**
     C**   SET OFF EOF INDICATOR
     C                     SETOF                     79
     C**
     C**   SET ARRAY INDEX TO ZERO.
     C                     Z-ADD0         X       40
     C**
     C**   LOOP TO READ CURRENCY DETAIL RECORDS.
     C           LOOP1     TAG                             ** LOOP1 TAG **
     C                     READ XXACODPP                 79
     C**
     C**   END IF ALL XXACODPP
     C   79                GOTO NEXT
     C**
     C**   IGNORE DELETED RECORDS AND NON-SUB-RECORD ONE.
     C           RECI      COMP 'D'                  8080
     C   80                GOTO LOOP1
     C**
     C**   INCREMENT ARRAY INDEX.
     C           X         ADD  1         X
     C**
     C**   MOVE DATA INTO ARRAYS.
     C                     MOVE IOACOD    ACD,X
     C                     MOVE RECTYP    REC,X
     C                     MOVE ALTAC     ALT,X
     C                     MOVE HCNUM1    HCS,X
     C                     MOVE IOCUSF    IOC,X
     C**
     C**   GO TO READ NEXT RECORD.
     C                     GOTO LOOP1
     C**
     C           NEXT      TAG                             ** NEXT TAG  **
     C*
     C**   SET OFF EOF INDICATOR
     C                     SETOF                     79
     C**
     C**   SET ARRAY INDEX TO ZERO.
     C                     Z-ADD0         X       40
     C**
     C**   LOOP TO READ CURRENCY DETAIL RECORDS.
     C           LOOP2     TAG                             ** LOOP2 TAG **
     C                     READ XXCUSTPP                 79
     C**
     C**   END IF ALL XXACODPP
     C   79                GOTO #IEND
     C**
     C**   IGNORE DELETED RECORDS AND NON-SUB-RECORD ONE.
     C           RECI      COMP 'D'                  8080
     C   80                GOTO LOOP2
     C**
     C**   INCREMENT ARRAY INDEX.
     C           X         ADD  1         X
     C**
     C**   MOVE DATA INTO ARRAYS.
     C                     MOVE IOCUST    CST,X
     C                     MOVE HCNUM2    HCO,X
     C**
     C**   GO TO READ NEXT RECORD.
     C                     GOTO LOOP2
     C**
     C           #IEND     TAG                             ** #IEND TAG  **
     C*
     C                     ENDSR
     C********************************************************************
