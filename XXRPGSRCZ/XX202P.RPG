     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SD - OENACE Code Report')                        *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  XX202P - Midas SD OENACE Code Report                         *
      *                                                               *
      *  Function: This program produces a report listing of all      *
      *    (I/C)   the OENACE Codes.                                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Last Amend No. ERZ032   *CREATE   Date 24Jan13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  ERZ032 - Customer Details Additional Information             *
      *                                                               *
      *****************************************************************
     FSDOENAV1IF  E           K        DISK
     FXX202AU O   E                    PRINTER
     F                                              KINFDS SPOOLU
     FXX202P1 O   E                    PRINTER                        UC
     F                                              KINFDS SPOOL1
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *  89 - End of File                                             *
      *                                                               *
      *  98 - Date Format: DDMMYY (off); MMDDYY (on)                  *
      *  90-99 - Used by Standard Subroutines                         *
      *                                                               *
      *  U7+U8 - Database Error                                       *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  SRINIT - Program Initialisation                              *
      *  SRPROC - Detail Processing                                   *
      *  SRRPT  - Process Report Lines                                *
      *                                                               *
      *  SRWRP1 - Write a Detail Record to the Report                 *
      *  WP1END - Write End of Report                                 *
      *                                                               *
      *  SRAUDT - Produce Audit Report and End Program                *
      *  RCFP1  - RCF Processing for P1 Report                        *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *  *PSSR  - Program Error Processing Subroutine                 *
      *                                                               *
      *****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      /SPACE 3
      *
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IRUNDAT    E DSRUNDAT
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
     ISPOOL1      DS
      ** File Information Data Structure for SDPC01P1.
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      ** File Information Data Structure for SDPC01AU.
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
      ** Dummy Data Structures used by Access Programs.
      *
     IDSFDY     E DSDSFDY
      ** First DS for access programs, short data structure
      *
     ISDBANK    E DSSDBANKPD
      *
      ** External data structures for Bank Details
      *
      /EJECT
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ** Perform Initialisation.
      *
     C                     EXSR SRINIT
      *
      ** Perform Detail Processing.
      *
     C                     EXSR SRPROC
      *
      ** Output Audit Report and End Program.
      *
     C                     EXSR SRAUDT
      *
     C                     MOVE '1'       *INLR
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
      /TITLE SR/SRINIT
      *****************************************************************
      *  SRINIT - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:     AOBANKR0 - Access program for bank details        *
      *             *PSSR    - Error control routine                  *
      *             RCFAU    - RCF processing for audit report        *
      *****************************************************************
      *
     C           SRINIT    BEGSR
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Receive Parameter List.
      *
     C           *ENTRY    PLIST
     C                     PARM           PRTCD   7
     C                     PARM           PMODE   1
     C                     PARM           PSEQ    5
     C                     PARM           PLEVL   1
     C                     PARM           PENTY   3
      *
      ** Initialise LDA field.
      *
     C                     MOVELPGM       DBPGM
      *
      ** Call Access Program for Bank Details - Title, Run Date and
      ** Run Day Number.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** Handle Database Error.
      *
     C           WRTCD     IFNE *BLANKS
     C                     MOVE 'SDBANKPD'DBFILE           ************
     C                     Z-ADD001       DBASE            *DBERROR 01*
     C                     EXSR *PSSR                      ************
     C                     ENDIF
      *
      ** Check System Date Format, DDMMYY (*IN98 off)
      **                           MMDDYY (*IN98 on).
      *
     C           BJDFIN    IFEQ 'M'
     C                     SETON                     98
     C                     ENDIF
      *
      ** Access RUNDAT for multibranching indicator
      *
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
      *
      ** RCF Processing for Audit File.
      *
     C                     EXSR RCFAU
      *
      ** Report Work fields.
      *
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRPROC
      *****************************************************************
      *  SRPROC - Subroutine to do the Detail Processing.             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:     SRAUDT  - Produce Audit report                    *
      *             RCFP1   - RCF processing for P1 report            *
      *             SRRPT   - Process Report Lines                    *
      *             WP1END  - Write End of Report                     *
      *****************************************************************
      *
     C           SRPROC    BEGSR
      *
      ** Read first record from File.
      *
     C                     READ SDOENAV1                 89
      *
      ** If End of Records, (*IN89), Perform: Audit Reporting (No
      ** Records).
      *
     C           *IN89     IFEQ *ON
     C                     EXSR SRAUDT
     C                     ELSE
     C                     OPEN XX202P1
     C                     EXSR RCFP1
     C                     MOVE 'Y'       WOPNP1  1
     C                     ENDIF
      *
      ** Do Until End of File.
      *
     C           *IN89     DOUEQ*ON
      *
      ** Count records read.
      *
     C                     ADD  1         RCOUNT  50
      *
      ** Process Report Lines.
      *
     C                     EXSR SRRPT
      *
      ** Read next record.
      *
     C                     READ SDOENAV1                 89
      *
      ** End of Do Until End of Valid Records.
     C                     ENDDO
      *
      *
      ** Write final records and Totals to Report.
      *
     C           WOPNP1    IFEQ 'Y'
     C                     EXSR WP1END
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRRPT
      *****************************************************************
      *  SRRPT  - Process Report Lines.                               *
      *                                                               *
      *  Called By: SRPROC                                            *
      *  Calls:     SRWRP1  - Write detail record to the report       *
      *****************************************************************
      *
     C           SRRPT     BEGSR
      *
      ** Move to output fields
      *
     C                     MOVELOENAC     RDOENA           OENACE Code
     C                     MOVELOENACD    RDDESC           Description
      *
      ** Write out the record.
      *
     C                     EXSR SRWRP1
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRWRP1
      *****************************************************************
      *  SRWRP1 - Subroutine to Write a Detail record to the Report.  *
      *                                                               *
      *  Called By: SRRPT                                             *
      *  Calls:     None.                                             *
      *****************************************************************
      *
     C           SRWRP1    BEGSR
      *
      ** Check that sufficient lines remain for the Format. If not,
      ** write out the Main Headings on a new page.
      *
     C                     Z-ADD1         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ** Write out Detail Record.
      *
     C                     WRITEDETAIL
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/WP1END
      *****************************************************************
      *  WP1END - Subroutine to Write End of Report.                  *
      *                                                               *
      *  Called By: SRPROC                                            *
      *  Calls:     None.                                             *
      *****************************************************************
      *
     C           WP1END    BEGSR
      *
      ** Check that sufficient lines remain for the Format. If not,
      ** write out the Main Headings on a new page.
      *
     C                     Z-ADD4         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ** Write out Total Format.
      *
     C                     WRITETRAILP1
      *
     C                     CLOSEXX202P1
     C                     MOVE 'N'       WOPNP1
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRAUDT
      *****************************************************************
      *  SRAUDT - Subroutine to Output Audit report and End Program.  *
      *                                                               *
      *  Called By: Main processing, SRPROC, *PSSR                    *
      *  Calls:     RCFSU   - RCF processing for audit report         *
      *****************************************************************
      *
     C           SRAUDT    BEGSR
      *
      ** Output Report Header and File Controls.
      *
     C                     WRITEHEADAU
     C                     WRITECONTROL
      *
      ** If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
     C                     WRITEDBERROR
     C                     ELSE
      *
      ** Or, if no records read, Output "No Details" message.
      *
     C           RCOUNT    IFEQ 0
     C                     WRITENODTLS
     C                     ENDIF
     C                     ENDIF
      *
      ** End Program and Return.
      *
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFP1
      *****************************************************************
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *  Called By: SRPROC                                            *
      *  Calls:     None.                                             *
      *****************************************************************
      *
     C           RCFP1     BEGSR
      *
      ** Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ     5
     C                     PARM           SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      ** If Error occurs during ZSFILE processing, then return to the
      ** Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFAU
      *****************************************************************
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *                                                               *
      *  Called By: SRINIT                                            *
      *  Calls:     None.                                             *
      *****************************************************************
      *
     C           RCFAU     BEGSR
      *
      ** Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ** If Error occurs during ZSFILE processing, then return to the
      ** Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *  Called By: SRINIT                                            *
      *  Calls:     SRAUDT  - Produce audit report                    *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
      ** Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
     C           *NAMVAR   DEFN LDA       WLDA  256
     C           *LOCK     IN   WLDA
     C                     OUT  WLDA
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     EXSR SRAUDT
     C                     ENDIF
      *
      ** If *PSSR already run, then just end the program here.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
**  CPY@
(c) Misys International Banking Systems Ltd. 2013
