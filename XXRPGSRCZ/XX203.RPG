     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SD - Maintain URS Code')                         *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  XX203 - Midas SD Maintain URS Code                           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Last Amend No. ERZ032   *CREATE   Date 24Jan13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  ERZ032 - Customer Details Additional Information             *
      *                                                               *
      *****************************************************************
      *  Subroutine Index                                             *
      *                                                               *
      *  #IINIT - Initial processing                                  *
      *  #ILDSF - Load subfile                                        *
      *  #ISCRD - Screen display processing                           *
      *  #IINDI - Set screen conditioning indicators                  *
      *  #IEXFM - Display subfile screen                              *
      *  #IRFSH - Refresh screen (F5)                                 *
      *  #IRLUP - Roll up processing (ROLLUP)                         *
      *  #ISFLS - Validate SFLSEL                                     *
      *  #IOPTN - Options processing                                  *
      *  #IADDR - Add/Amend/Enquire processing                        *
      *  #IVALD - Validate user input                                 *
      *  #IDELR - Delete definition                                   *
      *                                                               *
      *  #IMVFF - Move file fields to screen                          *
      *  #IMVFS - Move file fields to subfile                         *
      *  #IMVSC - Move screen fields to file                          *
      *  #IMSGC - Clear message queue                                 *
      *  #ICLRS - Clear subfile                                       *
      *  #ICLR1 - Clear subfile fields   (S1)                         *
      *  #ICLR2 - Clear selection fields (S2)                         *
      *  #IEXIT - Exit program (F3)                                   *
      *  ZASNMS - Error messages processing                           *
      *                                                               *
      *****************************************************************
      * Indicator Summary                                             *
      *                                                               *
      *   03 - Exit program (F3)                                      *
      *   05 - Refresh screen (F5)                                    *
      *   09 - Insert new record (F9)                                 *
      *   12 - Previous screen indicator (F27)                        *
      *   27 - Roll up indicator (ROLLUP)                             *
      *                                                               *
      *   32 - Read to SDURSTV1 - lock file                           *
      *   33 - Read to SDURSTV1 - don't lock file                     *
      *   34 - Readc to sflrcd                                        *
      *                                                               *
      *   41 - Error on field S1URST (URST Code)                      *
      *   42 - Error on field S1DESC (Description)                    *
      *   44 - Error on field/s S1SEL (Selection option)              *
      *                                                               *
      *   50 - Validation Check (ON if error)                         *
      *   51 - ERRMSGID                                               *
      *                                                               *
      *   60 - Display subfile                                        *
      *   61 - Display subfile control                                *
      *   62 - End of subfile                                         *
      *                                                               *
      *   70 - Enquiry Mode                                           *
      *   71 - Protect key field                                      *
      *   72 - Protect non-key fields                                 *
      *   73 - Protect selection fields (S2)                          *
      *   74 - SFLNXTCHG                                              *
      *   75 - F10 text display - confirm delete                      *
      *                                                               *
      *U7-U8 - General Error Indicators                               *
      *   LR - Last Record Indicator                                  *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
     FXX203DF CF  E                    WORKSTN                        UC
     F                                        UIRRN KSFILE #SFLRCD
      ** Display file
      *
     FSDURSTV1UF  E           K        DISK         KCOMIT       A    UC
      ** URST Code  LF
      ** format: SDURSTP0
      *
      *****************************************************************
      /SPACE
      *****************************************************************
      *
     E                    CPY@    1   1 80
      ** Array containing Copyright statement.
      *
      *
     IPSDS       SDS
      ** Program Status Data Structure
     I                                     *PROGRAM PGM
     I                                      244 253 JOB
     I                                      254 263 USER
     ILDA       E DSLDA                         256
      ** Local data area for data base errors.
      *
     IDSFDY     E DSDSFDY
      ** Data structure used for access objects
      *
     IRUNDAT    E DSRUNDAT
      ** Data Area giving Installation Control Details
      *
     I            DS
     I              '0123456789ABCDEFGHIJ-C         ALPHA
     I              'KLMNOPQRSTUVWXYZ '
      ** Named constant to check URST Code is alphanumeric
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            MAIN     - Control Processing.                     *
      *            ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~                     *
      * CALLS      #IINIT  -  Initial Processing.                     *
      *            #IINZS  -  Initialise subfile.                     *
      *            #ISCRD  -  Screen Display Processing.              *
      *            #IEXIT  -  Exit program.                           *
      *                                                               *
      * EMODE is either 'E' if program is called from Enquiry menu or *
      * 'M' if called from Maintenance.                               *
      *                                                               *
      *****************************************************************
     C           *ENTRY    PLIST
     C                     PARM           EMODE   1
      *****************************************************************
      *
      ** Initial processing do first time through only
     C           DONE      IFEQ *BLANK
     C                     EXSR #IINIT
     C                     ENDIF
      *
      ** Initialisation of and loading of subfile.
     C                     EXSR #IINZS
      *
      ** Process screen display and user input.
     C                     EXSR #ISCRD
      *
      ** End program if F3 pressed.
     C           *IN03     IFEQ *ON
     C                     EXSR #IEXIT
     C                     ENDIF
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            #IINIT   - Initial Processing.                     *
      *            ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~                     *
      *                                                               *
      * CALLED BY         Main Processing                             *
      *                                                               *
      *****************************************************************
     C           #IINIT    BEGSR
      *
      ** Set on Initialisation Done flag
     C                     MOVE 'Y'       DONE    1
      *
      ** Open files
     C                     OPEN XX203DF
     C                     OPEN SDURSTV1
      *
      ** Set up copyright parameter
     C                     MOVEACPY@      CPY2@  80
     C                     MOVE PGM       ZAPGM  10
     C                     MOVE PGM       ##PGM
      *
      ** Initialise subfile page
     C                     Z-ADD12        UISFPG  30       SFLPAG
      *
      ** Define Local Data Area for error handling, set rundate
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
     C                     MOVE AGMRDT    ##RUN
      *
     C           *LIKE     DEFN S1URST    WURST
     C           *LIKE     DEFN S1DESC    WDESC
      *
      ** Initialise work fields
      *                    Reload Subfile flag
     C                     MOVE *BLANKS   W0RSF   1
      *
      *                    Relative record number
     C                     Z-ADD0         UIRRN   50
      *
      *                    Store for highest record number processed
     C                     Z-ADD0         UIRNO   50
      *
      *                    Store for position of cursor
     C                     Z-ADD0         CURSOR  50
      *
      *                    Loop counter
     C                     Z-ADD0         COUNT   50
      *
      *                    Work field for CHECK result
     C                     Z-ADD0         RESULT  10
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            #IINZS   - Initialise subfile.                     *
      *            ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~                     *
      * CALLS      ZASNMS   - Error message handling.                 *
      *            #ILDSF   - Load subfile.                           *
      *            #ICLRS   - Clear subfile.                          *
      *                                                               *
      * CALLED BY             Main processing.                        *
      *            #ISCRD   - Screen display processing.              *
      *                                                               *
      *****************************************************************
     C           #IINZS    BEGSR
      *
      ** Initialise cursor, relative record number, & record count
     C                     Z-ADD1         UISFRN
     C                     Z-ADD0         UIRRN
     C                     Z-ADD0         UIRNO
      *
      ** Clear the subfile before reloading
     C                     EXSR #ICLRS
      *
      ** Set up DBF file key list
     C           KEY       KLIST
     C                     KFLD           URST             URST Code
      *
      ** If Selection fields are enabled - check if record selected
      ** and read that record.
     C                     MOVELS2URST    URST
     C           KEY       SETLLSDURSTV1
     C                     READ SDURSTV1            N  8733 37=error
      *
      ** Save previous selector values - this enables a check to see if
      ** new selection criteria have been entered
     C           *LIKE     DEFN S2URST    WZURST
     C                     MOVELS2URST    WZURST           URST Code
     C           *LIKE     DEFN S2DESC    WZDESC
     C                     MOVELS2DESC    WZDESC           Description
     C           *LIKE     DEFN S2DESC    WQDESC           Description of
      *
      ** load subfile
     C           *IN33     IFEQ *OFF
     C                     EXSR #ILDSF
     C                     ENDIF
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *             ILDSF   - Load the subfile.                       *
      *            ~~~~~~~~~~~~~~~~~~~~~~~~~~~~                       *
      * CALLS      #ICLR1 - Clear subfile fields.                     *
      *            #IMVFS - Move file fields to subfile.              *
      *                                                               *
      * CALLED BY  #IINZS - Initialise subfile.                       *
      *            #IRFSH - Refresh screen.                           *
      *            #IRLUP - Next page processing.                     *
      *                                                               *
      *****************************************************************
     C           #ILDSF    BEGSR
      *
      ** Start loop counter
     C                     Z-ADD1         COUNT
      *
      ** Save position for the cursor
     C           UIRRN     ADD  1         CURSOR
      *
      ** Load subfile until a subfile page is filled or the
      ** the end of the file is reached.
     C           *IN33     DOWEQ*OFF
     C           COUNT     ANDLEUISFPG
      *
      ** Clear the subfile screen fields before reloading.
     C                     EXSR #ICLR1
      *
      ** Check whether selection fields are in use
     C           S2DESC    IFNE *BLANK                     Description
     C                     CALL 'QCLSCAN'
     C                     PARM           URSDES           Description
     C                     PARM 20        WQA3N   30       Length
     C                     PARM 1         WQB3N   30       Start
     C                     PARM S2DESC    WQDESC           Mask
     C                     PARM 20        WQC3N   30       Length
     C                     PARM '1'       WQD1    1        Translate
     C                     PARM '1'       WQE1    1        Wild
     C                     PARM '?'       WQF1    1        Result
     C                     PARM           WQG3N   30
     C           WQG3N     CABLT1         TAG01            go to
     C                     ENDIF
      *
      ** Move the fields from the file to the screen.
     C                     EXSR #IMVFS
      *
      ** Increase the loop counter and relative record number by 1
     C                     ADD  1         COUNT
     C                     ADD  1         UIRRN
      *
      ** Write record
     C                     WRITE#SFLRCD
      *
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     C           TAG01     TAG
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      ** Read file SDURSTV1 and continue loading the subfile.
     C                     READ SDURSTV1            N    33
     C                     ENDDO
      *
      ** If the relative record number is greater than 0, set ON SFLDSP
      ** indicator.
     C                     MOVE *OFF      *IN60
     C           UIRRN     IFGT 0
     C                     MOVE *ON       *IN60
     C                     ENDIF
      *
      ** If there are records loaded set the cursor position
     C           COUNT     IFGT 1
     C                     Z-ADDCURSOR    UISFRN
     C                     ENDIF
      *
      ** Save the last record that has been loaded in the subfile
     C                     MOVE UIRRN     UIRNO
      *
      ** If all records have been read, switch off More at SFLEND
     C           *IN33     IFEQ *ON
     C                     MOVE *ON       *IN62            'bottom'
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            #ISCRD   - Display screen with subfile.            *
      *            ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~            *
      * CALLS      #IMSGC   - Clear message queue.                    *
      *            #IEXFM   - Display subfile screen.                 *
      *            #IEXIT   - Exit program.                           *
      *            #IRFSH   - Refresh screen.                         *
      *            #IADDR   - Add a new definition.                   *
      *            #IRLUP   - Roll up request.                        *
      *            #IOPTN   - Options screen.                         *
      *            #IINZS   - Initialise subfile.                     *
      *                                                               *
      * CALLED BY             Main processing.                        *
      *                                                               *
      *****************************************************************
     C           #ISCRD    BEGSR
      *
     C                     MOVEL'N'       W0RSF
      *
      ** Display screen until F3 is pressed or reload requested
     C           *IN03     DOWEQ*OFF
     C           W0RSF     ANDEQ'N'
      *
      ** If subfile empty (ie if no records fulfill selection criteria)
      *  ~~~~~~~~~~~~~~~~
     C           *IN33     IFEQ *ON
     C           UIRNO     ANDEQ0
      *
      ** Send message
      *                    'No data to display'
     C                     MOVEL'XXX1011' ZAMSID
     C                     MOVEL'XXUSRMSG'ZAMSGF
     C                     EXSR ZASNMS
     C                     MOVEA'010'     *IN,60
     C                     ENDIF
      *
      ** Subfile loaded.
      *  ~~~~~~~~~~~~~~~
      *
      ** Initialise error indicators.
     C                     MOVEA'00000'   *IN,41
     C                     MOVE *OFF      *IN50
      *
      ** Display screen
     C                     EXSR #IEXFM
      *
      ** Reset 'No more records in file' indicator.
     C                     MOVE *OFF      *IN51
      *
      ** Clear messages from program message queue
     C                     EXSR #IMSGC
      *
      ** Process the selection first before any other actions
      ** unless F3 or F5 was requested. Therefore, if selection, treat
      ** as if <ENTER> was pressed by user (i.e. turn off indicators
      ** for function keys that might have been set on like *IN09).
     C           *IN03     IFEQ *OFF
     C           *IN05     ANDEQ*OFF
     C           S2URST    IFNE WZURST
     C           S2DESC    ORNE WZDESC
     C                     MOVE S2URST    WZURST
     C                     MOVE S2DESC    WZDESC
     C                     EXSR #IRFSH
     C                     MOVE *OFF      *IN09
     C                     ITER
     C                     ENDIF
     C                     ENDIF
      *
     C                     SELEC
      *
      ** If F3 has been pressed exit program
     C           *IN03     WHEQ *ON
     C                     EXSR #IEXIT
      *
      ** If F5 has been pressed refresh screen (if Maintenance)
     C           *IN05     WHEQ *ON
     C           EMODE     IFEQ 'M'
     C                     EXSR #IRFSH
     C                     ENDIF
      *
      ** If F9 has been pressed allow user to add a record (if Maintennce)
     C           *IN09     WHEQ *ON
     C           EMODE     IFEQ 'M'
     C                     EXSR #IADDR
     C                     EXSR #ICLRS
     C                     ENDIF
      *
      ** If Roll-up is pressed, display next subfile page.
     C           *IN27     WHEQ *ON
     C                     EXSR #IRLUP
      *
      ** Otherwise validate SFLSEL then process options
     C                     OTHER
     C           UIRNO     IFGT 0
     C                     EXSR #ISFLS
     C                     ENDIF
      *
     C                     ENDSL
      *
     C                     ENDDO
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            #IINDI   - Set Screen Indicators                   *
      *            ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~                   *
      *                                                               *
      * CALLED BY  #IADDR -  Add/Amend/Enquire                        *
      *            #ISCRD -  Screen display processing                *
      *            #IDELR -  Delete a record                          *
      *            #IEXFM -  Screen display                           *
      *                                                               *
      *****************************************************************
     C           #IINDI    BEGSR
      *
     C                     MOVEA'000000'  *IN,70
      *
      ** Enquire Program
      *  ~~~~~~~~~~~~~~~
     C           EMODE     IFEQ 'E'
     C           UIOPT     IFEQ 'E'
     C                     MOVEA'111100'  *IN,70
     C                     ELSE
     C                     MOVEA'111000'  *IN,70
     C                     ENDIF
     C                     ELSE
      *
      ** Maintenance Program
      *  ~~~~~~~~~~~~~~~~~~~
     C                     SELEC
      *
      ** First Maintenance Screen
     C           UIOPT     WHEQ *BLANKS
     C           *IN09     ANDEQ*OFF
     C                     MOVEA'011000'  *IN,70
      *
      ** Enquire
     C           UIOPT     WHEQ 'E'
     C                     MOVEA'111100'  *IN,70
      *
      ** Amend
     C           UIOPT     WHEQ 'A'
     C                     MOVEA'010100'  *IN,70
      *
      ** Delete
     C           UIOPT     WHEQ 'D'
     C                     MOVEA'011101'  *IN,70
      *
      ** Add
     C           *IN09     WHEQ *ON
     C                     MOVEA'000100'  *IN,70
      *
     C                     ENDSL
     C                     ENDIF
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            #IEXFM   - Display subfile screen.                 *
      *            ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~                 *
      *                                                               *
      * CALLED BY  #ISCRD   - Screen display.                         *
      *                                                               *
      *****************************************************************
     C           #IEXFM    BEGSR
      *
     C                     EXSR #IINDI
     C                     MOVE *ON       *IN86
     C                     WRITEFOOTER1
     C                     WRITE#MSGCTL
     C                     EXFMT#SFLCTL
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            #IRLUP   - Roll-up request processing.             *
      *            ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~             *
      *                                                               *
      * CALLS      #ILDSF -  Load subfile.                            *
      *                                                               *
      * CALLED BY  #ISCRD -  Screen display.                          *
      *                                                               *
      *****************************************************************
     C           #IRLUP    BEGSR
      *
      ** If relative record number is less than highest saved value
      ** restore RRN to highest saved value
     C           UIRRN     IFLT UIRNO
     C                     MOVE UIRNO     UIRRN
     C                     ENDIF
      *
      ** Only allow roll up if more records available
     C           *IN33     IFEQ *ON
     C                     MOVE *ON       *IN51            'no records..'
     C                     MOVE *ON       *IN62            'bottom'
     C                     ELSE
      *
      ** Build subfile page.
     C                     EXSR #ILDSF
      *
     C                     ENDIF
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            #IRFSH   - Refresh screen.                         *
      *            ~~~~~~~~~~~~~~~~~~~~~~~~~~                         *
      *                                                               *
      * CALLS      #ICLRS   - Clear subfile.                          *
      *            #ILDSF   - Load subfile.                           *
      *                                                               *
      * CALLED BY  #ISCRD   - Screen display.                         *
      *            #IOPTN   - Options processing                      *
      *                                                               *
      *****************************************************************
     C           #IRFSH    BEGSR
      *
      ** Clear Option field.
     C           *IN05     IFEQ *ON
     C                     MOVE *BLANKS   S1SEL
     C                     EXSR #ICLR2
     C                     ENDIF
      *
      ** Clear message file
     C                     EXSR #IMSGC
      *
      ** Clear subfile.
     C                     EXSR #ICLRS
      *
      ** Set file back to the beginning and load the screen from start
      ** if F5 was pressed, else start as user has requested.
     C           S2URST    IFEQ *BLANKS
     C           *LOVAL    SETLLSDURSTV1
     C                     ELSE
     C           S2URST    SETLLSDURSTV1
     C                     ENDIF
     C                     READ SDURSTV1            N    33
      *
      ** Build subfile page.
     C                     Z-ADD1         UISFRN
     C                     Z-ADD0         UIRRN
     C                     Z-ADD0         UIRNO
     C                     EXSR #ILDSF
      *
     C                     MOVEL'N'       W0RSF            sfl.req.relo
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            #ISFLS   - Validate SFLSEL                         *
      *            ~~~~~~~~~~~~~~~~~~~~~~~~~~                         *
      *                                                               *
      * CALLS      #IOPTN   - Option Processing                       *
      *                                                               *
      * CALLED BY  #ISCRD   - Screen display.                         *
      *                                                               *
      *****************************************************************
     C           #ISFLS    BEGSR
      *
      ** Initialise general error indicator
     C                     MOVE *OFF      *IN50
      *
      ** Read changed subfile record
     C                     READC#SFLRCD                  34
      *
      ** Validate S1SEL
     C           *IN34     DOWEQ*OFF
     C                     MOVE *OFF      *IN44
     C                     SELEC
      *
     C           S1SEL     WHEQ ' '
      *
     C           S1SEL     WHEQ 'D'
     C           EMODE     ANDEQ'M'
      *
      ** Check for record lock
     C           S1URST    CHAINSDURSTV1             3231
     C           *IN31     IFEQ *ON
     C                     MOVE *ON       *IN44            error SFLSEL
     C                     MOVE *ON       *IN50            general erro
     C                     ELSE
     C           *IN32     IFEQ *ON
     C                     MOVE *ON       *IN44            error SFLSEL
     C                     MOVE *ON       *IN50            general erro
     C                     MOVEL'XXX1018' ZAMSID
     C                     MOVEL'XXUSRMSG'ZAMSGF
     C                     EXSR ZASNMS
     C                     ENDIF
     C                     ENDIF
      *
     C                     OTHER
     C                     MOVE *ON       *IN44            error SFLSEL
     C                     MOVE *ON       *IN50            general erro
      *                    '&1 is not a valid option'
     C                     MOVEL'XXX1009' ZAMSID
     C                     MOVELS1SEL     ZAMSDA
     C                     MOVEL'XXUSRMSG'ZAMSGF
     C                     EXSR ZASNMS
     C                     ENDSL
     C                     MOVE *ON       *IN74            SFLNXTCHG
     C                     UPDAT#SFLRCD
     C                     READC#SFLRCD                  34
     C                     ENDDO
     C                     MOVE *OFF      *IN74
      *
     C           *IN50     IFEQ *OFF
     C                     EXSR #IOPTN
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            #IOPTN   - Options processing.                     *
      *            ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~                     *
      *                                                               *
      * CALLS      #IDELR   - Delete a record.                        *
      *            ZASNMS   - Error handling processing.              *
      *                                                               *
      * CALLED BY  #ISCRD   - Screen display.                         *
      *                                                               *
      *****************************************************************
     C           #IOPTN    BEGSR
      *
      ** Read changed subfile record
     C                     READC#SFLRCD                  34
      *
      ** Save option and target filename of changed record.
     C           *IN34     DOWEQ*OFF
     C           *IN03     ANDEQ*OFF
     C           *IN12     ANDEQ*OFF
      *
     C                     MOVE S1SEL     UIOPT   1
     C                     MOVE S1URST    UIURST  2
     C                     MOVE *OFF      *IN12
      *
     C                     SELEC
      ** Enquire Program:
      *  ~~~~~~~~~~~~~~~~
     C           UIOPT     WHEQ 'E'
     C                     EXSR #IADDR
      *
      ** Maintenance Program:
      *  ~~~~~~~~~~~~~~~~~~~~
      ** If delete record
     C           UIOPT     WHEQ 'D'
     C           EMODE     ANDEQ'M'
     C                     EXSR #IDELR
      *
      ** If amend record
     C           UIOPT     WHEQ 'A'
     C           EMODE     ANDEQ'M'
     C                     EXSR #IADDR
     C                     ENDSL
      *
      ** Read changed subfile record
     C                     READC#SFLRCD                  34
     C                     ENDDO
      *
     C                     EXSR #IRFSH
      *
      ** Initialise workfields.
     C                     MOVE *BLANKS   UIOPT
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            #IADDR   - Add/Amend/Enquire processing            *
      *            ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~            *
      *                                                               *
      * CALLS      #IVALD - Validate user input.                      *
      *            #IMVSC - Move screen fields to file.               *
      *                                                               *
      * CALLED BY  #ISCRD - Screen display processing.                *
      *                                                               *
      *****************************************************************
     C           #IADDR    BEGSR
      *
      ** Enquiry Program:
      *  ~~~~~~~~~~~~~~~~
     C           EMODE     IFEQ 'E'
      *
      ** Set screen indicators then display second screen
     C                     EXSR #IINDI
     C                     WRITEFOOTER2
     C                     WRITE#MSGCTL
     C                     EXFMTXX203F1
     C                     ENDIF
      *
      ** Maintenance Program:
      *  ~~~~~~~~~~~~~~~~~~~~
     C           EMODE     IFEQ 'M'
      *
     C                     SELEC
     C           *IN09     WHEQ *ON
     C                     MOVE *BLANKS   S1URST
     C                     MOVE *BLANKS   S1DESC
     C                     ENDSL
      *
      ** Display second screen to add the data for the new record
      ** until F12 or F3 is pressed, or a record is successfully
      ** added.
     C                     MOVELS1DESC    WDESC
     C           *IN03     DOUEQ*ON
     C           *IN12     OREQ *ON
      *
     C                     EXSR #IINDI
     C                     WRITEFOOTER2
     C                     WRITE#MSGCTL
     C                     EXFMTXX203F1
     C                     EXSR #IMSGC
      *
     C                     SELEC
     C           *IN03     WHEQ *ON
     C           *IN12     OREQ *ON
     C           S1SEL     OREQ 'E'
     C                     LEAVE
      *
     C           *IN10     WHEQ *ON
      *
      ** Validate user input
     C                     EXSR #IVALD
      *
      ** If the user input is valid, write the record to the file
      ** and exit the loop - returning to first screen
     C           *IN50     IFEQ *OFF
      *
     C           *IN09     IFEQ *ON
     C                     EXSR #IMVSC
     C                     WRITESDURSTP0
     C                     COMIT
     C                     LEAVE
     C                     ELSE
      *
      ** Record no longer exists or may have been updated by another
      ** user.
     C           S1URST    CHAINSDURSTV1             32
     C           *IN32     IFEQ *ON
     C           S1SEL     ANDEQ'A'
     C           WDESC     ORNE URSDES
     C                     MOVEL'XXX1018' ZAMSID
     C                     MOVEL'XXUSRMSG'ZAMSGF
     C                     EXSR ZASNMS
     C                     ITER
     C                     ENDIF
      *
     C                     MOVE S1DESC    URSDES
     C                     UPDATSDURSTP0
     C                     COMIT
     C                     LEAVE
     C                     ENDIF
     C                     ENDIF
      *
     C                     OTHER
      *
      ** Validate user input
     C                     EXSR #IVALD
      *
      ** If the user input is valid
     C           *IN50     IFEQ *OFF
      *
      ** ENTER: Press F10 to confirm
     C                     MOVEL'XXX1024' ZAMSID
     C                     MOVEL'XXUSRMSG'ZAMSGF
     C                     EXSR ZASNMS
     C                     ENDIF
      *
     C                     ENDSL
      *
     C                     ENDDO
     C                     ENDIF
     C                     MOVE 'Y'       W0RSF
     C                     MOVE *OFF      *IN09
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            #IVALD   - Validate entries of new                 *
      *            ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~                *
      *                                                               *
      * CALLS      ZASNMS   - Error message processing.               *
      *            #IMSGC   - Clear messages from message queue.      *
      *                                                               *
      * CALLED BY  #IADDR   - Add a new definition.                   *
      *                                                               *
      *****************************************************************
     C           #IVALD    BEGSR
      *
      *
      ** Initialise error indicators.
     C                     MOVEA'0000'    *IN,41
     C                     MOVE *OFF      *IN50
      *
      ** URS Code - non-amendable, mandatory, 2A
      *  ~~~~~~~~
      ** field only input capable in Insert mode
     C           *IN09     IFEQ *ON
      *
      ** cannot be *BLANKS
     C           S1URST    IFEQ *BLANKS
     C                     MOVE *ON       *IN50            format error
     C                     MOVE *ON       *IN41            URST invali
      *                    Send message '*Value required'
     C                     MOVEL'Y2U0001' ZAMSID
     C                     MOVEL'Y2USRMSG'ZAMSGF
     C                     EXSR ZASNMS
     C                     ELSE
      *
      ** must be alphanumeric
      ** Alpha = ' 0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ'
     C                     Z-ADD0         RESULT
     C           ALPHA     CHECKS1URST    RESULT
     C           RESULT    IFNE *ZERO
     C                     MOVE *ON       *IN50            format error
     C                     MOVE *ON       *IN41            URSTinvalid
      *                    '&1 is not alphanumeric'
     C                     MOVELS1URST    ZAMSDA
     C                     MOVEL'XXX1012' ZAMSID
     C                     MOVEL'XXUSRMSG'ZAMSGF
     C                     EXSR ZASNMS
     C                     ENDIF
      *
      ** Cannot insert a URS Code  that already exists
     C           S1URST    CHAINSDURSTV1            N32
     C           *IN32     IFEQ *OFF
     C                     MOVE *ON       *IN50
     C                     MOVE *ON       *IN41
      *                    'URST Code &1 already exists'
     C                     MOVEL'XXX1020' ZAMSID
     C                     MOVELS1URST    ZAMSDA
     C                     MOVEL'XXUSRMSG'ZAMSGF
     C                     EXSR ZASNMS
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
      ** URST Code  Description - mandatory, 20A
      *  ~~~~~~~~~~~~~~~~~~~~~~~~
      ** cannot be *BLANKS
     C           S1DESC    IFEQ *BLANKS
     C                     MOVE *ON       *IN50            format error
     C                     MOVE *ON       *IN42            DESC invali
      *                    Send message '*Value required'
     C                     MOVEL'Y2U0001' ZAMSID
     C                     MOVEL'Y2USRMSG'ZAMSGF
     C                     EXSR ZASNMS
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            #IDELR   - Delete a record.                        *
      *            ~~~~~~~~~~~~~~~~~~~~~~~~~~~                        *
      *                                                               *
      * CALLS      #IMVFF   - Move file field names to screen.        *
      *            #IINZS   - Initialise subfile.                     *
      *                                                               *
      * CALLED BY  #IOPTN   - Options processing.                     *
      *                                                               *
      *****************************************************************
     C           #IDELR    BEGSR
      *
      ** Set screen indicators
     C                     EXSR #IINDI
      *
      ** Clear message queue
     C                     EXSR #IMSGC
      *
      ** Get data from file for the specified record.
     C                     EXSR #IMVFF
      *
      ** Display second screen with record details.
     C           *IN12     DOUEQ*ON
     C           *IN10     OREQ *ON
     C           *IN03     OREQ *ON
     C                     WRITEFOOTER2
     C                     WRITE#MSGCTL
     C                     EXFMTXX203F1
     C                     EXSR #IMSGC
      *
      ** If F10 is pressed delete record.
     C           *IN10     IFEQ *ON
     C                     DELETSDURSTP0
     C                     COMIT
      *
      ** If there are no more records in the file reset RRN
     C           *LOVAL    SETLLSDURSTV1
     C                     READ SDURSTV1            N    33
     C           *IN33     IFEQ *ON
     C                     Z-ADD1         UIRNO
     C                     MOVE 'Y'       W0RSF
     C                     ENDIF
     C                     LEAVE
     C                     ENDIF
      *
      ** Inform user of the valid actions, if <enter> was pressed.
     C           *IN03     IFEQ *OFF
     C           *IN10     ANDEQ*OFF
     C           *IN12     ANDEQ*OFF
      *
      ** ENTER: Press F10 to confirm
     C                     MOVEL'XXX1024' ZAMSID
     C                     MOVEL'XXUSRMSG'ZAMSGF
     C                     EXSR ZASNMS
      *
     C                     ENDIF
      *
     C                     ENDDO
      *
      ** If F3 pressed quit.
     C           *IN03     IFEQ *ON
     C                     EXSR #IEXIT
     C                     ENDIF
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            #IMVFF   - Move file fields to screen.             *
      *            ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~             *
      *                                                               *
      * CALLED BY  #IDELR   - Delete processing.                      *
      *                                                               *
      *****************************************************************
     C           #IMVFF    BEGSR
      *
      ** Get data for the specified record.
     C           UIURST    CHAINSDURSTV1             32
     C           *IN32     IFEQ *OFF
     C                     MOVELURST      S1URST
     C                     MOVELURSDES    S1DESC
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            #IMVFS   - Move file fields to subfile.            *
      *            ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~            *
      *                                                               *
      * CALLED BY  #ILDSF   - Screen display.                         *
      *                                                               *
      *****************************************************************
     C           #IMVFS    BEGSR
      *
     C                     MOVELURST      S1URST
     C                     MOVELURSDES    S1DESC
      *
      ** Get data for the specified record.
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            #IMVSC   - Move screen fields to file.             *
      *            ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~             *
      *                                                               *
      * CALLED BY  #IADDR   - Insert processing.                      *
      *                                                               *
      *****************************************************************
     C           #IMVSC    BEGSR
      *
     C                     MOVELS1URST    URST
     C                     MOVELS1DESC    URSDES
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            #ICLRS   - Clear the subfile.                      *
      *            ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~                      *
      *                                                               *
      * CALLED BY  #IRFSH   - Refresh screen.                         *
      *            #IINZS   - Initialise subfile.                     *
      *                                                               *
      *****************************************************************
     C           #ICLRS    BEGSR
      *
      ** Clear the subfile.
     C                     MOVEA'000'     *IN,60
     C                     WRITE#SFLCTL
     C                     MOVEA'110'     *IN,60
      *
      ** If no more records in the file, end of subfile
     C           *IN33     IFEQ *ON
     C                     MOVEA'010'     *IN,60
     C                     ENDIF
      *
      ** Reset Relative Record Number
     C                     Z-ADD0         UIRRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            #ICLR1   - Screen subfile fields clear.            *
      *            ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~            *
      *                                                               *
      * CALLED BY  #ILDSF   - Load subfile.                           *
      *                                                               *
      *****************************************************************
     C           #ICLR1    BEGSR
      *
     C                     MOVE *BLANKS   S1URST
     C                     MOVE *BLANKS   S1DESC
     C                     MOVE *BLANKS   S1SEL
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            #ICLR2   - Clear the selection fields              *
      *            ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~              *
      *                                                               *
      * CALLED BY  #IRFSH   - Refresh screen.                         *
      *                                                               *
      *****************************************************************
     C           #ICLR2    BEGSR
      *
     C                     MOVEL*BLANK    S2URST           URS Code
     C                     MOVEL*BLANK    S2DESC           Description
     C                     MOVEL*BLANK    WZURST           URS Code
     C                     MOVEL*BLANK    WZDESC           Description
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            #IMSGC - Clear messages from pgm message queue     *
      *            ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~     *
      *                                                               *
      * Called by    #ISCRD - Screen display.                         *
      *              #IVALD - Validation processing.                  *
      *                                                               *
      *****************************************************************
     C           #IMSGC    BEGSR
      *
      ** Clear messages from program message queue
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            #IEXIT   - Exit the program.                       *
      *            ~~~~~~~~~~~~~~~~~~~~~~~~~~~~                       *
      *                                                               *
      * CALLED BY            Main processing.                         *
      *            #ISCRD -  Screen display processing.               *
      *                                                               *
      *****************************************************************
     C           #IEXIT    BEGSR
      *
      ** Close files
     C                     CLOSEXX203DF
     C                     CLOSESDURSTV1
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      * ZASNMS    - Send program error messages                       *
      *                                                               *
      * Calls                                                         *
      *                                                               *
      * Called by - #IINZS                                            *
      *             #IOPTN                                            *
      *             #IVALD                                            *
      *                                                               *
      *****************************************************************
     C           ZASNMS    BEGSR
      *
     C           ZAPGM     IFEQ *BLANK
     C                     MOVEL##PGM     ZAPGM
     C                     ENDIF
      *
      ** If no message file specified, use default
     C           ZAMSGF    IFEQ *BLANK
     C                     MOVEL'XXUSRMSG'ZAMSGF
     C                     ENDIF
      *
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGM  10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message ID
     C                     PARM           ZAMSGF 10        Message file
     C                     PARM           ZAMSDA132        Message data
     C                     PARM           ZAMSTP  7        Message type
      *
      * Clear all fields for default mechanism next time
     C                     MOVEL*BLANK    ZAPGM
     C                     MOVEL*BLANK    ZAPGRL
     C                     MOVEL*BLANK    ZAMSID
     C                     MOVEL*BLANK    ZAMSGF
     C                     MOVEL*BLANK    ZAMSDA
     C                     MOVEL*BLANK    ZAMSTP
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     SETON                     U7U8LR
     C                     DUMP
      *
     C                     CALL 'DBERRCTL'
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2013
