     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('AO GCMS ICD details access program')                   *
      *****************************************************************
      *                                                               *
      *  Midas - Access Objects                                       *
      *                                                               *
      ***AOGCMSR0*-*Read*access*to*GCMS*ICD*details********************                       JMI113
      *  XXGCMSR0 - Read access to GCMS ICD details                   *                       JMI113
      *                                                               *
      ***(c)*Misys*International*Banking*Systems*Ltd.*2007*************                       JMI113
      *  (c) Finastra International Limited 2018                      *                       JMI113
      *                                                               *
      *  Last Amend No. JMI113             Date 16May18               *
      *  Prev Amend No. JMI019             Date 21Feb07               *
      *                 JMI019B            Date 28Apr05               *
      *                 JMI019  *CREATE    Date 10Jun03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  JMI113 - Midas GCMS Interface. Upgrade to FBM 2.1            *
      *  JMI019 - Upgrade as is                                       *
      *  JMI019 - Phase III MT999 Message Processing                  *
      *           Recompile due to changes in SDGCMSPD                *
      *  JMI019B - GCMS. File Name and Confirmation File              *
      *            Recompile due to changes in SDGCMSPD               *
      *  JMI019 - Midas-GCMS Interface                                *
      *                                                               *
      *****************************************************************
      *                                                               *
      * ID F  C  H  L  FUNCTION OF INDICATORS                         *
      *                                                               *
      *       01       Off on the first run                           *
      *       90       File error                                     *
      *       91       Open error                                     *
      *       92       Close error                                    *
      *       93       Empty file                                     *
      *       96       Option error                                   *
      *                                                               *
      *                                                               *
      /EJECT
     F********************************************************************
     F*FSTART
     F*
     F**********SDGCMSL1IF  E           K        DISK                           UC            JMI113
     FXXGCMSL1IF  E           K        DISK                           UC                      JMI113
     F*
      /EJECT
      ************************************************************
     E*ESTART
     E*
     E                    @MG     1   1 50
     E*
     E** Array containing Copyright statement
     E*
     E                    CPY@    1   1 80
     E*
      ************************************************************
      /EJECT
     I*ISTART
      ************************************************************
     I*
     IP@MGDA      DS
     I*
     I                                        1  50 @MG
     I*
     IPGMDS      SDS
     I*
     I** Program Status data structure
     I*
     I                                        1  10 PGNAME
     I                                       81  90 PGLIBR
     I                                      244 253 PGJBNM
     I                                      254 263 PGUSER
     I                                      264 2690PGJBNO
     I*
     I**********D@FMT     E DSSDGCMSPD                                                        JMI113
     ID@FMT     E DSXXGCMSPD                                                                  JMI113
     I*
     I** GCMS Record Format Data Structure
     I*
     IP@FMT     E DSDSSDY
     I*
     I** External Data structure to hold the outgoing record format
     I** of the file.
     I*
     IA@CPY       DS
     I                                        1  80 CPY@
     I*
     I** Copyright Array
     I*
      *
      ************************************************************
      /EJECT
      *****************************************************
      *
     C           *ENTRY    PLIST
     C           W@RTCD    PARM           P@RTCD  7        B:Return code
     C                     PARM           P@OPTN  7        I:Option
     C                     PARM D@FMT     P@FMT            O:Format
      *****************************************************
      *
     C                     SETOF                     909192 ¦ Reset
     C                     SETOF                     9396   ¦ error
     C           P@OPTN    IFEQ '*KEY   '
     C           P@OPTN    OREQ '*FIRST '
     C           P@OPTN    OREQ '*LAST  '
     C           P@OPTN    OREQ '*NEXT  '
     C           P@OPTN    OREQ '*PREV  '
     C           P@OPTN    OREQ '*FREE  '
     C           P@OPTN    OREQ '*VERIFY'
     C           P@OPTN    OREQ '*SAME  '
     C           *IN01     IFEQ '0'                          IF2
     C                     SETON                     01
     C**********           OPEN SDGCMSL1               91    Open file                        JMI113
     C                     OPEN XXGCMSL1               91    Open file                        JMI113
     C           *IN91     CABEQ'1'       T@SKIP         LR  Open error
     C********** *LOVAL    SETLLSDGCMSP0             9390    Set file                         JMI113
     C**N93*****           READ SDGCMSP0                 93                                   JMI113
     C**********           CLOSESDGCMSL1               92    Close file                       JMI113
     C           *LOVAL    SETLLXXGCMSP0             9390    Set file                         JMI113
     C  N93                READ XXGCMSP0                 93                                   JMI113
     C                     CLOSEXXGCMSL1               92    Close file                       JMI113
     C           *IN93     CABEQ'1'       T@SKIP             Empty file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C           *IN92     CABEQ'1'       T@SKIP         LR  Close error
     C                     END                               FI2
     C                     ELSE
     C                     SETON                     96      Parm error
     C                     END
      *
      ** No record found or End of file - return error code :
      *          _____________
     C           T@SKIP    TAG                             +++ SKIP +++
      *          ~~~~~~~~~~~~~
     C                     MOVE *BLANK    P@RTCD           No Errors
     C   90                MOVE '*ERROR ' P@RTCD           File error
     C   91                MOVE '*OPEN  ' P@RTCD           Open error
     C   92                MOVE '*CLOSE ' P@RTCD           Close error
     C   93                MOVE '*EMPTY ' P@RTCD           Empty file
     C   96                MOVE '*OPTION' P@RTCD           No selection
      *
      ** Caller requests message sent to program queue :
      *
     C           W@RTCD    IFEQ '*MSG   '                  IF 9 SND MSG
     C           P@RTCD    ANDNE*BLANK                      AND ERROR
     C   90                MOVE 'ACO1001' P@MGID             File error
     C   91                MOVE 'ACO1002' P@MGID             Open error
     C   92                MOVE 'ACO1003' P@MGID             Close error
     C   93                MOVE 'ACO1004' P@MGID             Empty file
     C   96                MOVE 'ACO1007' P@MGID
     C                     CALL 'ZASNMG'                     <SND MSG>
     C                     PARM *BLANK    W@RTCD  7          B:Return code
     C                     PARM PGNAME    P@PGNM 10          I:Prog name
     C                     PARM '*PRV'    P@DEST  5          I:Destination
     C                     PARM           P@MGID  7          I:Msg ID
     C                     PARM *BLANK    P@MSGF 10          I:Msg file
     C                     PARM *BLANK    P@MGKY  4          O:Msg key
     C                     PARM           P@MGDA 50          I:Msg data
     C                     PARM '*DIAG'   P@MGTP  7          I:Msg type
     C                     END                             FI 9
      *
      ** Caller requests DB error handling :
      *
     C           W@RTCD    IFEQ '*DBERR '                  IF 10
     C           P@RTCD    ANDNE*BLANK                       AND
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     END                             FI 10
      *
      ** Return to caller :
      *
     C                     RETRN
      *
      *****************************************************
      *ENDMAIN
**
XXGCMSPD  XXGCMSR0  GCMS Details
** CPY@
(c) Finastra International Limited 2018
