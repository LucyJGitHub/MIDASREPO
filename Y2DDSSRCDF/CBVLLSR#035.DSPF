      *****************************************************************
      *                                                               *
      *  Midas - (Module Name) Module                                 *
      *                                                               *
      *  DSPF/mmnnnnDF - (display file name)                          *
      *                                                               *
      *  (c) Finastra International Limited 2021                      *
      *                                                               *
      *  Last Amend No. MD056567 *CREATE   Date 20Sep20               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD056567 - Deliverable Data Split for Validation Lists       *
      *                                                               *
      *****************************************************************
      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas Y2 R2.1.1 Values list name search program')
     H*
     H* Copy of Y2VLLSR#1,     change REF file
     H*
     A*================================================================
     A                                      INDARA
     A                                      REF(Y2VLLSL0)
     A                                      PRINT(MPRINT)
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2021')
     A                                      SFL
     A                                      INDTXT(84 'Condition SFLNXTCHG.')
     A                                      INDTXT(31 'Invalid #1SEL ')
     A  84                                  SFLNXTCHG
     A            #1SEL          1   B 13  3CHECK(AB)
     A                                      VALUES('1')
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(RI PC)
     A            #1EXVL    R   20     13  6REFFLD(Y2EXVL)
     A            #1CNTX    R          13 30REFFLD(Y2CNTX)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SFLPAG(09) SFLSIZ(10)
     A                                      BLINK OVERLAY KEEP
     A  25                                  PUTOVR
     A                                      INDTXT(80 'Condition SFLCLR.')
     A                                      INDTXT(81 'Condition SFLDSP.')
     A                                      INDTXT(82 'Condition SFLEND.')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N80 81N25                            SFLDSP
     A N80 81 82                            SFLEND
     A*.COMMAND.KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(01 'Exit processing')
     A                                      HOME(30 'HOME key.')
     A*.SETOFFS.................................................................
     A                                      SETOF(99 'Global error ind.')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##PGM     R        H      REFFLD(##PGM Y2PGDSP)
     A                                  1  2'CBVLLSR1'
     A                                      COLOR(BLU)
     A            #2LSNO    R           1 13REFFLD(Y2LSNO)
     A                                      DSPATR(ND)
     A            ##USR     R           1 40REFFLD(##USR Y2PGDSP)
     A                                      DSPATR(ND)
     A            ##JOB     R           1 51REFFLD(##JOB Y2PGDSP)
     A                                      DSPATR(ND)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                  1 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 30'Display allowed values'
     A                                      DSPATR(HI)
     A            ##CMP         40   O  3 40DSPATR(ND)
     A                                  4  4'Field. . :'
     A            #2EXVL    R           4 18REFFLD(Y2EXVL)
     A                                  5  4'List name . :'
     A            #2CNTX    R           5 18REFFLD(Y2CNTX)
     A            #2STVL    R   20   B  7  4REFFLD(Y2EXVL)
     A                                      CHECK(LC)
     A                                      DSPATR(UL)
     A                                    +1'<== Postion'
     A                                  9  2'Type options, press Enter'
     A                                 10  4'1=Select'
     A  81                             12  2'Opt'
     A                                      DSPATR(HI)
     A  81                             12  6'Value'
     A                                      DSPATR(HI)
     A  81                             12 30'Description'
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #CMDKEYS                  TEXT('Command keys.')
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages.')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages.')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(1) SFLSIZ(15)
     A  25                                  OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================

