/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas ZA Lock/unlock/check object allocation')        */
/*********************************************************************/
/*                                                                   */
/*       Midas - Standard Routines                                   */
/*                                                                   */
/*       ZAC000001 - Lock/Unlock/Check Object Allocation             */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2003           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Last Amend No. CGP002  *CREATE    Date 01Aug03              */
/*       Prev Amend No. Xnnnnn             Date ddmmmyy              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CGP002 - New program.                                       */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&RTNCODE &OBJNAME &OBJTYPE &MODE)
 
             DCL        VAR(&RTNCODE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OBJNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OBJTYPE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MODE) TYPE(*CHAR) LEN(1)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2003')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CHGJOB     SWS(XXXXXX00)
 
/* Allocate the object exclusively */
/* (Deallocate the object first to avoid duplicate locks) */
 
             IF         COND(&MODE *EQ 'A') THEN(DO)
/* Deallocate the object first to avoid duplicate locks */
                DLCOBJ     OBJ((&OBJNAME &OBJTYPE *EXCL))
                MONMSG     MSGID(CPF0000)
/* Allocate the object exclusively */
                ALCOBJ     OBJ((&OBJNAME &OBJTYPE *EXCL)) WAIT(1)
/* If locked by another job */
                MONMSG     MSGID(CPF1002) EXEC(DO)
                   CHGVAR     VAR(&RTNCODE) VALUE('*INUSE    ')
                   GOTO       CMDLBL(END)
                ENDDO
/* If not locked by another job */
                CHGVAR     VAR(&RTNCODE) VALUE('*ALLOCATED')
                GOTO       CMDLBL(END)
             ENDDO
 
/* Deallocate the object */
 
             IF         COND(&MODE *EQ 'D') THEN(DO)
/* Deallocate the object */
                DLCOBJ     OBJ((&OBJNAME &OBJTYPE *EXCL))
                GOTO       CMDLBL(END)
             ENDDO
 
/* Check the object allocation */
 
             IF         COND(&MODE *EQ 'C') THEN(DO)
/* Attempt to allocate the object exclusively */
                ALCOBJ     OBJ((&OBJNAME &OBJTYPE *EXCL)) WAIT(1)
/* If locked by another job */
                MONMSG     MSGID(CPF1002) EXEC(DO)
                   CHGVAR     VAR(&RTNCODE) VALUE('*INUSE    ')
                   GOTO       CMDLBL(END)
                ENDDO
/* Deallocate the object as this was only a check */
                DLCOBJ     OBJ((&OBJNAME &OBJTYPE *EXCL))
                CHGVAR     VAR(&RTNCODE) VALUE('*AVAILABLE')
                GOTO       CMDLBL(END)
             ENDDO
 
             GOTO       CMDLBL(END)
 
ABNOR:
             CHGJOB     SWS(XXXXXX11)
 
END:
 
             ENDPGM
