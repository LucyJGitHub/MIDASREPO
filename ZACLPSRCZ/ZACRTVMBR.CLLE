/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI *  TEXT('Midas ZA Retrieve the next member')                   */
/*********************************************************************/
/*                                                                   */
/*       Midas - Common Programs                                     */
/*                                                                   */
/*       ZACRTVMBR - Retrieve the next member of a multi-membered    */
/*                   file                                            */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2001           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/* Midas Release 4.01 -----------------------------------------------*/
/*       Last Amend No. CSC011  *CREATE    Date 18Sep01              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CSC011 - 24x7 Midas Availability                            */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&FILE &CURR &NEXT)
 
             DCL        VAR(&FILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CURR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&NEXT) TYPE(*CHAR) LEN(10)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2001')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
             CHGJOB     SWS(00000000)
             CHGVAR     VAR(&NEXT) VALUE('          ')
 
/* Retrieve the first member */
 
             IF         COND(&CURR *EQ '          ') THEN(DO)
                RTVMBRD    FILE(&FILE) MBR(*FIRSTMBR) RTNMBR(&NEXT)
                MONMSG     MSGID(CPF3019) EXEC(CHGVAR VAR(&NEXT) +
                             VALUE('*NONE'))
                GOTO       CMDLBL(END)
             ENDDO
 
/* Otherwise retrieve the next member */
 
             RTVMBRD    FILE(&FILE) MBR(&CURR *NEXT) RTNMBR(&NEXT)
             MONMSG     MSGID(CPF3049) EXEC(CHGVAR VAR(&NEXT) +
                          VALUE('*NONE'))
             GOTO       CMDLBL(END)
 
/* Abnormal processing */
 
 ABNOR:      CHGJOB     SWS(XXXXXX11)
             CHGVAR     VAR(&NEXT) VALUE('*ERROR')
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          ZACRTVMBR ended abnormally - see job +
                          log') TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
/* End program */
 
 END:        ENDPGM
