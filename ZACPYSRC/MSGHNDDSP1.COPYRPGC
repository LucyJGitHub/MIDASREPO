      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member (RPG)                                   *
      *                                                               *
      *  MSGHNDDSP1 - Handle messages for *DSP functions              *
      *                                                               *
      *  This include member processes a set of error messages and    *
      *  warning messages in a standard *DSP module.  It requires that*
      *  the members MSGHNDDCL and MSGHNDDSP2 are also included.      *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CAP003  *CREATE    Date 27Aug98               *
      *  Prev Amend No. nnnnnn             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP003 - API Phase 2.                                        *
      *                                                               *
      *****************************************************************
 
      ** Set the transaction status field according to whether or not
      ** there are errors or warnings (set it to 'F' even if only warnings
      ** to ensure that the message handler actually sends the messages).
     C                   IF            MsgIDArr(1) = *blanks
     C                             AND WMsgIDArr(1) = *blanks
     C                   EVAL      TranStatus = 'S'
     C                   ELSE
     C                   EVAL      TranStatus = 'F'
     C                   ENDIF
 
      ** Generate a timestamp for the transaction
     C                   CALLB     'ZAGENTMSTM'
     C                   PARM                    TimeStamp
 
      ** Process the error messages, if any
     C                   IF        MsgIDArr(1) <> *blanks
 
     C                   MOVEA     MsgIDArr      WkMsgIDArr
     C                   MOVEA     FldNameArr    WkFldNmArr
     C                   MOVEA     MsgDtaArr     WkMsgDtArr
 
      ** Set the 'clear program message queue' flag to 'Y' for first pass
     C                   EVAL      ClrPgmMsgQ = 'Y'
 
     C                   EXSR      ProcMsgs
 
     C                   ENDIF
 
      ** Process the warning messages, if any
     C                   IF        WMsgIDArr(1) <> *blanks
 
     C                   MOVEA     WMsgIDArr     WkMsgIDArr
     C                   MOVEA     WFldNamArr    WkFldNmArr
     C                   MOVEA     WMsgDtaArr    WkMsgDtArr
 
      ** Set the 'clear program message queue' flag to 'N' for second
      ** pass, to prevent any messages sent above from being lost, unless
      ** ONLY warnings are being sent.
     C                   EVAL      ClrPgmMsgQ = 'N'
 
     C                   IF        MsgIDArr(1) = *blanks
     C                   EVAL      ClrPgmMsgQ = 'Y'
     C                   ENDIF
 
     C                   EXSR      ProcMsgs
 
     C                   ENDIF
 
      *****************************************************************
      * End of /COPY MSGHNDDSP1                                       *
      *****************************************************************
