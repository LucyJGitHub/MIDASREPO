      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member (RPG)                                   *
      *                                                               *
      *  MSGHNDDSP3 - Handle messages for *DSP functions              *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. CAS001  *CREATE    Date 23Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *                                                               *
      *****************************************************************
 
      ** Set the transaction status field according to whether or not
      ** there are errors or warnings (set it to 'F' even if only warnings
      ** to ensure that the message handler actually sends the messages).
     C                   IF            MsgIDAr2(1) = *blanks
     C                             AND WMsgIDAr2(1) = *blanks
     C                   EVAL      TranStatus = 'S'
     C                   ELSE
     C                   EVAL      TranStatus = 'F'
     C                   ENDIF
 
      ** Generate a timestamp for the transaction
     C                   CALLB     'ZAGENTMSTM'
     C                   PARM                    TimeStamp
 
      ** Process the error messages, if any
     C                   IF        MsgIDAr2(1) <> *blanks
 
     C                   MOVEA     MsgIDAr2      WkMsgIDAr2
     C                   MOVEA     FldNameAr2    WkFldNmAr2
     C                   MOVEA     MsgDtaAr2     WkMsgDtAr2
 
      ** Set the 'clear program message queue' flag to 'Y' for first pass
     C                   EVAL      ClrPgmMsgQ = 'Y'
 
     C                   EXSR      ProcMsgs
 
     C                   ENDIF
 
      ** Process the warning messages, if any
     C                   IF        WMsgIDAr2(1) <> *blanks
 
     C                   MOVEA     WMsgIDAr2     WkMsgIDAr2
     C                   MOVEA     WFldNamAr2    WkFldNmAr2
     C                   MOVEA     WMsgDtaAr2    WkMsgDtAr2
 
      ** Set the 'clear program message queue' flag to 'N' for second
      ** pass, to prevent any messages sent above from being lost, unless
      ** ONLY warnings are being sent.
     C                   EVAL      ClrPgmMsgQ = 'N'
 
     C                   IF        MsgIDAr2(1) = *blanks
     C                   EVAL      ClrPgmMsgQ = 'Y'
     C                   ENDIF
 
     C                   EXSR      ProcMsgs
 
     C                   ENDIF
 
      *****************************************************************
      * End of /COPY MSGHNDDSP3                                       *
      *****************************************************************
