      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member (RPG)                                   *
      *                                                               *
      *  RECMPSSR - *PSSR Processing                                  *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CRE008  *CREATE    Data 19Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRE008 - Cash Management                                     *
      *                                                               *
      *****************************************************************
      * *PSSR  --- ABNORMAL ERROR CONDITIONS
      *****************************************************************
     C     *PSSR         BEGSR
 
     C     RunBefore     IFEQ      *BLANKS
 
     C                   MOVE      'Y'           RunBefore         1
 
     C                   MOVEL     '*ERROR'      X_RTCD
 
     C                   DUMP
 
      * If no identified cause of error
 
     C     X_ERMS        IFEQ      *BLANK
     C                   EVAL      X_ERMS = 'UNIDENTIFIED ERROR'
     C                   ENDIF
 
      * Send a message
 
     C                   CALL      'AOCREPT'                            9999    *
     C                   PARM      'MEM5052'     #MSGID            7
     C                   PARM      'GBMIDAS '    #MSGF            10
     C                   PARM      *BLANKS       #MSGFL           10
     C                   PARM      X_ERMS        #MSGDT          256
     C                   PARM      '*PRV '       #MSGR             5
     C                   PARM      '*'           #PRGM            10
     C                   PARM      'MOPERQ '     #MSGQ            10
     C                   PARM      '*INFO  '     #MSGT             7
 
      * Update LDA
 
     C     *LOCK         IN        LDA
     C                   MOVEL     *BLANK        DBFile
     C                   MOVEL     *BLANK        DBKey
     C                   MOVEL     PSProcPgm     DBPgm
     C                   Z-ADD     999           DBase
     C                   OUT       LDA
 
      * Write to the error log
 
     C                   EXSR      WRT_ELOG
     C                   ENDIF
 
     C                   MOVEL     '9'           CTRLInfo1         1
     C                   SETON                                        LRU7U8
     C                   RETURN
 
     C                   ENDSR
      *****************************************************************
      /SPACE 5
      *****************************************************************
      * WRITE TO ERROR LOG
      *****************************************************************
     C     WRT_ELOG      BEGSR
 
     C                   CLEAR                   BUF200
 
     C                   EVAL      BUF200 = X_ERMS
 
     C                   CALLB     'SDWRTELOG'
     C                   PARM      PSProcPgm     I_PGM            10
     C                   PARM      PSProcMod     I_MOD            10
     C                   PARM                    BUF200          200
 
     C                   SETON                                        U7U8
 
     C                   ENDSR
      *****************************************************************
      * End of /COPY RECMPSSR                                         *
      *****************************************************************
