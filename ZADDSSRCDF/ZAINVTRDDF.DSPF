/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ZA Invalid transaction deletion')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Standard subprograms                                 *
     A*                                                               *
     A*  DSPF/ZAINVTRDDF - INVALID TRANSACTION DELETION               *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAP004  *CREATE    Date 27Aug98               *
      *                                    Date ddmmmyy               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *           Extend Front ofc Id to 40-long string               *
     A*  CAP004 - API's phase 3.                                      *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*
     A          R #RCDDTL1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A* COMMAND KEYS
     A                                      CF03
     A                                      CF10
     A                                      CF12
     A*
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'ZAINVTR'
     A                                  1 28'Invalid Transaction Deletion'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            DDRUNA         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
      *
     A                                  4  2'Action.......................:'
     A            DDACTN         1A  O  4 33TEXT('ACTION')
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  5  2'Front Office Transaction ID..:'
     A**Start of change CGL029
     A**********  DDFOTI        20A  O  5 33TEXT('FO TRAN ID')                                CGL029
     A            DDFO40        40A  O  5 33TEXT('FO TRAN ID')
     A**End of change CGL029
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  6  2'Front Office Assoc.Trans.ID..:'
     A            DDFOAT        20A  O  6 33TEXT('ASSOC FO TRAN ID')
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  7  2'Date and Time of Arrival.....:'
     A            DDDATA        10A  O  7 33TEXT('DATE OF ARRIVAL')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A            DDTIMA         8A  O  7 45TEXT('DATE OF ARRIVAL')
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
     A            DDNAR1        30A  O  8  2OVRDTA
     A            DDFLD1        35A  O  8 33OVRDTA
     A                                      DSPATR(HI)
      *
     A            DDNAR2        30A  O  9  2OVRDTA
     A            DDFLD2        35A  O  9 33OVRDTA
     A                                      DSPATR(HI)
      *
     A            DDNAR3        30A  O 10  2OVRDTA
     A            DDFLD3        35A  O 10 33OVRDTA
     A                                      DSPATR(HI)
      *
     A            DDNAR4        30A  O 11  2OVRDTA
     A            DDFLD4        35A  O 11 33OVRDTA
     A                                      DSPATR(HI)
      *
     A            DDNAR5        30A  O 12  2OVRDTA
     A            DDFLD5        35A  O 12 33OVRDTA
     A                                      DSPATR(HI)
      *
     A            DDNAR6        30A  O 13  2OVRDTA
     A            DDFLD6        35A  O 13 33OVRDTA
     A                                      DSPATR(HI)
      *
     A            DDNAR7        30A  O 14  2OVRDTA
     A            DDFLD7        35A  O 14 33OVRDTA
     A                                      DSPATR(HI)
      *
     A            DDNAR8        30A  O 15  2OVRDTA
     A            DDFLD8        35A  O 15 33OVRDTA
     A                                      DSPATR(HI)
      *
     A            DDNAR9        30A  O 16  2OVRDTA
     A            DDFLD9        35A  O 16 33OVRDTA
     A                                      DSPATR(HI)
      *
     A                                 18  2'Error messages associated with thi-
     A                                      s invalid transaction are:         -
     A                                               '
      *
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A                                 24  2'F3=Exit F12=Previous'
     A                                 24 23'F10=DELETE THIS INVALID TRANSACTIO-
     A                                      N FROM THE REPAIR QUEUE'
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(19)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(5) SFLSIZ(31)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  89
     AON89                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
