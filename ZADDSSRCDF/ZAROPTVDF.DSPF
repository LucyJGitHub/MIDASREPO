/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ZA Repair option code validation')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Standard subprograms                                 *
     A*                                                               *
     A*  DSPF/ZAROPTVDF - REPAIR OPTION CODE VALIDATION               *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP014  *CREATE    Date 29Nov99               *
      *                                    Date ddmmmyy               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  CAP014 - Repair function enhancements                        *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*
     A          R ZAROPTF1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A* COMMAND KEYS
     A                                      CF03
     A                                      CF12
     A*
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'ZAROPTV'
     A                                  1 27'Invalid Transaction Selection'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            DDRUNA         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
      *
     A                                  6  2'An incorrect option code was speci-
     A                                      fied on the repair browse screen.'
     A                                  8  2'That option code was:'
     A            DDIOPT         1A  O  8 24OVRDTA
     A                                      DSPATR(HI)
     A                                  9  2'The front office ID selected for r-
     A                                      epair was:'
     A**Start of change CGL029
     A**********  DDFOID        20A  O  9 47OVRDTA                                            CGL029
     A            DDFOID        40A  O 10 24OVRDTA
     A**End of change CGL029
     A                                      DSPATR(HI)
     A                                 11  2'The permissible values for option -
     A                                      code are listed below:'
     A                                 13  2'Option Code       Description'
 
      * Hidden fields to hold the message IDs for the messages which
      * contain the texts defining the option codes.
     A            ODS1MSG        7A  H
     A            ODS2MSG        7A  H
     A            ODS3MSG        7A  H
     A            ODS4MSG        7A  H
 
     A            DDOPT1         1A  O 14  6DSPATR(HI)
     A                                      OVRDTA
     A            DDODS1        55A  O 14 20DSPATR(HI)
     A                                      MSGID(&ODS1MSG ZZGBMSGF)
     A                                      OVRDTA
     A            DDOPT2         1A  O 15  6DSPATR(HI)
     A                                      OVRDTA
     A            DDODS2        55A  O 15 20DSPATR(HI)
     A                                      MSGID(&ODS2MSG ZZGBMSGF)
     A                                      OVRDTA
     A            DDOPT3         1A  O 16  6DSPATR(HI)
     A                                      OVRDTA
     A            DDODS3        55A  O 16 20DSPATR(HI)
     A                                      MSGID(&ODS3MSG ZZGBMSGF)
     A                                      OVRDTA
     A            DDOPT4         1A  O 17  6DSPATR(HI)
     A                                      OVRDTA
     A            DDODS4        55A  O 17 20DSPATR(HI)
     A                                      MSGID(&ODS4MSG ZZGBMSGF)
     A                                      OVRDTA
     A                                 20  2'Input the required option code:'
     A            DDOPT          1A  B 20 34DSPATR(HI)
     A  81                                  DSPATR(RI)
     A N81                                  DSPATR(UL)
     A                                      OVRDTA
     A            DDERR         55A  O 23  2DSPATR(HI)
     A                                      OVRDTA
     A                                 24  2'Enter   F3=Exit   F12=Previous    -
     A                                                                         -
     A                                              '
     A********************************************************************
