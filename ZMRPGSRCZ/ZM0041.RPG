     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Convert MIDAS amount to SWIFT format for MT940/950')  *
     F*****************************************************************
     F*                                                               *
     F*  Midas - MESSAGE MANAGEMENT UTILITIES.                        *
     F*                                                               *
     F*  ZM0041 - AMOUNT CONVERSION.                                  *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2019            *
      *                                                               *
      *  Last Amend No. MD052633 *CREATE   Date 08Oct19               *
     F*---------------------------------------------------------------*
     F*                                                               *
     F*  MD052633 - MT940's closing balance figures is 1 digit short -*
     F*             Increase the length of ZAMNT and ZSAMNT to handle *
     F*             numeric size of (15,0).                           *
     F*           - Applied for MD-53975                              *
     F*                                                               *
     F*****************************************************************
     F* PURPOSE.                                                     *
     F*                                                              *
     F*  To convert an  amount (ZAMNT) (15,0) to SWIFT               *
     F*  input format (ZSAMNT) (17A).                                *
     F*                                                              *
     F*  1.A decimal comma is inserted dependant on the number of    *
     F*    decimal places returned from the access to sdcurrpd.      *
     F*  2.Leading zeros must be dropped.                            *
     F*  3.The decimal comma must have at least one leading digit.   *
     F*                                                              *
     F****************************************************************
     F*                                                              *
     F* FUNCTION OF INDICATORS.                                      *
     F*                                                              *
     F* 90  -  0 Decimal places from sdcurrpd.                       *
     F* 91  -  1 Decimal place from sdcurrpd.                        *
     F* 92  -  2 Decimal places from sdcurrpd.                       *
     F* 93  -  3 Decimal places from sdcurrpd.                       *
     F* 94  -  Used in the blank of leading zeros.                   *
     F* 95  -  Used to end the blank of leading zeros.               *
     F*                                                              *
     F****************************************************************
     F/COPY WNCPYSRC,ZM0040F001
     E*
     E** COPYRIGHT STATEMENT ARRAY.
     E*
     E                    CPY@    1   1 80
     E*
     E** ARRAY USED IN CONVERSION.
     E*
     E                    ZAA        17  1
      /EJECT
     E****************************************************************
     I*
     I** DSTS STRUCTURE FOR COMPILATION - COPYRIGHT INSERTION
     I*
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I* Program data structure( to check the number of parameters)
     I           SDS
     I                                     *PARMS   WPARM
     IRUNDAT      DS
     I                                        1   7 MRDT
     I                                    P   8  100RDNB
     I                                       11  11 SUC
     I                                       12  12 DFF
     I                                       13  13 MBIN
     I*
     I** External Data structure for Currency Details.
     I*
     I/COPY WNCPYSRC,ZM0040I001
     ISDCURR    E DSSDCURRPD
     I*
     I** 1st DS for access program , short data structure.
     I*
     IDSFDY     E DSDSFDY
     I*
     I** 2nd DS for access program , long data structure.
     I*
     IDSSDY     E DSDSSDY
     I*
     I****************************************************************
     C*
     C**  Entry of currency and amount
     C*
     C           *ENTRY    PLIST
     C                     PARM           ZAMNT  150
     C                     PARM           ZCCY    3
     C                     PARM           ZSAMNT 17
     C                     PARM           ZSCCY   3
     C                     PARM           ZERR    1
     C                     PARM           ZSWDPC  1
     C*
     C                     SETOF                     909192
     C                     SETOF                     939495
     C                     MOVE *BLANKS   ZSAMNT
     C                     MOVE *BLANKS   ZSCCY
     C                     MOVE *BLANKS   ZERR
      *
      ** Get Rundate - Rundate  *
      *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
      *
      ** Control if feature 'Swift decimal places' is on.
      *
     C                     CALL 'AOSARDR0'
     C                     PARM '       ' @RTCD   7
     C                     PARM '*VERIFY' @OPTN   7
     C                     PARM 'CSW020'  @SARD   6
     C           @RTCD     IFEQ *BLANK
     C                     MOVE 'Y'       CSW020  1
     C                     ENDIF
     C*
     C*
     C** To determine the number of decimal places for the currency and
     C** to obtain the swift currency by accessing sdcurrpd (the currency)
     C** details file), using the midas currency as a key and handling any
     C** database errors in the program.
     C*
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY'    @OPTN   7
     C                     PARM ZCCY      @CCY    3
     C           SDCURR    PARM SDCURR    DSSDY
     C*
     C** To deal with data base errors.
     C*
     C           @RTCD     IFNE *BLANKS
     C                     MOVE '1'       ZERR
     C                     END
     C*
     C** Only do the following processing if there has not been a
     C** database error.
     C*
     C           @RTCD     IFEQ *BLANKS
     C*
     C** To move the currency field names.
     C*
     C                     MOVE A6SWCY    ZSCCY
     C                     MOVE A6NBDP    ZCDP    10
     C*
     C** Need to adjust amount if the Swift Decimal Places is
     C** different to the normal number of Decimal Places and the
     C** effective date has been reached.
     C*
     C           CSW020    IFEQ 'Y'
     C           A6NBDP    ANDNEA6SWDP
     C           A6SWDT    ANDLERDNB
     C*
     C           WPARM     IFEQ 6
     C           ZSWDPC    ANDNE'N'
     C           WPARM     OREQ 5
      *
     C* CHECK THE DIFFERENCE OF DECIMAL TO HAVE THE CORRECT AMOUNT
      *
     C           A6SWDP    IFLT ZCDP
     C           ZCDP      SUB  A6SWDP    X       10
     C                     ELSE
     C           A6SWDP    SUB  ZCDP      X
     C                     ENDIF
      *
     C           X         IFEQ 1
     C                     Z-ADD10        Y       40
     C                     ENDIF
     C           X         IFEQ 2
     C                     Z-ADD100       Y
     C                     ENDIF
     C           X         IFEQ 3
     C                     Z-ADD1000      Y
     C                     ENDIF
     C           X         IFEQ 4
     C                     Z-ADD10000     Y
     C                     ENDIF
      *
     C           A6SWDP    IFLT ZCDP
     C           ZAMNT     DIV  Y         ZAMNT
     C                     ELSE
     C           ZAMNT     MULT Y         ZAMNT
     C                     ENDIF
      *
     C                     Z-ADDA6SWDP    ZCDP
     C*
     C                     ENDIF
     C                     ENDIF
     C/COPY WNCPYSRC,ZM0040C001
     C*
     C** To obtain the number of currency decimal places.
     C*  No decimal places.
     C           ZCDP      IFEQ 0
     C                     MOVE '1'       *IN90
     C*
     C/COPY WNCPYSRC,ZM0040CCP2
     C*
     C                     END
     C*  One decimal place.
     C           ZCDP      IFEQ 1
     C                     MOVE '1'       *IN91
     C                     END
     C*  Two decimal places.
     C           ZCDP      IFEQ 2
     C                     MOVE '1'       *IN92
     C                     END
     C*  Three decimal places.
     C           ZCDP      IFEQ 3
     C                     MOVE '1'       *IN93
     C                     END
     C*
     C/COPY WNCPYSRC,ZM0040CCP1
     C*
     C*
     C** Ensure amount field is positive before processing
     C*
     C           ZAMNT     IFLT 0
     C           ZAMNT     MULT -1        ZAMNT
     C                     END
     C*
     C** To move the midas amount to the 17A field including the decimal
     C** comma. H16 is a holding field for the amount.
     C*
     C*
     C/COPY WNCPYSRC,ZM0040CCP3
     C*
     C                     MOVE ZAMNT     H16    16
     C*
     C/COPY WNCPYSRC,ZM0040CCP4
     C*
     C*
     C** For no decimal places. *in90 =1. The decimal comma is placed at
     C** the end of the amount.
     C*
     C           *IN90     IFEQ '1'
     C                     MOVELH16       ZAAL   17
     C                     MOVE ','       ZAAL
     C                     END
     C*
     C** For one decimal place. *in91 = 1. The amount field is split
     C** to enable the comma to be inserted one digit before the end.
     C** H1 is a holding field.
     C*
     C           *IN91     IFEQ '1'
     C                     MOVE H16       H1      1
     C                     MOVE ','       H16
     C                     MOVELH16       ZAAL
     C                     MOVE H1        ZAAL
     C                     END
     C*
     C** For two decimal places. *in92 = 1. The amount field is split
     C** to enable the comma to be inserted two digits before the end.
     C** H2 and H3 are holding fields.
     C*
     C           *IN92     IFEQ '1'
     C                     MOVE H16       H2      2
     C                     MOVELH16       H14    14
     C                     MOVE H2        H3      3
     C                     MOVEL','       H3
     C                     MOVELH14       ZAAL
     C                     MOVE H3        ZAAL
     C                     END
     C*
     C** For three decimal places. *in93 = 1. The amount field is split
     C** to enable the comma to be inserted three digits before the end.
     C** H2 , H3 and H4 are holding fields.
     C*
     C           *IN93     IFEQ '1'
     C                     MOVE H16       H3
     C                     MOVELH16       H13    13
     C                     MOVE H3        H4      4
     C                     MOVEL','       H4
     C                     MOVELH13       ZAAL
     C                     MOVE H4        ZAAL
     C                     END
     C*
     C** Blank out all leading zeros leaving at least one leading
     C** integer.  Any remaining leading zeros are dropped,except
     C** if there is one directly proceeding the decimal comma.
     C** This is retained.
     C*
     C                     MOVEAZAAL      ZAA
     C                     Z-ADD1         ZA      20
     C                     SETOF                     95
     C                     MOVE '1'       *IN94
     C*
     C           *IN94     DOUEQ'0'
     C*
     C           ZAA,ZA    IFEQ '0'
     C           ZAA,ZA    OREQ ' '
     C                     MOVE '1'       *IN94
     C                     ELSE
     C                     MOVE '0'       *IN94
     C                     END
     C*
     C           *IN94     IFEQ '1'
     C                     MOVE ' '       ZAA,ZA
     C                     ADD  1         ZA
     C                     END
     C*
     C           *IN94     IFEQ '0'
     C           ZAA,ZA    ANDEQ','
     C                     MOVE '1'       *IN95
     C                     END
     C*
     C           ZA        IFEQ 16
     C           ZCDP      ANDNE0
     C           ZAA,ZA    IFEQ ','
     C           ZCDP      ANDEQ1
     C                     ELSE
     C                     MOVE '0'       *IN94
     C                     SUB  1         ZA
     C                     MOVE ','       ZAA,ZA
     C                     END
     C                     END
     C*
     C                     END
     C*
     C           *IN95     IFEQ '1'
     C           ZA        SUB  1         ZA
     C                     MOVE '0'       ZAA,ZA
     C                     END
     C*
     C** Move to a 15A SWIFT format field for output.
     C*
     C                     MOVEAZAA,ZA    ZSAMNT 17
     C*
     C** 'End' for the database error.
     C*
     C/COPY WNCPYSRC,ZM0040C002
     C                     END
     C*
     C** Termination Processing.
     C*
     C                     SETON                     LR
     C                     RETRN
     C*
     C*****************************************************************
** CPY@
(c) Misys International Banking Systems Ltd. 2019
