     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas Obtain Confirmation Matching Status.')
     F****************************************************************
     F*                                                              *
     F*  Midas - MESSAGE MAINTENANCE UTILITIES.
     F*                                                              *
     F*  ZM0100 - OBTAIN CONFIRMATION MATCHING STATUS.               *
     F*                                                              *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*        COMPANY CONFIDENTIAL.                                 *
     F*                                                              *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. 171060             Date 27Feb01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 141572             Date 21Sep98               *
      *                 113352             Date 13Feb97               *
     F*                                                              *
     F****************************************************************
     F*                                                              *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  171060 - If confirmation is no longer on the system,         *
      *           then access PF/CFFEEDPD with highest sequence       *
      *           number. Applied fix 148906.                         *
     F*  141572  -  Recompile over changes in MGOREFPD               *
     F*  113352  -  RECOMPILE OVER CHANGES IN MGOREFPD               *
     F*                                                              *
     F****************************************************************
     F*  C R E A T I O N  P A R A M E T E R S.                       *
     F*                                                              *
     F*                                                              *
     F****************************************************************
     F* PURPOSE.                                                     *
     F*                                                              *
     F*  To retrieve match status returned by the Confirmation       *
     F*  Matching Module for confirmations and related pay/receives. *
     F*                                                              *
     F*  A TRN is input and is used to access the outgoing message   *
     F*  reference file .                                            *
     F*  1. If no record is found a 'D' is moved to the error line   *
     F*     and the program is terminated.                           *
     F*  2.If the first character of the message type is a '3' -     *
     F*    a confirmation - access the CF feedback file (CFFEEDL0)   *
     F*    If no record is found move an 'N' to the error line       *
     F*    and terminate the program.                                *
     F*  Else                                                        *
     F*    Use the partial key of <Module><Transaction> to           *
     F*    access the outgoing message file i.e. the latest          *
     F*    confirmation for the transaction which generated          *
     F*    this non-confirmation message.                            *
      *    If no record is found and ...                              *                       171060
      *    .. <Module> is 'DL', use a partial key to access the       *                       171060
      *    CFFEEDL0 file and find the record with the same partial    *                       171060
      *    key and with greatest sequence number                      *                       171060
     F*    If no record is found move an 'E' to the error            *
     F*    line and terminate the program.                           *
     F*  Else                                                        *
     F*    Use the TRN of the latest confirmation to access          *
     F*    the CF feedback file                                      *
     F*    If no record is found move an 'N' to the error            *
     F*    line and terminate the program.                           *
     F* 3.Set up the return parameters and terminate the program.    *
     F*                                                              *
     F****************************************************************
     F*                                                              *
     F* FUNCTION OF INDICATORS.                                      *
     F*                                                              *
     F* 90  GENERAL WORK - Deleting further leading zeros.           *
     F*                    and trailing zeros.                       *
     F* 91  GENERAL WORK -  Leading character is a ','.              *
     F*                                                              *
     F****************************************************************
     F*
     FCFFEEDL0IF  E           K        DISK
     FMGOREFL0IF  E           K        DISK
     F            MGOREFD0                          KRENAMEMGREF0
     FMGOREFLGIF  E           K        DISK
     F            MGOREFD0                          KRENAMEMGREFG
     F*
     F****************************************************************
     E*
     E** COPYRIGHT STATEMENT ARRAY.
     E*
     E                    CPY@    1   1 80
      /EJECT
     E****************************************************************
     I*
     I** DSTS STRUCTURE FOR COMPILATION - COPYRIGHT INSERTION
     I*
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *                                                                                       171060
      ** CFFEEDL0 KEY                                                                         171060
      *                                                                                       171060
     I            DS                                                                          171060
     I                                        1  16 CFKY                                      171060
     I                                        1   8 @TS1                                      171060
     I                                        1   2 @KY1                                      171060
     I                                        3   8 @KY2                                      171060
     I                                        9  11 @KY3                                      171060
     I****************************************************************
     C*
     C**  Parameters List.
     C**  1.Ztrn - Transaction Reference Number - Input.
     C**  2.Zscpms - Counterparty Match Status - Output.
     C**  3.Zsbkms - Broker Match Status - Output.
     C**  4.Zserr - Error Code - Output.
     C**          - N = no record found for this or the latest
     C**                confirmation.
     C**          - E = no confirmation found for this non-confirmation
     C**                message.
     C**          - D = TRN not found ; database error.
     C*
     C           *ENTRY    PLIST
     C                     PARM           ZTRN   16
     C                     PARM           ZSCPMS  20
     C                     PARM           ZSBKMS  20
     C                     PARM           ZSERR   1
     C*
     C                     MOVE *BLANKS   ZSCPMS
     C                     MOVE *BLANKS   ZSBKMS
     C                     MOVE *BLANKS   ZSERR
     C*
     C** Keylist to access mgorefpd by logical file mgoreflg.
     C*
     C           KEY       KLIST
     C                     KFLD           MODI
     C                     KFLD           MTRN
      *                                                                                       171060
      ** Set off flag to indicate if Status found without access                              171060
      ** to related confirmation message.                                                     171060
      *                                                                                       171060
     C                     MOVE '0'       DLFD    1                                           171060
     C*
     C** Use the TRN to access the outgoing message reference file
     C** MGOREFL0.
     C*
     C           ZTRN      CHAINMGREF0               90
     C*
     C** If no record is found.
     C*
     C           *IN90     IFEQ '1'
     C                     MOVE 'D'       ZSERR
     C                     SETON                     LR
     C                     RETRN
     C                     END
     C*
     C** If the message type is a confirmation ; i.e. 1st character of
     C** MTPY = '3'
     C*
     C                     MOVELMTPY      MTEST   1
     C           MTEST     IFEQ '3'
     C                     MOVE '1'       *IN91
     C*
     C** If the message is a confirmation access the CF feedback file.
     C*
     C           ZTRN      CHAINCFFEEDL0             92
     C*
     C** If no record is found.
     C*
     C           *IN92     IFEQ '1'
     C                     MOVE 'N'       ZSERR
     C                     SETON                         LR
     C                     RETRN
     C                     END
     C*
     C** Using a partial key of module and transaction to access
     C** the outgoing message file i.e. it is a pay/receive.
     C*
     C                     ELSE
     C*
     C           KEY       CHAINMGREFG               93
     C*
     C** If no confirmation is found for this non-confirmation message.
     C*
     C           *IN93     IFEQ '1'
      *                                                                                       171060
      ** Attempt to access feedback file directly with highest seq. no                        171060
      ** for Dealing module only                                                              171060
      *                                                                                       171060
     C                     MOVELZTRN      CFKY                                                171060
     C           @KY1      IFEQ 'DL'                                                          171060
     C                     MOVE '999'     @KY3                                                171060
     C           CFKY      SETGTCFFEEDL0                                                      171060
     C                     READPCFFEEDL0                 88                                   171060
     C                     MOVELIMRF      @TS2    8                                           171060
      *                                                                                       171060
      ** If deal number different, or record not found, return                                171060
      *                                                                                       171060
     C           *IN88     IFEQ '1'                                                           171060
     C           @TS1      ORNE @TS2                                                          171060
     C                     MOVE 'E'       ZSERR                                               171060
     C                     MOVE '1'       *INLR                                               171060
     C                     RETRN                                                              171060
     C                     ELSE                                                               171060
     C                     MOVE '1'       DLFD                                                171060
     C                     END                                                                171060
      *                                                                                       171060
      ** Not Dealing                                                                          171060
      *                                                                                       171060
     C                     ELSE                                                               171060
      *                                                                                       171060
     C                     MOVE 'E'       ZSERR
     C                     SETON                         LR
     C                     RETRN
     C                     END                                                                171060
     C                     END
     C*
     C** Transaction Reference number of the latest non-confirmation
     C** message.
     C*
     C           DLFD      IFNE '1'                                                           171060
     C           TRNO      CHAINCFFEEDL0             92
     C*
     C           *IN92     IFEQ '1'
     C                     MOVE 'N'       ZSERR
     C                     SETON                         LR
     C                     RETRN
     C                     END
     C*
     C                     END                                                                171060
     C*
     C                     END
     C*
     C**  To set up the return parameters.
     C*
     C                     Z-ADDCPSX      ZSCPMS
     C                     Z-ADDBKSX      ZSBKMS
     C*
     C**  Termination Processing.
     C*
     C                     SETON                     LR
     C*
     C**********************************************************************
** CPY@
(c) Misys International Banking Systems Ltd. 2001
