     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas Format SWIFT Address. Options A/C/D')
     F*****************************************************************
     F*                                                               *
     F*  Midas - MESSAGE MANAGEMENT UTILITIES.
     F*                                                               *
     F*  ZM1070 - FORMAT OF SWIFT ADDRESS.                            *
     F*                                                               *
     F* PURPOSE :                                                     *
     F*                                                               *
     F*  To set up either an A, C  or D type address for use within   *
     F*  a SWIFT format message. If the destination is not blank,     *
     F*  a flag will be set depending on whether the input is a       *
     F*  customer number, shortname or counterparty nostro.           *
     F*                                                               *
     F*  The destination, if entered, may be either:-                 *
     F*  1.Customer Number.                                           *
     F*  2.Customer Shortname.                                        *
     F*  3.Counterparty nostro code.                                  *
     F*                                                               *
     F*  If the input is a customer number an 'A' is moved to field   *
     F*  ZSTYPE , for a customer shortname a 'B' is moved in and for  *
     F*  a counterparty nostro a 'C' is moved in. If the destination  *
     F*  is blank, the ZSTYPE field remains blank.                    *
     F*                                                               *
     F*  If the SWIFT address is present for the destination the      *
     F*  program will set up an A type address, otherwise a D type    *
     F*  address with the name and address. If the destination is     *
     F*  blank the program will be set up a C type address.           *
     F*                                                               *
      *  Note: ZM1070 has been duplicated by CSW036 into ZM1071.      *   CSW036
      *        For that reason, any change applied to this program    *   CSW036
      *        might be relevant to its twin too.                     *   CSW036
      *                                                               *   CSW036
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 SL0068             Date 21May93               *
      *                 S01401             Date 10Feb93               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
     F*  SL0068 - Program should check for Access object return code  *  *
     F*           to be EQ *BLANKS for no error.                      *  *
     F*  S01401 - The generation of MT500 SWIFT Messages if the       *  *
     F*           option is available.  NEW PROGRAM.                  *  *
     F*                                                               *
      *****************************************************************
      *                                                               *
      *  C R E A T I O N     P A R A M E T E R S                      *
     F*****************************************************************
     E*
     E                    CPY@    1   1 80
     E** COPYRIGHT STATEMENT ARRAY.
     E*
     I*****************************************************************
     I*
     ISDCUST    E DSSDCUSTPD
     I** External Data structure for Customer Details.
     I*
     ISDCNST    E DSSDCNSTPD
     I** External Data structure for Counterparty Nostro Codes.
     I*
     IDSFDY     E DSDSFDY
     I** 1st DS for access program , short data structure.
     I*
     IDSSDY     E DSDSSDY
     I** 2nd DS for access program , long data structure.
     I*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*
     C**  Entry of address information
     C           *ENTRY    PLIST
     C                     PARM           ZDEST  12
     C                     PARM           ZACLN  35
     C                     PARM           ZSADD1 35
     C                     PARM           ZSADD2 35
     C                     PARM           ZSADD3 35
     C                     PARM           ZSADD4 35
     C                     PARM           ZSTAG   1
     C                     PARM           ZSTYPE  1
     C                     PARM           ZERR    1
     C                     PARM           ZCRNM  20                       M5T054
     C                     PARM           ZCRTN  10                       M5T054
     C*
     C                     MOVEACPY@      BIS@   80
     C*
     C**  Initialise output parameters
     C                     MOVE *BLANKS   ZSADD1
     C                     MOVE *BLANKS   ZSADD2
     C                     MOVE *BLANKS   ZSADD3
     C                     MOVE *BLANKS   ZSADD4
     C                     MOVE *BLANKS   ZSTAG
     C                     MOVE *BLANKS   ZSTYPE
     C                     MOVE *BLANKS   ZERR
     C                     MOVE *BLANKS   ZCRNM                           M5T054
     C                     MOVE *BLANKS   ZCRTN                           M5T054
     C*
     C**  If destination is blank - 'C' address tag
     C           ZDEST     IFEQ *BLANKS
     C                     MOVE 'C'       ZSTAG
     C                     ELSE
     C*
      ** Initially check for counterparty nostro.
     C                     MOVELZDEST     @CNCD
      *
     C                     CALL 'AOCNSTR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY'    @OPTN   7
     C                     PARM           @CNCD   8
     C           SDCNST    PARM SDCNST    DSFDY
     C*
     C**  If found set address type to 'C' (C/party nostro), and set up
     C**  Addresses
     C********** @RTCD     IFNE *BLANKS                                   SL0068
     C           @RTCD     IFEQ *BLANKS                                   SL0068
     C                     MOVE 'C'       ZSTYPE
      *
     C**  Check if SWIFT address is blank
     C           AWSWID    IFNE *BLANKS
     C                     MOVELAWSWID    ZSADD1
     C                     MOVE 'A'       ZSTAG
     C                     ELSE
     C                     MOVELAWCPNM    ZSADD1
     C                     MOVELAWCNTN    ZSADD2
     C                     MOVE 'D'       ZSTAG
     C                     END
     C*
     C**  Else assume that address is customer
     C                     ELSE
     C*
      ** Check for customer number or shortname
     C                     MOVELZDEST     @KEY1
      *
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY'    @OPTN   7
     C                     PARM           @KEY1  10
     C                     PARM *BLANKS   @KYST   7
     C           SDCUST    PARM SDCUST    DSSDY
     C*
     C** Indicate if Customer Number or Shortname; else return in error
     C           @KYST     IFEQ '*CNUM  '
     C                     MOVE 'A'       ZSTYPE
     C                     ELSE
     C           @KYST     IFEQ '*CSHT  '
     C                     MOVE 'B'       ZSTYPE
     C                     ELSE
     C                     MOVE '1'       ZERR
     C                     END
     C                     END
     C*
     C**  If there is a swift address on file use it , else use the
     C**  customer name and address.
     C           ZERR      IFNE '1'
     C           BBCSID    IFNE *BLANKS
     C                     MOVELBBCSID    ZSADD1
     C                     MOVELBBCRNM    ZCRNM                           M5T054
     C                     MOVELBBCRTN    ZCRTN                           M5T054
     C                     MOVE 'A'       ZSTAG
     C                     ELSE
     C                     MOVELBBCNA1    ZSADD1
     C                     MOVELBBCNA2    ZSADD2
     C                     MOVELBBCNA3    ZSADD3
     C                     MOVELBBCNA4    ZSADD4
     C                     MOVE 'D'       ZSTAG
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C**  Termination Processing.
     C                     MOVE '1'       *INLR
     C                     RETRN
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
** CPY@
(c) Finastra International Limited 2001
