     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ZM Format SWIFT address options A/B/D')
     F*****************************************************************
     F*                                                               *
     F*  Midas - MESSAGE MANAGEMENT UTILITIES.
     F*                                                               *
     F*  ZM1080 - FORMAT OF SWIFT ADDRESS. OPTIONS A, B OR D          *
     F*                                                               *
     F* PURPOSE :                                                     *
     F*                                                               *
     F*  To set up either an A, B  or D type address for use within   *
     F*  a SWIFT format message. If the destination is not blank,     *
     F*  a flag will be set depending on whether the input is a       *
     F*  customer number, shortname or counterparty nostro.           *
     F*                                                               *
     F*  The destination, if entered, may be either:-                 *
     F*  1.Customer Number.                                           *
     F*  2.Customer Shortname.                                        *
     F*  3.Counterparty nostro code.                                  *
     F*                                                               *
     F*  If the input is a customer number an 'A' is moved to field   *
     F*  ZSTYPE , for a customer shortname a 'B' is moved in and for  *
     F*  a counterparty nostro a 'C' is moved in. If the destination  *
     F*  is blank, the ZSTYPE field remains blank.                    *
     F*                                                               *
     F*  If the SWIFT address is present for the destination the      *
     F*  program will set up an A type address, otherwise a B or D    *
     F*  type address will be set up.  If the ZDEST customer is of    *
     F*  the same family as the ZSRCN customer, then a B type         *
     F*  address is set up, otherwise a D type address is set up.     *
     F*                                                               *
      *  Note: ZM1080 has been duplicated by CSW036 into ZM1081.      *   CSW036
      *        For that reason, any change applied to this program    *   CSW036
      *        might be relevant to its twin too.                     *   CSW036
      *                                                               *   CSW036
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD031             Date 10Apr06               *
      *  Prev Amend No. CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 196345             Date 31Jul01               *
      *                 CSW201             Date 02May01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 SL0062             Date 21May93               *
      *                 S01401             Date 10Feb93               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  196345 - Field 57 should have tag D instead of B if both     *
      *           Destination and Sender/Receiver do  not have parent *
      *           customers.                                          *
      *  CSW201 - SWIFT 2001 Standards Update                         *
     F*  SL0062 - Program should check for Access object return code  *  *
     F*           to be EQ *BLANKS for no error.                      *  *
     F*  S01401 - The generation of MT500 SWIFT Messages if the       *  *
     F*           option is available.  NEW PROGRAM.                  *  *
     F*                                                               *
      *****************************************************************
      *                                                               *
      *  C R E A T I O N     P A R A M E T E R S                      *
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     E*
     E                    CPY@    1   1 80
     E** COPYRIGHT STATEMENT ARRAY.
     E*
     E*****************************************************************
     I*
     ISDCUST    E DSSDCUSTPD
     I** External Data structure for Customer Details.
     I*
     ISDCNST    E DSSDCNSTPD
     I** External Data structure for Counterparty Nostro Codes.
     I*
     IDSFDY     E DSDSFDY
     I** 1st DS for access program , short data structure.
     I*
     IDSSDY     E DSDSSDY
     I** 2nd DS for access program , long data structure.
     I*
     I*
     IOPTBDS      DS
     I** DATA STRUCTURE FOR ADDRESS FIELD FOR OPTION B
     I                                        1  35 WWADDR
     I                                        1  20 WWCRNM
     I                                       22  31 WWCRTN
     I*
     IMEBICD    E DSMEBICDPD                                                                  CSW201
      ** Data Structure for BIC Directory                                                     CSW201
      *                                                                                       CSW201
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*
     C**  INPUT PARAMETERS:
     C**  ZRDEST - Customer number/shortname or counterparty nostro for
     C**           field
     C**  ZSRCN  - Customer number of sender or receiver
     C*
     C**  OUPUT PARAMETERS:
     C**  ZSADD1 - Address field 1
     C**  ZSADD2 - Address field 2
     C**  ZSADD3 - Address field 3
     C**  ZSADD4 - Address field 4
     C**  ZSTAG  - Returns option A, B or D
     C**  ZSTYPE - Returns type of input in ZDEST
     C**  ZERR   - Returns '1' if error found
     C*
     C**  Entry of address information
     C           *ENTRY    PLIST
     C                     PARM           ZDEST  12
     C                     PARM           ZSRCN   6
     C                     PARM           ZSADD1 35
     C                     PARM           ZSADD2 35
     C                     PARM           ZSADD3 35
     C                     PARM           ZSADD4 35
     C                     PARM           ZSTAG   1
     C                     PARM           ZSTYPE  1
     C                     PARM           ZERR    1
     C                     PARM           ZCRNM  20                       M5T054
     C                     PARM           ZCRTN  10                       M5T054
     C                     PARM           ZDBEI   1                                           CSW201
     C*
     C                     MOVEACPY@      BIS@   80
     C*
     C**  Initialise output parameters
     C                     MOVE *BLANKS   ZSADD1
     C                     MOVE *BLANKS   ZSADD2
     C                     MOVE *BLANKS   ZSADD3
     C                     MOVE *BLANKS   ZSADD4
     C                     MOVE *BLANKS   ZSTAG
     C                     MOVE *BLANKS   ZSTYPE
     C                     MOVE *BLANKS   ZERR
     C                     MOVE *BLANKS   ZCRNM                           M5T054
     C                     MOVE *BLANKS   ZCRTN                           M5T054
     C*
     C**  Obtain sender/receiver customer details
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM ZSRCN     @KEY2  10
     C                     PARM *BLANKS   @KYST   7
     C           SDCUST    PARM SDCUST    DSSDY
     C*
     C           @RTCD     IFNE *BLANKS
     C                     MOVE '1'       ZERR
     C                     GOTO ZMEND
     C                     END
     C*
     C**  Save sender/receiver parent customer number
     C                     MOVE BBPCNB    SRPCNB  6
     C*
     C**  Initially check for counterparty nostro.
     C                     MOVELZDEST     @CNCD
      *
     C                     CALL 'AOCNSTR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY'    @OPTN   7
     C                     PARM           @CNCD   8
     C           SDCNST    PARM SDCNST    DSFDY
     C*
     C**  If found set address type to 'C' (C/party nostro), and set up
     C**  Addresses
     C********** @RTCD     IFNE *BLANKS                                   SL0062
     C           @RTCD     IFEQ *BLANKS                                   SL0062
     C                     MOVE 'C'       ZSTYPE
      *
     C**  Check if SWIFT address is blank to decide on option A or D
     C**  (option C is not valid if counterparty nostro)
     C           AWSWID    IFNE *BLANKS
     C                     MOVELAWSWID    ZSADD1
     C                     MOVE 'A'       ZSTAG
      *                                                                                       CSW201
      ** If ZDBEI is 'Y', check if SWIFT ID exists in BIC Directory,                          CSW201
      ** then set ZSTAG to 'D' and setup address fields.                                      CSW201
      *                                                                                       CSW201
     C           ZDBEI     IFEQ 'Y'                                                           CSW201
     C                     MOVELAWSWID    WSWFID                                              CSW201
     C                     EXSR SRSBIC                                                        CSW201
     C           PFMTA     IFEQ 'N'                                                           CSW201
     C                     MOVEL'D'       ZSTAG                                               CSW201
     C                     MOVELAWCPNM    ZSADD1                                              CSW201
     C                     MOVELAWCNTN    ZSADD2                                              CSW201
     C                     ENDIF                                                              CSW201
     C                     ENDIF                                                              CSW201
     C                     ELSE
     C                     MOVELAWCPNM    ZSADD1
     C                     MOVELAWCNTN    ZSADD2
     C                     MOVE 'D'       ZSTAG
     C                     END
     C*
     C**  Else assume that address is customer
     C                     ELSE
     C*
      ** Check for customer number or shortname
     C                     MOVELZDEST     @KEY1
      *
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY'    @OPTN   7
     C                     PARM           @KEY1  10
     C                     PARM *BLANKS   @KYST   7
     C           SDCUST    PARM SDCUST    DSSDY
     C*
     C** Indicate if Customer Number or Shortname; else return in error
     C           @KYST     IFEQ '*CNUM  '
     C                     MOVE 'A'       ZSTYPE
     C                     ELSE
     C           @KYST     IFEQ '*CSHT  '
     C                     MOVE 'B'       ZSTYPE
     C                     ELSE
     C                     MOVE '1'       ZERR
     C                     END
     C                     END
     C*
     C**  If there is a swift address on file use it , else check for
     C**  option B or D
     C           ZERR      IFNE '1'
     C           BBCSID    IFNE *BLANKS
     C                     MOVELBBCSID    ZSADD1
     C                     MOVELBBCRNM    ZCRNM                           M5T054
     C                     MOVELBBCRTN    ZCRTN                           M5T054
     C                     MOVE 'A'       ZSTAG
      *                                                                                       CSW201
      ** If ZDBEI is 'Y', check if SWIFT ID exists in BIC Directory.                          CSW201
      ** If Destination is a customer, set ZSTAG to 'B' else, set                             CSW201
      ** ZSTAG to 'D' and setup address fields.                                               CSW201
      *                                                                                       CSW201
     C           ZDBEI     IFEQ 'Y'                                                           CSW201
     C                     MOVELBBCSID    WSWFID                                              CSW201
     C                     EXSR SRSBIC                                                        CSW201
     C           PFMTA     IFEQ 'N'                                                           CSW201
     C           BBPCNB    IFEQ SRPCNB                                                        CSW201
     C           BBPCNB    ANDNE*BLANKS                                                       196345
     C           SRPCNB    ANDNE*BLANKS                                                       196345
     C           BBPCNB    OREQ ZSRCN                                                         CSW201
     C           BBCUST    OREQ SRPCNB                                                        CSW201
     C                     MOVEL'B'       ZSTAG                                               CSW201
     C                     MOVELBBCRNM    WWCRNM                                              CSW201
     C                     MOVELBBCRTN    WWCRTN                                              CSW201
     C                     MOVELWWADDR    ZSADD1                                              CSW201
     C                     ELSE                                                               CSW201
     C                     MOVEL'D'       ZSTAG                                               CSW201
     C                     MOVELBBCNA1    ZSADD1                                              CSW201
     C                     MOVELBBCNA2    ZSADD2                                              CSW201
     C                     MOVELBBCNA3    ZSADD3                                              CSW201
     C                     MOVELBBCNA4    ZSADD4                                              CSW201
     C                     ENDIF                                                              CSW201
     C                     ENDIF                                                              CSW201
     C                     ENDIF                                                              CSW201
     C                     ELSE
     C*
     C**  If ZDEST and ZSRCN are parent/child, child/parent or share the
     C**  same parent, then option B, else option D
     C           BBPCNB    IFEQ SRPCNB
     C           BBPCNB    ANDNE*BLANKS                                                       196345
     C           SRPCNB    ANDNE*BLANKS                                                       196345
     C           BBPCNB    OREQ ZSRCN
     C           BBCUST    OREQ SRPCNB
     C                     MOVELBBCRNM    WWCRNM
     C                     MOVELBBCRTN    WWCRTN
     C                     MOVELWWADDR    ZSADD1
     C                     MOVE 'B'       ZSTAG
     C                     ELSE
     C                     MOVELBBCNA1    ZSADD1
     C                     MOVELBBCNA2    ZSADD2
     C                     MOVELBBCNA3    ZSADD3
     C                     MOVELBBCNA4    ZSADD4
     C                     MOVE 'D'       ZSTAG
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C           ZMEND     TAG                             *** ZMEND ***
     C*
     C**  Termination Processing.
     C                     MOVE '1'       *INLR
     C                     RETRN
     C*
      /EJECT                                                                                  CSW201
      *****************************************************************                       CSW201
      *  SRSBIC - SR to check if SWIFT ID exists in BIC directory.    *                       CSW201
      *                                                               *                       CSW201
      *  Called by: Main processing                                   *                       CSW201
      *****************************************************************                       CSW201
      *                                                                                       CSW201
     C           SRSBIC    BEGSR                                                              CSW201
      *                                                                                       CSW201
     C                     MOVE 'Y'       PFMTA   1                                           CSW201
      *                                                                                       CSW201
     C                     CALL 'AOBICDV0'                                                    CSW201
     C                     PARM *BLANKS   PRTCD  10                                           CSW201
     C                     PARM           WSWFID 18                                           CSW201
     C                     PARM           PTYPE   7                                           CSW201
     C           MEBICD    PARM MEBICD    DSSDY                                               CSW201
      *                                                                                       CSW201
      ** If SWIFT ID exists in BIC directory and subtype is either                            CSW201
      ** 'BEID' or 'TRCO', set PFMTA to 'N'.                                                  CSW201
      *                                                                                       CSW201
     C           PTYPE     IFEQ '*BIC'                                                        CSW201
     C           BDSUBT    IFEQ 'BEID'                                                        CSW201
     C           BDSUBT    OREQ 'TRCO'                                                        CSW201
     C                     MOVEL'N'       PFMTA                                               CSW201
     C                     ENDIF                                                              CSW201
     C                     ENDIF                                                              CSW201
      *                                                                                       CSW201
     C                     ENDSR                                                              CSW201
     C*****************************************************************
     C/EJECT
     C*****************************************************************
** CPY@
(c) Misys International Banking Systems Ltd. 2001
