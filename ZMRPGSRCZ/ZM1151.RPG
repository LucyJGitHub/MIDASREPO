     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas Format SWIFT Address. Options A or D 2')
     F*****************************************************************
     F*                                                               *
     F*  Midas - MESSAGE MANAGEMENT UTILITIES.
     F*                                                               *
     F*  ZM1150 - FORMAT OF SWIFT ADDRESS.                            *
     F*                                                               *
     F* PURPOSE :                                                     *
     F*                                                               *
     F*  To set up either an A or D type address for use within a     *
     F*  SWIFT format message and to set a flag dependant on          *
     F*  whether the input is a customer number , shortname or        *
     F*  counterparty nostro.                                         *
     F*                                                               *
     F*  The destination input may be either:-                        *
     F*  1.Customer Number.                                           *
     F*  2.Customer Shortname.                                        *
     F*  3.Counterparty nostro code.                                  *
     F*                                                               *
     F*  If the input is a customer number an 'A' is moved to field   *
     F*  ZSTYPE , for a customer shortname a 'B' is moved in and for  *
     F*  a counterparty nostro a 'C' is moved in.                     *
     F*                                                               *
     F*  If the SWIFT address is present for the destination the      *
     F*  program will set up an A type address, otherwise a D type    *
     F*  address with the name and address.                           *
     F*                                                               *
     F*  Note: Any change applied to ZM1150 may well be applied to    *
     F*        this program. For that reason, the previous CAR tags   *
     F*        have not been removed                                  *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD031             Date 10Apr06               *
      *  Prev Amend No. CSW037A            Date 02May05               *
      *                 CSW036   *CREATE   Date 15Dec04               *
      * ----- CSW036 -------------------------------------------------*
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4.01.01 ----------------------------------------*
      *                 CSW202             Date 20May02               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSW201             Date 02May01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01401             Date 10Feb93               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CSW202 - SWIFT 2002 Standards Update                         *
      *  CSW201 - SWIFT 2001 Standards Update                         *
     F*  S01401 - The generation of MT500 SWIFT Messages if the       *
     F*           option is available.  NEW PROGRAM.                  *
     F*                                                               *
      *****************************************************************
      *                                                               *
      *  C R E A T I O N     P A R A M E T E R S                      *
     F*****************************************************************
     F/EJECT
     F*****************************************************************
      *
     E                    CPY@    1   1 80
      ** COPYRIGHT STATEMENT ARRAY.
     E*
     I*****************************************************************
     I*
     ISDCUST    E DSSDCUSTPD
     I** External Data structure for Customer Details.
     I*
     ISDCNST    E DSSDCNSTPD
     I** External Data structure for Counterparty Nostro Codes.
     I*
     IDSFDY     E DSDSFDY
     I** 1st DS for access program , short data structure.
     I*
     IDSSDY     E DSDSSDY
     I** 2nd DS for access program , long data structure.
     I*                                                                   CSW036
     IDSLDY     E DSDSLDY                                                 CSW036
     I** Very Large Data Structure for Access Objects                     CSW036
     I*
     IMEBICD    E DSMEBICDPD                                                                  CSW201
      ** Data Structure for BIC Directory                                                     CSW201
      *                                                                                       CSW201
     IMEBEIS    E DSMEBEISPD                                                                  CSW202
      ** Data Structure for Business Entity Identifier file                                   CSW202
      *                                                                                       CSW202
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*
     C**  Entry of address information
     C           *ENTRY    PLIST
     C                     PARM           ZDEST  12
     C                     PARM           ZSADD1 35
     C                     PARM           ZSADD2 35
     C                     PARM           ZSADD3 35
     C                     PARM           ZSADD4 35
     C                     PARM           ZSTAG   1
     C                     PARM           ZSTYPE  1
     C                     PARM           ZERR    1
     C                     PARM           ZCRNM  20
     C                     PARM           ZCRTN  10
     C                     PARM           ZDBEI   1                                           CSW201
     C*
     C                     MOVEACPY@      BIS@   80
     C*
     C                     MOVE *BLANKS   ZSADD1
     C                     MOVE *BLANKS   ZSADD2
     C                     MOVE *BLANKS   ZSADD3
     C                     MOVE *BLANKS   ZSADD4
     C                     MOVE *BLANKS   ZSTAG
     C                     MOVE *BLANKS   ZSTYPE
     C                     MOVE *BLANKS   ZERR
     C                     MOVE *BLANKS   ZCRNM
     C                     MOVE *BLANKS   ZCRTN
      *                                                                                       CSW202
      ** Check if SWIFT 2002 changes is on.                                                   CSW202
      *                                                                                       CSW202
     C                     CALL 'SWIF2002'                                                    CSW202
     C                     PARM *BLANKS   P@RTCD                                              CSW202
      *                                                                                       CSW202
     C           P@RTCD    IFEQ 'CSW2002'                                                     CSW202
     C                     MOVEL'Y'       CSW202  1                                           CSW202
     C                     ELSE                                                               CSW202
     C                     MOVEL'N'       CSW202                                              CSW202
     C                     ENDIF                                                              CSW202
      *                                                                   CSW036
      ** Access SAR details file to check for presence of CSW036          CSW036
      *                                                                   CSW036
     C                     CALL 'AOSARDR0'                                CSW036
     C                     PARM '       ' RTCDA   7                       CSW036
     C                     PARM '*VERIFY' RTCDB   7                       CSW036
     C                     PARM 'CSW036'  RTCDC   6                       CSW036
      *                                                                   CSW036
     C                     CLEARCSW036                                    CSW036
     C           RTCDA     IFEQ *BLANK                                    CSW036
     C                     MOVE 'Y'       CSW036  1                       CSW036
     C                     ENDIF                                          CSW036
     C*
      ** Initially check for counterparty nostro.
     C                     MOVELZDEST     @CNCD
      *
     C                     CALL 'AOCNSTR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY'    @OPTN   7
     C                     PARM           @CNCD   8
     C           SDCNST    PARM SDCNST    DSFDY
     C*
     C** If found set address type to 'C' (C/party nostro), and set up
     C** addresses
     C           @RTCD     IFEQ *BLANKS
     C                     MOVE 'C'       ZSTYPE
     C*
     C           AWSWID    IFNE *BLANKS
     C                     MOVELAWSWID    ZSADD1
     C                     MOVE 'A'       ZSTAG
      *                                                                                       CSW201
      ** If ZDBEI is 'Y', check if SWIFT ID exists in BIC Directory,                          CSW201
      ** then set ZSTAG to 'D' and setup address fields.                                      CSW201
      *                                                                                       CSW201
     C           ZDBEI     IFEQ 'Y'                                                           CSW201
     C                     MOVELAWSWID    WSWFID                                              CSW201
     C                     EXSR SRSBIC                                                        CSW201
     C           PFMTA     IFEQ 'N'                                                           CSW201
     C                     MOVEL'D'       ZSTAG                                               CSW201
     C                     MOVELAWCPNM    ZSADD1                                              CSW201
     C                     MOVELAWCNTN    ZSADD2                                              CSW201
     C                     ENDIF                                                              CSW201
     C                     ENDIF                                                              CSW201
     C                     ELSE
     C                     MOVELAWCPNM    ZSADD1
     C                     MOVELAWCNTN    ZSADD2
     C                     MOVE 'D'       ZSTAG
     C                     END
     C*
     C** Else assume that address is customer
     C                     ELSE
     C*
      ** Check for customer number or shortname
     C                     MOVELZDEST     @KEY1
      *
     C*********************CALL 'AOCUSTR0'
     C                     CALL 'AOCUSTR1'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY'    @OPTN   7
     C                     PARM           @KEY1  10
     C                     PARM *BLANKS   @KYST   7
     C***********SDCUST    PARM SDCUST    DSSDY
     C           SDCUST    PARM SDCUST    DSLDY
     C*
     C** Indicate if Customer Number or Shortname; else return in error
     C           @KYST     IFEQ '*CNUM  '
     C                     MOVE 'A'       ZSTYPE
     C                     ELSE
     C           @KYST     IFEQ '*CSHT  '
     C                     MOVE 'B'       ZSTYPE
     C                     ELSE
     C                     MOVE '1'       ZERR
     C                     END
     C                     END
     C*
     C** If there is a swift address on file use it , else use the
     C** customer name and address.
     C           ZERR      IFNE '1'
     C           BBCSID    IFNE *BLANKS
     C                     MOVELBBCSID    ZSADD1
     C           CSW036    IFEQ 'Y'                                       CSW036
     C           BBCSI2    ANDNE*BLANKS                                   CSW036
     C                     MOVELBBCSI2    ZSADD1                          CSW036
     C                     ENDIF                                          CSW036
     C                     MOVELBBCRNM    ZCRNM
     C                     MOVELBBCRTN    ZCRTN
     C                     MOVE 'A'       ZSTAG
      *                                                                                       CSW201
      ** If ZDBEI is 'Y', check if SWIFT ID exists in BIC Directory,                          CSW201
      ** then set ZSTAG to 'D' and setup address fields.                                      CSW201
      *                                                                                       CSW201
     C           ZDBEI     IFEQ 'Y'                                                           CSW201
     C                     MOVELBBCSID    WSWFID                                              CSW201
     C           CSW036    IFEQ 'Y'                                       CSW036
     C           BBCSI2    ANDNE*BLANKS                                   CSW036
     C                     MOVELBBCSI2    WSWFID                          CSW036
     C                     ENDIF                                          CSW036
     C                     EXSR SRSBIC                                                        CSW201
     C           PFMTA     IFEQ 'N'                                                           CSW201
     C                     MOVEL'D'       ZSTAG                                               CSW201
     C                     MOVELBBCNA1    ZSADD1                                              CSW201
     C                     MOVELBBCNA2    ZSADD2                                              CSW201
     C                     MOVELBBCNA3    ZSADD3                                              CSW201
     C                     MOVELBBCNA4    ZSADD4                                              CSW201
     C                     ENDIF                                                              CSW201
     C                     ENDIF                                                              CSW201
     C                     ELSE
     C                     MOVELBBCNA1    ZSADD1
     C                     MOVELBBCNA2    ZSADD2
     C                     MOVELBBCNA3    ZSADD3
     C                     MOVELBBCNA4    ZSADD4
     C                     MOVE 'D'       ZSTAG
     C                     END
     C                     END
     C*
     C                     END
     C*
     C**  Termination Processing.
     C                     SETON                     LR
     C*
      /EJECT                                                                                  CSW201
      *****************************************************************                       CSW201
      *  SRSBIC - SR to check if SWIFT ID exists in BIC directory.    *                       CSW201
      *                                                               *                       CSW201
      *  Called by: Main processing                                   *                       CSW201
      *****************************************************************                       CSW201
      *                                                                                       CSW201
     C           SRSBIC    BEGSR                                                              CSW201
      *                                                                                       CSW201
     C                     MOVE 'Y'       PFMTA   1                                           CSW201
      *                                                                                       CSW201
     C                     CALL 'AOBICDV0'                                                    CSW201
     C                     PARM *BLANKS   PRTCD  10                                           CSW201
     C                     PARM           WSWFID 18                                           CSW201
     C                     PARM           PTYPE   7                                           CSW201
     C           MEBICD    PARM MEBICD    DSSDY                                               CSW201
      *                                                                                       CSW201
      ** If SWIFT ID exists in BIC directory and subtype is either                            CSW201
      ** 'BEID' or 'TRCO', set PFMTA to 'N'.                                                  CSW201
      *                                                                                       CSW201
     C           PTYPE     IFEQ '*BIC'                                                        CSW201
      *                                                                                       CSW202
      ** Call AOBEISR0 when CSW202 is switched on                                             CSW202
      *                                                                                       CSW202
     C           CSW202    IFEQ 'Y'                                                           CSW202
     C                     CALL 'AOBEISR0'                                                    CSW202
     C                     PARM *BLANKS   P@RTCD  7                                           CSW202
     C                     PARM '*VERIFY' POPTN   7                                           CSW202
     C                     PARM BDSUBT    PBICD   4                                           CSW202
     C           MEBEIS    PARM MEBEIS    DSFDY                                               CSW202
     C                     ENDIF                                                              CSW202
      *                                                                                       CSW202
     C           BDSUBT    IFEQ 'BEID'                                                        CSW201
     C           CSW202    ANDEQ'N'                                                           CSW202
     C           BDSUBT    OREQ 'TRCO'                                                        CSW201
     C           CSW202    ANDEQ'N'                                                           CSW202
     C           P@RTCD    OREQ *BLANKS                                                       CSW202
     C           CSW202    ANDEQ'Y'                                                           CSW202
     C                     MOVEL'N'       PFMTA                                               CSW201
     C                     ENDIF                                                              CSW201
     C                     ENDIF                                                              CSW201
      *                                                                                       CSW201
     C                     ENDSR                                                              CSW201
     C*****************************************************************
     C/EJECT
     C*****************************************************************
** CPY@
(c) Misys International Banking Systems Ltd. 2001
