     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas Convert Price to SW fmt; Left Align')
     F*****************************************************************
     F*                                                               *
     F*  Midas - MESSAGE MANAGEMENT UTILITIES.
     F*                                                               *
     F*  ZM1190 - EXCHANGE RATE CONVERSION.                           *
     F*                                                               *
     F* PURPOSE :                                                     *
     F*                                                               *
     F*  To convert the exchange rate (ZPRIC) (15,8) to SWIFT         *
     F*  input format (ZSPRIC) (16A) AND ZOPRIC (21A)                 *
     F*                                                               *
     F*  2.Leading and trailing zeros must be blanked.                *
     F*  3.The decimal comma must have at least one leading digit.    *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*  LAST AMEND NO. S01401             DATE 10FEB93               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  S01401 - The generation of MT500 SWIFT Messages if the       *
     F*           option is available.  NEW PROGRAM.                  *
     F*                                                               *
      *****************************************************************
      *                                                               *
      *  C R E A T I O N     P A R A M E T E R S                      *
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*    90  - USED IN BLANKING OUT OF LEADING ZEROS                *
     F*    91  - USED TO END BLANK OF LEADING ZEROS                   *
     F*    92  - USED IN BLANKING OUT OF TRAILING ZEROS               *
     F*                                                               *
     F*****************************************************************
     E*
     E                    CPY@    1   1 80
     E** COPYRIGHT STATEMENT ARRAY.
     E*
     E                    ZEA        16  1
     E** ARRAY USED IN CONVERSION.
     E*
     E                    ZAA        16  1
     E** ARRAY USED IN CONVERSION
     E*
     E                    ZS1        15  1 0
     E                    ZS2        21  1
     E** ARRAYS USED IN #SEDIT
     E*
     E****************************************************************
     E/EJECT
     E****************************************************************
     I*
     IDS1         DS
     I** DATA STRUCTURE FOR SPLITTING ZAA
     I                                        1  16 ZAA
     I                                        1   8 ZAA1
     I*
     IDS2         DS
     I** DATA STRUCTURE FOR SPLITTING ZEA
     I                                        1  16 ZEA
     I                                        9  16 ZEA1
     I*
     I            DS
     I** DATA STRUCTURE FOR #SEDIT
     I                                        1  150WORK15
     I                                        1  150ZS1
     I*
     I****************************************************************
     I/EJECT
     I****************************************************************
     C*
     C** Entry of exchange rate.
     C           *ENTRY    PLIST
     C                     PARM           ZPRIC  158
     C                     PARM           ZSPRIC 16
     C                     PARM           ZOPRIC 21
     C*
     C                     MOVEACPY@      BIS@   80
     C*
     C                     SETOF                     909192
     C                     MOVE *BLANKS   ZSPRIC
     C                     MOVE *BLANKS   ZOPRIC
     C*
     C** SET UP WORK FIELDS FOR #SEDIT
     C                     MOVE ZPRIC     ZFLD15 150
     C                     Z-ADD8         ZDECS   10
     C                     MOVE 'J'       ZECODE  1
     C*
     C** CALL #SEDIT TO FORMAT OUTPUT FIELD
     C                     EXSR #SEDIT
     C*
     C** ENSURE AMOUNT IS POSITIVE BEFORE PROCESSING
     C           ZPRIC     IFLT 0
     C           ZPRIC     MULT -1        ZPRIC
     C                     END
     C*
     C** IF ZPRIC IS ZERO
     C           ZPRIC     IFEQ 0
     C                     MOVEL'0,'      ZSPRIC
     C*
     C                     ELSE
     C*
     C** REMOVE ALL LEADING ZEROS EXCEPT ONE PRECEEDING THE COMMA
     C                     MOVE ZPRIC     H15    15
     C                     MOVE H15       H8      8
     C                     MOVELH15       H7      7
     C                     MOVE H8        H9      9
     C                     MOVEL','       H9
     C                     MOVELH7        H16    16
     C                     MOVE H9        H16
     C*
     C                     MOVEAH16       ZAA
     C                     Z-ADD1         ZA      20
     C                     MOVE '0'       *IN91
     C                     MOVE '1'       *IN90
     C*
     C           *IN90     DOUEQ'0'
     C*
     C           ZAA,ZA    IFEQ '0'
     C           ZAA,ZA    OREQ ' '
     C                     MOVE '1'       *IN90
     C*
     C                     ELSE
     C*
     C                     MOVE '0'       *IN90
     C                     END
     C*
     C           *IN90     IFEQ '1'
     C                     MOVE ' '       ZAA,ZA
     C                     ADD  1         ZA
     C                     END
     C*
     C           *IN90     IFEQ '0'
     C           ZAA,ZA    ANDEQ','
     C                     MOVE '1'       *IN91
     C                     END
     C*
     C                     END
     C*
     C           *IN91     IFEQ '1'
     C           ZA        SUB  1         ZA
     C                     MOVE '0'       ZAA,ZA
     C                     END
     C*
     C** Blank out of trailing zeros.
     C** Any zeros after the decimal comma are removed.Indicator 92
     C** is set on for a trailing zero. If any other digit is found
     C** the processing of trailing zeros terminates.
     C*
     C                     MOVEAH16       ZEA
     C                     Z-ADD16        ZE      20
     C*
     C           ZEA,ZE    IFEQ '0'
     C                     MOVE '1'       *IN92
     C                     END
     C*
     C           *IN92     DOWEQ'1'
     C                     MOVE '0'       *IN92
     C                     MOVE ' '       ZEA,ZE
     C                     SUB  1         ZE
     C*
     C           ZEA,ZE    IFEQ '0'
     C                     MOVE '1'       *IN92
     C                     END
     C*
     C                     END
     C*
     C** Move to a 16A SWIFT format field for output.
     C                     MOVEAZAA1      ZEA
     C                     MOVEAZEA,ZA    ZSPRIC
     C*
     C                     END
     C*
     C** Termination Processing.
     C                     SETON                     LR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C** #SEDIT subroutine to insert a decimal point and sign into a  *
     C** numeric field and to blank out leading zeros (optionaly      *
     C** inserting commas).                                           *
     C**                                                              *
     C*****************************************************************
     C*
     C           #SEDIT    BEGSR
     C*
     C** SET UP WORK FIELDS
     C                     Z-ADD0         ZS1
     C                     MOVE ' '       ZS2
     C*
     C                     Z-ADD15        Z1      20
     C                     Z-ADD20        Z2      20
     C*
     C           15        SUB  ZDECS     ZINTS   20
     C*
     C* Check if numeric field is negative
     C           ZFLD15    IFLT *ZEROS
     C                     MOVE '-'       ZS2,21
     C                     Z-SUBZFLD15    ZFLD15
     C                     END
     C*
     C                     Z-ADDZFLD15    WORK15
     C*
     C** CHECK TO SEE IF THERE ARE ANY DECIMAL PLACES
     C           ZDECS     CABEQ0         ZS10
     C*
     C** SET UP DECIMALS
     C                     Z-ADD*ZEROS    ZCNT    10
     C           ZCNT      DOUEQZDECS
     C                     MOVE ZS1,Z1    ZS2,Z2
     C                     SUB  1         Z2
     C                     ADD  1         ZCNT
     C                     SUB  1         Z1
     C                     END
     C*
     C** PUT IN DECIMAL PLACE
     C                     MOVE '.'       ZS2,Z2
     C                     SUB  1         Z2
     C*
     C           ZS10      TAG                             ** ZS10 TAG **
     C*
     C** SET UP INTEGERS
     C                     Z-ADD*ZEROS    CNT3    10
     C           Z1        DOUEQ*ZEROS
     C                     MOVE ZS1,Z1    ZS2,Z2
     C                     SUB  1         Z1
     C                     SUB  1         Z2
     C*
     C** If edit code is 'J', insert a ',' before each group of three
     C** digits - not if beginning of array reached.
     C           Z2        IFGT *ZEROS
     C           ZECODE    ANDEQ'J'
     C                     ADD  1         CNT3
     C*
     C           CNT3      IFEQ 3
     C                     MOVE ','       ZS2,Z2
     C                     SUB  1         Z2
     C                     Z-ADD*ZEROS    CNT3
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C** PUT IN LEADING BLANKS
     C                     Z-ADD1         Z2
     C           ZS2,Z2    DOWEQ'0'
     C           ZS2,Z2    OREQ ' '
     C           ZS2,Z2    OREQ ','
     C                     MOVE ' '       ZS2,Z2
     C                     ADD  1         Z2
     C           Z2        CABEQ22        ZS20
     C                     END
     C*
     C** IF NO INTEGERS PUT IN LEADING ZERO
     C           ZS20      TAG                             ** ZS20 TAG **
     C*
     C           Z2        IFEQ 22
     C                     SUB  2         Z2
     C                     MOVE '0'       ZS2,Z2
     C*
     C                     ELSE
     C*
     C           ZS2,Z2    IFEQ '.'
     C                     SUB  1         Z2
     C                     MOVE '0'       ZS2,Z2
     C                     END
     C*
     C                     END
     C*
     C** SET UP OUTPUT FIELD
     C                     MOVEAZS2,Z2    ZOPRIC
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
** CPY@
(c) Misys International Banking Systems Ltd. 2001
