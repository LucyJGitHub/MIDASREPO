      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member (RPG)                                   *
      *                                                               *
      *  XXXXXXX - (/COPY Name)                                       *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. 246526             Date 21Mar07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 10Mar06               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  246526 - Applied fix 240619.                                 *
      *  240619 - Do not check RECI after call to AOACODR0.  It was   *
      *           'P' and caused suspense posting for method '05'.    *
      *  CSD027 - Change Customer Number to alpha.                    *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *                                                               *
      *****************************************************************
     C**                                                             *
     C**  FFSETL SR.  - Determines from input Settlement Details     *
     C**  ~~~~~~~~~~    whether MEMOS or PRONO processing is required*
     C**                In addition,whilst doing this,the associated *
     C**                settlement account ID is constructed.        *
     C**                                                             *
     C**  NB. FOR THIS SUBROUTINE TO WORK THE FOLLOWING FILES NEED   *
     C**  TO HAVE BEEN ACCESSED PREVIOUSLY : PF/TABTB20              *    S01117
     C****TO*HAVE*BEEN*ACCESSED*PREVIOUSLY*:*PF/TABTB11*******************S01117
     C***************************************PF/TABTB20*******************S01117
     C**                                     PF/TABTB40              *
     C**                                     PF/TABTB80              *
     C**                                     PF/SDBRCHPD(for Branch) *    S01117
     C***************************************PF/TABLETR*(for*Branch)******S01117
     C**                                                             *
     C****************************************************************
     C**                                                             *
     C******INPUTS****-*FFBRCD**(3,0S)*BRANCH*CODE************************S01117
     C**    INPUTS    - FFBRCA    (3A) BOOKING BRANCH CODE           *    S01117
     C******************FFCBCD**(6,0S)*CUSTOMER/BROKER*CODE***********                        CSD027
     C**                FFCBCD    (6A) CUSTOMER/BROKER CODE          *                        CSD027
     C**                FFBRKI    (1A) BROKER INDICATOR              *
     C**                FFSETP  (2,0S) SETTLEMENT TYPE               *
     C**                FFSETA   (12A) SETTLEMENT ACCOUNT            *
     C**                FFSETB    (3A) SETTLEMENT BRANCH             *    S01117
     C**                FFCCY     (3A) CURRENCY                      *
     C******************FFDASQ**(2,0S)*DEFAULT*A/C*SEQUENCE***************S01117
     C*********************(=*Start*A/c*Seq*no.*for*the*Branch*if*********S01117
     C**********************Multi-branching,*else*'01')*******************S01117
     C**                *INU2     (1A) RETAIL IS PRESENT             *
     C**                *INU6     (1A) INPUT CYCLE MODE              *
     C******OUTPUTS***-*FFCNUM**(6,0S)*CUSTOMER*NUMBER********************S01117
     C**    OUTPUTS   - FFBRCB    (3A) BRANCH CODE                   *    S01117
     C******************FFCNUM**(6,0S)*CUSTOMER NUMBER ***************                 S01117 CSD027
     C**                FFCNUM    (6A) CUSTOMER NUMBER               *                        CSD027
     C**                FFACOD  (4,0S) ACCOUNT CODE                  *
     C**                FFACSQ  (2,0S) ACCOUNT SEQUENCE              *
     C**                FFNOSN  (2,0S) NOSTRO NUMBER to access PRONO *
     C**                FFMIND    (1A) MEMOS UPDATE INDICATOR        *
     C**                                (= Y or N or E)              *
     C**                FFPIND    (1A) PRONO UPDATE INDICATOR        *
     C**                                (= Y or N)                   *
     C**                *IN89     (1A) CHAIN FAIL INDICATOR          *
     C**                                                             *
     C****************************************************************
     C*
     C           FFSETL    BEGSR                           ** FFSETL SR **
     C*
     C**  Input Parameters
     C*
     C***********          Z-ADDFFBRCD    FFBRCD  30                      S01117
     C                     MOVE FFBRCA    FFBRCA  3                       S01117
     C**********           Z-ADDFFCBCD    FFCBCD  60                                          CSD027
     C                     MOVE FFCBCD    FFCBCD  6                                           CSD027
     C                     MOVE FFBRKI    FFBRKI  1
     C                     Z-ADDFFSETP    FFSETP  20
     C**********           MOVE FFSETA    FFSETA 12                                           CGL029
     C                     MOVE FFSETA    FFSETA 18                                           CGL029
     C                     MOVE FFSETB    FFSETB  3                       S01117
     C                     MOVE FFCCY     FFCCY   3
     C                     MOVE FFCCY     FFNCCY
     C***********          Z-ADDFFDASQ    FFDASQ  20                      S01117
     C*
     C**  Output Parameters
     C*
     C                     MOVE *BLANKS   FFBRCB  3                       S01117
     C**********           Z-ADD*ZERO     FFCNUM  60                                          CSD027
     C                     MOVEL*BLANK    FFCNUM  6                                           CSD027
     C**********           Z-ADD*ZERO     FFACOD  40                                          CGL029
     C                     Z-ADD*ZERO     FFACOD 100                                          CGL029
     C                     Z-ADD*ZERO     FFACSQ  20
     C                     Z-ADD*ZERO     FFNOSN  20
     C                     MOVE 'N'       FFMIND  1
     C                     MOVE 'N'       FFPIND  1
     C                     SETOF                     89
     C*
     C**  If this subroutine is not being run
     C**  in Input Cycle , then finish
     C*
     C           *INU6     CABEQ'0'       FFEEND
     C*
     C**  1. Determine the output Customer number, Account code and
     C**  Account sequence depending on settlement type.
     C*
     C**  - Settlement types 01,02,07,08,12 : details from Nostro record
     C*
     C           FFSETP    IFEQ 01
     C           FFSETP    OREQ 02
     C           FFSETP    OREQ 07
     C           FFSETP    OREQ 08
     C           FFSETP    OREQ 12
     C*
     C**    - Set PRONO Update ind. to 'Y'
     C                     MOVE 'Y'       FFPIND
     C*
     C           FFSETA    IFEQ *BLANK
     C                     Z-ADD0         FFNOSN
     C                     SETON                     89
     C                     GOTO FFEEND
     C                     ELSE
     C*
     C                     MOVELFFSETA    FFNOSN
     C           FFNKY     CHAINTABLETLF             89
     C  N89      RECI      COMP 'D'                  8989
     C*
     C**   - Record missing
     C           *IN89     IFEQ '1'
     C                     Z-ADD0         FFNOSN
     C                     GOTO FFEEND
     C                     ELSE
     C*
     C                     MOVE BRCA      FFBRCB                          S01117
     C**********           Z-ADDCNUM      FFCNUM                                              CSD027
     C                     MOVELCNUM      FFCNUM                                              CSD027
     C                     Z-ADDACOD      FFACOD
     C                     Z-ADDACSQ      FFACSQ
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C**  - Settlement type 03
     C*
     C           FFSETP    IFEQ 03
     C*
     C                     MOVE FFBRCA    FFBRCB                          S01117
     C                     MOVE BICN      FFCNUM                          S01117
     C***********          Z-ADDBICN      FFCNUM                          S01117
     C                     Z-ADDCSAC      FFACOD
     C                     Z-ADD1         FFACSQ                          S01117
     C***********          Z-ADDFFDASQ    FFACSQ                          S01117
     C*
     C                     END
     C*
     C**  - Settlement type 04
     C*
     C           FFSETP    IFEQ 04
     C*
     C                     MOVE FFBRCA    FFBRCB                          S01117
     C**********           Z-ADDFFCBCD    FFCNUM                                              CSD027
     C                     MOVELFFCBCD    FFCNUM                                              CSD027
     C                     Z-ADDCADC      FFACOD
     C                     Z-ADD1         FFACSQ                          S01117
     C***********          Z-ADDFFDASQ    FFACSQ                          S01117
     C*
     C                     END
     C*
     C**  - Settlement type 05
     C*
     C           FFSETP    IFEQ 05
     C*
     C                     MOVE FFSETB    FFBRCB                          S01117
     C**********           Z-ADDFFSCNM    FFCNUM                                              CSD027
     C                     MOVELFFSCNM    FFCNUM                                              CSD027
     C                     Z-ADDFFSACD    FFACOD
     C                     Z-ADDFFSASQ    FFACSQ
     C***********                                                         S01117
     C***A/C*Sequence*may*be*zero*(in*a*multibranching*environment)*******S01117
     C***********                                                         S01117
     C***********FFACSQ    IFEQ *ZERO                                     S01117
     C***********          Z-ADDFFDASQ    FFACSQ                          S01117
     C***********          END                                            S01117
     C*
     C                     END
     C*
     C**  - Settlement type 06
     C*
     C           FFSETP    IFEQ 06
     C*
     C                     MOVE FFBRCA    FFBRCB                          S01117
     C                     MOVE BICN      FFCNUM                          S01117
     C***********          Z-ADDBICN      FFCNUM                          S01117
     C                     Z-ADDCOMS      FFACOD
     C                     Z-ADD1         FFACSQ                          S01117
     C***********          Z-ADDFFDASQ    FFACSQ                          S01117
     C*
     C                     END
     C*
     C**  - Settlement type 14 : details from LF/ACNUM
     C*
     C           FFSETP    IFEQ 14
     C*
     C           FFACMK    CHAINACNUM                89
     C  N89      RECI      COMP 'D'                  8989
     C*
     C**   - Record missing
     C           *IN89     CABEQ'1'       FFEEND
     C*
     C                     MOVE BRCA      FFBRCB                          S01117
     C**********           Z-ADDCNUM      FFCNUM                                              CSD027
     C                     MOVELCNUM      FFCNUM                                              CSD027
     C                     Z-ADDACOD      FFACOD
     C                     Z-ADDACSQ      FFACSQ
     C*
     C                     END
     C*
     C**  - Settlement type 15
     C*
     C           FFSETP    IFEQ 15
     C*
     C                     MOVE FFBRCA    FFBRCB                          S01117
     C**********           Z-ADDFFCBCD    FFCNUM                                              CSD027
     C                     MOVELFFCBCD    FFCNUM                                              CSD027
     C*
     C           FFBRKI    IFEQ 'Y'
     C**    - Default Broker Account code
     C                     Z-ADDDFBA      FFACOD
     C*
     C                     ELSE
     C**    - Default Customer Account code
     C                     Z-ADDDFCA      FFACOD
     C                     END
     C*
     C***********          Z-ADDFFDASQ    FFACSQ                          S01117
     C                     Z-ADD1         FFACSQ                          S01117
     C*
     C                     END
     C*
     C**  If the Retail module is not supported
     C**  by the system , then finish
     C*
     C           *INU2     CABEQ'0'       FFEEND
     C*
     C**  2. Access  Account codes table file
     C*
     C********** FFAKY     CHAINTABTE10F             89                                       CGL029
     C                     CALL 'AOACODR0'                                                    CGL029
     C                     PARM *BLANKS   PRTCD   7                                           CGL029
     C                     PARM '*KEY   ' POPTN   7                                           CGL029
     C                     PARM           FFACOD                                              CGL029
     C           SDACOD    PARM SDACOD    DSSDY                                               CGL029
     C*                                                                                       CGL029
     C** Database error handling                                                              CGL029
     C*                                                                                       CGL029
     C           PRTCD     IFEQ *BLANKS                                                       CGL029
     C                     MOVE *OFF      *IN89                                               CGL029
     C                     ELSE                                                               CGL029
     C                     MOVE *ON       *IN89                                               CGL029
     C                     ENDIF                                                              CGL029
     C**N89***** RECI      COMP 'D'                  8989                                     240619
     C*
     C**   - Record missing
     C           *IN89     CABEQ'1'       FFEEND
     C*
     C**  If a Retail account access LF/ACCNT
     C*
     C           ATYP      IFEQ 'R'
     C*
     C           FFACTK    CHAINACCNT                89
     C*
     C**  If record exists and is neither flagged for deletion
     C**   nor closed then set MEMOS Update ind. to 'Y'.
     C*
     C           *IN89     IFEQ '0'
     C           RECI      ANDNE'*'
     C           ACST      ANDNE'C'
     C                     MOVE 'Y'       FFMIND
     C*
     C**  If no live open Retail A/C,
     C**   set MEMOS Update ind. to 'E'.
     C*
     C                     ELSE
     C                     MOVE 'E'       FFMIND
     C                     END
     C*
     C                     END
     C*
     C           FFEEND    ENDSR                           **  FFEEND  **
     C*
     C*****************************************************************
      *****************************************************************
      * End of /COPY XXXXXXX                                          *
      *****************************************************************
