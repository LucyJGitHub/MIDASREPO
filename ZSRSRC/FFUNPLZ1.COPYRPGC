      *****************************************************************
      *                                                               *
      *  Midas - /COPY FFUNPLZ1 (RPG)                                 *
      *                                                               *
      *  FFUNPLZ1 - Midas Calc. unrealise P/L  for given contract     *
      *                                                               *
      *  Last Amend No. AR746662           Date 03May11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. 247517             Date 09May07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      *                 241147A            Date 07Nov06               *
      *                 241147             Date 26Oct06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CFF006             Date 28Nov00               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 140017             Date 13Apr00               *
      *                 136056  *CREATE    Date 13Apr00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR746662 - Discrepancy in premium amount in Currency OTC     *
      *             screen and Projected Available Balance Enquiry    *
      *             Applied fix 249171.                               *
      *             (Child: Artf746663)                               *
      *  249171 - UAT-DCI Entries error in posting of currency option *
      *         - premium entries of DCI deals are found in UAT.      *
      *         - Divide premium by 100 if processing type is 5.      *
      *  247517 - Reversed fix 241147.                                *
      *  241147A - Use old processing for the computation of p/l.     *
      *  241147 - Correct the computation of p/l.                     *
      *  CFF006 - Fractional Ticks Enhancement.                       *
      *  140017 - Reverse sign of unrealised P/L                      *
      *  136056 - Calculate option P/L as premium X - premium X       *
      *                                                               *
      *****************************************************************
      *                                                               *
      * Any program to use this subroutine will also require the      *
      * use of the /COPY ZSRSRC,ZYLDZ1 for necessary I-specs          *
      *            /COPY ZSRSRC,FFYTOPZ1 for necessary C-specs        *
      *            /COPY ZSRSRC,FFTVCLZ1 for necessary C-specs        *
      *                                                               *
      *****************************************************************
      *
     C           FFUNPL    BEGSR
      *
     C                     Z-ADD*ZERO     YIELDX 158
     C                     Z-ADD*ZERO     YIELDY 158
     C                     Z-ADD*ZERO     PRICEX 159
     C                     Z-ADD*ZERO     PRICEY 159
     C                     Z-ADD*ZERO     FFPOL  130
     C                     Z-ADD*ZERO     FFNTVL 130
      *
      * If processing type is in range 1 through 6
      *
     C           ISPT      IFLE 6
      *
      * Calculate the transaction value FOR PRICE X
      *
     C                     Z-ADDFFPRCX    FFPRIC
     C                     EXSR FFTVCL
     C                     Z-ADDFFTNVL    FFNTVL
      *
      * Calculate the transaction value FOR PRICE Y
      *
     C                     Z-ADDFFPRCY    FFPRIC
     C                     EXSR FFTVCL
     C                     SUB  FFTNVL    FFNTVL
     C           ISPT      IFEQ 5                                                             249171
     C           FFNTVL    DIV  100       FFNTVL    H                                         249171
     C                     ENDIF                                                              249171
      *****************************************************************                      241147A
      ***Calculate*Profit*or*Loss*to*involve*Minimum*Price*Fluctuation*               CFF006 241147A
      ***if*Fractional*ticks*processed*is*'Y'**************************               CFF006 241147A
      *****************************************************************               CFF006 241147A
     C********** FFTKDM    IFNE 100                                                   CFF006 241147A
     C********** FFMNPF    ANDNE0                                                     CFF006 241147A
     C********** FFTKVL    MULT FFMNPF    FFTKV2 158                                   CFF006 241147
     C********** FFTKVL    DIV  FFMNPF    FFTKV2 158                                  241147 241147A
     C**********           DIV  100       FFTKV2                                      241147 241147A
     C********** FFNTVL    MULT FFTKV2    FFPOL     H                                 CFF006 241147A
     C**********           ELSE                                                       CFF006 241147A
      *                                                                                       247517
      ** Calculate Profit or Loss to involve Minimum Price Fluctuation                        247517
      ** if Fractional ticks processed is 'Y'                                                 247517
      *                                                                                       247517
     C           FFTKDM    IFNE 100                                                           247517
     C           FFMNPF    ANDNE0                                                             247517
     C           FFTKVL    MULT FFMNPF    FFTKV2 158                                          247517
     C           FFNTVL    MULT FFTKV2    FFPOL     H                                         247517
     C                     ELSE                                                               247517
      *
      * Calculate Profit or Loss by multiplying the NET TRANSACTION
      * Value by the Tick Value
      *
     C           FFNTVL    MULT FFTKVL    FFPOL
     C**********           ENDIF                                                      CFF006 241147A
     C                     ENDIF                                                              247517
      *
      * If processing type is in range 7 through 8
      *
     C                     ELSE                            IF ISPT<=6
      *
      *   Calculate P/L for options as premium X - premium Y
      *
     C           ISPT      IFEQ 8
      *
      * Calculate the premium value FOR PRICE X
      *
     C                     Z-ADDFFPRCX    FFPRIC
     C                     EXSR FFPMCL
     C                     Z-ADDFFPREM    W130   130H
      *
      * Calculate the premium value FOR PRICE Y
      *
     C                     Z-ADDFFPRCY    FFPRIC
     C                     EXSR FFPMCL
      *
      * Profit = Premium Y - Premium X
      *
     C***********FFPREM    SUB  W130      FFPOL                           140017
     C           W130      SUB  FFPREM    FFPOL                           140017
     C                     GOTO UNPLZZ
     C                     ENDIF
      *
      * CALCULATE YIELDS FOR EACH PRICE
      *
     C           100       SUB  FFPRCX    YIELDX
     C           100       SUB  FFPRCY    YIELDY
      *
      * Calculate PriceX and PriceY by executing subroutine FFYTOP with
      * YieldX and YieldY.
      *
     C                     Z-ADDYIELDX    FFYLD
     C                     EXSR FFYTOP
     C                     Z-ADDPRICEC    PRICEX
      *
     C                     Z-ADDYIELDY    FFYLD
     C                     EXSR FFYTOP
     C                     Z-ADDPRICEC    PRICEY
      *
      * Calculate profit or loss.
      * That is, Premium = (PriceX - PriceY) x FFNOC x FFCTAM
      *
     C           FFCNTT    IFEQ 1
     C           FFCNTT    OREQ 2
     C           FFCNTT    OREQ 3
     C           FFCTAM    DIV  10000     WWW130 130
     C           PRICEX    MULT WWW130    WX302  302H
     C           PRICEY    MULT WWW130    WY302  302H
     C                     SUB  WY302     WX302
     C                     MULT 100       WX302
     C           WX302     MULT FFNOC     FFPOL     H
     C                     GOTO UNPLZZ
     C                     END
     C*
     C           PRICEX    SUB  PRICEY    W309   309
     C                     MULT FFCTAM    W309
     C           W309      MULT FFNOC     FFPOL     H
     C                     DIV  100       FFPOL     H
     C           UNPLZZ    TAG
      *
      * Set net transaction value to zero. Whilst this is not the real
      * value, the value is not used for these processing types and its
      * calculation if therefore unnecessary.
      *
     C                     Z-ADD*ZERO     FFNTVL
      *
     C                     END                             IF ISPT <=6
      *
     C                     ENDSR
      *
      *****************************************************************
      * End of /COPY FFUNPLZ1                                         *
      *****************************************************************
