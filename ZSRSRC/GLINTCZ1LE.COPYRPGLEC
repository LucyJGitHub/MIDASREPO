      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member                                         *
      *                                                               *
      *  GLINTCZ1LE - Calculate Interest Between Any Two Day Numbers  *
      *               (ILE)                                           *
      *                                                               *
      *  Any changes made to this /COPY member may also need to be    *
      *  applied to GLINTCZ1.                                         *
      *                                                               *
      *  Last Amend No. CDL093             Date 08Apr14               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER016A            Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CDL033             Date 15Mar05               *
      *                 BUG6293 *CREATE    Date 15Mar05               *
      *                                                               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL093 - MM interest calculation type 9                      *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER016A - German Interest Calculation: Upgarde of FGE059     *
      *           to Midas Plus                                       *
      *  CDL033 - Included this /COPY as part of delivery.  This was  *
      *           introduced by BUG6293 which is still outstanding.   *
      *  BUG6293- Duplicate copy of GLINTCZ1 converted to ILE version *
      *  218899 - Wrap the call to AOSARDR0 in this subroutine to     *
      *           avoid muliple calls as it slow down the close       *
      *           of business.                                        *
      *  192780 - Change the sequence of operation in computing the   *
      *           interest in SR/GLINTC (i.e. MULT first before DIV)  *
      *           to get a more precise interest amount.              *
      *           Note: This fix was done as part of DBA04 packaging  *
      *           so all objects that use it were recompiled as part  *
      *           of the packaging process. Therefore this fix number *
      *           does not appear in the headerboxes of those objects.*
      *  CIR004 - Dealing Calculation Method Change                   *
      *  121768 - First digit from interest is lost because work      *
      *           field is too short (for big amounts in TRL)         *
      *  126017 - Amendment to calculation method 6 to allow for the  *
      *           fact that not every day in the period falls in a    *
      *           leap year, so cannot just divide by 366. Fix is to  *
      *           amend SR/ZINTDY to adjust the no. of non-leap year  *
      *           days by 366/365 to give a factored no. of days in   *
      *           the interest period.   This avoids the rounding     *
      *           errors which could result from doing many seperate  *
      *           calculations.                                       *
      *  066550 - Changed to used standard subroutine ZINTDY to       *
      *           correctly calculate number of days for the          *
      *           calculation method.                                 *
      *                                                               *
      *****************************************************************
     C**
     C**   CALCULATION METHODS ARE -
     C**   1  ACTUAL/365
     C**   2  ACTUAL/360
     C**   3  360/360 (SOMETIMES CALLED 30/360)
     C**   4  365/365 (EXCLUDING 29/02)
     C**   5  365/360 (EXCLUDING 29/02)
     C**   6  ACTUAL/366
     C**   0  ACR/366                                                                        CER016A
     C**
     C**   REQUIRES SUBRS. - ZDATE2, ZSAVE, & ZRSTOR.
     C**
     C**
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. 192780             Date 18Jul01               *
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. CIR004             Date 20Jan00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 121768  SL         Date 19Feb98               *
      *                 126017             Date 17Nov97               *
     C**                 066550              DATE   03FEB94
     C**                 000000              DATE   00XXX00
     C**
     C*****************************************************************
     C**                                                              *
      *  192780 - Change the sequence of operation in computing the   *
      *           interest in SR/GLINTC (i.e. MULT first before DIV)  *
      *           to get a more precise interest amount.              *
      *           Note: This fix was done as part of DBA04 packaging  *
      *           so all objects that use it were recompiled as part  *
      *           of the packaging process. Therefore this fix number *
      *           does not appear in the headerboxes of those objects.*
      *  CIR004 - Dealing Calculation Method Change                   *
     C**     121768 - First digit from interest is lost because work
     C**              field is too short (for big amounts in TRL)
     C*  126017 - Amendment to calculation method 6 to allow for the  *
     C*           fact that not every day in the period falls in a    *
     C*           leap year, so cannot just divide by 366. Fix is to  *
     C*           amend SR/ZINTDY to adjust the no. of non-leap year  *
     C*           days by 366/365 to give a factored no. of days in   *
     C*           the interest period.   This avoids the rounding     *
     C*           errors which could result from doing many seperate  *
     C*           calculations.                                       *
     C*  066550 - Changed to used standard subroutine ZINTDY to       *
     C*           correctly calculate number of days for the          *
     C*           calculation method.                                 *
     C*                                                               *
     C*****************************************************************
     C**
     CSR   GLINTC        BEGSR                                                  ** GLINTC  **
     C**
     C**   CALCULATIONS TO DEFINE INPUT FIELDS.
     CSR                 Z-ADD     ZIBEG         ZIBEG             5 0          STARTING DATE
     CSR                 Z-ADD     ZIEND         ZIEND             5 0          FINISHING DATE
     CSR                 Z-ADD     ZICALC        ZICALC            1 0          CALC. BASIS
     CSR                 Z-ADD     ZIAMT         ZIAMT            15 0          PRINCIPAL
     CSR                 Z-ADD     ZIRATE        ZIRATE           11 7          RATE OF INTRST
      *
      ** Check if Dealing Calculation Method is installed
      ** (only if the access object has not been called previously)
      *
     C     IR004C        IFNE      'Y'
     C                   CALL      'AOSARDR0'
     C                   PARM      *BLANKS       @RTCD             7
     C                   PARM      '*VERIFY'     @OPTN             7
     C                   PARM      'CIR004'      @SARD             6
     C     @RTCD         IFEQ      *BLANKS
     C                   MOVE      'Y'           CIR004            1
     C                   ELSE
     C                   MOVE      'N'           CIR004
     C                   ENDIF
     C                   MOVE      *BLANKS       @RTCD
     C                   MOVE      'Y'           IR004C            1
     C                   ENDIF
     C**
     C*
     C**  CALCULATE NUMBER OF DAYS OF THE CALCULATION METHOD USED
      *                                                                                       CDL093
      ** Interest Calculation type 9                                                          CDL093
      *                                                                                       CDL093
     C*
     C                   Z-ADD     ZIBEG         ZZBEG
     C                   Z-ADD     ZIEND         ZZEND
     C                   MOVE      ZICALC        ZZCALC
     C                   EXSR      ZINTDY
     C**
     C**  CALCULATE INTEREST
      *
      ** Multiply Rate & Day No. first
      *
     C     ZIAMT         MULT      ZIRATE        ZIWRK1           30 9
     C     ZICALC        IFEQ      6
     C     ZICALC        OREQ      9                                                          CDL093
     C     #29FEB        ANDEQ     'Y'                                                        CDL093
     C     ZIWRK1        MULT      INT6DY        ZIWRK2
     C                   ELSE
     C     ZIWRK1        MULT      ZZINDY        ZIWRK2           30 9
     C                   END
      *
     C     ZICALC        IFEQ      2
     C     ZICALC        OREQ      3
     C     ZICALC        OREQ      5
     C     ZICALC        OREQ      7
     C     CIR004        ANDEQ     'Y'
     C     ZICALC        OREQ      0                                                         CER016A
     C     ZIWRK2        DIV(H)    36000         ZINTR            30 9
     C                   END
     C     ZICALC        IFEQ      1
     C     ZICALC        OREQ      4
     C     ZICALC        OREQ      9                                                          CDL093
     C     #29FEB        ANDNE     'Y'                                                        CDL093
     C     ZIWRK2        DIV(H)    36500         ZINTR
     C                   END
     C     ZICALC        IFEQ      6
     C     ZICALC        OREQ      9                                                          CDL093
     C     #29FEB        ANDEQ     'Y'                                                        CDL093
     C     ZIWRK2        DIV(H)    36600         ZINTR
     C                   END
     C*
     CSR                 ENDSR
     C**
      *****************************************************************
      * End of /COPY GLINTCZ1                                         *
      *****************************************************************
