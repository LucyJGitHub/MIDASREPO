     C*****************************************************************
     C*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
     C*  Last Amend No. BUG8514            Date 30Sep05               *
      * Midas Release 4.01 -------------------------------------------*
     C*  Prev Amend No. CRT007             Date 14Jan02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     C*                 104270             DATE 17Sep97               *
     C*                 CRT001  *CREATE    DATE 28FEB95               *
     C*                                                               *
     C*---------------------------------------------------------------*
     C*                                                               *
     C*  BUG8514- Issue regarding teller overrides.                   *
     C*  CRT007 - Cashier Branch Offline Checking                     *
     C*  104270 - Initialise override (@O) array element(s).          *
     C*  CRT001 - Retail Teller System                                *
     C*                                                               *
     C*****************************************************************
     C*                                                               *
     C*            RTBRCH - Branch Different Check                    *
     C*                                                               *
     C* FLDS USED  Input                                              *
     C*            @BRCD  - Branch code                               *
     C*            @TLBC  - Teller Branch code                        *
     C*            @V     - Validation indicators array               *
     C*            @DF    - Branch Different array position           *
     C*            @ACIN  - Account indicator                         *
     C*            CRT003 - Determine if CRT003 is installed          *   CRT007
     C*                                                               *
     C* FLDS USED  Output                                             *
     C*            @WI    - Warning  error index                      *
     C*            @TI    - Terminal error index                      *
     C*            @O     - Override indicators array                 *
     C*            @T     - Terminal message codes array              *
     C*            @W     - Warning  message codes array              *
     C*                                                               *
     C*****************************************************************
     C           RTBRCH    BEGSR                           ** RTBRCH **
     C*
     C** Define parameters
     C*
     C                     Z-ADD@WI       @WI     20
     C                     Z-ADD@TI       @TI     20
     C                     MOVEL@BRCA     @BRCA   3
     C                     MOVEL@TLBC     @TLBC   3
     C                     MOVEL@ACIN     @ACIN   2
     C                     MOVELCRT003    CRT003  1                       CRT007
     C                     MOVELRABRNM    RABRNM  4                       CRT007
     C*                                                                   104270
     C** Initialise override array element                                104270
     C*                                                                   104270
     C           PFNCD     IFNE '***'                                                        BUG8514
     C                     MOVE ' '       @O,@DF                          104270
     C                     ENDIF                                                             BUG8514
     C*
     C** If branch code different?
     C*
     C           @BRCA     IFNE @TLBC
     C*                                                                   CRT007
     C** If CRT003 is installed                                           CRT007
     C*                                                                   CRT007
     C           CRT003    IFEQ 'Y'                                       CRT007
     C*                                                                   CRT007
     C** Save last key to access the branch details                       CRT007
     C*                                                                   CRT007
     C                     MOVE RABRNM    @RABRN  4                       CRT007
     C                     MOVE *BLANKS   RABRNM                          CRT007
     C                     MOVEL@BRCA     RABRNM                          CRT007
     C*                                                                   CRT007
     C** Read in Account Branch Details                                   CRT007
     C*                                                                   CRT007
     C                     CALL 'AOBRCHR0'                                CRT007
     C                     PARM *BLANKS   @RTCD   7                       CRT007
     C                     PARM '*KEY  '  @OPTN   7                       CRT007
     C                     PARM RABRNM    @BRCD   3                       CRT007
     C           SDBRCH    PARM SDBRCH    DSFDY                           CRT007
      *                                                                   CRT007
      **  If record not found?                                            CRT007
      *                                                                   CRT007
     C           @RTCD     IFNE *BLANKS                                   CRT007
     C           *LOCK     IN   LDA                                       CRT007
     C                     Z-ADD31        DBASE                           CRT007
     C                     MOVEL'SDBRCHPD'DBFILE                          CRT007
     C                     MOVEL@BRCD     DBKEY                           CRT007
     C                     EXSR DBERR                                     CRT007
     C                     END                                            CRT007
     C*                                                                   CRT007
     C** If Branch Status is not 'O' or 'N' or ' '                        CRT007
     C*                                                                   CRT007
     C           A8BRST    IFNE 'O'                                       CRT007
     C           A8BRST    ANDNE'N'                                       CRT007
     C           A8BRST    ANDNE' '                                       CRT007
     C                     ADD  1         @WI                             CRT007
     C*                                                                   CRT007
     C** If transaction is a retail account transfer                      CRT007
     C*                                                                   CRT007
     C           FNCD      IFEQ '00A'                                     CRT007
     C*                                                                   CRT007
     C           @ACIN     IFEQ '1'                                       CRT007
     C                     MOVEL'BR.OFF1' @W,@WI                          CRT007
     C                     MOVE @ACIN     @W,@WI                          CRT007
     C                     ENDIF                                          CRT007
     C*                                                                   CRT007
     C           @ACIN     IFEQ '2'                                       CRT007
     C                     MOVEL'BR.OFF2' @W,@WI                          CRT007
     C                     MOVE @ACIN     @W,@WI                          CRT007
     C                     ENDIF                                          CRT007
     C*                                                                   CRT007
     C                     ELSE                                           CRT007
     C*                                                                   CRT007
     C                     MOVEL'BR.OFF'  @W,@WI                          CRT007
     C                     MOVE @ACIN     @W,@WI                          CRT007
     C*                                                                   CRT007
     C                     ENDIF                                          CRT007
     C*                                                                   CRT007
     C                     ENDIF                                          CRT007
     C*                                                                   CRT007
     C** Re-access the Account Branch Details                             CRT007
     C*                                                                   CRT007
     C                     MOVE *BLANKS   RABRNM                          CRT007
     C                     MOVE @RABRN    RABRNM                          CRT007
     C*                                                                   CRT007
     C           RABRNM    IFNE *BLANKS                                   CRT007
     C                     CALL 'AOBRCHR0'                                CRT007
     C                     PARM *BLANKS   @RTCD   7                       CRT007
     C                     PARM '*KEY  '  @OPTN   7                       CRT007
     C                     PARM RABRNM    @BRCD   3                       CRT007
     C           SDBRCH    PARM SDBRCH    DSFDY                           CRT007
      *                                                                   CRT007
      **  If record not found?                                            CRT007
      *                                                                   CRT007
     C           @RTCD     IFNE *BLANKS                                   CRT007
     C           *LOCK     IN   LDA                                       CRT007
     C                     Z-ADD31        DBASE                           CRT007
     C                     MOVEL'SDBRCHPD'DBFILE                          CRT007
     C                     MOVEL@BRCD     DBKEY                           CRT007
     C                     EXSR DBERR                                     CRT007
     C                     END                                            CRT007
      *                                                                   CRT007
     C                     ENDIF                                          CRT007
      *                                                                   CRT007
     C                     ENDIF                                          CRT007
      *                                                                   CRT007
     C*
     C** If override allowed?
     C*
     C           @V,@DF    IFEQ 'Y'
     C                     ADD  1         @WI
     C                     MOVEL'BR.DIF'  @W,@WI
     C                     MOVE @ACIN     @W,@WI
     C                     MOVE 'Y'       @O,@DF
     C                     END
     C*
     C** If no override allowed?
     C*
     C           @V,@DF    IFEQ 'N'
     C                     ADD  1         @TI
     C                     MOVEL'BR.DIF'  @T,@TI
     C                     MOVE @ACIN     @T,@TI
     C                     END
     C*
     C                     END
     C*
     C                     ENDSR
     C*****************************************************************
