      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member (RPG)                                   *
      *                                                               *
      *  RTCLOSC1 - Shadow Updates for Account Closure                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 165543             Date 06Jun06               *
      *  Prev Amend No. CSD027             Date 10Mar06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 106661  *CREATE    Date 17SEP97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  165543 - Wrong CCY for Account Closure.                      *
      *  CSD027 - Change Customer Number to alpha.                    *
      *  106661 - Passbook print missing details if account closed    *
      *           by teller module (073390).                          *
      *                                                               *
      *****************************************************************
      * FLDS USED  Input                                              *
      *            @CLOS  - Account closing.                          *
      *            @BRCA  - Branch code.                              *
      *            @CNUM  - Customer number.                          *
      *            @CCY   - Currency.                                 *
      *            @ACOD  - Account code.                             *
      *            @ACSQ  - Account sequence.                         *
      *            @NATN  - Teller transaction number.                *
      *            @INTRS - Interest.                                 *
      *            @CHRG  - Charges.                                  *
      *                                                               *
      * FLDS USED  Output                                             *
      *                                                               *
      *****************************************************************
     C           WRSAC2    BEGSR                           ** WRASC2 **
      *
      ** If account closure write interest, charge, tax records
     C           @CLOS     IFEQ 'Y'                        B1
      *
     C                     MOVEL@BRCA     BRCA
     C**********           Z-ADD@CNUM     CUSN                                                CSD027
     C                     MOVEL@CNUM     CUSN                                                CSD027
     C**********           MOVEL@CCY      CCYD                            165543
     C                     MOVELACCY      CCYD                            165543
     C                     Z-ADD@ACOD     ACDE
     C                     Z-ADD@ACSQ     ASNC
     C                     Z-ADDBJRDNB    PTDT
     C                     Z-ADDBJRDNB    VUDT
     C                     MOVEL'TT'      TRYP
     C                     MOVE @NATN     TNMR
     C                     MOVE 'N'       APBI
      *
     C                     MOVE *BLANKS   PASS
      *
      ** Interest
     C           @INTRS    IFNE 0                          B2
      *
      ** Get narrative
     C                     MOVE *BLANKS   NRTD
     C                     MOVE *BLANKS   NRTD12 12
     C                     MOVE *BLANKS   NRTD18 18
     C                     MOVEL'CLOSING 'NRTD12
     C                     MOVE 'NET '    NRTD12
     C                     MOVEL'INTEREST'NRTD18
     C                     MOVELNRTD12    NRTD
     C                     MOVE NRTD18    NRTD
      *
      *  Credit/debit
     C           @INTRS    IFLT 0                          B3
     C                     Z-SUB@INTRS    MVAM
     C                     Z-ADD1         DORC
     C                     ELSE                            X3
     C                     Z-ADD@INTRS    MVAM
     C                     Z-ADD0         DORC
     C                     ENDIF                           E3
      *
     C                     WRITERSACMTPO
      *
     C                     ENDIF                           E2
      *
      ** Charges/commission
     C           @CHRG     IFNE 0                          B2
      *
      ** Get narrative
     C                     MOVE *BLANKS   NRTD
     C                     MOVEL'CHARGES 'NRTD
      *
     C                     Z-ADD@CHRG     MVAM
     C                     Z-ADD0         DORC
     C                     WRITERSACMTPO
      *
     C                     ENDIF                           E2
      *
     C                     ENDIF                           E1
      *
     C                     ENDSR
      *
      *****************************************************************
      * End of /COPY RTCLOSC1                                         *
      *****************************************************************
