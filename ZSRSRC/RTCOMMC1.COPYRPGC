     C*****************************************************************
     C*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     C*  Last Amend No. 153001             Date 26Apr99               *
     C*  Prev Amend No. CRT001  *CREATE    DATE 28FEB95               *
     C*                                                               *
     C*---------------------------------------------------------------*
     C*                                                               *
     C*  153001 - A/c failed to close because minimum charge posted   *
     C*           so balance was not zero. Apply fix 126992.          *
     C*  126992 - Transfer the adding of commission here to RTCHRGC1  *
     C* (153001)  to ensure that @CHRG will not be overwritten.       *
     C*  CRT001 - Retail Teller System                                *
     C*                                                               *
     C*****************************************************************
     C           RTCOMM    BEGSR                           ** RTCOMM **
     C*
     C           RECKEY    KLIST
     C                     KFLD           CKEY
     C*
     C** ACCESS ICD RETAIL (TABTREXC)
     C                     OPEN TABTREXC
     C                     READ TABTREXC                 99
     C*
     C** ERROR IN TABTREXC
     C           *IN99     IFEQ '1'
     C                     MOVE '87'      DBASE            ***************
     C                     MOVE 'TBREX'   DBFILE           ** DB ERR 87 **
     C                     MOVEL*BLANKS   DBKEY            ***************
     C                     EXSR DBERR
     C                     END
     C*
     C                     CLOSETABTREXC
     C*
     C** ACCESS ICD RETAIL (TABTBRE)
     C                     OPEN TABTBRE
     C                     READ TABTBRE                  99
     C** ERROR IN TABTBRE
     C           *IN99     IFEQ '1'
     C                     MOVE '81'      DBASE            ***************
     C                     MOVE 'TBRE '   DBFILE           ** DB ERR 81 **
     C                     MOVEL*BLANKS   DBKEY            ***************
     C                     EXSR DBERR
     C                     END
     C*
     C                     CLOSETABTBRE
     C*
     C** CHECK IF COMMISSIONS ARE PRESENT OR NOT
     C*
     C*
     C** Commission on debit turnover
     C           CDTO      IFEQ 'Y'                        - B1
     C** Commission on highest overdraft
     C           CHOV      OREQ 'Y'
     C** Fixed commission
     C           FXAC      OREQ 'Y'
     C** Charge on number of daily statements
     C           CNDS      OREQ 'Y'
     C** Commission on debit interest
     C           CDRI      OREQ 'Y'
     C** Commission on minimum average balance
     C           CMBL      OREQ 'Y'
     C** Charge on inactive account
     C           CINA      OREQ 'Y'
     C** VAT processing
     C           VATP      OREQ 'Y'
     C*
     C                     MOVE 'A'       ALPHA   1
     C*
     C                     MOVELCNUM      @KEY1
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM           @KEY1  10
     C                     PARM *BLANKS   @KYST   7
     C           SDCUST    PARM SDCUST    DSSDY
     C*
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD82        DBASE            ***************
     C                     MOVEL'SDCUSTPD'DBFILE           ** DBERR 082 **
     C                     MOVEL@KEY1     DBKEY            ***************
     C                     EXSR DBERR
     C                     END
     C*
     C           STFQ      IFEQ 'E'                        - B2
     C                     MOVE *BLANKS   ZTABKY 12
     C                     MOVEL'81'      ZTABKY
     C                     MOVE BBCGRP    ZTABKY
     C*
     C                     OPEN TABCGU
     C           ZTABKY    CHAINTABCGU               99
     C           *IN99     IFEQ '1'                        - B3
     C                     MOVE '83'      DBASE            ***************
     C                     MOVE 'TABCG'   DBFILE           ** DB ERR 83 **
     C                     MOVELZTABKY    DBKEY            ***************
     C                     EXSR DBERR
     C                     END                             - E3
     C                     CLOSETABCGU
     C*
     C           DYSI      IFEQ 'Y'                        - B3
     C                     MOVELCEXF      ALPHA
      *
     C           ALPHA     IFNE 'E'                        - B4
     C                     MOVE *BLANKS   KBLK8
     C                     MOVE 'D'       KRECI
     C                     MOVE CCY       KSCCY
      *
     C                     OPEN RECCY
     C           RECKEY    CHAINRECCY                99
     C           *IN99     IFEQ '1'                        - B5
     C                     MOVE '84'      DBASE            ***************
     C                     MOVE 'RECCY'   DBFILE           ** DB ERR 84 **
     C                     MOVELCKEY      DBKEY            ***************
     C                     EXSR DBERR
     C                     END                             - E5
     C                     CLOSERECCY
     C*
     C                     END                             - E4
     C                     END                             - E3
     C                     END                             - E2
     C*
     C* Format address references from ACCNT
     C*
     C                     MOVELADREF1    WWARRY 10
     C                     MOVE ADREF2    WWARRY
     C                     MOVEAWWARRY    ADR              Address referen
      *
     C                     OPEN RECOM
     C           KLCOMM    CHAINRECOM                99
     C           *IN99     IFEQ '1'                        - B2
     C                     MOVE '85'      DBASE            ***************
     C                     MOVE 'RECOM'   DBFILE           ** DB ERR 85 **
     C                     MOVEL@ACCN     DBKEY            ***************
     C                     EXSR DBERR
     C                     END                             - E2
     C                     CLOSERECOM
      *
     C                     Z-ADD*ZEROS    @@CDYS 150
     C                     Z-ADD*ZEROS    @@CMBE 150
      *
     C                     Z-ADD0         @@CACC 150
     C                     ADD  @@CMBE    @@CACC
     C                     ADD  ACCEA     @@CACC
     C                     ADD  CDIE      @@CACC
     C                     ADD  IACE      @@CACC
     C                     ADD  FXDC      @@CACC
     C                     ADD  CHDBE     @@CACC
      *
      * CALCULATE THE COMMISSSION ON NUMBER OF DAILY STATEMENTS
     C           CEXF      IFNE 'E'                        - B2
      * COMMISSION IS APPLICABLE
      *
      * ASSUME ACCOUNT WILL BE FLAG FOR  FOR CLOSING
     C           NOIS      ADD  NOCC      NCNDS  150
     C                     Z-ADD1         ALTNAD  50
      *
      * Process all addresses (up to 10) found in ADR
      *
     C                     Z-ADD1         A       10       ADR index
     C           ' '       LOKUPADR,A                    99
     C           *IN99     IFEQ '0'                        - B3
     C                     Z-ADD10        ALTNAD
     C                     ELSE                            - X3
     C           A         SUB  1         ALTNAD
     C                     END                             - E3
      *
     C           NCNDS     MULT ALTNAD    NCNDS
     C                     ADD  1         LSTP
      *
     C           CEXF      IFNE *BLANKS                    - B3
     C           NCNDS     MULT NCEXF     NCNDS
     C                     ELSE                            - X3
     C           NCNDS     MULT DSFC      NCNDS
     C                     END                             - E3
     C           NCNDS     IFLT DSMN                       - B3
     C                     Z-ADDDSMN      NCNDS
     C                     END                             - E3
     C           NCNDS     IFGT DSMX                       - B3
     C                     Z-ADDDSMX      NCNDS
     C                     END                             - E3
     C                     Z-ADDNCNDS     @@CDYS
     C                     END                             - E2
     C                     ADD  @@CDYS    @@CACC
      *
      **     VAT PROCESSING?
     C           VATP      IFEQ 'Y'                        - B2
     C                     OPEN ABSFI101
     C           1         CHAINABSFI1F1             99
     C           *IN99     IFEQ '1'                        - B3
     C                     MOVE '86'      DBASE            ***************
     C                     MOVE 'ABSFI'   DBFILE           ** DB ERR 86 **
     C                     MOVEL'1'       DBKEY            ***************
     C                     EXSR DBERR
     C                     END                             - E3
     C                     CLOSEABSFI101
      *
     C           @@CACC    MULT XTVA      @@TVAT 150
     C                     ADD  @@TVAT    @@CACC
     C                     END                             - E2
      *
     C**********           ADD  @@CACC    @CHRG                           153001
      *
     C                     END                             - E1
      *
     C                     ENDSR
      *****************************************************************
