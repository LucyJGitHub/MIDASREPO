     C*****************************************************************
     C*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     C*  Last Amend No. 117112             Date 22Apr97               *
     C*  Prev Amend No. CRT001  *CREATE    Date 28FEB95               *
     C*                                                               *
     C*---------------------------------------------------------------*
     C*                                                               *
     C*  117112 - Cashier Interface.  Several problems corrected.     *
     C*  CRT001 - Retail Teller System                                *
     C*                                                               *
     C*****************************************************************
     C*                                                               *
     C*            RTCONV - Currency conversion                       *
     C*                                                               *
     C* CALLS             -                                           *
     C*                                                               *
     C* CALLED BY         -                                           *
     C*                                                               *
     C* FLDS USED  Input                                              *
     C*            @TCCY  - Transaction currency                      *
     C*            @TCAM  - Transaction currency amount               *
     C*            @TCDP  - Transaction currency decimal positions    *
     C*            @TCMD  - Transaction currency multiple/divide ind. *
     C*            @EXRT  - Conversion rate                           *
     C*            @EQCY  - Equivalent currency code                  *
     C*            @EQDP  - Equiv.currency decimal positions          *
     C*            @EQMD  - Equiv.currency multiple/divide ind.       *
     C*            @BSCY  - Base  currency code                       *
     C*                                                               *
     C* FLDS USED  Output                                             *
     C*            @WKAM  - Amount (work field)                       *
     C*            @WKDP  - Decimal places (work field)               *
     C*            @EQAM  - Equiv.currency amount                     *
     C*                                                               *
     C*****************************************************************
     C           RTCONV    BEGSR                           ** RTCONV **
      *
      ** Define input parameters
     C                     MOVEL@TCCY     @TCCY   3
     C                     MOVEL@EQCY     @EQCY   3
     C                     Z-ADD0         @EQAM  150
     C                     Z-ADD@TCAM     @TCAM  150
     C                     Z-ADD@EQDP     @EQDP   10
     C                     Z-ADD@TCDP     @TCDP   10
     C                     MOVEL@EQMD     @EQMD   1
     C                     MOVEL@TCMD     @TCMD   1
     C                     Z-ADD@EXRT     @EXRT  138
      *
      ** If both currencies are equal?
     C           @EQCY     IFEQ @TCCY
     C                     Z-ADD@TCAM     @WKAM  153
     C                     Z-ADD@TCAM     @EQAM  150
      *
      ** Else convert input amount into equiv. currency equivalent
     C                     ELSE
      *
      ** If trans.currency is same as base currency
     C           @TCCY     IFEQ @BSCY
      *
     C           @EQMD     IFEQ 'M'
     C           @TCAM     DIV  @EXRT     @WKAM     H
     C                     ELSE
     C           @TCAM     MULT @EXRT     @WKAM     H
     C                     END
      *
     C                     ELSE
      *
      ** Else if equiv.currency is same as base currency
     C           @EQCY     IFEQ @BSCY
      *
     C           @TCMD     IFEQ 'D'
     C           @TCAM     DIV  @EXRT     @WKAM     H
     C                     ELSE
     C           @TCAM     MULT @EXRT     @WKAM     H
     C                     END
      *
     C                     ELSE
      *
     C           PMODE     IFEQ 'P'                                       117112
     C           @TCMD     IFEQ 'M'                                       117112
     C           @TCAM     DIV  @EXRT     @WKAM     H                     117112
     C                     ELSE                                           117112
     C           @TCAM     MULT @EXRT     @WKAM     H                     117112
     C                     ENDIF                                          117112
      *                                                                   117112
     C                     ELSE                                           117112
      *                                                                   117112
     C           @TCAM     MULT @EXRT     @WKAM     H
      *
     C                     ENDIF                                          117112
      *                                                                   117112
     C                     END
      *
     C                     END
      *
      ** Align the result for the differences in decimal places
      *
      ** Obtain the difference in currency decimal places
     C           @EQDP     SUB  @TCDP     @WKDP   10
      *
     C           @WKDP     IFEQ 0
     C                     Z-ADD@WKAM     @EQAM  150H
     C                     END
      *
     C           @WKDP     IFEQ -1
     C           @WKAM     DIV  10        @EQAM  150H
     C                     END
      *
     C           @WKDP     IFEQ -2
     C           @WKAM     DIV  100       @EQAM  150H
     C                     END
      *
     C           @WKDP     IFEQ -3
     C           @WKAM     DIV  1000      @EQAM     H
     C                     END
      *
     C           @WKDP     IFEQ 1
     C           @WKAM     MULT 10        @EQAM     H
     C                     END
      *
     C           @WKDP     IFEQ 2
     C           @WKAM     MULT 100       @EQAM     H
     C                     END
      *
     C           @WKDP     IFEQ 3
     C           @WKAM     MULT 1000      @EQAM     H
     C                     END
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
