     C*****************************************************************
     C*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     C*  LAST AMEND NO. CRT001  *CREATE    DATE 28FEB95               *
     C*                                                               *
     C*---------------------------------------------------------------*
     C*                                                               *
     C*  CRT001 - Retail Teller System                                *
     C*                                                               *
     C*****************************************************************
     C*                                                               *
     C*            RTLCNV - Conversion subroutine.                    *
     C*                                                               *
     C* FLDS USED  Input                                              *
     C*            @TCCY  - Transaction currency                      *
     C*            @LCCY  - Local currency                            *
     C*            @TCAM  - Transaction currency amount               *
     C*            @TCDP  - Transaction currency decimal positions    *
     C*            @TCMD  - Transaction currency multiple/divide ind. *
     C*            @TCRT  - Transaction currency spot rate            *
     C*            @LCDP  - Local currency decimal positions          *
     C*            @LCMD  - Local currency multiple/divide ind.       *
     C*            @LCRT  - Local currency spot rate                  *
     C*                                                               *
     C* FLDS USED  Output                                             *
     C*            @WKAM  - Amount (work field)                       *
     C*            @WKDP  - Decimal places (work field)               *
     C*            @LCAM  - Local currency amount                     *
     C*****************************************************************
     C           RTLCNV    BEGSR                           ** RTLCNV **
      *
      ** Define input parameters
     C                     MOVEL@TCCY     @TCCY   3
     C                     MOVEL@LCCY     @LCCY   3
     C                     Z-ADD0         @LCAM  150
     C                     Z-ADD@TCAM     @TCAM  150
     C                     Z-ADD@LCDP     @LCDP   10
     C                     Z-ADD@TCDP     @TCDP   10
     C                     MOVEL@LCMD     @LCMD   1
     C                     MOVEL@TCMD     @TCMD   1
     C                     Z-ADD@LCRT     @LCRT  138
     C                     Z-ADD@TCRT     @TCRT  138
      *
      ** If both currencies are equal?
     C           @LCCY     IFEQ @TCCY
     C                     Z-ADD@TCAM     @LCAM  150
      *
      ** Else convert input amount into base currency equivalent
     C                     ELSE
      *
     C           @TCMD     IFEQ 'D'
     C           @TCAM     DIV  @TCRT     @WKAM  153H
     C                     ELSE
     C           @TCAM     MULT @TCRT     @WKAM     H
     C                     END
      *
      ** Convert base equivalent into local currency equivalent
     C           @LCMD     IFEQ 'D'
     C           @WKAM     DIV  @LCRT     @WKAM     H
     C                     ELSE
     C           @WKAM     MULT @LCRT     @WKAM     H
     C                     END
      *
      ** Align the result for the differences in decimal places
      *
      ** Obtain the difference in currency decimal places
     C           @LCDP     SUB  @TCDP     @WKDP   10
      *
     C           @WKDP     IFEQ 0
     C                     Z-ADD@WKAM     @LCAM
     C                     END
      *
     C           @WKDP     IFEQ -1
     C           @WKAM     DIV  10        @LCAM     H
     C                     END
      *
     C           @WKDP     IFEQ -2
     C           @WKAM     DIV  100       @LCAM     H
     C                     END
      *
     C           @WKDP     IFEQ -3
     C           @WKAM     DIV  1000      @LCAM     H
     C                     END
      *
     C           @WKDP     IFEQ 1
     C           @WKAM     MULT 10        @LCAM     H
     C                     END
      *
     C           @WKDP     IFEQ 2
     C           @WKAM     MULT 100       @LCAM     H
     C                     END
      *
     C           @WKDP     IFEQ 3
     C           @WKAM     MULT 1000      @LCAM     H
     C                     END
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
