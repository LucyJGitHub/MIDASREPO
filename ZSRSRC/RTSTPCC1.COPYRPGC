     C*****************************************************************
     C*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
     C*  Last Amend No. BUG8514            Date 30Sep05               *
      * Midas Release 4 --------------- Base -------------------------*
     C*  Prev Amend No. 184205             Date 18Apr01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     C*                 104270             DATE 17SEP97               *
     C*                 CRT001  *CREATE    DATE 28FEB95               *
     C*                                                               *
     C*---------------------------------------------------------------*
     C*                                                               *
     C*  BUG8514- Issue regarding teller overrides.                   *
     C*  184205 - Correction to validation                            *
     C*  104270 - Initialise override (@O) array element(s).          *
     C*  CRT001 - Retail Teller System                                *
     C*                                                               *
     C*****************************************************************
     C*                                                               *
     C*            RTSTPC - Stopped cheque check                      *
     C*                                                               *
     C* FLDS USED  Input                                              *
     C*            @SC    - Stopped cheque array position             *
     C*            @ACCN  - Account number                            *
     C*            @CQNF  - Cheque reference number                   *
     C*            @TNAM  - Transaction amount                        *
     C*            @WI    - Warning  error index                      *
     C*            @TI    - Terminal error index                      *
     C*            @O     - Override indicators array                 *
     C*                                                               *
     C* FLDS USED  Output                                             *
     C*            @WI    - Warning  error index                      *
     C*            @TI    - Terminal error index                      *
     C*            @O     - Override indicators array                 *
     C*            @T     - Terminal message codes array              *
     C*            @W     - Warning  message codes array              *
     C*                                                               *
     C*****************************************************************
     C           RTSTPC    BEGSR                           ** RTSTPC **
      *
      ** Define parameters
     C                     MOVE @CQNF     @CQNF   80
     C                     MOVE @RUND     @RUND   50
     C                     Z-ADD0         @STCR   30
     C                     Z-ADD@TNAM     @TNAM  130
      *                                                                   104270
      ** Initialise override array element                                104270
      *                                                                   104270
     C           PFNCD     IFNE '***'                                                        BUG8514
     C                     MOVE ' '       @O,@SC                          104270
     C                     ENDIF                                                             BUG8514
      *
      ** Stopped cheque key list
     C           KLSTPC    KLIST
     C                     KFLD           @BRCA
     C                     KFLD           @CNUM
     C**********           KFLD           @CCY                            184205
     C                     KFLD           ACCY                            184205
     C                     KFLD           @ACOD
     C                     KFLD           @ACSQ
     C                     KFLD           @STCR
      *
      ** Set low limit on LF/STOPC using account number & read a rcd.
     C           KLSTPC    SETLLSTOPC
     C                     READ STOPC                    85
      *
      ** If different account? set on indicator to exit loop
     C           CNUM      IFNE @CNUM
     C********** CCY       ORNE @CCY                                      184205
     C           CCY       ORNE ACCY                                      184205
     C           ACOD      ORNE @ACOD
     C           ACSQ      ORNE @ACSQ
     C           BRCA      ORNE @BRCA
     C                     SETON                         85
     C                     END
      *
      ** Do while end-of-file is not reached  OR  same account read
      ** OR matches found
     C           *IN85     DOWEQ'0'
      *
      ** Cheque should appear as stopped if expiry date is today, not just
      ** if it is later than today.
      *
      ** If record read is a 'live' record and expiry date >= run date?
     C           RECI      IFEQ 'D'
     C           EXPD      ANDGE@RUND
      *
      ** Set matching indicator off
     C                     SETOF                     86
      *
      ** Perform stop cheque match process
      *
      ** If first cheque is non-blank?
     C           FCHQ      IFNE *ZEROS
      *
      ** If cheque-no = from-cheque-no?  return as match
     C           @CQNF     IFEQ FCHQ
     C                     SETON                     86
     C                     END
      *
      ** If cheque-no = to-cheque-no?    return as match
     C           @CQNF     IFEQ TCHQ
     C                     SETON                     86
     C                     END
      *
      ** If to-cheque > cheque-no > from-cheque-no?  return as match
     C           @CQNF     IFGT FCHQ
     C           @CQNF     ANDLTTCHQ
     C                     SETON                     86
     C                     END
      *
     C                     END
      *
      ** If stop payment amount is not zero & same as transaction amount?
     C           STPA      IFNE 0
     C********** FCHQ      ANDEQ*ZEROS                                    184205
     C           FCHQ      IFEQ *ZERO                                     184205
     C           @TNAM     ANDEQSTPA                                      184205
     C           @CQNF     OREQ *ZERO                                     184205
     C           @TNAM     ANDEQSTPA
     C                     SETON                     86
     C                     END                                            184205
     C                     END
      *
      ** If matches found?  set on indicator to exit loop
     C           *IN86     IFEQ '1'
     C                     SETON                     85
      *
      ** If override allowed
     C           @V,@SC    IFEQ 'Y'
     C                     ADD  1         @WI
     C                     MOVEL'STOPCHQ' @W,@WI
     C                     MOVE 'Y'       @O,@SC
     C                     END
      *
      ** If override not allowed
     C           @V,@SC    IFEQ 'N'
     C                     ADD  1         @TI
     C                     MOVEL'STOPCHQ' @T,@TI
     C                     END
      *
     C                     END
      *
     C                     END
      *
      ** Continue loop?
     C           *IN85     IFEQ '0'
     C                     READ STOPC                    85
      *
      ** If different account? set on indicator to exit loop
     C           CNUM      IFNE @CNUM
     C********** CCY       ORNE @CCY                                      184205
     C           CCY       ORNE ACCY                                      184205
     C           ACOD      ORNE @ACOD
     C           ACSQ      ORNE @ACSQ
     C                     SETON                         85
     C                     END
      *
     C                     END
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
