     C*****************************************************************
     C*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     C*  LAST AMEND NO. 104270             DATE 17SEP97               *
     C*  PREV AMEND NO. CRT001  *CREATE    DATE 28FEB95               *
     C*                                                               *
     C*---------------------------------------------------------------*
     C*                                                               *
     C*  104270 - Initialise override (@O) array element(s).          *
     C*  CRT001 - Retail Teller System                                *
     C*                                                               *
     C*****************************************************************
     C*                                                               *
     C*            RTXRAT - Exchange rate check                       *
     C*                                                               *
     C* CALLS             -                                           *
     C*                                                               *
     C* CALLED BY         -                                           *
     C*                                                               *
     C* FLDS USED  Input                                              *
     C*            @EXRT  - Exchange rate                             *
     C*            @CCY1  - Currency to convert from                  *
     C*            @CCY2  - Currency to convert into                  *
     C*            @BSCY  - Base  currency code                       *
     C*            @V     - Validation indicators array               *
     C*            @XR    - Exchange override position                *
     C*                                                               *
     C* FLDS USED  Output                                             *
     C*            @WI    - Warning  error index                      *
     C*            @TI    - Terminal error index                      *
     C*            @O     - Override indicators array                 *
     C*            @T     - Terminal message codes array              *
     C*            @W     - Warning  message codes array              *
     C*****************************************************************
     C           RTXRAT    BEGSR                           ** RTXRAT **
      *
      ** Define input parameters
     C                     MOVEL@CCY1     @CCY1   3
     C                     MOVEL@CCY2     @CCY2   3
     C                     Z-ADD@EXRT     @EXRT  138
      *                                                                   104270
      ** Initialise override array element                                104270
      *                                                                   104270
     C                     MOVE ' '       @O,@XR                          104270
      *
      ** If 'from' currency is same as base currency
     C           @CCY1     IFEQ @BSCY
      *
      ** Perform setup for @ccy2
     C                     MOVEL@CCY2     @CCY
     C                     EXSR RTCCYS
     C                     Z-ADDTRT,@I    @CNVRT 138
      *
     C                     ELSE
      *
      ** Else if 'to' currency is same as base currency
     C           @CCY2     IFEQ @BSCY
      *
      ** Perform setup for @ccy1
     C                     MOVEL@CCY1     @CCY
     C                     EXSR RTCCYS
     C                     Z-ADDTRT,@I    @CNVRT 138
      *
     C                     ELSE
      *
      ** If none of the currencies is base currency
      *
      ** Perform setup for @ccy1
     C                     MOVEL@CCY1     @CCY
     C                     EXSR RTCCYS
     C                     MOVELTMD,@I    @MD1    1
     C                     Z-ADDTRT,@I    @SPRT1 138
      *
      ** Perform setup for @ccy2
     C                     MOVEL@CCY2     @CCY
     C                     EXSR RTCCYS
     C                     MOVELTMD,@I    @MD2    1
     C                     Z-ADDTRT,@I    @SPRT2 138
      *
      ** If mult/div indicator of ccy1 is 'D' - divide
     C           @MD1      IFEQ 'D'
      *
      ** If mult/div indicator of ccy2 is 'D'
     C           @MD2      IFEQ 'D'
      *
     C           @SPRT2    DIV  @SPRT1    @CNVRT 138H
      *
     C                     ELSE
      *
      ** If mult/div indicator of ccy2 is 'M'
     C           @MD2      IFEQ 'M'
      *
     C           @SPRT2    MULT @SPRT1    @SPRT1
     C           1         DIV  @SPRT1    @CNVRT    H
      *
     C                     END
      *
     C                     END
      *
     C                     ELSE
      *
      ** If mult/div indicator of ccy1 is 'M' - multiply
     C           @MD1      IFEQ 'M'
      *
      ** If mult/div indicator of ccy2 is 'D'
     C           @MD2      IFEQ 'D'
      *
     C           @SPRT2    MULT @SPRT1    @CNVRT 138H
      *
     C                     ELSE
      *
      ** If mult/div indicator of ccy2 is 'M'
     C           @MD2      IFEQ 'M'
      *
     C           @SPRT1    DIV  @SPRT2    @CNVRT    H
      *
     C                     END
      *
     C                     END
      *
     C                     END
      *
     C                     END
      *
     C                     END
      *
     C                     END
      *
      ** Obtain the difference between exch.rate & conversion rate
     C           @EXRT     SUB  @CNVRT    @DIFF  138
     C           @DIFF     IFLT 0
     C                     MULT -1        @DIFF
     C                     END
      *
      ** Compute 10% of conversion rate
     C           @CNVRT    DIV  10        @CNVRT    H
      *
      ** If difference is more than 10% of conversion rate
     C           @DIFF     IFGT @CNVRT
      *
      ** If xrate-chk is 'y' (override allowed)
     C           @V,@XR    IFEQ 'Y'
     C                     ADD  1         @WI
     C                     MOVEL'XRTWARN' @W,@WI
     C                     MOVE 'Y'       @O,@XR
     C                     END
      *
      ** If override not allowed - xrate-chk is 'n'
     C           @V,@XR    IFEQ 'N'
     C                     ADD  1         @TI
     C                     MOVEL'XRTWARN' @T,@TI
     C                     END
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
