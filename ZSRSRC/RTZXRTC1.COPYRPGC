      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member (RPG)                                   *
      *                                                               *
      *  RTZXRTC1 - Calculate cross-ccy multiply exchange rate        *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. 150864 *Create     Date 25Jan99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *   150864 - Calculate cross-ccy exchange rate                  *
      *                                                               *
      *****************************************************************
      *                                                               *
      *   Input  - ZRATE1 (13,8) Exchange rate 1 (to base/Euro)       *
      *            ZMDI1  (1A)   Multiply/divide ind. for ZRATE1      *
      *            ZRATE2 (13,8) Exchange rate 2 (to base/Euro)       *
      *            ZMDI2  (1A)   Multiply/divide ind. for ZRATE2      *
      *   Output - ZRATEX (13,8) Calculated multiply exchange rate    *
      *                                                               *
      *****************************************************************
      *
     C           RTZXRT    BEGSR                           ** RTZXRT **
      *
     C                     Z-ADD*ZEROS    ZRATEX 138
      *
      * Do not calculate with a zero rate
     C           ZRATE1    IFNE *ZEROS
     C           ZRATE2    ANDNE*ZEROS
      *
      * Mult./divide ind. the same
     C           ZMDI1     IFEQ ZMDI2
      *
      * If both multiply calc. is rate1/rate2.
      * If both divide calc. is rate2/rate1.
     C           ZMDI1     IFEQ 'M'
     C           ZRATE1    DIV  ZRATE2    ZRATEX    H
     C                     ELSE
     C           ZRATE2    DIV  ZRATE1    ZRATEX    H
     C                     ENDIF
      *
      * Mult./divide ind. not the same
     C                     ELSE
      *
      * If rate1 is mult. & rate2 is divide calc. is rate1 x rate2.
      * If rate1 is divide & rate2 is mult. calc. is 1/(rate1 x rate2).
     C           ZRATE1    MULT ZRATE2    ZRATEX    H
      *
     C           ZMDI1     IFEQ 'D'
     C           1         DIV  ZRATEX    ZRATEX    H
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      * End of /COPY RTZXRTC1                                         *
      *****************************************************************
