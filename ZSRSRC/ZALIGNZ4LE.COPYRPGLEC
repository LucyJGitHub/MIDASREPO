      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member (RPG)                                   *
      *                                                               *
      *  ZALIGNZ4 - To validate and right align numeric fields        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Any changes made to this /COPY member may also need to be    *
      *  incorporated in ZALIGNZ4LE.                                  *
      *                                                               *
      *  Last Amend No. 245692  *CREATE    Date 10Dec13               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  245692 - Incorrect validation of rates when the decimal se-  *
      *           parator in the Dealing ICD was defined as "," ins-  *
      *           tead of ".".                                        *
      *         - Applied for MD-23981.                               *
      *                                                               *
      *****************************************************************
      *
      ** ZALIGN SR. to validate and right align numeric fields.
      *
     C     ZALIGN        BEGSR
      *
     C                   SETOFF                                       929399
      *
      ** Save input field in array, ZA1.
      *
     C                   MOVEA     ZFIELD        ZA1
      *
      ** Calculation to define number structure control fields.
      *
     C                   Z-ADD     ZADIG         ZADIG             2 0
     C                   Z-ADD     ZADEC         ZADEC             1 0
      *
      ** Calculations to define/clear fields.
      *
     C                   MOVE      ' '           ZA2
     C                   MOVEA     ZA2           ZFIELD           16
     C                   Z-ADD     0             ZX                2 0
     C                   Z-ADD     0             ZY                2 0
      *
      ** Ensure required structure of field is valid.
      *
     C     ZADIG         ADD       ZADEC         ZZ                2 0
     C     ZZ            COMP      15                                 99
     C   99              GOTO      ZAEND
      *
      ** Loop to find decimal point, blanks and characters.
      *
     C     ZATAG1        TAG
     C     ZX            ADD       1             ZX
      *
      ** Check for decimal point.  Error if second decimal point.
      *
     C     ZA1(ZX)       COMP      BNDCSP                                 90
     C   90
     CAN 93              SETON                                        99
     C   99              GOTO      ZAEND
      *
      ** Check for 'M' constant.  Error if not last character entered
      *
     C     ZA1(ZX)       COMP      'M'                                    96
     C   96              DO
     C     ZX            COMP      15                                 99  99
     C   99              GOTO      ZAEND
     C     ZX            ADD       1             ZX
     C     ZA1(ZX)       COMP      ' '                                9999
     C   99              GOTO      ZAEND
     C     ZX            SUB       1             ZX
     C     ZX            COMP      9                                  99
     C   99              GOTO      ZAEND
     C                   END
      *
      ** Check for 'T' constant.  Error if not last character entered
      *
     C     ZA1(ZX)       COMP      'T'                                    97
     C   97              DO
     C     ZX            COMP      15                                 99  99
     C   99              GOTO      ZAEND
     C     ZX            ADD       1             ZX
     C     ZA1(ZX)       COMP      ' '                                9999
     C   99              GOTO      ZAEND
     C     ZX            SUB       1             ZX
     C     ZX            COMP      12                                 99
     C   99              GOTO      ZAEND
     C                   END
      *
      ** Clear alphameric constant from ZA1
      *
     C   96
     COR 97              MOVE      ' '           ZA1(ZX)
      *
      ** Check for blanks.  Bypass for first blank after a digit.
      *
     C     ZA1(ZX)       COMP      ' '                                    91
     C   91
     CAN 92              GOTO      ZATAG2
      *
      ** Check for numeric, if not '.' or ' '.
      *
     C  N90
     CANN91ZA1(ZX)       COMP      '0'                                  99
     C  N90
     CANN91
     CANN99ZA1(ZX)       COMP      '9'                                99
     C   99              GOTO      ZAEND
      *
      ** Store digits in array and how many.
      ** ZY, total of digits in the input field.
      ** ZZ, total of digits to the left of decimal point.
      *
     C  N90
     CANN91ZY            ADD       1             ZY                   92
     C  N90
     CANN91              MOVE      ZA1(ZX)       ZA2(ZY)
     C   90              Z-ADD     ZY            ZZ                   93  93
      *
     C     ZX            COMP      16                                   94
     C   94              GOTO      ZATAG1
      *
     C     ZATAG2        TAG
      *
      ** IF 'M' specified, multiply ZA1 by 1 million
      *
     C   96ZZ            ADD       6             ZZ
      *
      ** If 'T' specifoed, multiply ZA1 by 1 thousand
      *
     C   97ZZ            ADD       3             ZZ
      *
      ** Fill in zeros in any blanks left of decimal point
      *
     C   96
     CAN 93
     COR 97
     CAN 93ZZ            DOWGT     ZY
     C     ZY            ADD       1             ZY
     C                   MOVE      '0'           ZA2(ZY)
     C                   END
      *
      ** If constant specified with no decimal point zeroise blanks
      *
     C   96
     CANN93              DO        6
     C     ZY            ADD       1             ZY
     C                   MOVE      '0'           ZA2(ZY)
     C                   END
      *
     C   97
     CANN93              DO        3
     C     ZY            ADD       1             ZY
     C                   MOVE      '0'           ZA2(ZY)
     C                   END
      *
      ** If no decimal point found, set total field ZZ.
      *
     C  N93              Z-ADD     ZY            ZZ
      *
      ** Check for embedded blanks.
      *
     C   91
     CAN 92              MOVEA     ZA1(ZX)       ZFIELD
     C   91
     CAN 92ZFIELD        COMP      ' '                                9999
     C   99              GOTO      ZAEND
      *
      ** Ensure that number of digits entered either side
      ** of decimal point are not more than allowed.
      *
     C     ZZ            COMP      ZADIG                              99
     C     ZY            SUB       ZZ            ZX
     C  N99ZADEC         SUB       ZX            ZX                     9995
     C   99              GOTO      ZAEND
     C   95              GOTO      ZATAG4
      *
      ** Fill the trailing blanks with zeros.
      *
     C     ZY            ADD       ZX            ZZ
     C     ZATAG3        TAG
     C     ZY            ADD       1             ZY
     C                   MOVE      '0'           ZA2(ZY)
     C     ZY            COMP      ZZ                                   94
     C   94              GOTO      ZATAG3
      *
      ** Right-align the value by moving back to array, ZA1.
      *
     C     ZATAG4        TAG
     C                   MOVE      '0'           ZA1
     C                   Z-ADD     16            ZX
     C     ZATAG5        TAG
     C     ZY            COMP      0                                      94
     C   94              GOTO      ZATAG6
     C                   MOVE      ZA2(ZY)       ZA1(ZX)
     C     ZY            SUB       1             ZY
     C     ZX            SUB       1             ZX
     C                   GOTO      ZATAG5
     C     ZATAG6        TAG
      *
      ** Move validated and right-aligned number back into ZFIELD.
      *
     C                   MOVEA     ZA1           ZFIELD
      *
     C                   SETOFF                                       9697
      *
     C     ZAEND         ENDSR
      *
      ********************************************************************
