      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member (RPG)                                   *
      *                                                               *
      *  XXXXXXX - (/COPY Name)                                       *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. nnnnnn  *CREATE    Date ddmmmyy               *
      *  Prev Amend No. nnnnnn             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  nnnnnn - (change description)                                *
      *                                                               *
      *****************************************************************
     C****************************************************************
     C*                                                              *
     C*                                                              *
     C*     Midas - AMOUNT EDIT                                  *
     C*                                                              *
     C*     ZAMTE  -  CONVERT AMOUNT TO DISPLAY -38                  *
     C*               (BASED ON MM1015)                              *
     C*                                                              *
     C*     This routine converts a 15N amount with up to 3 implied  *
     C*     decimal places into a 12A field with thousands integer   *
     C*     half adjusted & leading zeroes suppressed.               *
     C*                                                              *
     C*     AMEND NO. XXXXXX                  DATE   XX/XX/XX        *
     C*                                                              *
     C*       CALLS: NONE                                            *
     C*                                                              *
     C*       INPUT: @@IAMT 15,0  INPUT AMOUNT                       *
     C*              @@CCY  3A    INPUT CURRENCY                     *
     C*              @@CDP  1,0   CURRENCY DECIMAL PLACES            *
     C*                                                              *
     C*      OUTPUT: @@AMNT 12A   AMOUNT TO DISPLAY                  *
     C*                                                              *
     C*        USES: @@I          INDEX TO ARRAY OF AMOUNT           *
     C*              @@C          INDEX TO ARRAY OF DISPLAY AMOUNT   *
     C*              @D           ARRAY OF AMOUNT                    *
     C*              @J           ARRAY OF DISPLAY AMOUNT            *
     C*                                                              *
     C*                                                              *
     C****************************************************************
     C*
     C           ZAMTE     BEGSR
     C*
     C*          INITIALISE ARRAYS
     C*
     C                     MOVEA*BLANKS   @D
     C                     MOVEA*BLANKS   @J
     C*
     C** IF -VE AMOUNT THEN MAKE +VE
     C           @@IAMT    IFLT 0
     C                     Z-SUB@@IAMT    @@IAMT
     C                     END
     C*
     C** Round up figures to the nearest thousand.
     C** & store initial array index value
     C           @@CDP     IFEQ 0                          B1
     C                     ADD  500       @@IAMT 150       *1
     C                     Z-ADD12        @@I              *1
     C                     END                             E1
     C*
     C           @@CDP     IFEQ 1                          B1
     C                     ADD  5000      @@IAMT           *1
     C                     Z-ADD11        @@I              *1
     C                     END                             E1
     C*
     C           @@CDP     IFEQ 2                          B1
     C                     ADD  50000     @@IAMT           *1
     C                     Z-ADD10        @@I              *1
     C                     END                             E1
     C*
     C           @@CDP     IFEQ 3                          B1
     C                     ADD  500000    @@IAMT           *1
     C                     Z-ADD9         @@I              *1
     C                     END                             E1
     C*
     C**  Place input amount into a 15 x 1 byte array
     C                     MOVEA@@AMDS    @D
     C*
     C**  Load output amount into a 12 x 1 byte array
     C                     Z-ADD12        @@C
     C           @@I       DOWGT0                          B1
     C                     MOVE @D,@@I    @J,@@C           *1
     C                     SUB  1         @@C     20       *1
     C                     SUB  1         @@I     20       *1
     C                     END                             E1
     C*
     C** Test successive elements of output amount setting zeroes to
     C** blanks until the first element or a 'non zero' is reached.
     C                     Z-ADD1         @@C
     C*
     C           @J,@@C    DOUNE'0'                        B1
     C           @J,@@C    ANDNE*BLANK                     *1
     C           @@C       OREQ 12                         *1
     C                     MOVE *BLANK    @J,@@C           *1
     C                     ADD  1         @@C              *1
     C                     END                             E1
     C*
     C** MOVE @J TO DISPLAY FIELD
     C                     MOVEA@J        @@AMNT 12
     C                     GOTO ZAMTE9
     C*
     C** DUMMY MOVES TO DEFINE FIELDS NOT DEFINED ELSEWHERE
     C                     MOVE @@CCY     @@CCY   3
     C                     MOVE @@CDP     @@CDP   10
     C           ZAMTE9    ENDSR                           **ZAMTE9**
      *****************************************************************
      * End of /COPY XXXXXXX                                          *
      *****************************************************************
