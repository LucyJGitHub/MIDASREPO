     C*****************************************************************
     C*                                                               *
     C*  ZAPORT - SUBROUTINE TO APPORTION AN AMOUNT                   *
     C*                                                               *
     C*  INPUT  - ZNPSN  (15,0) NOMINAL                               *
     C*           ZPNPSN (15,0) NOMINAL                               *
     C*           APIN (15,0) AP NUMERATOR                            *
     C************FROM*TABTG10******CDP***(1,0)*NOM.CCY*DEC.PLS.*******   S01117
     C*           FROM SECTYD:                                        *
     C*         - NMDP  (1,0) NOMINAL DEC. PLS.                       *
     C*           POWER8 ARRAY MUST BE INCLUDED IN CALLING PROGRAM    *
     C*                                                               *
     C*  OUTPUT - ZAPOUT   (15,0) APPORTIONED RESULT                  *
     C*                                                               *
     C*  CALLS - NONE                                                 *
     C*                                                               *
     C*****************************************************************
     C*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     C*  LAST AMEND NO. 055950             DATE 11MAY94               *
     C*  PREV AMEND NO. S01117             DATE 17JAN91               *
     C*                                                               *
     C*---------------------------------------------------------------*
     C*                                                               *
     C*  055950 - Due to split PI there is a rounding error on the    *
     C*           purchase interest key of 0.01 for the sale trade.   *
     C*           If ZNPSN is equal to ZPNPSN then ZAPOUT = ZAPIN     *
     C*           without doing anymore processing.                   *
     C*  S01117 - Misleading text (Multibranching)                    *
     C*  E00000 - Apportion an amount                                 *
     C*                                                               *
     C*****************************************************************
     C*
     C           ZAPORT    BEGSR                           **ZAPORT BEGSR
     C*
     C*** Define fields
     C*
     C                     Z-ADDZAPIN     ZAPIN  150
     C                     Z-ADDZAPOUT    ZAPOUT 150
     C                     Z-ADDZWR159    ZWR159 159
     C                     Z-ADDZWR151    ZWR151 151
     C                     Z-ADDZWR150    ZWR150 150
     C                     Z-ADDZDF150    ZDF150 150
     C                     Z-ADDZNPSN     ZNPSN  150
     C                     Z-ADDZPNPSN    ZPNPSN 150
     C*
     C           ZNPSN     IFEQ *ZERO
     C           ZAPIN     OREQ *ZERO
     C                     Z-ADD*ZERO     ZAPOUT
     C                     GOTO ZAPEND
     C                     END
     C*
     C           ZPNPSN    IFEQ *ZERO
     C           ZPNPSN    OREQ ZNPSN                                     055950
     C                     Z-ADDZAPIN     ZAPOUT
     C                     GOTO ZAPEND
     C                     END
     C*
     C*** Find LOW order truncation
     C*
     C           ZAPIN     DIV  ZPNPSN    ZWR159
     C           ZWR159    MULT ZPNPSN    ZWR150
     C           ZWR150    DIV  ZPNPSN    ZWR159
     C           ZWR159    MULT ZPNPSN    ZWR150
     C           ZAPIN     SUB  ZWR150    ZDF150
     C*
     C*** Find HIGH order truncation
     C*
     C           ZDF150    MULT ZNPSN     ZWR150
     C           ZWR150    DIV  ZNPSN     ZWR151
     C           ZWR151    MULT ZNPSN     ZWR150
     C           ZWR150    DIV  ZNPSN     ZWR151
     C           ZDF150    SUB  ZWR151    ZDF150
     C*
     C*** Apportion Difference
     C*
     C           ZDF150    DIV  ZPNPSN    ZAPOUT
     C           ZAPOUT    MULT ZNPSN     ZAPOUT                          E21097
     C*
     C*** Apportion Decimal part after division
     C*
     C           ZWR159    MULT ZNPSN     ZDF150    H
     C                     ADD  ZDF150    ZAPOUT
     C*
     C*** Apportion Integer part after division
     C*
     C           ZWR150    DIV  ZPNPSN    ZDF150
     C                     ADD  ZDF150    ZAPOUT
     C*
     C           ZAPEND    ENDSR                           *** ENDSR    ***
     C*
     C*****************************************************************
