     C**************************************************************************
     C*                                                                        *
     C*  ZBAKRT SR. TO DETERMINE THE FX RATE BETWEEN TWO CURRENCIES            *
     C*  ON A DATE IN THE PAST. THE RATE RETURNED IS ALWAYS A MULTIPLY         *
     C*  RATE BETWEEN THE FROM CURRENCY AND THE TO CURRENCY.                   *
     C*                                                                        *
     C*  INPUT  FIELDS                                                         *
     C*       ZCCYF   3A     FROM CURRENCY                                     *
     C*       ZCCYT   3A     TO CURRENCY                                       *
     C*       ZSDATE  50     PAST DATE                                         *
     C*                                                                        *
     C*  OUTPUT  FIELDS                                                        *
     C*       ZCRSRT 138     PAST FX RATE BETWEEN FROM AND TO CURRENCIES       *
     C*       ZERROR  1A     DATABASE ERROR IN SUBROUTINE                      *
     C*                                                                        *
     C*  INPUT  FILE                                                           *
     C*       PF/PMHSEXPD    HISTORIC FX RATES                                 *
     C*                                                                        *
     C*                                                                        *
     C*  CALLS:                                                                *
     C*       ZCCYCN                                                           *
     C*                                                                        *
     C*  CALLED BY:                                                            *
     C*                                                                        *
     C**************************************************************************
     C*                                                                        *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     C*  LAST AMEND NO. S01486 MD          DATE 03NOV94                        *
     C*                                                                        *
     C*------------------------------------------------------------------------*
     C*                                                                        *
     C*  S01486 - Portfolio Management Upgrade - rename file                   *
     C*                                                                        *
     C**************************************************************************
     C           ZBAKRT    BEGSR                           *** ZBAKRT ***
     C*
     C** DEFINE ALL FIELDS
     C                     MOVE ZCCYF     ZCCYF   3
     C                     MOVE ZCCYT     ZCCYT   3
     C                     Z-ADDZSDATE    ZSDATE  50
     C                     Z-ADDZCRSRT    ZCRSRT 138
     C                     MOVE 'N'       ZERROR  1
     C*
     C           ZWKEY     KLIST
     C                     KFLD           ZWCCY   3
     C                     KFLD           ZWDATE  50
     C*
     C** GET PAST RATE FOR 'FROM' CURRENCY
     C                     MOVE ZCCYF     ZWCCY
     C                     Z-ADDZSDATE    ZWDATE
     C***********ZWKEY     SETGTPMHSEXPP                                  S01486
     C           ZWKEY     SETGTPMHSEXPD                                  S01486
     C***********          READPPMHSEXPP                 90               S01486
     C                     READPPMHSEXPD                 90               S01486
     C*
     C           *IN90     IFEQ '1'
     C           Q8CCY     ORNE ZCCYF
     C                     MOVE *LOVAL    ZWDATE
     C***********ZWKEY     SETLLPMHSEXPP                                  S01486
     C           ZWKEY     SETLLPMHSEXPD                                  S01486
     C***********          READ PMHSEXPP                 90               S01486
     C                     READ PMHSEXPD                 90               S01486
     C           *IN90     IFEQ '1'
     C           Q8CCY     ORNE ZCCYF
     C                     MOVE 'Y'       ZERROR
     C                     END
     C                     END
     C*
     C                     Z-ADDQ8SPOT    ZRATEF
     C                     MOVE Q8MDV1    ZMDINF
     C                     Z-ADDQ8CDP     ZCDPF
     C*
     C** GET PAST RATE FOR 'TO' CURRENCY
     C                     MOVE ZCCYT     ZWCCY
     C                     Z-ADDZSDATE    ZWDATE
     C***********ZWKEY     SETGTPMHSEXPP                                  S01486
     C           ZWKEY     SETGTPMHSEXPD                                  S01486
     C***********          READPPMHSEXPP                 90               S01486
     C                     READPPMHSEXPD                 90               S01486
     C*
     C           *IN90     IFEQ '1'
     C           Q8CCY     ORNE ZCCYT
     C                     MOVE *LOVAL    ZWDATE
     C***********ZWKEY     SETLLPMHSEXPP                                  S01486
     C           ZWKEY     SETLLPMHSEXPD                                  S01486
     C***********          READ PMHSEXPP                 90               S01486
     C                     READ PMHSEXPD                 90               S01486
     C           *IN90     IFEQ '1'
     C           Q8CCY     ORNE ZCCYT
     C                     MOVE 'Y'       ZERROR
     C                     END
     C                     END
     C*
     C                     Z-ADDQ8SPOT    ZRATET
     C                     MOVE Q8MDV1    ZMDINT
     C                     Z-ADDQ8CDP     ZCDPT
     C*
     C** CALCULATE CROSS RATE
     C                     EXSR ZCCYCN
     C*
     C           ZCRSMD    IFEQ 'D'
     C           1         DIV  ZCRSRT    ZCRSRT
     C                     END
     C*
     C           BAKEND    ENDSR
     C********************************************************************
