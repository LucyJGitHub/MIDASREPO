     C**************************************************************************
     C*                                                                        *
     C*  ZBAKRT SR. TO DETERMINE THE FX RATE BETWEEN TWO CURRENCIES            *
     C*  ON A DATE IN THE PAST. THE RATE RETURNED IS ALWAYS A MULTIPLY         *
     C*  RATE BETWEEN THE FROM CURRENCY AND THE TO CURRENCY.                   *
     C*                                                                        *
     C*  Any changes made to this /COPY member may also need to be applied     *
     C*  to ZBAKRT.                                                            *
     C*                                                                        *
     C*  INPUT  FIELDS                                                         *
     C*       ZCCYF   3A     FROM CURRENCY                                     *
     C*       ZCCYT   3A     TO CURRENCY                                       *
     C*       ZSDATE  50     PAST DATE                                         *
     C*                                                                        *
     C*  OUTPUT  FIELDS                                                        *
     C*       ZCRSRT 138     PAST FX RATE BETWEEN FROM AND TO CURRENCIES       *
     C*       ZERROR  1A     DATABASE ERROR IN SUBROUTINE                      *
     C*                                                                        *
     C*  INPUT  FILE                                                           *
     C*       PF/PMHSEXPD    HISTORIC FX RATES                                 *
     C*                                                                        *
     C*                                                                        *
     C*  CALLS:                                                                *
     C*       ZCCYCN                                                           *
     C*                                                                        *
     C*  CALLED BY:                                                            *
     C*                                                                        *
     C*****************************************************************
     C*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL033             Date 15Mar05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. BUG6293 *CREATE    Date 15Mar05               *
      *                                                               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL033 - Included this /COPY as part of delivery.  This was  *
      *           introduced by BUG6293 which is still outstanding.   *
      *  BUG6293 - Duplicate copy of ZBAKRT converted to ILE version  *
      *  S01486 - Portfolio Management Upgrade - rename file          *
      *                                                               *
     C*****************************************************************
     C     ZBAKRT        BEGSR                                                  *** ZBAKRT ***
     C*
     C** DEFINE ALL FIELDS
     C                   MOVE      ZCCYF         ZCCYF             3
     C                   MOVE      ZCCYT         ZCCYT             3
     C                   Z-ADD     ZSDATE        ZSDATE            5 0
     C                   Z-ADD     ZCRSRT        ZCRSRT           13 8
     C                   MOVE      'N'           ZERROR            1
     C*
     C     ZWKEY         KLIST
     C                   KFLD                    ZWCCY             3
     C                   KFLD                    ZWDATE            5 0
     C*
     C** GET PAST RATE FOR 'FROM' CURRENCY
     C                   MOVE      ZCCYF         ZWCCY
     C                   Z-ADD     ZSDATE        ZWDATE
     C     ZWKEY         SETGT     PMHSEXPD
     C                   READP     PMHSEXPD                               90
     C*
     C     *IN90         IFEQ      '1'
     C     Q8CCY         ORNE      ZCCYF
     C                   MOVE      *LOVAL        ZWDATE
     C     ZWKEY         SETLL     PMHSEXPD
     C                   READ      PMHSEXPD                               90
     C     *IN90         IFEQ      '1'
     C     Q8CCY         ORNE      ZCCYF
     C                   MOVE      'Y'           ZERROR
     C                   END
     C                   END
     C*
     C                   Z-ADD     Q8SPOT        ZRATEF
     C                   MOVE      Q8MDV1        ZMDINF
     C                   Z-ADD     Q8CDP         ZCDPF
     C*
     C** GET PAST RATE FOR 'TO' CURRENCY
     C                   MOVE      ZCCYT         ZWCCY
     C                   Z-ADD     ZSDATE        ZWDATE
     C     ZWKEY         SETGT     PMHSEXPD
     C                   READP     PMHSEXPD                               90
     C*
     C     *IN90         IFEQ      '1'
     C     Q8CCY         ORNE      ZCCYT
     C                   MOVE      *LOVAL        ZWDATE
     C     ZWKEY         SETLL     PMHSEXPD
     C                   READ      PMHSEXPD                               90
     C     *IN90         IFEQ      '1'
     C     Q8CCY         ORNE      ZCCYT
     C                   MOVE      'Y'           ZERROR
     C                   END
     C                   END
     C*
     C                   Z-ADD     Q8SPOT        ZRATET
     C                   MOVE      Q8MDV1        ZMDINT
     C                   Z-ADD     Q8CDP         ZCDPT
     C*
     C** CALCULATE CROSS RATE
     C                   EXSR      ZCCYCN
     C*
     C     ZCRSMD        IFEQ      'D'
     C     1             DIV       ZCRSRT        ZCRSRT
     C                   END
     C*
     C     BAKEND        ENDSR
     C********************************************************************
