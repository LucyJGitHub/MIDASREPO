      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member (RPG)                                   *
      *                                                               *
      *  ZCBDYSZ1- Standard Subroutine to Calculate the Number of Days*
      *            between two Dates and the Number of Days in Year,  *
      *            for a Calculation Basis, (Dealing version).        *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. 247544             Date 16May07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CIR013             Date 07Apr05               *
      *                 CPK016             Date 13Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 182370             Date 05Jun01               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CIR004             Date 20Jan00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  247544 - Increase definition of W@NLP2 to match w/ ZINTDYZ2  *
      *           processing as amended by 242286.                    *
      *  CIR013 - Additional Calculation Method for FRA/IRS/CCF       *
      *  CPK016 - Release 5 Packaging.                                *
      *           - Place the call to AOSARDR0 in a protective IF     *
      *             to avoid unnecessary re-calls.                    *
      *  182370 - Incorrect calculation of number of days for calcu-  *
      *           lation basis 6. Amended /COPY such that for calcu-  *
      *           lation basis 6, the number of days per year is      *
      *           equal to 366 when the year in question is a leap    *
      *           year, and 365 days/year for non-leap years. Calcu-  *
      *           lation was made so that processing would be in-line *
      *           with that of /COPY ZINTDY.                          *
      *  CIR004 - Dealing Calculation Method Change                   *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Input:  ZCBSDT ( 5,0) Start Date                             *
      *          ZCBEDT ( 5,0) End Date                               *
      *          ZCBCBS ( 1,0) Calculation Basis                      *
      *                                                               *
      *  Output: ZCBDAY ( 5,0) Number of Days in Period               *
      *          ZCBDIV ( 3,0) Number of Days in Year                 *
      *                                                               *
      *  Calls:  ZDATE2 - Convert Midas Day No to Date Format         *
      *                                                               *
      *  Calculation Bases are:                                       *
      *                                                               *
      *  1 - Actual/365                                               *
      *  2 - Actual/360                                               *
      *  3 - 30/360                                                   *
      *  4 - Actual/365 (Excluding 29FEB)                             *
      *  5 - Actual/360 (Excluding 29FEB)                             *
      *  6 - Actual/366                                               *
      *                                                               *
      *****************************************************************
      *
     C           ZCBDYS    BEGSR                           *** ZCBDYS ***
      *
      ***  Initialise fields.
      *
     C                     Z-ADDZCBSDT    ZCBSDT  50
     C                     Z-ADDZCBEDT    ZCBEDT  50
     C                     Z-ADDZCBCBS    ZCBCBS  10
     C                     Z-ADD0         ZCBDAY  50
     C                     Z-ADD1         ZCBDIV  30
     C                     Z-ADD0         ZINT6                           182370
     C                     Z-ADD0         W@NLP1                          182370
     C                     Z-ADD0         W@NLP2                          182370
      *                                                                                       CPK016
      * Call access objects only once in a /COPY                                              CPK016
      *                                                                                       CPK016
     C           AORUN     IFEQ ' '                                                           CPK016
      *                                                                   CIR004
      ** Check if Dealing Calculation Method is installed                 CIR004
      *                                                                   CIR004
     C                     CALL 'AOSARDR0'                                CIR004
     C                     PARM *BLANKS   @RTCD   7                       CIR004
     C                     PARM '*VERIFY' @OPTN   7                       CIR004
     C                     PARM 'CIR004'  @SARD   6                       CIR004
     C           @RTCD     IFEQ *BLANKS                                   CIR004
     C                     MOVE 'Y'       CIR004  1                       CIR004
     C                     ELSE                                           CIR004
     C                     MOVE 'N'       CIR004                          CIR004
     C                     ENDIF                                          CIR004
     C                     MOVE *BLANKS   @RTCD                           CIR004
      *                                                                                       CPK016
      * Flag access objects as having been run                                                CPK016
      *                                                                                       CPK016
     C                     MOVE 'Y'       AORUN   1                                           CPK016
      *                                                                                       CPK016
     C                     ENDIF                                                              CPK016
      *
     C           ZCBCBS    IFEQ 9                                         CIR013
     C                     Z-ADDZCBSDT    @@DAYN                          CIR013
     C                     EXSR ZDATE9                                    CIR013
     C                     Z-ADD@@VDT     ZZDAT1                          CIR013
     C*                                                                   CIR013
     C                     Z-ADDZCBEDT    @@DAYN                          CIR013
     C                     EXSR ZDATE9                                    CIR013
     C                     Z-ADD@@VDT     ZZDAT2                          CIR013
     C*                                                                   CIR013
     C* #29FEB - Flag to represent the existence of a 29th Feb. between   CIR013
     C*          the start and end dates                                  CIR013
     C                     MOVE 'N'       #29FEB  1                       CIR013
     C*                                                                   CIR013
     C* ZZYYA - start year from data structure over ZZDAT1 in ZCBDYSZ2    CIR013
     C* #29WKY = incremental year initially at start year                 CIR013
     C                     Z-ADDZZYYA     #29WKY  40                      CIR013
     C*                                                                   CIR013
     C* Move to subfields of @@VDT - data structure from ZDATE9Z2 - 8,0   CIR013
     C                     Z-ADDZZYYA     @@YR                            CIR013
     C                     Z-ADD2         @@Z71M                          CIR013
     C                     Z-ADD29        @@Z71D                          CIR013
     C* #29YA - 29/02/start year (fixed workfield)                        CIR013
     C                     Z-ADD@@VDT     #29YA   80                      CIR013
     C* ZZYYB - end year from data structure over ZZDAT2 in ZCBDYSZ2      CIR013
     C                     Z-ADDZZYYB     @@YR                            CIR013
     C* #29YB - 29/02/end year (fixed workfield)                          CIR013
     C                     Z-ADD@@VDT     #29YB   80                      CIR013
     C*                                                                   CIR013
      * Loop round incrementing the year from start to end                CIR013
     C           #29WKY    DOUGTZZYYB                                     CIR013
     C* Check to see whether 29/02/xx is a valid date by checking         CIR013
     C* to see whether any of the years falling between the two dates     CIR013
     C* are leap years.                                                   CIR013
     C           ZZDAT1    IFLE #29YA                                     CIR013
     C           #29WKY    ANDEQZZYYA                                     CIR013
     C           ZZDAT2    ANDGE#29YA                                     CIR013
     C           #29WKY    ORGT ZZYYA                                     CIR013
     C           #29WKY    ANDLTZZYYB                                     CIR013
     C           ZZDAT2    ORGE #29YB                                     CIR013
     C           #29WKY    ANDEQZZYYB                                     CIR013
     C           ZZDAT1    ANDLE#29YB                                     CIR013
     C*                                                                   CIR013
     C           #29WKY    DIV  4         ZLYR30  30                      CIR013
     C                     MVR            ZLY     10                      CIR013
     C* Leap year                                                         CIR013
     C           ZLY       IFEQ 0                                         CIR013
     C                     MOVE 'Y'       #29FEB                          CIR013
     C                     LEAVE                                          CIR013
     C                     ENDIF                                          CIR013
     C                     ENDIF                                          CIR013
     C*                                                                   CIR013
     C                     ADD  1         #29WKY                          CIR013
     C                     ENDDO                                          CIR013
     C*                                                                   CIR013
     C                     ENDIF                                          CIR013
      *---------------------------------------------------------------*
      ***   First define the Number of Days in the Year.              *
      *---------------------------------------------------------------*
      *
      *****365*Days - Calculation*Basis = 1*or*4.**                       CIR013
      ***  365 Days - Calculation Basis = 1 or 4 or 9                     CIR013
      *
     C           ZCBCBS    IFEQ 1
     C           ZCBCBS    OREQ 4
     C           ZCBCBS    OREQ 9                                         CIR013
     C           #29FEB    ANDEQ'N'                                       CIR013
     C                     Z-ADD365       ZCBDIV
     C                     END
      *
      ***  360 Days - Calculation Basis = 2, 3 or 5.
      *
     C           ZCBCBS    IFEQ 2
     C           ZCBCBS    OREQ 3
     C           ZCBCBS    OREQ 5
     C           ZCBCBS    OREQ 7                                         CIR004
     C           CIR004    ANDEQ'Y'                                       CIR004
     C                     Z-ADD360       ZCBDIV
     C                     END
      *
      *****366*Days - Calculation*Basis = 6.**                            CIR013
      ***  366 Days - Calculation Basis = 6 or 9                          CIR013
      *
     C           ZCBCBS    IFEQ 6
     C           ZCBCBS    OREQ 9                                         CIR013
     C           #29FEB    ANDEQ'Y'                                       CIR013
     C                     Z-ADD366       ZCBDIV
     C                     END
      *
      *---------------------------------------------------------------*
      ***   Then calculate the Number of Days in the Period.          *
      *---------------------------------------------------------------*
      *
      ***  If the Start Date is Greater Than or Equal To the End Date,
      ***  output the Number of Days in Period Equal to zero.
      *
     C           ZCBSDT    IFGE ZCBEDT
     C                     Z-ADD0         ZCBDAY
     C                     GOTO ZCBEND
     C                     END
      *
      ***  The Actual Number of Days in the Period is the difference
      ***  between the Start Date and the End Date (Midas Date Format).
      *
     C           ZCBEDT    SUB  ZCBSDT    ZCBDAY
      *
      *****Calculation*Bases*1,*2*and*6:*Actual*Number*of*Days.********   182370
      ***  Calculation Bases 1, and 2: Actual Number of Days.             182370
      ***  ----------------------------------------------------
      *****For*these*three,*ZCBDAY*already*contains*the*final*result,**   182370
      ***  For these two, ZCBDAY already contains the final result,       182370
      ***  so no need for any further calculations.
      *
     C           ZCBCBS    IFEQ 1
     C           ZCBCBS    OREQ 2
     C***********ZCBCBS    OREQ 6                                         182370
     C                     GOTO ZCBEND
     C                     END
      *                                                                   182370
      ** Calculation Basis 6: Actual Number of Days.                      182370
      ** -------------------------------------------                      182370
      ** For calculation basis 6, take the number of days in year         182370
      ** to be equal to 366 days for leap years and 365 days for          182370
      ** non-leap years.                                                  182370
      *                                                                   182370
     C           ZCBCBS    IFEQ 6                                         182370
     C           ZCBCBS    OREQ 9                                         CIR013
     C           #29FEB    ANDEQ'Y'                                       CIR013
      *                                                                   182370
     C                     Z-ADDZCBSDT    @@DAYN  50                      182370
     C                     EXSR ZDATE9                                    182370
     C                     Z-ADD@@VDT     ZZDAT1                          182370
      *                                                                   182370
     C                     Z-ADDZCBEDT    @@DAYN                          182370
     C                     EXSR ZDATE9                                    182370
     C                     Z-ADD@@VDT     ZZDAT2                          182370
      *                                                                   182370
      ** Find the number of leap year and non-leap year days in the       182370
      ** interest period. The number of non-leap year days are then       182370
      ** adjusted by a factor of 366/365 to allow for a divisor of        182370
      ** 366. This figure is then added to the actual leap year days      182370
      ** to give a factored number of days in the interest period.        182370
      *                                                                   182370
     C                     Z-ADD0         WLEAP   50                      182370
     C                     Z-ADD0         WNLEAP  50                      182370
      *                                                                   182370
      ** If the start and end date are in the same year, check if the     182370
      ** year is a leap year and add number of interest days to the       182370
      ** relevant total                                                   182370
      *                                                                   182370
     C                     Z-ADD0         ZZWRK                           182370
     C           ZZYYA     IFEQ ZZYYB                                     182370
     C           ZZYYA     DIV  4         ZZWRK   80                      182370
     C                     MVR            ZZWRK                           182370
      *                                                                   182370
     C           ZZWRK     IFNE 0                                         182370
     C                     ADD  ZCBDAY    WNLEAP                          182370
     C                     ELSE                                           182370
     C                     ADD  ZCBDAY    WLEAP                           182370
     C                     ENDIF                                          182370
      *                                                                   182370
      ** else must break down the interest period into leap years         182370
      ** and non-leap years.                                              182370
      *                                                                   182370
     C                     ELSE                                           182370
      *                                                                   182370
      ** Save end date year value.                                        182370
      *                                                                   182370
     C                     Z-ADDZZDAT2    WZDAT2  80                      182370
     C                     Z-ADDZZYYB     WZZYYB  40                      182370
      *                                                                   182370
      ** Add one to starting year number.                                 182370
      *                                                                   182370
     C           ZZYYA     ADD  1         ZZYYB                           182370
      *                                                                   182370
      ** Get MIDAS day number for 1st Jan of year after start date.       182370
      *                                                                   182370
     C                     MOVE '01'      ZZMMB                           182370
     C                     MOVE '01'      ZZDDB                           182370
     C                     MOVE ZZDAT2    ZZDTIN                          182370
     C                     EXSR ZDAT10                                    182370
     C                     Z-ADDZZMDNO    WENDDT  50                      182370
     C           WENDDT    SUB  ZCBSDT    ZCBDAY                          182370
      *                                                                   182370
      ** Check if current year is a leap year.                            182370
      *                                                                   182370
     C                     Z-ADD0         ZZWRK                           182370
     C           ZZYYA     DIV  4         ZZWRK                           182370
     C                     MVR            ZZWRK                           182370
      *                                                                   182370
     C           ZZWRK     IFNE 0                                         182370
     C                     ADD  ZCBDAY    WNLEAP                          182370
     C                     ELSE                                           182370
     C                     ADD  ZCBDAY    WLEAP                           182370
     C                     ENDIF                                          182370
      *                                                                   182370
      ** Accumulate leap year and non-leap year days for complete         182370
      ** years in the interest period.                                    182370
      *                                                                   182370
     C           ZZYYB     DOWLTWZZYYB                                    182370
      *                                                                   182370
      ** Check if current year is a leap year.                            182370
      *                                                                   182370
C    C                     Z-ADD0         ZZWRK                           182370
     C           ZZYYB     DIV  4         ZZWRK                           182370
     C                     MVR            ZZWRK                           182370
      *                                                                   182370
     C           ZZWRK     IFNE 0                                         182370
     C                     ADD  365       WNLEAP                          182370
     C                     ELSE                                           182370
     C                     ADD  366       WLEAP                           182370
     C                     ENDIF                                          182370
      *                                                                   182370
      ** Add one to year number.                                          182370
      *                                                                   182370
     C                     ADD  1         ZZYYB                           182370
      *                                                                   182370
     C                     ENDDO                                          182370
      *                                                                   182370
      ** Finally calculate the days for the last part-year up to          182370
      ** the end date. Get MIDAS day number for 1st Jan of current year.  182370
      *                                                                   182370
     C                     MOVE '01'      ZZMMB                           182370
     C                     MOVE '01'      ZZDDB                           182370
     C                     MOVE ZZDAT2    ZZDTIN                          182370
     C                     EXSR ZDAT10                                    182370
     C                     Z-ADDZZMDNO    ZCBSDT                          182370
     C           ZCBEDT    SUB  ZCBSDT    ZCBDAY                          182370
      *                                                                   182370
      ** Check if year is a leap year.                                    182370
      *                                                                   182370
     C                     Z-ADD0         ZZWRK                           182370
     C           ZZYYB     DIV  4         ZZWRK                           182370
     C                     MVR            ZZWRK                           182370
      *                                                                   182370
     C           ZZWRK     IFNE 0                                         182370
     C                     ADD  ZCBDAY    WNLEAP                          182370
     C                     ELSE                                           182370
     C                     ADD  ZCBDAY    WLEAP                           182370
     C                     ENDIF                                          182370
      *                                                                   182370
     C                     ENDIF                                          182370
      *                                                                   CIR013
      * Factoring of non-leap year days is not required for method 9      CIR013
     C           ZCBCBS    IFEQ 9                                         CIR013
     C                     Z-ADDWNLEAP    W@NLP2                          CIR013
     C                     ELSE                                           CIR013
      *                                                                   182370
      ** Multiply non-leap year days by 366/365                           182370
      *                                                                   182370
     C           WNLEAP    MULT 366       W@NLP1 150                      182370
     C           W@NLP1    DIV  365       W@NLP2 159H                                         247544
     C********** W@NLP1    DIV  365       W@NLP2 157H                                  182370 247544
      *                                                                   CIR013
     C                     ENDIF                                          CIR013
      *                                                                   182370
      ** Add the result to the number of leap year days to give           182370
      ** the total days in interest period.                               182370
      *                                                                   182370
     C           WLEAP     ADD  W@NLP2    ZINT6  159H                                         247544
     C********** WLEAP     ADD  W@NLP2    ZINT6  157H                                  182370 247544
      *                                                                   182370
      ** Return original value of ZZDAT2 and ZZYYB.                       182370
      *                                                                   182370
     C                     Z-ADDWZDAT2    ZZDAT2                          182370
     C                     Z-ADDWZZYYB    ZZYYB                           182370
     C                     GOTO ZCBEND                                    182370
     C                     ENDIF                                          182370
      *
      ***  Calculation Bases 4 and 5: Actual Days, excluding 29th Feb.
      ***  -----------------------------------------------------------
      ***  Take Actual Days and subtract all the 29th February Days:
      ***  This is done by checking the ZF29 array, (Midas Dates for
      ***  29th February), to get the indices of the first 29FEB after
      ***  each of the Start Date and The End Date. The difference is
      ***  the Number of 29FEBs in the Period.
      *
     C           ZCBCBS    IFEQ 4
     C           ZCBCBS    OREQ 5
     C           ZCBCBS    OREQ 9                                         CIR013
     C           #29FEB    ANDEQ'N'                                       CIR013
     C                     Z-ADD1         ZS      20
     C                     Z-ADD0         ZI1     20
     C                     Z-ADD0         ZI2     20
      *
      ***  Find Index of first FEB29 after Start Date.
      ***  (if Start Date is later than last FEB29 in array then set
      ***  ZI1 to last index + 1 (= 33) ).
      *
     C           ZF29,32   IFLT ZCBSDT
     C                     Z-ADD33        ZI1
     C                     ELSE
      *
     C           ZI1       DOUNE0
     C           ZF29,ZS   IFGT ZCBSDT
     C                     Z-ADDZS        ZI1
     C                     ELSE
     C                     ADD  1         ZS
     C                     END
     C                     END
     C                     END
      *
      ***  Continue, to find Index of first FEB29 after End Date. Note:
      ***  leave ZS as is, so as to continue in array from that point.
      ***  (if End Date is later than last FEB29 in array then set
      ***  ZI2 to last index + 1 (= 33) ).
      *
     C           ZF29,32   IFLT ZCBEDT
     C                     Z-ADD33        ZI2
     C                     ELSE
      *
     C           ZI2       DOUNE0
     C           ZF29,ZS   IFGT ZCBEDT
     C                     Z-ADDZS        ZI2
     C                     ELSE
     C                     ADD  1         ZS
     C                     END
     C                     END
     C                     END
      *
     C           ZI2       SUB  ZI1       ZS
     C                     SUB  ZS        ZCBDAY
     C                     GOTO ZCBEND
     C                     END
      *
      ***  Calculation Basis 3: 30 Day months, (including '30th Feb.')
      ***  -----------------------------------------------------------
      *
      ***  Convert Input Dates to DD/MM/YY or MM/DD/YY Format.
      *
     C                     Z-ADDZCBSDT    ZDAYNO
     C                     EXSR ZDATE2
      *
     C                     MOVE ZDATE     ZYR1    20
     C           *IN98     IFEQ '0'
     C                     MOVELZDATE     ZDY1    20
     C                     MOVE ZDATE     ZWRK4   40
     C                     MOVELZWRK4     ZMT1    20
     C                     ELSE
     C                     MOVELZDATE     ZMT1
     C                     MOVELZDATE     ZWRK4
     C                     MOVE ZWRK4     ZDY1
     C                     END
      *
     C                     Z-ADDZCBEDT    ZDAYNO
     C                     EXSR ZDATE2
      *
     C                     MOVE ZDATE     ZYR2    20
     C           *IN98     IFEQ '0'
     C                     MOVELZDATE     ZDY2    20
     C                     MOVE ZDATE     ZWRK4   40
     C                     MOVELZWRK4     ZMT2    20
     C                     ELSE
     C                     MOVELZDATE     ZMT2
     C                     MOVELZDATE     ZWRK4
     C                     MOVE ZWRK4     ZDY2
     C                     END
      *
      ***  Change Days to 30 if 31.
      *
     C           ZDY1      IFEQ 31
     C                     MOVE 30        ZDY1
     C                     END
     C           ZDY2      IFEQ 31
     C                     MOVE 30        ZDY2
     C                     END
      *
      ***  To keep the Day Difference positive, if Start Day is Greater
      ***  Than End Day, add 30 to End Day and reduce End Month by 1,
      ***  (i.e. add one month's worth of days to End Day).
      *
     C           ZDY1      IFGT ZDY2
     C                     ADD  30        ZDY2
     C                     SUB  1         ZMT2
     C                     END
      *
      ***  Calculate Day Difference.
      *
     C           ZDY2      SUB  ZDY1      ZDYDF   50
      *
      ***  Similarly, if Start Month Greater Than End Month, add 12
      ***  to End Month and reduce End Year by 1.
      *
     C           ZMT1      IFGT ZMT2
     C                     ADD  12        ZMT2
     C           ZYR2      IFEQ 00
     C                     Z-ADD99        ZYR2
     C                     ELSE
     C                     SUB  1         ZYR2
     C                     END
     C                     END
      *
      ***  Calculate Month Difference in Days.
      *
     C           ZMT2      SUB  ZMT1      ZMTDF   50
     C                     MULT 30        ZMTDF
      *
      ***  Subtract Start Year from End Year: If result Less Than zero
      ***  then End Date must be in the following century, so add 100.
      *
     C           ZYR2      SUB  ZYR1      ZYRDF   50
     C           ZYRDF     IFLT 0
     C                     ADD  100       ZYRDF
     C                     END
      *
      ***  Calculate Year Difference in Days.
      *
     C                     MULT 360       ZYRDF
      *
      ***  Total the three differences to get the Number of Days in
      ***  the Period.
      *
     C           ZDYDF     ADD  ZMTDF     ZCBDAY
     C                     ADD  ZYRDF     ZCBDAY
      *
      *---------------------------------------------------------------*
      *
     C           ZCBEND    TAG                             *** ZCBEND ***
      *
     C                     ENDSR
      *
      *****************************************************************
      * End of /COPY ZCBDYSZ1                                         *
      *****************************************************************
