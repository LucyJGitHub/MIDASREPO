      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member (RPG)                                   *
      *                                                               *
      *  ZCCYXR_ILE - Convert Amount in Ccy to Another Directly       *
      *                                                               *
      *  Function: TO CONVERT AN AMOUNT IN ONE CURRENCY TO ANOTHER    *
      *            CURRENCY USING AN EXCHANGE RATE BETWEEN THE TWO    *
      *            CURRENCIES.                                        *
      *                                                               *
      *  INPUT  FIELDS                                                *
      *       ZAMTF  150     AMOUNT IN 'FROM' CURRENCY                *
      *       ZCRSRT 138     CROSS EXCHANGE RATE                      *
      *       ZCRSMD  1A     CROSS RATE MULTIPLY/DIVIDE INDICATOR     *
      *                      NOTE: FIELD EITHER SET UP IN SR/ZCCYCN   *
      *                      OR IF ONE M/D INDICATOR FOR NON-BASE     *
      *                      CURRENCY                                 *
      *       ZCDPF   10     CURRENCY DECIMAL PLACES FOR 'FROM'       *
      *                      CURRENCY                                 *
      *       ZCDPT   10     CURRENCY DECIMAL PLACES FOR 'TO' CURRENCY*
      *                                                               *
      *   OUTPUT  FIELDS                                              *
      ********ZAMTT**150*****AMOUNT*IN*'TO'*CURRENCY*******************                     MD061615
      *       ZAMTT  160     AMOUNT IN 'TO' CURRENCY                  *                     MD061615
      *                      (IE. CONVERTED AMOUNT)                   *
      *                                                               *
      *   WORK FIELDS                                                 *
      *       ZWKPX   10     POWER INDEX                              *
      *       ZWK153 153                                              *
      *                                                               *
      *   ALSO REQUIRED                                               *
      *       POWER ARRAY                                             *
      *                                                               *
      *  Last Amend No. MD061615           Date 10Nov23               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. CFT006  *CREATE    Date 11May00               *
      *                 nnnnnn             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD061615 - ME_FT_IMM background job is dumping in New York.  *
      *           - Increased lenght of ZAMTT field to 16,0.          *
      *  CFT006 - Converted version of ZCCYXR.                        *
      *                                                               *
      *****************************************************************
      *
     C     ZCCYXR        BEGSR                                                  *** ZCCYXR ***
      *
      **   DEFINE ALL FIELDS
     C                   Z-ADD     ZAMTF         ZAMTF            15 0
     C                   Z-ADD     ZCDPF         ZCDPF             1 0
     C                   Z-ADD     ZCDPT         ZCDPT             1 0
     C**********         Z-ADD     ZAMTT         ZAMTT            15 0                      MD061615
     C                   Z-ADD     ZAMTT         ZAMTT            16 0                      MD061615
     C                   Z-ADD     ZCRSRT        ZCRSRT           13 8
     C                   MOVE      ZCRSMD        ZCRSMD            1
     C                   Z-ADD     ZWKPX         ZWKPX             1 0
     C                   Z-ADD     ZWK153        ZWK153           15 3
      *
      ** IF CROSS EXCHANGE RATE IS EQUAL TO ZERO
      ** SET ZAMTT  TO *ZERO  & EXIT
     C     ZCRSRT        IFEQ      0
     C                   Z-ADD     *ZEROS        ZAMTT
     C                   GOTO      ZCCEND
     C                   END
      *
      ** IF DECIMAL PLACES  FOR 'FROM' CURRENCY (ZCDPF) EQUALS DECIMAL
      ** DECIMAL PLACES  FOR 'TO' CURRENCY
     C     ZCDPF         IFEQ      ZCDPT
      *
      ** IF CROSS RATE MULTIPLY/DIVIDE INDICATOR EQUALS 'D'
      ** SET AMOUNT IN 'TO' EQUAL TO AMOUNT IN FROM CURRENCY (ZAMTF)
      ** DIVIDED BY CROSS EXCHANGE RATE
     C     ZCRSMD        IFEQ      'D'
     C                   Z-ADD     ZAMTF         ZAMTT
     C     ZAMTT         DIV(H)    ZCRSRT        ZAMTT
     C                   END
      *
      ** IF CROSS RATE MULTIPLY/DIVIDE INDICATOR EQUALS 'M'
      ** SET AMOUNT IN 'TO' EQUAL TO AMOUNT IN FROM CURRENCY (ZAMTF)
      ** MULTIPLIED BY CROSS EXCHANGE RATE
     C     ZCRSMD        IFEQ      'M'
     C                   Z-ADD     ZAMTF         ZAMTT
     C     ZAMTT         MULT(H)   ZCRSRT        ZAMTT
     C                   END
     C                   END
      *
      ** IF DECIMAL PLACES  FOR 'FROM' CURRENCY (ZCDPF)  NOT EQUALS DECIMAL
      ** DECIMAL PLACES  FOR 'TO' CURRENCY
     C     ZCDPF         IFNE      ZCDPT
      *
      ** SUBTRACT DECIMAL PLACES 'FROM' FROM DECIMAL PLACES 'TO' IN POWER INDEX
     C     ZCDPT         SUB       ZCDPF         ZWKPX
     C     ZWKPX         ADD       4             B                 3 0
      *
      ** IF POWER ELEMENT (8,4) LESS THAN 1
     C     POWER(B)      IFLT      1
      *
      ** MULTIPLY AMOUNT IN FROM CURRENCY BY ELEMENT IN POWER ARRAY
     C     POWER(B)      MULT      ZAMTF         ZWK153
      *
      ** SET AMOUNT IN 'TO' EQUAL TO AMOUNT WORK FIELD ZWK153
      ** DIVIDED    BY CROSS EXCHANGE RATE
     C     ZCRSMD        IFEQ      'D'
     C     ZWK153        DIV(H)    ZCRSRT        ZAMTT
     C                   END
      *
      ** SET AMOUNT IN 'TO' EQUAL TO AMOUNT WORK FIELD ZWK153
      ** MULTIPLIED BY CROSS EXCHANGE RATE
     C     ZCRSMD        IFEQ      'M'
     C     ZWK153        MULT(H)   ZCRSRT        ZAMTT
     C                   END
     C                   END
      *
      ** IF POWER ELEMENT (8,4) GREATER OR EQUAL TO 1
     C     POWER(B)      IFGE      1
      *
      ** MULTIPLY AMOUNT IN FROM CURRENCY BY ELEMENT IN POWER ARRAY
     C     ZAMTF         MULT      POWER(B)      ZAMTF
      *
      ** TEST CROSS MULT/DIV INDICATOR
     C     ZCRSMD        IFEQ      'D'
      *
      ** SET AMOUNT IN 'TO' EQUAL TO AMOUNT IN 'FROM' CURRENCY
      ** DIVIDED BY CROSS EXCHANGE RATE
     C     ZAMTF         DIV(H)    ZCRSRT        ZAMTT
     C                   END
      *
      ** TEST CROSS MULT/DIV INDICATOR
     C     ZCRSMD        IFEQ      'M'
      *
      ** SET AMOUNT IN 'TO' EQUAL TO AMOUNT IN 'FROM' CURRENCY
      ** MULTIPLIED BY CROSS EXCHANGE RATE
     C     ZAMTF         MULT(H)   ZCRSRT        ZAMTT
     C                   END
     C                   END
     C                   END
     C     ZCCEND        ENDSR
      *****************************************************************
      * End of /COPY ZCCYXR_ILE                                       *
      *****************************************************************
