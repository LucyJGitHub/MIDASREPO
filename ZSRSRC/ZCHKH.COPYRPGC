      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member (RPG)                                   *
      *                                                               *
      *  ZCHKH - Check if Holiday for Ccy/Locn                        *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. 151295  *CREATE    Date ddmmmyy               *
      *  Prev Amend No. nnnnnn             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  151295 - Problems authorising repayment schedules.           *
      *                                                               *
      *****************************************************************
      *
      /EJECT
      *****************************************************************
     C           ZCHKH     BEGSR
      **
      *Define input parameters
      **
     C                     Z-ADDZDAYNO    ZDAYNO  50
     C                     MOVE ZCCY      ZCCY    3
     C                     MOVE ZLOC      ZLOC    3
      **
      *Define output parameter
      **
     C                     MOVE ZIND      ZIND    1
      *
      * Standard sub-routine to determine if a MIDAS day no. is a
      * holiday or working day for a given currency and (optionally)
      * location.
      * NB. This sub-routine must be used in conjunction with ZACCH.
      *
      * Access holiday record.
      *
      *
     C                     MOVE '*SETGT ' ZOPTN
     C                     EXSR ZACCH
      *
      * If no record was found then all days for year are assumed to be
      * working days, so set holiday indicator appropriately.
      *
     C           RTNCD     IFEQ '*NRF   '                  IF 1
      *
     C                     MOVE 'W'       ZIND
      *
     C                     ELSE                            ELSE 1
      *
      * Subtract 1st. Jan date from given date and add 1 to give the
      * nth day in the year (ZINDX).
      *
     C           ZDAYNO    IFGT ZSJAN                                     151295
     C           ZDAYNO    SUB  ZSJAN     ZINDX                           151295
     C                     ADD  1         ZINDX                           151295
     C                     ELSE                                           151295
     C           ZDAYNO    SUB  DGJDNB    ZINDX
     C                     ADD  1         ZINDX
     C                     ENDIF                                          151295
      *
      * Move the nth day in the year value to the holiday indicator
      *
     C                     MOVE ZHL,ZINDX ZIND
      *
      * If holiday indicator is 'X' change to 'H' to show holiday
      *
     C           ZIND      IFEQ 'X'                         IF 2
     C                     MOVE 'H'       ZIND
     C                     ELSE
     C                     MOVE 'W'       ZIND
     C                     END                              FI 2
      *
     C                     END                             FI 1
      *
     C                     ENDSR
      *
      *****************************************************************
      * End of /COPY ZCHKH                                            *
      *****************************************************************
      /EJECT
