      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member                                         *
      *                                                               *
      *  ZCONVNZ1LE - Subroutine to convert an amount from one        *
      *               currency to another (ILE)                       *
      *                                                               *
      *  Any changes made to this /COPY member may also need to be    *
      *  applied to ZCONVNZ1.                                         *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL033             Date 15Mar05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev amend No. BUG6293 *CREATE    Date 15Mar05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL033 - Included this /COPY as part of delivery.  This was  *
      *           introduced by BUG6293 which is still outstanding.   *
      *  BUG6293- Duplicate copy of ZCONVNZ1 converted to ILE version *
      *  153673 - When converting from Base ccy back to EUR , inverted*
      *           value of MDIN should be used .  Coded such as only  *
      *           to be executed when called from DL0410 pgm.         *
      *   NOTE: - This subroutine ,most likely ,is correctly converting
      *           amounts from A-one  to B-another ccy using A's MDIN *
      *           indicator. But when converting backwards from B to  *
      *           A and still using A's MDIN indicator, the inverted  *
      *           operation for the same MDIN should be used          *
      *                                                               *
      *  XXXXXX - GENERATE FX DEALS ON EXERCISE                       *
      *                                                               *
     C*****************************************************************
     C**
     C**   ZCONVN - SUBROUTINE TO CONVERT AN  AMOUNT FROM ONE CURRENCY
     C**               TO ANOTHER
     C**
     C**   INPUT  - ZAMTCI (15,0) AMOUNT INPUT
     C**            ZEXCH  (13,8) EXCHANGE RATE
     C**            ZMD    (1)    MULTIPLY/DIVIDE INDICATOR
     C**            ZCDPI  (1,0)  CURRENCY DECIMAL PLACES INPUT
     C**            ZCDPO  (1,0)  CURRENCY DECIMAL PLACES TO BE CONVERTED TO
     C**            POWER   -     COMPILE TIME ARRAY MUST BE INCLUDED
     C**                          IN THE PROGRAM SOURCE
     C**
     C**  OUTPUT -  ZAMTCO (15,0) AMOUNT OUTPUT
     C**
     C**********************************************************************
     C*
     C     ZCONVN        BEGSR                                                  ** ZCONVN **
     C*
     C* DEFINE INPUT AND OUTPUT FIELDS
     C*
     C                   Z-ADD     ZAMTCI        ZAMTCI           15 0
     C                   Z-ADD     0             ZAMTCO           15 0
     C                   Z-ADD     ZCDPI         ZCDPI             1 0
     C                   Z-ADD     ZCDPO         ZCDPO             1 0
     C                   Z-ADD     ZEXCH         ZEXCH            13 8
     C                   MOVE      ZMD           ZMD               1
     C*
     C* IF EXCHANGE RATE IS 0 - OUTPUT CURRENCY AMOUNT AS ZERO
     C*
     C     ZEXCH         IFEQ      0
     C                   Z-ADD     0             ZAMTCO
     C                   GOTO      EZCNVN
     C                   END
     C*
     C* IF DECIMAL PLACES ARE THE SAME FOR BOTH CURRENCIES
     C*
     C     ZCDPI         IFEQ      ZCDPO
     C*
     C***  CALCULATE OUTPUT CURRENCY AMOUNT BY DIVIDING EXCHANGE RATE INTO
     C*** OR MULTIPLYING EXCHANGE RATE BY CURRENCY AMOUNT INPUT DEPENDING
     C*** ON INDICATOR
     C*
      *  When converting from Base ccy back to EUR , inverted value of
      *  MDIN should be used. Check for unique field WUFLAG to validate
      *  it has been called from DL0410 .
      *
      * Define new fields lengths
     C                   MOVE      WBINCY        WBINCY            1
     C                   MOVE      WUFLAG        WUFLAG            1
     C     WUFLAG        IFEQ      'Y'
     C     WBINCY        ANDNE     'Y'
      *
     C     ZMD           IFEQ      'D'
     C     ZAMTCI        MULT(H)   ZEXCH         ZAMTCO
     C                   ELSE
     C     ZAMTCI        DIV(H)    ZEXCH         ZAMTCO
     C                   END
      *
     C                   ELSE
      *
     C     ZMD           IFEQ      'D'
     C     ZAMTCI        DIV(H)    ZEXCH         ZAMTCO
     C                   ELSE
     C     ZAMTCI        MULT(H)   ZEXCH         ZAMTCO
     C                   END
     C*
     C                   END
      *
     C                   ELSE
     C*
     C*** IF DECIMAL PLACES DIFFER -
     C***      CALCULATE DIFFERENCE IN DECIMAL PLACES TO USE AS POWER INDEX
     C*
     C     ZCDPO         SUB       ZCDPI         ZPX               1 0
     C                   ADD       4             ZPX
     C*
     C*** CALCULATE CURRENCY AMOUNT OUTPUT BY MULTIPLYING INPUT AMOUNT BY
     C***  POWER ARRAY ENTRY AND THEN MULTIPLYING RESULT BY EXCHANGE RATE
     C***  DEPENDING ON INDICATOR
     C*
     C     POWER(ZPX)    IFLT      1                                            from d.p *LT
     C     ZAMTCI        MULT      POWER(ZPX)    Z15#3            15 3           to d.p.
      *
      *  Do the same procedure if converting from Base ccy back to EUR
      *
     C     WUFLAG        IFEQ      'Y'
     C     WBINCY        ANDNE     'Y'
      *
     C     ZMD           IFEQ      'D'
     C     Z15#3         MULT(H)   ZEXCH         ZAMTCO
     C                   ELSE
     C     Z15#3         DIV(H)    ZEXCH         ZAMTCO
     C                   END
      *
     C                   ELSE
      *
     C     ZMD           IFEQ      'D'
     C     Z15#3         DIV(H)    ZEXCH         ZAMTCO
     C                   ELSE
     C     Z15#3         MULT(H)   ZEXCH         ZAMTCO
     C                   END
      *
     C                   END
      *
     C                   ELSE                                                   from d.p. *GT
     C     ZAMTCI        MULT      POWER(ZPX)    ZAMTCI                           to d.p.
      *
     C     WUFLAG        IFEQ      'Y'
     C     WBINCY        ANDNE     'Y'
      *
     C     ZMD           IFEQ      'D'
     C     ZAMTCI        MULT(H)   ZEXCH         ZAMTCO
     C                   ELSE
     C     ZAMTCI        DIV(H)    ZEXCH         ZAMTCO
     C                   END
      *
     C                   ELSE
      *
     C     ZMD           IFEQ      'D'
     C     ZAMTCI        DIV(H)    ZEXCH         ZAMTCO
     C                   ELSE
     C     ZAMTCI        MULT(H)   ZEXCH         ZAMTCO
     C                   END
     C*
     C                   END
      *
     C                   END
      *
     C*
     C                   END
     C*
     C     EZCNVN        ENDSR                                                  ** EZCNVN **
     C*
     C*****************************************************************
