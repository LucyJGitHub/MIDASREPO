      *****************************************************************
      *                                                               *
      *  Midas - Standard Subroutines                                 *
      *                                                               *
      *  ZCONVZ1ILE - Convert an amount from one currency to another  *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CLE031  *CREATE    Date 09Sep03               *
      *                 155225             Date 31Mar99               *
      *                 S01117             Date 07Jan91               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE031 - ZCONVZ1 converted to ILE.                           *
      *  155225 - When converting from Base ccy back to EUR , inverted*
      *           value of MDIN should be used .  Coded such as only  *
      *           to be executed when called from DL0410 pgm.         *
      *   NOTE: - This subroutine ,most likely ,is correctly converting
      *           amounts from A-one  to B-another ccy using A's MDIN *
      *           indicator. But when converting backwards from B to  *
      *           A and still using A's MDIN indicator, the inverted  *
      *           operation for the same MDIN should be used          *
      *  S01117 - SD file changes (multibranching)                    *
      *                                                               *
      *****************************************************************
      *                                                               *
      *    INPUT  - ZAMTCI (15,0) AMOUNT INPUT                        *
      *             ZEXCH  (13,8) EXCHANGE RATE                       *
      *             ZMD    (1)    MULTIPLY/DIVIDE INDICATOR           *
      *             ZCCYI  (3)    CURRENCY INPUT                      *
      *             ZCCYO  (3)    CURRENCY TO BE CONVERTED TO         *
      *             POWER   -     COMPILE TIME ARRAY MUST BE INCLUDED *
      *                           IN THE PROGRAM SOURCE               *
      *             ZCDPI  (1,0)  (FROM) CURRENCY DECIMAL PLACES      *
      *             ZCDPO  (1,0)  (TO) CURRENCY DECIMAL PLACES        *
      *                                                               *
      *   OUTPUT -  ZAMTCO (15,0) AMOUNT OUTPUT                       *
      *                                                               *
      *   CALLS  -  NONE                                              *
      *                                                               *
      *****************************************************************
     C     ZCONV         BEGSR                                                  ** ZCONV **
      *
      * DEFINE INPUT AND OUTPUT FIELDS
      *
     C                   MOVE      ZCCYI         ZCCYI             3
     C                   MOVE      ZCCYO         ZCCYO             3
     C                   Z-ADD     ZCDPI         ZCDPI             1 0
     C                   Z-ADD     ZCDPO         ZCDPO             1 0
     C                   Z-ADD     ZAMTCI        ZAMTCI           15 0
     C                   Z-ADD     0             ZAMTCO           15 0
     C                   Z-ADD     ZEXCH         ZEXCH            13 8
     C                   MOVE      ZMD           ZMD               1
      *
      * IF EXCHANGE RATE IS 0 - OUTPUT CURRENCY AMOUNT AS ZERO
      *
     C     ZEXCH         IFEQ      0
     C                   Z-ADD     0             ZAMTCO
     C                   GOTO      EZCONV
     C                   END
      *
      * IF DECIMAL PLACES ARE THE SAME FOR BOTH CURRENCIES
      *
     C     ZCDPI         IFEQ      ZCDPO
     C*
      ***  CALCULATE OUTPUT CURRENCY AMOUNT BY DIVIDING EXCHANGE RATE INTO
      *** OR MULTIPLYING EXCHANGE RATE BY CURRENCY AMOUNT INPUT DEPENDING
      *** ON INDICATOR
      *
      *  When converting from Base ccy back to EUR , inverted value of
      *  MDIN should be used. Check for unique field WUFLAG to validate
      *  it has been called from DL0410 .
      *
      * Define new fields lengths
     C                   MOVE      WBINCY        WBINCY            1
     C                   MOVE      WUFLAG        WUFLAG            1
     C     WUFLAG        IFEQ      'Y'
     C     WBINCY        ANDNE     'Y'
      *
     C     ZMD           IFEQ      'D'
     C     ZAMTCI        MULT(H)   ZEXCH         ZAMTCO
     C                   ELSE
     C     ZAMTCI        DIV(H)    ZEXCH         ZAMTCO
     C                   END
      *
     C                   ELSE
     C     ZMD           IFEQ      'D'
     C     ZAMTCI        DIV(H)    ZEXCH         ZAMTCO
     C                   ELSE
     C     ZAMTCI        MULT(H)   ZEXCH         ZAMTCO
     C                   END
     C*
     C                   END
      *
     C                   ELSE
      *
      *** IF DECIMAL PLACES DIFFER -
      ***      CALCULATE DIFFERENCE IN DECIMAL PLACES TO USE AS POWER INDEX
      *
     C     ZCDPO         SUB       ZCDPI         ZPX               1 0
     C                   ADD       4             ZPX
      *
      *** CALCULATE CURRENCY AMOUNT OUTPUT BY MULTIPLYING INPUT AMOUNT BY
      ***  POWER ARRAY ENTRY AND THEN MULTIPLYING RESULT BY EXCHANGE RATE
      ***  DEPENDING ON INDICATOR
      *
     C     POWER(ZPX)    IFLT      1                                            from d.p *LT
     C     ZAMTCI        MULT      POWER(ZPX)    Z15#3            15 3           to d.p.
      *
      *  Do the same procedure if converting from Base ccy back to EUR
      *
     C     WUFLAG        IFEQ      'Y'
     C     WBINCY        ANDNE     'Y'
      *
     C     ZMD           IFEQ      'D'
     C     Z15#3         MULT(H)   ZEXCH         ZAMTCO
     C                   ELSE
     C     Z15#3         DIV(H)    ZEXCH         ZAMTCO
     C                   END
      *
     C                   ELSE
      *
     C     ZMD           IFEQ      'D'
     C     Z15#3         DIV(H)    ZEXCH         ZAMTCO
     C                   ELSE
     C     Z15#3         MULT(H)   ZEXCH         ZAMTCO
     C                   END
      *
     C                   END
      *
     C                   ELSE                                                   from d.p. *GT
     C     ZAMTCI        MULT      POWER(ZPX)    ZAMTCI                           to d.p.
      *
     C     WUFLAG        IFEQ      'Y'
     C     WBINCY        ANDNE     'Y'
      *
     C     ZMD           IFEQ      'D'
     C     ZAMTCI        MULT(H)   ZEXCH         ZAMTCO
     C                   ELSE
     C     ZAMTCI        DIV(H)    ZEXCH         ZAMTCO
     C                   END
      *
     C                   ELSE
      *
     C     ZMD           IFEQ      'D'
     C     ZAMTCI        DIV(H)    ZEXCH         ZAMTCO
     C                   ELSE
     C     ZAMTCI        MULT(H)   ZEXCH         ZAMTCO
     C                   END
      *
     C                   END
      *
     C                   END
      *
     C                   END
      *
     C     EZCONV        ENDSR                                                  ** EZCONV **
      *
      *****************************************************************
      * End of /COPY ZCONVZ1ILE                                       *
      *****************************************************************
