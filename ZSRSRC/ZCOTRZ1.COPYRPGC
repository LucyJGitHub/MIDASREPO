     C**********************************************************************
     C*
     C*  ZCOTR - SUBROUTINE TO CALCULATE COUNTERVALUE OF TRADES
     C*
     C*  INPUT  - FROM TRADE FILE - PRIC (15,8) TRADE PRICE
     C*                             NOML (11,0) NOMINAL
     C*                             RALL (9,5)  REALLOWANCE
     C*                             ITRA (13,0) INTEREST AMOUNT
     C*                             TCA1,TCA2,TCA3,TCA4,TCA5,TCA6         S01176
     C*                             TCA7,TAXA (13,0) CHARGES              S01176
     C*                             TBCA,TCCA (13,0) COMMISSIONS
     C*                             TDTP (2) TRADE TYPE
     C*           FROM ZCONS      - CONSIDERATION (15,0)
     C************FROM*CLINTSE****-*C1DI*(1)*DISCRETIONARY*INDICATOR******S01117
     C****************************-*CFCT*(9,8)*CURRENT*FACTOR*************S01117
     C*           FROM INVTPD     - PROT (1) PROCESSING TYPE OF SECURITY
     C*           FROM SECTYD     - SPBS (1) PRICE BASIS
     C*                           - NMCY (3) NOMINAL CURRENCY
     C*                           - NMDP (1,0) NOMINAL DECIMAL PLACES
     C*                           - CFCT (9,8) CURRENT FACTOR             S01117
     C*           FROM CURRENCIES - ZCDP                                  S01117
     C*           ARRAY           - POWER8
     C*
     C*  OUTPUT - ZCTRVL (15,0) COUNTERVALUE
     C*
     C*  CALLS - ZCNSD
     C*
     C***NEEDS*TABTG10*FILE*FOR*NOMINAL*CURENCY*DECIMAL*PLACES*           S01117
     C*
     C*  INDICATORS USED  - 90 NOMINAL CURRENCY HAS 0 DECIMAL PLACES
     C*                     91 NOMINAL CURRENCY HAS 1 DECIMAL PLACE
     C*                     92 NOMINAL CURRENCY HAS 2 DECIMAL PLACES
     C*                     93 NOMINAL CURRENCY HAS 3 DECIMAL PLACES
     C*                     99 NOMINAL CURRENCY NOT ON FILE
     C*                                                                  *
     C********************************************************************
     C**                                                                 *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     C** LAST AMEND NO. 055420             DATE 24NOV93                  *
     C** PREV AMEND NO. S01117             DATE 07JAN91                  *
     C**                E21099             DATE 14FEB90                  *
     C**                E20232             DATE 13DEC89                  *
     C**                                                                 *
     C**-----------------------------------------------------------------*
     C**                                                                 *
     C** 055420 - COUNTERVALUE SHOULD BE HALF-ADJUSTED.                  *
     C** S01117 - SD file changes (multibranching)                       *
     C** E21099 - Should use CFCT for Current FActor and not TCFC.       *E21099
     C** E20232 - This subroutine created for use in SE0050 and SE0090   *E20232
     C**          to resolve high order truncation of fields.            *E20232
     C**                                                                 *
     C**=================================================================*
     C*
     C           ZCOTR     BEGSR                           **ZCOTR  BEGSR
     C*
     C* Define fields
     C*
     C                     Z-ADD0         ZCTRVL 150
     C                     Z-ADD0         ZCCTOT 152
     C                     Z-ADD0         ZRALL  150
     C                     Z-ADD0         ZRALL0 150
     C                     Z-ADD0         ZRALL1 151
     C                     Z-ADD0         ZRALL2 152
     C                     Z-ADD0         ZRALL3 153
     C                     Z-ADDZWRALL    ZWRALL 158
     C***********                                                         S01117
     C**Fetch*Nominal*currency*decimal*places*for*result*field*           S01117
     C***********                                                         S01117
     C***********          MOVELNMCY      WORK4   4                       S01117
     C***********          MOVE '1'       WORK4                           S01117
     C***********          MOVEL'20'      KEY12A 12                       S01117
     C***********          MOVE WORK4     KEY12A                          S01117
     C***********KEY12A    CHAINTABTG10F             99                   S01117
     C**N99******RECI      COMP '*'                      99               S01117
     C************IN99     CABEQ'1'       EZCOTR                          S01117
     C*
     C* Set indicator for number of decimal places
     C*
     C                     MOVEA'0000'    *IN,90
     C***********90        ADD  CDP       V       20                      S01117
     C           90        ADD  ZCDP      V       20                      S01117
     C                     MOVEA'1'       *IN,V
     C*
     C*    Calculate the reallowance amount -
     C*        = reallowance x face value (=nominal)
     C*    If price basis is P divide reallowance amount by 100
     C*
     C   90      ZWRALL    MULT NOML      ZRALL0    H
     C   91      ZWRALL    MULT NOML      ZRALL1    H
     C   92      ZWRALL    MULT NOML      ZRALL2    H
     C   93      ZWRALL    MULT NOML      ZRALL3    H
     C*
     C           SPBS      IFEQ 'P'
     C   90      ZRALL0    DIV  100       ZRALL0    H
     C   91      ZRALL1    DIV  100       ZRALL1    H
     C   92      ZRALL2    DIV  100       ZRALL2    H
     C   93      ZRALL3    DIV  100       ZRALL3    H
     C                     END
     C*
     C   90                MOVE ZRALL0    ZRALL
     C   91                MOVE ZRALL1    ZRALL
     C   92                MOVE ZRALL2    ZRALL
     C   93                MOVE ZRALL3    ZRALL
     C*
     C*  ALLOW FOR NOMINAL DECIMAL PLACES
     C*
     C           5         SUB  NMDP      DC
     C           ZRALL     MULT POWER8,DC ZRALL
     C*
     C*  Calculate consideration
     C*
     C                     Z-ADDPRIC      ZPRC
     C                     Z-ADDNOML      ZNOML
     C                     EXSR ZCNSD
     C*
     C* Calculate total charge/commission -
     C*    add the two commission amounts
     C*    add the five charge amounts to the contract price total
     C*
     C                     ADD  TBCA      ZCCTOT
     C                     ADD  TCCA      ZCCTOT
     C                     ADD  TCA1      ZCCTOT
     C                     ADD  TCA2      ZCCTOT
     C                     ADD  TCA3      ZCCTOT
     C                     ADD  TCA4      ZCCTOT
     C                     ADD  TCA5      ZCCTOT
     C                     ADD  TCA6      ZCCTOT
     C                     ADD  TCA7      ZCCTOT
     C                     ADD  TAXA      ZCCTOT
     C*
     C*  Countervalue = Consideration + Reallowance amount +
     C*         Total c/c payable + Purchased interest.                e
     C*
     C           PROT      IFEQ '8'
     C********** ZCONS     MULT TCFC      ZCTRVL                          E21099
     C***********ZCONS     MULT CFCT      ZCTRVL                   E21099 055420
     C           ZCONS     MULT CFCT      ZCTRVL    H                     055420
     C                     ELSE
     C                     Z-ADDZCONS     ZCTRVL
     C                     END
     C*
     C                     ADD  ZRALL     ZCTRVL
     C                     ADD  ZCCTOT    ZCTRVL
     C                     ADD  ITRA      ZCTRVL
     C*
     C           EZCOTR    ENDSR                           *** ENDSR    ***
     C*
     C*****************************************************************
