      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member (RPG)                                   *
      *                                                               *
      *  XXXXXXX - (/COPY Name)                                       *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. nnnnnn  *CREATE    Date ddmmmyy               *
      *  Prev Amend No. nnnnnn             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  nnnnnn - (change description)                                *
      *                                                               *
      *****************************************************************
     C********************************************************************
     C**
     C**   ZDATE3 SR. TO CALCULATE ANY NUMBER OF WORKING
     C**              DAYS FORWARD IN ANY CURRENCY.
     C**              HOLIDAYS FILE ACCESSED BY CHAIN.
     C**
     C           ZDATE3    BEGSR                           *** ZDATE3 ***
     C**
     C**   CALCULATIONS TO DEFINE INPUT FIELDS.
     C                     Z-ADDZDAYNO    ZDAYNO  50       STARTING DATE
     C                     MOVE ZCCY      ZCCY    3        DATE CURRENCY
     C                     Z-ADDZNRDYS    ZNRDYS  20       NBR OF WORKING
     C**                                                   DAYS FORWARD
     C**   CLEAR COUNT OF NBR OF DAYS ADDED, & DEFINE ZDYNBR.
     C                     Z-ADD0         ZDCNT   20
     C                     Z-ADDZDAYNO    ZDYNBR  50
     C**
     C**   LOOP TO INCREMENT DAY NUMBER AND CHECK FOR HOLIDAY.
     C           ZDCNT     DOWLTZNRDYS
     C**
     C                     ADD  1         ZDYNBR
     C**
     C**   CHAIN FOR HOLIDAY RECORD 1 - IF NOT FOUND, NO HOLIDAY
     C           ZDYNBR    CHAINTABLETJF             90
     C           *IN90     IFEQ '1'
     C           RECI      ORNE 'D'
     C                     ADD  1         ZDCNT
     C                     GOTO ZDLOP1
     C                     END
     C**
     C**   CHECK IF DAY IS HOLIDAY FOR CURRENCY.
     C           ZCCY      LOKUPHCCY                     93
     C**
     C**   DETERMINE TYPE FOUND, IF INCLUDE AND NOT IN ARRAY/EXCLUDE AND
     C**   IN ARRAY, THEN MUST CHAIN TO RECORD 2 TO SEE IF IT IS A HOL.
     C           INEX      IFEQ 'E'
     C           *IN93     ANDEQ'1'
     C           INEX      OREQ 'I'
     C           *IN93     ANDEQ'0'
     C**
     C**   IF BLANK ENTRIES IN HCCY, THEN NO RECORD 2 WILL BE PRESENT
     C           '   '     LOKUPHCCY                     94ON IF NO RCD.2
     C           *IN94     IFEQ '1'
     C                     ADD  1         ZDCNT
     C                     GOTO ZDLOP1
     C                     END
     C**
     C**   CHAIN FOR HOLIDAY RECORD, RECORD 2
     C                     READ TABLETJF                 90
     C           *IN90     IFEQ '1'
     C           RECI      ORNE 'D'
     C           HOL       ORNE ZDYNBR
     C                     ADD  1         ZDCNT
     C                     GOTO ZDLOP1
     C                     END
     C**
     C**   CHECK IF DAY IS HOLIDAY FOR CURRENCY.
     C           ZCCY      LOKUPHCCY                     93
     C**
     C**   DETERMINE TYPE FOUND, IF INCLUDE AND NOT IN ARRAY/EXCLUDE AND
     C**   IN ARRAY, THEN NOT A HOLIDAY
     C           INEX      IFEQ 'E'
     C           *IN93     ANDEQ'1'
     C           INEX      OREQ 'I'
     C           *IN93     ANDEQ'0'
     C           ZDCNT     ADD  1         ZDCNT
     C                     END
     C                     END
     C**
     C**  END OF DO LOOP
     C           ZDLOP1    TAG                             *** ZDLOP1 ***
     C                     END
     C**
     C                     ENDSR
     C**
     C**
     C********************************************************************
      *****************************************************************
      * End of /COPY XXXXXXX                                          *
      *****************************************************************
