      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member (RPG)                                   *
      *                                                               *
      *  XXXXXXX - (/COPY Name)                                       *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. nnnnnn  *CREATE    Date ddmmmyy               *
      *  Prev Amend No. nnnnnn             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  nnnnnn - (change description)                                *
      *                                                               *
      *****************************************************************
     C********************************************************************
     C**
     C**   ZDATE8 SR. TO CONVERT A DAY NUMBER TO DATE FORMATS
     C**              USING THE MONTH SHORTNAMES FILE.
     C**
     C**   THE YEAR 2000, BEING DIVISIBLE BY 400, IS A LEAP YEAR.
     C**
     C           ZDATE8    BEGSR                           *** ZDATE8 ***
     C**
     C**   CLEAR LEAP YEAR WORK FIELD.
     C                     MOVE 'N'       ZLEAP   1
     C**
     C**   CLEAR DATE FIELDS.
     C                     Z-ADD0         ZDATE   60
     C                     MOVEL'       ' ZADATE  7
     C**
     C**   CALCULATION TO DEFINE INPUT DAY NUMBER.
     C                     Z-ADDZDAYNO    ZDAYNO  50
     C**
     C**   DETERMINE YEAR NUMBER.
     C**
     C**   ADJUST DAY NUMBER IN CASE LAST DAY OF YEAR.
     C           ZDAYNO    SUB  1         ZDAYN1  50
     C           ZDAYN1    IFLT 0                          B1
     C                     GOTO ZDEND6                     *1
     C                     END                             E1
     C**
     C**   CALCULATE NUMBER OF 4 YEAR SPANS SINCE 31/12/1971.
     C           ZDAYN1    DIV  1461      ZLYR    20
     C                     MVR            ZDAYN1           SAVE REMAINING
     C**                                                   DAYS
     C**   CALCULATE NUMBER OF REMAINING YEARS.
     C                     Z-ADD1         ZWRK2   20
     C           ZZTAG1    TAG                             ** ZZTAG1 TAG *
     C           ZDAYN1    IFGE ZYDY,ZWRK2                 B1
     C           ZWRK2     ADD  1         ZWRK2            *1
     C                     GOTO ZZTAG1                     *1
     C                     END                             E1
     C*
     C           ZWRK2     SUB  1         ZWRK2
     C**
     C**   DECREMENT DAY NO. BY DAYS IN REMAINING YEARS.
     C           ZWRK2     IFNE 0                          B1
     C           ZDAYN1    SUB  ZYDY,ZWRK2ZDAYN1           *1
     C                     END                             E1
     C**
     C**   CALCULATE ACTUAL YEAR NUMBER.
     C           ZLYR      MULT 4         ZWRK3   30
     C           ZWRK3     ADD  72        ZWRK3            BASE IS 1972
     C                     Z-ADDZWRK3     ZYEAR   20
     C           ZYEAR     ADD  ZWRK2     ZYEAR            YEAR
     C**
     C**   DETERMINE MONTH NUMBER.
     C**
     C**   ADJUST DAY NO. IN CASE LAST DAY OF LEAP YEAR FEBRUARY.
     C           ZWRK2     IFEQ 0                          B1
     C           ZDAYN1    IFEQ 59                         B*2
     C                     MOVE 'Y'       ZLEAP            **2
     C                     END                             E*2
     C           ZDAYN1    IFGE 59                         B*2
     C           ZDAYN1    SUB  1         ZDAYN1           **2
     C                     END                             E*2
     C                     END                             E1
     C**
     C**   CALCULATE MONTH NUMBER.
     C                     Z-ADD2         ZWRK2
     C           ZZTAG2    TAG                             ** ZZTAG2 TAG *
     C           ZDAYN1    IFGE ZMDY,ZWRK2                 B1
     C           ZWRK2     ADD  1         ZWRK2            *1
     C                     GOTO ZZTAG2                     *1
     C                     END                             E1
     C*
     C           ZWRK2     SUB  1         ZWRK2
     C**
     C                     Z-ADDZWRK2     ZMTH    20       MONTH
     C**
     C**   DETERMINE DAY OF MONTH.
     C**
     C**   ADD BACK LAST DAY OF YEAR ADJUSTMENT.
     C           ZDAYN1    ADD  1         ZDAYN1
     C**
     C**   CALCULATE DAY OF MONTH.
     C           ZDAYN1    SUB  ZMDY,ZWRK2ZDAY    20       DAY
     C**
     C**   ADD BACK LEAP YEAR 29TH FEBUARY ADJUSTMENT, IF REQUIRED.
     C           ZLEAP     IFEQ 'Y'                        B1
     C           ZDAY      ADD  1         ZDAY             *1
     C                     END                             E1
     C**
     C**   FORMAT DATE, DDMMYY OR MMDDYY.
     C  N98                MOVELZDAY      ZWRK4   40
     C   98                MOVE ZDAY      ZWRK4
     C  N98                MOVE ZMTH      ZWRK4
     C   98                MOVELZMTH      ZWRK4
     C                     MOVELZWRK4     ZDATE
     C                     MOVE ZYEAR     ZDATE
     C*
     C** Format alpha date, DDMMMYY or MMMDDYY as required
     C*
     C           *IN98     IFEQ '0'                        B1
     C*
     C                     MOVELZDAY      ZWRK5   5        *1
     C           ZDAY      IFLT 10                         B*2
     C                     MOVEL' '       ZWRK5            **2
     C                     END                             E*2
     C                     MOVE @MT,ZMTH  ZWRK5            *1
     C*
     C                     ELSE                            X1
     C*
     C                     MOVE ZDAY      ZWRK5            *1
     C                     MOVEL@MT,ZMTH  ZWRK5            *1
     C*
     C                     END                             E1
     C*
     C                     MOVELZWRK5     ZADATE
     C                     MOVE ZYEAR     ZADATE
     C**
     C           ZDEND6    ENDSR                           * ZDEND6 ENDSR*
     C**
     C**
     C********************************************************************
      *****************************************************************
      * End of /COPY XXXXXXX                                          *
      *****************************************************************
