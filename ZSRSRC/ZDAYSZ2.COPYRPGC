     C**********************************************************************
     C**
     C**  ZDAYS  SUBROUTINE TO CALCULATE NO. OF DAYS BETWEEN TWO DATES
     C**            FOR INTEREST CALCULATIONS
     C**
     C**  INPUT - ZSDATE (5,0) START DATE
     C**          ZEDATE (5,0) END DATE
     C**          DYBS   (1)   DAY BASIS FROM SECTYD
     C**          LCPN   (5,0) LAST COUPON DATE FROM SECTYD
     C**          ITLD   (5,0) INITIAL DATE FROM SECTYD
     C**          IADJ   (3,0) INTEREST ADJUSTMENT DAYS FROM SECTYD
     C**          ARRAY 'ZF29' 29TH FEB DAYS
     C**
     C**  OUTPUT - ZDDAYS (5,0) NO OF DAYS
     C**
     C**  CALLS ZDATE2
     C**
     C********************************************************************
     C**                                                                 *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     C** Last Amend no. 102506             Date 14Oct98                  *
     C** Prev Amend no. 100017             Date 17Apr96                  *
     C**                086491             Date 15Jun95                  *
     C**                E20085             DATE 06NOV89                  *
     C**                                                                 *
     C**-----------------------------------------------------------------*
     C** 102506 - Remove fix 086491 as this cause incorrect calculation  *
     C**          of number of days for day basis 1. Replaced with       *
     C**          067811 which accrue 30 days for day basis '1' and '5'  *
     C**          for February.                                          *
     C** 100017 - Position settlement unable to pick up day adj. on      *
     C**          SECTYD. Change subroutine to use DADJN to stop         *
     C**          interest day adjustment being used for amortisation.   *
     C** 086491 - Wrong number of days calculated for day basis 1        *
     C**          when start or end day is last day of February.         *
     C** E20085 - Interest and Coupons Calculations Rewrite -            *E20085
     C**           -  Add Day Basis 5- ie 30 Day Months US Domestic      *E20085
     C**              method.                                            *E20085
     C********************************************************************
     C*
     C           ZDAYS     BEGSR                           ** ZDAYS BEGSR **
     C*
     C* DEFINE INPUT FIELDS
     C*
     C                     Z-ADDZSDATE    ZSDATE  50
     C                     Z-ADDZEDATE    ZEDATE  50
     C                     MOVE DADJN     DADJN   1                       100017
     C                     Z-ADDZIDNAT    ZIDNAT  20                      102506
     C                     MOVE ZACLT     ZACLT   1                       102506
     C                     MOVE ACLD      ACLD    1                       102506
     C* OUTPUT FIELDS                                                     102506
     C                     Z-ADD*ZERO     ZODNAT  20                      102506
     C*
     C* IF START DATE IS GREATER THAN END DATE
     C*       OUTPUT DAY DIFFERENCE AS 0
     C*
     C           ZSDATE    IFGT ZEDATE
     C                     Z-ADD0         ZDDAYS
     C                     GOTO EZDAYS
     C                     END
     C*
     C* IF DAY BASIS IS 4 (FLAT - NO INTEREST TO BE ACCRUED)
     C*
     C           DYBS      IFEQ '4'
     C                     Z-ADD0         ZDDAYS  50
     C                     GOTO EZDAYS
     C                     END
     C*
     C*****IF*DAY*BASIS*IS*1*-*(30*DAY*MONTHS*INCLUDING*'30TH FEB')**     E20085
     C***  If Day Basis = 1 or 5, (30 Day months incl. '30th Feb.').      E20085
     C*
     C** CONVERT INPUT DATES TO DD/MM/YY OR MM/DD/YY FORMAT
     C*
     C           DYBS      IFEQ '1'
     C           DYBS      OREQ '5'                                       E20085
     C                     Z-ADDZSDATE    ZDAYNO
     C                     EXSR ZDATE2
     C*
     C                     MOVE ZDATE     ZYR1    20
     C           *IN98     IFEQ '0'
     C                     MOVELZDATE     ZDY1    20
     C                     MOVE ZDATE     ZWRK4   40
     C                     MOVELZWRK4     ZMT1    20
     C                     ELSE
     C                     MOVELZDATE     ZMT1
     C                     MOVELZDATE     ZWRK4
     C                     MOVE ZWRK4     ZDY1
     C                     END
     C*
     C                     Z-ADDZEDATE    ZDAYNO
     C                     EXSR ZDATE2
     C*
     C                     MOVE ZDATE     ZYR2    20
     C           *IN98     IFEQ '0'
     C                     MOVELZDATE     ZDY2    20
     C                     MOVE ZDATE     ZWRK4   40
     C                     MOVELZWRK4     ZMT2    20
     C                     ELSE
     C                     MOVELZDATE     ZMT2
     C                     MOVELZDATE     ZWRK4
     C                     MOVE ZWRK4     ZDY2
     C                     END
     C*                                                                   102506
     C** Use day number accrued to (if defined) as start day              102506
     C*                                                                   102506
     C           ZIDNAT    IFNE *ZERO                                     102506
     C                     Z-ADDZIDNAT    ZDY1                            102506
     C                     END                                            102506
     C*
     C******CHANGE*DAYS*TO*30*IF*31********                               E20085
     C**  If Start Day is 31, change it to 30.                            E20085
     C*
     C           ZDY1      IFEQ 31
     C                     MOVE 30        ZDY1
     C                     END
     C*                                                                   E20085
     C***  If Day Basis = '1', then if End Day is 31, change it to 30.    E20085
     C*                                                                   E20085
     C           DYBS      IFEQ '1'                                       E20085
     C           ZDY2      IFEQ 31
     C                     MOVE 30        ZDY2
     C                     END
     C                     END                                            E20085
     C***********                                                  086491 102506
     C*****If*day*basis*=*'1'                                      086491 102506
     C*****Check*if*start*day*is*last*day*of*february              086491 102506
     C***********DYBS      IFEQ '1'                        B2      086491 102506
     C***********ZDY1      IFGE 28                         B3      086491 102506
     C***********ZMT1      ANDEQ2                                  086491 102506
     C***********ZYR1      DIV  4         LEAP    30               086491 102506
     C***********          MVR            LEAP                     086491 102506
     C***********LEAP      IFEQ 0                          B4 LEAP 086491 102506
     C***********ZDY1      IFEQ 29                         B5      086491 102506
     C***********          Z-ADD30        ZDY1                     086491 102506
     C***********          END                             E5      086491 102506
     C***********          ELSE                            X4 NOT  086491 102506
     C***********ZDY1      IFEQ 28                         B5      086491 102506
     C***********          Z-ADD30        ZDY1                     086491 102506
     C***********          END                             E5      086491 102506
     C***********          END                             E4      086491 102506
     C***********          END                             E3      086491 102506
     C*****Check*if*end*day*is*last*day*of*february                086491 102506
     C***********ZDY2      IFGE 28                         B3      086491 102506
     C***********ZMT2      ANDEQ2                                  086491 102506
     C***********ZYR2      DIV  4         LEAP                     086491 102506
     C***********          MVR            LEAP                     086491 102506
     C***********LEAP      IFEQ 0                          B4 LEAP 086491 102506
     C***********ZDY2      IFEQ 29                         B5      086491 102506
     C***********          Z-ADD30        ZDY2                     086491 102506
     C***********          END                             E5      086491 102506
     C***********          ELSE                            X4 NOT  086491 102506
     C***********ZDY2      IFEQ 28                         B5      086491 102506
     C***********          Z-ADD30        ZDY2                     086491 102506
     C***********          END                             E5      086491 102506
     C***********          END                             E4      086491 102506
     C***********          END                             E3      086491 102506
     C***********          END                             E2      086491 102506
     C*                                                                   102506
     C***  If Day Basis = '5', if Start Day is 30 or 31,                  102506
     C***  and End Day is 31, change it to 30.                            102506
     C*                                                                   102506
     C           DYBS      IFEQ '5'                                       102506
     C           ZDY1      IFEQ 30                                        102506
     C           ZDY1      OREQ 31                                        102506
     C           ZDY2      IFEQ 31                                        102506
     C                     MOVE 30        ZDY2                            102506
     C                     END                                            102506
     C                     END                                            102506
     C                     END                                            102506
     C*                                                                   102506
     C***  If Last Day Accruals                                           102506
     C*                                                                   102506
     C           ACLD      IFEQ 'Y'                                       102506
     C*                                                                   102506
     C***  If Start Date is not maturity date (just for yield accrual)    102506
     C***   Allow for accrual from the start of February                  102506
     C*                                                                   102506
     C           ZACLT     IFEQ 'Y'                                       102506
     C           ZSDATE    ANDNEMATY                                      102506
     C           CPNR      ANDEQ*ZERO                                     102506
     C*                                                                   102506
     C***  If Start Date is last day of February                          102506
     C*                                                                   102506
     C           ZMT1      IFEQ 2                                         102506
     C** 28th?                                                            102506
     C           ZDY1      IFEQ 28                                        102506
     C           ZSDATE    ADD  1         DAY29   50                      102506
     C           DAY29     LOKUPZF29                     95               102506
     C           *IN95     IFEQ '0'                                       102506
     C                     MOVE 30        ZDY1                            102506
     C                     MOVE 30        ZODNAT                          102506
     C                     END                                            102506
     C                     END                                            102506
     C** 29th?                                                            102506
     C           ZDY1      IFEQ 29                                        102506
     C                     MOVE 30        ZDY1                            102506
     C                     MOVE 30        ZODNAT                          102506
     C                     END                                            102506
     C*                                                                   102506
     C                     END                                            102506
     C                     END                                            102506
     C*                                                                   102506
     C***  If End Date is not a coupon date (just for interest accrual)   102506
     C***  or End Date is not maturity date (just for discount accrual)   102506
     C***   Allow for accrual to the end of February                      102506
     C*                                                                   102506
     C           ZACLT     IFEQ 'I'                                       102506
     C           ZEDATE    ANDNENCD                                       102506
     C           ZACLT     OREQ 'D'                                       102506
     C           ZEDATE    ANDNEMATY                                      102506
     C*                                                                   102506
     C***  If End Date is last day of February                            102506
     C*                                                                   102506
     C           ZMT2      IFEQ 2                                         102506
     C** 28th?                                                            102506
     C           ZDY2      IFEQ 28                                        102506
     C           ZEDATE    ADD  1         DAY29   50                      102506
     C           DAY29     LOKUPZF29                     95               102506
     C           *IN95     IFEQ '0'                                       102506
     C                     MOVE 30        ZDY2                            102506
     C                     MOVE 30        ZODNAT                          102506
     C                     END                                            102506
     C                     END                                            102506
     C** 29th?                                                            102506
     C           ZDY2      IFEQ 29                                        102506
     C                     MOVE 30        ZDY2                            102506
     C                     MOVE 30        ZODNAT                          102506
     C                     END                                            102506
     C*                                                                   102506
     C                     END                                            102506
     C                     END                                            102506
     C                     END                                            102506
     C***********                                                  E20085 102506
     C*****If*the*Day*Basis*=*'5';*first*process*a*February*Start* E20085 102506
     C***********                                                  E20085 102506
     C***********DYBS      IFEQ '5'                                E20085 102506
     C***********                                                  E20085 102506
     C***********ZMT1      IFEQ 2                                  E20085 102506
     C***********ZDY1      ANDGE28                                 E20085 102506
     C***********                                                  E20085 102506
     C*****If*Start*Date*is*LAST*Day*of*February,*change*it*to*30. E20085 102506
     C*****Note:*for*the*28th,*must*check*that*the*29th*is*not*in* E20085 102506
     C***********                                                  E20085 102506
     C***********ZDY1      IFEQ 29                                 E20085 102506
     C***********          MOVE 30        ZDY1                     E20085 102506
     C***********          ELSE                                    E20085 102506
     C***********ZSDATE    ADD  1         DAY29   50               E20085 102506
     C***********DAY29     LOKUPZF29                     95        E20085 102506
     C************IN95     IFEQ '0'                                E20085 102506
     C***********          MOVE 30        ZDY1                     E20085 102506
     C***********          END                                     E20085 102506
     C***********          END                                     E20085 102506
     C***********                                                  E20085 102506
     C***********          END                                     E20085 102506
     C***********                                                  E20085 102506
     C*****If*the*Start*Day*is*30*and*the*End*Day*is*31,*then*move E20085 102506
     C*****to*the*End*Day,*Else*for*DYBS*5,*we*just*leave*it*alone E20085 102506
     C***********                                                  E20085 102506
     C***********ZDY1      IFEQ 30                                 E20085 102506
     C***********ZDY2      ANDEQ31                                 E20085 102506
     C***********          MOVE 30        ZDY2                     E20085 102506
     C***********          END                                     E20085 102506
     C***********                                                  E20085 102506
     C***********          END                                     E20085 102506
     C*
     C** IF END DAY IS LESS THAN START DAY - ADD 30 TO END DAY AND
     C**   REDUCE END MONTH BY 1
     C*
     C           ZDY2      IFLT ZDY1
     C                     ADD  30        ZDY2
     C           ZMT2      SUB  1         ZMT2
     C                     END
     C*
     C** CALCULATE DAY DIFFERENCE
     C*
     C           ZDY2      SUB  ZDY1      ZDYDF   50
     C*
     C** IF START MONTH GREATER THAN END MONTH - ADD 12 TO END MONTH
     C** AND REDUCE END YEAR BY 1
     C*
     C           ZMT1      IFGT ZMT2
     C                     ADD  12        ZMT2
     C           ZYR2      IFEQ 00
     C                     Z-ADD99        ZYR2
     C                     ELSE
     C           ZYR2      SUB  1         ZYR2
     C                     END
     C                     END
     C*
     C** CALCULATE MONTH DIFFERENCE IN DAYS
     C*
     C           ZMT2      SUB  ZMT1      ZMTDF   50
     C*
     C           ZMTDF     MULT 30        ZMTDF
     C           ZDYDF     ADD  ZMTDF     ZDDAYS  50
     C*
     C** SUBTRACT START YEAR FROM END YEAR - IF RESULT LESS THAN 0
     C** THEN ADD 100 FOR CHANGE IN CENTURY
     C*
     C           ZYR2      SUB  ZYR1      ZYRDF   50
     C           ZYRDF     IFLT 0
     C                     ADD  100       ZYRDF
     C                     END
     C*
     C** CALCULATE YEAR DIFFERENCE IN DAYS
     C*
     C           360       MULT ZYRDF     ZYRDF
     C*
     C           ZDDAYS    ADD  ZYRDF     ZDDAYS
     C*
     C                     END
     C*
     C* IF DAY BASIS IS '2' (ACTUAL DAYS EXCLUDING 29TH FEB)
     C*              OR '3' (ACTUAL DAYS INCLUDING 29TH FEB)
     C*              OR '6' (FIRST AND LAST DAY ACCRUALS)
     C*               SUBTRACT START DATE FROM END DATE
     C*
     C           DYBS      IFEQ '2'
     C           DYBS      OREQ '3'
     C           DYBS      OREQ '6'
     C           ZEDATE    SUB  ZSDATE    ZDDAYS
     C                     END
     C*
     C* IF DAY BASIS IS 2 - (ACTUAL DAYS EXCLUDING 29TH FEB)
     C*               SUBTRACT NUMBER OF 29TH FEB DAYS
     C*
     C           DYBS      IFEQ '2'
     C                     Z-ADD1         ZS      20
     C                     Z-ADD0         ZI1     20
     C                     Z-ADD0         ZI2     20
     C*
     C           ZI1       DOUNE0
     C           ZF29,ZS   IFGT ZSDATE
     C                     Z-ADDZS        ZI1
     C                     ELSE
     C                     ADD  1         ZS
     C                     END
     C                     END
     C*
     C           ZI2       DOUNE0
     C           ZF29,ZS   IFGT ZEDATE
     C                     Z-ADDZS        ZI2
     C                     ELSE
     C                     ADD  1         ZS
     C                     END
     C                     END
     C*
     C           ZI2       SUB  ZI1       ZS
     C           ZDDAYS    SUB  ZS        ZDDAYS
     C                     END
     C*
     C* CALCULATE LATER OF LAST COUPON DATE AND INITIAL DATE
     C*
     C           LCPN      IFGT ITLD
     C                     Z-ADDLCPN      ZWDATE  50
     C                     ELSE
     C                     Z-ADDITLD      ZWDATE
     C                     END
     C*
     C* IF DAY BASIS IS 6 (FIRST AND LAST DAY ACCRUALS)
     C*               IF START DATE EQUALS LATER OF LAST COUPON DATE AND
     C*               INITIAL DATE ADD 1 TO NO OF DAYS
     C*
     C           DYBS      IFEQ '6'
     C           ZSDATE    ANDEQZWDATE
     C                     ADD  1         ZDDAYS
     C                     END
     C*
     C* IF INTEREST ADJUSTMENT DAYS ARE NOT ZERO AND START DATE IS EQUAL TO
     C* LATER OF LAST COUPON DATE AND INITIAL DATE - ADD ADJUSTMENT DAYS TO
     C* NUMBER OF DAYS ALREADY CALCULATED
     C*
     C           DYBS      IFNE '4'
     C           IADJ      ANDNE0
     C***********ZSDATE    ANDEQZWDATE                                    100017
     C           DADJN     ANDNE'N'                                       100017
     C                     ADD  IADJ      ZDDAYS
     C                     END
     C*
     C           EZDAYS    ENDSR                           ** EZDAYS **
     C**********************************************************************
