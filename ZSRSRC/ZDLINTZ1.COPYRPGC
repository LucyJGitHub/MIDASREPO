      *****************************************************************
      *                                                               *
      *  ZDLINT - Standard Subroutine to Calculate the Interest       *
      *           Accruing between two Dates. (Dealing version).      *
      *                                                               *
      *  Input:  ZDLAMT (13,0) Principal Amount                       *
      *          ZDLRAT (13,9) Effective Interest Rate                *
      *          ZCBSDT ( 5,0) Start Date         }                   *
      *          ZCBEDT ( 5,0) End Date           }{for SR/ZCBDYS}    *
      *          ZCBCBS ( 1,0) Calculation Basis  }                   *
      *                                                               *
      *  Output: ZDLOUT (13,0) Interest Amount                        *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CIR013             Date 07Apr05               *
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. 182370             Date 05Jun01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CIR013 - Additional Calculation Method for FRA/IRS/CCF       *
      *  182370 - Incorrect calculation of number of days for calcu-  *
      *           lation basis 6. Amended /COPY such that for calcu-  *
      *           lation basis 6, the number of days per year is      *
      *           equal to 366 when the year in question is a leap    *
      *           year, and 365 days/year for non-leap years. Calcu-  *
      *           lation was made so that processing would be in-line *
      *           with that of /COPY ZINTDY.                          *
      *                                                               *
      *****************************************************************
      *
     C           ZDLINT    BEGSR                           *** ZDLINT ***
      *
      ***  Initialise Fields.
      *
     C                     Z-ADDZDLAMT    ZDLAMT 130
     C                     Z-ADDZDLRAT    ZDLRAT 139
     C                     Z-ADD0         ZDLOUT 130
      *
      ***  Find Number of Days in Period and Year.
      *
     C                     EXSR ZCBDYS
      *
      ***              Principal * Rate * No. of Days in Period
      ***  Interest = ------------------------------------------
      ***                     No. of Days in Year * 100
      *
     C           ZCBDIV    IFNE 0
      *                                                                   182370
     C           ZCBCBS    IFEQ 6                                         182370
     C           ZCBCBS    OREQ 9                                         CIR013
     C           #29FEB    ANDEQ'Y'                                       CIR013
     C           ZDLAMT    MULT ZINT6     ZDLTOP                          182370
     C                     ELSE                                           182370
     C           ZDLAMT    MULT ZCBDAY    ZDLTOP 309
     C                     ENDIF                                          182370
      *                                                                   182370
     C                     MULT ZDLRAT    ZDLTOP
     C           ZCBDIV    MULT 100       ZDLBOT  50
     C           ZDLTOP    DIV  ZDLBOT    ZDLOUT    H
     C                     END
      *
     C                     ENDSR
      *
      *****************************************************************
