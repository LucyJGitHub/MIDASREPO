      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member (RPG)                                   *
      *                                                               *
      *  ZEXPSCLIL1 -                                                 *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CTI002  *CREATE    Date 06Oct98               *
      *  Prev Amend No. nnnnnn             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CTI002 - Recode as ILE version.                              *
      *                                                               *
      *****************************************************************
      *
     C     SCALE         BEGSR
      *
     C     *LIKE         DEFINE    EKLMCA        @DELTP
     C     *LIKE         DEFINE    EKPRD1        @SCAMT           +8
     C     *LIKE         DEFINE    EKPRD1        @TDATE
      *
      *
     C                   MOVEA     *IN(65)       SAV1
     C                   MOVEA     *IN(66)       SAV2
     C                   MOVEA     *IN(67)       SAV3
      *
     C                   SETOFF                                         6667
      *
     C     @DELTP        CHAIN     EMEXSCL1                           65
     C*
      ** If record found, or scaling type not equal to N.(Otherwise
      ** the amount is ignored in these calcs and is not scaled.
     C     *IN65         IFEQ      *OFF
     C     EKSCTY        ANDNE     'N'
      *
      ** If scaling type = I (Interpolation)
     C     EKSCTY        IFEQ      'I'
      *
     C                   MOVE      'N'           @SFLAG            1
      ** If 'TO DATE' <= First period(EKPRD1) then scaling factor is
      ** the first percentage factor(EKPR1P).
     C     @TDATE        IFLE      EKPRD1
     C                   Z-ADD     EKPR1P        @SCLGF
     C                   MOVE      'Y'           @SFLAG
     C                   ENDIF
      *
      ** If 'TO DATE' >= second period(EKPRD2) then scaling factor is
      ** the second percentage factor(EKPR2P).
     C     @TDATE        IFGE      EKPRD2
     C                   Z-ADD     EKPR2P        @SCLGF
     C                   MOVE      'Y'           @SFLAG
     C                   ENDIF
      *
      ** If neither of the above two, do calculations.
     C     @SFLAG        IFEQ      'N'
      *
     C     @TDATE        SUB       EKPRD1        @WRK1             5 0
     C     EKPRD2        SUB       EKPRD1        @WRK2             5 0
     C     @WRK1         DIV       @WRK2         @WRK3            11 5
     C     EKPR2P        SUB       EKPR1P        @WRK4             3 0
     C     @WRK3         MULT      @WRK4         @WRK5             7 4
     C     @WRK5         ADD       EKPR1P        @SCLGF            7 3
      *
     C                   ENDIF
      *
     C                   ENDIF
      *
      ** If scaling type = T (Threshold) or = B (Tiered)
     C     EKSCTY        IFEQ      'T'
     C     EKSCTY        OREQ      'B'
      *
      ** Fill up period array from LF/EMEMXSL1
     C                   Z-ADD     EKPRD1        PER(1)
     C                   Z-ADD     EKPRD2        PER(2)
     C                   Z-ADD     EKPRD3        PER(3)
     C                   Z-ADD     EKPRD4        PER(4)
     C                   Z-ADD     EKPRD5        PER(5)
     C                   Z-ADD     EKPRD6        PER(6)
      *
      ** Fill up period array from LF/EMEMXSL1
     C                   Z-ADD     EKPR1P        PCT(1)
     C                   Z-ADD     EKPR2P        PCT(2)
     C                   Z-ADD     EKPR3P        PCT(3)
     C                   Z-ADD     EKPR4P        PCT(4)
     C                   Z-ADD     EKPR5P        PCT(5)
     C                   Z-ADD     EKPR6P        PCT(6)
      *
     C                   ENDIF
      *
      ** Find scaling percentage.(Threshold)
     C     EKSCTY        IFEQ      'T'
      *
      ** Find period end equal to or higher than 'TO DATE'
     C                   Z-ADD     1             @@C               1 0
     C     @TDATE        LOOKUP    PER(@@C)                           66  67
      *
      ** Move the corresponding percentage into the scaling flag field
     C                   MOVEA     PCT(@@C)      @WRK4
     C                   Z-ADD     @WRK4         @SCLGF
      *
     C                   ENDIF
      *
      ** Find scaling percentage.(Tiered)
     C     EKSCTY        IFEQ      'B'
      *
      ** Find period end equal to or higher than 'TO DATE'
     C                   Z-ADD     1             @@C
     C     @TDATE        LOOKUP    PER(@@C)                           66  67
      *
      ** Add the percentages up to and including the corresponding
      ** band in which the TO DATE was situated.
     C                   Z-ADD     *ZERO         @WRK4
     C                   Z-ADD     1             @D                1 0
     C     @D            DOUGT     @@C
     C                   ADD       PCT(@D)       @WRK4
     C                   ADD       1             @D
     C                   ENDDO
      *
     C                   Z-ADD     @WRK4         @SCLGF
      *
     C                   ENDIF
      *
      ** Calculate scaled exposure.
     C     @SCLGF        MULT      @SCAMT        @SCAMT
     C     @SCAMT        DIV       100           @SCAMT
      *
     C                   ENDIF
      *
     C                   MOVEA     @SAVID        *IN(65)
      *
     C                   ENDSR
     C/EJECT
      *****************************************************************
      * End of /COPY ZEXPSCLIL1                                       *
      *****************************************************************
