     C********************************************************************
      *                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *    LAST AMEND NO. EM0013             DATE 03JUN91                *
      *                                                                  *
      ********************************************************************
      *                                                                  *
      *    EM0013 - INDICATORS 65,66,67 NEED TO BE STORED AND THEN       *
      *             SET TO OLD VALUES AT THE END OF THE COPY MEMBER      *
      *                                                                  *
     C********************************************************************S01332
      *                                                                   S01332
     C           SCALE     BEGSR                                          S01332
     C                     MOVEA*IN,65    @SAVID  3                       EM0013
      *                                                                   S01332
     C                     SETOF                     6667                 S01332
      *                                                                   S01332
     C           @DELTP    CHAINEMEXSCL1             65                   S01332
     C*                                                                   S01332
      ** If record found, or scaling type not equal to N.(Otherwise       S01332
      ** the amount is ignored in these calcs and is not scaled.          S01332
     C           *IN65     IFEQ '0'                        B1             S01332
     C           EKSCTY    ANDNE'N'                        *1             S01332
      *                                                                   S01332
      ** If scaling type = I (Interpolation)                              S01332
     C           EKSCTY    IFEQ 'I'                        B*2            S01332
      *                                                                   S01332
     C                     MOVE 'N'       @SFLAG  1                       S01332
      ** If 'TO DATE' <= First period(EKPRD1) then scaling factor is      S01332
      ** the first percentage factor(EKPR1P).                             S01332
     C           @TDATE    IFLE EKPRD1                     B**3           S01332
     C                     Z-ADDEKPR1P    @SCLGF                          S01332
     C                     MOVE 'Y'       @SFLAG                          S01332
     C                     END                             E**3           S01332
      *                                                                   S01332
      ** If 'TO DATE' >= second period(EKPRD2) then scaling factor is     S01332
      ** the second percentage factor(EKPR2P).                            S01332
     C           @TDATE    IFGE EKPRD2                     B**3           S01332
     C                     Z-ADDEKPR2P    @SCLGF                          S01332
     C                     MOVE 'Y'       @SFLAG                          S01332
     C                     END                             E**3           S01332
      *                                                                   S01332
      ** If neither of the above two, do calculations.                    S01332
     C           @SFLAG    IFEQ 'N'                        B**3           S01332
      *                                                                   S01332
     C           @TDATE    SUB  EKPRD1    @WRK1   50                      S01332
     C           EKPRD2    SUB  EKPRD1    @WRK2   50                      S01332
     C           @WRK1     DIV  @WRK2     @WRK3  115                      S01332
     C           EKPR2P    SUB  EKPR1P    @WRK4   30                      S01332
     C           @WRK3     MULT @WRK4     @WRK5   74                      S01332
     C           @WRK5     ADD  EKPR1P    @SCLGF  73H                     S01332
      *                                                                   S01332
     C                     END                             E**3           S01332
      *                                                                   S01332
     C                     END                             E*2            S01332
      *                                                                   S01332
      ** If scaling type = T (Threshold) or = B (Tiered)                  S01332
     C           EKSCTY    IFEQ 'T'                        B*2            S01332
     C           EKSCTY    OREQ 'B'                        B*2            S01332
      *                                                                   S01332
      ** Fill up period array from LF/EMEMXSL1                            S01332
     C                     Z-ADDEKPRD1    PER,1                           S01332
     C                     Z-ADDEKPRD2    PER,2                           S01332
     C                     Z-ADDEKPRD3    PER,3                           S01332
     C                     Z-ADDEKPRD4    PER,4                           S01332
     C                     Z-ADDEKPRD5    PER,5                           S01332
     C                     Z-ADDEKPRD6    PER,6                           S01332
      *                                                                   S01332
      ** Fill up period array from LF/EMEMXSL1                            S01332
     C                     Z-ADDEKPR1P    PCT,1                           S01332
     C                     Z-ADDEKPR2P    PCT,2                           S01332
     C                     Z-ADDEKPR3P    PCT,3                           S01332
     C                     Z-ADDEKPR4P    PCT,4                           S01332
     C                     Z-ADDEKPR5P    PCT,5                           S01332
     C                     Z-ADDEKPR6P    PCT,6                           S01332
      *                                                                   S01332
     C                     END                             E*2            S01332
      *                                                                   S01332
      ** Find scaling percentage.(Threshold)                              S01332
     C           EKSCTY    IFEQ 'T'                        B*2            S01332
      *                                                                   S01332
      ** Find period end equal to or higher than 'TO DATE'                S01332
     C                     Z-ADD1         @@C     10                      S01332
     C           @TDATE    LOKUPPER,@@C              66  67               S01332
      *                                                                   S01332
      ** Move the corresponding percentage into the scaling flag field    S01332
     C                     MOVEAPCT,@@C   @WRK4                           S01332
     C                     Z-ADD@WRK4     @SCLGF                          S01332
      *                                                                   S01332
     C                     END                             E*2            S01332
      *                                                                   S01332
      ** Find scaling percentage.(Tiered)                                 S01332
     C           EKSCTY    IFEQ 'B'                        B*2            S01332
      *                                                                   S01332
      ** Find period end equal to or higher than 'TO DATE'                S01332
     C                     Z-ADD1         @@C     10                      S01332
     C           @TDATE    LOKUPPER,@@C              66  67               S01332
      *                                                                   S01332
      ** Add the percentages up to and including the corresponding        S01332
      ** band in which the TO DATE was situated.                          S01332
     C                     Z-ADD*ZERO     @WRK4                           S01332
     C                     Z-ADD1         @D      10       B**3           S01332
     C           @D        DOUGT@@C                                       S01332
     C                     ADD  PCT,@D    @WRK4                           S01332
     C                     ADD  1         @D                              S01332
     C                     END                             E**3           S01332
      *                                                                   S01332
     C                     Z-ADD@WRK4     @SCLGF                          S01332
      *                                                                   S01332
     C                     END                             E*2            S01332
      *                                                                   S01332
      ** Calculate scaled exposure.                                       S01332
     C           @SCLGF    MULT @SCAMT    @SCAMT                          S01332
     C           @SCAMT    DIV  100       @SCAMT                          S01332
      *                                                                   S01332
     C                     END                             E1             S01332
      *                                                                   S01332
     C                     MOVEA@SAVID    *IN,65                          EM0013
      *                                                                   EM0013
     C                     ENDSR                                          S01332
1506 C/EJECT                                                              S01332
1398 C********************************************************************S01332
