      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member                                         *
      *                                                               *
      *  ZFEB29Z2LE - Check for the existence of Feb 29 within a      *
      *               period.                                         *
      *                                                               *
      *  Function:  /COPY checks for the existence of Feb 29 within a *
      *             period.                                           *
      *             Start date and end date must be in the format     *
      *             YYYYMMDD.                                         *
      *             Flag #29FEB is set to 'Y' if a Feb 29 occurs      *
      *             during the period, otherwise it is set to 'N'.    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *    /COPY, Client or Country amendments must not be            *
      *    applied to this source.                                    *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CIR013  *CREATE    Date 14Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CIR013 - Additional Calculation Method for FRA/IRS/CCF       *
      *                                                               *
      *****************************************************************
 
     C     #Feb29        Begsr
 
     C                   Eval      #29Feb = 'N'
 
      ** Check for a Feb 29 within the start and end dates
     C                   If        ZZStartDate <= ZZEndDate
 
     C                   Eval      WkStartDate = ZZStartDate
     C                   Eval      WkEndDate = ZZEndDate
 
      ** If the start date year is divisible by 4 (or by 400 for a year
      ** ending in 00) ie. a leap year, and start date on/before 29 Feb and
      ** end date on/after 29 Feb for that year, set flag to 'Y'.
     C                   If        ((WkYY1 <> 0 and %Rem(WkCCYY1:4) = 0) or
     C                              (WkYY1 = 0 and %Rem(WkCCYY1:400) = 0))
     C                             and WkMM1 <= 2 and (WkCCYY2 > WkCCYY1 or
     C                             (WkCCYY1 = WkCCYY2 and WkMMDD2 >= 0229))
     C                   Eval      #29Feb = 'Y'
 
     C                   Elseif    WkCCYY2 > WkCCYY1
     C                   Eval      WkCCYY = WkCCYY1 + 1
 
      ** If end date year > start date year, check each year upto period end
     C                   DoU       WkCCYY > WkCCYY2
 
      ** If the year is divisible by 4 (or by 400 for a year ending in 00)
      ** ie. a leap year, and end date on/after 29 Feb for that year, set
      ** flag to 'Y'.
     C                   If        ((WkYY <> 0 and %Rem(WkCCYY:4) = 0) or
     C                              (WkYY = 0 and %Rem(WkCCYY:400) = 0))
     C                             and (WkCCYY2 > WkCCYY or
     C                             (WkCCYY2 = WkCCYY and WkMMDD2 >= 0229))
     C                   Eval      #29Feb = 'Y'
     C                   Leave
     C                   Endif
 
     C                   Eval      WkCCYY += 1
     C                   Enddo
 
     C                   Endif
     C                   Endif
 
     C                   Endsr
 
      *****************************************************************
      * End of /COPY ZFEB29Z2LE                                       *
      *****************************************************************
