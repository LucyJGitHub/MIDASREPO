      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member (RPG)                                   *
      *                                                               *
      *  XXXXXXX - (/COPY Name)                                       *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. nnnnnn  *CREATE    Date ddmmmyy               *
      *  Prev Amend No. nnnnnn             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  nnnnnn - (change description)                                *
      *                                                               *
      *****************************************************************
     C*****************************************************************
     C*   ZFFRT  - Determine a Forward Forward Interest Rate          *
     C*            (based on MM1001 & MM1011)                         *
     C*  CALLED BY    -                                               *
     C*                                                               *
     C*  CALLS        -                                               *
     C*                                                               *
     C*  FIELDS INPUT @@BORA - Borrow rate for start date             *
     C*               @@BORB - Borrow rate for end date               *
     C*               @@LERA - Lend rate for start date               *
     C*               @@LERB - Lend rate for end date                 *
     C*               @@DYTA - Number of days to start date           *
     C*               @@DYTB - Number of days to end date             *
     C*               @@CCY  - Deal currency                          *
     C*               @@DICM - Default interest calculation method    *
     C*                                                               *
     C*  FIELDS OUTPUT @@BORC - Borrow rate for specified date        *
     C*                @@LERC - Lend rate for specified date          *
     C*                                                               *
     C*  WORKFIELDS   @@FLD1  - Borrow end rate X Number to End days  *
     C*               @@FLD2  - Lend start rate X Number to start days*
     C*               @@FLD3  - Number of days from Start to end      *
     C*               @@FLD5  - Number of Days X 100                  *
     C*               @@FLD6  - 1 plus @@FLD5                         *
     C*               @@NUMA  - Numerator work field of equation      *
     C*               @@DEM   - Denominator work field of equation    *
     C*               @@DAYO  - Number of days in year                *
     C*                                                               *
     C*****************************************************************
     C           ZFFRT     BEGSR
     C*
     C** IF DFLT INTEREST CALCULATION METHOD '1' OR '4' SET DAYS IN
     C** TO '365'
     C           @@DICM    IFEQ 1                          B1
     C           @@DICM    OREQ 4                          *1
     C                     MOVE 365       @@DAYO           *1
     C                     END                             E1
     C*
     C** IF DFLT INTEREST CALCULATION METHOD '2' '3' OR '5' SET DAYS IN
     C** TO '360'
     C           @@DICM    IFEQ 2                          B1
     C           @@DICM    OREQ 3                          *1
     C           @@DICM    OREQ 5                          *1
     C                     MOVE 360       @@DAYO           *1
     C                     END                             E1
     C*
     C** IF DFLT INTEREST CALCULATION METHOD '6' SET DAYS IN YEAR '360'
     C           @@DICM    IFEQ 6                          B1
     C                     MOVE 366       @@DAYO           *1
     C                     END                             E1
     C**
     C**  Calculate MARKET BORROW RATE                                    ME1221
     C           @@BORB    MULT @@DYTB    @@FLD1 157
     C           @@LERA    MULT @@DYTA    @@FLD2 157
     C           @@FLD1    SUB  @@FLD2    @@NUMA 157
     C*
     C**  Number of days from Start to End date
     C           @@DYTB    SUB  @@DYTA    @@FLD3  50
     C*
     C           @@DAYO    MULT 100       @@FLD5  50
     C           @@FLD2    DIV  @@FLD5    @@FLD6 157
     C                     ADD  1         @@FLD6
     C*
     C           @@FLD3    MULT @@FLD6    @@DEM  157
     C*
     C           @@NUMA    DIV  @@DEM     @@BORC 117H                     ME1221
     C*
     C**  Calculate MARKET LENDING RATE                                   ME1221
     C           @@LERB    MULT @@DYTB    @@FLD1 157
     C           @@BORA    MULT @@DYTA    @@FLD2 157
     C           @@FLD1    SUB  @@FLD2    @@NUMA 157
     C*
     C           @@FLD2    DIV  @@FLD5    @@FLD6 157
     C                     ADD  1         @@FLD6
     C*
     C           @@FLD3    MULT @@FLD6    @@DEM  157
     C*
     C           @@NUMA    DIV  @@DEM     @@LERC 117H                     ME1221
     C**
     C           ZFFRT9    ENDSR                           ***ZFFRT9***
     C/EJECT
      *****************************************************************
      * End of /COPY XXXXXXX                                          *
      *****************************************************************
