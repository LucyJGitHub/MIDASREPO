     C********************************************************************
      *                                                               *
      *  Midas - /COPY Member (RPG)                                   *
      *                                                               *
      *  ZFRPEDZ3 - Midas ZS Edit amount                              *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. 191617             Date 28Aug01               *
      *  Prev Amend No. nnnnnn  *CREATE    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  191617 - Amounts having a value of 0 and decimal place       *
      *           equal to 0 are being displayed as blanks.           *
      *                                                               *
     C********************************************************************
     C**                                                                 *
     C** ZFRPED subroutine to insert a decimal point and sign into a     *
     C** numeric field and to blank out leading zeros (optionaly         *
     C** inserting commas).                                              *
     C**     Input fields:   ZFLD15 15/0                                 *
     C**                     ZDECS  1/0                                  *
     C**                     ZECODE 1/  ('A','C','J','L' or blank)       *
     C**                                ( Default is 'J')                *
     C**     Arrays:         ZS1    15/1/0                               *
     C**                     ZS2    22/1/                                *
     C**                                                                 *
     C**     Output fields:  ZOUT22 22                                   *
     C**     Output fields:  ZOUT25 25  (ALIGNED BY DECIMAL POINT)       *
     C**          (The last 2 characters will be '  ','CR',or '- ')      *
     C**
     C           ZFRPED    BEGSR                           **  ZFRPED   *
     C*
     C* Define/Clear fields
     C*
     C                     Z-ADDZFLD15    ZFLD15 150
     C                     Z-ADDZDECS     ZDECS   10
     C                     MOVE ZECODE    ZECODE  1
     C                     MOVE *BLANKS   ZOUT22 22
     C                     MOVE *BLANKS   ZOUT25 25
     C           ZECODE    IFEQ *BLANKS
     C                     MOVE 'J'       ZECODE
     C                     END
     C*
     C*       SET UP WORK FIELDS
     C*
     C                     Z-ADD0         ZS1
     C                     MOVE ' '       ZS2
     C*
     C                     Z-ADD15        Z1      20
     C                     Z-ADD20        Z2      20
     C*
     C           15        SUB  ZDECS     ZINTS   20
     C*
     C* Check if numeric field is negative
     C*
     C           ZFLD15    IFLT *ZEROS
     C           ZECODE    IFEQ 'A'
     C           ZECODE    OREQ 'C'
     C                     MOVEA'CR'      ZS2,21
     C                     ELSE
     C                     MOVE '-'       ZS2,21
     C                     END
     C                     Z-SUBZFLD15    ZFLD15
     C                     END
     C*
     C                     Z-ADDZFLD15    ZWRK15
     C*
     C*       CHECK TO SEE IF THERE ARE ANY DECIMAL PLACES
     C*
     C           ZDECS     CABEQ0         ZS10
     C*
     C*       SET UP DECIMALS
     C*
     C                     Z-ADD*ZEROS    ZCNT    10
     C           ZCNT      DOUEQZDECS
     C                     MOVE ZS1,Z1    ZS2,Z2
     C                     SUB  1         Z2
     C                     ADD  1         ZCNT
     C                     SUB  1         Z1
     C                     END
     C*
     C*       PUT IN DECIMAL PLACE
     C*
     C                     MOVE '.'       ZS2,Z2
     C                     SUB  1         Z2
     C*
     C           ZS10      TAG                             ** ZS10 TAG **
     C*
     C*       SET UP INTEGERS
     C*
     C                     Z-ADD*ZEROS    CNT3    10
     C           Z1        DOUEQ*ZEROS
     C                     MOVE ZS1,Z1    ZS2,Z2
     C                     SUB  1         Z1
     C                     SUB  1         Z2
     C*
     C* If edit code is 'J', insert a ',' before each group of three
     C* digits - not if beginning of array reached.
     C*
     C           Z2        IFGT *ZEROS
     C           ZECODE    ANDEQ'J'
     C           Z2        ORGT *ZEROS
     C           ZECODE    ANDEQ'A'
     C                     ADD  1         CNT3
     C           CNT3      IFEQ 3
     C                     MOVE ','       ZS2,Z2
     C                     SUB  1         Z2
     C                     Z-ADD*ZEROS    CNT3
     C                     END
     C                     END
     C*
     C                     END
     C*
     C*       PUT IN LEADING BLANKS
     C*
     C                     Z-ADD1         Z2
     C           ZS2,Z2    DOWEQ'0'
     C           ZS2,Z2    OREQ ' '
     C           ZS2,Z2    OREQ ','
     C                     MOVE ' '       ZS2,Z2
     C                     ADD  1         Z2
     C           Z2        CABEQ23        ZS20
     C                     END
     C*
     C*       IF NO INTEGERS PUT IN LEADING ZERO
     C*
     C           ZS20      TAG                             ** ZS20 TAG **
     C*
     C           Z2        IFEQ 23
     C           ZFLD15    IFEQ 0                                                             191617
     C           ZDECS     ANDEQ0                                                             191617
     C                     SUB  3         Z2                                                  191617
     C                     ELSE                                                               191617
     C                     SUB  2         Z2
     C                     ENDIF                                                              191617
     C                     MOVE '0'       ZS2,Z2
     C                     ELSE
     C*
     C           ZS2,Z2    IFEQ '.'
     C                     SUB  1         Z2
     C                     MOVE '0'       ZS2,Z2
     C                     END
     C                     END
     C*
     C*       SET UP OUTPUT FIELD
     C*
     C                     MOVEAZS2       ZOUT22
     C*
     C           ZDECS     IFEQ 0
     C                     MOVE ZOUT22    ZOUT0
     C                     ELSE
     C**
     C           ZDECS     IFEQ 1
     C                     MOVE ZOUT22    ZOUT1
     C                     ELSE
     C**
     C           ZDECS     IFEQ 2
     C                     MOVE ZOUT22    ZOUT2
     C                     ELSE
     C**
     C                     MOVE ZOUT22    ZOUT3
     C                     END
     C**
     C                     END
     C**
     C                     END
     C**
     C                     ENDSR
     C*
      *****************************************************************
      * End of /COPY ZFRPEDZ3                                         *
     C********************************************************************
