      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member (RPG)                                   *
      *                                                               *
      *  XXXXXXX - (/COPY Name)                                       *
      *                                                               *
      *  Last Amend No. CLE064             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. nnnnnn  *CREATE    Date ddmmmyy               *
      *                 nnnnnn             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE064 - Enhancement of Lending Frequencies                  *
      *  nnnnnn - (change description)                                *
      *                                                               *
      *****************************************************************
     C********************************************************************
     C**
     C**   ZFRQB2-SR. TO INCREMENT A DAY NUMBER DATE BY A
     C**              PERIOD VALUE DETERMINED BY A CODE.
     C**
     C**   Created 05NOV91 as modified version of ZFREQB.
     C**   Next date is allowed to fall on a non-working day for
     C**   frequencies M,T,Q,X,Y.
     C**
     C********************************************************************
     C           ZFRQB2    BEGSR                           *** ZFRQB2 ***
     C**
     C**   CALCULATIONS TO DEFINE/CLEAR FIELDS.
     C                     MOVE ZFREQ     ZFREQ   1
     C                     Z-ADDZDAYNO    ZDAYNO  50
     C                     Z-ADDZMDAY     ZMDAY   20
     C                     MOVE ZCCY      ZCCY    3
     C                     MOVE ZLOC      ZLOC    3
     C                     MOVE *BLANK    ZZERR@  7
     C**
     C**   CONVERT DAY NUMBER TO DATE.
     C                     EXSR ZDATE2
      *
      ** If the day number input (zmday ) is zero set it equal to the
      ** day number of the start date.
      *
     C           ZMDAY     IFEQ 0
     C*
     C                     MOVELZADATE    ZZWRK2  2
     C                     MOVE ZZWRK2    ZMDAY
     C*
     C                     END
      *
     C                     SETOF                     90
     C**
     C**   CHECK FOR AND INCREMENT FOR CODE D-DAILY-WORKING DAY.
     C           ZFREQ     IFEQ 'D'
     C           ZDAYNO    ADD  1         ZDAYNO
     C                     GOTO ZFHCHK
     C                     END
     C**
     C**   CHECK FOR AND INCREMENT FOR CODE W-WEEKLY.
     C           ZFREQ     IFEQ 'W'
     C           ZDAYNO    ADD  7         ZDAYNO
     C                     GOTO ZFEND
     C                     END
     C**
     C**   CHECK FOR AND INCREMENT FOR CODE F-FORTNIGHTLY.
     C           ZFREQ     IFEQ 'F'
     C           ZDAYNO    ADD  14        ZDAYNO
     C                     GOTO ZFEND
     C                     END
     C**
     C**   CHECK FOR AND INCREMENT FOR CODE S-SEMI-MONTHLY.
     C           ZFREQ     IFEQ 'S'
     C           ZDAY      IFLE 15
     C                     Z-ADD31        ZDAY
     C                     ELSE
     C                     Z-ADD15        ZDAY
     C           ZMTH      ADD  1         ZMTH
     C                     END
     C                     END
     C**
     C**   CHECK FOR AND INCREMENT FOR CODE M-MONTHLY.
     C           ZFREQ     IFEQ 'M'
     C                     Z-ADDZMDAY     ZDAY
     C           ZMTH      ADD  1         ZMTH
     C                     END
     C**
     C**   CHECK FOR AND INCREMENT FOR CODE R-SIX DAYS BEFORE END OF MONTH
     C           ZFREQ     IFEQ 'R'
     C                     Z-ADD31        ZDAY
     C           ZMTH      ADD  1         ZMTH
     C                     END
     C**
     C**   CHECK FOR AND INCREMENT FOR CODE T-BIMONTHLY.
     C           ZFREQ     IFEQ 'T'
     C                     Z-ADDZMDAY     ZDAY
     C           ZMTH      ADD  2         ZMTH
     C                     END
     C**
     C**   CHECK FOR AND INCREMENT FOR CODE Q-QUARTERLY.
     C           ZFREQ     IFEQ 'Q'
     C                     Z-ADDZMDAY     ZDAY
     C           ZMTH      ADD  3         ZMTH
     C                     END
     C**
     C**   CHECK FOR AND INCREMENT FOR CODE X-SIX MONTHLY.
     C           ZFREQ     IFEQ 'X'
     C                     Z-ADDZMDAY     ZDAY
     C           ZMTH      ADD  6         ZMTH
     C                     END
     C**
     C**   CHECK FOR AND INCREMENT FOR CODE Y-YEARLY.
     C           ZFREQ     IFEQ 'Y'
     C                     Z-ADDZMDAY     ZDAY
     C           ZYEAR     ADD  1         ZYEAR
     C                     END
     C**
     C**   CHECK FOR AND INCREMENT FOR CODE L-LAST CALENDAR DAY OF MONTH.
     C           ZFREQ     IFEQ 'L'
     C                     Z-ADD31        ZDAY
     C           ZMTH      ADD  1         ZMTH
     C                     END
     C**
     C**   CHECK FOR AND INCREMENT FOR CODE B-LAST WORKING DAY OF MONTH.
     C           ZFREQ     IFEQ 'B'
     C                     Z-ADD31        ZDAY
     C           ZMTH      ADD  1         ZMTH
     C                     END
      *                                                                                       CLE064
      ** If frequency = '4' for 4-monthly.                                                    CLE064
      *                                                                                       CLE064
     C           ZFREQ     IFEQ '4'                                                           CLE064
     C                     Z-ADDZMDAY     ZDAY                                                CLE064
     C           ZMTH      ADD  4         ZMTH                                                CLE064
     C                     ENDIF                                                              CLE064
      *                                                                                       CLE064
      ** If frequency = '5' for 5-monthly.                                                    CLE064
      *                                                                                       CLE064
     C           ZFREQ     IFEQ '5'                                                           CLE064
     C                     Z-ADDZMDAY     ZDAY                                                CLE064
     C           ZMTH      ADD  5         ZMTH                                                CLE064
     C                     ENDIF                                                              CLE064
      *                                                                                       CLE064
      ** If frequency = '7' for 7-monthly.                                                    CLE064
      *                                                                                       CLE064
     C           ZFREQ     IFEQ '7'                                                           CLE064
     C                     Z-ADDZMDAY     ZDAY                                                CLE064
     C           ZMTH      ADD  7         ZMTH                                                CLE064
     C                     ENDIF                                                              CLE064
      *                                                                                       CLE064
      ** If frequency = '8' for 8-monthly.                                                    CLE064
      *                                                                                       CLE064
     C           ZFREQ     IFEQ '8'                                                           CLE064
     C                     Z-ADDZMDAY     ZDAY                                                CLE064
     C           ZMTH      ADD  8         ZMTH                                                CLE064
     C                     ENDIF                                                              CLE064
      *                                                                                       CLE064
      ** If frequency = '9' for 9-monthly.                                                    CLE064
      *                                                                                       CLE064
     C           ZFREQ     IFEQ '9'                                                           CLE064
     C                     Z-ADDZMDAY     ZDAY                                                CLE064
     C           ZMTH      ADD  9         ZMTH                                                CLE064
     C                     ENDIF                                                              CLE064
      *                                                                                       CLE064
      ** If frequency = 'V' for 10-monthly.                                                   CLE064
      *                                                                                       CLE064
     C           ZFREQ     IFEQ 'V'                                                           CLE064
     C                     Z-ADDZMDAY     ZDAY                                                CLE064
     C           ZMTH      ADD  10        ZMTH                                                CLE064
     C                     ENDIF                                                              CLE064
      *                                                                                       CLE064
      ** If frequency = 'E' for 11-monthly.                                                   CLE064
      *                                                                                       CLE064
     C           ZFREQ     IFEQ 'E'                                                           CLE064
     C                     Z-ADDZMDAY     ZDAY                                                CLE064
     C           ZMTH      ADD  11        ZMTH                                                CLE064
     C                     ENDIF                                                              CLE064
      *                                                                                       CLE064
      ** If frequency = 'C' for two-yearly.                                                   CLE064
      *                                                                                       CLE064
     C           ZFREQ     IFEQ 'C'                                                           CLE064
     C                     Z-ADDZMDAY     ZDAY                                                CLE064
     C           ZYEAR     ADD  2         ZYEAR                                               CLE064
     C                     ENDIF                                                              CLE064
      *                                                                                       CLE064
      ** If frequency = 'I' for three-yearly.                                                 CLE064
      *                                                                                       CLE064
     C           ZFREQ     IFEQ 'I'                                                           CLE064
     C                     Z-ADDZMDAY     ZDAY                                                CLE064
     C           ZYEAR     ADD  3         ZYEAR                                               CLE064
     C                     ENDIF                                                              CLE064
      *                                                                                       CLE064
      ** If frequency = 'J' for four-yearly.                                                  CLE064
      *                                                                                       CLE064
     C           ZFREQ     IFEQ 'J'                                                           CLE064
     C                     Z-ADDZMDAY     ZDAY                                                CLE064
     C           ZYEAR     ADD  4         ZYEAR                                               CLE064
     C                     ENDIF                                                              CLE064
      *                                                                                       CLE064
      ** If frequency = 'N' for five-yearly.                                                  CLE064
      *                                                                                       CLE064
     C           ZFREQ     IFEQ 'N'                                                           CLE064
     C                     Z-ADDZMDAY     ZDAY                                                CLE064
     C           ZYEAR     ADD  5         ZYEAR                                               CLE064
     C                     ENDIF                                                              CLE064
      *                                                                                       CLE064
      ** If frequency = 'U' for ten-yearly.                                                   CLE064
      *                                                                                       CLE064
     C           ZFREQ     IFEQ 'U'                                                           CLE064
     C                     Z-ADDZMDAY     ZDAY                                                CLE064
     C           ZYEAR     ADD  10        ZYEAR                                               CLE064
     C                     ENDIF                                                              CLE064
     C**
     C**   CHECK FOR AND INCREMENT FOR CODE A-MONTHLY ACCRUAL/PROFIT DATE.
     C           ZFREQ     IFEQ 'A'
     C                     CALL 'AOGELRR0'                   <G/LDG.ICD>
     C                     PARM *BLANK    P@AORC  7          B:Return code
     C                     PARM '*FIRST'  P@OPTN  7          I:Option
     C           SDGELR    PARM *BLANK    DSFDY              O:Rec.format
     C           P@AORC    IFEQ *BLANK
     C                     Z-ADDBKAPDT    ZDAYNO
     C                     ELSE
     C                     MOVEL'*ERROR'  ZZERR@
     C                     END
     C**
     C                     GOTO ZFEND
     C                     END
     C**
     C**   REFORMAT DATE AND ENSURE VALID.
     C**
     C**   VALIDATE MONTH.
     C           ZMTH      IFGT 12
     C           ZMTH      SUB  12        ZMTH
     C           ZYEAR     ADD  1         ZYEAR
     C                     END
     C**
     C**   CHECK FOR LEAP YEAR.
     C           ZYEAR     DIV  4         ZLYR    20
     C                     MVR            ZLY     10
     C**
     C**   VALIDATE DAY NUMBER.
     C                     MOVE ZFMD,ZMTH ZWRK2   20
     C           ZMTH      IFEQ 2
     C           ZLY       ANDEQ0
     C                     Z-ADD29        ZWRK2
     C                     END
     C**
     C           ZDAY      IFGT ZWRK2
     C                     Z-ADDZWRK2     ZDAY
     C                     END
     C**
     C**   CHECK IF CODE R, TO SUBTRACT 6 DAYS.
     C           ZFREQ     IFEQ 'R'
     C           ZDAY      SUB  6         ZDAY
     C                     END
     C**
     C**   REFORMAT DATE, DDMMYY OR MMDDYY, AND CONVERT TO DAY NUMBER.
     C                     MOVELZDAY      ZWRK4   40
     C                     MOVE ZMTH      ZWRK4
     C   98                MOVELZMTH      ZWRK4
     C   98                MOVE ZDAY      ZWRK4
     C                     MOVELZWRK4     ZDATE
     C                     MOVE ZYEAR     ZDATE
     C                     EXSR ZDATE1
     C**
     C**   CHECK IF CODE B, TO BE CHECKED FOR HOLIDAY.
     C           ZFREQ     COMP 'B'                      91
     C   91N99             GOTO ZFHCHK
     C**
     C                     GOTO ZFEND
     C**
     C**   CHECK IF DAY NUMBER IS A HOLIDAY.
     C**
     C           ZFHCHK    TAG                             ** ZFHCHK TAG *
     C                     SETOF                     94
     C           ZFREQ     COMP 'D'                      91
     C           ZFREQ     COMP 'B'                      92
     C*
     C**   CHECK IF CURRENCY ON HOLIDAY.
     C                     EXSR ZCHKH
     C           ZIND      CABEQ'W'       ZFEND
     C**
     C   92                GOTO ZDECR
     C*
     C*    INCREMENT OR DECREMENT DAY NUMBER AS REQUIRED.
     C   91      ZDAYNO    ADD  1         ZDAYNO
     C*
     C           ZDECR     TAG                             ** ZDECR TAG **
     C   92      ZDAYNO    SUB  1         ZDAYNO           INCLUDE FOUND
     C                     GOTO ZFHCHK
     C**
     C**
     C           ZFEND     ENDSR                           * ZFEND ENDSR *
     C**
     C********************************************************************
      *****************************************************************
      * End of /COPY XXXXXXX                                          *
      *****************************************************************
