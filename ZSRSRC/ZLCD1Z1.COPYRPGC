      *****************************************************************
      *                                                               *
      *  Midas - Standard Subroutines                                 *
      *                                                               *
      *  ZLCD1Z1 - Calculate the Last Coupon Date from Coupon array   *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSE071             Date 19Jul05               *
      *  Prev Amend No. 170783             Date 15Dec99               *
      *                 CSE035             Date 22Apr02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE071 - Multiple Holidays Re Securities                     *
      *  170783 - Wrong start period in SE0905P1 Accruals report.     *
      *           Start date should be equal to initial date when     *
      *           rundate is prior to first coupon date.  Patterned   *
      *           after fix 130862.                                   *
      *  CSE035 - NX/NNX Coupon Payment Processing.                   *
      *           This was patterned to ZLCDZ1                        *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  INPUT - CD ARRAY - Coupon Dates from PF/SECTYD               *
      *          CR ARRAY - Rate/Payment indicators from PF/SECTYD    *
      *          ITLD   (5,0) Initial Date from PF/SECTYD             *
      *          FCPN   (5,0) First Coupon Date from PF/SECTYD        *
      *          ZECD   (5,0) Event Control Date                      *
      *          *IN98        Date Format from RUNDAT via program     *
      *                                                               *
      *  OUTPUT - ZZLCD1(5,0) Last Coupon Date before Control Date    *
      *                                                               *
      *  CALLS  - ZDATE1                                              *
      *           ZDATE2                                              *
      *           SE0045 - Effect of holidays on FRN coupon dates     *
      *                                                               *
      *****************************************************************
     C           ZLCD1     BEGSR                           *** ZLCD   ***
      *
      ***  Define fields:
      *
     C                     Z-ADDZECD      ZECD    50
     C                     Z-ADD*ZEROS    ZZLXD   50
     C                     Z-ADD*ZEROS    ZZLCD1  50
      *
      ***  if event control date is maturity date this is last coupon date
      *
     C           ZECD      IFGE MATY
     C                     Z-ADDMATY      ZZLXD
     C                     GOTO EZLCD1
     C                     END
      *
      **  If Today is first coupon day then it must also be last
      **  coupon day.
      *
     C           ZECD      IFEQ FCPN
     C                     Z-ADDFCPN      ZZLXD
     C                     GOTO EZLCD1
     C                     ENDIF
      *
      ***  Call ZDATE2 to obtain MMDD for the Control Date
      *
     C                     Z-ADDZECD      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZDATE     ZZECD4  40
     C           *IN98     IFEQ '0'
     C                     MOVELZZECD4    ZZWRKD
     C                     MOVE ZZECD4    ZZWRKM
     C                     Z-ADDZZWRKD    ZZECD4
     C                     MOVELZZWRKM    ZZECD4
     C                     END
      *
      ***  If the Date Format is DDMMYY, then turn the array of Coupon
      ***  Dates around to MMDD format.
      *
     C           *IN98     IFEQ '0'
     C                     DO   12        X       20
     C                     MOVE CD,X      ZZWRKM  20
     C                     MOVELCD,X      ZZWRKD  20
     C                     Z-ADDZZWRKD    CD,X
     C                     MOVELZZWRKM    CD,X
     C                     END
     C                     END
      *
      ***  Get year of Control Date
     C                     MOVE ZDATE     ZECDYR  20
      *
      ***  Find the Prior Coupon Date from the Arrays:
      ***  If the Rate/Payment Indicators are all blank, then ZZLXD
      ***  will equal Initial Date. (SHOULDN'T OCCUR)
      *
     C                     Z-ADDITLD      ZZLXD
      *
      ** As SE0045 has been changed to always return the Last Coupon
      ** Date as is on the file, for Interest calculations we need to
      ** set LCPN to zero at this point so that SE0045 will calculate
      ** the Last Coupon Date according to the array.
      *
     C                     Z-ADDLCPN      UULCPN  50
     C                     Z-ADD*ZERO     LCPN
      *
     C           CRDS      IFNE *BLANKS
     C                     MOVE '0'       ZFOUND  1
     C                     Z-ADD0         TX      20
     C                     Z-ADD12        X
      *
     C           X         DOWGT0
     C           CR,X      IFEQ 'P'
     C           CR,X      OREQ 'C'
     C*
     C** If 29Feb is used in the coupon array, then there is a need to
     C** check if the current year is a leap year. If it is not,
     C** the coupon date is changed to 28. But there is a need to store
     C** the date so that it can be restored back after the check.
     C*
     C                     MOVE CD,X      ZZWRKD
     C                     MOVELCD,X      ZZWRKM
     C           ZZWRKM    IFEQ 2
     C           ZZWRKD    ANDEQ29
     C                     Z-ADDZECDYR    ZZYR    20
     C           ZZYR      ADD  28        ZZYR
     C           ZZYR      DIV  4         ZZYR
     C                     MVR            ZMVR    10
     C           ZMVR      IFGT 0
     C                     Z-ADDX         TX
     C                     MOVE '28'      CD,X
     C                     END
     C                     END
     C*
     C           ZZECD4    IFGE CD,X
      *
      ** If BCNV is not blank then call SE0045 before exiting the loop.
      ***  Set up Coupon Date
      **
     C           BCNV      IFNE ' '
     C           BCNV      ANDNE'N'
     C                     Z-ADDZECDYR    ZDATE
     C                     MOVELCD,X      ZDATE
      *
      ***  Convert the Coupon Date to Midas Day Number using ZDATE1
      ***  If DDMMYY format, turn it around to MMDDYY format
      *
     C           *IN98     IFEQ '0'
     C                     MOVELZDATE     ZZWRK   40
     C                     MOVE ZZWRK     ZZWRKM
     C                     MOVELZZWRK     ZZWRKD
     C                     Z-ADDZZWRKD    ZZWRK
     C                     MOVELZZWRKM    ZZWRK
     C                     MOVELZZWRK     ZDATE
     C                     END
      *
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    ZZLCD
     C                     Z-ADDZZLCD     ZZREAL
      *
     C                     MOVE 'L'       NCI     1
     C                     CALL 'SE0045'
     C                     PARM           SITP
     C                     PARM           ZECD
     C                     PARM           ITLD
     C                     PARM           FCPN
     C                     PARM           CD
     C                     PARM           CR
     C                     PARM           NMCY
     C                     PARM           ZZLCD
     C                     PARM           NCI
     C                     PARM           BCNV
     C                     PARM           MATY
     C                     PARM           LCPN
     C                     PARM           HCY1                            CSE071
     C                     PARM           HCY2                            CSE071
     C                     PARM           HCY3                            CSE071
     C                     PARM           HCY4                            CSE071
     C                     PARM           HCY5                            CSE071
     C                     PARM           HCY6                            CSE071
     C                     PARM           HCY7                            CSE071
     C                     PARM           HCY8                            CSE071
     C                     PARM           HCY9                            CSE071
      *
      ***  Call ZDATE2 to obtain MMDD for the new Last Coupon Date
      *
     C                     Z-ADDZZLCD     ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZDATE     ZZNEW4  40
     C           *IN98     IFEQ '0'
     C                     MOVELZZNEW4    ZZWRKD
     C                     MOVE ZZNEW4    ZZWRKM
     C                     Z-ADDZZWRKD    ZZNEW4
     C                     MOVELZZWRKM    ZZNEW4
     C                     END
     C*
     C                     Z-ADDZZREAL    ZZLCD
     C*
     C           ZZECD4    IFGE ZZNEW4
     C                     MOVE '1'       ZFOUND
     C                     GOTO ZLCX00
     C                     END
     C                     ELSE
     C                     MOVE '1'       ZFOUND
     C                     GOTO ZLCX00
     C                     END
     C                     END
     C                     END
     C                     SUB  1         X
     C                     END
      *
     C           ZLCX00    TAG                             ZLCD00 TAG
      *
      ** Restore the Last Coupon Date (LCPN)
      *
     C                     Z-ADDUULCPN    LCPN
      *
      ***  If found, add the Control Date's YY to form Coupon Date
      *
     C           ZFOUND    IFEQ '1'
     C                     Z-ADDZECDYR    ZDATE
     C                     MOVELCD,X      ZDATE
      *
      *** Else if not found, find the Last Coupon of the previous year
      *
     C                     ELSE
     C                     Z-ADD12        X
      *
     C           X         DOWGT0
     C           CR,X      IFEQ 'P'
     C           CR,X      OREQ 'C'
      *
      ***  Check for the year 2000.
     C           ZECDYR    IFEQ 0
     C                     Z-ADD99        ZDATE
     C                     ELSE
     C                     Z-ADDZECDYR    ZDATE
     C                     SUB  1         ZDATE
     C                     END
      *
     C** If 29Feb is used in the coupon array, then there is a need to
     C** check if the current year is a leap year. If it is not,
     C** the coupon date is changed to 28. But there is a need to store
     C** the date so that it can be restored back after the check.
     C*
     C           TX        IFNE 0
     C                     MOVE '29'      CD,TX
     C                     Z-ADD0         TX
     C                     END
     C*
     C                     MOVE CD,X      ZZWRKD
     C                     MOVELCD,X      ZZWRKM
     C           ZZWRKM    IFEQ 2
     C           ZZWRKD    ANDEQ29
     C           ZECDYR    SUB  1         ZZYR
     C           ZZYR      ADD  28        ZZYR
     C           ZZYR      DIV  4         ZZYR
     C                     MVR            ZMVR    10
     C           ZMVR      IFGT 0
     C                     Z-ADDX         TX
     C                     MOVE '28'      CD,X
     C                     END
     C                     END
     C*
     C                     MOVELCD,X      ZDATE
     C                     MOVE '1'       ZFOUND
     C                     GOTO ZLCX01
     C                     END
     C                     SUB  1         X
     C                     END
      *
     C           ZLCX01    TAG                             ZLCD01 TAG
     C                     END
      *
      ***  Convert the Coupon Date to Midas Day Number using ZDATE1
      ***  (Not done if no Rate/ Payment Indicators are P or C)
      ***  If DDMMYY format, turn it around to MMDDYY format
      *
     C           ZFOUND    IFEQ '1'
      *
     C           *IN98     IFEQ '0'
     C                     MOVELZDATE     ZZWRK   40
     C                     MOVE ZZWRK     ZZWRKM
     C                     MOVELZZWRK     ZZWRKD
     C                     Z-ADDZZWRKD    ZZWRK
     C                     MOVELZZWRKM    ZZWRK
     C                     MOVELZZWRK     ZDATE
     C                     END
      *
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    ZZLXD
     C                     END
      ** Should check if rundate is prior to first coupon date of                             170783
      ** Security.  If so, use initial date as last coupon date.                              170783
      *                                                                                       170783
     C           ITLD      IFNE *ZEROS                                                        170783
     C           ZECD      IFLE ITLD                                                          170783
     C           ZECD      ORLT FCPN                                                          170783
     C           ZECD      ANDGEITLD                                                          170783
     C                     Z-ADDITLD      ZZLXD                                               170783
     C                     ENDIF                                                              170783
     C                     ENDIF                                                              170783
      *                                                                                       170783
      *
      ** Get the value of Last coupon date from the coupon array before
      ** moving the ITLD as Last Coupon Date.
      *
     C                     MOVE ZZLXD     ZZLCD1
      *
     C                     MOVELCSE035    CSE035  1
     C           CSE035    IFNE 'Y'
      *
      *
      ***  If the Initial Date on the Security record is not zero, then
      ***  If the resultant Prior Coupon Date is less than the Initial
      ***  Date or First Coupon, set Prior Coupon Date = Initial Date.
      *
     C           ITLD      IFNE *ZEROS
      *
      ** Made conditioning changes to allow fix 130862.
      ** (First coupon date should be = initial date when rundate is
      ** between initial and first coupon date.)
      *
     C           ZECD      IFLE ITLD
     C           ZECD      ORLT FCPN
     C           ZECD      ANDGEITLD
     C                     Z-ADDITLD      ZZLXD
     C*
     C                     ELSE
     C*
     C*** IF Last coupon date is *LE 1st_Coupon_Date
     C***    and *GT initial date
     C*
     C           FCPN      IFNE *ZEROS
     C           ZZLXD     ANDGTITLD
     C           ZZLXD     ANDLEFCPN
     C                     Z-ADDFCPN      ZZLXD
     C                     ELSE
     C           ZZLXD     SUB  FCPN      @DDIF   50
     C           @DDIF     IFLT 10
     C                     Z-ADDFCPN      ZZLXD
     C                     END
     C                     END
     C                     END
      *
      *** IF Event_Control_Date is *LE 1st_Coupon_Date
     C           FCPN      IFNE *ZEROS
     C           ZZLXD     ANDGTITLD
     C           ZZLXD     ANDLEFCPN
     C                     Z-ADDFCPN      ZZLXD
     C                     END
      *
      ***  If the Initial Date on the Security record is zero, then if
      ***  the resultant Prior Coupon Date is less than the Issue Date
      ***  or First Coupon, set Prior Coupon Date = Issue Date.
      *
     C                     ELSE
      *
     C           ZZLXD     IFLT ISSD
     C           ZECD      ORLT FCPN
     C                     Z-ADDISSD      ZZLXD
     C                     ELSE
     C*
     C*** IF Last Coupon Date is *LE 1st_Coupon_Date
     C***    and *GT issue date
     C           FCPN      IFNE *ZEROS
     C           ZZLXD     ANDGTISSD
     C           ZZLXD     ANDLEFCPN
     C                     Z-ADDFCPN      ZZLXD
     C                     ELSE
     C           ZZLXD     SUB  FCPN      @DDIF
     C           @DDIF     IFLT 10
     C                     Z-ADDFCPN      ZZLXD
     C                     END
     C                     END
     C                     END
      *** IF Event_Control_Date is *LE 1st_Coupon_Date
     C           FCPN      IFNE *ZEROS
     C           ZZLXD     ANDGTISSD
     C           ZZLXD     ANDLEFCPN
     C                     Z-ADDFCPN      ZZLXD
     C                     END
      *
      *
     C                     END
      *
     C                     ENDIF
      *
     C                     END
     C*
     C**  Restore 29Feb to the array if it is changed.
     C*
     C           TX        IFNE 0
     C                     MOVE '29'      CD,TX
     C                     END
      *
      ***  If the Date Format is DDMMYY, then return the Array
      ***  of Coupon Dates to it's former state of DDMM.
      *
     C           *IN98     IFEQ '0'
     C                     DO   12        X       20
     C                     MOVELCD,X      ZZWRKM  20
     C                     MOVE CD,X      ZZWRKD  20
     C                     Z-ADDZZWRKM    CD,X
     C                     MOVELZZWRKD    CD,X
     C                     END
     C                     END
      *
      * At this stage the field ZZLXD holds details of REAL LAST COUPON
      * DATE save this field into work field ZZREAL for use in SE2235.
      *
     C                     Z-ADDZZLXD     ZZREAL  50
      *
      ** If BCNV is not blank then call SE0045
      ** OR BCNV is not 'N' then call SE0045
      **
     C           BCNV      IFNE ' '
     C           BCNV      ANDNE'N'
     C                     MOVE 'L'       NCI     1
     C                     CALL 'SE0045'
     C                     PARM           SITP
     C                     PARM           ZECD
     C                     PARM           ITLD
     C                     PARM           FCPN
     C                     PARM           CD
     C                     PARM           CR
     C                     PARM           NMCY
     C                     PARM           ZZLXD
     C                     PARM           NCI
     C                     PARM           BCNV
     C                     PARM           MATY
     C                     PARM           LCPN
     C                     PARM           HCY1                            CSE071
     C                     PARM           HCY2                            CSE071
     C                     PARM           HCY3                            CSE071
     C                     PARM           HCY4                            CSE071
     C                     PARM           HCY5                            CSE071
     C                     PARM           HCY6                            CSE071
     C                     PARM           HCY7                            CSE071
     C                     PARM           HCY8                            CSE071
     C                     PARM           HCY9                            CSE071
     C                     ENDIF
     C*
     C           EZLCD1    ENDSR
      *
      *****************************************************************
      * End of /COPY ZLCD1Z1                                          *
      *****************************************************************
