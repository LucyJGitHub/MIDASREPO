      *****************************************************************
      **                                                             **
      ** ZLCD   - Subroutine to calculate the Last Coupon Date prior **
      **          to an Event Control Date (For Example: the Value   **
      **          Date of a Trade).                                  **
      **                                                             **
      ** INPUT - CD ARRAY - Coupon Dates from PF/SECTYD              **
      **         CR ARRAY - Rate/Payment indicators from PF/SECTYD   **
      **         ITLD   (5,0) Initial Date from PF/SECTYD            **
      **         FCPN   (5,0) First Coupon Date from PF/SECTYD       **
      **         ZECD   (5,0) Event Control Date                     **
      ************IN98********Date*Format*from*PF/TABTB10**************   S01117
      **         *IN98        Date Format from RUNDAT via program    **   S01117
      **         HCY1         CSE071 Holidays                        **   CSE071
      **         HCY2         CSE071 Holidays                        **   CSE071
      **         HCY3         CSE071 Holidays                        **   CSE071
      **         HCY4         CSE071 Holidays                        **   CSE071
      **         HCY5         CSE071 Holidays                        **   CSE071
      **         HCY6         CSE071 Holidays                        **   CSE071
      **         HCY7         CSE071 Holidays                        **   CSE071
      **         HCY8         CSE071 Holidays                        **   CSE071
      **         HCY9         CSE071 Holidays                        **   CSE071
      **                                                             **
      ** OUTPUT - ZZLCD (5,0) Last Coupon Date before Control Date   **
      **                                                             **
      ** CALLS  - ZDATE1                                             **
      **          ZDATE2                                             **
      **          SE0045 - Effect of holidays on FRN coupon dates    **
      **                                                             **
      *****************************************************************
      **                                                             **
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      ** Last Amend No. BUG8884            Date 05Oct05              **
      ** Prev Amend No. CSE071             Date 19Jul05              **
      **                170783             Date 15Dec99              **
      **                CSE035             Date 22Apr02              **
      **                206316             Date 19Jul02              **
      * Midas Release 4.01 -------------------------------------------*
      **                CSE031             Date 09Nov01              **
      **                183891             Date 22Sep00              **
      **                168141             Date 29Sep99              **
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      **                130862             Date 16Oct98              **
      **                CSE005             Date 22Jan97              **
      **                100228             DATE 30MAY96              **
      **                091254             DATE 20OCT95              **
      **                079952             DATE 20OCT95              **
      **                072688             DATE 20OCT95              **
      **                E27092             DATE 04SEP91              **
      **                S01117             DATE 18JAN91              **
      **                E21616             DATE 03APR90              **
      **                E20085             DATE 03NOV89              **
      **                                                             **
      *---------------------------------------------------------------*
      **                                                             **
      ** BUG8884- CSE071 holidays currencies not defined             **
      ** CSE071 - Multiple Holidays Re Securities                    **
      ** 170783 - Wrong start period in SE0905P1 Accruals report.    **
      **          Start date should be equal to initial date when    **
      **          rundate is prior to first coupon date.  Patterned  **
      **          after fix 130862.                                  **
      ** CSE035 - NX/NNX Coupon Payment Processing.                  **
      ** 206316 - ZDAYNO is incorrect for Interest calculations if   **
      **          CSE031 is switched on.                             **
      ** CSE031 - Coupon Fixing for Floating Rate Notes              **
      ** 183891 - If BCNV is non-blank, call SE0045 BEFORE deciding  **
      **          that the Last Coupon Date has been found.          **
      ** 168141 - Switchable feature CSE005 introduced the ability   **
      **          to use next working day as last coupon date if that**
      **          date was a non working day. Therefore need to est- **
      **          ablish real coupon date to stop looping.Have agreed**
      **          with core management (Keith Eagle) that since this **
      **          will not effect any other program than SE2235 will **
      **          do not need to recompile all programs using this   **
      **          sub routine. Program therefore saves REAL LAST     **
      **          COUPON DATE before calling SE0045. T Sales 29/9/99 **
      ** 130862 - Last and next coupon dates should be the same as   **
      **          Rundate when rundate = first coupon date.          **
      ** CSE005 - Effect of holidays on FRN coupon dates.            **
      ** 100228 - Process added to allow 29Feb to be used in the     **
      **          the coupon array in the Securities detail.         **
      ** 091254 - When the first coupon date do not correspond  to   **
      **          a coupon date in the coupon date array, the last   **
      **          coupon date for the second period is incorrect.    **
      ** 079952 - Fix last coupon date for 072688                    **
      **          When the first coupon date do not corresponde to   **
      **          a coupon date array, the last coupon date for the  **
      **          second period is incorrect. This is because fix    **
      **          072688 is incomplete.                              **
      ** 072688 - If ECD is less/equal to 1st coupon date            **
      **          then the last coupon date equals 1st coupon date.  **
      ** E27092 - IF ECD IS MATURITY DATE THIS SHOULD BE THE LAST    **
      **          COUPON DATE                                        **
      ** S01117 - Release 10 multibranching.                         **
      ** E21616 - If the Subroutine has to find the latest Coupon in **   E21616
      **          the previous year, it subtracts 1 from ZECDYR. If  **   E21616
      **          the year is '2000', we get '00 - 1', ie.'-1'.      **   E21616
      **          Add an IF to cater for ZECDYR = 00.                **   E21616
      ** E20085 - Created for Interest and Coupons calculations      **   E20085
      **          rewrite.                                           **   E20085
      **                                                             **
      *****************************************************************
      *
     C           ZLCD      BEGSR                           *** ZLCD   ***
      *
      ***  Define fields:
      *
     C*********************Z-ADDECD       ECD     50       DEFINED 072688 079952
     C                     Z-ADDZECD      ZECD    50
     C                     Z-ADD*ZEROS    ZZLCD   50
      *                                                                   E27092
      ***  if event control date is maturity date this is last coupon dateE27092
      *                                                                   E27092
     C***********ZECD      IFEQ MATY                               E27092 091254
     C           ZECD      IFGE MATY                                      091254
     C***********          Z-ADDZECD      ZZLCD                    E27092 091254
     C                     Z-ADDMATY      ZZLCD                           091254
     C                     GOTO EZLCD                                     E27092
     C                     END                                            E27092
      *                                                                   130862
      **  If Today is first coupon day then it must also be last          130862
      **  coupon day.                                                     130862
      *                                                                   130862
     C           ZECD      IFEQ FCPN                                      130862
     C                     Z-ADDFCPN      ZZLCD                           130862
     C                     GOTO EZLCD                                     130862
     C                     ENDIF                                          130862
      *
      ***  Call ZDATE2 to obtain MMDD for the Control Date
      *
     C                     Z-ADDZECD      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZDATE     ZZECD4  40
     C           *IN98     IFEQ '0'
     C                     MOVELZZECD4    ZZWRKD
     C                     MOVE ZZECD4    ZZWRKM
     C                     Z-ADDZZWRKD    ZZECD4
     C                     MOVELZZWRKM    ZZECD4
     C                     END
      *
      ***  If the Date Format is DDMMYY, then turn the array of Coupon
      ***  Dates around to MMDD format.
      *
     C           *IN98     IFEQ '0'
     C                     DO   12        X       20
     C                     MOVE CD,X      ZZWRKM  20
     C                     MOVELCD,X      ZZWRKD  20
     C                     Z-ADDZZWRKD    CD,X
     C                     MOVELZZWRKM    CD,X
     C                     END
     C                     END
      *
      ***  Get year of Control Date
     C                     MOVE ZDATE     ZECDYR  20
      *
      ***  Find the Prior Coupon Date from the Arrays:
      ***  If the Rate/Payment Indicators are all blank, then ZZLCD
      ***  will equal Initial Date. (SHOULDN'T OCCUR)
      *
     C                     Z-ADDITLD      ZZLCD
      *
      ***As*SE0045*has*been*changed*to*always*return*the*Last*Coup CSE031 206316
      ***Date*as*is*on*the*file,*for*Interest*calculations*we*need*CSE031 206316
      ***set*LCPN*to*zero*at*this*point*so*that*SE0045*will*calcul CSE031 206316
      ***the*Last*Coupon*Date*according*to*the*array.              CSE031 206316
      *                                                            CSE031 206316
     C*********************Z-ADDLCPN      UULCPN  50               CSE031 206316
     C*********************Z-ADD*ZERO     LCPN                     CSE031 206316
      *                                                            CSE031 206316
     C           CRDS      IFNE *BLANKS
     C                     MOVE '0'       ZFOUND  1
     C                     Z-ADD0         TX      20                      100228
     C                     Z-ADD12        X
      *
     C           X         DOWGT0
     C           CR,X      IFEQ 'P'
     C           CR,X      OREQ 'C'
     C*                                                                   100228
     C** If 29Feb is used in the coupon array, then there is a need to    100228
     C** check if the current year is a leap year. If it is not,          100228
     C** the coupon date is changed to 28. But there is a need to store   100228
     C** the date so that it can be restored back after the check.        100228
     C*                                                                   100228
     C                     MOVE CD,X      ZZWRKD                          100228
     C                     MOVELCD,X      ZZWRKM                          100228
     C           ZZWRKM    IFEQ 2                                         100228
     C           ZZWRKD    ANDEQ29                                        100228
     C                     Z-ADDZECDYR    ZZYR    20                      100228
     C           ZZYR      ADD  28        ZZYR                            100228
     C           ZZYR      DIV  4         ZZYR                            100228
     C                     MVR            ZMVR    10                      100228
     C           ZMVR      IFGT 0                                         100228
     C                     Z-ADDX         TX                              100228
     C                     MOVE '28'      CD,X                            100228
     C                     END                                            100228
     C                     END                                            100228
     C*                                                                   100228
     C           ZZECD4    IFGE CD,X
      *                                                                   183891
      ** If BCNV is not blank then call SE0045 before exiting the loop.   183891
      ***  Set up Coupon Date                                             183891
      **                                                                  183891
     C           BCNV      IFNE ' '                                       183891
     C           BCNV      ANDNE'N'                                       CSE031
     C                     Z-ADDZECDYR    ZDATE                           183891
     C                     MOVELCD,X      ZDATE                           183891
      *                                                                   183891
      ***  Convert the Coupon Date to Midas Day Number using ZDATE1       183891
      ***  If DDMMYY format, turn it around to MMDDYY format              183891
      *                                                                   183891
     C           *IN98     IFEQ '0'                                       183891
     C                     MOVELZDATE     ZZWRK   40                      183891
     C                     MOVE ZZWRK     ZZWRKM                          183891
     C                     MOVELZZWRK     ZZWRKD                          183891
     C                     Z-ADDZZWRKD    ZZWRK                           183891
     C                     MOVELZZWRKM    ZZWRK                           183891
     C                     MOVELZZWRK     ZDATE                           183891
     C                     END                                            183891
      *                                                                   183891
     C                     EXSR ZDATE1                                    183891
     C                     Z-ADDZDAYNO    ZZLCD                           183891
     C                     Z-ADDZZLCD     ZZREAL                          183891
      *                                                                   183891
     C                     MOVE 'L'       NCI     1                       183891
     C                     CALL 'SE0045'                                  183891
     C                     PARM           SITP                            183891
     C                     PARM           ZECD                            183891
     C                     PARM           ITLD                            183891
     C                     PARM           FCPN                            183891
     C                     PARM           CD                              183891
     C                     PARM           CR                              183891
     C                     PARM           NMCY                            183891
     C                     PARM           ZZLCD                           183891
     C                     PARM           NCI                             183891
     C                     PARM           BCNV                            183891
     C                     PARM           MATY                            183891
     C                     PARM           LCPN                            CSE031
     C******************** PARM           HCY1                            CSE071BUG8884
     C******************** PARM           HCY2                            CSE071BUG8884
     C******************** PARM           HCY3                            CSE071BUG8884
     C******************** PARM           HCY4                            CSE071BUG8884
     C******************** PARM           HCY5                            CSE071BUG8884
     C******************** PARM           HCY6                            CSE071BUG8884
     C******************** PARM           HCY7                            CSE071BUG8884
     C******************** PARM           HCY8                            CSE071BUG8884
     C******************** PARM           HCY9                            CSE071BUG8884
     C                     PARM           HCY1    3                       BUG8884
     C                     PARM           HCY2    3                       BUG8884
     C                     PARM           HCY3    3                       BUG8884
     C                     PARM           HCY4    3                       BUG8884
     C                     PARM           HCY5    3                       BUG8884
     C                     PARM           HCY6    3                       BUG8884
     C                     PARM           HCY7    3                       BUG8884
     C                     PARM           HCY8    3                       BUG8884
     C                     PARM           HCY9    3                       BUG8884
      *                                                                   183891
      ***  Call ZDATE2 to obtain MMDD for the new Last Coupon Date        183891
      *                                                                   183891
     C                     Z-ADDZZLCD     ZDAYNO                          183891
     C                     EXSR ZDATE2                                    183891
     C                     MOVELZDATE     ZZNEW4  40                      183891
     C           *IN98     IFEQ '0'                                       183891
     C                     MOVELZZNEW4    ZZWRKD                          183891
     C                     MOVE ZZNEW4    ZZWRKM                          183891
     C                     Z-ADDZZWRKD    ZZNEW4                          183891
     C                     MOVELZZWRKM    ZZNEW4                          183891
     C                     END                                            183891
     C*                                                                   183891
     C                     Z-ADDZZREAL    ZZLCD                           183891
     C*                                                                   183891
     C           ZZECD4    IFGE ZZNEW4                                    183891
     C                     MOVE '1'       ZFOUND                          183891
     C                     GOTO ZLCD00                                    183891
     C                     END                                            183891
     C                     ELSE                                           183891
     C                     MOVE '1'       ZFOUND
     C                     GOTO ZLCD00
     C                     END                                            183891
     C                     END
     C                     END
     C                     SUB  1         X
     C                     END
      *
     C           ZLCD00    TAG                             ZLCD00 TAG
      *                                                            CSE031 206316
      ***Restore*the*Last*Coupon*Date*(LCPN)                       CSE031 206316
      *                                                            CSE031 206316
     C*********************Z-ADDUULCPN    LCPN                     CSE031 206316
      *
      ***  If found, add the Control Date's YY to form Coupon Date
      *
     C           ZFOUND    IFEQ '1'
     C                     Z-ADDZECDYR    ZDATE
     C                     MOVELCD,X      ZDATE
      *
      *** Else if not found, find the Last Coupon of the previous year
      *
     C                     ELSE
     C                     Z-ADD12        X
      *
     C           X         DOWGT0
     C           CR,X      IFEQ 'P'
     C           CR,X      OREQ 'C'
      *                                                                   E21616
      ***  Check for the year 2000.                                       E21616
     C           ZECDYR    IFEQ 0                                         E21616
     C                     Z-ADD99        ZDATE                           E21616
     C                     ELSE                                           E21616
     C                     Z-ADDZECDYR    ZDATE
     C                     SUB  1         ZDATE
     C                     END                                            E21616
      *                                                                   E21616
     C** If 29Feb is used in the coupon array, then there is a need to    100228
     C** check if the current year is a leap year. If it is not,          100228
     C** the coupon date is changed to 28. But there is a need to store   100228
     C** the date so that it can be restored back after the check.        100228
     C*                                                                   100228
     C           TX        IFNE 0                                         100228
     C                     MOVE '29'      CD,TX                           100228
     C                     Z-ADD0         TX                              100228
     C                     END                                            100228
     C*                                                                   100228
     C                     MOVE CD,X      ZZWRKD                          100228
     C                     MOVELCD,X      ZZWRKM                          100228
     C           ZZWRKM    IFEQ 2                                         100228
     C           ZZWRKD    ANDEQ29                                        100228
     C           ZECDYR    SUB  1         ZZYR                            100228
     C           ZZYR      ADD  28        ZZYR                            100228
     C           ZZYR      DIV  4         ZZYR                            100228
     C                     MVR            ZMVR    10                      100228
     C           ZMVR      IFGT 0                                         100228
     C                     Z-ADDX         TX                              100228
     C                     MOVE '28'      CD,X                            100228
     C                     END                                            100228
     C                     END                                            100228
     C*                                                                   100228
     C                     MOVELCD,X      ZDATE
     C                     MOVE '1'       ZFOUND
     C                     GOTO ZLCD01
     C                     END
     C                     SUB  1         X
     C                     END
      *
     C           ZLCD01    TAG                             ZLCD01 TAG
     C                     END
      *
      ***  Convert the Coupon Date to Midas Day Number using ZDATE1
      ***  (Not done if no Rate/ Payment Indicators are P or C)
      ***  If DDMMYY format, turn it around to MMDDYY format
      *
     C           ZFOUND    IFEQ '1'
      *
     C           *IN98     IFEQ '0'
     C                     MOVELZDATE     ZZWRK   40
     C                     MOVE ZZWRK     ZZWRKM
     C                     MOVELZZWRK     ZZWRKD
     C                     Z-ADDZZWRKD    ZZWRK
     C                     MOVELZZWRKM    ZZWRK
     C                     MOVELZZWRK     ZDATE
     C                     END
      *
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    ZZLCD
     C                     END
      *                                                                                       170783
      ** Should check if rundate is prior to first coupon date of                             170783
      ** Security.  If so, use initial date as last coupon date.                              170783
      *                                                                                       170783
     C           ITLD      IFNE *ZEROS                                                        170783
     C           ZECD      IFLE ITLD                                                          170783
     C           ZECD      ORLT FCPN                                                          170783
     C           ZECD      ANDGEITLD                                                          170783
     C                     Z-ADDITLD      ZZLCD                                               170783
     C                     ENDIF                                                              170783
     C                     ENDIF                                                              170783
      *                                                                                       CSE035
     C                     MOVELCSE035    CSE035  1                                           CSE035
     C           CSE035    IFNE 'Y'                                                           CSE035
      *                                                                   206316
      ** If CSE031 is on, SE0045 will always return the Last Coupon       206316
      ** Date as is on the file. Therefore, at this point, for            206316
      ** Interest calculations, ZDAYNO is incorrect as it uses the        206316
      ** Coupon array to calculate the Last Coupon Date. We need to       206316
      ** Set ZDAYNO to LCPN if CSE031 is on.                              206316
      *                                                                   206316
      ** Check if feature CSE031 is on                                    206316
     C                     CALL 'AOSARDR0'                                206316
     C                     PARM           URTNCD  7                       206316
     C                     PARM '*VERIFY' UOPTN   7                       206316
     C                     PARM 'CSE031'  UUKEY   6                       206316
     C                     PARM           UDSFDY 76                       206316
      *                                                                   CSE071
     C                     MOVE 'N'       CSE031  1                       CSE071
     C           URTNCD    IFEQ *BLANKS                                   CSE071
     C                     MOVE 'Y'       CSE031                          CSE071
     C                     ENDIF                                          CSE071
      *                                                                   CSE071
      ** Check if feature CSE071 is on                                    CSE071
     C                     CALL 'AOSARDR0'                                CSE071
     C                     PARM           URTNCD                          CSE071
     C                     PARM '*VERIFY' UOPTN                           CSE071
     C                     PARM 'CSE071'  UUKEY                           CSE071
     C                     PARM           UDSFDY                          CSE071
      *                                                                   CSE071
     C                     MOVE 'N'       CSE071  1                       CSE071
     C           URTNCD    IFEQ *BLANKS                                   CSE071
     C                     MOVE 'Y'       CSE071                          CSE071
     C                     ENDIF                                          CSE071
      *                                                                   CSE071
      ** If CSE031 feature is on                                          206316
      ** or CSE071 feature is on                                          CSE071
     C********** URTNCD    IFEQ *BLANKS                             206316CSE071
     C           CSE031    IFEQ 'Y'                                       CSE071
     C           CSE071    OREQ 'Y'                                       CSE071
     C           BCNV      IFNE ' '                                       206316
     C           BCNV      ANDNE'N'                                       206316
     C           LCPN      ANDNE*ZEROS                                    206316
     C                     Z-ADDLCPN      ZDAYNO                          206316
     C                     Z-ADDLCPN      ZZLCD                           206316
     C                     ENDIF                                          206316
     C                     ENDIF                                          206316
      *
      ***  If the Initial Date on the Security record is not zero, then
      ***  If the resultant Prior Coupon Date is less than the Initial
      ***  Date or First Coupon, set Prior Coupon Date = Initial Date.
      *
     C           ITLD      IFNE *ZEROS
      *
      ** Made conditioning changes to allow fix 130862.                   130862
      ** (First coupon date should be = initial date when rundate is      130862
      ** between initial and first coupon date.)                          130862
      *                                                                   130862
     C***********ZZLCD     IFLT ITLD                                      130862
     C***********ZZLCD     ORLT FCPN                                      079952
     C***********ZECD      ORLT FCPN                               079952 130862
     C           ZECD      IFLE ITLD                                      130862
     C           ZECD      ORLT FCPN                                      130862
     C           ZECD      ANDGEITLD                                      130862
     C                     Z-ADDITLD      ZZLCD
     C*                                                                   130862
     C                     ELSE                                           091254
     C*                                                                   091254
     C*** IF Last coupon date is *LE 1st_Coupon_Date                      091254
     C***    and *GT initial date                                         091254
     C*                                                                   091254
     C           FCPN      IFNE *ZEROS                                    091254
     C           ZZLCD     ANDGTITLD                                      091254
     C           ZZLCD     ANDLEFCPN                                      091254
     C                     Z-ADDFCPN      ZZLCD                           091254
     C                     ELSE                                           091254
     C           ZZLCD     SUB  FCPN      @DDIF   50                      091254
     C           @DDIF     IFLT 10                                        091254
     C                     Z-ADDFCPN      ZZLCD                           091254
     C                     END                                            091254
     C                     END                                            091254
     C                     END
      *
      *** IF Event_Control_Date is *LE 1st_Coupon_Date                    072688
     C           FCPN      IFNE *ZEROS                                    072688
     C***********ECD       ANDLEFCPN                               072688 079952
     C           ZZLCD     ANDGTITLD                                      079952
     C           ZZLCD     ANDLEFCPN                                      079952
     C                     Z-ADDFCPN      ZZLCD                           072688
     C                     END                                            072688
      *                                                                   072688
      ***  If the Initial Date on the Security record is zero, then if
      ***  the resultant Prior Coupon Date is less than the Issue Date
      ***  or First Coupon, set Prior Coupon Date = Issue Date.
      *
     C                     ELSE
      *
     C           ZZLCD     IFLT ISSD
     C***********ZZLCD     ORLT FCPN                                      079952
     C           ZECD      ORLT FCPN                                      079952
     C                     Z-ADDISSD      ZZLCD
     C                     ELSE                                           091254
     C*                                                                   091254
     C*** IF Last Coupon Date is *LE 1st_Coupon_Date                      091254
     C***    and *GT issue date                                           091254
     C           FCPN      IFNE *ZEROS                                    091254
     C           ZZLCD     ANDGTISSD                                      091254
     C           ZZLCD     ANDLEFCPN                                      091254
     C                     Z-ADDFCPN      ZZLCD                           091254
     C                     ELSE                                           091254
     C           ZZLCD     SUB  FCPN      @DDIF                           091254
     C           @DDIF     IFLT 10                                        091254
     C                     Z-ADDFCPN      ZZLCD                           091254
     C                     END                                            091254
     C                     END                                            091254
     C                     END
      *** IF Event_Control_Date is *LE 1st_Coupon_Date                    072688
     C           FCPN      IFNE *ZEROS                                    072688
     C***********ECD       ANDLEFCPN                               072688 079952
     C           ZZLCD     ANDGTISSD                                      079952
     C           ZZLCD     ANDLEFCPN                                      079952
     C                     Z-ADDFCPN      ZZLCD                           072688
     C                     END                                            072688
      *                                                                   072688
      *
     C                     END
      *                                                                                       CSE035
     C                     ENDIF                                                              CSE035
      *                                                                                       CSE035
     C                     END
     C*                                                                   100228
     C**  Restore 29Feb to the array if it is changed.                    100228
     C*                                                                   100228
     C           TX        IFNE 0                                         100228
     C                     MOVE '29'      CD,TX                           100228
     C                     END                                            100228
      *
      ***  If the Date Format is DDMMYY, then return the Array
      ***  of Coupon Dates to it's former state of DDMM.
      *
     C           *IN98     IFEQ '0'
     C                     DO   12        X       20
     C                     MOVELCD,X      ZZWRKM  20
     C                     MOVE CD,X      ZZWRKD  20
     C                     Z-ADDZZWRKM    CD,X
     C                     MOVELZZWRKD    CD,X
     C                     END
     C                     END
      *                                                                   168141
      * At this stage the field ZZLCD holds details of REAL LAST COUPON   168141
      * DATE save this field into work field ZZREAL for use in SE2235.    168141
      *                                                                   168141
     C                     Z-ADDZZLCD     ZZREAL  50                      168141
      *
      ** If BCNV is not blank then call SE0045                            CSE005
      ** OR BCNV is not 'N' then call SE0045                              CSE031
      **                                                                  CSE005
     C           BCNV      IFNE ' '                                       CSE005
     C           BCNV      ANDNE'N'                                       CSE031
     C                     MOVE 'L'       NCI     1                       CSE005
     C                     CALL 'SE0045'                                  CSE005
     C                     PARM           SITP                            CSE005
     C                     PARM           ZECD                            CSE005
     C                     PARM           ITLD                            CSE005
     C                     PARM           FCPN                            CSE005
     C                     PARM           CD                              CSE005
     C                     PARM           CR                              CSE005
     C                     PARM           NMCY                            CSE005
     C                     PARM           ZZLCD                           CSE005
     C                     PARM           NCI                             CSE005
     C                     PARM           BCNV                            CSE005
     C                     PARM           MATY                            CSE005
     C                     PARM           LCPN                            CSE031
     C                     PARM           HCY1                            CSE071
     C                     PARM           HCY2                            CSE071
     C                     PARM           HCY3                            CSE071
     C                     PARM           HCY4                            CSE071
     C                     PARM           HCY5                            CSE071
     C                     PARM           HCY6                            CSE071
     C                     PARM           HCY7                            CSE071
     C                     PARM           HCY8                            CSE071
     C                     PARM           HCY9                            CSE071
     C                     ENDIF                                          CSE005
     C*                                                                   CSE005
     C           EZLCD     ENDSR
      *
      *****************************************************************
