      *****************************************************************
      **                                                             **
      ** ZLCR   - Subroutine to calculate the Last Rate Change prior **
      **          to an Event Control Date (For Example: the Value   **
      **          Date of a Trade).                                  **
      **                                                             **
      ** INPUT - CD ARRAY - Coupon/Rate Dates from PF/SECTYD         **
      **         CR ARRAY - Rate/Payment indicators from PF/SECTYD   **
      **         ITLD   (5,0) Initial Date from PF/SECTYD            **
      **         FCPN   (5,0) First Coupon/Rate Date from PF/SECTYD  **
      **         ZECD   (5,0) Event Control Date                     **
      **         *IN98        Date Format from RUNDAT via program    **
      **         HCY1         CSE071 Holidays                        **   CSE071
      **         HCY2         CSE071 Holidays                        **   CSE071
      **         HCY3         CSE071 Holidays                        **   CSE071
      **         HCY4         CSE071 Holidays                        **   CSE071
      **         HCY5         CSE071 Holidays                        **   CSE071
      **         HCY6         CSE071 Holidays                        **   CSE071
      **         HCY7         CSE071 Holidays                        **   CSE071
      **         HCY8         CSE071 Holidays                        **   CSE071
      **         HCY9         CSE071 Holidays                        **   CSE071
      **                                                             **
      ** OUTPUT - ZZLCR (5,0) Last Rate Change Date Before Control   **
      **          Date                                               **
      **                                                             **
      ** CALLS  - ZDATE1                                             **
      **          ZDATE2                                             **
      **          SE0045 - Effect of holidays on FRN coupon dates    **
      **                                                             **
      *****************************************************************
      **                                                             **
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      ** Last Amend No. CSE071             Date 19Jul05              **
      * Midas Release 4.01 -------------------------------------------*
      ** Prev Amend No. CSE031             Date 09Nov01              **
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      **                CSE005             DATE 23JAN97              **
      **                CSE004             DATE 28NOV96              **
      **                                                             **
      *---------------------------------------------------------------*
      **                                                             **
      *  CSE071 - Multiple Holidays Re Securities                     *
      *  CSE031 - Coupon Fixing for Floating Rate Notes               *
      *  CSE005 - Effect of holidays on FRN coupon dates.             *
      *  CSE004 - FRN Rate Change Warning Report                      *
      **                                                             **
      *****************************************************************
      *
     C           ZLCR      BEGSR                           *** ZLCR   ***
      *
      ***  Define fields:
      *
     C                     Z-ADDZECD      ZECD    50
     C                     Z-ADD*ZEROS    ZZLCR   50
      *
      ***  if event control date is maturity date this is last rate change date
      *
     C           ZECD      IFGE MATY
     C                     Z-ADDMATY      ZZLCR
     C                     GOTO EZLCR
     C                     END
      *
      ***  Call ZDATE2 to obtain MMDD for the Control Date
      *
     C                     Z-ADDZECD      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZDATE     ZZECD4  40
     C           *IN98     IFEQ '0'
     C                     MOVELZZECD4    ZZWRKD
     C                     MOVE ZZECD4    ZZWRKM
     C                     Z-ADDZZWRKD    ZZECD4
     C                     MOVELZZWRKM    ZZECD4
     C                     END
      *
      ***  If the Date Format is DDMMYY, then turn the array of Coupon
      ***  /Rate Dates around to MMDD format.
      *
     C           *IN98     IFEQ '0'
     C                     DO   12        X       20
     C                     MOVE CD,X      ZZWRKM  20
     C                     MOVELCD,X      ZZWRKD  20
     C                     Z-ADDZZWRKD    CD,X
     C                     MOVELZZWRKM    CD,X
     C                     END
     C                     END
      *
      ***  Get year of Control Date
     C                     MOVE ZDATE     ZECDYR  20
      *
      ***  Find the Prior Coupon/Rate Date from the Arrays:
      ***  If the Rate/Payment Indicators are all blank, then ZZLCR
      ***  will equal Initial Date. (SHOULDN'T OCCUR)
      *
     C                     Z-ADDITLD      ZZLCR
      *
     C           CRDS      IFNE *BLANKS
     C                     MOVE '0'       ZFOUND  1
     C                     Z-ADD0         TX      20
     C                     Z-ADD12        X
      *
     C           X         DOWGT0
     C           CR,X      IFEQ 'R'
     C           CR,X      OREQ 'C'
     C*
     C** If 29Feb is used in the coupon/rate array, then there is a
     C** need to check if the current year is a leap year. If it is
     C** not, the coupon/rate date is changed to 28. But there is a
     C** need to store the date so that it can be restored back after
     C** the check.
     C*
     C                     MOVE CD,X      ZZWRKD
     C                     MOVELCD,X      ZZWRKM
     C           ZZWRKM    IFEQ 2
     C           ZZWRKD    ANDEQ29
     C                     Z-ADDZECDYR    ZZYR    20
     C           ZZYR      ADD  28        ZZYR
     C           ZZYR      DIV  4         ZZYR
     C                     MVR            ZMVR    10
     C           ZMVR      IFGT 0
     C                     Z-ADDX         TX
     C                     MOVE '28'      CD,X
     C                     END
     C                     END
     C*
     C           ZZECD4    IFGE CD,X
     C                     MOVE '1'       ZFOUND
     C                     GOTO ZLCR00
     C                     END
     C                     END
     C                     SUB  1         X
     C                     END
      *
     C           ZLCR00    TAG                             ZLCR00 TAG
      *
      *** If found, add the Control Date's YY to form Coupon/Rate Date
      *
     C           ZFOUND    IFEQ '1'
     C                     Z-ADDZECDYR    ZDATE
     C                     MOVELCD,X      ZDATE
      *
      *** Else if not found, find the Last Coupon/Rate  of the
      *** previous year
      *
     C                     ELSE
     C                     Z-ADD12        X
      *
     C           X         DOWGT0
     C           CR,X      IFEQ 'R'
     C           CR,X      OREQ 'C'
      *
      ***  Check for the year 2000.
     C           ZECDYR    IFEQ 0
     C                     Z-ADD99        ZDATE
     C                     ELSE
     C                     Z-ADDZECDYR    ZDATE
     C                     SUB  1         ZDATE
     C                     END
      *
     C** If 29Feb is used in the coupon/rate array, then there is a
     C** need to check if the current year is a leap year. If it is
     C** not, the coupon/rate date is changed to 28. But there is a
     C** need to store the date so that it can be restored back after
     C** the check.
      *
     C           TX        IFNE 0
     C                     MOVE '29'      CD,TX
     C                     Z-ADD0         TX
     C                     END
     C*
     C                     MOVE CD,X      ZZWRKD
     C                     MOVELCD,X      ZZWRKM
     C           ZZWRKM    IFEQ 2
     C           ZZWRKD    ANDEQ29
     C           ZECDYR    SUB  1         ZZYR
     C           ZZYR      ADD  28        ZZYR
     C           ZZYR      DIV  4         ZZYR
     C                     MVR            ZMVR    10
     C           ZMVR      IFGT 0
     C                     Z-ADDX         TX
     C                     MOVE '28'      CD,X
     C                     END
     C                     END
     C*
     C                     MOVELCD,X      ZDATE
     C                     MOVE '1'       ZFOUND
     C                     GOTO ZLCR01
     C                     END
     C                     SUB  1         X
     C                     END
      *
     C           ZLCR01    TAG                             ZLCR01 TAG
     C                     END
      *
      ***  Convert the Coupon/Rate Date to Midas Day Number using
      ***  ZDATE1 (Not done if no Rate/ Payment Indicators are R or C)
      ***  If DDMMYY format, turn it around to MMDDYY format
      *
     C           ZFOUND    IFEQ '1'
      *
     C           *IN98     IFEQ '0'
     C                     MOVELZDATE     ZZWRK   40
     C                     MOVE ZZWRK     ZZWRKM
     C                     MOVELZZWRK     ZZWRKD
     C                     Z-ADDZZWRKD    ZZWRK
     C                     MOVELZZWRKM    ZZWRK
     C                     MOVELZZWRK     ZDATE
     C                     END
      *
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    ZZLCR
     C                     END
      *
      ***  If the Initial Date on the Security record is not zero, then
      ***  If the resultant Prior Coupon/Rate Date is less than the
      ***  Initial Date or First Coupon/Rate, set:
      ***  Prior Coupon/Rate Date = Initial Date.
      *
     C           ITLD      IFNE *ZEROS
      *
     C           ZZLCR     IFLT ITLD
     C           ZECD      ORLT FCPN
     C                     Z-ADDITLD      ZZLCR
     C                     ELSE
     C*
     C*** IF Last coupon/rate date is *LE 1st_Coupon/Rate_Date
     C***    and *GT initial date
     C*
     C           FCPN      IFNE *ZEROS
     C           ZZLCR     ANDGTITLD
     C           ZZLCR     ANDLEFCPN
     C                     Z-ADDFCPN      ZZLCR
     C                     ELSE
     C           ZZLCR     SUB  FCPN      @DDIF   50
     C           @DDIF     IFLT 10
     C                     Z-ADDFCPN      ZZLCR
     C                     END
     C                     END
     C                     END
      *
      *** IF Event_Control_Date is *LE 1st_Coupon/Rate_Date
     C           FCPN      IFNE *ZEROS
     C           ZZLCR     ANDGTITLD
     C           ZZLCR     ANDLEFCPN
     C                     Z-ADDFCPN      ZZLCR
     C                     END
      *
      ***  If the Initial Date on the Security record is zero, then if
      ***  the resultant Prior Coupon/Rate Date is less than the
      ***  Issue Date or First Coupon/Rate, set:
      ***  Prior Coupon/Rate Date = Issue Date.
      *
     C                     ELSE
      *
     C           ZZLCR     IFLT ISSD
     C           ZECD      ORLT FCPN
     C                     Z-ADDISSD      ZZLCR
     C                     ELSE
     C*
     C*** IF Last Coupon/Rate Date is *LE 1st_Coupon/Rate_Date
     C***    and *GT issue date
     C           FCPN      IFNE *ZEROS
     C           ZZLCR     ANDGTISSD
     C           ZZLCR     ANDLEFCPN
     C                     Z-ADDFCPN      ZZLCR
     C                     ELSE
     C           ZZLCR     SUB  FCPN      @DDIF
     C           @DDIF     IFLT 10
     C                     Z-ADDFCPN      ZZLCR
     C                     END
     C                     END
     C                     END
      *** IF Event_Control_Date is *LE 1st_Coupon/Rate_Date
     C           FCPN      IFNE *ZEROS
     C           ZZLCR     ANDGTISSD
     C           ZZLCR     ANDLEFCPN
     C                     Z-ADDFCPN      ZZLCR
     C                     END
      *
      *
     C                     END
     C                     END
     C*
     C**  Restore 29Feb to the array if it is changed.
     C*
     C           TX        IFNE 0
     C                     MOVE '29'      CD,TX
     C                     END
      *
      ***  If the Date Format is DDMMYY, then return the Array
      ***  of Coupon/Rate Dates to it's former state of DDMM.
      *
     C           *IN98     IFEQ '0'
     C                     DO   12        X       20
     C                     MOVELCD,X      ZZWRKM  20
     C                     MOVE CD,X      ZZWRKD  20
     C                     Z-ADDZZWRKM    CD,X
     C                     MOVELZZWRKD    CD,X
     C                     END
     C                     END
      **                                                                  CSE005
      ** If BCNV is not blank then call SE0045.                           CSE005
      ** OR BCNV is not 'N' then call SE0045.                             CSE031
      **                                                                  CSE005
     C           BCNV      IFNE ' '                                       CSE005
     C           BCNV      ANDNE'N'                                       CSE031
     C                     MOVE 'L'       NCI     1                       CSE005
     C                     CALL 'SE0045'                                  CSE005
     C                     PARM           SITP                            CSE005
     C                     PARM           ZECD                            CSE005
     C                     PARM           ITLD                            CSE005
     C                     PARM           FCPN                            CSE005
     C                     PARM           CD                              CSE005
     C                     PARM           CR                              CSE005
     C                     PARM           NMCY                            CSE005
     C                     PARM           ZZLCR                           CSE005
     C                     PARM           NCI                             CSE005
     C                     PARM           BCNV                            CSE005
     C                     PARM           MATY                            CSE005
     C                     PARM           LCPN                            CSE031
     C                     PARM           HCY1                            CSE071
     C                     PARM           HCY2                            CSE071
     C                     PARM           HCY3                            CSE071
     C                     PARM           HCY4                            CSE071
     C                     PARM           HCY5                            CSE071
     C                     PARM           HCY6                            CSE071
     C                     PARM           HCY7                            CSE071
     C                     PARM           HCY8                            CSE071
     C                     PARM           HCY9                            CSE071
     C                     ENDIF                                          CSE005
      *
     C           EZLCR     ENDSR
      *
      *****************************************************************
