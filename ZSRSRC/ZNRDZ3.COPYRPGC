     C*****************************************************************
     C*                                                               *
     C*  SUBROUTINE - ZNRD                                            *
     C*  Determine Next Rollover Date                                 *
     C*                                                               *
     C*                                                               *
     C*  INPUT - CD Array - Coupon dates - SECTYD Security File       *
     C*          CR Array - Rate/Payt Indics - SECTYD Security File   *
     C*          FCPN - First Coupon Date - SECTYD Security File      *
     C*          MATY - Maturity Date - SECTYD Security File          *
     C*          ECD - Event Control Date - ZEVCD process             *
     C*          *IN98 - Date Format - from RUNDAT via program        *
     C*                                                               *
     C*  OUTPUT - NRD (5,0) - Next Rollover Date                      *
     C*                                                               *
     C*  CALLS  - ZDATE1                                              *
     C*           ZDATE2                                              *
     C*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     C*  Last Amend No. CRM001  *CREATE         DATE 06FEB95          *
     C*  Prev Amend No. Xnnnnn                  DATE ddmmmyy          *
     C*                                                               *
     C*---------------------------------------------------------------*
     C*                                                               *
     C*  CRM001 - Midas/Tmark Interface                               *
     C*                                                               *
     C*****************************************************************
     C*
     C           ZNRD      BEGSR                           *** ZNRD   ***
     C*
     C** Call ZDATE2 to obtain MMDD for event control date
     C** Use the greater of ECD and ITLD as basis for the calculation
     C*
     C           ECD       IFGE ITLD
     C                     Z-ADDECD       ZDAYNO
     C                     ELSE
     C                     Z-ADDITLD      ZDAYNO
     C                     END
     C*
     C                     EXSR ZDATE2
     C                     MOVELZDATE     ZZECD4  40
     C*
     C           *IN98     IFEQ '0'
     C                     MOVELZZECD4    ZZWRKD
     C                     MOVE ZZECD4    ZZWRKM
     C                     Z-ADDZZWRKD    ZZECD4
     C                     MOVELZZWRKM    ZZECD4
     C                     END
     C*
     C**  If Date format is DDMMYY turn round array of coupon
     C**  dates to MMDD
     C*
     C           *IN98     IFEQ '0'
     C                     DO   12        X       20
     C                     MOVE CD,X      ZZWRKM  20
     C                     MOVELCD,X      ZZWRKD  20
     C                     Z-ADDZZWRKD    CD,X
     C                     MOVELZZWRKM    CD,X
     C                     END
     C                     END
     C*
     C**  Get year for ECD
     C*
     C                     MOVE ZDATE     ZECDYR  20
     C*
     C**  Find next rollover date from arrays -
     C**    If rate/payt inds all blank NRD = Maturity Date
     C*
     C                     Z-ADDMATY      NRD     50
     C           CRDS      IFNE *BLANKS
     C*
     C**  If rate/payt inds not all blank -
     C**    Search for next highest coupon
     C*
     C                     MOVE '0'       ZFOUND  1
     C                     DO   12        X
     C           CR,X      IFEQ 'R'
     C           CR,X      OREQ 'C'
     C           ZZECD4    IFLE CD,X
     C                     MOVE '1'       ZFOUND
     C                     GOTO ZNRD00
     C                     END
     C                     END
     C                     END
     C*
     C**     Found - add ECD'S YY to form date
     C*
     C           ZNRD00    TAG                             ZNRD00 TAG
     C           ZFOUND    IFEQ '1'
     C                     Z-ADDZECDYR    ZDATE
     C                     MOVELCD,X      ZDATE
     C*
     C**     Not found - Use first coupon
     C**                 Add next year's YY to form date
     C*
     C                     ELSE
     C                     DO   12        X
     C           CR,X      IFEQ 'R'
     C           CR,X      OREQ 'C'
     C                     Z-ADDZECDYR    ZDATE
     C                     ADD  1         ZDATE
     C                     MOVELCD,X      ZDATE
     C                     MOVE '1'       ZFOUND
     C                     GOTO ZNRD01
     C                     END
     C                     END
     C                     END
     C           ZNRD01    TAG                             ZNRD01 TAG
     C*
     C**  Convert date to day no using ZDATE1
     C**  (not done if no rate/payt ind is R OR C)
     C**  If DDMMYY format turn round to MMDDYY
     C*
     C           ZFOUND    IFEQ '1'
     C           *IN98     IFEQ '0'
     C                     MOVELZDATE     ZZWRK   40
     C                     MOVE ZZWRK     ZZWRKM
     C                     MOVELZZWRK     ZZWRKD
     C                     Z-ADDZZWRKD    ZZWRK
     C                     MOVELZZWRKM    ZZWRK
     C                     MOVELZZWRK     ZDATE
     C                     END
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    NRD
     C                     END
     C*
     C**  If NRD > Maturity date , reset NRD = maturity date
     C*
     C           NRD       IFGT MATY
     C                     Z-ADDMATY      NRD
     C                     END
     C                     END
     C*
     C**  If the First Coupon Date is not zero, then
     C**  If NRD < 1st coupon date , reset NRD = 1st coupon date
     C*
     C           FCPN      IFNE *ZEROS
     C           NRD       ANDLTFCPN
     C                     Z-ADDFCPN      NRD
     C                     END
     C*
     C** If date format is DDMMYY turn back array of coupon dates to
     C**   DDMM
     C*
     C           *IN98     IFEQ '0'
     C                     DO   12        X       20
     C                     MOVELCD,X      ZZWRKM  20
     C                     MOVE CD,X      ZZWRKD  20
     C                     Z-ADDZZWRKM    CD,X
     C                     MOVELZZWRKD    CD,X
     C                     END
     C                     END
     C*
     C                     ENDSR
     C*****************************************************************
