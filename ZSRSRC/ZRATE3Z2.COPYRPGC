      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member (RPG)                                   *
      *                                                               *
      *  XXXXXXX - (/COPY Name)                                       *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. nnnnnn  *CREATE    Date ddmmmyy               *
      *  Prev Amend No. nnnnnn             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  nnnnnn - (change description)                                *
      *                                                               *
      *****************************************************************
     C*****************************************************************
     C*                                                               *
     C* ZRATE3 - Subroutine to convert a 13,9 rate to display, no     *
     C*          sign and no right justify                            *
     C*                                                               *
     C* INPUT  : ZINPR  (13,9) Rate to be edited.                     *
     C*                                                               *
     C* OUTPUT : ZOUTR  (14A)  Edited output rate.                    *
     C*                                                               *
     C* USES   : ZWDECS Field to hold decimal digits for input rate.  *
     C*          ZWINTS Field to hold integers for input rate.        *
     C*          ZN     Counter field.                                *
     C*          ZW     Work array.                                   *
     C*                                                               *
     C*****************************************************************
     C           ZRATE3    BEGSR
     C*
     C**  Initialise fields used.
     C*
     C                     MOVE *BLANKS   ZWOUTR 14
     C                     MOVE *BLANKS   ZWDECS  9
     C                     MOVE *BLANKS   ZWINTS  4
     C                     Z-ADD0         ZN      20
     C*
     C**  Initialise work array.
     C*
     C                     MOVEA*BLANKS   ZW
     C*
     C**  If the rate is negative reverse the sign.
     C*
     C           ZINPR     IFLT 0
     C                     Z-SUBZINPR     ZINPR  139
     C                     END
     C*
     C**  Separate integers and decimal digits into two fields.
     C*
     C                     MOVE ZINPR     ZWDECS
     C                     MOVELZINPR     ZWINTS
     C*
     C**  Move integers, decimal separator and decimal digits into
     C**  work array.
     C*
     C                     MOVEAZWINTS    ZW
     C                     MOVE '.'       ZW,5
     C                     MOVEAZWDECS    ZW,6
     C*
     C**  Remove leading zeros from the rate and replace with blanks.
     C*
     C                     Z-ADD1         ZN
     C           ZN        DOWLE3
     C           ZW,ZN     ANDEQ'0'
     C                     MOVE *BLANK    ZW,ZN
     C                     ADD  1         ZN
     C                     END
     C*
     C**  Truncate trailing zeros and replace with blanks. If rate is
     C**  an integer a single zero after the decimal place should be
     C**  displayed.
     C*
     C                     Z-ADD14        ZN
     C           ZN        DOWGE7
     C           ZW,ZN     ANDEQ'0'
     C                     MOVE *BLANK    ZW,ZN
     C                     SUB  1         ZN
     C                     END
     C*
     C**  Move the final array into the output field.
     C*
     C                     MOVEAZW        ZOUTR  14
     C*
     C           ZRAT39    ENDSR                           **ZRAT39**
      *****************************************************************
      * End of /COPY XXXXXXX                                          *
      *****************************************************************
