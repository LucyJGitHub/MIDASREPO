      *****************************************************************
      **                                                             **
      ** ZRATE  - Subroutine to calculate effective interest rate.   **
      **          (Currently applies to Partly Paid and Asset-Backed **
      **           Securities).                                      **
      **                                                             **
      ** INPUT  - ZCPNR (11,7) Current Coupon Rate                   **
      **          ZPPDI (1A)   Partly Paid Indicator                 **
      **          ZPPCP (15,0) Current Partly Paid Price             **
      **          ZSFPP (15,0) Fully Paid Price                      **
      **          ZPROT (1A)   Processing Type                       **
      ************ZCFCT**9,8)**Current Factor**************************   052254
      **          ZCFCT (10,9) Current Factor                        **   052254
      **                                                             **
      ** OUTPUT - IRATE (15,9) Effective Interest Rate               **
      **                                                             **
      *****************************************************************
      **                                                             **
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      ** LAST AMEND NO. 052254             DATE 07JAN94              **
      ** PREV AMEND NO. E20085             DATE 03NOV89              **
      *                                                              **
      *--------------------------------------------------------------**
      **                                                             **
      *  052254 - CURRENT FACTOR amended from 9,8 to 10,9            **
      ** E20085 - Rewrite of Subroutine.                             **
      **         - Includes change to correct Partly Paids.          **
      **         - Effective Interest Rate field, IRATE, has been    **
      **           redefined to increase its accuracy.               **
      **         - Field ZSINP is removed because it is not used.    **
      **         - Processing has been restructured to make it much  **
      **           simpler, more efficient and easier to read.       **
      **                                                             **
      *****************************************************************
      *
     C           ZRATE     BEGSR                           *** ZRATE  ***
      *
      ***  Define the fields
      *
     C                     Z-ADDZCPNR     ZCPNR  117
     C                     MOVE ZPPDI     ZPPDI   1
     C                     Z-ADDZPPCP     ZPPCP  150
     C                     Z-ADDZSFPP     ZSFPP  150
     C                     MOVE ZPROT     ZPROT   1
     C**********           Z-ADDZCFCT     ZCFCT   98                      052254
     C                     Z-ADDZCFCT     ZCFCT  109                      052254
      *
      ***  If Coupon Rate or Partly Paid Price are zero then set the
      ***  Effective Interest Rate to zero and Exit, else set up the
      ***  Interest Rate with the Current Coupon Rate.
      *
     C           ZCPNR     IFEQ *ZEROS
     C           ZPPDI     OREQ 'Y'
     C           ZPPCP     ANDEQ*ZEROS
      *
     C                     Z-ADD*ZEROS    IRATE
     C                     GOTO EZRATE
      *
     C                     ELSE
      *
     C                     Z-ADDZCPNR     IRATE  159
      *
     C                     END
      *
      ***  If Partly Paid, then calculate the fraction of Part Payment
      ***  and multiply it with the Interest Rate.
      ***  (If PP Price = FP Price, fraction = 1, so skip. Note: since
      ***   FP >= PP, we have also avoided possible division by zero.)
      *
     C           ZPPDI     IFEQ 'Y'
     C           ZPPCP     ANDNEZSFPP
      *
     C           ZPPCP     DIV  ZSFPP     WRK109 109
     C                     MULT WRK109    IRATE     H
      *
     C                     END
      *
      ***  If Processing Type = '8' Security, then multiply the
      ***  Interest Rate by the Current Factor.
      *
     C           ZPROT     IFEQ '8'
      *
     C                     MULT ZCFCT     IRATE     H
     C*
     C                     END
      *
     C           EZRATE    ENDSR
      *
      **********************************************************************
