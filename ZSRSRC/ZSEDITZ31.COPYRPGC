     C********************************************************************
     C********************************************************************
     C**                                                                 *
     C** ZSEDIT subroutine to insert a decimal point and sign into a     *
     C** numeric field and to blank out leading zeros (optionaly         *
     C** inserting commas).                                              *
     C**                                                                 *
     C********************************************************************
     C**                                                                 *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     C** LAST AMEND NO. E20532             DATE 09DEC89                  *
     C**                                                                 *
     C**-----------------------------------------------------------------*
     C**                                                                 *
     C** E20532 - As part of this fix ZSEDIT has been improved to        *E20532
     C**          insert a floating character (whatever is passed        *E20532
     C**          in ZECHAR ) before the leading digit of a field.       *E20532
     C**                                                                 *
     C**=================================================================*
     C**                                                                 *
     C**     Input fields:   ZFLD15 15/0                                 *
     C**                     ZDECS  1/0                                  *
     C**                     ZECODE 1/  ('J', 'L' or blank)              *
     C**                     ZECHAR 1/  ('character' or blank)           *E20532
     C**                                                                 *
     C**     Arrays:         ZS1    15/1/0                               *
     C**                     ZS2    21/1/                                *
     C**                                                                 *
     C**     Output fields:  ZOUT21 21                                   *
     C**                                                                 *
     C**     Indicators   :  *IN97                                       *E20532
     C**                                                                 *E20532
     CSR         ZSEDIT    BEGSR                           **  ZSEDIT   *
     C*
     C* Define/Clear fields
     C*
     C                     Z-ADDZFLD15    ZFLD15 150
     C                     Z-ADDZDECS     ZDECS   10
     C                     MOVE ZECODE    ZECODE  1
     C                     MOVE ZECHAR    ZECHAR  1                       E20532
     C                     MOVE *BLANKS   ZOUT21 21
     C*
     C*       SET UP WORK FIELDS
     C*
     C                     Z-ADD0         ZS1
     C                     MOVE ' '       ZS2
     C*
     C                     Z-ADD15        Z1      20
     C                     Z-ADD20        Z2      20
     C*
     C           15        SUB  ZDECS     ZINTS   20
     C*
     C* Check if numeric field is negative
     C*
     C           ZFLD15    IFLT *ZEROS
     C                     MOVE '-'       ZS2,21
     C                     Z-SUBZFLD15    ZFLD15
     C                     END
     C*
     C                     Z-ADDZFLD15    WORK15
     C*
     C*       CHECK TO SEE IF THERE ARE ANY DECIMAL PLACES
     C*
     C           ZDECS     CABEQ0         ZS10
     C*
     C*       SET UP DECIMALS
     C*
     C                     Z-ADD*ZEROS    ZCNT    10
     C           ZCNT      DOUEQZDECS
     C                     MOVE ZS1,Z1    ZS2,Z2
     C                     SUB  1         Z2
     C                     ADD  1         ZCNT
     C                     SUB  1         Z1
     C                     END
     C*
     C*       PUT IN DECIMAL PLACE
     C*
     C                     MOVE '.'       ZS2,Z2
     C                     SUB  1         Z2
     C*
     C           ZS10      TAG                             ** ZS10 TAG **
     C*
     C*       SET UP INTEGERS
     C*
     C                     Z-ADD*ZEROS    CNT3    10
     C           Z1        DOUEQ*ZEROS
     C                     MOVE ZS1,Z1    ZS2,Z2
     C                     SUB  1         Z1
     C                     SUB  1         Z2
     C*
     C* If edit code is 'J', insert a ',' before each group of three
     C* digits - not if beginning of array reached.
     C*
     C           Z2        IFGT *ZEROS
     C           ZECODE    ANDEQ'J'
     C                     ADD  1         CNT3
     C           CNT3      IFEQ 3
     C                     MOVE ','       ZS2,Z2
     C                     SUB  1         Z2
     C                     Z-ADD*ZEROS    CNT3
     C                     END
     C                     END
     C*
     C                     END
     C*
     C********PUT*IN*LEADING*BLANKS*                                      E20532
     C*       PUT IN LEADING BLANKS and Edit Character if relevant.       E20532
     C*
     C                     Z-ADD1         Z2                              E20532
     C                     SETOF                     97                   E20532
     C*                                                                   E20532
     C           Z2        DOWLT22                                        E20532
     C           *IN97     ANDNE'1'                                       E20532
     C*                                                                   E20532
     C***********ZS2,Z2****DOWEQ'0'                                       E20532
     C           ZS2,Z2    IFEQ '0'                                       E20532
     C           ZS2,Z2    OREQ ' '
     C           ZS2,Z2    OREQ ','
     C                     MOVE ' '       ZS2,Z2
     C*                                                                   E20532
     C                     ELSE                                           E20532
     C*                                                                   E20532
     C                     SETON                     97                   E20532
     C*                                                                   E20532
     C           Z2        IFGT 1                                         E20532
     C           ZECHAR    ANDNE' '                                       E20532
     C                     SUB  1         Z2                              E20532
     C                     MOVE ZECHAR    ZS2,Z2                          E20532
     C                     ELSE                                           E20532
     C                     SUB  1         Z2                              E20532
     C                     END                                            E20532
     C                     END                                            E20532
     C                     ADD  1         Z2                              E20532
     C*                                                                   E20532
     C***********Z2********CABEQ22        ZS20                            E20532
     C                     END
     C*                                                                   E20532
     C                     SETOF                     97                   E20532
     C*
     C*       IF NO INTEGERS PUT IN LEADING ZERO
     C*
     C***********ZS20******TAG                             ** ZS20 TAG ** E20532
     C*
     C           Z2        IFEQ 22
     C                     SUB  2         Z2
     C                     MOVE '0'       ZS2,Z2
     C*                                                                   E20532
     C           ZECHAR    IFNE ' '                                       E20532
     C*                                                                   E20532
     C                     SUB  1         Z2                              E20532
     C                     MOVE ZECHAR    ZS2,Z2                          E20532
     C                     END                                            E20532
     C*                                                                   E20532
     C                     ELSE
     C*
     C           ZS2,Z2    IFEQ '.'
     C                     SUB  1         Z2
     C                     MOVE '0'       ZS2,Z2
     C*                                                                   E20532
     C           ZECHAR    IFNE ' '                                       E20532
     C                     SUB  1         Z2                              E20532
     C                     MOVE ZECHAR    ZS2,Z2                          E20532
     C                     END                                            E20532
     C*                                                                   E20532
     C                     END
     C                     END
     C*
     C*       SET UP OUTPUT FIELD
     C*
     C                     MOVEAZS2       ZOUT21
     C                     MOVE *BLANKS   ZECHAR                          E20532
     C*
     CSR                   ENDSR
     C*****************************************************************
