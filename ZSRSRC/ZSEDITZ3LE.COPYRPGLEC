      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member (RPG)                                   *
      *                                                               *
      *  ZSEDITZ3LE - Ins Dec Point and Sign into Numeric             *
      *                                                               *
      *  ZSEDIT subroutine to insert a decimal point and sign into a  *
      *  numeric field and to blank out leading zeros (optionally     *
      *  inserting commas)                                            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Last Amend No. nnnnnn  *CREATE    Date 13SEP00               *
      *  Prev Amend No. nnnnnn             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  nnnnnn - (change description)                                *
      *                                                               *
      *****************************************************************
      *                                                               *
      *      Input fields:   ZFLD15 15/0                              *
      *                      ZDECS  1/0                               *
      *                      ZECODE 1/  ('J', 'L' or blank)           *
      *                                                               *
      *      Arrays:         ZS1    15/1/0                            *
      *                      ZS2    21/1/                             *
      *                                                               *
      *      Output fields:  ZOUT21 21                                *
      *                                                               *
      *****************************************************************
      *
     CSR   ZSEDIT        BEGSR                                                  **  ZSEDIT   *
      *
      * Define/Clear fields
      *
     C                   Z-ADD     ZFLD15        ZFLD15           15 0
     C                   Z-ADD     ZDECS         ZDECS             1 0
     C                   MOVE      ZECODE        ZECODE            1
     C                   MOVE      *BLANKS       ZOUT21           21
      *
      * SET UP WORK FIELDS
      *
     C                   Z-ADD     0             ZS1
     C                   MOVE      ' '           ZS2
      *
     C                   Z-ADD     15            Z1                2 0
     C                   Z-ADD     20            Z2                2 0
      *
     C     15            SUB       ZDECS         ZINTS             2 0
      *
      * Check if numeric field is negative
      *
     C     ZFLD15        IFLT      *ZEROS
     C                   MOVE      '-'           ZS2(21)
     C                   Z-SUB     ZFLD15        ZFLD15
     C                   END
      *
     C                   Z-ADD     ZFLD15        WORK15
      *
      * CHECK TO SEE IF THERE ARE ANY DECIMAL PLACES
      *
     C     ZDECS         CABEQ     0             ZS10
      *
      * SET UP DECIMALS
      *
     C                   Z-ADD     *ZEROS        ZCNT              1 0
     C     ZCNT          DOUEQ     ZDECS
     C                   MOVE      ZS1(Z1)       ZS2(Z2)
     C                   SUB       1             Z2
     C                   ADD       1             ZCNT
     C                   SUB       1             Z1
     C                   END
      *
      * PUT IN DECIMAL PLACE
      *
     C                   MOVE      '.'           ZS2(Z2)
     C                   SUB       1             Z2
      *
     C     ZS10          TAG                                                    ** ZS10 TAG **
      *
      * SET UP INTEGERS
      *
     C                   Z-ADD     *ZEROS        CNT3              1 0
     C     Z1            DOUEQ     *ZEROS
     C                   MOVE      ZS1(Z1)       ZS2(Z2)
     C                   SUB       1             Z1
     C                   SUB       1             Z2
      *
      * If edit code is 'J', insert a ',' before each group of three
      * digits - not if beginning of array reached.
      *
     C     Z2            IFGT      *ZEROS
     C     ZECODE        ANDEQ     'J'
     C                   ADD       1             CNT3
     C     CNT3          IFEQ      3
     C                   MOVE      ','           ZS2(Z2)
     C                   SUB       1             Z2
     C                   Z-ADD     *ZEROS        CNT3
     C                   END
     C                   END
      *
     C                   END
      *
      * PUT IN LEADING BLANKS
      *
     C                   Z-ADD     1             Z2
     C     ZS2(Z2)       DOWEQ     '0'
     C     ZS2(Z2)       OREQ      ' '
     C     ZS2(Z2)       OREQ      ','
     C                   MOVE      ' '           ZS2(Z2)
     C                   ADD       1             Z2
     C     Z2            CABEQ     22            ZS20
     C                   END
      *
      * IF NO INTEGERS PUT IN LEADING ZERO
      *
     C     ZS20          TAG                                                    ** ZS20 TAG **
      *
     C     Z2            IFEQ      22
     C                   SUB       2             Z2
     C                   MOVE      '0'           ZS2(Z2)
     C                   ELSE
      *
     C     ZS2(Z2)       IFEQ      '.'
     C                   SUB       1             Z2
     C                   MOVE      '0'           ZS2(Z2)
     C                   END
     C                   END
      *
      * SET UP OUTPUT FIELD
      *
     C                   MOVEA     ZS2           ZOUT21
      *
     CSR                 ENDSR
      *****************************************************************
      * End of /COPY ZSEDITZ3LE                                       *
      *****************************************************************
