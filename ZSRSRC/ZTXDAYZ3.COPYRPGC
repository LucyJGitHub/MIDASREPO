      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member (RPG)                                   *
      *                                                               *
      *  XXXXXXX - (/COPY Name)                                       *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. nnnnnn  *CREATE    Date ddmmmyy               *
      *  Prev Amend No. nnnnnn             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  nnnnnn - (change description)                                *
      *                                                               *
      *****************************************************************
     C********************************************************************
     C**
     C**   ZTXDAY SR. TO CALCULATE ANY NUMBER OF WORKING
     C**              DAYS FORWARD IN ANY CURRENCY.
     C**              HOLIDAYS FILE ACCESSED BY CHAIN.
     C**
     C           ZTXDAY    BEGSR                           *** ZTXDAY ***
     C**
     C**   CALCULATIONS TO DEFINE INPUT FIELDS.
     C                     Z-ADDZDAYNO    ZDAYNO  50       STARTING DATE
     C                     MOVE ZCCY      ZCCY    3        DATE CURRENCY
     C                     Z-ADDZNRDYS    ZNRDYS  20       NBR OF WORKING
     C**                                                   DAYS FORWARD
     C**   CLEAR COUNT OF NBR OF DAYS ADDED, & DEFINE ZDYNBR.
     C                     Z-ADD0         ZDCNT   20
     C                     Z-ADDZDAYNO    ZDYNBR  50
     C**
     C**   EXIT IF NO DAYS FORWARD
     C           ZNRDYS    CABEQ0         ZTXEND
     C**
     C**   LOOP TO INCREMENT DAY NUMBER AND CHECK FOR HOLIDAY.
     C           ZDCNT     DOWLTZNRDYS
     C**
     C                     ADD  1         ZDYNBR
     C**
     C**   CHAIN FOR HOLIDAY RECORD 1 - IF NOT FOUND, NO HOLIDAY
     C           ZDYNBR    CHAINTABLETJF             90
     C           *IN90     IFEQ '1'
     C           RECI      ORNE 'D'
     C                     ADD  1         ZDCNT
     C                     GOTO ZTOAGN
     C                     ELSE
     C                     MOVEAHCCY      ZHC,1
     C                     END
     C**
     C**   CHECK IF DAY IS HOLIDAY FOR CURRENCY.
     C           ZCCY      LOKUPZHC                      93
     C  N93      LOCY      LOKUPZHC                      93
     C**
     C**   DETERMINE TYPE FOUND, IF INCLUDE AND NOT IN ARRAY/EXCLUDE AND
     C**   IN ARRAY, THEN MUST CHAIN TO RECORD 2 TO SEE IF IT IS A HOL.
     C           INEX      IFEQ 'E'
     C           *IN93     ANDEQ'1'
     C           INEX      OREQ 'I'
     C           *IN93     ANDEQ'0'
     C**
     C**   IF BLANK ENTRIES IN HCCY, THEN NO RECORD 2 WILL BE PRESENT
     C                     Z-ADD1         Z       20
     C           '   '     LOKUPZHC,Z                    94ON IF NO RCD.2
     C           Z         IFLT 26
     C                     ADD  1         ZDCNT
     C                     GOTO ZTOAGN
     C                     END
     C**
     C**   CHAIN FOR HOLIDAY RECORD, RECORD 2
     C                     READ TABLETJF                 90
     C           *IN90     IFEQ '1'
     C           RECI      ORNE 'D'
     C           HOL       ORNE ZDYNBR
     C                     ADD  1         ZDCNT
     C                     GOTO ZTOAGN
     C                     ELSE
     C                     MOVEAHCCY      ZHC,26
     C                     END
     C**
     C**   CHECK IF DAY IS HOLIDAY FOR CURRENCY.
     C           ZCCY      LOKUPZHC                      93
     C  N93      LOCY      LOKUPZHC                      93
     C**
     C**   DETERMINE TYPE FOUND, IF INCLUDE AND NOT IN ARRAY/EXCLUDE AND
     C**   IN ARRAY, THEN NOT A HOLIDAY
     C           INEX      IFEQ 'E'
     C           *IN93     ANDEQ'1'
     C           INEX      OREQ 'I'
     C           *IN93     ANDEQ'0'
     C           ZDCNT     ADD  1         ZDCNT
     C                     END
     C                     END
     C**
     C**  END OF DO LOOP
     C           ZTOAGN    TAG                             *** ZTOAGN ***
     C                     END
     C**
     C           ZTXEND    ENDSR
     C**
     C**
     C********************************************************************
      *****************************************************************
      * End of /COPY XXXXXXX                                          *
      *****************************************************************
