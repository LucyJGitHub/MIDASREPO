      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member (RPG)                                   *
      *                                                               *
      *  XXXXXXX - (/COPY Name)                                       *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. nnnnnn  *CREATE    Date ddmmmyy               *
      *  Prev Amend No. nnnnnn             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  nnnnnn - (change description)                                *
      *                                                               *
      *****************************************************************
     C********************************************************************
     C*  Subroutine- ZYMTX
     C*  Function  - To calculate yield based on days to maturity
     C*
     C*
     C*  Input  - ZMDAT (5,0) Maturity Date
     C*           ZBASD (5,0) Base date
     C*           ZND   25 X(5,0) Array of no. of days to maturity
     C*           ZRT   25 X(15,8) Array of yield rates
     C*
     C*  Output - ZY1 (15,8) Yield rate
     C*           ZB1 (5,0) Days to maturity
     C*           ZB2 (5,0) Previous days to maturity
     C*           ZY2 (15,8) Previous yield rate
     C*           ZB3 (5,0) Next days to maturity
     C*           ZY3 (15,8) Previous yield rate
     C*
     C********************************************************************
     C*
     C           ZYMTX     BEGSR
     C*
     C** DEFINE AND INTIALISE VARIABLES
     C*
     C                     Z-ADD0         ZB1     50
     C                     Z-ADD0         ZB2     50
     C                     Z-ADD0         ZB3     50
     C                     Z-ADD0         ZY1    158
     C                     Z-ADD0         ZY2    158
     C                     Z-ADD0         ZY3    158
     C                     Z-ADD0         ZX      20
     C                     MOVE *BLANK    ZIND    1
     C                     Z-ADDZBASD     ZBASD   50
     C                     Z-ADDZMDAT     ZMDAT   50
     C*
     C** CALCULATE NO. OF DAYS TO MATURITY
     C*
     C           ZMDAT     SUB  ZBASD     ZB1
     C*
     C** IF THE MATURITY DATE IS LESS THAN THE BASE DATE
     C** IE. THE SECURITY IS UNFIXED AND OVERDUE, THE YIELD RATE IS THE
     C** YIELD ASSOCIATED WITH THE LOWEST DAY NO. ENTRY
     C*
     C           ZMDAT     IFLT ZBASD
     C                     Z-ADDZRT,1     ZY1
     C                     Z-ADD0         ZB2
     C                     Z-ADD0         ZB3
     C                     Z-ADD0         ZY2
     C                     Z-ADD0         ZY3
     C*
     C** PROCESSING IF MAT. DATE IS HIGHER THAN OR EQUAL TO BASE DATE
     C*
     C                     ELSE
     C*
     C                     MOVE 'N'       ZIND
     C                     Z-ADD1         ZX
     C*
     C           ZX        DOWLE25
     C           ZIND      ANDEQ'N'
     C*
     C** IF NO. OF DAYS TO MATURITY EQUALS THE CONTENTS OF ZND,ZX
     C** THE YIELD RATE (ZY1) WILL BE THE ASSOCIATED YIELD RATE FROM
     C** ZRT
     C*
     C           ZB1       IFEQ ZND,ZX
     C                     Z-ADDZRT,ZX    ZY1
     C                     Z-ADD0         ZB2
     C                     Z-ADD0         ZB3
     C                     Z-ADD0         ZY2
     C                     Z-ADD0         ZY3
     C                     MOVE 'Y'       ZIND
     C                     END
     C*
     C** IF NO. OF DAYS TO MATURITY IS GREATER  THAN THE  CONTENTS OF
     C** ZND,ZX AND ZRT=0  THE YIELD RATE WILL BE THE YIELD RATE          JP0001
     C** ZRT,ZX-1                                                         JP0001
     C*
     C***********ZB1       IFGT ZND,ZX                                    JP0001
     C***********ZX        ANDEQ25                                        JP0001
     C***********          Z-ADDZRT,25    ZY1                             JP0001
     C***********          Z-ADDZND,24    ZB2                             JP0001
     C***********          Z-ADDZRT,24    ZY2                             JP0001
     C***********          Z-ADDZND,25    ZB3                             JP0001
     C***********          Z-ADDZRT,25    ZY3                             JP0001
     C***********          MOVE 'Y'       ZIND                            JP0001
     C***********          END                                            JP0001
     C*                                                                   JP0001
     C           ZB1       IFGT ZND,ZX                                    JP0001
     C           ZND,ZX    ANDEQ0                                         JP0001
     C           ZX        SUB  1         ZZ                              JP0001
     C                     Z-ADDZRT,ZZ    ZY1                             JP0001
     C                     MOVE 'Y'       ZIND                            JP0001
     C                     END                                            JP0001
     C*                                                                   JP0001
     C** IF NO. OF DAYS TO MATURITY IS LESS THAN THE  CONTENTS OF
     C** ZND,ZX AND ZX <> 1 THE YIELD RATE WILL CALCULATED BY ASSUMING
     C** A UNIFORM CHANGE OF YIELD RATE BETWEEN ZND,ZX-1 AND ZND,ZX
     C*
     C* ZZ  = ZX - 1
     C*                      (                                     )
     C* ZY1 =   ZRT,ZX  -    ( ( ZND,ZX - ZB1)   (ZRT,ZX - ZRT,ZZ) )
     C*                      (                X  ~~~~~~~~~~~~~~~~~ )
     C*                      (                   (ZND,ZX - ZND,ZZ) )
     C*                      (                                     )
     C*
     C           ZB1       IFLT ZND,ZX
     C           ZX        ANDNE1
     C*
     C           ZX        SUB  1         ZZ      20
     C           ZND,ZX    SUB  ZND,ZZ    ZC      50
     C           ZRT,ZX    SUB  ZRT,ZZ    ZD     158
     C*
     C** CALCULATE YIELD RATE
     C*
     C           ZND,ZX    SUB  ZB1       ZH      50
     C           ZH        DIV  ZC        ZF     158
     C           ZF        MULT ZD        ZG     158
     C           ZRT,ZX    SUB  ZG        ZY1
     C                     Z-ADDZND,ZZ    ZB2
     C                     Z-ADDZRT,ZZ    ZY2
     C                     Z-ADDZND,ZX    ZB3
     C                     Z-ADDZRT,ZX    ZY3
     C                     MOVE 'Y'       ZIND
     C                     END
     C*
     C** IF NO. OF DAYS TO MATURITY IS LESS THAN THE  CONTENTS OF
     C** ZND,ZX AND ZX = 1 THE YIELD RATE WILL BE ZRT,1
     C*
     C           ZB1       IFLT ZND,ZX
     C           ZX        ANDEQ1
     C*
     C                     Z-ADDZRT,1     ZY1
     C                     Z-ADD0         ZB2
     C                     Z-ADD0         ZY2
     C                     Z-ADDZND,1     ZB3
     C                     Z-ADDZRT,1     ZY3
     C                     MOVE 'Y'       ZIND
     C                     END
     C*
     C                     ADD  1         ZX
     C                     END
     C*
     C                     END
     C*
     C                     ENDSR
     C*
     C********************************************************************
      *****************************************************************
      * End of /COPY XXXXXXX                                          *
      *****************************************************************
